OBJECT Codeunit 5056 CustCont-Update
{
  OBJECT-PROPERTIES
  {
    Date=11/12/10;
    Time=12:00:00;
    Version List=NAVW16.00.10;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      RMSetup@1000 : Record 5079;
      ContBusRel@1001 : Record 5054;
      Cont@1002 : Record 5050;
      OldCont@1003 : Record 5050;
      NoSeriesMgt@1004 : Codeunit 396;
      ContNo@1005 : Code[20];
      NoSeries@1006 : Code[10];

    PROCEDURE OnInsert@3(VAR Cust@1000 : Record 18);
    BEGIN
      RMSetup.GET;
      IF RMSetup."Bus. Rel. Code for Customers" = '' THEN
        EXIT;

      InsertNewContact(Cust,TRUE);
    END;

    PROCEDURE OnModify@1(VAR Cust@1000 : Record 18);
    VAR
      ChangeLogMgt@1001 : Codeunit 423;
      RecRef@1003 : RecordRef;
      xRecRef@1002 : RecordRef;
    BEGIN
      WITH ContBusRel DO BEGIN
        SETCURRENTKEY("Link to Table","No.");
        SETRANGE("Link to Table","Link to Table"::Customer);
        SETRANGE("No.",Cust."No.");
        IF NOT FIND('-') THEN
          EXIT;
        Cont.GET("Contact No.");
        OldCont := Cont;
      END;

      ContNo := Cont."No.";
      NoSeries := Cont."No. Series";
      Cont.VALIDATE("E-Mail",Cust."E-Mail");
      Cont.TRANSFERFIELDS(Cust);
      Cont."No." := ContNo ;
      Cont."No. Series" := NoSeries;
      Cont.VALIDATE(Name);
      Cont.OnModify(OldCont);
      Cont.MODIFY(TRUE);

      xRecRef.GETTABLE(OldCont);
      RecRef.GETTABLE(Cont);
      ChangeLogMgt.LogModification(RecRef,xRecRef);
    END;

    PROCEDURE OnDelete@2(VAR Cust@1000 : Record 18);
    BEGIN
      WITH ContBusRel DO BEGIN
        SETCURRENTKEY("Link to Table","No.");
        SETRANGE("Link to Table","Link to Table"::Customer);
        SETRANGE("No.", Cust."No.");
        DELETEALL(TRUE);
      END;
    END;

    PROCEDURE InsertNewContact@5(VAR Cust@1000 : Record 18;LocalCall@1001 : Boolean);
    BEGIN
      IF NOT LocalCall THEN BEGIN
        RMSetup.GET;
        RMSetup.TESTFIELD("Bus. Rel. Code for Customers");
      END;

      WITH Cont DO BEGIN
        INIT;
        TRANSFERFIELDS(Cust);
        Cont.VALIDATE(Name);
        Cont.VALIDATE("E-Mail");
        "No." := '';
        "No. Series" := '';
        RMSetup.TESTFIELD("Contact Nos.");
        NoSeriesMgt.InitSeries(RMSetup."Contact Nos.",'',0D,"No.","No. Series");
        Type := Type::Company;
        TypeChange;
        SetSkipDefault(FALSE);
        INSERT(TRUE);
      END;

      WITH ContBusRel DO BEGIN
        INIT;
        "Contact No." := Cont."No.";
        "Business Relation Code" := RMSetup."Bus. Rel. Code for Customers";
        "Link to Table" := "Link to Table"::Customer;
        "No." := Cust."No.";
        INSERT(TRUE);
      END;
    END;

    PROCEDURE InsertNewContactPerson@4(VAR Cust@1001 : Record 18;LocalCall@1003 : Boolean);
    VAR
      ContComp@1000 : Record 5050;
      ContBusRel@1002 : Record 5054;
    BEGIN
      IF NOT LocalCall THEN BEGIN
        RMSetup.GET;
        RMSetup.TESTFIELD("Bus. Rel. Code for Customers");
      END;

      ContBusRel.SETCURRENTKEY("Link to Table","No.");
      ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Customer);
      ContBusRel.SETRANGE("No.",Cust."No.");
      IF ContBusRel.FIND('-') THEN
        IF ContComp.GET(ContBusRel."Contact No.") THEN
          WITH Cont DO BEGIN
            INIT;
            "No." := '';
            VALIDATE(Type,Type::Person);
            INSERT(TRUE);
            "Company No." := ContComp."No.";
            VALIDATE(Name,Cust.Contact);
            InheritCompanyToPersonData(ContComp,FALSE);
            MODIFY(TRUE);
            Cust."Primary Contact No." := "No.";
          END
    END;

    BEGIN
    END.
  }
}
