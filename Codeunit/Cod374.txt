OBJECT Codeunit 374 Bank Acc. Recon. Apply Entries
{
  OBJECT-PROPERTIES
  {
    Date=08/06/01;
    Time=12:00:00;
    Version List=NAVW13.00;
  }
  PROPERTIES
  {
    TableNo=274;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      BankAccReconLine2@1000 : Record 274;
      BankAccLedgEntry@1001 : Record 271;
      ApplyBankAccLedgEntry@1002 : Form 381;
      CheckLedgEntry@1003 : Record 272;
      ApplyCheckLedgEntry@1004 : Form 382;
      OK@1005 : Boolean;

    PROCEDURE ApplyEntries@2(VAR BankAccReconLine@1000 : Record 274);
    BEGIN
      BankAccReconLine2 := BankAccReconLine;
      BankAccReconLine2.TESTFIELD("Ready for Application",TRUE);
      WITH BankAccReconLine2 DO BEGIN
        CASE Type OF
          Type::"Bank Account Ledger Entry":
            BEGIN
              BankAccLedgEntry.RESET;
              BankAccLedgEntry.SETCURRENTKEY("Bank Account No.",Open);
              BankAccLedgEntry.SETRANGE("Bank Account No.","Bank Account No.");
              BankAccLedgEntry.SETRANGE(Open,TRUE);
              BankAccLedgEntry.SETFILTER(
                "Statement Status",'%1|%2',BankAccLedgEntry."Statement Status"::Open,
                BankAccLedgEntry."Statement Status"::"Bank Acc. Entry Applied");
              BankAccLedgEntry.SETFILTER("Statement No.",'''''|%1',"Statement No.");
              BankAccLedgEntry.SETFILTER("Statement Line No.",'0|%1',"Statement Line No.");
              ApplyBankAccLedgEntry.SetStmtLine(BankAccReconLine);
              ApplyBankAccLedgEntry.SETRECORD(BankAccLedgEntry);
              ApplyBankAccLedgEntry.SETTABLEVIEW(BankAccLedgEntry);
              ApplyBankAccLedgEntry.LOOKUPMODE(TRUE);
              OK := ApplyBankAccLedgEntry.RUNMODAL = ACTION::LookupOK;
              CLEAR(ApplyBankAccLedgEntry);
            END;
          Type::"Check Ledger Entry":
            BEGIN
              CheckLedgEntry.RESET;
              CheckLedgEntry.SETCURRENTKEY("Bank Account No.",Open);
              CheckLedgEntry.SETRANGE("Bank Account No.","Bank Account No.");
              CheckLedgEntry.SETRANGE(Open,TRUE);
              CheckLedgEntry.SETFILTER(
                "Entry Status",'%1|%2',CheckLedgEntry."Entry Status"::Posted,
                CheckLedgEntry."Entry Status"::"Financially Voided");
              CheckLedgEntry.SETFILTER(
                "Statement Status",'%1|%2',CheckLedgEntry."Statement Status"::Open,
                CheckLedgEntry."Statement Status"::"Check Entry Applied");
              CheckLedgEntry.SETFILTER("Statement No.",'''''|%1',"Statement No.");
              CheckLedgEntry.SETFILTER("Statement Line No.",'0|%1',"Statement Line No.");
              ApplyCheckLedgEntry.SetStmtLine(BankAccReconLine);
              ApplyCheckLedgEntry.SETRECORD(CheckLedgEntry);
              ApplyCheckLedgEntry.SETTABLEVIEW(CheckLedgEntry);
              ApplyCheckLedgEntry.LOOKUPMODE(TRUE);
              OK := ApplyCheckLedgEntry.RUNMODAL = ACTION::LookupOK;
              CLEAR(ApplyCheckLedgEntry);
            END;
        END;
      END;
    END;

    BEGIN
    END.
  }
}
