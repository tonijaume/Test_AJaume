OBJECT Codeunit 254 BOM Jnl.-B.Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=08/06/01;
    Time=12:00:00;
    Version List=NAVW13.00;
  }
  PROPERTIES
  {
    TableNo=234;
    OnRun=BEGIN
            BOMJnlBatch.COPY(Rec);
            Code;
            Rec := BOMJnlBatch;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to post the journals and print the posting report?;ESP=Confirma que desea registrar los diarios e imprimir el informe de registro?';
      Text001@1001 : TextConst 'ENU=The journals were successfully posted.;ESP=Se han registrado correctamente los diarios.';
      Text002@1002 : TextConst 'ENU="It was not possible to post all of the journals. ";ESP="No ha sido posible registrar todos los diarios. "';
      Text003@1003 : TextConst 'ENU=The journals that were not successfully posted are now marked.;ESP=Los diarios que no se han registrado estn marcados.';
      BOMJnlTemplate@1004 : Record 88;
      BOMJnlBatch@1005 : Record 234;
      BOMJnlLine@1006 : Record 89;
      BOMReg@1007 : Record 239;
      BOMJnlPostBatch@1008 : Codeunit 33;
      JnlWithErrors@1009 : Boolean;

    LOCAL PROCEDURE Code@1();
    BEGIN
      WITH BOMJnlBatch DO BEGIN
        BOMJnlTemplate.GET("Journal Template Name");
        BOMJnlTemplate.TESTFIELD("Posting Report ID");

        IF NOT CONFIRM(Text000,FALSE) THEN
          EXIT;

        FIND('-');
        REPEAT
          BOMJnlLine."Journal Template Name" := "Journal Template Name";
          BOMJnlLine."Journal Batch Name" := Name;
          BOMJnlLine."Line No." := 1;
          CLEAR(BOMJnlPostBatch);
          IF BOMJnlPostBatch.RUN(BOMJnlLine) THEN BEGIN
            MARK(FALSE);
            IF BOMReg.GET(BOMJnlLine."Line No.") THEN BEGIN
              BOMReg.SETRECFILTER;
              REPORT.RUN(BOMJnlTemplate."Posting Report ID",FALSE,FALSE,BOMReg);
            END;
          END ELSE BEGIN
            MARK(TRUE);
            JnlWithErrors := TRUE;
          END;
        UNTIL NEXT = 0;

        IF NOT JnlWithErrors THEN
          MESSAGE(Text001)
        ELSE
          MESSAGE(
            Text002 +
            Text003);

        IF NOT FIND('=><') THEN BEGIN
          RESET;
          Name := '';
        END;
      END;
    END;

    BEGIN
    END.
  }
}
