OBJECT Codeunit 7000003 BG/PO-Post and Print
{
  OBJECT-PROPERTIES
  {
    Date=31/07/03;
    Time=12:00:00;
    Version List=NAVES4.00;
  }
  PROPERTIES
  {
    Permissions=TableData 7000005=rm,
                TableData 7000006=rm,
                TableData 7000007=rm,
                TableData 7000020=rm,
                TableData 7000021=rm,
                TableData 7000022=rm;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text1100000@1000 : TextConst 'ENU=This Bill Group has not been printed. Do you want to continue?;ESP=La remesa no se ha impreso. 每onfirma que desea continuar?';
      Text1100001@1001 : TextConst 'ENU=The posting process has been cancelled by the user.;ESP=El proceso de registro ha sido cancelado por el usuario.';
      Text1100002@1002 : TextConst 'ENU=Do you want to post the Bill Group?;ESP=每onfirma que desea registrar la remesa?';
      Text1100003@1003 : TextConst 'ENU=This Payment Order has not been printed. Do you want to continue?;ESP=La orden pago no se ha impreso. 每onfirma que desea continuar?';
      Text1100004@1004 : TextConst 'ENU=Do you want to post the Payment Order?;ESP=每onfirma que desea registrar la orden de pago?';
      CarteraReportSelection@1100000 : Record 7000013;

    PROCEDURE ReceivablePostOnly@2(BillGr@1100000 : Record 7000005);
    BEGIN
      IF BillGr."No. Printed" = 0  THEN BEGIN
        IF NOT
          CONFIRM(
            Text1100000)
        THEN
          ERROR(Text1100001);
      END ELSE
        IF NOT
          CONFIRM(
            Text1100002,FALSE)
        THEN
          ERROR(Text1100001);

      BillGr.SETRECFILTER;
      REPORT.RUNMODAL(REPORT::"Post Bill Group",
        (BillGr."Dealing Type" = BillGr."Dealing Type"::Discount),
        FALSE,
        BillGr);
    END;

    PROCEDURE ReceivablePostAndPrint@3(BillGr@1100000 : Record 7000005);
    VAR
      PostedBillGr@1100001 : Record 7000006;
    BEGIN
      BillGr.SETRECFILTER;
      REPORT.RUNMODAL(REPORT::"Post Bill Group",
        (BillGr."Dealing Type" = BillGr."Dealing Type"::Discount),
        FALSE,
        BillGr);

      COMMIT;

      IF PostedBillGr.GET(BillGr."No.") THEN BEGIN
        PostedBillGr.SETRECFILTER;
        CarteraReportSelection.RESET;
        CarteraReportSelection.SETRANGE(Usage,CarteraReportSelection.Usage::"Posted Bill Group");
        CarteraReportSelection.FIND('-');
        REPEAT
          CarteraReportSelection.TESTFIELD("Report ID");
          REPORT.RUN(CarteraReportSelection."Report ID",FALSE,FALSE,PostedBillGr);
        UNTIL CarteraReportSelection.NEXT = 0;
      END;
    END;

    PROCEDURE PayablePostOnly@4(PmtOrd@1100000 : Record 7000020);
    BEGIN
      IF PmtOrd."No. Printed" = 0  THEN BEGIN
        IF NOT
          CONFIRM(
            Text1100003)
        THEN
          ERROR(Text1100001);
      END ELSE
        IF NOT
          CONFIRM(
            Text1100004,FALSE)
        THEN
          ERROR(Text1100001);

      PmtOrd.SETRECFILTER;
      REPORT.RUNMODAL(REPORT::"Post Payment Order",FALSE,FALSE,PmtOrd);
    END;

    PROCEDURE PayablePostAndPrint@1(PmtOrd@1100000 : Record 7000020);
    VAR
      PostedPmtOrd@1100001 : Record 7000021;
    BEGIN
      PmtOrd.SETRECFILTER;
      REPORT.RUNMODAL(REPORT::"Post Payment Order",FALSE,FALSE,PmtOrd);

      COMMIT;

      IF PostedPmtOrd.GET(PmtOrd."No.") THEN BEGIN
        PostedPmtOrd.SETRECFILTER;
        CarteraReportSelection.RESET;
        CarteraReportSelection.SETRANGE(Usage,CarteraReportSelection.Usage::"Posted Payment Order");
        CarteraReportSelection.FIND('-');
        REPEAT
          CarteraReportSelection.TESTFIELD("Report ID");
          REPORT.RUN(CarteraReportSelection."Report ID",FALSE,FALSE,PostedPmtOrd);
        UNTIL CarteraReportSelection.NEXT = 0;
      END;
    END;

    PROCEDURE PrintCounter@1170000000(Table@1170000004 : Integer;Number@1170000005 : Code[20]);
    VAR
      PostedBillGr@1170000003 : Record 7000006;
      ClosedBillGr@1170000002 : Record 7000007;
      PostedPaymentOrder@1170000001 : Record 7000021;
      ClosedPaymentOrder@1170000000 : Record 7000022;
      BillGr@1000 : Record 7000005;
      PaymentOrder@1001 : Record 7000020;
    BEGIN
      CASE TRUE OF
        Table = DATABASE::"Bill Group":
          BEGIN
            BillGr.GET(Number);
            BillGr."No. Printed" := BillGr."No. Printed" + 1;
            BillGr.MODIFY;
          END;
        Table = DATABASE::"Payment Order":
          BEGIN
            PaymentOrder.GET(Number);
            PaymentOrder."No. Printed" := PaymentOrder."No. Printed" + 1;
            PaymentOrder.MODIFY;
          END;
        Table = DATABASE::"Posted Bill Group":
          BEGIN
            PostedBillGr.GET(Number);
            PostedBillGr."No. Printed" := PostedBillGr."No. Printed" + 1;
            PostedBillGr.MODIFY;
          END;
        Table = DATABASE::"Closed Bill Group":
          BEGIN
            ClosedBillGr.GET(Number);
            ClosedBillGr."No. Printed" := ClosedBillGr."No. Printed" + 1;
            ClosedBillGr.MODIFY;
          END;
        Table = DATABASE::"Posted Payment Order":
          BEGIN
            PostedPaymentOrder.GET(Number);
            PostedPaymentOrder."No. Printed" := PostedPaymentOrder."No. Printed" + 1;
            PostedPaymentOrder.MODIFY;
          END;
        Table = DATABASE::"Closed Payment Order":
          BEGIN
            ClosedPaymentOrder.GET(Number);
            ClosedPaymentOrder."No. Printed" := ClosedPaymentOrder."No. Printed" + 1;
            ClosedPaymentOrder.MODIFY;
          END;
        END;
      COMMIT;
    END;

    BEGIN
    END.
  }
}
