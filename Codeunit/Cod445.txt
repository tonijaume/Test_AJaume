OBJECT Codeunit 445 Purch.-Post Prepmt.  (Yes/No)
{
  OBJECT-PROPERTIES
  {
    Date=13/02/07;
    Time=12:00:00;
    Version List=NAVW15.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to post the prepayments for %1 %2?;ESP=¨Desea registrar los prepagos para %1 %2?';
      Text001@1001 : TextConst 'ENU=Do you want to post a credit memo for the prepayments for %1 %2?;ESP=¨Desea registrar un abono para los prepagos para %1 %2?';
      PurchInvHeader@1003 : Record 122;
      PurchCrMemoHeader@1002 : Record 124;

    PROCEDURE PostPrepmtInvoiceYN@1(VAR PurchHeader2@1001 : Record 38;Print@1002 : Boolean);
    VAR
      PurchHeader@1003 : Record 38;
      PurchPostPrepmt@1000 : Codeunit 444;
    BEGIN
      PurchHeader.COPY(PurchHeader2);
      WITH PurchHeader DO BEGIN
        IF NOT CONFIRM(Text000,FALSE,"Document Type","No.") THEN
          EXIT;

        PurchPostPrepmt.Invoice(PurchHeader);

        IF Print THEN
          GetReport(PurchHeader,0);

        COMMIT;
        PurchHeader2 := PurchHeader;
      END;
    END;

    PROCEDURE PostPrepmtCrMemoYN@2(VAR PurchHeader2@1001 : Record 38;Print@1002 : Boolean);
    VAR
      PurchHeader@1003 : Record 38;
      PurchPostPrepmt@1000 : Codeunit 444;
    BEGIN
      PurchHeader.COPY(PurchHeader2);
      WITH PurchHeader DO BEGIN
        IF NOT CONFIRM(Text001,FALSE,"Document Type","No.") THEN
          EXIT;

        PurchPostPrepmt.CreditMemo(PurchHeader);

        IF Print THEN
          GetReport(PurchHeader,1);

        COMMIT;
        PurchHeader2 := PurchHeader;
      END;
    END;

    PROCEDURE GetReport@4(VAR PurchHeader@1000 : Record 38;DocumentType@1001 : 'Invoice,Credit Memo');
    VAR
      ReportSelection@1003 : Record 77;
    BEGIN
      WITH PurchHeader DO BEGIN
        CASE DocumentType OF
          DocumentType::Invoice:
            BEGIN
              PurchInvHeader."No." := "Last Prepayment No.";
              PurchInvHeader.SETRECFILTER;
              PrintReport(ReportSelection.Usage::"P.Invoice");
            END;
          DocumentType::"Credit Memo":
            BEGIN
              PurchCrMemoHeader."No." := "Last Prepmt. Cr. Memo No.";
              PurchCrMemoHeader.SETRECFILTER;
              PrintReport(ReportSelection.Usage::"P.Cr.Memo");
            END;
        END;
      END;
    END;

    LOCAL PROCEDURE PrintReport@3(ReportUsage@1000 : Integer);
    VAR
      ReportSelection@1001 : Record 77;
    BEGIN
      ReportSelection.SETRANGE(Usage,ReportUsage);
      ReportSelection.FIND('-');
      REPEAT
        ReportSelection.TESTFIELD("Report ID");
        CASE ReportUsage OF
          ReportSelection.Usage::"P.Invoice":
            REPORT.RUN(ReportSelection."Report ID",FALSE,FALSE,PurchInvHeader);
          ReportSelection.Usage::"P.Cr.Memo":
            REPORT.RUN(ReportSelection."Report ID",FALSE,FALSE,PurchCrMemoHeader);
        END;
      UNTIL ReportSelection.NEXT = 0;
    END;

    BEGIN
    END.
  }
}
