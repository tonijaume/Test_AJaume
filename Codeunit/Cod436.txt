OBJECT Codeunit 436 Acc. Sched. BarChart DrillDown
{
  OBJECT-PROPERTIES
  {
    Date=13/02/07;
    Time=12:00:00;
    Version List=NAVW15.00;
  }
  PROPERTIES
  {
    TableNo=450;
    OnRun=BEGIN
            AccSchedLine."Schedule Name" := DELCHR(COPYSTR(Tag,1,10),'>');
            CASE "Series No." OF
              1: EVALUATE(AccSchedLine."Line No.",DELCHR(COPYSTR(Tag,11,8),'>'));
              2: EVALUATE(AccSchedLine."Line No.",DELCHR(COPYSTR(Tag,19,8),'>'));
              3: EVALUATE(AccSchedLine."Line No.",DELCHR(COPYSTR(Tag,27,8),'>'));
            END;
            AccSchedLine.FIND;
            ColumnLayout."Column Layout Name" := DELCHR(COPYSTR(Tag,35,10),'>');
            EVALUATE(ColumnLayout."Line No.",DELCHR(COPYSTR(Tag,45,8),'>'));
            ColumnLayout.FIND;
            s := DELCHR(COPYSTR(Tag,53,20),'>');
            IF s <> '' THEN
              AccSchedLine.SETFILTER("Date Filter",s);
            s := DELCHR(COPYSTR(Tag,73,10),'>');
            IF s <> '' THEN
              AccSchedLine.SETFILTER("Budget Filter",s);
            s := DELCHR(COPYSTR(Tag,83,42),'>');
            IF s <> '' THEN
              AccSchedLine.SETFILTER("Dimension 1 Filter",s);
            s := DELCHR(COPYSTR(Tag,125,42),'>');
            IF s <> '' THEN
              AccSchedLine.SETFILTER("Dimension 2 Filter",s);
            s := DELCHR(COPYSTR(Tag,167,42),'>');
            IF s <> '' THEN
              AccSchedLine.SETFILTER("Dimension 3 Filter",s);
            s := DELCHR(COPYSTR(Tag,209,42),'>');
            IF s <> '' THEN
              AccSchedLine.SETFILTER("Dimension 4 Filter",s);

            AccSchedManagement.CheckAnalysisView(AccSchedLine."Schedule Name",ColumnLayout."Column Layout Name",TRUE);
            IF AccSchedManagement.CalcCell(AccSchedLine,ColumnLayout,FALSE) = 0 THEN; // init codeunit

            IF ColumnLayout."Column Type" = ColumnLayout."Column Type"::Formula THEN
              MESSAGE(Text002,ColumnLayout.Formula)
            ELSE
              WITH AccSchedLine DO
              IF "Totaling Type" IN ["Totaling Type"::Formula,"Totaling Type"::"Set Base For Percent"] THEN
                MESSAGE(Text003,Totaling)
              ELSE
                IF Totaling <> '' THEN BEGIN
                  COPYFILTER("Business Unit Filter",GLAcc."Business Unit Filter");
                  COPYFILTER("Budget Filter",GLAcc."Budget Filter");
                  AccSchedManagement.SetGLAccRowFilters(GLAcc,AccSchedLine);
                  AccSchedManagement.SetGLAccColumnFilters(GLAcc,AccSchedLine,ColumnLayout);
                  AccSchedName.GET(AccSchedLine."Schedule Name");
                  IF AccSchedName."Analysis View Name" = '' THEN BEGIN
                    COPYFILTER("Dimension 1 Filter",GLAcc."Global Dimension 1 Filter");
                    COPYFILTER("Dimension 2 Filter",GLAcc."Global Dimension 2 Filter");
                    COPYFILTER("Business Unit Filter",GLAcc."Business Unit Filter");
                    GLAcc.FILTERGROUP(2);
                    GLAcc.SETFILTER("Global Dimension 1 Filter",AccSchedManagement.GetDimTotalingFilter(1,"Dimension 1 Totaling"));
                    GLAcc.SETFILTER("Global Dimension 2 Filter",AccSchedManagement.GetDimTotalingFilter(2,"Dimension 2 Totaling"));
                    GLAcc.FILTERGROUP(6);
                    GLAcc.SETFILTER("Global Dimension 1 Filter",AccSchedManagement.GetDimTotalingFilter(1,ColumnLayout."Dimension 1 Totaling")
            );
                    GLAcc.SETFILTER("Global Dimension 2 Filter",AccSchedManagement.GetDimTotalingFilter(2,ColumnLayout."Dimension 2 Totaling")
            );
                    GLAcc.SETFILTER("Business Unit Filter",ColumnLayout."Business Unit Totaling");
                    GLAcc.FILTERGROUP(0);
                    FORM.RUN(FORM::"Chart of Accounts (G/L)",GLAcc)
                  END ELSE BEGIN
                    GLAcc.COPYFILTER("Date Filter",GLAccAnalysisView."Date Filter");
                    GLAcc.COPYFILTER("Budget Filter",GLAccAnalysisView."Budget Filter");
                    GLAcc.COPYFILTER("Business Unit Filter",GLAccAnalysisView."Business Unit Filter");
                    GLAccAnalysisView.SETRANGE("Analysis View Filter",AccSchedName."Analysis View Name");
                    COPYFILTER("Dimension 1 Filter",GLAccAnalysisView."Dimension 1 Filter");
                    COPYFILTER("Dimension 2 Filter",GLAccAnalysisView."Dimension 2 Filter");
                    COPYFILTER("Dimension 3 Filter",GLAccAnalysisView."Dimension 3 Filter");
                    COPYFILTER("Dimension 4 Filter",GLAccAnalysisView."Dimension 4 Filter");
                    GLAccAnalysisView.FILTERGROUP(2);
                    GLAccAnalysisView.SETFILTER("Dimension 1 Filter",AccSchedManagement.GetDimTotalingFilter(1,"Dimension 1 Totaling"));
                    GLAccAnalysisView.SETFILTER("Dimension 2 Filter",AccSchedManagement.GetDimTotalingFilter(2,"Dimension 2 Totaling"));
                    GLAccAnalysisView.SETFILTER("Dimension 3 Filter",AccSchedManagement.GetDimTotalingFilter(3,"Dimension 3 Totaling"));
                    GLAccAnalysisView.SETFILTER("Dimension 4 Filter",AccSchedManagement.GetDimTotalingFilter(4,"Dimension 4 Totaling"));
                    GLAccAnalysisView.FILTERGROUP(6);
                    GLAccAnalysisView.SETFILTER(
                      "Dimension 1 Filter",
                      AccSchedManagement.GetDimTotalingFilter(1,ColumnLayout."Dimension 1 Totaling"));
                    GLAccAnalysisView.SETFILTER(
                      "Dimension 2 Filter",
                      AccSchedManagement.GetDimTotalingFilter(2,ColumnLayout."Dimension 2 Totaling"));
                    GLAccAnalysisView.SETFILTER(
                      "Dimension 3 Filter",
                      AccSchedManagement.GetDimTotalingFilter(3,ColumnLayout."Dimension 3 Totaling"));
                    GLAccAnalysisView.SETFILTER(
                      "Dimension 4 Filter",
                      AccSchedManagement.GetDimTotalingFilter(4,ColumnLayout."Dimension 4 Totaling"));
                    GLAccAnalysisView.SETFILTER("Business Unit Filter",ColumnLayout."Business Unit Totaling");
                    GLAccAnalysisView.FILTERGROUP(0);
                    CLEAR(ChartofAccAnalysisView);
                    ChartofAccAnalysisView.InsertTempGLAccAnalysisViews(GLAcc);
                    ChartofAccAnalysisView.SETTABLEVIEW(GLAccAnalysisView);
                    ChartofAccAnalysisView.RUN;
                  END;
                END;
          END;

  }
  CODE
  {
    VAR
      AccSchedLine@1004 : Record 85;
      GLAcc@1003 : Record 15;
      GLAccAnalysisView@1002 : Record 376;
      ColumnLayout@1001 : Record 334;
      AccSchedName@1008 : Record 84;
      ChartofAccAnalysisView@1000 : Form 569;
      Text002@1006 : TextConst 'ENU=Column formula: %1;ESP=F¢rmula columna: %1';
      Text003@1005 : TextConst 'ENU=Row formula: %1;ESP=F¢rmula fila: %1';
      AccSchedManagement@1007 : Codeunit 8;
      s@1009 : Text[50];

    BEGIN
    END.
  }
}
