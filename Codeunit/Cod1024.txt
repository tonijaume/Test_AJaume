OBJECT Codeunit 1024 Job Jnl.-B.Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=13/02/07;
    Time=12:00:00;
    Version List=NAVW15.00;
  }
  PROPERTIES
  {
    TableNo=237;
    OnRun=BEGIN
            JobJnlBatch.COPY(Rec);
            Code;
            Rec.COPY(JobJnlBatch);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to post the journals and print the posting report?;ESP=Confirma que desea registrar los diarios e imprimir el informe de registro?';
      Text001@1001 : TextConst 'ENU=The journals were successfully posted.;ESP=Se han registrado correctamente los diarios.';
      Text002@1002 : TextConst 'ENU="It was not possible to post all of the journals. ";ESP="No ha sido posible registrar todos los diarios. "';
      Text003@1003 : TextConst 'ENU=The journals that were not successfully posted are now marked.;ESP=Los diarios que no se han registrado estn marcados.';
      JobJnlTemplate@1004 : Record 209;
      JobJnlBatch@1005 : Record 237;
      JobJnlLine@1006 : Record 210;
      JobReg@1007 : Record 241;
      JobJnlPostbatch@1008 : Codeunit 1013;
      JnlWithErrors@1009 : Boolean;

    LOCAL PROCEDURE Code@1();
    BEGIN
      WITH JobJnlBatch DO BEGIN
        JobJnlTemplate.GET("Journal Template Name");
        JobJnlTemplate.TESTFIELD("Posting Report ID");

        IF NOT CONFIRM(Text000) THEN
          EXIT;

        FIND('-');
        REPEAT
          JobJnlLine."Journal Template Name" := "Journal Template Name";
          JobJnlLine."Journal Batch Name" := Name;
          JobJnlLine."Line No." := 1;
          CLEAR(JobJnlPostbatch);
          IF JobJnlPostbatch.RUN(JobJnlLine) THEN BEGIN
            MARK(FALSE);
            IF JobReg.GET(JobJnlLine."Line No.") THEN BEGIN
              JobReg.SETRECFILTER;
              REPORT.RUN(JobJnlTemplate."Posting Report ID",FALSE,FALSE,JobReg);
            END;
          END ELSE BEGIN
            MARK(TRUE);
            JnlWithErrors := TRUE;
          END;
        UNTIL NEXT = 0;

        IF NOT JnlWithErrors THEN
          MESSAGE(Text001)
        ELSE
          MESSAGE(
            Text002 +
            Text003);

        IF NOT FIND('=><') THEN BEGIN
          RESET;
          Name := '';
        END;
      END;
    END;

    BEGIN
    END.
  }
}
