OBJECT Codeunit 5651 Insurance Jnl.-Check Line
{
  OBJECT-PROPERTIES
  {
    Date=08/06/01;
    Time=12:00:00;
    Version List=NAVW13.00;
  }
  PROPERTIES
  {
    TableNo=5635;
    OnRun=VAR
            TempJnlLineDim@1000 : TEMPORARY Record 356;
          BEGIN
            GLSetup.GET;
            IF "Shortcut Dimension 1 Code" <> '' THEN BEGIN
              TempJnlLineDim."Table ID" := DATABASE::"Insurance Journal Line";
              TempJnlLineDim."Journal Template Name" := "Journal Template Name";
              TempJnlLineDim."Journal Batch Name" := "Journal Batch Name";
              TempJnlLineDim."Journal Line No." := "Line No.";
              TempJnlLineDim."Dimension Code" := GLSetup."Global Dimension 1 Code";
              TempJnlLineDim."Dimension Value Code" := "Shortcut Dimension 1 Code";
              TempJnlLineDim.INSERT;
            END;
            IF "Shortcut Dimension 2 Code" <> '' THEN BEGIN
              TempJnlLineDim."Table ID" := DATABASE::"Insurance Journal Line";
              TempJnlLineDim."Journal Template Name" := "Journal Template Name";
              TempJnlLineDim."Journal Batch Name" := "Journal Batch Name";
              TempJnlLineDim."Journal Line No." := "Line No.";
              TempJnlLineDim."Dimension Code" := GLSetup."Global Dimension 2 Code";
              TempJnlLineDim."Dimension Value Code" := "Shortcut Dimension 2 Code";
              TempJnlLineDim.INSERT;
            END;
            RunCheck(Rec,TempJnlLineDim);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=The combination of dimensions used in %1 %2, %3, %4 is blocked. %5;ESP=La combin. de dimensiones utilizada en %1 %2, %3, %4 est  bloq. %5';
      Text001@1001 : TextConst 'ENU=A dimension used in %1 %2, %3, %4 has caused an error. %5;ESP=La dimensi¢n util. en %1 %2, %3, %4 ha causado error. %5';
      GLSetup@1002 : Record 98;
      FASetup@1003 : Record 5603;
      DimMgt@1004 : Codeunit 408;
      CallNo@1005 : Integer;

    PROCEDURE RunCheck@1(VAR InsuranceJnlLine@1000 : Record 5635;VAR JnlLineDim@1001 : Record 356);
    VAR
      TableID@1002 : ARRAY [10] OF Integer;
      No@1003 : ARRAY [10] OF Code[20];
    BEGIN
      WITH InsuranceJnlLine DO BEGIN
        IF "Insurance No." = '' THEN
          EXIT;
        TESTFIELD("Insurance No.");
        TESTFIELD("Document No.");
        TESTFIELD("Posting Date");
        TESTFIELD("FA No.");
        IF CallNo = 0 THEN BEGIN
          FASetup.GET;
          FASetup.TESTFIELD("Insurance Depr. Book");
        END;
        CallNo := 1;

        IF NOT DimMgt.CheckJnlLineDimComb(JnlLineDim) THEN
          ERROR(
            Text000,
            TABLECAPTION,"Journal Template Name","Journal Batch Name","Line No.",
            DimMgt.GetDimCombErr);

        TableID[1] := DATABASE::Insurance;
        No[1] := "Insurance No.";
        IF NOT DimMgt.CheckJnlLineDimValuePosting(JnlLineDim,TableID,No) THEN
          IF "Line No." <> 0 THEN
            ERROR(
              Text001,
              TABLECAPTION,"Journal Template Name","Journal Batch Name","Line No.",
              DimMgt.GetDimValuePostingErr)
          ELSE
            ERROR(DimMgt.GetDimValuePostingErr);
      END;
    END;

    BEGIN
    END.
  }
}
