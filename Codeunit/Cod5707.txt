OBJECT Codeunit 5707 TransferOrder-Post + Print
{
  OBJECT-PROPERTIES
  {
    Date=08/06/01;
    Time=12:00:00;
    Version List=NAVW13.00;
  }
  PROPERTIES
  {
    TableNo=5740;
    OnRun=BEGIN
            TransHeader.COPY(Rec);
            Code;
            Rec := TransHeader;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=&Ship,&Receive;ESP=&Enviar,&Recibir';
      TransHeader@1001 : Record 5740;
      TransShptHeader@1002 : Record 5744;
      TransRcptHeader@1003 : Record 5746;

    LOCAL PROCEDURE Code@2();
    VAR
      TransLine@1000 : Record 5741;
      DefaultNumber@1001 : ' ,Shipment,Receipt';
      TransferPostShipment@1002 : Codeunit 5704;
      TransferPostReceipt@1003 : Codeunit 5705;
      Selection@1004 : ' ,Shipment,Receipt';
    BEGIN
      WITH TransHeader DO BEGIN
        TransLine.SETRANGE("Document No.","No.");
        IF TransLine.FIND('-') THEN BEGIN
          REPEAT
            IF (TransLine."Quantity Shipped" < TransLine.Quantity) AND
               (DefaultNumber = DefaultNumber::" ") THEN
              DefaultNumber := DefaultNumber::Shipment;
            IF (TransLine."Quantity Received" < TransLine.Quantity) AND
               (DefaultNumber = DefaultNumber::" ") THEN
              DefaultNumber := DefaultNumber::Receipt;
          UNTIL (TransLine.NEXT = 0) OR (DefaultNumber > 0);
        END;
        IF DefaultNumber = 0 THEN
          DefaultNumber := 1;
        Selection := STRMENU(Text000,DefaultNumber);
        CASE Selection OF
          0:
            EXIT;
          1:
            BEGIN
              TransferPostShipment.RUN(TransHeader);
              TransShptHeader."No." := "Last Shipment No.";
              TransShptHeader.SETRECFILTER;
              TransShptHeader.PrintRecords(FALSE);
            END;
          2:
            BEGIN
              TransferPostReceipt.RUN(TransHeader);
              TransRcptHeader."No." := "Last Receipt No.";
              TransRcptHeader.SETRECFILTER;
              TransRcptHeader.PrintRecords(FALSE);
            END;
        END;
      END;
    END;

    BEGIN
    END.
  }
}
