OBJECT Codeunit 5772 Whse.-Purch. Release
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      WhseRqst@1000 : Record 5765;
      PurchLine@1001 : Record 39;
      Location@1002 : Record 14;
      OldLocationCode@1003 : Code[10];
      First@1004 : Boolean;

    PROCEDURE Release@1(PurchHeader@1000 : Record 38);
    VAR
      WhseType@1002 : 'Inbound,Outbound';
      OldWhseType@1001 : 'Inbound,Outbound';
    BEGIN
      WITH PurchHeader DO BEGIN
        CASE "Document Type" OF
          "Document Type"::Order:
              WhseRqst."Source Document" := WhseRqst."Source Document"::"Purchase Order";
          "Document Type"::"Return Order":
              WhseRqst."Source Document" := WhseRqst."Source Document"::"Purchase Return Order";
          ELSE
            EXIT;
        END;

        PurchLine.SETCURRENTKEY("Document Type","Document No.","Location Code");
        PurchLine.SETRANGE("Document Type","Document Type");
        PurchLine.SETRANGE("Document No.","No.");
        PurchLine.SETRANGE(Type,PurchLine.Type::Item);
        PurchLine.SETRANGE("Drop Shipment",FALSE);
        PurchLine.SETRANGE("Job No.",'');
        PurchLine.SETRANGE("Work Center No.",'');
        IF PurchLine.FIND('-') THEN BEGIN
          First := TRUE;
          REPEAT
            IF (("Document Type" = "Document Type":: Order) AND
                (PurchLine.Quantity >= 0)) OR
               (("Document Type" = "Document Type"::"Return Order") AND
                (PurchLine.Quantity < 0))
            THEN
              WhseType := WhseType::Inbound
            ELSE
              WhseType := WhseType::Outbound;
            IF First OR (PurchLine."Location Code" <> OldLocationCode) OR
               (WhseType <> OldWhseType)
            THEN
              CreateWhseRqst(PurchHeader,WhseType);
            First := FALSE;
            OldLocationCode := PurchLine."Location Code";
            OldWhseType := WhseType;
          UNTIL PurchLine.NEXT = 0;
        END;

        WhseRqst.RESET;
        WhseRqst.SETCURRENTKEY("Source Type","Source Subtype","Source No.");
        WhseRqst.SETRANGE(Type,WhseRqst.Type);
        WhseRqst.SETRANGE("Source Type",DATABASE::"Purchase Line");
        WhseRqst.SETRANGE("Source Subtype","Document Type");
        WhseRqst.SETRANGE("Source No.","No.");
        WhseRqst.SETRANGE("Document Status",Status::Open);
        WhseRqst.DELETEALL(TRUE);
      END;
    END;

    PROCEDURE Reopen@2(PurchHeader@1000 : Record 38);
    BEGIN
      WITH PurchHeader DO BEGIN
        CASE "Document Type" OF
          "Document Type"::Order:
            WhseRqst.Type := WhseRqst.Type::Inbound;
          "Document Type"::"Return Order":
            WhseRqst.Type := WhseRqst.Type::Outbound;
        END;

        WhseRqst.RESET;
        WhseRqst.SETCURRENTKEY("Source Type","Source Subtype","Source No.");
        WhseRqst.SETRANGE(Type,WhseRqst.Type);
        WhseRqst.SETRANGE("Source Type",DATABASE::"Purchase Line");
        WhseRqst.SETRANGE("Source Subtype","Document Type");
        WhseRqst.SETRANGE("Source No.","No.");
        WhseRqst.SETRANGE("Document Status",Status::Released);
        IF WhseRqst.FIND('-') THEN
          REPEAT
            WhseRqst."Document Status" := Status::Open;
            WhseRqst.MODIFY;
          UNTIL WhseRqst.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE CreateWhseRqst@3(VAR PurchHeader@1000 : Record 38;WhseType@1002 : 'Inbound,Outbound');
    VAR
      PurchLine2@1001 : Record 39;
    BEGIN
      IF ((WhseType = WhseType::Outbound) AND
          (Location.RequireShipment(PurchLine."Location Code") OR
           Location.RequirePicking(PurchLine."Location Code"))) OR
         ((WhseType = WhseType::Inbound) AND
          (Location.RequireReceive(PurchLine."Location Code") OR
           Location.RequirePutaway(PurchLine."Location Code")))
      THEN BEGIN
        PurchLine2.COPY(PurchLine);
        PurchLine2.SETRANGE("Location Code",PurchLine."Location Code");
        PurchLine2.SETRANGE("Unit of Measure Code",'');
        IF PurchLine2.FIND('-') THEN
          PurchLine2.TESTFIELD("Unit of Measure Code");

        WhseRqst.Type := WhseType;
        WhseRqst."Source Type" := DATABASE::"Purchase Line";
        WhseRqst."Source Subtype" := PurchHeader."Document Type";
        WhseRqst."Source No." := PurchHeader."No.";
        WhseRqst."Shipment Method Code" := PurchHeader."Shipment Method Code";
        WhseRqst."Document Status" := PurchHeader.Status::Released;
        WhseRqst."Location Code" := PurchLine."Location Code";
        WhseRqst."Destination Type" := WhseRqst."Destination Type"::Vendor;
        WhseRqst."Destination No." := PurchHeader."Buy-from Vendor No.";
        WhseRqst."External Document No." := PurchHeader."Vendor Shipment No.";
        IF WhseType = WhseType::Inbound THEN
          WhseRqst."Expected Receipt Date" := PurchHeader."Expected Receipt Date"
        ELSE
          WhseRqst."Shipment Date" := PurchHeader."Expected Receipt Date";
        PurchHeader.SETRANGE("Location Filter",PurchLine."Location Code");
        PurchHeader.CALCFIELDS("Completely Received");
        WhseRqst."Completely Handled" := PurchHeader."Completely Received";
        IF NOT WhseRqst.INSERT THEN
          WhseRqst.MODIFY;
      END;
    END;

    BEGIN
    END.
  }
}
