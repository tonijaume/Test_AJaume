OBJECT Codeunit 99000830 Create Reserv. Entry
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    Permissions=TableData 337=rim;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Reservation is illegal.;ESP=Reserva es incorrecta.';
      InsertReservEntry@1001 : Record 337;
      InsertReservEntry2@1002 : Record 337;
      LastReservEntry@1003 : Record 337;
      TempTrkgSpec1@1005 : TEMPORARY Record 336;
      TempTrkgSpec2@1004 : TEMPORARY Record 336;
      Text001@1006 : TextConst 'ENU=Cannot match item tracking.;ESP=No coincide seguimiento producto.';
      OverruleItemTracking@1008 : Boolean;
      Inbound@1009 : Boolean;
      UseQtyToInvoice@1010 : Boolean;
      QtyToHandleAndInvoiceIsSet@1024 : Boolean;

    PROCEDURE CreateEntry@5(ItemNo@1000 : Code[20];VariantCode@1001 : Code[10];LocationCode@1002 : Code[10];Description@1004 : Text[50];ExpectedReceiptDate@1005 : Date;ShipmentDate@1006 : Date;TransferredFromEntryNo@1007 : Integer;Status@1008 : 'Reservation,Tracking,Surplus,Prospect');
    VAR
      ReservEntry@1009 : Record 337;
      ReservEntry2@1010 : Record 337;
      ReservEntry3@1011 : Record 337;
      ReservMgt@1012 : Codeunit 99000845;
      LastEntryNo@1016 : Integer;
      TrackingSpecificationExists@1013 : Boolean;
      FirstSplit@1014 : Boolean;
    BEGIN
      TempTrkgSpec1.RESET;
      TempTrkgSpec2.RESET;
      TempTrkgSpec1.DELETEALL;
      TempTrkgSpec2.DELETEALL;

      // Status Surplus gets special treatment.

      IF Status < Status::Surplus THEN
        IF InsertReservEntry."Quantity (Base)" = 0 THEN
          EXIT;

      InsertReservEntry.TESTFIELD("Source Type");

      ReservEntry := InsertReservEntry;
      ReservEntry."Reservation Status" := Status;
      ReservEntry."Item No." := ItemNo;
      ReservEntry."Variant Code" := VariantCode;
      ReservEntry."Location Code" := LocationCode;
      ReservEntry.Description := Description;
      ReservEntry."Creation Date" := WORKDATE;
      ReservEntry."Created By" := USERID;
      ReservEntry."Expected Receipt Date" := ExpectedReceiptDate;
      ReservEntry."Shipment Date" := ShipmentDate;
      ReservEntry."Transferred from Entry No." := TransferredFromEntryNo;
      ReservEntry.Positive := (ReservEntry."Quantity (Base)" > 0);
      ReservEntry.Quantity := ReservEntry."Quantity (Base)"/ReservEntry."Qty. per Unit of Measure";
      IF NOT QtyToHandleAndInvoiceIsSet THEN BEGIN
        ReservEntry."Qty. to Handle (Base)" := ReservEntry."Quantity (Base)";
        ReservEntry."Qty. to Invoice (Base)" := ReservEntry."Quantity (Base)";
      END;


      IF Status < Status::Surplus THEN BEGIN
        InsertReservEntry2.TESTFIELD("Source Type");

        ReservEntry2 := ReservEntry;
        ReservEntry2."Quantity (Base)" := -ReservEntry."Quantity (Base)";
        ReservEntry2."Qty. to Handle (Base)" := -ReservEntry."Qty. to Handle (Base)";
        ReservEntry2."Qty. to Invoice (Base)" := -ReservEntry."Qty. to Invoice (Base)";
        ReservEntry2.Positive := (ReservEntry2."Quantity (Base)" > 0);
        ReservEntry2."Source Type" := InsertReservEntry2."Source Type";
        ReservEntry2."Source Subtype" := InsertReservEntry2."Source Subtype";
        ReservEntry2."Source ID" := InsertReservEntry2."Source ID";
        ReservEntry2."Source Batch Name" := InsertReservEntry2."Source Batch Name";
        ReservEntry2."Source Prod. Order Line" := InsertReservEntry2."Source Prod. Order Line";
        ReservEntry2."Source Ref. No." := InsertReservEntry2."Source Ref. No.";
        ReservEntry2."Serial No." := InsertReservEntry2."Serial No.";
        ReservEntry2."Lot No." := InsertReservEntry2."Lot No.";
        ReservEntry2."Qty. per Unit of Measure" := InsertReservEntry2."Qty. per Unit of Measure";
        ReservEntry2.Quantity := ReservEntry2."Quantity (Base)"/ReservEntry2."Qty. per Unit of Measure";
        IF NOT QtyToHandleAndInvoiceIsSet THEN BEGIN
          ReservEntry2."Qty. to Handle (Base)" := ReservEntry2."Quantity (Base)";
          ReservEntry2."Qty. to Invoice (Base)" := ReservEntry2."Quantity (Base)";
        END;

        IF NOT ReservEntry2.Positive THEN
          ReservEntry2."Appl.-from Item Entry" := 0;
        IF Status = Status::Reservation THEN
          IF TransferredFromEntryNo = 0 THEN BEGIN
            ReservMgt.MakeRoomForReservation(ReservEntry2);
            TrackingSpecificationExists :=
              ReservMgt.CollectTrackingSpecification(TempTrkgSpec2);
          END;
        CheckValidity(ReservEntry2);
        AdjustDateIfItemLedgerEntry(ReservEntry2);
      END;

      IF NOT ReservEntry.Positive THEN
        ReservEntry."Appl.-from Item Entry" := 0;

      CheckValidity(ReservEntry);
      AdjustDateIfItemLedgerEntry(ReservEntry);
      IF Status = Status::Reservation THEN
        IF TransferredFromEntryNo = 0 THEN BEGIN
          ReservMgt.MakeRoomForReservation(ReservEntry);
          TrackingSpecificationExists := TrackingSpecificationExists OR
            ReservMgt.CollectTrackingSpecification(TempTrkgSpec1);
        END;

      IF TrackingSpecificationExists THEN
        SetupSplitReservEntry(ReservEntry,ReservEntry2);

      ReservEntry3.LOCKTABLE;
      IF ReservEntry3.FINDLAST THEN
        LastEntryNo := ReservEntry3."Entry No.";

      FirstSplit := TRUE;
      WHILE SplitReservEntry(ReservEntry,ReservEntry2,TrackingSpecificationExists,FirstSplit) DO BEGIN
        ReservEntry."Entry No." := LastEntryNo + 1;
        ReservEntry.UpdateItemTracking;
        ReservEntry.INSERT;
        IF Status < Status::Surplus THEN BEGIN
          ReservEntry2."Entry No." := ReservEntry."Entry No.";
          ReservEntry2.UpdateItemTracking;
          ReservEntry2.INSERT;
        END;
        LastEntryNo := ReservEntry."Entry No.";
      END;

      LastReservEntry := ReservEntry;

      CLEAR(InsertReservEntry);
      CLEAR(InsertReservEntry2);
      CLEAR(QtyToHandleAndInvoiceIsSet);
    END;

    PROCEDURE CreateReservEntry@6(ItemNo@1000 : Code[20];VariantCode@1001 : Code[10];LocationCode@1002 : Code[10];Description@1004 : Text[50];ExpectedReceiptDate@1005 : Date;ShipmentDate@1006 : Date;TransferedFromEntryNo@1007 : Integer);
    BEGIN
      CreateEntry(ItemNo,VariantCode,LocationCode,Description,
        ExpectedReceiptDate,ShipmentDate,TransferedFromEntryNo,0);
    END;

    PROCEDURE CreateReservEntryFor@40(ForType@1000 : Option;ForSubtype@1001 : Integer;ForID@1002 : Code[20];ForBatchName@1003 : Code[10];ForProdOrderLine@1004 : Integer;ForRefNo@1005 : Integer;ForQtyPerUOM@1006 : Decimal;Quantity@1007 : Decimal;ForSerialNo@1008 : Code[20];ForLotNo@1009 : Code[20]);
    BEGIN
      InsertReservEntry."Source Type" := ForType;
      InsertReservEntry."Source Subtype" := ForSubtype;
      InsertReservEntry."Source ID" := ForID;
      InsertReservEntry."Source Batch Name" := ForBatchName;
      InsertReservEntry."Source Prod. Order Line" := ForProdOrderLine;
      InsertReservEntry."Source Ref. No." := ForRefNo;
      InsertReservEntry."Quantity (Base)" := SignFactor(InsertReservEntry) * Quantity;
      InsertReservEntry."Qty. per Unit of Measure" := ForQtyPerUOM;
      InsertReservEntry."Serial No." := ForSerialNo;
      InsertReservEntry."Lot No." := ForLotNo;

      InsertReservEntry.TESTFIELD("Qty. per Unit of Measure");
    END;

    PROCEDURE CreateReservEntryFrom@57(FromType@1000 : Option;FromSubtype@1001 : Integer;FromID@1002 : Code[20];FromBatchName@1003 : Code[10];FromProdOrderLine@1004 : Integer;FromRefNo@1005 : Integer;FromQtyPerUOM@1006 : Decimal;FromSerialNo@1007 : Code[20];FromLotNo@1008 : Code[20]);
    BEGIN
      InsertReservEntry2."Source Type" := FromType;
      InsertReservEntry2."Source Subtype" := FromSubtype;
      InsertReservEntry2."Source ID" := FromID;
      InsertReservEntry2."Source Batch Name" := FromBatchName;
      InsertReservEntry2."Source Prod. Order Line" := FromProdOrderLine;
      InsertReservEntry2."Source Ref. No." := FromRefNo;
      InsertReservEntry2."Qty. per Unit of Measure" := FromQtyPerUOM;
      InsertReservEntry2."Serial No." := FromSerialNo;
      InsertReservEntry2."Lot No." := FromLotNo;

      InsertReservEntry2.TESTFIELD("Qty. per Unit of Measure");
    END;

    PROCEDURE SetBinding@8(Binding@1000 : ' ,Order-to-order');
    BEGIN
      InsertReservEntry.Binding := Binding;
      InsertReservEntry2.Binding := Binding;
    END;

    PROCEDURE SetPlanningFlexibility@4(Flexibility@1000 : 'Unlimited,None');
    BEGIN
      InsertReservEntry."Planning Flexibility" := Flexibility;
      InsertReservEntry2."Planning Flexibility" := Flexibility;
    END;

    PROCEDURE SetDates@13(WarrantyDate@1001 : Date;ExpirationDate@1000 : Date);
    BEGIN
      InsertReservEntry."Warranty Date" := WarrantyDate;
      InsertReservEntry."Expiration Date" := ExpirationDate;
    END;

    PROCEDURE SetQtyToHandleAndInvoice@12(QtyToHandleBase@1000 : Decimal;QtyToInvoiceBase@1001 : Decimal);
    BEGIN
      InsertReservEntry."Qty. to Handle (Base)" := QtyToHandleBase;
      InsertReservEntry."Qty. to Invoice (Base)" := QtyToInvoiceBase;
      QtyToHandleAndInvoiceIsSet := TRUE;
    END;

    PROCEDURE SetNewSerialLotNo@11(NewSerialNo@1001 : Code[20];NewLotNo@1000 : Code[20]);
    BEGIN
      InsertReservEntry."New Serial No." := NewSerialNo;
      InsertReservEntry."New Lot No." := NewLotNo;
    END;

    PROCEDURE SetNewExpirationDate@19(NewExpirationDate@1001 : Date);
    BEGIN
      InsertReservEntry."New Expiration Date" := NewExpirationDate;
    END;

    PROCEDURE CreateRemainingReservEntry@58(VAR OldReservEntry@1000 : Record 337;RemainingQuantity@1001 : Decimal);
    VAR
      OldReservEntry2@1002 : Record 337;
    BEGIN
      CreateReservEntryFor(
        OldReservEntry."Source Type",OldReservEntry."Source Subtype",
        OldReservEntry."Source ID",OldReservEntry."Source Batch Name",
        OldReservEntry."Source Prod. Order Line",OldReservEntry."Source Ref. No.",
        OldReservEntry."Qty. per Unit of Measure",RemainingQuantity,
        OldReservEntry."Serial No.",OldReservEntry."Lot No.");
      InsertReservEntry."Warranty Date" := OldReservEntry."Warranty Date";
      InsertReservEntry."Expiration Date" := OldReservEntry."Expiration Date";

      IF OldReservEntry."Reservation Status" < OldReservEntry."Reservation Status"::Surplus THEN
        IF OldReservEntry2.GET(OldReservEntry."Entry No.",NOT OldReservEntry.Positive) THEN BEGIN // Get the related entry
          CreateReservEntryFrom(
            OldReservEntry2."Source Type",OldReservEntry2."Source Subtype",
            OldReservEntry2."Source ID",OldReservEntry2."Source Batch Name",
            OldReservEntry2."Source Prod. Order Line",OldReservEntry2."Source Ref. No.",OldReservEntry2."Qty. per Unit of Measure",
            OldReservEntry2."Serial No.",OldReservEntry2."Lot No.");
            InsertReservEntry2."Warranty Date" := OldReservEntry2."Warranty Date";
            InsertReservEntry2."Expiration Date" := OldReservEntry2."Expiration Date";
        END;
      CreateEntry(
        OldReservEntry."Item No.",OldReservEntry."Variant Code",
        OldReservEntry."Location Code",OldReservEntry.Description,
        OldReservEntry."Expected Receipt Date",OldReservEntry."Shipment Date",
        OldReservEntry."Entry No.",OldReservEntry."Reservation Status");
    END;

    PROCEDURE TransferReservEntry@1(NewType@1000 : Option;NewSubtype@1001 : Integer;NewID@1002 : Code[20];NewBatchName@1003 : Code[10];NewProdOrderLine@1004 : Integer;NewRefNo@1005 : Integer;QtyPerUOM@1006 : Decimal;OldReservEntry@1007 : Record 337;TransferQty@1008 : Decimal) : Decimal;
    VAR
      NewReservEntry@1009 : Record 337;
      RelatedReservEntry@1016 : Record 337;
      ReservEntry@1021 : Record 337;
      Location@1020 : Record 14;
      ItemTrkgMgt@1017 : Codeunit 6500;
      CurrSignFactor@1010 : Integer;
      xTransferQty@1011 : Decimal;
      QtyToHandleThisLine@1014 : Decimal;
      QtyToInvoiceThisLine@1013 : Decimal;
      QtyInvoiced@1015 : Decimal;
      CarriedSerialNo@1022 : Code[20];
      CarriedLotNo@1023 : Code[20];
      UseQtyToHandle@1012 : Boolean;
      SNRequired@1018 : Boolean;
      LNRequired@1019 : Boolean;
    BEGIN
      IF TransferQty = 0 THEN
        EXIT;

      UseQtyToHandle :=
        ((OldReservEntry."Serial No." <> '') OR (OldReservEntry."Lot No." <> '')) AND
        NOT OverruleItemTracking;

      CurrSignFactor := SignFactor(OldReservEntry);
      TransferQty := TransferQty * CurrSignFactor;
      xTransferQty := TransferQty;

      IF UseQtyToHandle THEN BEGIN // Used when handling Item Tracking
        QtyToHandleThisLine := OldReservEntry."Qty. to Handle (Base)";
        QtyToInvoiceThisLine := OldReservEntry."Qty. to Invoice (Base)";
        IF ABS(TransferQty) > ABS(QtyToHandleThisLine) THEN
          TransferQty := QtyToHandleThisLine;
        IF UseQtyToInvoice THEN BEGIN // Used when posting sales and purchase
          IF ABS(TransferQty) > ABS(QtyToInvoiceThisLine) THEN
            TransferQty := QtyToInvoiceThisLine;
        END;
      END ELSE BEGIN
        QtyToHandleThisLine := OldReservEntry."Quantity (Base)";
      END;

      IF QtyToHandleThisLine = 0 THEN
        EXIT(xTransferQty * CurrSignFactor);

      NewReservEntry.TRANSFERFIELDS(OldReservEntry,FALSE);

      NewReservEntry."Entry No." := OldReservEntry."Entry No.";
      NewReservEntry.Positive := OldReservEntry.Positive;
      NewReservEntry."Source Type" := NewType;
      NewReservEntry."Source Subtype" := NewSubtype;
      NewReservEntry."Source ID" := NewID;
      NewReservEntry."Source Batch Name" := NewBatchName;
      NewReservEntry."Source Prod. Order Line" := NewProdOrderLine;
      NewReservEntry."Source Ref. No." := NewRefNo;
      NewReservEntry."Qty. per Unit of Measure" := QtyPerUOM;

      // Item Tracking on consumption, output and drop shipment:
      IF (NewType = DATABASE::"Item Journal Line") AND (NewSubtype IN [5,6]) OR OverruleItemTracking THEN
        IF (InsertReservEntry."New Serial No." <> '') OR (InsertReservEntry."New Lot No." <> '') THEN BEGIN
          NewReservEntry."Serial No." := InsertReservEntry."New Serial No.";
          NewReservEntry."Lot No." := InsertReservEntry."New Lot No.";
          IF NewReservEntry."Qty. to Handle (Base)" = 0 THEN
            NewReservEntry."Qty. to Handle (Base)" := NewReservEntry."Quantity (Base)";
          InsertReservEntry."New Serial No." := '';
          InsertReservEntry."New Lot No." := '';

          // If an order-to-order supply is being posted, item tracking must be carried to the related demand:
          IF (TransferQty >= 0) AND (NewReservEntry.Binding = NewReservEntry.Binding::"Order-to-Order") THEN BEGIN
            CarriedSerialNo := NewReservEntry."Serial No.";
            CarriedLotNo := NewReservEntry."Lot No.";
            IF NOT UseQtyToHandle THEN
              // the IT is set only in Consumption/Output Journal and we need to update all fields properly
              QtyToInvoiceThisLine := NewReservEntry."Quantity (Base)";
          END;
        END;

      IF InsertReservEntry."Appl.-to Item Entry" <> 0 THEN BEGIN
        NewReservEntry."Appl.-to Item Entry" := InsertReservEntry."Appl.-to Item Entry";
        InsertReservEntry."Appl.-to Item Entry" := 0;
      END;

      IF NewReservEntry."Source Type" = DATABASE::"Item Ledger Entry" THEN
        IF NewReservEntry."Quantity (Base)" > 0 THEN
          NewReservEntry."Expected Receipt Date" := 0D
        ELSE
          NewReservEntry."Shipment Date" := 31129999D;

      NewReservEntry.UpdateItemTracking;

      IF (TransferQty >= 0) <> OldReservEntry.Positive THEN BEGIN // If sign has swapped due to negative posting
        // Create a new but unchanged version of the original reserventry:
        SetQtyToHandleAndInvoice(QtyToHandleThisLine,QtyToInvoiceThisLine);
        CreateRemainingReservEntry(OldReservEntry,(OldReservEntry."Quantity (Base)") * CurrSignFactor);
        NewReservEntry.VALIDATE("Quantity (Base)",TransferQty);
        // Correct primary key - swap "Positive":
        NewReservEntry.Positive := NOT NewReservEntry.Positive;

        IF NOT ReservEntry.GET(NewReservEntry."Entry No.",NewReservEntry.Positive) THEN BEGIN
          // Means that only one record exists = surplus or prospect
          NewReservEntry.INSERT;
          // Delete the original record:
          NewReservEntry.Positive := NOT NewReservEntry.Positive;
          NewReservEntry.DELETE;
        END ELSE BEGIN // A set of records exist = reservation or tracking
          NewReservEntry.MODIFY;
          // Get the original record and modify quantity:
          NewReservEntry.GET(NewReservEntry."Entry No.",NOT NewReservEntry.Positive); // Get partner-record
          NewReservEntry.VALIDATE("Quantity (Base)",-TransferQty);
          NewReservEntry.MODIFY;
        END;
      END ELSE
        IF ABS(TransferQty) < ABS(OldReservEntry."Quantity (Base)") THEN BEGIN
          IF OldReservEntry.Binding = OldReservEntry.Binding::"Order-to-Order" THEN
            SetBinding(OldReservEntry.Binding::"Order-to-Order");
          IF ABS(QtyToInvoiceThisLine) > ABS(TransferQty) THEN
            QtyInvoiced := TransferQty
          ELSE
            QtyInvoiced := QtyToInvoiceThisLine;
          SetQtyToHandleAndInvoice(QtyToHandleThisLine - TransferQty,QtyToInvoiceThisLine - QtyInvoiced);
          CreateRemainingReservEntry(OldReservEntry,(OldReservEntry."Quantity (Base)"-TransferQty) * CurrSignFactor);
          NewReservEntry.VALIDATE("Quantity (Base)",TransferQty);
          NewReservEntry.MODIFY;
          IF NewReservEntry.GET(NewReservEntry."Entry No.",NOT NewReservEntry.Positive) THEN BEGIN // Get partner-record
            NewReservEntry.VALIDATE("Quantity (Base)",-TransferQty);
            NewReservEntry.MODIFY;
          END;
        END ELSE BEGIN
          NewReservEntry.MODIFY;
          TransferQty := NewReservEntry."Quantity (Base)";
          IF NewReservEntry."Source Type" = DATABASE::"Item Ledger Entry" THEN BEGIN
            IF NewReservEntry.GET(NewReservEntry."Entry No.",NOT NewReservEntry.Positive) THEN BEGIN // Get partner-record
              IF NewReservEntry."Quantity (Base)" < 0 THEN
                NewReservEntry."Expected Receipt Date" := 0D
              ELSE
                NewReservEntry."Shipment Date" := 31129999D;
              NewReservEntry.MODIFY;
            END;

            // If necessary create Whse. Item Tracking Lines
            IF (NewReservEntry."Source Type" = DATABASE::"Sales Line") AND
               (OldReservEntry."Source Type" = DATABASE::"Item Journal Line") AND
               (OldReservEntry."Reservation Status" = OldReservEntry."Reservation Status"::Reservation)
            THEN BEGIN
              ItemTrkgMgt.CheckWhseItemTrkgSetup(OldReservEntry."Item No.",SNRequired,LNRequired,FALSE);
              IF (SNRequired OR LNRequired) AND
                 Location.RequireShipment(OldReservEntry."Location Code")
              THEN
                CreateWhseItemTrkgLines(NewReservEntry);
            END;
          END ELSE
            IF (CarriedSerialNo + CarriedLotNo) <> '' THEN
              NewReservEntry.GET(NewReservEntry."Entry No.",NOT NewReservEntry.Positive); // Get partner-record
        END;

      IF (CarriedSerialNo + CarriedLotNo) <> '' THEN BEGIN
        IF NewReservEntry."Qty. to Handle (Base)" = 0 THEN
          NewReservEntry.VALIDATE("Quantity (Base)");
        NewReservEntry."Serial No." := CarriedSerialNo;
        NewReservEntry."Lot No." := CarriedLotNo;
        NewReservEntry.UpdateItemTracking;
        IF NewReservEntry.MODIFY THEN ;
      END;

      xTransferQty -= TransferQty;
      EXIT(xTransferQty * CurrSignFactor);
    END;

    PROCEDURE SignFactor@2(VAR ReservEntry@1000 : Record 337) : Integer;
    BEGIN
      // Demand is regarded as negative, supply is regarded as positive.
      CASE ReservEntry."Source Type" OF
        DATABASE::"Sales Line":
          IF ReservEntry."Source Subtype" IN [3,5] THEN // Credit memo, Return Order = supply
            EXIT(1)
          ELSE
            EXIT(-1);
        DATABASE::"Requisition Line":
          IF ReservEntry."Source Subtype" = 1 THEN
            EXIT(-1)
          ELSE
            EXIT(1);
        DATABASE::"Purchase Line":
          IF ReservEntry."Source Subtype" IN [3,5] THEN // Credit memo, Return Order = demand
            EXIT(-1)
          ELSE
            EXIT(1);
        DATABASE::"Item Journal Line":
          IF (ReservEntry."Source Subtype" = 4) AND (Inbound) THEN
            EXIT(1)
          ELSE
            IF ReservEntry."Source Subtype" IN [1,3,4,5] THEN // Sale, Negative Adjmt., Transfer, Consumption
              EXIT(-1)
            ELSE
              EXIT(1);
        DATABASE::"Job Journal Line":
          EXIT(-1);
        DATABASE::"BOM Journal Line":
          EXIT(1);
        DATABASE::"Item Ledger Entry":
          EXIT(1);
        DATABASE::"Prod. Order Line":
          EXIT(1);
        DATABASE::"Prod. Order Component":
          EXIT(-1);
        DATABASE::"Planning Component":
          EXIT(-1);
        DATABASE::"Transfer Line":
          IF ReservEntry."Source Subtype" = 0 THEN // Outbound
            EXIT(-1)
          ELSE
            EXIT(1);
        DATABASE::"Service Line":
          IF ReservEntry."Source Subtype" IN [3] THEN // Credit memo
            EXIT(1)
          ELSE
            EXIT(-1);
      END;
    END;

    PROCEDURE CheckValidity@3(VAR ReservEntry@1000 : Record 337);
    VAR
      IsError@1001 : Boolean;
    BEGIN
      IF ReservEntry."Reservation Status" <> ReservEntry."Reservation Status"::Reservation THEN
        EXIT;
      IF ReservEntry.Binding = ReservEntry.Binding::"Order-to-Order" THEN
        CASE ReservEntry."Source Type" OF
          DATABASE::"Sales Line":
            IsError := NOT (ReservEntry."Source Subtype" IN [1,5]);

          DATABASE::"Purchase Line":
            IsError := NOT (ReservEntry."Source Subtype" IN [1,5]);

          DATABASE::"Prod. Order Line":
            IsError := ReservEntry."Source Subtype" = 4;

          DATABASE::"Prod. Order Component":
            IsError := ReservEntry."Source Subtype" = 4;

          DATABASE::"Item Journal Line":
          // Item Journal Lines with Entry Type Transfer can carry reservations during posting:
            IsError := (ReservEntry."Source Subtype" <> 4) AND
              (ReservEntry."Source Ref. No." <> 0);

          DATABASE::"Job Journal Line",
          DATABASE::"BOM Journal Line":
            IsError := TRUE;
        END
      ELSE
        CASE ReservEntry."Source Type" OF
          DATABASE::"Sales Line":
            IsError := NOT (ReservEntry."Source Subtype" IN [1,5]);

          DATABASE::"Purchase Line":
            IsError := NOT (ReservEntry."Source Subtype" IN [1,5]);

          DATABASE::"Prod. Order Line":
            IsError := ReservEntry."Source Subtype" IN[1,4];

          DATABASE::"Prod. Order Component":
            IsError := ReservEntry."Source Subtype" IN[1,4];

          DATABASE::"Requisition Line",
          DATABASE::"Planning Component",
          DATABASE::"BOM Journal Line":
            IsError := TRUE;

          DATABASE::"Item Journal Line":
          // Item Journal Lines with Entry Type Transfer can carry reservations during posting:
            IsError := (ReservEntry."Source Subtype" <> 4) AND
              (ReservEntry."Source Ref. No." <> 0);
        END;

      IF IsError THEN
        ERROR(Text000);
    END;

    PROCEDURE GetLastEntry@7(VAR ReservEntry@1000 : Record 337);
    BEGIN
      ReservEntry := LastReservEntry;
    END;

    PROCEDURE HasSamePointer@10(VAR ReservEntry@1000 : Record 337;VAR Reserventry2@1001 : Record 337) : Boolean;
    BEGIN
      EXIT
        ((ReservEntry."Source Type" = Reserventry2."Source Type") AND
        (ReservEntry."Source Subtype" = Reserventry2."Source Subtype") AND
        (ReservEntry."Source ID" = Reserventry2."Source ID") AND
        (ReservEntry."Source Batch Name" = Reserventry2."Source Batch Name") AND
        (ReservEntry."Source Prod. Order Line" = Reserventry2."Source Prod. Order Line") AND
        (ReservEntry."Source Ref. No." = Reserventry2."Source Ref. No."));
    END;

    PROCEDURE AdjustDateIfItemLedgerEntry@9(VAR ReservEntry@1000 : Record 337);
    BEGIN
      IF ReservEntry."Source Type" = DATABASE::"Item Ledger Entry" THEN
        IF ReservEntry."Quantity (Base)" > 0 THEN
          ReservEntry."Expected Receipt Date" := 0D
        ELSE
          ReservEntry."Shipment Date" := 31129999D;
    END;

    LOCAL PROCEDURE SetupSplitReservEntry@14(VAR ReservEntry@1000 : Record 337;VAR ReservEntry2@1001 : Record 337);
    VAR
      NonReleasedQty@1002 : Decimal;
    BEGIN
      // Preparing the looping through Item Tracking.

      // Ensure that the full quantity is represented in the list of Tracking Specifications:
      NonReleasedQty := ReservEntry."Quantity (Base)";
      IF TempTrkgSpec1.FINDSET THEN REPEAT
        NonReleasedQty -= TempTrkgSpec1."Quantity (Base)";
      UNTIL TempTrkgSpec1.NEXT = 0;

      IF NonReleasedQty <> 0 THEN BEGIN
        TempTrkgSpec1.INIT;
        TempTrkgSpec1.TRANSFERFIELDS(ReservEntry);
        TempTrkgSpec1.VALIDATE("Quantity (Base)",NonReleasedQty);
        IF TempTrkgSpec1."Source Type" <> DATABASE::"Item Ledger Entry" THEN BEGIN
          TempTrkgSpec1."Serial No." := '';
          TempTrkgSpec1."Lot No." := '';
        END;
        TempTrkgSpec1.INSERT;
      END;

      IF NOT (ReservEntry."Reservation Status" < ReservEntry."Reservation Status"::Surplus) THEN
        EXIT;

      NonReleasedQty := ReservEntry2."Quantity (Base)";
      IF TempTrkgSpec2.FINDSET THEN REPEAT
        NonReleasedQty -= TempTrkgSpec2."Quantity (Base)";
      UNTIL TempTrkgSpec2.NEXT = 0;

      IF NonReleasedQty <> 0 THEN BEGIN
        TempTrkgSpec2.INIT;
        TempTrkgSpec2.TRANSFERFIELDS(ReservEntry2);
        TempTrkgSpec2.VALIDATE("Quantity (Base)",NonReleasedQty);
        IF TempTrkgSpec2."Source Type" <> DATABASE::"Item Ledger Entry" THEN BEGIN
          TempTrkgSpec2."Serial No." := '';
          TempTrkgSpec2."Lot No." := '';
        END;
        TempTrkgSpec2.INSERT;
      END;

      BalanceLists;
    END;

    LOCAL PROCEDURE BalanceLists@15();
    VAR
      TempTrkgSpec3@1001 : TEMPORARY Record 336;
      TempTrkgSpec4@1000 : TEMPORARY Record 336;
      LastEntryNo@1004 : Integer;
      NextState@1002 : 'SetFilter1,SetFilter2,LoosenFilter1,LoosenFilter2,Split,Error,Finish';
    BEGIN
      TempTrkgSpec1.RESET;
      TempTrkgSpec2.RESET;
      TempTrkgSpec1.SETCURRENTKEY("Lot No.","Serial No.");
      TempTrkgSpec2.SETCURRENTKEY("Lot No.","Serial No.");

      IF NOT TempTrkgSpec1.FINDLAST THEN
        EXIT;

      REPEAT
        CASE NextState OF
          NextState::SetFilter1:
            BEGIN
              TempTrkgSpec1.SETRANGE("Serial No.",TempTrkgSpec2."Serial No.");
              TempTrkgSpec1.SETRANGE("Lot No.",TempTrkgSpec2."Lot No.");
              IF TempTrkgSpec1.FINDLAST THEN
                NextState := NextState::Split
              ELSE
                NextState := NextState::LoosenFilter1;
            END;
          NextState::LoosenFilter1:
            BEGIN
              IF TempTrkgSpec2."Quantity (Base)" > 0 THEN BEGIN
                TempTrkgSpec1.SETRANGE("Serial No.",'');
                TempTrkgSpec1.SETRANGE("Lot No.",'');
              END ELSE BEGIN
                IF TempTrkgSpec2."Serial No." = '' THEN
                  TempTrkgSpec1.SETRANGE("Serial No.");
                IF TempTrkgSpec2."Lot No." = '' THEN
                  TempTrkgSpec1.SETRANGE("Lot No.");
              END;
              IF TempTrkgSpec1.FINDLAST THEN
                NextState := NextState::Split
              ELSE
                NextState := NextState::Error;
            END;
          NextState::SetFilter2:
            BEGIN
              TempTrkgSpec2.SETRANGE("Serial No.",TempTrkgSpec1."Serial No.");
              TempTrkgSpec2.SETRANGE("Lot No.",TempTrkgSpec1."Lot No.");
              IF TempTrkgSpec2.FINDLAST THEN
                NextState := NextState::Split
              ELSE
                NextState := NextState::LoosenFilter2;
            END;
          NextState::LoosenFilter2:
            BEGIN
              IF TempTrkgSpec1."Quantity (Base)" > 0 THEN BEGIN
                TempTrkgSpec2.SETRANGE("Serial No.",'');
                TempTrkgSpec2.SETRANGE("Lot No.",'');
              END ELSE BEGIN
                IF TempTrkgSpec1."Serial No." = '' THEN
                  TempTrkgSpec2.SETRANGE("Serial No.");
                IF TempTrkgSpec1."Lot No." = '' THEN
                  TempTrkgSpec2.SETRANGE("Lot No.");
              END;
              IF TempTrkgSpec2.FINDLAST THEN
                NextState := NextState::Split
              ELSE
                NextState := NextState::Error;
            END;
          NextState::Split:
            BEGIN
              TempTrkgSpec3 := TempTrkgSpec1;
              TempTrkgSpec4 := TempTrkgSpec2;
              IF ABS(TempTrkgSpec1."Quantity (Base)") = ABS(TempTrkgSpec2."Quantity (Base)") THEN BEGIN
                TempTrkgSpec1.DELETE;
                TempTrkgSpec2.DELETE;
                TempTrkgSpec1.SETRANGE("Serial No.");
                TempTrkgSpec1.SETRANGE("Lot No.");
                IF TempTrkgSpec1.FINDLAST THEN
                  NextState := NextState::SetFilter2
                ELSE BEGIN
                  TempTrkgSpec2.RESET;
                  IF TempTrkgSpec2.FINDLAST THEN
                    NextState := NextState::Error
                  ELSE
                    NextState := NextState::Finish;
                END;
              END ELSE
                IF ABS(TempTrkgSpec1."Quantity (Base)") < ABS(TempTrkgSpec2."Quantity (Base)") THEN BEGIN
                  TempTrkgSpec2.VALIDATE("Quantity (Base)",TempTrkgSpec2."Quantity (Base)" +
                    TempTrkgSpec1."Quantity (Base)");
                  TempTrkgSpec4.VALIDATE("Quantity (Base)",-TempTrkgSpec1."Quantity (Base)");
                  TempTrkgSpec1.DELETE;
                  TempTrkgSpec2.MODIFY;
                  NextState := NextState::SetFilter1;
                END ELSE BEGIN
                  TempTrkgSpec1.VALIDATE("Quantity (Base)",TempTrkgSpec1."Quantity (Base)" +
                    TempTrkgSpec2."Quantity (Base)");
                  TempTrkgSpec3.VALIDATE("Quantity (Base)",-TempTrkgSpec2."Quantity (Base)");
                  TempTrkgSpec2.DELETE;
                  TempTrkgSpec1.MODIFY;
                  NextState := NextState::SetFilter2;
                END;
              TempTrkgSpec3."Entry No." := LastEntryNo + 1;
              TempTrkgSpec4."Entry No." := LastEntryNo + 1;
              TempTrkgSpec3.INSERT;
              TempTrkgSpec4.INSERT;
              LastEntryNo := TempTrkgSpec3."Entry No.";
            END;
          NextState::Error: ERROR(Text001);
        END;
      UNTIL NextState = NextState::Finish;

      TempTrkgSpec1.RESET;
      TempTrkgSpec2.RESET;
      TempTrkgSpec3.RESET;
      TempTrkgSpec4.RESET;

      IF TempTrkgSpec3.FINDSET THEN REPEAT
        TempTrkgSpec1 := TempTrkgSpec3;
        TempTrkgSpec1.INSERT;
      UNTIL TempTrkgSpec3.NEXT = 0;

      IF TempTrkgSpec4.FINDSET THEN REPEAT
        TempTrkgSpec2 := TempTrkgSpec4;
        TempTrkgSpec2.INSERT;
      UNTIL TempTrkgSpec4.NEXT = 0;
    END;

    LOCAL PROCEDURE SplitReservEntry@17(VAR ReservEntry@1001 : Record 337;VAR ReservEntry2@1000 : Record 337;VAR TrackingSpecificationExists@1003 : Boolean;VAR FirstSplit@1002 : Boolean) : Boolean;
    BEGIN
      IF NOT TrackingSpecificationExists THEN
        IF NOT FirstSplit THEN
          EXIT(FALSE)
        ELSE BEGIN
          FirstSplit := FALSE;
          EXIT(TRUE);
        END;

      TempTrkgSpec1.RESET;
      IF NOT TempTrkgSpec1.FINDFIRST THEN
        EXIT(FALSE);

      ReservEntry."Serial No." := TempTrkgSpec1."Serial No.";
      ReservEntry."Lot No." := TempTrkgSpec1."Lot No.";
      ReservEntry."Warranty Date" := TempTrkgSpec1."Warranty Date";
      ReservEntry."Expiration Date" := TempTrkgSpec1."Expiration Date";
      ReservEntry.VALIDATE("Quantity (Base)",TempTrkgSpec1."Quantity (Base)");
      TempTrkgSpec1.DELETE;

      IF ReservEntry."Reservation Status" < ReservEntry."Reservation Status"::Surplus THEN BEGIN
        TempTrkgSpec2.GET(TempTrkgSpec1."Entry No.");
        ReservEntry2."Serial No." := TempTrkgSpec2."Serial No.";
        ReservEntry2."Lot No." := TempTrkgSpec2."Lot No.";
        ReservEntry2."Warranty Date" := TempTrkgSpec2."Warranty Date";
        ReservEntry2."Expiration Date" := TempTrkgSpec2."Expiration Date";
        ReservEntry2.VALIDATE("Quantity (Base)",TempTrkgSpec2."Quantity (Base)");
        TempTrkgSpec2.DELETE;
      END;

      EXIT(TRUE);
    END;

    PROCEDURE CreateWhseItemTrkgLines@21(ReservEntry@1000 : Record 337);
    VAR
      WhseShipmentLine@1001 : Record 7321;
      WhseWkshLine@1002 : Record 7326;
      ItemTrkgMgt@1003 : Codeunit 6500;
    BEGIN
      WITH WhseShipmentLine DO BEGIN
        SETCURRENTKEY("Source Type","Source Subtype","Source No.","Source Line No.");
        SETRANGE("Source Type",ReservEntry."Source Type");
        SETRANGE("Source Subtype",ReservEntry."Source Subtype");
        SETRANGE("Source No.",ReservEntry."Source ID");
        SETRANGE("Source Line No.",ReservEntry."Source Ref. No.");
        IF FINDFIRST THEN BEGIN
          ItemTrkgMgt.InitWhseWkshLine(WhseWkshLine,
            2,"No.","Line No.","Source Type","Source Subtype","Source No.","Source Line No.",0);
          ItemTrkgMgt.CreateWhseItemTrkgForResEntry(ReservEntry,WhseWkshLine);
        END;
      END;
    END;

    PROCEDURE SetApplyToEntryNo@16(EntryNo@1000 : Integer);
    BEGIN
      InsertReservEntry."Appl.-to Item Entry" := EntryNo;
    END;

    PROCEDURE SetApplyFromEntryNo@78(EntryNo@1000 : Integer);
    BEGIN
      InsertReservEntry."Appl.-from Item Entry" := EntryNo;
    END;

    PROCEDURE SetOverruleItemTracking@18(Overrule@1000 : Boolean);
    BEGIN
      OverruleItemTracking := Overrule;
    END;

    PROCEDURE SetInbound@20(NewInbound@1000 : Boolean);
    BEGIN
      Inbound := NewInbound;
    END;

    PROCEDURE SetUseQtyToInvoice@22(UseQtyToInvoice2@1000 : Boolean);
    BEGIN
      UseQtyToInvoice := UseQtyToInvoice2;
    END;

    PROCEDURE UpdateItemTrackingAfterPosting@23(VAR ReservEntry@1000 : Record 337);
    VAR
      Location@1001 : Record 14;
      CreatePick@1002 : Codeunit 7312;
      TotalResvPickShipQty@1003 : Decimal;
      TotalPickQty@1008 : Decimal;
      TotalShippedQty@1009 : Decimal;
      CurrTotalResvPickShipQty@1004 : Decimal;
      CurrSourceRefNo@1005 : Integer;
      CurrLocation@1007 : Code[10];
      ReachedEndOfResvEntries@1006 : Boolean;
    BEGIN
      IF NOT ReservEntry.FINDSET(TRUE) THEN EXIT;

      REPEAT
        CurrSourceRefNo := ReservEntry."Source Ref. No.";
        CurrLocation := ReservEntry."Location Code";
        CurrTotalResvPickShipQty := 0;

        REPEAT
          ReservEntry."Qty. to Handle (Base)" := ReservEntry."Quantity (Base)";
          ReservEntry."Qty. to Invoice (Base)" := ReservEntry."Quantity (Base)";
          CurrTotalResvPickShipQty += -ReservEntry."Reserved Pick & Ship Qty.";
          ReservEntry.MODIFY;
          IF ReservEntry.NEXT = 0 THEN
            ReachedEndOfResvEntries := TRUE;
        UNTIL ReachedEndOfResvEntries OR (ReservEntry."Source Ref. No." <> CurrSourceRefNo);

        IF Location.GET(CurrLocation) AND (Location."Bin Mandatory" OR Location."Require Pick") THEN BEGIN
          TotalPickQty :=
            CreatePick.CheckOutBound(
              ReservEntry."Source Type",
              ReservEntry."Source Subtype",
              ReservEntry."Source ID",
              CurrSourceRefNo,
              ReservEntry."Source Prod. Order Line");
          TotalShippedQty := GetTotalShippedQtyBaseByWhse(ReservEntry,CurrSourceRefNo);
          TotalResvPickShipQty := TotalPickQty - TotalShippedQty;

          IF CurrTotalResvPickShipQty > TotalResvPickShipQty THEN // delete excess qty
            CallAdjustReservation(ReservEntry,
              CurrSourceRefNo,
              CurrTotalResvPickShipQty - TotalResvPickShipQty,
              1)
          ELSE
            IF CurrTotalResvPickShipQty < TotalResvPickShipQty THEN // modify qty to new total
              CallAdjustReservation(ReservEntry,
                CurrSourceRefNo,
                TotalResvPickShipQty,
                0)
        END;

        // iterate over each set of Source Ref No.
      UNTIL ReservEntry."Source Ref. No." = CurrSourceRefNo;
    END;

    LOCAL PROCEDURE GetTotalShippedQtyBaseByWhse@24(ReservEntry@1000 : Record 337;SourceRefNo@1002 : Integer) ShippedQty : Decimal;
    VAR
      PstdWhseShipLine@1001 : Record 7323;
    BEGIN
      PstdWhseShipLine.SETCURRENTKEY(
        "Source Type","Source Subtype","Source No.","Source Line No.");
      PstdWhseShipLine.SETRANGE("Source Type",ReservEntry."Source Type");
      PstdWhseShipLine.SETRANGE("Source Subtype",ReservEntry."Source Subtype");
      PstdWhseShipLine.SETRANGE("Source No.",ReservEntry."Source ID");
      PstdWhseShipLine.SETRANGE("Source Line No.",SourceRefNo);
      IF PstdWhseShipLine.FINDSET THEN
        REPEAT
          ShippedQty += PstdWhseShipLine."Qty. (Base)";
        UNTIL PstdWhseShipLine.NEXT = 0;
    END;

    PROCEDURE CallAdjustReservation@25(ReservEntry@1000 : Record 337;SourceRefNo@1004 : Integer;Quantity@1001 : Decimal;Action@1002 : 'Modify,Delete');
    VAR
      CreatePick@1003 : Codeunit 7312;
    BEGIN
      IF ReservEntry."Source Type" = DATABASE::"Prod. Order Component" THEN
        CreatePick.AdjustReservation(
          Quantity,
          ReservEntry."Source Type",
          ReservEntry."Source Subtype",
          ReservEntry."Source ID",
          ReservEntry."Source Prod. Order Line",
          SourceRefNo,
          Action)
      ELSE
        CreatePick.AdjustReservation(
          Quantity,
          ReservEntry."Source Type",
          ReservEntry."Source Subtype",
          ReservEntry."Source ID",
          SourceRefNo,
          0,
          Action);
    END;

    BEGIN
    END.
  }
}
