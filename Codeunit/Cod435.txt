OBJECT Codeunit 435 IC Inbox Import
{
  OBJECT-PROPERTIES
  {
    Date=15/09/04;
    Time=12:00:00;
    Version List=NAVW14.00;
  }
  PROPERTIES
  {
    TableNo=418;
    OnRun=VAR
            CompanyInfo@1004 : Record 79;
            ICPartner@1027 : Record 413;
            TempICOutboxTrans@1014 : TEMPORARY Record 414;
            TempICOutBoxJnlLine@1013 : TEMPORARY Record 415;
            TempICIOBoxJnlDim@1012 : TEMPORARY Record 423;
            TempICOutBoxSalesHdr@1011 : TEMPORARY Record 426;
            TempICOutBoxSalesLine@1010 : TEMPORARY Record 427;
            TempICOutBoxPurchHdr@1009 : TEMPORARY Record 428;
            TempICOutBoxPurchLine@1008 : TEMPORARY Record 429;
            TempICDocDim@1007 : TEMPORARY Record 442;
            ICInboxJnlLine@1017 : Record 419;
            ICInboxSalesHdr@1021 : Record 434;
            ICInboxSalesLine@1020 : Record 435;
            ICInboxPurchHdr@1018 : Record 436;
            ICInboxPurchLine@1019 : Record 437;
            ICInboxJnlLineDim@1022 : Record 423;
            ICInboxDocDim@1023 : Record 442;
            ICInboxOutboxMgt@1015 : Codeunit 427;
            CmDlg@1005 : Codeunit 412;
            ICOutboxImpExpXML@1003 : XMLport 12;
            IFile@1002 : File;
            FileName@1001 : Text[250];
            StartFileName@1006 : Text[250];
            IStr@1000 : InStream;
            FromICPartnerCode@1016 : Code[20];
            ToICPartnerCode@1026 : Code[20];
            NewTableID@1024 : Integer;
            i@1025 : Integer;
          BEGIN
            CompanyInfo.GET;
            CompanyInfo.TESTFIELD("IC Partner Code");
            IF CompanyInfo."IC Inbox Type" = CompanyInfo."IC Inbox Type"::"File Location" THEN
              StartFileName := CompanyInfo."IC Inbox Details";
            IF StartFileName <> '' THEN BEGIN
              IF StartFileName[STRLEN(StartFileName)] <> '\' THEN
                StartFileName := StartFileName + '\';
              StartFileName := StartFileName + '*.xml';
            END;
            FileName := CmDlg.OpenFile(STRSUBSTNO(Text001,TABLECAPTION),StartFileName,4,'*.xml|*.xml',0);
            IF (FileName = '') OR (FileName = StartFileName) THEN
              EXIT;
            IFile.OPEN(FileName);
            IFile.CREATEINSTREAM(IStr);
            ICOutboxImpExpXML.SETSOURCE(IStr);
            ICOutboxImpExpXML.IMPORT;
            IFile.CLOSE;
            FromICPartnerCode := ICOutboxImpExpXML.GetFromICPartnerCode;
            ToICPartnerCode := ICOutboxImpExpXML.GetToICPartnerCode;
            IF ToICPartnerCode <> CompanyInfo."IC Partner Code" THEN
              ERROR(
                Text003,ICPartner.TABLECAPTION,ToICPartnerCode,
                CompanyInfo.FIELDCAPTION("IC Partner Code"),CompanyInfo."IC Partner Code");

            ICOutboxImpExpXML.GetICOutboxTrans(TempICOutboxTrans);
            ICOutboxImpExpXML.GetICOutBoxJnlLine(TempICOutBoxJnlLine);
            ICOutboxImpExpXML.GetICIOBoxJnlDim(TempICIOBoxJnlDim);
            ICOutboxImpExpXML.GetICOutBoxSalesHdr(TempICOutBoxSalesHdr);
            ICOutboxImpExpXML.GetICOutBoxSalesLine(TempICOutBoxSalesLine);
            ICOutboxImpExpXML.GetICOutBoxPurchHdr(TempICOutBoxPurchHdr);
            ICOutboxImpExpXML.GetICOutBoxPurchLine(TempICOutBoxPurchLine);
            ICOutboxImpExpXML.GetICSalesDocDim(TempICDocDim);
            ICOutboxImpExpXML.GetICSalesDocLineDim(TempICDocDim);
            ICOutboxImpExpXML.GetICPurchDocDim(TempICDocDim);
            ICOutboxImpExpXML.GetICPurchDocLineDim(TempICDocDim);
            FromICPartnerCode := ICOutboxImpExpXML.GetFromICPartnerCode;

            IF TempICOutboxTrans.FIND('-') THEN
              REPEAT
                ICInboxOutboxMgt.OutboxTransToInbox(TempICOutboxTrans,Rec,FromICPartnerCode);

                TempICOutBoxJnlLine.SETRANGE("Transaction No.",TempICOutboxTrans."Transaction No.");
                TempICOutBoxJnlLine.SETRANGE("IC Partner Code",TempICOutboxTrans."IC Partner Code");
                TempICOutBoxJnlLine.SETRANGE("Transaction Source",TempICOutboxTrans."Transaction Source");
                IF TempICOutBoxJnlLine.FIND('-') THEN
                  REPEAT
                    ICInboxOutboxMgt.OutboxJnlLineToInbox(Rec,TempICOutBoxJnlLine,ICInboxJnlLine);
                    TempICIOBoxJnlDim.SETRANGE("Transaction No.",TempICOutboxTrans."Transaction No.");
                    TempICIOBoxJnlDim.SETRANGE("IC Partner Code",TempICOutboxTrans."IC Partner Code");
                    TempICIOBoxJnlDim.SETRANGE("Transaction Source",TempICOutboxTrans."Transaction Source");
                    TempICIOBoxJnlDim.SETRANGE("Line No.",ICInboxJnlLine."Line No.");
                    IF TempICIOBoxJnlDim.FIND('-') THEN
                      REPEAT
                        ICInboxOutboxMgt.OutboxJnlLineDimToInbox(
                          ICInboxJnlLine,TempICIOBoxJnlDim,ICInboxJnlLineDim,DATABASE::"IC Inbox Jnl. Line");
                      UNTIL TempICIOBoxJnlDim.NEXT = 0;
                  UNTIL TempICOutBoxJnlLine.NEXT = 0;

                TempICOutBoxSalesHdr.SETRANGE("IC Transaction No.",TempICOutboxTrans."Transaction No.");
                TempICOutBoxSalesHdr.SETRANGE("IC Partner Code",TempICOutboxTrans."IC Partner Code");
                TempICOutBoxSalesHdr.SETRANGE("Transaction Source",TempICOutboxTrans."Transaction Source");
                IF TempICOutBoxSalesHdr.FIND('-') THEN
                  REPEAT
                    ICInboxOutboxMgt.OutboxSalesHdrToInbox(Rec,TempICOutBoxSalesHdr,ICInboxPurchHdr);
                  UNTIL TempICOutBoxSalesHdr.NEXT = 0;

                TempICOutBoxSalesLine.SETRANGE("IC Transaction No.",TempICOutboxTrans."Transaction No.");
                TempICOutBoxSalesLine.SETRANGE("IC Partner Code",TempICOutboxTrans."IC Partner Code");
                TempICOutBoxSalesLine.SETRANGE("Transaction Source",TempICOutboxTrans."Transaction Source");
                IF TempICOutBoxSalesLine.FIND('-') THEN
                  REPEAT
                    ICInboxOutboxMgt.OutboxSalesLineToInbox(Rec,TempICOutBoxSalesLine,ICInboxPurchLine);
                  UNTIL TempICOutBoxSalesLine.NEXT = 0;

                TempICOutBoxPurchHdr.SETRANGE("IC Transaction No.",TempICOutboxTrans."Transaction No.");
                TempICOutBoxPurchHdr.SETRANGE("IC Partner Code",TempICOutboxTrans."IC Partner Code");
                TempICOutBoxPurchHdr.SETRANGE("Transaction Source",TempICOutboxTrans."Transaction Source");
                IF TempICOutBoxPurchHdr.FIND('-') THEN
                  REPEAT
                    ICInboxOutboxMgt.OutboxPurchHdrToInbox(Rec,TempICOutBoxPurchHdr,ICInboxSalesHdr);
                  UNTIL TempICOutBoxPurchHdr.NEXT = 0;

                TempICOutBoxPurchLine.SETRANGE("IC Transaction No.",TempICOutboxTrans."Transaction No.");
                TempICOutBoxPurchLine.SETRANGE("IC Partner Code",TempICOutboxTrans."IC Partner Code");
                TempICOutBoxPurchLine.SETRANGE("Transaction Source",TempICOutboxTrans."Transaction Source");
                IF TempICOutBoxPurchLine.FIND('-') THEN
                  REPEAT
                    ICInboxOutboxMgt.OutboxPurchLineToInbox(Rec,TempICOutBoxPurchLine,ICInboxSalesLine);
                  UNTIL TempICOutBoxPurchLine.NEXT = 0;

                TempICDocDim.SETRANGE("Transaction No.",TempICOutboxTrans."Transaction No.");
                TempICDocDim.SETRANGE("IC Partner Code",TempICOutboxTrans."IC Partner Code");
                TempICDocDim.SETRANGE("Transaction Source",TempICOutboxTrans."Transaction Source");
                IF TempICDocDim.FIND('-') THEN
                  REPEAT
                    CASE TempICDocDim."Table ID" OF
                      DATABASE::"IC Outbox Sales Header": NewTableID := DATABASE::"IC Inbox Purchase Header";
                      DATABASE::"IC Outbox Sales Line": NewTableID := DATABASE::"IC Inbox Purchase Line";
                      DATABASE::"IC Outbox Purchase Header": NewTableID := DATABASE::"IC Inbox Sales Header";
                      DATABASE::"IC Outbox Purchase Line": NewTableID := DATABASE::"IC Inbox Sales Line";
                    END;
                    ICInboxOutboxMgt.OutboxDocDimToInbox(
                      TempICDocDim,ICInboxDocDim,NewTableID,FromICPartnerCode,"Transaction Source");
                  UNTIL TempICDocDim.NEXT = 0;
              UNTIL TempICOutboxTrans.NEXT = 0;

            i := STRLEN(FileName);
            WHILE (i > 1) AND (FileName[i] <> '\') DO
              i := i - 1;
            IF i > 1 THEN BEGIN
              StartFileName := STRSUBSTNO('%1%2%3',COPYSTR(FileName,1,i),Text002,COPYSTR(FileName,i));
              IF FILE.COPY(FileName,StartFileName) THEN
                FILE.ERASE(FileName)
              ELSE
                IF CONFIRM(Text004,FALSE,COPYSTR(FileName,i + 1),STRSUBSTNO('%1%2',COPYSTR(FileName,1,i),Text002)) THEN
                  FILE.ERASE(FileName);
            END;
          END;

  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=Select file to import into %1;ESP=Selecc. archivo para imp. en %1';
      Text002@1001 : TextConst 'ENU=Archive;ESP=Archivo';
      Text003@1002 : TextConst 'ENU=The selected xml file contains data sent to %1 %2. Current company''s %3 is %4.;ESP=El archivo xml seleccionado contiene datos enviados a %1 %2. El %3 de la compa¤¡a actual es %4.';
      Text004@1003 : TextConst 'ENU=The file %1 cannot be moved to %2 because the folder does not exist.\Do you want to delete the file instead?;ESP=No es posible mover el archivo %1 a %2, la carpeta no existe.\¨Desea eliminar el archivo?';

    BEGIN
    END.
  }
}
