OBJECT Codeunit 64 Sales-Get Shipment
{
  OBJECT-PROPERTIES
  {
    Date=11/12/10;
    Time=12:00:00;
    Version List=NAVW16.00.10;
  }
  PROPERTIES
  {
    TableNo=37;
    OnRun=BEGIN
            SalesHeader.GET("Document Type","Document No.");
            SalesHeader.TESTFIELD("Document Type",SalesHeader."Document Type"::Invoice);
            SalesHeader.TESTFIELD(Status,SalesHeader.Status::Open);

            SalesShptLine.SETCURRENTKEY("Bill-to Customer No.");
            SalesShptLine.SETRANGE("Bill-to Customer No.",SalesHeader."Bill-to Customer No.");
            SalesShptLine.SETFILTER("Qty. Shipped Not Invoiced",'<>0');
            SalesShptLine.SETRANGE("Currency Code",SalesHeader."Currency Code");
            SalesShptLine.SETRANGE("Authorized for Credit Card",FALSE);
            GetShipments.SETTABLEVIEW(SalesShptLine);
            GetShipments.SetSalesHeader(SalesHeader);
            GetShipments.LOOKUPMODE := TRUE;
            IF GetShipments.RUNMODAL <> ACTION::Cancel THEN;
          END;

  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=The %1 on the %2 %3 and the %4 %5 must be the same.;ESP=El %1 en el %2 %3 y el %4 %5 debe ser el mismo.';
      SalesHeader@1001 : Record 36;
      SalesLine@1002 : Record 37;
      SalesShptHeader@1003 : Record 110;
      SalesShptLine@1004 : Record 111;
      GetShipments@1005 : Form 5708;
      Text002@1007 : TextConst 'ENU=Creating Sales Invoice Lines\;ESP=Creando l¡neas de fra. venta\';
      Text003@1006 : TextConst 'ENU=Inserted lines             #1######;ESP=L¡neas insertadas          #1######';

    PROCEDURE CreateInvLines@1(VAR SalesShptLine2@1000 : Record 111);
    VAR
      TempFromLineDim@1002 : TEMPORARY Record 357;
      TempToLineDim@1003 : TEMPORARY Record 357;
      DimMgt@1004 : Codeunit 408;
      Window@1006 : Dialog;
      LineCount@1005 : Integer;
      TransferLine@1001 : Boolean;
    BEGIN
      WITH SalesShptLine2 DO BEGIN
        SETFILTER("Qty. Shipped Not Invoiced",'<>0');
        IF FINDSET THEN BEGIN
          SalesLine.LOCKTABLE;
          SalesLine.SETRANGE("Document Type",SalesHeader."Document Type");
          SalesLine.SETRANGE("Document No.",SalesHeader."No.");
          SalesLine."Document Type" := SalesHeader."Document Type";
          SalesLine."Document No." := SalesHeader."No.";
          Window.OPEN(Text002 + Text003);

          REPEAT
            LineCount := LineCount + 1;
            Window.UPDATE(1,LineCount);
            IF SalesShptHeader."No." <> "Document No." THEN BEGIN
              SalesShptHeader.GET("Document No.");
              TransferLine := TRUE;
              IF SalesShptHeader."Currency Code" <> SalesHeader."Currency Code" THEN BEGIN
                MESSAGE(
                  Text001,
                  SalesHeader.FIELDCAPTION("Currency Code"),
                  SalesHeader.TABLECAPTION, SalesHeader."No.",
                  SalesShptHeader.TABLECAPTION,SalesShptHeader."No.");
                  TransferLine := FALSE;
              END;
              IF SalesShptHeader."Bill-to Customer No." <> SalesHeader."Bill-to Customer No." THEN BEGIN
                MESSAGE(
                  Text001,
                  SalesHeader.FIELDCAPTION("Bill-to Customer No."),
                  SalesHeader.TABLECAPTION, SalesHeader."No.",
                  SalesShptHeader.TABLECAPTION,SalesShptHeader."No.");
                  TransferLine := FALSE;
              END;
            END;
            IF TransferLine THEN BEGIN
              SalesShptLine := SalesShptLine2;
              SalesShptLine.InsertInvLineFromShptLine(SalesLine,TempFromLineDim);
              DimMgt.MoveTempFromDimToTempToDim(TempFromLineDim,TempToLineDim);
            END;
          UNTIL NEXT = 0;
          GetItemChargeAssgnt(SalesShptLine2);
          DimMgt.TransferTempToDimToDocDim(TempToLineDim);
        END;
      END;
    END;

    PROCEDURE SetSalesHeader@2(VAR SalesHeader2@1000 : Record 36);
    BEGIN
      SalesHeader.GET(SalesHeader2."Document Type",SalesHeader2."No.");
      SalesHeader.TESTFIELD("Document Type",SalesHeader."Document Type"::Invoice);
    END;

    PROCEDURE GetItemChargeAssgnt@5800(VAR SalesShptLine@1000 : Record 111);
    VAR
      SalesOrderLine@1001 : Record 37;
      ItemChargeAssgntSales@1002 : Record 5809;
    BEGIN
      WITH SalesShptLine DO BEGIN
        SETRANGE(Type,Type::"Charge (Item)");
        IF FINDSET THEN
          REPEAT
            IF SalesOrderLine.GET(SalesOrderLine."Document Type"::Order,"Order No.","Order Line No.")
            THEN BEGIN
              ItemChargeAssgntSales.LOCKTABLE;
              ItemChargeAssgntSales.RESET;
              ItemChargeAssgntSales.SETRANGE("Document Type",SalesOrderLine."Document Type");
              ItemChargeAssgntSales.SETRANGE("Document No.",SalesOrderLine."Document No.");
              ItemChargeAssgntSales.SETRANGE("Document Line No.",SalesOrderLine."Line No.");
              IF ItemChargeAssgntSales.FINDSET THEN BEGIN
                ItemChargeAssgntSales.CALCSUMS("Qty. to Assign");
                IF ItemChargeAssgntSales."Qty. to Assign" <> 0 THEN
                  CopyItemChargeAssgnt(SalesOrderLine,SalesShptLine,ItemChargeAssgntSales."Qty. to Assign");
              END;
            END;
          UNTIL NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE CopyItemChargeAssgnt@5801(SalesOrderLine@1000 : Record 37;SalesShptLine@1001 : Record 111;QtyToAssign@1002 : Decimal);
    VAR
      SalesShptLine2@1003 : Record 111;
      SalesLine2@1004 : Record 37;
      ItemChargeAssgntSales@1005 : Record 5809;
      ItemChargeAssgntSales2@1006 : Record 5809;
      InsertChargeAssgnt@1007 : Boolean;
    BEGIN
      WITH SalesOrderLine DO BEGIN
        ItemChargeAssgntSales.SETRANGE("Document Type","Document Type");
        ItemChargeAssgntSales.SETRANGE("Document No.","Document No.");
        ItemChargeAssgntSales.SETRANGE("Document Line No.","Line No.");
        IF ItemChargeAssgntSales.FINDSET THEN
          REPEAT
            IF ItemChargeAssgntSales."Qty. to Assign" <> 0 THEN BEGIN
              ItemChargeAssgntSales2 := ItemChargeAssgntSales;
              SalesLine2.SETRANGE("Shipment No.",SalesShptLine."Document No.");
              SalesLine2.SETRANGE("Shipment Line No.",SalesShptLine."Line No.");
              IF SalesLine2.FINDSET THEN
                REPEAT
                  SalesLine2.CALCFIELDS("Qty. to Assign");
                  InsertChargeAssgnt := SalesLine2."Qty. to Assign" <> SalesLine2.Quantity;
                UNTIL (SalesLine2.NEXT = 0) OR InsertChargeAssgnt;

              IF InsertChargeAssgnt THEN BEGIN
                ItemChargeAssgntSales2."Document Type" := SalesLine2."Document Type";
                ItemChargeAssgntSales2."Document No." := SalesLine2."Document No.";
                ItemChargeAssgntSales2."Document Line No." := SalesLine2."Line No.";
                ItemChargeAssgntSales2."Qty. Assigned" := 0;
                IF ABS(QtyToAssign) < ABS(ItemChargeAssgntSales2."Qty. to Assign") THEN
                  ItemChargeAssgntSales2."Qty. to Assign" := QtyToAssign;
                IF ABS(SalesLine2.Quantity - SalesLine2."Qty. to Assign") <
                   ABS(ItemChargeAssgntSales2."Qty. to Assign")
                THEN
                  ItemChargeAssgntSales2."Qty. to Assign" :=
                    SalesLine2.Quantity - SalesLine2."Qty. to Assign";
                ItemChargeAssgntSales2.VALIDATE("Unit Cost");

                IF ItemChargeAssgntSales2."Applies-to Doc. Type" = "Document Type" THEN BEGIN
                  ItemChargeAssgntSales2."Applies-to Doc. Type" := SalesLine2."Document Type";
                  ItemChargeAssgntSales2."Applies-to Doc. No." := SalesLine2."Document No.";
                  SalesShptLine2.SETCURRENTKEY("Order No.","Order Line No.");
                  SalesShptLine2.SETRANGE("Order No.",ItemChargeAssgntSales."Applies-to Doc. No.");
                  SalesShptLine2.SETRANGE("Order Line No.",ItemChargeAssgntSales."Applies-to Doc. Line No.");
                  SalesShptLine2.SETRANGE("Document No.",SalesShptLine."Document No.");
                  IF SalesShptLine2.FINDFIRST THEN BEGIN
                    SalesLine2.SETCURRENTKEY("Document Type","Shipment No.","Shipment Line No.");
                    SalesLine2.SETRANGE("Document Type",SalesOrderLine."Document Type"::Invoice);
                    SalesLine2.SETRANGE("Shipment No.",SalesShptLine2."Document No.");
                    SalesLine2.SETRANGE("Shipment Line No.",SalesShptLine2."Line No.");
                    IF SalesLine2.FINDFIRST AND (SalesLine2.Quantity <> 0) THEN
                      ItemChargeAssgntSales2."Applies-to Doc. Line No." := SalesLine2."Line No."
                    ELSE
                     InsertChargeAssgnt := FALSE;
                  END ELSE
                    InsertChargeAssgnt := FALSE;
                END;
              END;

              IF InsertChargeAssgnt AND (ItemChargeAssgntSales2."Qty. to Assign" <> 0) THEN BEGIN
                ItemChargeAssgntSales2.INSERT;
                QtyToAssign := QtyToAssign - ItemChargeAssgntSales2."Qty. to Assign";
              END;
            END;
          UNTIL ItemChargeAssgntSales.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}
