OBJECT Codeunit 350 IntraJnlManagement
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    Permissions=TableData 261=imd,
                TableData 262=imd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=INTRASTAT;ESP=INTRASTAT';
      Text001@1001 : TextConst 'ENU=Intrastat Journal;ESP=Diario Intrastat';
      Text002@1002 : TextConst 'ENU=DEFAULT;ESP=GENERICO';
      Text003@1003 : TextConst 'ENU=Default Journal;ESP=Diario gen‚rico';
      OpenFromBatch@1004 : Boolean;

    PROCEDURE TemplateSelection@1(FormID@1003 : Integer;VAR IntrastatJnlLine@1002 : Record 263;VAR JnlSelected@1001 : Boolean);
    VAR
      IntraJnlTemplate@1000 : Record 261;
    BEGIN
      JnlSelected := TRUE;

      IntraJnlTemplate.RESET;
      IntraJnlTemplate.SETRANGE("Form ID",FormID);

      CASE IntraJnlTemplate.COUNT OF
        0:
          BEGIN
            IntraJnlTemplate.INIT;
            IntraJnlTemplate.Name := Text000;
            IntraJnlTemplate.Description := Text001;
            IntraJnlTemplate.VALIDATE("Form ID");
            IntraJnlTemplate.INSERT;
            COMMIT;
          END;
        1:
          IntraJnlTemplate.FIND('-');
        ELSE
          JnlSelected := FORM.RUNMODAL(0,IntraJnlTemplate) = ACTION::LookupOK;
      END;
      IF JnlSelected THEN BEGIN
        IntrastatJnlLine.FILTERGROUP(2);
        IntrastatJnlLine.SETRANGE("Journal Template Name",IntraJnlTemplate.Name);
        IntrastatJnlLine.FILTERGROUP(0);
        IF OpenFromBatch THEN BEGIN
          IntrastatJnlLine."Journal Template Name" := '';
          FORM.RUN(IntraJnlTemplate."Form ID",IntrastatJnlLine);
        END;
      END;
    END;

    PROCEDURE TemplateSelectionFromBatch@9(VAR IntrastatJnlBatch@1003 : Record 262);
    VAR
      IntrastatJnlLine@1000 : Record 263;
      IntraJnlTemplate@1002 : Record 261;
      JnlSelected@1001 : Boolean;
    BEGIN
      OpenFromBatch := TRUE;
      IntraJnlTemplate.GET(IntrastatJnlBatch."Journal Template Name");
      IntraJnlTemplate.TESTFIELD("Form ID");
      IntrastatJnlBatch.TESTFIELD(Name);

      IntrastatJnlLine.FILTERGROUP := 2;
      IntrastatJnlLine.SETRANGE("Journal Template Name",IntraJnlTemplate.Name);
      IntrastatJnlLine.FILTERGROUP := 0;

      IntrastatJnlLine."Journal Template Name" := '';
      IntrastatJnlLine."Journal Batch Name" := IntrastatJnlBatch.Name;
      FORM.RUN(IntraJnlTemplate."Form ID",IntrastatJnlLine);
    END;

    PROCEDURE OpenJnl@2(VAR CurrentJnlBatchName@1000 : Code[10];VAR IntrastatJnlLine@1001 : Record 263);
    BEGIN
      CheckTemplateName(IntrastatJnlLine.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName);
      IntrastatJnlLine.FILTERGROUP(2);
      IntrastatJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      IntrastatJnlLine.FILTERGROUP(0);
    END;

    PROCEDURE OpenJnlBatch@10(VAR IntrastatJnlBatch@1003 : Record 262);
    VAR
      IntraJnlTemplate@1001 : Record 261;
      IntrastatJnlLine@1004 : Record 263;
      IntrastatJnlBatch2@1000 : Record 262;
      JnlSelected@1002 : Boolean;
    BEGIN
      IF IntrastatJnlBatch.GETFILTER("Journal Template Name") <> '' THEN
        EXIT;
      IntrastatJnlBatch.FILTERGROUP(2);
      IF IntrastatJnlBatch.GETFILTER("Journal Template Name") <> '' THEN BEGIN
        IntrastatJnlBatch.FILTERGROUP(0);
        EXIT;
      END;
      IntrastatJnlBatch.FILTERGROUP(0);

      IF NOT IntrastatJnlBatch.FIND('-') THEN BEGIN
        IF NOT IntraJnlTemplate.FIND('-') THEN
          TemplateSelection(0,IntrastatJnlLine,JnlSelected);
        IF IntraJnlTemplate.FIND('-') THEN
          CheckTemplateName(IntraJnlTemplate.Name,IntrastatJnlBatch.Name);
      END;
      IntrastatJnlBatch.FIND('-');
      JnlSelected := TRUE;
      IF IntrastatJnlBatch.GETFILTER("Journal Template Name") <> '' THEN
         IntraJnlTemplate.SETRANGE(Name,IntrastatJnlBatch.GETFILTER("Journal Template Name"));
      CASE IntraJnlTemplate.COUNT OF
        1:
          IntraJnlTemplate.FIND('-');
        ELSE
          JnlSelected := FORM.RUNMODAL(0,IntraJnlTemplate) = ACTION::LookupOK;
      END;
      IF NOT JnlSelected THEN
        ERROR('');

      IntrastatJnlBatch.FILTERGROUP(2);
      IntrastatJnlBatch.SETRANGE("Journal Template Name",IntraJnlTemplate.Name);
      IntrastatJnlBatch.FILTERGROUP(0);
    END;

    PROCEDURE CheckTemplateName@3(CurrentJnlTemplateName@1000 : Code[10];VAR CurrentJnlBatchName@1001 : Code[10]);
    VAR
      IntraJnlTemplate@1002 : Record 261;
      IntrastatJnlBatch@1003 : Record 262;
    BEGIN
      IntrastatJnlBatch.SETRANGE("Journal Template Name",CurrentJnlTemplateName);
      IF NOT IntrastatJnlBatch.GET(CurrentJnlTemplateName,CurrentJnlBatchName) THEN BEGIN
        IF NOT IntrastatJnlBatch.FIND('-') THEN BEGIN
          IntraJnlTemplate.GET(CurrentJnlTemplateName);
          IntrastatJnlBatch.INIT;
          IntrastatJnlBatch."Journal Template Name" := IntraJnlTemplate.Name;
          IntrastatJnlBatch.Name := Text002;
          IntrastatJnlBatch.Description := Text003;
          IntrastatJnlBatch.INSERT;
          COMMIT;
        END;
        CurrentJnlBatchName := IntrastatJnlBatch.Name;
      END;
    END;

    PROCEDURE CheckName@4(CurrentJnlBatchName@1000 : Code[10];VAR IntrastatJnlLine@1001 : Record 263);
    VAR
      IntrastatJnlBatch@1002 : Record 262;
    BEGIN
      IntrastatJnlBatch.GET(IntrastatJnlLine.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName);
    END;

    PROCEDURE SetName@5(CurrentJnlBatchName@1000 : Code[10];VAR IntrastatJnlLine@1001 : Record 263);
    BEGIN
      IntrastatJnlLine.FILTERGROUP(2);
      IntrastatJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      IntrastatJnlLine.FILTERGROUP(0);
      IF IntrastatJnlLine.FIND('-') THEN;
    END;

    PROCEDURE LookupName@7(CurrentJnlTemplateName@1000 : Code[10];CurrentJnlBatchName@1001 : Code[10];VAR EntrdJnlBatchName@1002 : Text[10]) : Boolean;
    VAR
      IntrastatJnlBatch@1003 : Record 262;
    BEGIN
      IntrastatJnlBatch."Journal Template Name" := CurrentJnlTemplateName;
      IntrastatJnlBatch.Name := CurrentJnlBatchName;
      IntrastatJnlBatch.FILTERGROUP(2);
      IntrastatJnlBatch.SETRANGE("Journal Template Name",CurrentJnlTemplateName);
      IntrastatJnlBatch.FILTERGROUP(0);
      IF FORM.RUNMODAL(0,IntrastatJnlBatch) <> ACTION::LookupOK THEN
        EXIT(FALSE);

      EntrdJnlBatchName := IntrastatJnlBatch.Name;
      EXIT(TRUE);
    END;

    PROCEDURE CalcStatisticalValue@6(VAR IntrastatJnlLine@1000 : Record 263;LastIntrastatJnlLine@1001 : Record 263;VAR StatisticalValue@1002 : Decimal;VAR TotalStatisticalValue@1003 : Decimal;VAR ShowStatisticalValue@1004 : Boolean;VAR ShowTotalStatisticalValue@1005 : Boolean);
    VAR
      TempIntrastatJnlLine@1006 : Record 263;
      TempIntrastatJnlLine2@1007 : Record 263;
    BEGIN
      TempIntrastatJnlLine.COPYFILTERS(IntrastatJnlLine);

      IF TempIntrastatJnlLine.CALCSUMS("Statistical Value") THEN BEGIN
        IF IntrastatJnlLine."Line No." <> 0 THEN // 0 = New record
          TotalStatisticalValue := TempIntrastatJnlLine."Statistical Value"
        ELSE BEGIN
          TotalStatisticalValue := TempIntrastatJnlLine."Statistical Value" + LastIntrastatJnlLine."Statistical Value";
        END;

        ShowTotalStatisticalValue := TRUE;
      END ELSE BEGIN
        ShowTotalStatisticalValue := FALSE;
      END;

      IF IntrastatJnlLine."Line No." <> 0 THEN BEGIN // 0 = New record
        TempIntrastatJnlLine.SETFILTER("Line No.",'<=%1',IntrastatJnlLine."Line No.");
        IF TempIntrastatJnlLine.CALCSUMS("Statistical Value") THEN BEGIN
          StatisticalValue := TempIntrastatJnlLine."Statistical Value";
          ShowStatisticalValue := TRUE;
        END ELSE
          ShowStatisticalValue := FALSE;
      END ELSE BEGIN
        TempIntrastatJnlLine.SETFILTER("Line No.",'<=%1',LastIntrastatJnlLine."Line No.");
        IF TempIntrastatJnlLine.CALCSUMS("Statistical Value") THEN BEGIN
          TempIntrastatJnlLine2.COPYFILTERS(IntrastatJnlLine);
          TempIntrastatJnlLine2 := LastIntrastatJnlLine;
          IF TempIntrastatJnlLine2.NEXT <> 0 THEN BEGIN
            StatisticalValue := TempIntrastatJnlLine."Statistical Value";
          END ELSE BEGIN
            StatisticalValue := TempIntrastatJnlLine."Statistical Value" + LastIntrastatJnlLine."Statistical Value";
          END;

          ShowStatisticalValue := TRUE;
        END ELSE
          ShowStatisticalValue := FALSE;
      END;
    END;

    BEGIN
    END.
  }
}
