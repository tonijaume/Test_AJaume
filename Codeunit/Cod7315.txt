OBJECT Codeunit 7315 Whse. Internal Pick Release
{
  OBJECT-PROPERTIES
  {
    Date=13/02/07;
    Time=12:00:00;
    Version List=NAVW15.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1005 : TextConst 'ENU=There is nothing to release for %1 %2.;ESP=No hay nada para lanzar para %1 %2.';
      Text001@1000 : TextConst 'ENU=You have to delete all related Warehouse Worksheet Lines first before you can reopen the Whse. Internal Pick.;ESP=Primero debe eliminar todas las l¡ns. hoja almac‚n relacionadas para poder reabrir el pick. interno alm.';
      HideValidationDialog@1001 : Boolean;
      Text002@1002 : TextConst 'ENU=You have to delete all related Warehouse Activity Lines first before you can reopen the Whse. Internal Pick.;ESP=Debe eliminar todas las l¡neas de actividad de almac‚n relacionadas para poder reabrir el pick. interno alm.';

    PROCEDURE Release@1(VAR WhsePickHeader@1000 : Record 7333);
    VAR
      Location@1004 : Record 14;
      WhsePickRqst@1003 : Record 7325;
      WhsePickLine@1001 : Record 7334;
    BEGIN
      WITH WhsePickHeader DO BEGIN
        IF Status = Status::Released THEN
          EXIT;

        WhsePickLine.SETRANGE("No.","No.");
        WhsePickLine.SETFILTER(Quantity,'<>0');
        IF NOT WhsePickLine.FIND('-') THEN
          ERROR(Text000,TABLECAPTION,"No.");

        IF "Location Code" <> '' THEN BEGIN
          Location.GET("Location Code");
          Location.TESTFIELD("Require Pick");
        END ELSE
          CheckPickRequired("Location Code");

        REPEAT
          WhsePickLine.TESTFIELD("Item No.");
          WhsePickLine.TESTFIELD("Unit of Measure Code");
          IF Location."Directed Put-away and Pick" THEN
            WhsePickLine.TESTFIELD("To Zone Code");
          IF Location."Bin Mandatory" THEN
            WhsePickLine.TESTFIELD("To Bin Code");
        UNTIL WhsePickLine.NEXT = 0;

        Status := Status::Released;
        MODIFY;

        CreateWhsePickRqst(WhsePickHeader);

        WhsePickRqst.SETRANGE("Document Type",WhsePickRqst."Document Type"::"Internal Pick");
        WhsePickRqst.SETRANGE("Document No.","No.");
        WhsePickRqst.SETRANGE(Status,Status::Open);
        WhsePickRqst.DELETEALL(TRUE);

        COMMIT;

      END;
    END;

    PROCEDURE Reopen@2(WhsePickHeader@1000 : Record 7333);
    VAR
      WhsePickRqst@1001 : Record 7325;
      PickWkshLine@1002 : Record 7326;
      WhseActivLine@1003 : Record 5767;
    BEGIN
      WITH WhsePickHeader DO BEGIN
        IF Status = Status::Open THEN
          EXIT;

        PickWkshLine.SETCURRENTKEY("Whse. Document Type","Whse. Document No.");
        PickWkshLine.SETRANGE("Whse. Document Type",PickWkshLine."Whse. Document Type"::"Internal Pick");
        PickWkshLine.SETRANGE("Whse. Document No.","No.");
        IF PickWkshLine.FIND('-') THEN
          ERROR(Text001);

        WhseActivLine.SETCURRENTKEY("Whse. Document No.","Whse. Document Type","Activity Type");
        WhseActivLine.SETRANGE("Whse. Document No.","No.");
        WhseActivLine.SETRANGE("Whse. Document Type",WhseActivLine."Whse. Document Type"::"Internal Pick");
        WhseActivLine.SETRANGE("Activity Type",WhseActivLine."Activity Type"::Pick);
        IF WhseActivLine.FIND('-') THEN
          ERROR(Text002);

        WhsePickRqst.SETRANGE("Document Type",WhsePickRqst."Document Type"::"Internal Pick");
        WhsePickRqst.SETRANGE("Document No.","No.");
        WhsePickRqst.SETRANGE(Status,Status::Released);
        IF WhsePickRqst.FIND('-') THEN
          REPEAT
            WhsePickRqst.Status := WhsePickRqst.Status::Open;
            WhsePickRqst.MODIFY;
          UNTIL WhsePickRqst.NEXT = 0;

        Status := Status::Open;
        MODIFY;
      END;
    END;

    LOCAL PROCEDURE CreateWhsePickRqst@3(VAR WhsePickHeader@1000 : Record 7333);
    VAR
      WhsePickRqst@1001 : Record 7325;
      Location@1002 : Record 14;
    BEGIN
      WITH WhsePickHeader DO BEGIN
        IF Location.RequirePicking("Location Code") THEN BEGIN
          WhsePickRqst."Document Type" := WhsePickRqst."Document Type"::"Internal Pick";
          WhsePickRqst."Production Subtype" := WhsePickRqst."Production Subtype"::" ";
          WhsePickRqst."Document No." := "No.";
          WhsePickRqst.Status := Status;
          WhsePickRqst."Location Code" := "Location Code";
          WhsePickRqst."Zone Code" := "To Zone Code";
          WhsePickRqst."Bin Code" := "To Bin Code";
          "Document Status" := GetDocumentStatus(0);
          WhsePickRqst."Completely Picked" :=
            "Document Status" = "Document Status"::"Completely Picked";
          IF NOT WhsePickRqst.INSERT THEN
            WhsePickRqst.MODIFY;
        END;
      END;
    END;

    PROCEDURE SetHideValidationDialog@14(NewHideValidationDialog@1000 : Boolean);
    BEGIN
      HideValidationDialog := NewHideValidationDialog;
    END;

    BEGIN
    END.
  }
}
