OBJECT Codeunit 5053 TAPIManagement
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=No Contact found.;ESP=No se encontr¢ Contacto.';
      Text002@1000 : TextConst 'ENU=No registered phone numbers have been found for this %1.;ESP=No se han encontrado n£meros de tel‚fono registrados para %1.';

    PROCEDURE Dial@1(PhoneNumber@1001 : Text[80]) Success@1000 : Boolean;
    VAR
      TAPI@1002 : Automation "{3C2554FE-2B41-4543-B3E8-E64296773DA5} 2.0:{6564775B-27BD-4595-9D77-0065FC9C71E3}:Unknown Automation Server.Unknown Class";
    BEGIN
      CREATE(TAPI,TRUE,TRUE);
      Success := TAPI.DialNumber(PhoneNumber);
      CLEAR(TAPI);
    END;

    PROCEDURE DialContCustVendBank@5(TableNo@1000 : Integer;No@1001 : Code[20];PhoneNo@1002 : Text[30];ContAltAddrCode@1003 : Code[10]);
    VAR
      InteractionTmplSetup@1004 : Record 5122;
      ContBusRel@1005 : Record 5054;
      Contact@1006 : Record 5050;
      Todo@1008 : Record 5080;
      TempSegmentLine@1007 : TEMPORARY Record 5077;
    BEGIN
      CASE TableNo OF
        DATABASE::Contact:
          Contact.GET(No);
        DATABASE::"To-do":
          BEGIN
            Todo.GET(No);
            Todo.TESTFIELD("Contact No.");
            Contact.GET(Todo."Contact No.");
          END;
        ELSE BEGIN
          ContBusRel.RESET;
          ContBusRel.SETCURRENTKEY("Link to Table","No.");
          CASE TableNo OF
            DATABASE::Customer:
              ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Customer);
            DATABASE::Vendor:
              ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Vendor);
            DATABASE::"Bank Account":
              ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::"Bank Account");
          END;
          ContBusRel.SETRANGE("No.",No);
          IF ContBusRel.FIND('-') THEN
            Contact.GET(ContBusRel."Contact No.")
          ELSE
            ERROR(Text001);
        END;
      END;

      // Call Make Phone Call Wizard
      TempSegmentLine.MakePhoneCallFromContact(Contact,Todo,TableNo,PhoneNo,ContAltAddrCode);
    END;

    PROCEDURE ShowNumbers@4(ContactNo@1000 : Code[20];ContAltAddrCode@1001 : Code[10]) : Text[260];
    VAR
      ContactThrough@1002 : TEMPORARY Record 5100;
      Contact@1003 : Record 5050;
      Contact2@1004 : Record 5050;
      ContAltAddr@1005 : Record 5051;
      ContAltAddrCode2@1006 : Code[10];
      I@1007 : Integer;
    BEGIN
      IF NOT Contact.GET(ContactNo) THEN
        EXIT;

      IF ContAltAddrCode = '' THEN
        ContAltAddrCode2 := Contact.ActiveAltAddress(TODAY)
      ELSE ContAltAddrCode2 := ContAltAddrCode;

      WITH ContactThrough DO BEGIN
        INIT;
        "Contact No." := ContactNo;
        Name := Contact.Name;
        IF Contact."Phone No." <> '' THEN BEGIN
          I := I + 1;
          Description := COPYSTR(Contact.FIELDCAPTION("Phone No."),1,MAXSTRLEN(Description));
          Number := Contact."Phone No.";
          Type := Contact.Type;
          INSERT;
        END;
        IF Contact."Mobile Phone No." <> '' THEN BEGIN
          I := I + 1;
          Key := I;
          Description := COPYSTR(Contact.FIELDCAPTION("Mobile Phone No."),1,MAXSTRLEN(Description));
          Number := Contact."Mobile Phone No.";
          Type := Contact.Type;
          INSERT;
        END;

        // Alternative address
        IF ContAltAddr.GET(Contact."No.",ContAltAddrCode2) THEN BEGIN
          IF ContAltAddr."Phone No." <> '' THEN BEGIN
            I := I + 1;
            Key := I;
            Description := TrimCode(ContAltAddr.Code) + ' - ' + ContAltAddr.FIELDCAPTION("Phone No.");
            Number := ContAltAddr."Phone No.";
            Type := Contact.Type;
            INSERT;
          END;
          IF ContAltAddr."Mobile Phone No." <> '' THEN BEGIN
            I := I + 1;
            Key := I;
            Description := TrimCode(ContAltAddr.Code) + ' - ' + ContAltAddr.FIELDCAPTION("Mobile Phone No.");
            Number := ContAltAddr."Mobile Phone No.";
            Type := Contact.Type;
            INSERT;
          END;
        END;

      END;

      // Get linked company phonenumbers
      IF (Contact.Type = Contact.Type::Person) AND (Contact."Company No." <> '') THEN BEGIN
        Contact2.GET(Contact."Company No.");

        IF ContAltAddrCode = '' THEN
          ContAltAddrCode2 := Contact2.ActiveAltAddress(TODAY)
        ELSE
          ContAltAddrCode2 := ContAltAddrCode;

        WITH ContactThrough DO BEGIN
          "Contact No." := ContactNo;
          Name := Contact2.Name;
          IF Contact2."Phone No." <> '' THEN BEGIN
            I := I + 1;
            Key := I;
            Description := COPYSTR(Contact2.FIELDCAPTION("Phone No."),1,MAXSTRLEN(Description));
            Number := Contact2."Phone No.";
            Type := Contact2.Type;
            INSERT;
          END;
          IF Contact2."Mobile Phone No." <> '' THEN BEGIN
            I := I + 1;
            Key := I;
            Description := COPYSTR(Contact2.FIELDCAPTION("Mobile Phone No."),1,MAXSTRLEN(Description));
            Number := Contact2."Mobile Phone No.";
            Type := Contact2.Type;
            INSERT;
          END;

          // Alternative address
          IF ContAltAddr.GET(Contact2."No.",ContAltAddrCode2) THEN BEGIN
            IF ContAltAddr."Phone No." <> '' THEN BEGIN
              I := I + 1;
              Key := I;
              Description := TrimCode(ContAltAddr.Code) + ' - ' + ContAltAddr.FIELDCAPTION("Phone No.");
              Number := ContAltAddr."Phone No.";
              Type := Contact2.Type;
              INSERT;
            END;
            IF ContAltAddr."Mobile Phone No." <> '' THEN BEGIN
              I := I + 1;
              Key := I;
              Description := TrimCode(ContAltAddr.Code) + ' - ' + ContAltAddr.FIELDCAPTION("Mobile Phone No.");
              Number := ContAltAddr."Mobile Phone No.";
              Type := Contact2.Type;
              INSERT;
            END;
          END;
        END;
      END;
      IF ContactThrough.FIND('-') THEN BEGIN
        IF FORM.RUNMODAL(FORM::"Contact Through",ContactThrough) = ACTION::LookupOK THEN
          EXIT(ContactThrough.Number);
      END ELSE
        ERROR(Text002,Contact.TABLECAPTION);
    END;

    LOCAL PROCEDURE TrimCode@3(Code@1001 : Code[20]) TrimString@1000 : Text[20];
    BEGIN
      TrimString := COPYSTR(Code,1,1) + LOWERCASE(COPYSTR(Code,2,STRLEN(Code) - 1))
    END;

    BEGIN
    END.
  }
}
