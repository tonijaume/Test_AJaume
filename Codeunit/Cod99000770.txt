OBJECT Codeunit 99000770 Where-Used Management
{
  OBJECT-PROPERTIES
  {
    Date=21/05/03;
    Time=12:00:00;
    Version List=NAVW13.70;
  }
  PROPERTIES
  {
    Permissions=TableData 99000771=r,
                TableData 99000779=r,
                TableData 99000790=imd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      ProdBOMHeader@1000 : Record 99000771;
      WhereUsedList@1001 : TEMPORARY Record 99000790;
      UOMMgt@1002 : Codeunit 5402;
      VersionMgt@1003 : Codeunit 99000756;
      CostCalcMgt@1004 : Codeunit 5836;
      MultiLevel@1005 : Boolean;
      NextWhereUsedEntryNo@1006 : Integer;

    PROCEDURE FindRecord@2(Which@1000 : Text[30];VAR WhereUsedList2@1001 : Record 99000790) : Boolean;
    BEGIN
      WhereUsedList := WhereUsedList2;
      IF NOT WhereUsedList.FIND(Which) THEN
        EXIT(FALSE);
      WhereUsedList2 := WhereUsedList;

      EXIT(TRUE);
    END;

    PROCEDURE NextRecord@3(Steps@1000 : Integer;VAR WhereUsedList2@1001 : Record 99000790) : Integer;
    VAR
      CurrentSteps@1002 : Integer;
    BEGIN
      WhereUsedList := WhereUsedList2;
      CurrentSteps := WhereUsedList.NEXT(Steps);
      IF CurrentSteps <> 0 THEN
        WhereUsedList2 := WhereUsedList;

      EXIT(CurrentSteps);
    END;

    PROCEDURE WhereUsedFromItem@4(Item@1000 : Record 27;"Calc.Date"@1001 : Date;NewMultiLevel@1002 : Boolean);
    BEGIN
      WhereUsedList.DELETEALL;
      NextWhereUsedEntryNo := 1;
      MultiLevel := NewMultiLevel;

      BuildWhereUsedList(1,Item."No.","Calc.Date",1,1);
    END;

    PROCEDURE WhereUsedFromProdBOM@5(ProdBOM@1000 : Record 99000771;"Calc.Date"@1001 : Date;NewMultiLevel@1002 : Boolean);
    BEGIN
      WhereUsedList.DELETEALL;
      NextWhereUsedEntryNo := 1;
      MultiLevel := NewMultiLevel;

      BuildWhereUsedList(2,ProdBOM."No.","Calc.Date",1,1);
    END;

    LOCAL PROCEDURE BuildWhereUsedList@1(Type@1000 : ' ,Item,Production BOM';No@1001 : Code[20];"Calc.Date"@1002 : Date;Level@1003 : Integer;Quantity@1004 : Decimal);
    VAR
      ItemAssembly@1005 : Record 27;
      ProdBOMComponent@1006 : Record 99000772;
    BEGIN
      IF Level > 30 THEN
        EXIT;

      IF Type = Type::"Production BOM" THEN BEGIN
        ItemAssembly.SETCURRENTKEY("Production BOM No.");
        ItemAssembly.SETRANGE("Production BOM No.",No);
        IF ItemAssembly.FIND('-') THEN
          REPEAT
            WhereUsedList."Entry No." := NextWhereUsedEntryNo;
            WhereUsedList."Item No." := ItemAssembly."No.";
            WhereUsedList.Description := ItemAssembly.Description;
            WhereUsedList."Level Code" := Level;
            WhereUsedList."Quantity Needed" :=
              Quantity *
              (1 + ItemAssembly."Scrap %" / 100) *
              UOMMgt.GetQtyPerUnitOfMeasure(
                ItemAssembly,
                ItemAssembly."Base Unit of Measure") /
              UOMMgt.GetQtyPerUnitOfMeasure(
                ItemAssembly,
                VersionMgt.GetBOMUnitOfMeasure(
                  ItemAssembly."Production BOM No.",
                  VersionMgt.GetBOMVersion(
                    ItemAssembly."Production BOM No.",
                    "Calc.Date",FALSE)));
            WhereUsedList."Version Code" := VersionMgt.GetBOMVersion(No,"Calc.Date",FALSE);
            WhereUsedList.INSERT;
            NextWhereUsedEntryNo := NextWhereUsedEntryNo + 1;
            IF MultiLevel THEN
              BuildWhereUsedList(
                1,
                ItemAssembly."No.",
                "Calc.Date",
                Level + 1,
                WhereUsedList."Quantity Needed");
          UNTIL ItemAssembly.NEXT = 0;
      END;


      ProdBOMComponent.SETCURRENTKEY(Type,"No.");
      ProdBOMComponent.SETRANGE(Type,Type);
      ProdBOMComponent.SETRANGE("No.",No);
      IF "Calc.Date" <> 0D THEN BEGIN
        ProdBOMComponent.SETFILTER("Starting Date",'%1|..%2',0D,"Calc.Date");
        ProdBOMComponent.SETFILTER("Ending Date",'%1|%2..',0D,"Calc.Date");
      END;

      IF ProdBOMComponent.FIND('-') THEN
        REPEAT
          IF VersionMgt.GetBOMVersion(
               ProdBOMComponent."Production BOM No.","Calc.Date",TRUE) =
               ProdBOMComponent."Version Code"
          THEN BEGIN
            ProdBOMHeader.GET(ProdBOMComponent."Production BOM No.");
            BuildWhereUsedList(
              2,
              ProdBOMComponent."Production BOM No.",
              "Calc.Date",
              Level,
              CostCalcMgt.CalcCompItemQtyBase(ProdBOMComponent,"Calc.Date",Quantity,'',FALSE));
          END;
        UNTIL ProdBOMComponent.NEXT = 0;
    END;

    BEGIN
    END.
  }
}
