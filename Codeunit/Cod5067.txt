OBJECT Codeunit 5067 SearchManagement
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    SingleInstance=Yes;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      CSearch@1023 : Automation "{B22D5415-8BAC-4DC0-8E1B-416F5BD7F998} 1.0:{7061E21B-789E-4B2A-B1A8-14DC552B844D}:Unknown Automation Server.Unknown Class";
      BlurrySearch@1031 : Automation "{B22D5415-8BAC-4DC0-8E1B-416F5BD7F998} 1.0:{50CB5C49-0B0F-4007-9C98-37B09DC907CF}:Unknown Automation Server.Unknown Class";
      SearchWord@1001 : Record 5117;
      SearchWordDetail@1000 : Record 5118;
      Contact@1004 : Record 5050;
      IntLogEntry@1012 : Record 5065;
      Opportunity@1014 : Record 5092;
      Todo@1013 : Record 5080;
      RlshpMgtCommentLine@1018 : Record 5061;
      RMSetup@1015 : Record 5079;
      AllowParsing@1021 : Boolean;
      Text008@1017 : TextConst 'ENU=Incorrect Query.;ESP=Consulta incorrecta.';
      HasQuestionMark@1029 : Boolean;
      HasAsterix@1028 : Boolean;
      HasCharacter@1027 : Boolean;
      HasApostrophe@1026 : Boolean;
      HasLogicalOperator@1024 : Boolean;
      HasDelimiter@1022 : Boolean;
      HasSpace@1025 : Boolean;
      BlurryKey1@1033 : Text[80];
      BlurryKey2@1032 : Text[80];
      Delimiters@1005 : Text[250];
      i@1006 : Integer;
      Text009@1007 : TextConst 'ENU=Not specified/deleted/moved.;ESP=No especificado/eliminado/desplazado.';

    PROCEDURE ParseField@1(KeyValue@1008 : Code[20];SubNo@1003 : Code[20];TextToParse@1009 : Text[80];TableName@1001 : 'Contact,Interaction Log Entry,To-do,Opportunity';FieldNumber@1002 : Integer);
    VAR
      SeparateWord@1006 : Text[80];
      WordPos@1000 : Integer;
      StartPos@1005 : Integer;
    BEGIN
      RMSetup.GET;

      IF NOT (RMSetup."Index Mode" = RMSetup."Index Mode"::Manual) OR AllowParsing
      THEN BEGIN
        Delimiters := ' @*?' + RMSetup.Delimiters;
        DeletePrevDetails(KeyValue,SubNo,TableName,FieldNumber);

        WordPos := 1;
        StartPos := 1;
        FOR i := 1 TO STRLEN(TextToParse) DO BEGIN
          CASE STRPOS(Delimiters,FORMAT(TextToParse[i])) OF
            0: ;
            1..STRLEN(' @*?'):
            BEGIN
              IF i > StartPos THEN BEGIN
                SeparateWord := COPYSTR(TextToParse,StartPos,i - StartPos);
                SearchWord.InsertWord(
                  SeparateWord,KeyValue,SubNo,TableName,FieldNumber,WordPos);
                WordPos := WordPos + 1;
              END;
              StartPos := i + 1;
            END;
            ELSE BEGIN
              IF i > StartPos THEN BEGIN
                SeparateWord := COPYSTR(TextToParse,StartPos,i - StartPos);
                SearchWord.InsertWord(
                  SeparateWord,KeyValue,SubNo,TableName,FieldNumber,WordPos);
                WordPos := WordPos + 1;
              END;
              SeparateWord := FORMAT(TextToParse[i]);
              SearchWord.InsertWord(
                SeparateWord,KeyValue,SubNo,TableName,FieldNumber,WordPos);
              WordPos := WordPos + 1;
              StartPos := i + 1;
            END;
          END;
        END;
        IF STRLEN(TextToParse) >= StartPos THEN BEGIN
          SeparateWord := COPYSTR(TextToParse,StartPos,STRLEN(TextToParse) - StartPos + 1);
          SearchWord.InsertWord(
            SeparateWord,KeyValue,SubNo,TableName,FieldNumber,WordPos);
        END;
      END
        ELSE
          EXIT;
    END;

    PROCEDURE DeletePrevDetails@2(KeyValue@1004 : Code[20];SubNo@1005 : Code[20];TableName@1002 : 'Contact,Interaction Log Entry,To-do,Opportunity';FieldNumber@1000 : Integer);
    BEGIN
      SearchWordDetail.RESET;
      SearchWordDetail.SETCURRENTKEY("No.","Sub No.","Table Name");
      SearchWordDetail.SETRANGE("No.",KeyValue);
      SearchWordDetail.SETRANGE("Sub No.",SubNo);
      SearchWordDetail.SETRANGE("Table Name",TableName);
      SearchWordDetail.SETRANGE("Field No.",FieldNumber);
      SearchWordDetail.DELETEALL;
      SearchWordDetail.RESET;
    END;

    PROCEDURE ParseContact@4(VAR Contact@1000 : Record 5050;AllowContactParsing@1001 : Boolean);
    BEGIN
      AllowParsing := AllowContactParsing;
      WITH Contact DO BEGIN
        ParseField(
          "No.","No.",Name,SearchWordDetail."Table Name"::Contact,FIELDNO(Name));
        ParseField(
          "No.","No.","Name 2",SearchWordDetail."Table Name"::Contact,FIELDNO("Name 2"));
        ParseField(
          "No.","No.",Address,SearchWordDetail."Table Name"::Contact,FIELDNO(Address));
        ParseField(
          "No.","No.","Address 2",SearchWordDetail."Table Name"::Contact,FIELDNO("Address 2"));
        ParseField(
          "No.","No.",City,SearchWordDetail."Table Name"::Contact,FIELDNO(City));
        ParseField(
          "No.","No.","Phone No.",SearchWordDetail."Table Name"::Contact,FIELDNO("Phone No."));
        ParseField(
          "No.","No.","Mobile Phone No.",SearchWordDetail."Table Name"::Contact,FIELDNO("Mobile Phone No."));
        ParseField(
          "No.","No.",Pager,SearchWordDetail."Table Name"::Contact,FIELDNO(Pager));
        ParseField(
          "No.","No.","Telex No.",SearchWordDetail."Table Name"::Contact,FIELDNO("Telex No."));
        ParseField(
          "No.","No.","Fax No.",SearchWordDetail."Table Name"::Contact,FIELDNO("Fax No."));
        ParseField(
          "No.","No.","Telex Answer Back",SearchWordDetail."Table Name"::Contact,FIELDNO("Telex Answer Back"));
        ParseField(
          "No.","No.","VAT Registration No.",SearchWordDetail."Table Name"::Contact,FIELDNO("VAT Registration No."));
        ParseField(
          "No.","No.","Post Code",SearchWordDetail."Table Name"::Contact,FIELDNO("Post Code"));
        ParseField(
          "No.","No.",County,SearchWordDetail."Table Name"::Contact,FIELDNO(County));
        ParseField(
          "No.","No.","E-Mail",SearchWordDetail."Table Name"::Contact,FIELDNO("E-Mail"));
        ParseField(
          "No.","No.","Company Name",SearchWordDetail."Table Name"::Contact,FIELDNO("Company Name"));
        ParseField(
          "No.","No.","Search Name",SearchWordDetail."Table Name"::Contact,FIELDNO("Search Name"));
        ParseField(
          "No.","No.","Home Page",SearchWordDetail."Table Name"::Contact,FIELDNO("Home Page"));
      END;
    END;

    PROCEDURE ParseIntLogEntry@7(VAR IntLogEntry@1000 : Record 5065;AllowIntLogEntryParsing@1001 : Boolean);
    BEGIN
      AllowParsing := AllowIntLogEntryParsing;
      WITH IntLogEntry DO BEGIN
        ParseField(
          "Contact No.",
          FORMAT("Entry No."),
          Description,
          SearchWordDetail."Table Name"::"Interaction Log Entry",
          FIELDNO(Description));
        ParseField(
          "Contact No.",
          FORMAT("Entry No."),
          Subject,
          SearchWordDetail."Table Name"::"Interaction Log Entry",
          FIELDNO(Subject));
      END;
    END;

    PROCEDURE ParseToDo@10(VAR Todo@1000 : Record 5080;AllowTodoParsing@1001 : Boolean);
    BEGIN
      AllowParsing := AllowTodoParsing;
      ParseField(
        Todo."Contact No.",
        Todo."No.",
        Todo.Description,
        SearchWordDetail."Table Name"::"To-do",
        Todo.FIELDNO(Description));
    END;

    PROCEDURE ParseOpportunity@12(VAR Opportunity@1000 : Record 5092;AllowOpportParsing@1001 : Boolean);
    BEGIN
      AllowParsing := AllowOpportParsing;
      ParseField(
        Opportunity."Contact No.",
        Opportunity."No.",
        Opportunity.Description,
        SearchWordDetail."Table Name"::Opportunity,
        Opportunity.FIELDNO(Description));
    END;

    PROCEDURE ParseComments@8(VAR RlshpMgtCommentLine@1000 : Record 5061;AllowCommentParsing@1002 : Boolean);
    BEGIN
      AllowParsing := AllowCommentParsing;
      IF RlshpMgtCommentLine."Table Name" =  RlshpMgtCommentLine."Table Name"::Contact THEN
        ParseField(
          RlshpMgtCommentLine."No.",
          FORMAT(RlshpMgtCommentLine."Line No."),
          RlshpMgtCommentLine.Comment,
          SearchWordDetail."Table Name"::"Rlshp. Mgt. Comment Line",
          RlshpMgtCommentLine.FIELDNO(Comment));
    END;

    PROCEDURE DeleteContactDetails@5(KeyValue@1000 : Code[20]);
    BEGIN
      WITH Contact DO BEGIN
        IF GET(KeyValue) THEN BEGIN
          DeletePrevDetails(
            "No.","No.",SearchWordDetail."Table Name"::Contact,FIELDNO(Name));
          DeletePrevDetails(
            "No.","No.",SearchWordDetail."Table Name"::Contact,FIELDNO("Name 2"));
          DeletePrevDetails(
            "No.","No.",SearchWordDetail."Table Name"::Contact,FIELDNO(Address));
          DeletePrevDetails(
            "No.","No.",SearchWordDetail."Table Name"::Contact,FIELDNO("Address 2"));
          DeletePrevDetails(
            "No.","No.",SearchWordDetail."Table Name"::Contact,FIELDNO(City));
          DeletePrevDetails(
            "No.","No.",SearchWordDetail."Table Name"::Contact,FIELDNO("Phone No."));
          DeletePrevDetails(
            "No.","No.",SearchWordDetail."Table Name"::Contact,FIELDNO("Mobile Phone No."));
          DeletePrevDetails(
            "No.","No.",SearchWordDetail."Table Name"::Contact,FIELDNO(Pager));
          DeletePrevDetails(
            "No.","No.",SearchWordDetail."Table Name"::Contact,FIELDNO("Telex No."));
          DeletePrevDetails(
            "No.","No.",SearchWordDetail."Table Name"::Contact,FIELDNO("Fax No."));
          DeletePrevDetails(
            "No.","No.",SearchWordDetail."Table Name"::Contact,FIELDNO("Telex Answer Back"));
          DeletePrevDetails(
            "No.","No.",SearchWordDetail."Table Name"::Contact,FIELDNO("VAT Registration No."));
          DeletePrevDetails(
            "No.","No.",SearchWordDetail."Table Name"::Contact,FIELDNO("Post Code"));
          DeletePrevDetails(
            "No.","No.",SearchWordDetail."Table Name"::Contact,FIELDNO(County));
          DeletePrevDetails(
            "No.","No.",SearchWordDetail."Table Name"::Contact,FIELDNO("E-Mail"));
          DeletePrevDetails(
            "No.","No.",SearchWordDetail."Table Name"::Contact,FIELDNO("Company Name"));
          DeletePrevDetails(
            "No.","No.",SearchWordDetail."Table Name"::Contact,FIELDNO("Search Name"));
          DeletePrevDetails(
            "No.","No.",SearchWordDetail."Table Name"::Contact,FIELDNO("Home Page"));
        END;
      END;
    END;

    PROCEDURE DeleteIntLogEntryDetails@20(KeyValue@1000 : Integer);
    VAR
      KeyValueInt@1001 : Integer;
    BEGIN
      WITH IntLogEntry DO BEGIN
        IF GET(KeyValue) THEN BEGIN
          DeletePrevDetails(
            "Contact No.",
            FORMAT("Entry No."),
            SearchWordDetail."Table Name"::"Interaction Log Entry",
            FIELDNO(Description));
          DeletePrevDetails(
            "Contact No.",
            FORMAT("Entry No."),
            SearchWordDetail."Table Name"::"Interaction Log Entry",
            FIELDNO(Subject));
        END;
      END;
    END;

    PROCEDURE DeleteTodoDetails@11(KeyValue@1000 : Code[20]);
    BEGIN
      WITH Todo DO BEGIN
        IF GET(KeyValue) THEN BEGIN
          DeletePrevDetails(
            "Contact No.","No.",SearchWordDetail."Table Name"::"To-do",FIELDNO(Description));
        END;
      END;
    END;

    PROCEDURE DeleteOpportunityDetails@16(KeyValue@1000 : Code[20]);
    BEGIN
      WITH Opportunity DO BEGIN
        IF GET(KeyValue) THEN BEGIN
          DeletePrevDetails(
            "Contact No.","No.",SearchWordDetail."Table Name"::Opportunity,FIELDNO(Description));
        END;
      END;
    END;

    PROCEDURE DeleteCommentDetails@18(KeyValue@1000 : Code[20];LineNo@1001 : Integer);
    BEGIN
      WITH RlshpMgtCommentLine DO BEGIN
        SETCURRENTKEY("Table Name","No.","Sub No.","Line No.");
        SETRANGE("Table Name","Table Name"::Contact);
        SETRANGE("No.",KeyValue);
        SETRANGE("Sub No.",0);
        SETRANGE("Line No.",LineNo);
        IF FIND('-') THEN
          REPEAT
            DeletePrevDetails(
              "No.",FORMAT("Line No."),SearchWordDetail."Table Name"::"Rlshp. Mgt. Comment Line",FIELDNO(Comment));
          UNTIL NEXT = 0;
      END;
    END;

    PROCEDURE ComposeQueryLine@19(VAR SearchDetail@1000 : Record 5115) Query@1001 : Text[1024];
    BEGIN
      SearchDetail."Word(s)" := DELCHR(SearchDetail."Word(s)",'<',' ');

      Query := SearchDetail."Word(s)";

      AnalyseAtom(Query);

      IF HasApostrophe THEN BEGIN
        i := 1;
        WHILE Query[i] <> 0 DO BEGIN
          IF Query[i] = '''' THEN BEGIN
            Query := INSSTR(Query,'''',i+1);
            i:=i+1;
          END;
        i:=i+1;
        END;
      END;

      IF NOT (HasAsterix AND HasQuestionMark) AND
        (HasSpace OR HasLogicalOperator OR HasApostrophe OR HasDelimiter)
      THEN
        Query := '''' + Query + ''''
      ELSE
        IF NOT SearchDetail."Match Case" THEN
          Query := '@' + Query;

      IF SearchDetail.Exclude THEN
        Query := '<>' + Query;

      IF SearchDetail."Logical Operator" = SearchDetail."Logical Operator"::"OR"
        THEN
          Query := '| ' + Query
        ELSE
          Query := '& ' + Query;
    END;

    PROCEDURE AnalyseAtom@15(Atom@1000 : Text[1024]);
    VAR
      Delimiters@1001 : Text[250];
    BEGIN
      HasQuestionMark := FALSE;
      HasAsterix := FALSE;
      HasCharacter := FALSE;
      HasSpace := FALSE;
      HasLogicalOperator := FALSE;
      HasApostrophe := FALSE;
      HasDelimiter := FALSE;

      RMSetup.GET;
      Delimiters := DELCHR(RMSetup.Delimiters,'<=>','*?@');

      FOR i := 1 TO STRLEN(Atom) DO BEGIN
        CASE Atom[i] OF
          '?':
          HasQuestionMark := TRUE;
          '*':
          HasAsterix := TRUE;
          ' ':
          HasSpace := TRUE;
          '&':
          HasLogicalOperator := TRUE;
          '|':
          HasLogicalOperator := TRUE;
          '<':
          IF Atom[i+1] = '>' THEN HasLogicalOperator := TRUE ELSE BEGIN
            IF STRPOS(Delimiters,FORMAT(Atom[i])) <> 0 THEN
              HasDelimiter := TRUE
            ELSE
              HasCharacter := TRUE;
          END;
          '''':
          HasApostrophe := TRUE;
        ELSE
          IF STRPOS(Delimiters,FORMAT(Atom[i])) <> 0 THEN
            HasDelimiter := TRUE
          ELSE
            HasCharacter := TRUE;
        END;
      END;
    END;

    PROCEDURE CheckLine@21(VAR SearchDetail@1000 : Record 5115);
    VAR
      Text001@1001 : TextConst 'ENU=Word does not contain any symbol.;ESP=Word no contiene ning£n s¡mbolo.';
      Text002@1005 : TextConst 'ENU=Wildcard ''@'' is not acceptable. Please use the Match Case checkbox.;ESP=No se acepta el car cter ''@''. Por favor, utilice la casilla de May£sculas y min£sculas.';
      Text003@1011 : TextConst 'ENU=Incorrect Search Line.;ESP=L¡nea b£squeda incorrecta.';
      Query@1004 : Text[255];
    BEGIN
      Query := SearchDetail."Word(s)";
      IF STRLEN(Query) = 0 THEN
        ERROR(Text001);

      IF STRPOS(Query,'@') = 1 THEN
        ERROR(Text002);

      IF ISCLEAR(CSearch) THEN
        CREATE(CSearch);

      AnalyseAtom(Query);

      IF HasApostrophe THEN BEGIN
        i := 1;
        WHILE Query[i] <> 0 DO BEGIN
          IF Query[i] = '''' THEN BEGIN
            Query := INSSTR(Query,'''',i+1);
            i:=i+1;
          END;
        i:=i+1;
        END;
      END;

      IF HasApostrophe OR
        HasLogicalOperator OR
        HasDelimiter OR
        HasSpace
      THEN
        Query := '''' + Query + ''''
      ELSE
        IF NOT SearchDetail."Match Case" THEN
          Query := '@' + Query;

      RMSetup.GET;
      CSearch.SetOneCharacterWords(RMSetup.Delimiters);

      IF NOT CSearch.ParseQuery(Query) THEN
        ERROR(Text003);
    END;

    PROCEDURE ComposeSearchDetailTable@24(Query@1001 : Text[1024];VAR SearchDetail@1000 : Record 5115);
    VAR
      Atom@1005 : Text[1024];
      CurrAtom@1004 : Text[1024];
      PrevNotOperator@1003 : Boolean;
      NotOperator@1009 : Boolean;
      PrevKeyValue@1011 : Integer;
      PrevPhraseNo@1008 : Integer;
      PrevLogicalOperator@1007 : Integer;
      LogicalOperator@1002 : Integer;
      PhraseNo@1010 : Integer;
      AtomCount@1006 : Integer;
    BEGIN
      IF ISCLEAR(CSearch) THEN
        CREATE(CSearch);
      WITH SearchWordDetail DO BEGIN
        CSearch.Reset;

        RMSetup.GET;
        CSearch.SetOneCharacterWords(RMSetup.Delimiters);

        IF NOT CSearch.ParseQuery(Query) THEN BEGIN
          CLEAR(CSearch);
          ERROR(Text008);
        END;
        AtomCount := CSearch.GetAtomCount;

        FOR i := 0 TO AtomCount-1 DO BEGIN
          CSearch.GetAtom(
            i,Atom,LogicalOperator,NotOperator,PhraseNo,BlurryKey1,BlurryKey2);

          IF PhraseNo = 0 THEN BEGIN
            IF PrevPhraseNo <> 0 THEN BEGIN
              InsertSearchDetailLine(
                SearchDetail,
                PrevKeyValue,
                PrevLogicalOperator,
                PrevNotOperator,
                CurrAtom);
              PrevPhraseNo := PhraseNo;
            END;
            InsertSearchDetailLine(
              SearchDetail,
              i,
              LogicalOperator,
              NotOperator,
              Atom);
          END
          ELSE
            CASE PrevPhraseNo OF
              0: BEGIN
                PrevKeyValue := i;
                PrevLogicalOperator := LogicalOperator;
                PrevNotOperator := NotOperator;
                PrevPhraseNo := PhraseNo;
                CurrAtom := Atom;
              END;
              PhraseNo :
                CurrAtom := CurrAtom + ' ' + Atom;
              ELSE
                BEGIN
                  InsertSearchDetailLine(
                    SearchDetail,
                    PrevKeyValue,
                    PrevLogicalOperator,
                    PrevNotOperator,
                    CurrAtom);
                  PrevKeyValue := i;
                  PrevLogicalOperator := LogicalOperator;
                  PrevNotOperator := NotOperator;
                  PrevPhraseNo := PhraseNo;
                  CurrAtom := Atom;
                END;
           END;
        END;
        IF PrevPhraseNo <> 0 THEN BEGIN
          InsertSearchDetailLine(
            SearchDetail,
            PrevKeyValue,
            PrevLogicalOperator,
            PrevNotOperator,
            CurrAtom);
        END;
      END;
    END;

    PROCEDURE InsertSearchDetailLine@23(VAR SearchDetail@1004 : Record 5115;KeyValue@1000 : Integer;LogicalOperator@1001 : 'OR,AND';NotOperator@1002 : Boolean;Word@1003 : Text[1024]);
    BEGIN
      SearchDetail."Line No." := (KeyValue + 1) * 10000;
      IF LogicalOperator = 1 THEN
        SearchDetail."Logical Operator" := SearchDetail."Logical Operator"::"AND"
      ELSE
        SearchDetail."Logical Operator" := SearchDetail."Logical Operator"::"OR";
      IF NotOperator = TRUE THEN
        SearchDetail.Exclude := TRUE
      ELSE
        SearchDetail.Exclude := FALSE;
      IF STRPOS(Word,'@') = 1 THEN BEGIN
        SearchDetail."Match Case" := FALSE;
        Word := DELCHR(Word,'<','@');
      END
        ELSE
         SearchDetail."Match Case" := TRUE;
      SearchDetail."Word(s)" := Word;
      SearchDetail.INSERT;
    END;

    PROCEDURE ProcessQuery@6(Query@1000 : Text[1024];VAR SearchResult@1002 : Record 5113;VAR SearchResultDetail@1005 : Record 5116;"Search Area"@1006 : 'All,Contact,Interaction Log Entry,To-do,Opportunity,Rlshp. Mgt. Comment Line';"Contact Type"@1008 : 'All,Company,Person';UseBlurry@1018 : Boolean);
    VAR
      SearchWordDetailTemp@1017 : TEMPORARY Record 5118;
      ContactNo@1014 : Text[20];
      Atom@1019 : Text[1024];
      AreaFilter@1004 : Text[80];
      ContactTypeFilter@1001 : Text[80];
      SubNo@1015 : Text[20];
      NotOperator@1011 : Boolean;
      AtomCount@1016 : Integer;
      CurrAtomNo@1013 : Integer;
      NoOfResultRecords@1012 : Integer;
      Operator@1010 : Integer;
      PhraseWordNo@1009 : Integer;
      TableOptionNo@1007 : Integer;
      SubNoInt@1003 : Integer;
    BEGIN
      IF ISCLEAR(CSearch) THEN
        CREATE(CSearch);
      CSearch.Reset;

      RMSetup.GET;
      CSearch.SetOneCharacterWords(RMSetup.Delimiters);

      IF NOT CSearch.ParseQuery(Query) THEN
        ERROR(Text008);

      AtomCount := CSearch.GetAtomCount;
      CurrAtomNo := 0;

      SearchWordDetail.RESET;
      CASE "Search Area" OF
        "Search Area"::Contact:
          SearchWordDetail.SETRANGE("Table Name",SearchWordDetail."Table Name"::Contact);
        "Search Area"::"Interaction Log Entry":
          SearchWordDetail.SETRANGE("Table Name",SearchWordDetail."Table Name"::"Interaction Log Entry");
        "Search Area"::"To-do":
          SearchWordDetail.SETRANGE("Table Name",SearchWordDetail."Table Name"::"To-do");
        "Search Area"::Opportunity:
          SearchWordDetail.SETRANGE("Table Name",SearchWordDetail."Table Name"::Opportunity);
        "Search Area"::"Rlshp. Mgt. Comment Line":
          SearchWordDetail.SETRANGE("Table Name",SearchWordDetail."Table Name"::"Rlshp. Mgt. Comment Line");
      END;
      CASE "Contact Type" OF
        "Contact Type"::Company:
          SearchWordDetail.SETRANGE("Contact Type",SearchWordDetail."Contact Type"::Company);
        "Contact Type"::Person:
          SearchWordDetail.SETRANGE("Contact Type",SearchWordDetail."Contact Type"::Person);
      END;

      IF SearchWordDetail.GETFILTERS <> '' THEN BEGIN
        AreaFilter := SearchWordDetail.GETFILTER("Table Name");
        ContactTypeFilter := SearchWordDetail.GETFILTER("Contact Type");
      END;

      WHILE AtomCount <> CurrAtomNo DO BEGIN
        CSearch.GetAtom(CurrAtomNo,Atom,Operator,NotOperator,PhraseWordNo,BlurryKey1,BlurryKey2);
        CSearch.SetCurrentAtom(Atom);

        CASE TRUE OF
          (STRLEN(Atom) = 1):
            BEGIN
              IF Atom = '''' THEN
                SearchWord.SETRANGE(SearchWord.Word,'''')
              ELSE
                IF Atom IN [')','(','<','>','&','|','='] THEN BEGIN
                  Atom := '''' + Atom + '''';
                  SearchWord.SETFILTER(SearchWord.Word,Atom);
                END
              ELSE
                SearchWord.SETFILTER(SearchWord.Word,Atom);
            END;
          (STRLEN(Atom) > 1):
            BEGIN
              IF (STRPOS(Atom,')') <> 0) OR
                (STRPOS(Atom,'(') <> 0) OR
                (STRPOS(Atom,'=') <> 0) OR
                (STRPOS(Atom,'<') <> 0) OR
                (STRPOS(Atom,'>') <> 0) OR
                (STRPOS(Atom,'&') <> 0) OR
                (STRPOS(Atom,'|') <> 0)
              THEN BEGIN
                Atom := '''' + Atom + '''';
                SearchWord.SETFILTER(SearchWord.Word,Atom);
              END ELSE
                SearchWord.SETFILTER(SearchWord.Word,Atom);
            END;
        END;

        IF NotOperator THEN BEGIN
          IF SearchWord.FIND('-') THEN BEGIN
            SearchWordDetail.RESET;
            SearchWordDetail.SETFILTER("Table Name",AreaFilter);
            SearchWordDetail.SETFILTER("Contact Type",ContactTypeFilter);
            IF SearchWordDetail.FIND('-') THEN BEGIN
              SearchWordDetailTemp.DELETEALL;
              REPEAT
                SearchWordDetailTemp := SearchWordDetail;
                SearchWordDetailTemp.INSERT;
              UNTIL SearchWordDetail.NEXT = 0;
            END;

            REPEAT
              REPEAT
                SearchWordDetailTemp.RESET;
                SearchWordDetailTemp.SETRANGE("Search Word Entry No.",SearchWord."Entry No.");
                IF SearchWordDetailTemp.FIND('-') THEN BEGIN
                  SubNo := SearchWordDetailTemp."No.";
                  SearchWordDetailTemp.RESET;
                  SearchWordDetailTemp.SETRANGE("No.",SubNo);
                  SearchWordDetailTemp.DELETEALL;
                END;
                SearchWordDetailTemp.RESET;
                SearchWordDetailTemp.SETRANGE("Search Word Entry No.",SearchWord."Entry No.");
              UNTIL NOT SearchWordDetailTemp.FIND('-');
            UNTIL SearchWord.NEXT = 0;

            SearchWordDetailTemp.RESET;

            IF SearchWordDetailTemp.FIND('-') THEN
              REPEAT
                CSearch.PutQueryResult(
                  FORMAT(SearchWordDetailTemp."No."),
                  FORMAT(SearchWordDetailTemp."Sub No."),
                  SearchWordDetailTemp."Table Name",
                  SearchWordDetailTemp."Field No.",
                  SearchWordDetailTemp."Word Position");
              UNTIL SearchWordDetailTemp.NEXT = 0;
          END
          ELSE BEGIN
            SearchWordDetail.RESET;
            SearchWordDetail.SETFILTER("Table Name",AreaFilter);
            SearchWordDetail.SETFILTER("Contact Type",ContactTypeFilter);
            IF SearchWordDetail.FIND('-') THEN
              REPEAT
                CSearch.PutQueryResult(
                  FORMAT(SearchWordDetail."No."),
                  FORMAT(SearchWordDetail."Sub No."),
                  SearchWordDetail."Table Name",
                  SearchWordDetail."Field No.",
                  SearchWordDetail."Word Position");
              UNTIL SearchWordDetail.NEXT = 0;
          END;
        END
        ELSE BEGIN
          PassToCsearch(SearchWord);
          IF UseBlurry AND NOT (PhraseWordNo <> 0) THEN BEGIN
            IF (STRPOS(Atom,'*') = 0) AND (STRPOS(Atom,'?') = 0 ) THEN BEGIN
              SearchWord.RESET;
              IF BlurryKey1 <> '' THEN BEGIN
                SearchWord.SETFILTER(SearchWord.BlurryKey1,BlurryKey1);
                PassBlurrySetToCsearch(SearchWord);
              END;
              SearchWord.RESET;
              IF BlurryKey2 <> '' THEN BEGIN
                SearchWord.SETFILTER(SearchWord.BlurryKey2,BlurryKey2);
                PassBlurrySetToCsearch(SearchWord);
              END;
            END;
          END;
        END;
      CurrAtomNo := CurrAtomNo + 1;
      END;

      CSearch.PrepareSearchResult;

      NoOfResultRecords := CSearch.GetResultRecordCount;

      IF NoOfResultRecords <> 0 THEN BEGIN
        FOR i := 0 TO NoOfResultRecords - 1 DO BEGIN
          CSearch.GetResultRecord(
            i,ContactNo,SubNo,TableOptionNo);

          SearchResult.SETRANGE("Contact No.",ContactNo);
          IF NOT SearchResult.FIND('-') THEN BEGIN
            SearchResult.INIT;
            SearchResult."Line No." := i;
            SearchResult."Contact No." := ContactNo;
            IF Contact.GET(ContactNo) THEN
              SearchResult."Contact Name" := Contact.Name
            ELSE
              SearchResult."Contact Name" := Text009;
            SearchResult."No. of Records" := 1;
            SearchResult.INSERT;
          END
           ELSE
             BEGIN
               SearchResult."No. of Records" := SearchResult."No. of Records" + 1;
               SearchResult.MODIFY;
             END;
           SearchResultDetail.INIT;
           SearchResultDetail."Line No." := i;
           SearchResultDetail."Search Result Line No." := ContactNo;
           SearchResultDetail."No." := SubNo;
           CASE TableOptionNo OF
             0:
               BEGIN
                 SearchResultDetail."Table Name" := SearchResultDetail."Table Name"::Contact;
                 IF Contact.GET(SubNo) THEN
                   SearchResultDetail.Description := Contact.Name;
               END;
             1:
               BEGIN
                 SearchResultDetail."Table Name" := SearchResultDetail."Table Name"::"Interaction Log Entry";
                 IF EVALUATE(SubNoInt,SubNo) THEN
                   IF IntLogEntry.GET(SubNoInt) THEN
                     SearchResultDetail.Description := IntLogEntry.Description;
               END;
             2:
               BEGIN
                 SearchResultDetail."Table Name" := SearchResultDetail."Table Name"::"To-do";
                 IF Todo.GET(SubNo) THEN
                   SearchResultDetail.Description := Todo.Description;
               END;
             3:
               BEGIN
                 SearchResultDetail."Table Name" := SearchResultDetail."Table Name"::Opportunity;
                 IF Opportunity.GET(SubNo) THEN
                   SearchResultDetail.Description := Opportunity.Description;
               END;
             4:
               BEGIN
                 SearchResultDetail."Table Name" := SearchResultDetail."Table Name"::"Rlshp. Mgt. Comment Line";
                   IF EVALUATE(SubNoInt,SubNo) THEN
                     IF RlshpMgtCommentLine.GET(RlshpMgtCommentLine."Table Name"::Contact,
                       ContactNo,0,SubNoInt)
                   THEN
                     SearchResultDetail.Description := RlshpMgtCommentLine.Comment;
               END;
           END;
          SearchResultDetail.INSERT;
          SearchResult.RESET;
        END;
      END;
    END;

    PROCEDURE PassBlurrySetToCsearch@14(VAR SearchWord@1000 : Record 5117);
    BEGIN
      IF SearchWord.FIND('-') THEN BEGIN
        REPEAT
          SearchWordDetail.SETRANGE("Search Word Entry No.",SearchWord."Entry No.");
          IF SearchWordDetail.FIND('-') THEN
            REPEAT
              CSearch.PutBlurryResult(
                FORMAT(SearchWordDetail."No."),
                FORMAT(SearchWordDetail."Sub No."),
                SearchWordDetail."Table Name",
                SearchWordDetail."Field No.",
                SearchWordDetail."Word Position");
            UNTIL SearchWordDetail.NEXT = 0;
        UNTIL SearchWord.NEXT = 0;
      END;
    END;

    PROCEDURE PassToCsearch@13(VAR SearchWord@1000 : Record 5117);
    BEGIN
      IF SearchWord.FIND('-') THEN BEGIN
        REPEAT
          SearchWordDetail.SETRANGE("Search Word Entry No.",SearchWord."Entry No.");
           IF SearchWordDetail.FIND('-') THEN
             REPEAT
               CSearch.PutQueryResult(
                 FORMAT(SearchWordDetail."No."),
                 FORMAT(SearchWordDetail."Sub No."),
                 SearchWordDetail."Table Name",
                 SearchWordDetail."Field No.",
                 SearchWordDetail."Word Position");
             UNTIL SearchWordDetail.NEXT = 0;
        UNTIL SearchWord.NEXT = 0;
      END;
    END;

    PROCEDURE GenerateBlurryKeys@3(VAR SearchWord@1000 : Record 5117);
    BEGIN
      IF ISCLEAR(BlurrySearch) THEN
        CREATE(BlurrySearch);
      BlurrySearch.GenerateKey(
        SearchWord.Word,
        SearchWord.BlurryKey1,
        SearchWord.BlurryKey2);
    END;

    BEGIN
    END.
  }
}
