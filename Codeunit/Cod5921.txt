OBJECT Codeunit 5921 ServComponent-Copy from BOM
{
  OBJECT-PROPERTIES
  {
    Date=15/09/04;
    Time=12:00:00;
    Version List=NAVW14.00;
  }
  PROPERTIES
  {
    TableNo=5940;
    OnRun=BEGIN
            ServItem.GET(Rec."No.");
            ServItem.TESTFIELD("Item No.");

            ServItemComponent.LOCKTABLE;
            ServItemComponent.RESET;
            ServItemComponent.SETRANGE(Active,TRUE);
            ServItemComponent.SETRANGE("Parent Service Item No.",ServItem."No.");
            IF ServItemComponent.FIND('+') THEN
              LineNo := ServItemComponent."Line No."
            ELSE
              LineNo := 0;

            BOMComp.SETCURRENTKEY("Parent Item No.","Line No.");
            BOMComp.SETRANGE("Parent Item No.",ServItem."Item No.");
            BOMComp.SETRANGE(Type,BOMComp.Type::Item);
            IF BOMComp.FIND('-') THEN BEGIN
              REPEAT
                IF BOMComp."Quantity per" <> ROUND(BOMComp."Quantity per",1) THEN
                  ERROR(Text001,ServItem.TABLECAPTION,BOMComp.FIELDCAPTION("Quantity per"));
                FOR Index := 1 TO BOMComp."Quantity per" DO BEGIN
                  LineNo := LineNo + 10000;
                  Item.GET(BOMComp."No.");
                  CLEAR(ServItemComponent);
                  ServItemComponent.Active := TRUE;
                  ServItemComponent.INIT;
                  ServItemComponent."Parent Service Item No.":= ServItem."No.";
                  ServItemComponent."Line No." := LineNo;
                  ServItemComponent.Type := ServItemComponent.Type::Item;
                  ServItemComponent."No." := BOMComp."No.";
                  ServItemComponent."Variant Code" := BOMComp."Variant Code";
                  ServItemComponent."Date Installed" := ServItem."Installation Date";
                  ServItemComponent.Description := Item.Description;
                  ServItemComponent."Description 2" := Item."Description 2";
                  ServItemComponent."From Line No." := 0;
                  IF NOT ServItemComponent.INSERT THEN
                    ServItemComponent.MODIFY;
                END;
              UNTIL BOMComp.NEXT = 0;
            END ELSE
              ERROR(
                Text000,
                BOMComp.TABLECAPTION,ServItem.FIELDCAPTION("Item No."),ServItem."Item No.");
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 for %2 %3 cannot be found.;ESP=No se encontr¢ %1 para %2 %3.';
      ServItem@1001 : Record 5940;
      BOMComp@1002 : Record 90;
      ServItemComponent@1003 : Record 5941;
      Item@1004 : Record 27;
      LineNo@1005 : Integer;
      Index@1006 : Integer;
      Text001@1007 : TextConst 'ENU=You cannot copy the component list for this %1 from BOM. The %2 of one or more BOM components is not a whole number.;ESP=No es posible copiar de la LM la lista de componentes del %1. El valor %2 de al menos un componente de la LM no es un n£mero entero.';

    BEGIN
    END.
  }
}
