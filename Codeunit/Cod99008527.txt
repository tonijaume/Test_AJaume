OBJECT Codeunit 99008527 BizTalk Mail
{
  OBJECT-PROPERTIES
  {
    Date=17/12/01;
    Time=12:00:00;
    Version List=NAVW13.10;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      MAPIMessages@1000 : OCX "{20C62CAB-15DA-101B-B9A8-444553540000}:''";
      MAPISession@1001 : OCX "{20C62CA0-15DA-101B-B9A8-444553540000}:''";

    PROCEDURE NewMessage@2(ToName@1000 : Text[80];Subject@1001 : Text[250];Body@1002 : Text[250];AttachFileName@1003 : Text[250]);
    VAR
      BizTalkManagementSetup@1004 : Record 99008512;
    BEGIN
      MAPISession.DownLoadMail := FALSE;
      IF MAPISession.SessionID = 0 THEN BEGIN
        IF BizTalkManagementSetup.GET THEN
          IF BizTalkManagementSetup."E-Mail Profile Name" <> '' THEN BEGIN
            MAPISession.UserName := BizTalkManagementSetup."E-Mail Profile Name";
            MAPISession.LogonUI := FALSE;
          END;
        MAPISession.SignOn;
      END;
      MAPIMessages.SessionID := MAPISession.SessionID;

      MAPIMessages.Compose;

      SetNames(1,ToName);

      MAPIMessages.MsgSubject := Subject;

      IF AttachFileName = '' THEN
        MAPIMessages.MsgNoteText := Body
      ELSE BEGIN
        MAPIMessages.MsgNoteText := Body + ' ';
        MAPIMessages.AttachmentIndex := MAPIMessages.AttachmentCount;
        MAPIMessages.AttachmentType := 0;
        MAPIMessages.AttachmentPathName := AttachFileName;
        MAPIMessages.AttachmentPosition(STRLEN(Body));
      END;

      MAPIMessages.Action(3); // Send
    END;

    PROCEDURE SetNames@1(MessageType@1000 : Integer;ToName@1001 : Text[80]);
    VAR
      SepPosition@1002 : Integer;
      RecipIndex@1003 : Integer;
      Name@1004 : Text[80];
      NameStr@1005 : Text[80];
    BEGIN
      IF ToName <> '' THEN BEGIN
        RecipIndex := 0;
        NameStr := ToName;
        REPEAT
          SepPosition := STRPOS(NameStr,';');
          IF SepPosition > 0 THEN
            Name := COPYSTR(NameStr,1,SepPosition-1)
          ELSE
            Name := COPYSTR(NameStr,1);
          IF Name <> '' THEN BEGIN
            MAPIMessages.RecipIndex := RecipIndex;
            MAPIMessages.RecipType := MessageType;
            MAPIMessages.RecipDisplayName := Name;
            RecipIndex := RecipIndex + 1;
          END;
          NameStr := COPYSTR(NameStr,SepPosition+1);
        UNTIL SepPosition = 0;
        MAPIMessages.AddressResolveUI(FALSE);
        MAPIMessages.ResolveName;
      END;
    END;

    BEGIN
    END.
  }
}
