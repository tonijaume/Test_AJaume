OBJECT Codeunit 5055257 Liq. Jnl. - Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=13/02/07;
    Time=12:00:00;
    Version List=LIQEM5.00;
  }
  PROPERTIES
  {
    TableNo=5055256;
    OnRun=BEGIN
            LiqJnlLine.COPY(Rec);
            Code;
            Rec.COPY(LiqJnlLine);
          END;

  }
  CODE
  {
    VAR
      Text1140000@1140000 : TextConst 'ENU=cannot be filtered when posting recurring journals;ESP=no puede contener un filtro cuando se registra un diario peri¢dico';
      Text1140001@1140001 : TextConst 'ENU=Do you want to post the journal lines and print the posting report?;ESP=¨Confirma que desea registrar las l¡ns. de diario e imprimir el informe de reg.?';
      Text1140002@1140002 : TextConst 'ENU=There is nothing to post.;ESP=No hay nada que registrar.';
      Text1140003@1140003 : TextConst 'ENU=The journal lines were successfully posted.;ESP=Se han registrado correctamente las l¡neas del diario.';
      Text1140004@1140004 : TextConst 'ENU=The journal lines were successfully posted.;ESP=Se han registrado correctamente las l¡neas del diario.';
      Text1140005@1140005 : TextConst 'ENU=You are now in the %1 journal.;ESP=Se encuentra en el diario %1.';
      LiqJnlTemplate@1140006 : Record 5055254;
      LiqJnlLine@1140007 : Record 5055256;
      LiqReg@1140008 : Record 5055258;
      LiqJnlPostBatch@1140009 : Codeunit 5055254;
      TempJnlBatchName@1140010 : Code[10];

    LOCAL PROCEDURE Code@1140000();
    BEGIN
      WITH LiqJnlLine DO BEGIN
        LiqJnlTemplate.GET("Journal Template Name");
        LiqJnlTemplate.TESTFIELD("Posting Report ID");
        IF LiqJnlTemplate.Recurring AND (GETFILTER("Posting Date") <> '') THEN
          FIELDERROR("Posting Date",Text1140000);

        IF NOT CONFIRM(Text1140001) THEN
          EXIT;

        TempJnlBatchName := "Journal Batch Name";

        LiqJnlPostBatch.RUN(LiqJnlLine);

        IF LiqReg.GET("Line No.") THEN BEGIN
          LiqReg.SETRECFILTER;
          REPORT.RUN(LiqJnlTemplate."Posting Report ID",FALSE,FALSE,LiqReg);
        END;

        IF "Line No." = 0 THEN
          MESSAGE(Text1140002)
        ELSE
          IF TempJnlBatchName = "Journal Batch Name" THEN
            MESSAGE(Text1140003)
          ELSE
            MESSAGE(
              Text1140004 +
              Text1140005,
              "Journal Batch Name");

        IF NOT FIND('=><') OR (TempJnlBatchName <> "Journal Batch Name") THEN BEGIN
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          FILTERGROUP(0);
          "Line No." := 1;
        END;
      END;
    END;

    BEGIN
    END.
  }
}
