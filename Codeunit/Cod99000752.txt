OBJECT Codeunit 99000752 Check Routing Lines
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Circular reference in routing %1 when calculating %2. Counted sequences %3. Max. lines %4.;ESP=Referencia circular en ruta %1 al calcular %2. Secuencias contadas %3. M x. de l¡neas %4.';
      Text001@1001 : TextConst 'ENU=back;ESP=atr s';
      Text002@1002 : TextConst 'ENU=Actual number of termination processes in route %1 is %2. They should be 1. Check %3.;ESP=El n§ real de procesos de finalizaci¢n en ruta %1 es %2. Deber¡a ser 1. Compruebe %3.';
      Text003@1003 : TextConst 'ENU=Actual number of start processes in route %1 is %2. They should be 1. Check %3.;ESP=El n§ real de procesos de inicio en ruta %1 es %2. Deber¡a ser 1. Compruebe %3';
      Text004@1004 : TextConst 'ENU=Not all routing lines are sequenced backwards on routing %1. Check %2.;ESP=No todas las l¡neas de ruta se ordenan hacia atr s en ruta %1. Compruebe %2.';
      Text005@1005 : TextConst 'ENU=Not all routing lines are sequenced forward on routing %1. Check %2.;ESP=No todas las l¡neas ruta se ordenan hacia adelante en ruta %1.Compruebe %2.';
      Text006@1006 : TextConst 'ENU=Previous operations for %1 cannot be found.;ESP=No se encontraron operaciones anteriores para %1.';
      Text007@1007 : TextConst 'ENU=Next operations for %1 cannot be found.;ESP=No se encontraron operaciones posteriores para %1.';
      ErrList@1008 : Text[50];

    LOCAL PROCEDURE ErrorInRouting@1(RoutingCode@1000 : Code[20];Direction@1001 : Text[20];ActualSequence@1002 : Integer;MaxSequences@1003 : Integer);
    BEGIN
      ERROR(
        Text000,
        RoutingCode,
        Direction,
        ActualSequence,
        MaxSequences);
    END;

    LOCAL PROCEDURE InsertInErrList@8(RtngLine@1000 : Record 99000764);
    BEGIN
      IF (STRLEN(ErrList) + STRLEN(RtngLine."Operation No.") + 1) > MAXSTRLEN(ErrList) THEN
        EXIT;

      IF STRLEN(ErrList) > 0 THEN
        ErrList := ErrList + ',' + RtngLine."Operation No."
      ELSE
        ErrList := RtngLine."Operation No.";
    END;

    PROCEDURE SetNextOperations@10(RtngHeader@1000 : Record 99000763;VersionCode@1001 : Code[10]);
    VAR
      RtngLine@1002 : Record 99000764;
      RtngLine2@1003 : Record 99000764;
    BEGIN
      RtngLine.SETRANGE("Routing No.",RtngHeader."No.");
      RtngLine.SETRANGE("Version Code",VersionCode);
      RtngLine.MODIFYALL("Next Operation No.",'');

      IF RtngLine.FIND('-') THEN
        REPEAT
          RtngLine2 := RtngLine;
          RtngLine2.SETRANGE("Routing No.",RtngHeader."No.");
          RtngLine2.SETRANGE("Version Code",VersionCode);
          IF RtngLine2.FIND('>') THEN BEGIN
            RtngLine."Next Operation No." := RtngLine2."Operation No.";
            RtngLine.MODIFY;
          END;
        UNTIL RtngLine.NEXT = 0;
    END;

    LOCAL PROCEDURE CalcPreviousOperations@9(RtngHeader@1000 : Record 99000763;VersionCode@1001 : Code[10]);
    VAR
      RtngLine@1002 : Record 99000764;
      RtngLine2@1003 : Record 99000764;
    BEGIN
      RtngLine.SETRANGE("Routing No.",RtngHeader."No.");
      RtngLine.SETRANGE("Version Code",VersionCode);
      RtngLine.MODIFYALL("Previous Operation No.",'');

      IF RtngLine.FIND('-') THEN
        REPEAT
          IF RtngLine."Next Operation No." <> '' THEN BEGIN
            RtngLine2.SETRANGE("Routing No.",RtngHeader."No.");
            RtngLine2.SETRANGE("Version Code",VersionCode);
            RtngLine2.SETFILTER("Operation No.",RtngLine."Next Operation No.");
            IF RtngLine2.FIND('-') THEN
              REPEAT
                IF RtngLine2."Previous Operation No." <> '' THEN
                  RtngLine2."Previous Operation No." :=
                    RtngLine2."Previous Operation No." + '|';
                RtngLine2."Previous Operation No." :=
                  RtngLine2."Previous Operation No." +
                  RtngLine."Operation No.";
                RtngLine2.MODIFY;
              UNTIL RtngLine2.NEXT = 0;
          END;
        UNTIL RtngLine.NEXT = 0;
    END;

    LOCAL PROCEDURE SetRtngLineSequenceBack@6(RoutingType@1000 : 'Serial,Parallel';VAR RtngLine@1001 : Record 99000764;Maxsequences@1002 : Integer;Actsequences@1003 : Integer);
    VAR
      RtngLine2@1004 : Record 99000764;
      SequenceNo@1005 : Integer;
    BEGIN
      IF RoutingType = RoutingType::Parallel THEN BEGIN
        IF (Actsequences - 1) > Maxsequences THEN
          ErrorInRouting(
            RtngLine."Routing No.",
            Text001,
            Actsequences,
            Maxsequences);

        IF RtngLine."Sequence No. (Backward)" < Actsequences THEN BEGIN
          RtngLine."Sequence No. (Backward)" := Actsequences;
          RtngLine.MODIFY;
        END;

        RtngLine2.SETRANGE("Routing No.",RtngLine."Routing No.");
        RtngLine2.SETRANGE("Version Code",RtngLine."Version Code");
        IF RtngLine."Previous Operation No." <> '' THEN BEGIN
          RtngLine2.SETFILTER("Operation No.",RtngLine."Previous Operation No.");
          IF RtngLine2.FIND('-') THEN
            REPEAT
              SetRtngLineSequenceBack(
                RoutingType,
                RtngLine2,
                Maxsequences,
                Actsequences + 1);
            UNTIL RtngLine2.NEXT = 0;
        END;
      END ELSE BEGIN
        SequenceNo := 1;
        RtngLine2.SETRANGE("Routing No.",RtngLine."Routing No.");
        RtngLine2.SETRANGE("Version Code",RtngLine."Version Code");
        IF RtngLine2.FIND('+') THEN
          REPEAT
            RtngLine2."Sequence No. (Backward)" := SequenceNo;
            RtngLine2.MODIFY;
            SequenceNo := SequenceNo + 1;
          UNTIL RtngLine2.NEXT(-1) = 0;
      END;
    END;

    PROCEDURE SetRtngLineSequenceForward@7(RoutingType@1000 : 'Serial,Parallel';RtngLine@1001 : Record 99000764;MaxSequences@1002 : Integer;ActSequences@1003 : Integer);
    VAR
      RtngLine2@1004 : Record 99000764;
      SequenceNo@1005 : Integer;
    BEGIN
      IF RoutingType = RoutingType::Parallel THEN BEGIN
        IF ActSequences > MaxSequences THEN
          ErrorInRouting(
            RtngLine."Routing No.",
            Text001,
            ActSequences,
            MaxSequences);

        IF RtngLine."Sequence No. (Forward)" < ActSequences THEN BEGIN
          RtngLine."Sequence No. (Forward)" := ActSequences;
          RtngLine.MODIFY;
        END;

        RtngLine2.SETRANGE("Routing No.",RtngLine."Routing No.");
        RtngLine2.SETRANGE("Version Code",RtngLine."Version Code");
        IF RtngLine."Next Operation No." <> '' THEN BEGIN
          RtngLine2.SETFILTER("Operation No.",RtngLine."Next Operation No.");
          IF RtngLine2.FIND('-') THEN
            REPEAT
              SetRtngLineSequenceForward(
                RoutingType,
                RtngLine2,
                MaxSequences,
                ActSequences + 1);
            UNTIL RtngLine2.NEXT = 0;
        END;

      END ELSE BEGIN
        SequenceNo := 1;
        RtngLine2.SETRANGE("Routing No.",RtngLine."Routing No.");
        RtngLine2.SETRANGE("Version Code",RtngLine."Version Code");
        IF RtngLine2.FIND('-') THEN
          REPEAT
            RtngLine2."Sequence No. (Forward)" := SequenceNo;
            RtngLine2.MODIFY;
            SequenceNo := SequenceNo + 1;
          UNTIL RtngLine2.NEXT = 0;
      END;
    END;

    PROCEDURE CalcSequenceBack@2(RtngHeader@1000 : Record 99000763;VersionCode@1001 : Code[10]);
    VAR
      RtngLine@1002 : Record 99000764;
      MaxSeq@1003 : Integer;
    BEGIN
      RtngLine.SETRANGE("Routing No.",RtngHeader."No.");
      RtngLine.SETRANGE("Version Code",VersionCode);
      IF RtngLine.FIND('-') THEN
        REPEAT
          RtngLine."Sequence No. (Backward)" := 0;
          RtngLine."Fixed Scrap Qty. (Accum.)" := 0;
          RtngLine."Scrap Factor % (Accumulated)" := 0;
          RtngLine.MODIFY;
        UNTIL RtngLine.NEXT = 0;

      MaxSeq := RtngLine.COUNT;

      RtngLine.SETFILTER("Next Operation No.",'%1','');
      RtngLine.FIND('-');
      SetRtngLineSequenceBack(RtngHeader.Type,RtngLine,MaxSeq,1);
    END;

    LOCAL PROCEDURE CalcSequenceForward@3(RtngHeader@1000 : Record 99000763;VersionCode@1001 : Code[10]);
    VAR
      RtngLine@1002 : Record 99000764;
      MaxSeq@1003 : Integer;
    BEGIN
      RtngLine.SETRANGE("Routing No.",RtngHeader."No.");
      RtngLine.SETRANGE("Version Code",VersionCode);
      RtngLine.MODIFYALL("Sequence No. (Forward)",0);

      MaxSeq := RtngLine.COUNT;

      RtngLine.SETFILTER("Previous Operation No.",'%1','');
      RtngLine.FIND('-');
      SetRtngLineSequenceForward(RtngHeader.Type,RtngLine,MaxSeq,1);
    END;

    PROCEDURE NeedsCalculation@11(RtngHeader@1000 : Record 99000763;VersionCode@1001 : Code[10]) : Boolean;
    VAR
      RtngLine@1002 : Record 99000764;
    BEGIN
      RtngLine.SETRANGE("Routing No.",RtngHeader."No.");
      RtngLine.SETRANGE("Version Code",VersionCode);
      RtngLine.SETRANGE(Recalculate,TRUE);
      EXIT(RtngLine.FIND('-'));
    END;

    PROCEDURE Calculate@4(RtngHeader@1000 : Record 99000763;VersionCode@1001 : Code[10]);
    VAR
      RtngVersion@1002 : Record 99000786;
      RtngLine@1003 : Record 99000764;
      RtngLine2@1004 : Record 99000764;
      CalcScrapFactor@1005 : Decimal;
      CalcScrapQty@1006 : Decimal;
      ScrapFactorThis@1007 : Decimal;
      ScrapQtyThis@1008 : Decimal;
    BEGIN
      RtngLine.SETCURRENTKEY("Routing No.","Version Code","Sequence No. (Backward)");
      RtngLine.SETRANGE("Routing No.",RtngHeader."No.");
      RtngLine.SETRANGE("Version Code",VersionCode);
      IF RtngLine.ISEMPTY THEN
        EXIT;

      IF VersionCode <> '' THEN BEGIN
        RtngVersion.GET(RtngHeader."No.",VersionCode);
        RtngHeader.Type := RtngVersion.Type;
      END;

      IF RtngHeader.Type = RtngHeader.Type::Serial THEN
        SetNextOperations(RtngHeader,VersionCode);

      CalcPreviousOperations(RtngHeader,VersionCode);
      CalcSequenceBack(RtngHeader,VersionCode);
      CalcSequenceForward(RtngHeader,VersionCode);

      IF RtngLine.FIND('-') THEN
        REPEAT
          IF RtngLine."Next Operation No." <> '' THEN BEGIN
            RtngLine2.SETRANGE("Routing No.",RtngLine."Routing No.");
            RtngLine2.SETRANGE("Version Code",VersionCode);
            RtngLine2.SETFILTER("Operation No.",RtngLine."Next Operation No.");
            CalcScrapFactor := 0;
            CalcScrapQty := 0;
            IF RtngLine2.FIND('-') THEN
              REPEAT
                ScrapFactorThis :=
                  RtngLine2."Scrap Factor % (Accumulated)";
                ScrapQtyThis := ROUND(RtngLine2."Fixed Scrap Qty. (Accum.)",0.00001);
                IF CalcScrapFactor < ScrapFactorThis THEN
                  CalcScrapFactor := ScrapFactorThis;
                IF CalcScrapQty < ScrapQtyThis THEN
                  CalcScrapQty := ScrapQtyThis;
              UNTIL RtngLine2.NEXT = 0;
          END;
          IF CalcScrapFactor <> 0 THEN BEGIN
            IF RtngLine."Scrap Factor %" <> 0 THEN
              CalcScrapFactor :=
                ROUND(
                  (1 + CalcScrapFactor) *
                  (1 + RtngLine."Scrap Factor %" / 100),0.00001) - 1;
          END ELSE
            CalcScrapFactor :=
              ROUND(1 + RtngLine."Scrap Factor %" / 100,0.00001) - 1;
          CalcScrapQty := CalcScrapQty + RtngLine."Fixed Scrap Quantity";
          RtngLine."Fixed Scrap Qty. (Accum.)" := CalcScrapQty;
          RtngLine."Scrap Factor % (Accumulated)" := CalcScrapFactor;
          RtngLine.MODIFY;
        UNTIL RtngLine.NEXT = 0;

      RtngLine.MODIFYALL(Recalculate,FALSE);
      Check(RtngHeader,VersionCode);
    END;

    PROCEDURE Check@5(RtngHeader@1000 : Record 99000763;VersionCode@1001 : Code[10]);
    VAR
      RtngLine@1002 : Record 99000764;
      RtngLine2@1003 : Record 99000764;
      NoOfProcesses@1004 : Integer;
    BEGIN
      ErrList := '';

      RtngLine.SETRANGE("Routing No.",RtngHeader."No.");
      RtngLine.SETRANGE("Version Code",VersionCode);

      RtngLine.SETFILTER("Next Operation No.",'%1','');

      NoOfProcesses := RtngLine.COUNT;
      IF NoOfProcesses <> 1 THEN BEGIN
        REPEAT
          InsertInErrList(RtngLine);
        UNTIL RtngLine.NEXT = 0;
        ERROR(
          Text002,
          RtngHeader."No.",
          NoOfProcesses,
          ErrList);
      END;

      RtngLine.SETFILTER("Previous Operation No.",'%1','');
      RtngLine.SETRANGE("Next Operation No.");
      NoOfProcesses := RtngLine.COUNT;
      IF NoOfProcesses <> 1 THEN BEGIN
        REPEAT
          InsertInErrList(RtngLine);
        UNTIL RtngLine.NEXT = 0;
        ERROR(
          Text003,
          RtngHeader."No.",
          NoOfProcesses,
          ErrList);
      END;
      RtngLine.SETRANGE("Previous Operation No.");

      RtngLine.SETRANGE("Sequence No. (Backward)",0);
      IF RtngLine.FIND('-') THEN BEGIN
        REPEAT
          InsertInErrList(RtngLine);
        UNTIL RtngLine.NEXT = 0;
        ERROR(
          Text004,
          RtngLine."Routing No.",
          ErrList);
      END;
      RtngLine.SETRANGE("Sequence No. (Backward)");

      RtngLine.SETRANGE("Sequence No. (Forward)",0);
      IF RtngLine.FIND('-') THEN BEGIN
        REPEAT
          InsertInErrList(RtngLine);
        UNTIL RtngLine.NEXT = 0;
        ERROR(
          Text005,
          RtngLine."Routing No.",
          ErrList);
      END;
      RtngLine.SETRANGE("Sequence No. (Forward)");

      RtngLine.SETCURRENTKEY("Routing No.","Version Code","Sequence No. (Backward)");
      RtngLine.SETFILTER("Previous Operation No.",'<>%1','');

      IF RtngLine.FIND('-') THEN
        REPEAT
          RtngLine2.SETRANGE("Routing No.",RtngLine."Routing No.");
          RtngLine2.SETRANGE("Version Code",VersionCode);
          RtngLine2.SETFILTER("Operation No.",RtngLine."Previous Operation No.");
          IF NOT RtngLine2.FIND('-') THEN
            ERROR(
              Text006,
              RtngLine."Routing No.");
        UNTIL RtngLine.NEXT = 0;

      RtngLine.SETCURRENTKEY("Routing No.","Version Code","Sequence No. (Forward)");
      RtngLine.SETFILTER("Next Operation No.",'<>%1','');
      RtngLine.SETRANGE("Operation No.");

      IF RtngLine.FIND('-') THEN
        REPEAT
          RtngLine2.SETRANGE("Routing No.",RtngLine."Routing No.");
          RtngLine2.SETRANGE("Version Code",VersionCode);
          RtngLine2.SETFILTER("Operation No.",RtngLine."Next Operation No.");
          IF NOT RtngLine2.FIND('-') THEN
            ERROR(
              Text007,
              RtngLine."Routing No.");
        UNTIL RtngLine.NEXT = 0;
    END;

    BEGIN
    END.
  }
}
