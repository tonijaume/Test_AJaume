OBJECT Codeunit 111 Vend. Entry-SetAppl.ID
{
  OBJECT-PROPERTIES
  {
    Date=02/10/12;
    Time=12:00:00;
    Modified=Yes;
    Version List=AIC2009,NAVW15.00,NAVES4.00;
  }
  PROPERTIES
  {
    Permissions=TableData 25=imd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text1100100@1100001 : TextConst 'ENU=%1 cannot be applied, since it is included in a payment order.;ESP=%1 no se puede liquidar, ya que est  incluido en una orden pago.';
      Text1100101@1100002 : TextConst 'ENU=" Remove it from its payment order and try again.";ESP=" B¢rrelo de la orden pago e int‚ntelo de nuevo."';
      ApplicationManagement@1100003 : Codeunit 1;
      Doc@1100000 : Record 7000002;
      VendEntryApplID@1000 : Code[20];
      Text001@1103355000 : TextConst 'ESP=Este movimiento esta marcado para liquidar, desea desmarcarlo.';

    PROCEDURE SetApplId@1(VAR VendLedgEntry@1000 : Record 25;ApplyingVendLedgEntry@1001 : Record 25;AppliedAmount@1004 : Decimal;PmtDiscAmount@1003 : Decimal;AppliesToID@1005 : Code[20]);
    BEGIN
      VendLedgEntry.LOCKTABLE;
      IF VendLedgEntry.FIND('-') THEN BEGIN
        // Make Applies-to ID
        IF VendLedgEntry."Applies-to ID" <> '' THEN BEGIN
          // $001
          IF CONFIRM(Text001) THEN
          VendEntryApplID := ''
          ELSE
            EXIT;
        END
        ELSE BEGIN
          VendEntryApplID := AppliesToID;
          IF VendEntryApplID = '' THEN BEGIN
            VendEntryApplID := USERID;
            IF VendEntryApplID = '' THEN
              VendEntryApplID := '***';
          END;
        END;

        // Set Applies-to ID
        REPEAT
          VendLedgEntry.TESTFIELD(Open,TRUE);
          IF VendLedgEntry."Document Situation" = VendLedgEntry."Document Situation"::"Posted BG/PO" THEN
            ERROR(
              Text1100100,
              VendLedgEntry.Description);
          IF (((VendLedgEntry."Document Type" = VendLedgEntry."Document Type"::Bill) OR
          (VendLedgEntry."Document Type" = VendLedgEntry."Document Type"::Invoice))) AND
          ApplicationManagement.AccessToCartera THEN
            IF Doc.GET(Doc.Type::Payable,VendLedgEntry."Entry No.") THEN
              IF Doc."Bill Gr./Pmt. Order No." <> '' THEN
                ERROR(
                  Text1100100 +
                  Text1100101,
                  VendLedgEntry.Description);
          VendLedgEntry."Applies-to ID" := VendEntryApplID;
          IF VendLedgEntry."Applies-to ID" = '' THEN BEGIN
            VendLedgEntry."Accepted Pmt. Disc. Tolerance" := FALSE;
            VendLedgEntry."Accepted Payment Tolerance" := 0;
          END;
          //Set Amount to Apply

          IF ((VendLedgEntry."Amount to Apply" <> 0) AND (VendEntryApplID = '')) OR
            (VendEntryApplID = '')
          THEN
            VendLedgEntry."Amount to Apply" := 0
          ELSE
            IF VendLedgEntry."Amount to Apply" = 0 THEN BEGIN
              VendLedgEntry.CALCFIELDS("Remaining Amount");
              VendLedgEntry."Amount to Apply" := VendLedgEntry."Remaining Amount"
            END;

          IF VendLedgEntry."Entry No." = ApplyingVendLedgEntry."Entry No." THEN
            VendLedgEntry."Applying Entry" := ApplyingVendLedgEntry."Applying Entry";
          VendLedgEntry.MODIFY;
        UNTIL VendLedgEntry.NEXT = 0;
      END;
    END;

    BEGIN
    {
      // INCOMING

      $001 Si el campo Liquidar por ID esta rellenado, se advierte al usuario de que se va a vaciar
    }
    END.
  }
}
