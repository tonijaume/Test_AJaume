OBJECT Codeunit 99555 CNT - Renumerar fras venta reg
{
  OBJECT-PROPERTIES
  {
    Date=15/05/13;
    Time=[ 6:41:45];
    Modified=Yes;
    Version List=AJS,TOOL;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            RenumeraAbonoVenta('V-ABO-01028', 'VNC0000009');

            MESSAGE('Proceso finalizado');
          END;

  }
  CODE
  {
    VAR
      rDimDoc@1100217007 : Record 359;
      rDimDoc2@1100217009 : Record 359;
      rMovCont@1100217004 : Record 17;
      rMovCli@1100217005 : Record 21;
      rDetCli@1100217006 : Record 379;
      rMovIva@1100217008 : Record 254;

    PROCEDURE RenumeraFacturaVenta@1100217000(pwDocumento@1100217000 : Code[20];pwNuevoDocumento@1100217001 : Code[20]);
    VAR
      rCabFra@1100217005 : Record 112;
      rCabFra2@1100217004 : Record 112;
      rLinFra@1100217003 : Record 113;
      rLinFra2@1100217002 : Record 113;
    BEGIN
      // RenumeraFacturaVenta

      IF NOT rCabFra.GET(pwDocumento) THEN
        ERROR('No existe la factura venta %1', pwDocumento);

      //. Cabecera
      rCabFra2 := rCabFra;
      rCabFra2."No." := pwNuevoDocumento;
      rCabFra2.INSERT;

      rCabFra.DELETE;

      //. Lineas de factura
      rLinFra.SETRANGE("Document No.", pwDocumento);
      rLinFra.FINDSET;
      REPEAT
        rLinFra2 := rLinFra;
        rLinFra2."Document No." := pwNuevoDocumento;
        rLinFra2.INSERT;
      UNTIL rLinFra.NEXT = 0;
      rLinFra.DELETEALL;

      //. Dimensiones del documento
      rDimDoc.SETRANGE("Table ID"    , 112, 113);
      rDimDoc.SETRANGE("Document No.", pwDocumento);
      IF rDimDoc.FINDSET THEN
      BEGIN
        REPEAT
          rDimDoc2 := rDimDoc;
          rDimDoc2."Document No." := pwNuevoDocumento;
          rDimDoc2.INSERT;
        UNTIL rDimDoc.NEXT = 0;
        rDimDoc.DELETEALL;
      END;

      //. Movimientos de contabilidad
      rMovCont.SETCURRENTKEY("Document No.");
      rMovCont.SETRANGE("Document No.", pwDocumento);
      rMovCont.FINDSET;
      rMovCont.MODIFYALL("Document No.", pwNuevoDocumento);

      //. Movimientos de cliente
      rMovCli.SETCURRENTKEY("Document No.");
      rMovCli.SETRANGE("Document No.", pwDocumento);
      rMovCli.FINDSET;
      REPEAT
        rDetCli.SETCURRENTKEY("Cust. Ledger Entry No.");
        rDetCli.SETRANGE("Cust. Ledger Entry No.", rMovCli."Entry No.");
        rDetCli.FINDSET;
        rDetCli.MODIFYALL("Document No.", pwNuevoDocumento);
      UNTIL rMovCli.NEXT = 0;
      rMovCli.MODIFYALL("Document No.", pwNuevoDocumento);

      //. Movimientos de IVA
      rMovIva.SETCURRENTKEY("Document No.");
      rMovIva.SETRANGE("Document No.", pwDocumento);
      IF rMovIva.FINDSET THEN
        rMovIva.MODIFYALL("Document No.", pwNuevoDocumento);
    END;

    PROCEDURE RenumeraAbonoVenta@1100217001(pwDocumento@1100217001 : Code[20];pwNuevoDocumento@1100217000 : Code[20]);
    VAR
      rCabAbo@1100217005 : Record 114;
      rCabAbo2@1100217004 : Record 114;
      rLinAbo@1100217003 : Record 115;
      rLinAbo2@1100217002 : Record 115;
    BEGIN
      // RenumeraAbonoVenta

      IF NOT rCabAbo.GET(pwDocumento) THEN
        ERROR('No existe el abono venta %1', pwDocumento);

      //. Cabecera
      rCabAbo2 := rCabAbo;
      rCabAbo2."No." := pwNuevoDocumento;
      rCabAbo2.INSERT;

      rCabAbo.DELETE;

      //. Lineas de factura
      rLinAbo.SETRANGE("Document No.", pwDocumento);
      rLinAbo.FINDSET;
      REPEAT
        rLinAbo2 := rLinAbo;
        rLinAbo2."Document No." := pwNuevoDocumento;
        rLinAbo2.INSERT;
      UNTIL rLinAbo.NEXT = 0;
      rLinAbo.DELETEALL;

      //. Dimensiones del documento
      rDimDoc.SETRANGE("Table ID"    , 114, 115);
      rDimDoc.SETRANGE("Document No.", pwDocumento);
      IF rDimDoc.FINDSET THEN
      BEGIN
        REPEAT
          rDimDoc2 := rDimDoc;
          rDimDoc2."Document No." := pwNuevoDocumento;
          rDimDoc2.INSERT;
        UNTIL rDimDoc.NEXT = 0;
        rDimDoc.DELETEALL;
      END;

      //. Movimientos de contabilidad
      rMovCont.SETCURRENTKEY("Document No.");
      rMovCont.SETRANGE("Document No.", pwDocumento);
      rMovCont.FINDSET;
      rMovCont.MODIFYALL("Document No.", pwNuevoDocumento);

      //. Movimientos de cliente
      rMovCli.SETCURRENTKEY("Document No.");
      rMovCli.SETRANGE("Document No.", pwDocumento);
      rMovCli.FINDSET;
      REPEAT
        rDetCli.SETCURRENTKEY("Cust. Ledger Entry No.");
        rDetCli.SETRANGE("Cust. Ledger Entry No.", rMovCli."Entry No.");
        rDetCli.FINDSET;
        rDetCli.MODIFYALL("Document No.", pwNuevoDocumento);
      UNTIL rMovCli.NEXT = 0;
      rMovCli.MODIFYALL("Document No.", pwNuevoDocumento);

      //. Movimientos de IVA
      rMovIva.SETCURRENTKEY("Document No.");
      rMovIva.SETRANGE("Document No.", pwDocumento);
      IF rMovIva.FINDSET THEN
        rMovIva.MODIFYALL("Document No.", pwNuevoDocumento);
    END;

    BEGIN
    END.
  }
}
