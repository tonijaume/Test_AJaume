OBJECT Codeunit 99000768 Production BOM-Copy
{
  OBJECT-PROPERTIES
  {
    Date=08/06/01;
    Time=12:00:00;
    Version List=NAVW13.00;
  }
  PROPERTIES
  {
    TableNo=99000771;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=The %1 cannot be copied to itself.;ESP=%1 no puede copiarse en s¡ misma.';
      Text001@1001 : TextConst 'ENU=%1 on %2 %3 must not be %4;ESP=%1 en %2 %3 no debe ser %4';
      Text002@1002 : TextConst 'ENU=%1 on %2 %3 %4 must not be %5;ESP=%1 en %2 %3 %4 no debe ser %5';

    PROCEDURE CopyBOM@1(BOMHeaderNo@1000 : Code[20];FromVersionCode@1001 : Code[10];CurrentBOMHeader@1002 : Record 99000771;ToVersionCode@1003 : Code[10]);
    VAR
      FromProdBOMComponent@1004 : Record 99000772;
      ToProdBOMComponent@1005 : Record 99000772;
      FromProdBOMCompComment@1006 : Record 99000776;
      ToProdBOMCompComment@1007 : Record 99000776;
      ProdBomVersion@1008 : Record 99000779;
    BEGIN
      IF (CurrentBOMHeader."No." = BOMHeaderNo) AND
        (FromVersionCode = ToVersionCode)
      THEN
        ERROR(Text000,CurrentBOMHeader.TABLECAPTION);

      IF ToVersionCode = '' THEN BEGIN
        IF CurrentBOMHeader.Status = CurrentBOMHeader.Status::Certified THEN
          ERROR(
            Text001,
            CurrentBOMHeader.FIELDCAPTION(Status),
            CurrentBOMHeader.TABLECAPTION,
            CurrentBOMHeader."No.",
            CurrentBOMHeader.Status);
      END ELSE BEGIN
        ProdBomVersion.GET(
          CurrentBOMHeader."No.",ToVersionCode);
        IF ProdBomVersion.Status = ProdBomVersion.Status::Certified THEN
          ERROR(
            Text002,
            ProdBomVersion.FIELDCAPTION(Status),
            ProdBomVersion.TABLECAPTION,
            ProdBomVersion."Production BOM No.",
            ProdBomVersion."Version Code",
            ProdBomVersion.Status);
      END;

      ToProdBOMComponent.SETRANGE("Production BOM No.",CurrentBOMHeader."No.");
      ToProdBOMComponent.SETRANGE("Version Code",ToVersionCode);
      ToProdBOMComponent.DELETEALL;

      ToProdBOMCompComment.SETRANGE("Production BOM No.",CurrentBOMHeader."No.");
      ToProdBOMCompComment.SETRANGE("Version Code",ToVersionCode);
      ToProdBOMCompComment.DELETEALL;

      FromProdBOMComponent.SETRANGE("Production BOM No.",BOMHeaderNo);
      FromProdBOMComponent.SETRANGE("Version Code",FromVersionCode);

      IF FromProdBOMComponent.FIND('-') THEN
        REPEAT
          ToProdBOMComponent := FromProdBOMComponent;
          ToProdBOMComponent."Production BOM No." := CurrentBOMHeader."No.";
          ToProdBOMComponent."Version Code" := ToVersionCode;
          ToProdBOMComponent.INSERT;
        UNTIL FromProdBOMComponent.NEXT = 0;

      FromProdBOMCompComment.SETRANGE("Production BOM No.",BOMHeaderNo);
      FromProdBOMCompComment.SETRANGE("Version Code",FromVersionCode);

      IF FromProdBOMCompComment.FIND('-') THEN
        REPEAT
          ToProdBOMCompComment := FromProdBOMCompComment;
          ToProdBOMCompComment."Production BOM No." := CurrentBOMHeader."No.";
          ToProdBOMCompComment."Version Code" := ToVersionCode;
          ToProdBOMCompComment.INSERT;
        UNTIL FromProdBOMCompComment.NEXT = 0;
    END;

    PROCEDURE CopyFromVersion@2(VAR ProdBOMVersionList2@1000 : Record 99000779);
    VAR
      BOMHeader@1001 : Record 99000771;
      OldProdBOMVersionList@1002 : Record 99000779;
    BEGIN
      OldProdBOMVersionList := ProdBOMVersionList2;

      BOMHeader."No." := ProdBOMVersionList2."Production BOM No.";
      IF FORM.RUNMODAL(0,ProdBOMVersionList2) = ACTION::LookupOK THEN BEGIN
        IF OldProdBOMVersionList.Status = OldProdBOMVersionList.Status::Certified THEN
          ERROR(
            Text002,
            OldProdBOMVersionList.FIELDCAPTION(Status),
            OldProdBOMVersionList.TABLECAPTION,
            OldProdBOMVersionList."Production BOM No.",
            OldProdBOMVersionList."Version Code",
            OldProdBOMVersionList.Status);
        CopyBOM(BOMHeader."No.",ProdBOMVersionList2."Version Code",BOMHeader,OldProdBOMVersionList."Version Code");
      END;

      ProdBOMVersionList2 := OldProdBOMVersionList;
    END;

    BEGIN
    END.
  }
}
