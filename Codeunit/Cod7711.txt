OBJECT Codeunit 7711 Miniform Whse. Activity Line
{
  OBJECT-PROPERTIES
  {
    Date=25/02/04;
    Time=12:00:00;
    Version List=NAVW13.70.01;
  }
  PROPERTIES
  {
    TableNo=7700;
    OnRun=VAR
            MiniformMgmt@1000 : Codeunit 7702;
          BEGIN
            MiniformMgmt.Initialize(MiniformHeader,Rec,DOMxmlin,ReturnedNode,RootNode,XMLDOMMgmt,ADCSCommunication,LoginID,CurrentCode,
            StackCode,LocationFilter);

            IF Code <> CurrentCode THEN
              PrepareData
            ELSE
              ProcessInput;

            CLEAR(DOMxmlin);
          END;

  }
  CODE
  {
    VAR
      Text000@1009 : TextConst 'ENU=Function not Found;ESP=Funci¢n no encontrada';
      MiniformHeader@1015 : Record 7700;
      DOMxmlin@1011 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 3.0:{F6D90F11-9C73-11D3-B32E-00C04F990BB4}:'Microsoft XML, v3.0'.DOMDocument";
      ReturnedNode@1012 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 3.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v3.0'.IXMLDOMNode";
      RootNode@1010 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 3.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v3.0'.IXMLDOMNode";
      XMLDOMMgmt@1007 : Codeunit 99008516;
      ADCSCommunication@1003 : Codeunit 7701;
      ADCSNasStartup@1024 : Codeunit 7700;
      LoginID@1004 : Text[250];
      Remark@1002 : Text[250];
      LocationFilter@1000 : Text[250];
      Text004@1021 : TextConst 'ENU=Invalid %1.;ESP=%1 no valido.';
      Text006@1001 : TextConst 'ENU=No input Node found;ESP=No se ha encontrado el Nodo de entrada';
      Text007@1014 : TextConst 'ENU=Record not found;ESP=No se ha encontrado ning£n registro';
      Text008@1013 : TextConst 'ENU=End of Document;ESP=Fin del documento';
      Text009@1019 : TextConst 'ENU=Qty. does not match;ESP=La cantidad no coincide';
      Text011@1017 : TextConst 'ENU=Invalid Quantity;ESP=Cantidad no v lida';
      CurrentCode@1006 : Text[250];
      StackCode@1008 : Text[250];
      ActiveInputField@1026 : Integer;
      Text012@1027 : TextConst 'ENU=No Lines available;ESP=No hay l¡neas disponibles';
      RecRef@1005 : RecordRef;

    LOCAL PROCEDURE ProcessInput@10();
    VAR
      TableNo@1008 : Integer;
      RecordId@1004 : RecordID;
      FldNo@1000 : Integer;
      WhseActivityLine@1002 : Record 5767;
      FuncGroup@1001 : Record 7702;
      TextValue@1007 : Text[250];
    BEGIN
      IF XMLDOMMgmt.FindNode(RootNode,'Header/Input',ReturnedNode) THEN
        TextValue := ReturnedNode.text
      ELSE
        ERROR(Text006);

      EVALUATE(TableNo,ADCSCommunication.GetNodeAttribute(ReturnedNode,'TableNo'));
      RecRef.OPEN(TableNo);
      EVALUATE(RecordId,ADCSCommunication.GetNodeAttribute(ReturnedNode,'RecordID'));
      IF RecRef.GET(RecordId) THEN BEGIN
        RecRef.SETTABLE(WhseActivityLine);
        WhseActivityLine.SETCURRENTKEY("Activity Type","No.","Sorting Sequence No.");
        WhseActivityLine.SETRANGE("Activity Type",WhseActivityLine."Activity Type");
        WhseActivityLine.SETRANGE("No.",WhseActivityLine."No.");
        RecRef.GETTABLE(WhseActivityLine);
        ADCSCommunication.SetRecRef(RecRef);
      END ELSE BEGIN
        ADCSCommunication.RunPreviousMiniform(DOMxmlin);
        EXIT;
      END;

      FuncGroup.KeyDef := ADCSCommunication.GetFunctionKey(MiniformHeader.Code,TextValue);
      ActiveInputField := 1;

      CASE FuncGroup.KeyDef OF
        FuncGroup.KeyDef::Esc :
          ADCSCommunication.RunPreviousMiniform(DOMxmlin);
        FuncGroup.KeyDef::First :
          ADCSCommunication.FindRecRef(RecRef,0,MiniformHeader."No. of Records in List");
        FuncGroup.KeyDef::LnDn :
          IF NOT ADCSCommunication.FindRecRef(RecRef,1,MiniformHeader."No. of Records in List") THEN
            Remark := Text008;
        FuncGroup.KeyDef::LnUp :
          ADCSCommunication.FindRecRef(RecRef,2,MiniformHeader."No. of Records in List");
        FuncGroup.KeyDef::Last :
          ADCSCommunication.FindRecRef(RecRef,3,MiniformHeader."No. of Records in List");
        FuncGroup.KeyDef::PgDn :
          IF NOT ADCSCommunication.FindRecRef(RecRef,4,MiniformHeader."No. of Records in List") THEN
            Remark := Text008;
        FuncGroup.KeyDef::PgUp :
          ADCSCommunication.FindRecRef(RecRef,5,MiniformHeader."No. of Records in List");
        FuncGroup.KeyDef::Reset :
          Reset(WhseActivityLine);
        FuncGroup.KeyDef::Register : BEGIN
          Register(WhseActivityLine);
          IF Remark = '' THEN
            ADCSCommunication.RunPreviousMiniform(DOMxmlin)
          ELSE
            SendForm(ActiveInputField);
        END;
        FuncGroup.KeyDef::Input : BEGIN
          EVALUATE(FldNo,ADCSCommunication.GetNodeAttribute(ReturnedNode,'FieldID'));
          CASE FldNo OF
            WhseActivityLine.FIELDNO("Bin Code"): CheckBinNo(WhseActivityLine,UPPERCASE(TextValue));
            WhseActivityLine.FIELDNO("Item No."): CheckItemNo(WhseActivityLine,UPPERCASE(TextValue));
            WhseActivityLine.FIELDNO("Qty. to Handle"): CheckQty(WhseActivityLine,TextValue);
          ELSE BEGIN
            ADCSCommunication.FieldSetvalue(RecRef,FldNo,TextValue);
              RecRef.SETTABLE(WhseActivityLine);
          END;
        END;

        WhseActivityLine.MODIFY;
        RecRef.GETTABLE(WhseActivityLine);
        ADCSCommunication.SetRecRef(RecRef);
        ActiveInputField := ADCSCommunication.GetActiveInputNo(CurrentCode,FldNo);
        IF Remark = '' THEN
          IF ADCSCommunication.LastEntryField(CurrentCode,FldNo) THEN BEGIN
            RecRef.GETTABLE(WhseActivityLine);
            IF NOT ADCSCommunication.FindRecRef(RecRef,1,ActiveInputField) THEN BEGIN
              Remark := Text008;
            END ELSE
              ActiveInputField := 1;
          END ELSE
            ActiveInputField += 1;
          END;
        ELSE
          ERROR(Text000);
      END;

      IF NOT (FuncGroup.KeyDef IN [FuncGroup.KeyDef::Esc,FuncGroup.KeyDef::Register]) THEN
        SendForm(ActiveInputField);
    END;

    LOCAL PROCEDURE CheckBinNo@17(VAR WhseActLine@1003 : Record 5767;InputValue@1002 : Text[250]);
    BEGIN
      IF InputValue = WhseActLine."Bin Code" THEN
        EXIT;

      Remark := STRSUBSTNO(Text004,WhseActLine.FIELDCAPTION("Bin Code"));
    END;

    LOCAL PROCEDURE CheckItemNo@16(VAR WhseActLine@1006 : Record 5767;InputValue@1005 : Text[250]);
    VAR
      ItemIdent@1000 : Record 7704;
    BEGIN
      IF InputValue = WhseActLine."Item No." THEN
        EXIT;

      IF NOT ItemIdent.GET(InputValue) THEN
        Remark := STRSUBSTNO(Text004,ItemIdent.FIELDCAPTION("Item No."));

      IF ItemIdent."Item No." <> WhseActLine."Item No." THEN
        Remark := STRSUBSTNO(Text004,ItemIdent.FIELDCAPTION("Item No."));

      IF ((ItemIdent."Variant Code" <> '') AND (ItemIdent."Variant Code" <> WhseActLine."Variant Code")) THEN
        Remark := STRSUBSTNO(Text004,ItemIdent.FIELDCAPTION("Variant Code"));

      IF ((ItemIdent."Unit of Measure Code" <> '') AND (ItemIdent."Unit of Measure Code" <> WhseActLine."Unit of Measure Code")) THEN
        Remark := STRSUBSTNO(Text004,ItemIdent.FIELDCAPTION("Unit of Measure Code"));
    END;

    LOCAL PROCEDURE CheckQty@26(VAR WhseActLine@1001 : Record 5767;InputValue@1000 : Text[250]);
    VAR
      QtyToHandle@1002 : Decimal;
    BEGIN
      IF InputValue = '' THEN BEGIN
        Remark := Text011;
        EXIT;
      END;

      WITH WhseActLine DO BEGIN
        EVALUATE(QtyToHandle,InputValue);
        IF QtyToHandle = ABS(QtyToHandle) THEN BEGIN
          CheckItemNo(WhseActLine,"Item No.");
          IF QtyToHandle <= "Qty. Outstanding" THEN
            VALIDATE("Qty. to Handle",QtyToHandle)
          ELSE
            Remark := Text011;
        END ELSE
          Remark := Text011;
      END;
    END;

    LOCAL PROCEDURE Reset@29(VAR WhseActLine2@1004 : Record 5767);
    VAR
      WhseActLine@1003 : Record 5767;
    BEGIN
      IF NOT WhseActLine.GET(WhseActLine2."Activity Type",WhseActLine2."No.",WhseActLine2."Line No.") THEN
        ERROR(Text007);

      Remark := '';
      WhseActLine.VALIDATE("Qty. to Handle",0);
      WhseActLine.MODIFY;

      RecRef.GETTABLE(WhseActLine);
      ADCSCommunication.SetRecRef(RecRef);
      ActiveInputField := 1;
    END;

    LOCAL PROCEDURE Register@30(WhseActLine2@1000 : Record 5767);
    VAR
      WhseActLine@1003 : Record 5767;
      WhseActivityRegister@1005 : Codeunit 7307;
    BEGIN
      IF NOT WhseActLine.GET(WhseActLine2."Activity Type",WhseActLine2."No.",WhseActLine2."Line No.") THEN
        ERROR(Text007);
      IF NOT BalanceQtyToHandle(WhseActLine) THEN
        Remark := Text009
      ELSE BEGIN
        WhseActivityRegister.ShowHideDialog(TRUE);
        WhseActivityRegister.RUN(WhseActLine);
      END;
    END;

    LOCAL PROCEDURE BalanceQtyToHandle@18(VAR WhseActivLine2@1000 : Record 5767) : Boolean;
    VAR
      WhseActLine@1004 : Record 5767;
      QtyToPick@1002 : Decimal;
      QtyToPutAway@1003 : Decimal;
    BEGIN
      WhseActLine.COPY(WhseActivLine2);
      WITH WhseActLine DO BEGIN
        SETCURRENTKEY("Activity Type","No.","Item No.","Variant Code","Action Type");
        SETRANGE("Activity Type","Activity Type");
        SETRANGE("No.","No.");
        SETRANGE("Action Type");

        IF FIND('-') THEN
          REPEAT
            SETRANGE("Item No.","Item No.");
            SETRANGE("Variant Code","Variant Code");
            SETRANGE("Serial No.","Serial No.");
            SETRANGE("Lot No.","Lot No.");

            IF (WhseActivLine2."Action Type" = WhseActivLine2."Action Type"::Take) OR
               (WhseActivLine2.GETFILTER("Action Type") = '')
            THEN BEGIN
              SETRANGE("Action Type","Action Type"::Take);
              IF FIND('-') THEN
                REPEAT
                  QtyToPick := QtyToPick + "Qty. to Handle (Base)";
                UNTIL NEXT = 0;
            END;

            IF (WhseActivLine2."Action Type" = WhseActivLine2."Action Type"::Place) OR
               (WhseActivLine2.GETFILTER("Action Type") = '')
            THEN BEGIN
              SETRANGE("Action Type","Action Type"::Place);
              IF FIND('-') THEN
                REPEAT
                  QtyToPutAway := QtyToPutAway + "Qty. to Handle (Base)";
                UNTIL NEXT = 0;
            END;

            IF QtyToPick <> QtyToPutAway THEN
              EXIT(FALSE);

            SETRANGE("Action Type");
            FIND('+');
            SETRANGE("Item No.");
            SETRANGE("Variant Code");
            SETRANGE("Serial No.");
            SETRANGE("Lot No.");
            QtyToPick := 0;
            QtyToPutAway := 0;
          UNTIL NEXT = 0;
        END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE PrepareData@1();
    VAR
      TableNo@1004 : Integer;
      RecordId@1003 : RecordID;
      WhseActivityLine@1002 : Record 5767;
      WhseActivityHeader@1000 : Record 5766;
    BEGIN
      XMLDOMMgmt.FindNode(RootNode,'Header/Input',ReturnedNode);

      EVALUATE(TableNo,ADCSCommunication.GetNodeAttribute(ReturnedNode,'TableNo'));
      RecRef.OPEN(TableNo);
      EVALUATE(RecordId,ADCSCommunication.GetNodeAttribute(ReturnedNode,'RecordID'));
      IF RecRef.GET(RecordId) THEN BEGIN
        RecRef.SETTABLE(WhseActivityHeader);
        WhseActivityLine.SETRANGE("Activity Type",WhseActivityHeader.Type);
        WhseActivityLine.SETRANGE("No.",WhseActivityHeader."No.");
        IF NOT WhseActivityLine.FIND('-') THEN BEGIN
          ADCSNasStartup.SendError(Text012);
          EXIT;
        END;
        RecRef.GETTABLE(WhseActivityLine);
        ADCSCommunication.SetRecRef(RecRef);
        ActiveInputField := 1;
        SendForm(ActiveInputField);
      END ELSE
        ERROR(Text007);
    END;

    LOCAL PROCEDURE SendForm@3(InputField@1000 : Integer);
    BEGIN
      // Prepare Miniform
      ADCSCommunication.EncodeMiniForm(MiniformHeader,StackCode,DOMxmlin,InputField,Remark,LoginID);
      ADCSCommunication.GetReturnXML(DOMxmlin);
      ADCSNasStartup.SendXMLReply(DOMxmlin);
    END;

    EVENT DOMxmlin@1011::ondataavailable@198();
    BEGIN
    END;

    EVENT DOMxmlin@1011::onreadystatechange@-609();
    BEGIN
    END;

    BEGIN
    END.
  }
}
