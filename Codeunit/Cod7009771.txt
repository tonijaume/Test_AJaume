OBJECT Codeunit 7009771 Actualizacion ventana
{
  OBJECT-PROPERTIES
  {
    Date=02/10/12;
    Time=12:00:00;
    Modified=Yes;
    Version List=AIC2009,DA;
  }
  PROPERTIES
  {
    SingleInstance=Yes;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text001@1103355007 : TextConst 'ENU=" No. of Records #1######\";ESP=" N§ de registros #1######\";FRA=Numeri dei Records  # 1 ###### \;ITA=Numeri dei Records  #1###### \';
      Text002@1103355006 : TextConst 'ENU=" Progress       @2@@@@@@@@@@@@@\";ESP=" Progreso        @2@@@@@@@@@@@@@\";FRA=" ProgrŠs        @2@@@@@@@@@@@@@\";ITA=Avanzamento @2 @@@@@@@@@\';
      Text003@1103355005 : TextConst 'ENU=" Ending time    #3##########\\";ESP=" Hora fin        #3###\\";ITA=Ora di fine #3### \ \';
      Text005@1103355003 : TextConst 'ENU=" Description    #4#######################\";ESP=" Descripci¢n     #4#######################\";FRA=" Description     #4#######################\";ITA=" Descrizione #4####################### \"';
      Text006@1103355002 : TextConst 'ENU=" Table          #5#######################\";ESP=" Tabla           #5#######################\";FRA=" Table           #5#######################\";ITA=" Tabella #5####################### \"';
      Text007@1103355001 : TextConst 'ENU=" No. of Records #6######\";ESP=" N§ de registros #6######\";ITA=Numero dei records #6######  \';
      Text008@1103355000 : TextConst 'ENU=" Progress       @7@@@@@@@@@@@@@";ESP=" Progreso        @7@@@@@@@@@@@@@\";FRA=" ProgrŠs        @7@@@@@@@@@@@@@\";ITA=Avanzamento @7@@@@@@@@@\';
      Window@1103355018 : Dialog;
      LastUpdateTime@1103355017 : Time;
      StartTime@1103355016 : Time;
      Opened@1103355015 : Boolean;
      Started@1103355014 : Boolean;
      NoOfRecords@1103355013 : BigInteger;
      TableRecordNo@1103355012 : BigInteger;
      RecordNo@1103355011 : BigInteger;
      TableNoOfRecords@1103355010 : BigInteger;
      Title@1103355009 : Text[30];
      Titleb@1103355020 : Text[30];
      Small@1103355019 : Boolean;

    PROCEDURE Open@1();
    BEGIN
      IF Small THEN
        Window.OPEN(
          Title + '\'+
          Text005 +
          Text007 +
          Text008)
      ELSE
        Window.OPEN(
          Title + '\' +
          Text001 +
          Text002 +
          Text003 +
          Titleb + '\' +
          Text005 +
          Text007 +
          Text008);

      LastUpdateTime := TIME;
      Opened := TRUE;
    END;

    PROCEDURE UpdateNoOfRecords@4(TableNoOfRecords2@1000 : Integer;Description@1103355000 : Text[50]) : Boolean;
    BEGIN
      IF NOT Started THEN BEGIN
        NoOfRecords := NoOfRecords + TableNoOfRecords2;
        EXIT(FALSE);
      END
      ELSE BEGIN
        IF TableNoOfRecords2 = 0 THEN
          EXIT(FALSE);

        IF NOT Opened THEN BEGIN
          Open;
          IF Small THEN
            NoOfRecords := TableNoOfRecords2
          ELSE
            Window.UPDATE(1,NoOfRecords);
        END;

        TableRecordNo := 0L;
        TableNoOfRecords := TableNoOfRecords2;
        Window.UPDATE(4,Description);
        Window.UPDATE(6,TableNoOfRecords);
        Window.UPDATE(7,0);
        UpdateWindow;

        EXIT(TRUE);
      END;
    END;

    PROCEDURE Start@5();
    BEGIN
      Started := TRUE;
      StartTime := TIME;
      TableRecordNo := TableNoOfRecords;
      RecordNo := 0L;
    END;

    PROCEDURE Update@2();
    BEGIN
      IF NOT Small THEN
        RecordNo := RecordNo + 1L;
      TableRecordNo := TableRecordNo + 1L;
      IF ABS(TIME - LastUpdateTime) > 1000 THEN
        UpdateWindow;
    END;

    LOCAL PROCEDURE UpdateWindow@7();
    VAR
      TimeToFinish@1103355000 : Time;
    BEGIN
      IF NOT Small THEN
        IF (RecordNo <> 0) AND (NoOfRecords <> 0) THEN BEGIN
          Window.UPDATE(2,ROUND(RecordNo / NoOfRecords * 10000L,1));
          IF (RecordNo/(TIME-StartTime)) < 2147483647 THEN BEGIN //+001
            TimeToFinish := TIME + ROUND((NoOfRecords-RecordNo)/(RecordNo/(TIME-StartTime)),1);
            Window.UPDATE(3, FORMAT(TimeToFinish, 0, '<Hours24,2>:<Minutes,2>'));
          END; //+001
        END;

      IF (TableRecordNo <> 0) AND (TableNoOfRecords <> 0) THEN
        Window.UPDATE(7,ROUND(TableRecordNo / TableNoOfRecords * 10000L,1));

      LastUpdateTime := TIME;
    END;

    PROCEDURE Initialize@1103355009(Small2@1103355001 : Boolean;Title2@1103355000 : Text[30];Titleb2@1103355002 : Text[30]);
    BEGIN
      Finish();
      LastUpdateTime := 0T;
      StartTime := 0T;
      NoOfRecords := 0;
      TableRecordNo := 0;
      RecordNo := 0;
      TableNoOfRecords := 0;
      Small := Small2;
      Title := Title2;
      Titleb := Titleb2;
      IF Small THEN
        Started := TRUE
      ELSE
        Started := FALSE;
    END;

    PROCEDURE Finish@1103355000();
    BEGIN
      IF Opened THEN BEGIN
        Window.OPEN('Limpiando');
        Window.CLOSE;
        Opened := FALSE;
      END;
    END;

    BEGIN
    {
      001 25/11/11 PLB: INC-58291. Error sobrepasamiento de decimal a integer.
    }
    END.
  }
}
