OBJECT Codeunit 429 IC Dimension Value-Indent
{
  OBJECT-PROPERTIES
  {
    Date=15/09/04;
    Time=12:00:00;
    Version List=NAVW14.00;
  }
  PROPERTIES
  {
    TableNo=412;
    OnRun=BEGIN
            IF NOT
               CONFIRM(
                 STRSUBSTNO(
                   Text000 +
                   Text001 +
                   Text002 +
                   Text003,"Dimension Code"),TRUE)
            THEN
              EXIT;
            ICDimVal.SETRANGE("Dimension Code","Dimension Code");
            Indent;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU="This function updates the indentation of all the IC dimension values for IC dimension %1. ";ESP="Esta funci¢n actualiza la sangr¡a de todos los valores de dimensi¢n IC de la dimensi¢n IC %1. "';
      Text001@1001 : TextConst 'ENU="All IC dimension values between a Begin-Total and the matching End-Total are indented by one level. ";ESP="A todos los valores de dimensi¢n IC entre principio-total y el fin-total correspondiente se les aplica un nivel de sangr¡a. "';
      Text002@1002 : TextConst 'ENU=The Totaling field for each End-Total is also updated.\\;ESP=Campo totales para cada Fin-Total est  tambi‚n actualiz.\\';
      Text003@1003 : TextConst 'ENU=Do you want to indent the IC dimension values?;ESP=¨Desea aplicar la sangr¡a a los valores de dimensi¢n IC?';
      Text004@1004 : TextConst 'ENU=Indenting IC Dimension Values @1@@@@@@@@@@@@@@@@@@;ESP=Sangr¡a de valores de dim. IC @1@@@@@@@@@@@@@@@@@@';
      ICDimVal@1006 : Record 412;
      Window@1007 : Dialog;
      ICDimValCode@1008 : ARRAY [10] OF Code[20];
      i@1009 : Integer;
      Text005@1005 : TextConst 'ENU=End-Total %1 is missing a matching Begin-Total.;ESP=Fin-Tot %1 est  perdiendo un ajuste de inicio-tot.';

    PROCEDURE Indent@1();
    VAR
      NoOfDimVals@1000 : Integer;
      Progress@1001 : Integer;
    BEGIN
      Window.OPEN(Text004);

      NoOfDimVals := ICDimVal.COUNTAPPROX;
      IF NoOfDimVals = 0 THEN
        NoOfDimVals := 1;
      WITH ICDimVal DO
        IF FIND('-') THEN
          REPEAT
            Progress := Progress + 1;
            Window.UPDATE(1,10000 * Progress DIV NoOfDimVals);
            IF "Dimension Value Type" = "Dimension Value Type"::"End-Total" THEN BEGIN
              IF i < 1 THEN
                ERROR(
                  Text005,
                  Code);
              i := i - 1;
            END;

            Indentation := i;
            MODIFY;

            IF "Dimension Value Type" = "Dimension Value Type"::"Begin-Total" THEN BEGIN
              i := i + 1;
              ICDimValCode[i] := Code;
            END;
          UNTIL NEXT = 0;

      Window.CLOSE;
    END;

    BEGIN
    END.
  }
}
