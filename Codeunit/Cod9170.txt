OBJECT Codeunit 9170 Conf./Personalization Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            InitializeProfiles;
          END;

  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=This will delete all configuration changes made for this profile.  Do you want to continue?;ESP=Esto eliminar  todos los cambios de configuraci¢n realizados para este perfil. ¨Desea continuar?';
      Text002@1001 : TextConst 'ENU=This will delete all personalization changes made by this user.  Do you want to continue?;ESP=Esto eliminar  todos los cambios de personalizaci¢n realizados por el usuario. ¨Desea continuar?';
      Text003@1044 : TextConst 'ENU=You cannot delete a profile with default Role Center.;ESP=No puede eliminar un perfil con el µrea de trabajo predeterminada.';
      Text004@1043 : TextConst 'ENU=Accounting Manager;ESP=Administrador de contabilidad';
      Text005@1042 : TextConst 'ENU=Accounting Manager;ESP=Administrador de contabilidad';
      Text006@1041 : TextConst 'ENU=AP Coordinator;ESP=Coordinador Cuentas por pagar';
      Text007@1040 : TextConst 'ENU=Accounts Payable Coordinator;ESP=Coordinador Cuentas por pagar';
      Text008@1039 : TextConst 'ENU=AR Administrator;ESP=Coordinador Cobros';
      Text009@1038 : TextConst 'ENU=Accounts Receivable Administrator;ESP=Administrador Cobros';
      Text010@1037 : TextConst 'ENU=Bookkeeper;ESP=Contable';
      Text011@1036 : TextConst 'ENU=Bookkeeper;ESP=Contable';
      Text012@1035 : TextConst 'ENU=Sales Manager;ESP=Director de ventas';
      Text013@1034 : TextConst 'ENU=Sales Manager;ESP=Director de ventas';
      Text014@1033 : TextConst 'ENU=Order Processor;ESP=Procesador de pedidos';
      Text015@1032 : TextConst 'ENU="Sales Order Processor ";ESP="Procesador de pedidos de ventas "';
      Text016@1031 : TextConst 'ENU=Purchasing Agent;ESP=Agente de compras';
      Text017@1030 : TextConst 'ENU=Purchasing Agent;ESP=Agente de compras';
      Text018@1029 : TextConst 'ENU=Shipping and Receiving - WMS;ESP=Env¡o y recepci¢n - SGA';
      Text019@1028 : TextConst 'ENU=Shipping and Receiving - Warehouse Management System;ESP=Env¡o y recepci¢n - Sistema de gesti¢n de almacenes';
      Text020@1027 : TextConst 'ENU=Shipping and Receiving;ESP=Env¡o y recepci¢n';
      Text021@1026 : TextConst 'ENU=Shipping and Receiving - Order-by-Order;ESP=Env¡o y recepci¢n - Pedido a pedido';
      Text022@1025 : TextConst 'ENU=Warehouse Worker - WMS;ESP=Trabajador de almac‚n - SGA';
      Text023@1024 : TextConst 'ENU=Warehouse Worker - Warehouse Management System;ESP=Trabajador de almac‚n - Sistema de gesti¢n de almacenes';
      Text024@1023 : TextConst 'ENU=Production Planner;ESP=Planificador de producci¢n';
      Text025@1022 : TextConst 'ENU=Production Planner;ESP=Planificador de producci¢n';
      Text026@1021 : TextConst 'ENU=Shop Supervisor;ESP=Supervisor de tienda';
      Text027@1020 : TextConst 'ENU=Shop Supervisor - Manufacturing Comprehensive;ESP=Supervisor de tienda - Fabricaci¢n exhaustiva';
      Text028@1019 : TextConst 'ENU=Shop Supervisor - Foundation;ESP=Supervisor tienda - Fundaci¢n';
      Text029@1018 : TextConst 'ENU=Shop Supervisor - Manufacturing Foundation;ESP=Supervisor de tienda - Fundaci¢n de fabricaci¢n';
      Text030@1017 : TextConst 'ENU=Machine Operator;ESP=Operador de m quina';
      Text031@1016 : TextConst 'ENU=Machine Operator - Manufacturing Comprehensive;ESP=Operador de m quina - Fabricaci¢n exhaustiva';
      Text032@1015 : TextConst 'ENU=Resource Manager;ESP=Administrador de recursos';
      Text033@1014 : TextConst 'ENU=Resource Manager;ESP=Administrador de recursos';
      Text034@1013 : TextConst 'ENU=Project Manager;ESP=Director de proyectos';
      Text035@1012 : TextConst 'ENU=Project Manager;ESP=Director de proyectos';
      Text036@1011 : TextConst 'ENU=Dispatcher;ESP=Distribuidor';
      Text037@1010 : TextConst 'ENU=Dispatcher - Customer Service;ESP=Distribuidor - Servicio al cliente';
      Text038@1009 : TextConst 'ENU=Outbound Technician;ESP=T‚cnico de salida';
      Text039@1008 : TextConst 'ENU=Outbound Technician - Customer Service;ESP=T‚cnico de salida - Servicio al cliente';
      Text040@1007 : TextConst 'ENU=IT Manager;ESP=Director de TI';
      Text041@1006 : TextConst 'ENU=IT Manager;ESP=Director de TI';
      Text042@1005 : TextConst 'ENU="President ";ESP="Presidente "';
      Text043@1004 : TextConst 'ENU="President ";ESP="Presidente "';
      Text044@1003 : TextConst 'ENU=President - Small Business;ESP=Presidente - Peque¤a empresa';
      Text045@1002 : TextConst 'ENU=President - Small Business;ESP=Presidente - Peque¤a empresa';

    PROCEDURE InitializeProfiles@20();
    VAR
      Profile@1100 : Record 2000000072;
    BEGIN
      Profile.LOCKTABLE;
      IF Profile.FINDFIRST THEN
        EXIT;
      InsertProfile(Text004,Text005,9001);
      InsertProfile(Text006,Text007,9002);
      InsertProfile(Text008,Text009,9003);
      InsertProfile(Text010,Text011,9004);
      InsertProfile(Text012,Text013,9005);
      InsertProfile(Text014,Text015,9006);
      InsertProfile(Text016,Text017,9007);
      InsertProfile(Text018,Text019,9000);
      InsertProfile(Text020,Text021,9008);
      InsertProfile(Text022,Text023,9009);
      InsertProfile(Text024,Text025,9010);
      InsertProfile(Text026,Text027,9012);
      InsertProfile(Text028,Text029,9011);
      InsertProfile(Text030,Text031,9013);
      InsertProfile(Text032,Text033,9014);
      InsertProfile(Text034,Text035,9015);
      InsertProfile(Text036,Text037,9016);
      InsertProfile(Text038,Text039,9017);
      InsertProfile(Text040,Text041,9018);
      InsertProfile(Text042,Text043,9019);
      InsertProfile(Text044,Text045,9020);
      COMMIT;
    END;

    PROCEDURE InsertProfile@21(ProfileID@1000 : Code[30];Description@1001 : Text[250];RoleCenterID@1002 : Integer);
    VAR
      Profile@1101 : Record 2000000072;
      Object@1102 : Record 2000000001;
    BEGIN
      Object.SETRANGE(Type,Object.Type::Page);
      Object.SETRANGE(ID,RoleCenterID);
      IF Object.ISEMPTY THEN
        EXIT;

      Profile.INIT;
      Profile."Profile ID" := ProfileID;
      Profile.Description := Description;
      Profile."Role Center ID" := RoleCenterID;
      Profile."Default Role Center" := (RoleCenterID = DefaultRoleCenterID);
      Profile.INSERT;
    END;

    PROCEDURE DefaultRoleCenterID@22() : Integer;
    BEGIN
      EXIT(9006);
    END;

    PROCEDURE CopyProfile@45(Profile@1035 : Record 2000000072;NewProfileID@1036 : Code[30]);
    VAR
      NewProfile@1037 : Record 2000000072;
      ProfileMetadata@1040 : Record 2000000074;
      NewProfileMetadata@1038 : Record 2000000074;
    BEGIN
      NewProfile.INIT;
      NewProfile.VALIDATE("Profile ID",NewProfileID);
      NewProfile.TESTFIELD("Profile ID");
      NewProfile.VALIDATE(Description,Profile.Description);
      NewProfile.VALIDATE("Role Center ID",Profile."Role Center ID");
      NewProfile.INSERT;

      ProfileMetadata.SETRANGE("Profile ID",Profile."Profile ID");
      IF ProfileMetadata.FINDSET THEN
        REPEAT
          ProfileMetadata.CALCFIELDS("Page Metadata Delta");

          NewProfileMetadata.INIT;
          NewProfileMetadata.COPY(ProfileMetadata);
          NewProfileMetadata."Profile ID" := NewProfileID;
          NewProfileMetadata.INSERT;
        UNTIL ProfileMetadata.NEXT = 0;
    END;

    PROCEDURE ClearProfileConfiguration@1(Profile@1000 : Record 2000000072);
    VAR
      ProfileMetadata@1001 : Record 2000000074;
    BEGIN
      IF NOT CONFIRM(Text001) THEN
        EXIT;

      ProfileMetadata.SETRANGE("Profile ID", Profile."Profile ID");
      ProfileMetadata.DELETEALL(TRUE);
    END;

    PROCEDURE ClearUserPersonalization@2(User@1000 : Record 2000000073);
    VAR
      UserMetadata@1001 : Record 2000000075;
    BEGIN
      IF NOT CONFIRM(Text002) THEN
        EXIT;

      UserMetadata.SETRANGE("User SID", User."User SID");
      UserMetadata.DELETEALL(TRUE);
    END;

    PROCEDURE ExportProfiles@3(FileName@1000 : Text[250];VAR Profile@1001 : Record 2000000072);
    VAR
      FileOutStream@1002 : OutStream;
      ProfileFile@1003 : File;
    BEGIN
      ProfileFile.CREATE(FileName);
      ProfileFile.CREATEOUTSTREAM(FileOutStream);
      XMLPORT.EXPORT(XMLPORT::"Profile Import/Export",FileOutStream,Profile);
      ProfileFile.CLOSE;
    END;

    PROCEDURE ImportProfiles@4(FileName@1000 : Text[250]);
    VAR
      FileInStream@1001 : InStream;
      ProfileFile@1002 : File;
    BEGIN
      ProfileFile.OPEN(FileName);
      ProfileFile.CREATEINSTREAM(FileInStream);
      XMLPORT.IMPORT(XMLPORT::"Profile Import/Export",FileInStream);
      ProfileFile.CLOSE;
    END;

    PROCEDURE ChangeDefaultRoleCenter@24(ProfileID@1110 : Code[30]);
    VAR
      Profile@1111 : Record 2000000072;
    BEGIN
      Profile.SETRANGE("Default Role Center",TRUE);
      Profile.SETFILTER("Profile ID",'<> %1',ProfileID);
      IF Profile.FINDFIRST THEN BEGIN
        Profile."Default Role Center" := FALSE;
        Profile.MODIFY;
      END;
    END;

    PROCEDURE DeleteProfile@25(Profile@1112 : Record 2000000072);
    VAR
      UserPersonalization@1113 : Record 2000000073;
      ProfileMetadata@1114 : Record 2000000074;
    BEGIN
      IF Profile."Default Role Center" THEN
        ERROR(Text003);

      ProfileMetadata.SETFILTER("Profile ID",Profile."Profile ID");
      ProfileMetadata.DELETEALL(TRUE);

      UserPersonalization.SETCURRENTKEY("Profile ID");
      UserPersonalization.SETFILTER("Profile ID",Profile."Profile ID");
      IF UserPersonalization.FINDSET(TRUE,TRUE) THEN BEGIN
        REPEAT
          UserPersonalization."Profile ID" := '';
          UserPersonalization.MODIFY;
        UNTIL UserPersonalization.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}
