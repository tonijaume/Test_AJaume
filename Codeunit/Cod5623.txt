OBJECT Codeunit 5623 FA MoveEntries
{
  OBJECT-PROPERTIES
  {
    Date=08/06/01;
    Time=12:00:00;
    Version List=NAVW13.00;
  }
  PROPERTIES
  {
    Permissions=TableData 5601=rm,
                TableData 5625=rm,
                TableData 5629=rm;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Only disposed fixed assets can be deleted.;ESP=S¢lo se pueden eliminar activos vendidos/dados de baja.';
      Text001@1001 : TextConst 'ENU=You cannot delete a fixed asset that has ledger entries in a fiscal year that has not been closed yet.;ESP=No se puede eliminar un activo con movimientos en un periodo contable que a£n no se haya cerrado.';
      Text002@1002 : TextConst 'ENU=The field %1 cannot be changed for a fixed asset with ledger entries.;ESP=No se puede cambiar el campo %1 por un activo con movimientos.';
      Text003@1003 : TextConst 'ENU=The field %1 cannot be changed for a fixed asset with insurance coverage ledger entries.;ESP=No se puede cambiar el campo %1 por un activo con movimientos de seguro.';
      FA@1004 : Record 5600;
      FALedgEntry@1005 : Record 5601;
      MaintenanceLedgEntry@1006 : Record 5625;
      InsCoverageLedgEntry@1007 : Record 5629;
      AccountingPeriod@1008 : Record 50;
      FiscalYearStartDate@1009 : Date;

    PROCEDURE MoveFAEntries@1(FADeprBook@1000 : Record 5612);
    BEGIN
      CLEARALL;
      FA.LOCKTABLE;
      FALedgEntry.LOCKTABLE;
      MaintenanceLedgEntry.LOCKTABLE;
      InsCoverageLedgEntry.LOCKTABLE;
      FA.GET(FADeprBook."FA No.");

      AccountingPeriod.SETCURRENTKEY(Closed);
      AccountingPeriod.SETRANGE(Closed,FALSE);
      IF AccountingPeriod.FIND('-') THEN
        FiscalYearStartDate := AccountingPeriod."Starting Date"
      ELSE
        FiscalYearStartDate := 0D;

      FALedgEntry.SETCURRENTKEY("FA No.","Depreciation Book Code","FA Posting Date");
      MaintenanceLedgEntry.SETCURRENTKEY("FA No.","Depreciation Book Code","FA Posting Date");

      FALedgEntry.SETRANGE("FA No.",FADeprBook."FA No.");
      MaintenanceLedgEntry.SETRANGE("FA No.",FADeprBook."FA No.");

      FALedgEntry.SETRANGE("Depreciation Book Code",FADeprBook."Depreciation Book Code");
      MaintenanceLedgEntry.SETRANGE("Depreciation Book Code",FADeprBook."Depreciation Book Code");

      IF FA."Budgeted Asset" THEN
        DeleteNo(FADeprBook)
      ELSE BEGIN
        IF FALedgEntry.FIND('-') THEN
          IF FADeprBook."Disposal Date" = 0D THEN
            ERROR(Text000);

        FALedgEntry.SETFILTER("FA Posting Date",'>=%1',FiscalYearStartDate);
        IF FALedgEntry.FIND('-') THEN
          CreateError(0);

        MaintenanceLedgEntry.SETFILTER("FA Posting Date",'>=%1',FiscalYearStartDate);
        IF MaintenanceLedgEntry.FIND('-') THEN
          CreateError(0);

        FALedgEntry.SETRANGE("FA Posting Date");
        MaintenanceLedgEntry.SETRANGE("FA Posting Date");

        FALedgEntry.SETCURRENTKEY(
          "FA No.","Depreciation Book Code","FA Posting Category","FA Posting Type","Posting Date");
        MaintenanceLedgEntry.SETCURRENTKEY("FA No.","Depreciation Book Code","Posting Date");

        FALedgEntry.SETFILTER("Posting Date",'>=%1',FiscalYearStartDate);
        IF FALedgEntry.FIND('-') THEN
          CreateError(0);
        MaintenanceLedgEntry.SETFILTER("Posting Date",'>=%1',FiscalYearStartDate);
        IF MaintenanceLedgEntry.FIND('-') THEN
          CreateError(0);

        FALedgEntry.SETRANGE("Posting Date");
        MaintenanceLedgEntry.SETRANGE("Posting Date");
        DeleteNo(FADeprBook);
      END;
    END;

    LOCAL PROCEDURE DeleteNo@8(VAR FADeprBook@1000 : Record 5612);
    BEGIN
      FALedgEntry.MODIFYALL("FA No.",'');
      FALedgEntry.SETRANGE("FA No.");
      FALedgEntry.SETCURRENTKEY("Canceled from FA No.");
      FALedgEntry.SETRANGE("Canceled from FA No.",FADeprBook."FA No.");
      FALedgEntry.MODIFYALL("Canceled from FA No.",'');

      MaintenanceLedgEntry.MODIFYALL("FA No.",'');
      MoveFAInsuranceEntries(FADeprBook."FA No.");
    END;

    PROCEDURE MoveInsuranceEntries@3(Insurance@1000 : Record 5628);
    BEGIN
      WITH InsCoverageLedgEntry DO BEGIN
        RESET;
        LOCKTABLE;
        SETCURRENTKEY("Insurance No.");
        SETRANGE("Insurance No.",Insurance."No.");
        IF FIND('-') THEN
          REPEAT
            "FA No." := '';
            "Insurance No." := '';
            MODIFY;
          UNTIL NEXT = 0;
      END;
    END;

    PROCEDURE MoveFAInsuranceEntries@6(FANo@1000 : Code[20]);
    BEGIN
      WITH InsCoverageLedgEntry DO BEGIN
        SETCURRENTKEY("FA No.");
        SETRANGE("FA No.",FANo);
        IF FIND('-') THEN
          REPEAT
            "Insurance No." := '';
            "FA No." := '';
            MODIFY;
          UNTIL NEXT = 0;
      END;
    END;

    PROCEDURE ChangeBudget@5(FA@1000 : Record 5600);
    BEGIN
      FALedgEntry.RESET;
      MaintenanceLedgEntry.RESET;
      InsCoverageLedgEntry.RESET;

      FALedgEntry.LOCKTABLE;
      MaintenanceLedgEntry.LOCKTABLE;
      InsCoverageLedgEntry.LOCKTABLE;

      FALedgEntry.SETCURRENTKEY("FA No.","Depreciation Book Code","FA Posting Date");
      MaintenanceLedgEntry.SETCURRENTKEY("FA No.","Depreciation Book Code","FA Posting Date");
      InsCoverageLedgEntry.SETCURRENTKEY("FA No.");

      FALedgEntry.SETRANGE("FA No.",FA."No.");
      MaintenanceLedgEntry.SETRANGE("FA No.",FA."No.");
      InsCoverageLedgEntry.SETRANGE("FA No.",FA."No.");

      IF FALedgEntry.FIND('-') THEN
        CreateError(1);
      IF MaintenanceLedgEntry.FIND('-') THEN
        CreateError(1);
      IF InsCoverageLedgEntry.FIND('-') THEN
        CreateError(2);
    END;

    LOCAL PROCEDURE CreateError@2(CheckType@1000 : 'Delete,Budget,Insurance');
    VAR
      FA@1001 : Record 5600;
    BEGIN
      CASE CheckType OF
        CheckType::Delete:
          ERROR(
            Text001);
        CheckType::Budget:
          ERROR(
            Text002,
            FA.FIELDCAPTION("Budgeted Asset"));
        CheckType::Insurance:
          ERROR(
            Text003,
            FA.FIELDCAPTION("Budgeted Asset"));
      END;
    END;

    BEGIN
    END.
  }
}
