OBJECT Codeunit 340 VATStmtManagement
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    Permissions=TableData 255=imd,
                TableData 257=imd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=VAT;ESP=IVA';
      Text001@1001 : TextConst 'ENU=VAT Statement;ESP=Declaraci¢n IVA';
      Text002@1002 : TextConst 'ENU=DEFAULT;ESP=GENERICO';
      Text003@1003 : TextConst 'ENU=Default Statement;ESP=Declaraci¢n gen‚r.';
      OpenFromBatch@1004 : Boolean;

    PROCEDURE TemplateSelection@1(FormID@1003 : Integer;VAR VATStmtLine@1002 : Record 256;VAR StmtSelected@1001 : Boolean);
    VAR
      VATStmtTmpl@1000 : Record 255;
    BEGIN
      StmtSelected := TRUE;

      VATStmtTmpl.RESET;
      VATStmtTmpl.SETRANGE("Form ID",FormID);

      CASE VATStmtTmpl.COUNT OF
        0:
          BEGIN
            VATStmtTmpl.INIT;
            VATStmtTmpl.Name := Text000;
            VATStmtTmpl.Description := Text001;
            VATStmtTmpl.VALIDATE("Form ID");
            VATStmtTmpl.INSERT;
            COMMIT;
          END;
        1:
          VATStmtTmpl.FIND('-');
        ELSE
          StmtSelected := FORM.RUNMODAL(0,VATStmtTmpl) = ACTION::LookupOK;
      END;
      IF StmtSelected THEN BEGIN
        VATStmtLine.FILTERGROUP(2);
        VATStmtLine.SETRANGE("Statement Template Name",VATStmtTmpl.Name);
        VATStmtLine.FILTERGROUP(0);
        IF OpenFromBatch THEN BEGIN
          VATStmtLine."Statement Template Name" := '';
          FORM.RUN(VATStmtTmpl."Form ID",VATStmtLine);
        END;
      END;
    END;

    PROCEDURE TemplateSelectionFromBatch@9(VAR VATStmtName@1003 : Record 257);
    VAR
      VATStmtLine@1000 : Record 256;
      VATStmtTmpl@1002 : Record 255;
      JnlSelected@1001 : Boolean;
    BEGIN
      OpenFromBatch := TRUE;
      VATStmtTmpl.GET(VATStmtName."Statement Template Name");
      VATStmtTmpl.TESTFIELD("Form ID");
      VATStmtName.TESTFIELD(Name);

      VATStmtLine.FILTERGROUP := 2;
      VATStmtLine.SETRANGE("Statement Template Name",VATStmtTmpl.Name);
      VATStmtLine.FILTERGROUP := 0;

      VATStmtLine."Statement Template Name" := '';
      VATStmtLine."Statement Name" := VATStmtName.Name;
      FORM.RUN(VATStmtTmpl."Form ID",VATStmtLine);
    END;

    PROCEDURE OpenStmt@2(VAR CurrentStmtName@1000 : Code[10];VAR VATStmtLine@1001 : Record 256);
    BEGIN
      CheckTemplateName(VATStmtLine.GETRANGEMAX("Statement Template Name"),CurrentStmtName);
      VATStmtLine.FILTERGROUP(2);
      VATStmtLine.SETRANGE("Statement Name",CurrentStmtName);
      VATStmtLine.FILTERGROUP(0);
    END;

    PROCEDURE OpenStmtBatch@10(VAR VATStmtName@1005 : Record 257);
    VAR
      VATStmtTmpl@1004 : Record 255;
      VATStmtLine@1001 : Record 256;
      VATStmtName2@1000 : Record 257;
      JnlSelected@1003 : Boolean;
    BEGIN
      IF VATStmtName.GETFILTER("Statement Template Name") <> '' THEN
        EXIT;
      VATStmtName.FILTERGROUP(2);
      IF VATStmtName.GETFILTER("Statement Template Name") <> '' THEN BEGIN
        VATStmtName.FILTERGROUP(0);
        EXIT;
      END;
      VATStmtName.FILTERGROUP(0);

      IF NOT VATStmtName.FIND('-') THEN BEGIN
        IF NOT VATStmtTmpl.FIND('-') THEN
          TemplateSelection(0,VATStmtLine,JnlSelected);
        IF VATStmtTmpl.FIND('-') THEN
          CheckTemplateName(VATStmtTmpl.Name,VATStmtName.Name);
      END;
      VATStmtName.FIND('-');
      JnlSelected := TRUE;
      IF VATStmtName.GETFILTER("Statement Template Name") <> '' THEN
         VATStmtTmpl.SETRANGE(Name,VATStmtName.GETFILTER("Statement Template Name"));
      CASE VATStmtTmpl.COUNT OF
        1:
          VATStmtTmpl.FIND('-');
        ELSE
          JnlSelected := FORM.RUNMODAL(0,VATStmtTmpl) = ACTION::LookupOK;
      END;
      IF NOT JnlSelected THEN
        ERROR('');

      VATStmtName.FILTERGROUP(0);
      VATStmtName.SETRANGE("Statement Template Name",VATStmtTmpl.Name);
      VATStmtName.FILTERGROUP(2);
    END;

    PROCEDURE CheckTemplateName@3(CurrentStmtTemplateName@1000 : Code[10];VAR CurrentStmtName@1001 : Code[10]);
    VAR
      VATStmtTmpl@1002 : Record 255;
      VATStmtName@1003 : Record 257;
    BEGIN
      VATStmtName.SETRANGE("Statement Template Name",CurrentStmtTemplateName);
      IF NOT VATStmtName.GET(CurrentStmtTemplateName,CurrentStmtName) THEN BEGIN
        IF NOT VATStmtName.FIND('-') THEN BEGIN
          VATStmtTmpl.GET(CurrentStmtTemplateName);
          VATStmtName.INIT;
          VATStmtName."Statement Template Name" := VATStmtTmpl.Name;
          VATStmtName.Name := Text002;
          VATStmtName.Description := Text003;
          VATStmtName.INSERT;
          COMMIT;
        END;
        CurrentStmtName := VATStmtName.Name;
      END;
    END;

    PROCEDURE CheckName@4(CurrentStmtName@1000 : Code[10];VAR VATStmtLine@1001 : Record 256);
    VAR
      VATStmtName@1002 : Record 257;
    BEGIN
      VATStmtName.GET(VATStmtLine.GETRANGEMAX("Statement Template Name"),CurrentStmtName);
    END;

    PROCEDURE SetName@5(CurrentStmtName@1000 : Code[10];VAR VATStmtLine@1001 : Record 256);
    BEGIN
      VATStmtLine.FILTERGROUP(2);
      VATStmtLine.SETRANGE("Statement Name",CurrentStmtName);
      VATStmtLine.FILTERGROUP(0);
      IF VATStmtLine.FIND('-') THEN;
    END;

    PROCEDURE LookupName@6(CurrentStmtTemplateName@1000 : Code[10];CurrentStmtName@1001 : Code[10];VAR EntrdStmtName@1002 : Text[10]) : Boolean;
    VAR
      VATStmtName@1003 : Record 257;
    BEGIN
      VATStmtName."Statement Template Name" := CurrentStmtTemplateName;
      VATStmtName.Name := CurrentStmtName;
      VATStmtName.FILTERGROUP(2);
      VATStmtName.SETRANGE("Statement Template Name",CurrentStmtTemplateName);
      VATStmtName.FILTERGROUP(0);
      IF FORM.RUNMODAL(0,VATStmtName) <> ACTION::LookupOK THEN
        EXIT(FALSE);

      EntrdStmtName := VATStmtName.Name;
      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}
