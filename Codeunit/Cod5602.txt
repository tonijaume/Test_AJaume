OBJECT Codeunit 5602 FA Get G/L Account No.
{
  OBJECT-PROPERTIES
  {
    Date=08/06/01;
    Time=12:00:00;
    Version List=NAVW13.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      FAPostingGr@1000 : Record 5606;
      GLAccNo@1001 : Code[20];

    PROCEDURE GetAccNo@1(VAR FALedgEntry@1000 : Record 5601) : Code[20];
    BEGIN
      WITH FALedgEntry DO BEGIN
        FAPostingGr.GET("FA Posting Group");
        GLAccNo := '';
        IF "FA Posting Category" = "FA Posting Category"::" " THEN
          CASE "FA Posting Type" OF
            "FA Posting Type"::"Acquisition Cost":
              BEGIN
                FAPostingGr.TESTFIELD("Acquisition Cost Account");
                GLAccNo := FAPostingGr."Acquisition Cost Account";
              END;
            "FA Posting Type"::Depreciation:
              BEGIN
                FAPostingGr.TESTFIELD("Accum. Depreciation Account");
                GLAccNo := FAPostingGr."Accum. Depreciation Account";
              END;
            "FA Posting Type"::"Write-Down":
              BEGIN
                FAPostingGr.TESTFIELD("Write-Down Account");
                GLAccNo := FAPostingGr."Write-Down Account";
              END;
            "FA Posting Type"::Appreciation:
              BEGIN
                FAPostingGr.TESTFIELD("Appreciation Account");
                GLAccNo := FAPostingGr."Appreciation Account";
              END;
            "FA Posting Type"::"Custom 1":
              BEGIN
                FAPostingGr.TESTFIELD("Custom 1 Account");
                GLAccNo := FAPostingGr."Custom 1 Account";
              END;
            "FA Posting Type"::"Custom 2":
              BEGIN
                FAPostingGr.TESTFIELD("Custom 2 Account");
                GLAccNo := FAPostingGr."Custom 2 Account";
              END;
            "FA Posting Type"::"Proceeds on Disposal":
              BEGIN
                FAPostingGr.TESTFIELD("Sales Acc. on Disp. (Gain)");
                GLAccNo := FAPostingGr."Sales Acc. on Disp. (Gain)";
              END;
            "FA Posting Type"::"Gain/Loss":
              BEGIN
                IF "Result on Disposal" = "Result on Disposal"::Gain THEN BEGIN
                  FAPostingGr.TESTFIELD("Gains Acc. on Disposal");
                  GLAccNo := FAPostingGr."Gains Acc. on Disposal";
                END;
                IF "Result on Disposal" = "Result on Disposal"::Loss THEN BEGIN
                  FAPostingGr.TESTFIELD("Losses Acc. on Disposal");
                  GLAccNo := FAPostingGr."Losses Acc. on Disposal";
                END;
              END;
          END;

        IF "FA Posting Category" = "FA Posting Category"::Disposal THEN
          CASE "FA Posting Type" OF
            "FA Posting Type"::"Acquisition Cost":
              BEGIN
                FAPostingGr.TESTFIELD("Acq. Cost Acc. on Disposal");
                GLAccNo := FAPostingGr."Acq. Cost Acc. on Disposal";
              END;
            "FA Posting Type"::Depreciation:
              BEGIN
                FAPostingGr.TESTFIELD("Accum. Depr. Acc. on Disposal");
                GLAccNo := FAPostingGr."Accum. Depr. Acc. on Disposal";
              END;
            "FA Posting Type"::"Write-Down":
              BEGIN
                FAPostingGr.TESTFIELD("Write-Down Acc. on Disposal");
                GLAccNo := FAPostingGr."Write-Down Acc. on Disposal";
              END;
            "FA Posting Type"::Appreciation:
              BEGIN
                FAPostingGr.TESTFIELD("Appreciation Acc. on Disposal");
                GLAccNo := FAPostingGr."Appreciation Acc. on Disposal";
              END;
            "FA Posting Type"::"Custom 1":
              BEGIN
                FAPostingGr.TESTFIELD("Custom 1 Account on Disposal");
                GLAccNo := FAPostingGr."Custom 1 Account on Disposal";
              END;
            "FA Posting Type"::"Custom 2":
              BEGIN
                FAPostingGr.TESTFIELD("Custom 2 Account on Disposal");
                GLAccNo := FAPostingGr."Custom 2 Account on Disposal";
              END;
            "FA Posting Type"::"Book Value on Disposal":
              BEGIN
                IF "Result on Disposal" = "Result on Disposal"::Gain THEN BEGIN
                  FAPostingGr.TESTFIELD("Book Val. Acc. on Disp. (Gain)");
                  GLAccNo := FAPostingGr."Book Val. Acc. on Disp. (Gain)";
                END;
                IF "Result on Disposal" = "Result on Disposal"::Loss THEN BEGIN
                  FAPostingGr.TESTFIELD("Book Val. Acc. on Disp. (Loss)");
                  GLAccNo := FAPostingGr."Book Val. Acc. on Disp. (Loss)";
                END;
                "Result on Disposal" := "Result on Disposal"::" ";
              END;
          END;

        IF "FA Posting Category" = "FA Posting Category"::"Bal. Disposal" THEN
          CASE "FA Posting Type" OF
            "FA Posting Type"::"Write-Down":
              BEGIN
                FAPostingGr.TESTFIELD("Write-Down Bal. Acc. on Disp.");
                GLAccNo := FAPostingGr."Write-Down Bal. Acc. on Disp.";
              END;
            "FA Posting Type"::Appreciation:
              BEGIN
                FAPostingGr.TESTFIELD("Apprec. Bal. Acc. on Disp.");
                GLAccNo := FAPostingGr."Apprec. Bal. Acc. on Disp.";
              END;
            "FA Posting Type"::"Custom 1":
              BEGIN
                FAPostingGr.TESTFIELD("Custom 1 Bal. Acc. on Disposal");
                GLAccNo := FAPostingGr."Custom 1 Bal. Acc. on Disposal";
              END;
            "FA Posting Type"::"Custom 2":
              BEGIN
                FAPostingGr.TESTFIELD("Custom 2 Bal. Acc. on Disposal");
                GLAccNo := FAPostingGr."Custom 2 Bal. Acc. on Disposal";
              END;
          END;
      END;
      EXIT(GLAccNo);
    END;

    PROCEDURE GetMaintenanceAccNo@3(VAR MaintenanceLedgEntry@1000 : Record 5625) : Code[20];
    BEGIN
      FAPostingGr.GET(MaintenanceLedgEntry."FA Posting Group");
      FAPostingGr.TESTFIELD("Maintenance Expense Account");
      EXIT(FAPostingGr."Maintenance Expense Account");
    END;

    BEGIN
    END.
  }
}
