OBJECT Codeunit 99000756 VersionManagement
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE GetBOMVersion@1(BOMHeaderNo@1000 : Code[20];Date@1001 : Date;OnlyCertified@1002 : Boolean) : Code[10];
    VAR
      ProdBOMVersion@1003 : Record 99000779;
    BEGIN
      ProdBOMVersion.SETCURRENTKEY("Production BOM No.","Starting Date");
      ProdBOMVersion.SETRANGE("Production BOM No.",BOMHeaderNo);
      ProdBOMVersion.SETFILTER("Starting Date",'%1|..%2',0D,Date);
      IF OnlyCertified THEN
        ProdBOMVersion.SETRANGE(Status,ProdBOMVersion.Status::Certified);
      IF NOT ProdBOMVersion.FIND('+') THEN
        CLEAR(ProdBOMVersion);

      EXIT(ProdBOMVersion."Version Code");
    END;

    PROCEDURE GetAndTestCertifiedBOMVersion@4(VAR ProdBOMHeader@1002 : Record 99000771;VAR ProdBOMVersion@1003 : Record 99000779;BOMHeaderNo@1001 : Code[20];Date@1000 : Date;HideError@1004 : Boolean) : Code[10];
    BEGIN
      CLEAR(ProdBOMHeader);
      CLEAR(ProdBOMVersion);
      ProdBOMHeader.GET(BOMHeaderNo);
      ProdBOMVersion.SETCURRENTKEY("Production BOM No.","Starting Date");
      ProdBOMVersion.SETRANGE("Production BOM No.",BOMHeaderNo);
      ProdBOMVersion.SETFILTER("Starting Date",'%1|..%2',0D,Date);
      IF ProdBOMVersion.FIND('+') THEN BEGIN
        IF NOT HideError THEN
          ProdBOMVersion.TESTFIELD(Status,ProdBOMVersion.Status::Certified);
      END ELSE
        IF NOT HideError THEN
          ProdBOMHeader.TESTFIELD(Status,ProdBOMHeader.Status::Certified);

      EXIT(ProdBOMVersion."Version Code");
    END;

    PROCEDURE GetBOMUnitOfMeasure@3(BOMHeaderNo@1000 : Code[20];VersionCode@1001 : Code[10]) : Code[10];
    VAR
      ProdBOMHeader@1002 : Record 99000771;
      ProdBOMVersion@1003 : Record 99000779;
    BEGIN
      IF BOMHeaderNo = ''THEN
        EXIT('');

      IF VersionCode = '' THEN BEGIN
        ProdBOMHeader.GET(BOMHeaderNo);
        EXIT(ProdBOMHeader."Unit of Measure Code");
      END;

      ProdBOMVersion.GET(BOMHeaderNo,VersionCode);
      EXIT(ProdBOMVersion."Unit of Measure Code");
    END;

    PROCEDURE GetRtngVersion@2(RoutingNo@1000 : Code[20];Date@1001 : Date;OnlyCertified@1002 : Boolean) : Code[10];
    VAR
      RtngVersion@1003 : Record 99000786;
    BEGIN
      RtngVersion.SETCURRENTKEY("Routing No.","Starting Date");
      RtngVersion.SETRANGE("Routing No.",RoutingNo);
      RtngVersion.SETFILTER("Starting Date",'%1|..%2',0D,Date);
      IF OnlyCertified THEN
        RtngVersion.SETRANGE(Status,RtngVersion.Status::Certified);
      IF NOT RtngVersion.FIND('+') THEN
        CLEAR(RtngVersion);

      EXIT(RtngVersion."Version Code");
    END;

    PROCEDURE GetAndTestCertifiedRtngVersion@5(VAR RtngHeader@1002 : Record 99000763;VAR RtngVersion@1003 : Record 99000786;RtngNo@1001 : Code[20];Date@1000 : Date;HideError@1004 : Boolean) : Code[10];
    BEGIN
      CLEAR(RtngHeader);
      CLEAR(RtngVersion);
      RtngHeader.GET(RtngNo);
      RtngVersion.SETCURRENTKEY("Routing No.","Starting Date");
      RtngVersion.SETRANGE("Routing No.",RtngNo);
      RtngVersion.SETFILTER("Starting Date",'%1|..%2',0D,Date);
      IF RtngVersion.FIND('+') THEN BEGIN
        IF NOT HideError THEN
          RtngVersion.TESTFIELD(Status,RtngVersion.Status::Certified);
      END ELSE
        IF NOT HideError THEN
          RtngHeader.TESTFIELD(Status,RtngHeader.Status::Certified);

      EXIT(RtngVersion."Version Code");
    END;

    BEGIN
    END.
  }
}
