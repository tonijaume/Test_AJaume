OBJECT Codeunit 5672 Insurance Jnl.-Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=08/06/01;
    Time=12:00:00;
    Version List=NAVW13.00;
  }
  PROPERTIES
  {
    TableNo=5635;
    OnRun=BEGIN
            InsuranceJnlLine.COPY(Rec);
            Code;
            Rec.COPY(InsuranceJnlLine);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to post the journal lines and print the posting report?;ESP=¨Confirma que desea registrar las l¡ns. de diario e imprimir el informe de reg.?';
      Text001@1001 : TextConst 'ENU=There is nothing to post.;ESP=No hay nada que registrar.';
      Text002@1002 : TextConst 'ENU=The journal lines were successfully posted.;ESP=Se han registrado correctamente las l¡neas del diario.';
      Text003@1003 : TextConst 'ENU=The journal lines were successfully posted. You are now in the %1 journal.;ESP=Se registraron correctamente las l¡neas diario. Se encuentra en el diario %1.';
      InsuranceJnlTempl@1004 : Record 5633;
      InsuranceJnlLine@1005 : Record 5635;
      InsuranceReg@1006 : Record 5636;
      InsuranceJnlPostBatch@1007 : Codeunit 5653;
      TempJnlBatchName@1008 : Code[10];

    LOCAL PROCEDURE Code@1();
    BEGIN
      WITH InsuranceJnlLine DO BEGIN
        InsuranceJnlTempl.GET("Journal Template Name");
        InsuranceJnlTempl.TESTFIELD("Posting Report ID");

        IF NOT CONFIRM(Text000,FALSE) THEN
          EXIT;

        TempJnlBatchName := "Journal Batch Name";

        InsuranceJnlPostBatch.RUN(InsuranceJnlLine);

        IF InsuranceReg.GET("Line No.") THEN BEGIN
          InsuranceReg.SETRECFILTER;
          REPORT.RUN(InsuranceJnlTempl."Posting Report ID",FALSE,FALSE,InsuranceReg);
        END;

        IF "Line No." = 0 THEN
          MESSAGE(Text001)
        ELSE
          IF TempJnlBatchName = "Journal Batch Name" THEN
            MESSAGE(Text002)
          ELSE
            MESSAGE(
              Text003,
              "Journal Batch Name");

        IF NOT FIND('=><') OR (TempJnlBatchName <> "Journal Batch Name") THEN BEGIN
          RESET;
          FILTERGROUP := 2;
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          FILTERGROUP := 0;
          "Line No." := 1;
        END;
      END;
    END;

    BEGIN
    END.
  }
}
