OBJECT Codeunit 443 Sales-Post Prepayment (Yes/No)
{
  OBJECT-PROPERTIES
  {
    Date=13/02/07;
    Time=12:00:00;
    Version List=NAVW15.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to post the prepayments for %1 %2?;ESP=¨Desea registrar los prepagos para %1 %2?';
      Text001@1001 : TextConst 'ENU=Do you want to post a credit memo for the prepayments for %1 %2?;ESP=¨Desea registrar un abono para los prepagos para %1 %2?';
      SalesInvHeader@1003 : Record 112;
      SalesCrMemoHeader@1002 : Record 114;

    PROCEDURE PostPrepmtInvoiceYN@1(VAR SalesHeader2@1001 : Record 36;Print@1002 : Boolean);
    VAR
      SalesHeader@1003 : Record 36;
      SalesPostPrepayments@1000 : Codeunit 442;
    BEGIN
      SalesHeader.COPY(SalesHeader2);
      WITH SalesHeader DO BEGIN
        IF NOT CONFIRM(Text000,FALSE,"Document Type","No.") THEN
          EXIT;

        SalesPostPrepayments.Invoice(SalesHeader);

        IF Print THEN
          GetReport(SalesHeader,0);

        COMMIT;
        SalesHeader2 := SalesHeader;
      END;
    END;

    PROCEDURE PostPrepmtCrMemoYN@2(VAR SalesHeader2@1001 : Record 36;Print@1002 : Boolean);
    VAR
      SalesHeader@1003 : Record 36;
      SalesPostPrepayments@1000 : Codeunit 442;
    BEGIN
      SalesHeader.COPY(SalesHeader2);
      WITH SalesHeader DO BEGIN
        IF NOT CONFIRM(Text001,FALSE,"Document Type","No.") THEN
          EXIT;

        SalesPostPrepayments.CreditMemo(SalesHeader);

        IF Print THEN
          GetReport(SalesHeader,1);

        COMMIT;
        SalesHeader2 := SalesHeader;
      END;
    END;

    PROCEDURE GetReport@4(VAR SalesHeader@1000 : Record 36;DocumentType@1001 : 'Invoice,Credit Memo');
    VAR
      ReportSelection@1003 : Record 77;
    BEGIN
      WITH SalesHeader DO BEGIN
        CASE DocumentType OF
          DocumentType::Invoice:
            BEGIN
              SalesInvHeader."No." := "Last Prepayment No.";
              SalesInvHeader.SETRECFILTER;
              PrintReport(ReportSelection.Usage::"S.Invoice");
            END;
          DocumentType::"Credit Memo":
            BEGIN
              SalesCrMemoHeader."No." := "Last Prepmt. Cr. Memo No.";
              SalesCrMemoHeader.SETRECFILTER;
              PrintReport(ReportSelection.Usage::"S.Cr.Memo");
            END;
        END;
      END;
    END;

    LOCAL PROCEDURE PrintReport@3(ReportUsage@1000 : Integer);
    VAR
      ReportSelection@1001 : Record 77;
    BEGIN
      ReportSelection.SETRANGE(Usage,ReportUsage);
      ReportSelection.FIND('-');
      REPEAT
        ReportSelection.TESTFIELD("Report ID");
        CASE ReportUsage OF
          ReportSelection.Usage::"S.Invoice":
            REPORT.RUN(ReportSelection."Report ID",FALSE,FALSE,SalesInvHeader);
          ReportSelection.Usage::"S.Cr.Memo":
            REPORT.RUN(ReportSelection."Report ID",FALSE,FALSE,SalesCrMemoHeader);
        END;
      UNTIL ReportSelection.NEXT = 0;
    END;

    BEGIN
    END.
  }
}
