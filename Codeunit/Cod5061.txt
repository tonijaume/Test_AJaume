OBJECT Codeunit 5061 SegHistoryManagement
{
  OBJECT-PROPERTIES
  {
    Date=08/06/01;
    Time=12:00:00;
    Version List=NAVW13.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      SegHist@1000 : Record 5078;
      SegHeader@1001 : Record 5076;

    PROCEDURE InsertLine@1(SegmentNo@1000 : Code[20];ContactNo@1001 : Code[20];LineNo@1002 : Integer);
    BEGIN
      InitLine(SegmentNo,ContactNo,LineNo);
      SegHist."Action Taken" := SegHist."Action Taken"::Insertion;
      SegHist.INSERT;
    END;

    PROCEDURE DeleteLine@2(SegmentNo@1000 : Code[20];ContactNo@1001 : Code[20];LineNo@1002 : Integer);
    BEGIN
      InitLine(SegmentNo,ContactNo,LineNo);
      SegHist."Action Taken" := SegHist."Action Taken"::Deletion;
      SegHist.INSERT;
    END;

    LOCAL PROCEDURE InitLine@5(SegmentNo@1000 : Code[20];ContactNo@1001 : Code[20];LineNo@1002 : Integer);
    BEGIN
      SegHeader.GET(SegmentNo);
      SegHeader.CALCFIELDS("No. of Criteria Actions");
      SegHist.INIT;
      SegHist."Segment No." := SegmentNo;
      SegHist."Segment Action No." := SegHeader."No. of Criteria Actions";
      SegHist."Segment Line No." := LineNo;
      SegHist."Contact No." := ContactNo;
    END;

    PROCEDURE GoBack@3(SegmentNo@1000 : Code[20]);
    VAR
      SegLine@1001 : Record 5077;
      SegCriteriaLine@1002 : Record 5097;
      NextLineNo@1003 : Integer;
    BEGIN
      SegHist.LOCKTABLE;
      SegHeader.GET(SegmentNo);
      SegHeader.CALCFIELDS("No. of Criteria Actions");

      SegLine.RESET;
      SegLine.SETRANGE("Segment No.",SegmentNo);
      IF SegLine.FIND('+') THEN
        NextLineNo := SegLine."Line No." + 10000
      ELSE
        NextLineNo := 10000;

      SegHist.SETRANGE("Segment No.",SegmentNo);
      SegHist.SETRANGE("Segment Action No.",SegHeader."No. of Criteria Actions");
      IF SegHist.FIND('+') THEN BEGIN
        SegHist.SETRANGE("Segment Action No.", SegHist."Segment Action No.");
        REPEAT
          CASE SegHist."Action Taken" OF
            SegHist."Action Taken"::Insertion: BEGIN
              SegLine.RESET;
              SegLine.SETRANGE("Segment No.",SegHist."Segment No.");
              SegLine.SETRANGE("Contact No.",SegHist."Contact No.");
              SegLine.DELETEALL(TRUE);
            END;
            SegHist."Action Taken"::Deletion: BEGIN
              SegLine.INIT;
              SegLine."Segment No." := SegmentNo;
              SegLine."Line No." := NextLineNo;
              SegLine.VALIDATE("Contact No.",SegHist."Contact No.");
              SegLine.INSERT(TRUE);
              NextLineNo := NextLineNo + 10000;
            END;
          END;
        UNTIL SegHist.NEXT(-1) = 0;
        SegHist.DELETEALL;
      END;

      SegCriteriaLine.SETRANGE("Segment No.",SegmentNo);
      IF SegCriteriaLine.FIND('+') THEN
        REPEAT
          SegCriteriaLine.DELETE;
        UNTIL (SegCriteriaLine.Type = SegCriteriaLine.Type::Action) OR (SegCriteriaLine.NEXT(-1) = 0);
    END;

    BEGIN
    END.
  }
}
