OBJECT Codeunit 6801 EP Web Part Request Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=25/05/05;
    Time=12:00:00;
    Version List=NAVW14.00.01;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=Only one table has been defined for this %1.;ESP=S¢lo se defini¢ una tabla para %1.';
      Text002@1001 : TextConst 'ENU=No header table has been defined for this %1.;ESP=No se defini¢ ninguna tabla de cabecera para %1.';
      Text003@1002 : TextConst 'ENU=No line table has been defined for this %1.;ESP=No se ha definido ninguna tabla de l¡nea para %1.';

    PROCEDURE GetTableNoByReqTypeForActions@1000000002(RequestType@1000000000 : Code[20];GroupCode@1000000003 : Code[20]) TableNo : Integer;
    VAR
      ReqType@1000000002 : Record 6805;
      ReqTypeTable@1000000001 : Record 6811;
    BEGIN
      ReqType.GET(GroupCode, RequestType);

      ReqTypeTable.RESET;
      ReqTypeTable.SETRANGE("Group Code", GroupCode);

      ReqTypeTable.SETRANGE("WP Request Code", RequestType);
      IF ReqType.Type = ReqType.Type::List THEN
        ReqTypeTable.SETRANGE("Header or Line",ReqTypeTable."Header or Line"::Line)
      ELSE
        ReqTypeTable.SETRANGE("Header or Line",ReqTypeTable."Header or Line"::Header);
      IF ReqTypeTable.FIND('-') THEN
        TableNo := ReqTypeTable."Table No."
      ELSE
        TableNo := 0;
    END;

    PROCEDURE GetFilterFieldsTable@1000000003(TableNo@1000000000 : Integer;VAR Fields@1000000001 : Record 2000000041);
    BEGIN
      Fields.RESET;
      Fields.FILTERGROUP(2);
      Fields.SETRANGE(TableNo,TableNo);
      Fields.FILTERGROUP(0);
      IF Fields.FIND('-') THEN;
    END;

    PROCEDURE GetTableCaptionbyNo@1000000001(TableNo@1000000001 : Integer) Caption : Text[70];
    VAR
      AllObjWithCaption@1000000000 : Record 2000000058;
    BEGIN
      IF AllObjWithCaption.GET(1,TableNo) THEN
        Caption := AllObjWithCaption."Object Caption"
      ELSE
        Caption := '';
    END;

    PROCEDURE ShowConnHeadTableConf@1000000004(VAR EPGrpReqTypeTables@1000000000 : Record 6811;FieldOrKey@1000000008 : Boolean);
    VAR
      EPGrpReqTypeTables2@1000000002 : Record 6811;
      EPConnHeadLine@1000 : Record 6815;
      EPWebPartRequest@1003 : Record 6805;
      frmConnHeadTableField@1002 : Form 6815;
      frmConnHeadTableKey@1001 : Form 6816;
      HeadTableNo@1000000003 : Integer;
      LineTableNo@1000000004 : Integer;
    BEGIN
      EPGrpReqTypeTables2.RESET;
      EPGrpReqTypeTables2.SETRANGE("Group Code",EPGrpReqTypeTables."Group Code");
      EPGrpReqTypeTables2.SETRANGE("WP Request Code",EPGrpReqTypeTables."WP Request Code");
      IF EPGrpReqTypeTables2.COUNT < 2 THEN
        ERROR(Text001,EPWebPartRequest.TABLECAPTION);


      // Get the Head TableNo
      EPGrpReqTypeTables2.RESET;
      EPGrpReqTypeTables2.SETRANGE("Group Code",EPGrpReqTypeTables."Group Code");
      EPGrpReqTypeTables2.SETRANGE("WP Request Code",EPGrpReqTypeTables."WP Request Code");
      EPGrpReqTypeTables2.SETRANGE("Header or Line",EPGrpReqTypeTables2."Header or Line"::Header);
      IF EPGrpReqTypeTables2.FIND('-') THEN
        HeadTableNo := EPGrpReqTypeTables2."Table No."
      ELSE
        ERROR(Text002,EPWebPartRequest.TABLECAPTION);

      // Get the Line TableNo
      EPGrpReqTypeTables2.RESET;
      EPGrpReqTypeTables2.SETRANGE("Group Code",EPGrpReqTypeTables."Group Code");
      EPGrpReqTypeTables2.SETRANGE("WP Request Code",EPGrpReqTypeTables."WP Request Code");
      EPGrpReqTypeTables2.SETRANGE("Header or Line",EPGrpReqTypeTables2."Header or Line"::Line);
      IF EPGrpReqTypeTables2.FIND('-') THEN
        LineTableNo := EPGrpReqTypeTables2."Table No."
      ELSE
        ERROR(Text003,EPWebPartRequest.TABLECAPTION);

      EPConnHeadLine.RESET;
      EPConnHeadLine.FILTERGROUP(4);
      EPConnHeadLine.SETRANGE("Group Code",EPGrpReqTypeTables."Group Code");
      EPConnHeadLine.SETRANGE("WP Request Code",EPGrpReqTypeTables."WP Request Code");
      EPConnHeadLine.SETRANGE("Header Table No.",HeadTableNo);
      IF NOT FieldOrKey THEN
        EPConnHeadLine.SETRANGE("Apply Filter to",EPConnHeadLine."Apply Filter to"::Field)
      ELSE
        EPConnHeadLine.SETRANGE("Apply Filter to",EPConnHeadLine."Apply Filter to"::Key);
      EPConnHeadLine.FILTERGROUP(0);

      IF NOT FieldOrKey THEN BEGIN
        frmConnHeadTableField.SetTableNos(HeadTableNo,LineTableNo);
        frmConnHeadTableField.SETTABLEVIEW(EPConnHeadLine);
        frmConnHeadTableField.RUNMODAL;
      END ELSE BEGIN
        frmConnHeadTableKey.SetTableNos(HeadTableNo,LineTableNo);
        frmConnHeadTableKey.SETTABLEVIEW(EPConnHeadLine);
        frmConnHeadTableKey.RUNMODAL;
      END;
    END;

    BEGIN
    END.
  }
}
