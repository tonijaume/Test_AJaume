OBJECT Codeunit 99000753 Routing Line-Copy Lines
{
  OBJECT-PROPERTIES
  {
    Date=08/06/01;
    Time=12:00:00;
    Version List=NAVW13.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=The %1 cannot be copied to itself.;ESP=%1 no puede copiarse en s¡ misma.';
      Text001@1001 : TextConst 'ENU=%1 on %2 %3 must not be %4;ESP=%1 en %2 %3 no debe ser %4';
      Text002@1002 : TextConst 'ENU=%1 on %2 %3 %4 must not be %5;ESP=%1 en %2 %3 %4 no debe ser %5';

    PROCEDURE CopyRouting@1(FromRtngHeaderNo@1000 : Code[20];FromVersionCode@1001 : Code[10];VAR RtngHeader@1002 : Record 99000763;ToVersionCode@1003 : Code[10]);
    VAR
      RtngVersion@1004 : Record 99000786;
      RtngLine@1005 : Record 99000764;
      RtngLine2@1006 : Record 99000764;
      RtngTool@1007 : Record 99000802;
      FromRoutTool@1008 : Record 99000802;
      RtngPersonnel@1009 : Record 99000803;
      FromRtngPersonnel@1010 : Record 99000803;
      RtngQltyMeas@1011 : Record 99000805;
      FromRtngQltyMeas@1012 : Record 99000805;
      RtngComment@1013 : Record 99000775;
      FromRtngComment@1014 : Record 99000775;
    BEGIN
      IF (FromRtngHeaderNo = RtngHeader."No.") AND
         (FromVersionCode = ToVersionCode)
      THEN
        ERROR(Text000,RtngHeader.TABLECAPTION);

      IF ToVersionCode = '' THEN BEGIN
        IF RtngHeader.Status = RtngHeader.Status::Certified THEN
          ERROR(
            Text001,
            RtngHeader.FIELDCAPTION(Status),
            RtngHeader.TABLECAPTION,
            RtngHeader."No.",
            RtngHeader.Status);
      END ELSE BEGIN
        RtngVersion.GET(RtngHeader."No.",ToVersionCode);
        IF RtngVersion.Status = RtngVersion.Status::Certified THEN
          ERROR(
            Text002,
            RtngVersion.FIELDCAPTION(Status),
            RtngVersion.TABLECAPTION,
            RtngVersion."Routing No.",
            RtngVersion."Version Code",
            RtngVersion.Status);
      END;

      RtngLine.SETRANGE("Routing No.",RtngHeader."No.");
      RtngLine.SETRANGE("Version Code",ToVersionCode);
      RtngLine.DELETEALL;

      RtngTool.SETRANGE("Routing No.",RtngHeader."No.");
      RtngTool.SETRANGE("Version Code",ToVersionCode);
      RtngTool.DELETEALL;

      RtngPersonnel.SETRANGE("Routing No.",RtngHeader."No.");
      RtngPersonnel.SETRANGE("Version Code",ToVersionCode);
      RtngPersonnel.DELETEALL;

      RtngQltyMeas.SETRANGE("Routing No.",RtngHeader."No.");
      RtngQltyMeas.SETRANGE("Version Code",ToVersionCode);
      RtngQltyMeas.DELETEALL;

      RtngComment.SETRANGE("Routing No.",RtngHeader."No.");
      RtngComment.SETRANGE("Version Code",ToVersionCode);
      RtngComment.DELETEALL;

      RtngLine2.SETRANGE("Routing No.",FromRtngHeaderNo);
      RtngLine2.SETRANGE("Version Code",FromVersionCode);

      IF RtngLine2.FIND('-') THEN
        REPEAT
          RtngLine := RtngLine2;
          RtngLine."Routing No." := RtngHeader."No.";
          RtngLine."Version Code" := ToVersionCode;
          RtngLine.INSERT;
        UNTIL RtngLine2.NEXT = 0;

      FromRoutTool.SETRANGE("Routing No.",FromRtngHeaderNo);
      FromRoutTool.SETRANGE("Version Code",FromVersionCode);
      IF FromRoutTool.FIND('-') THEN
        REPEAT
          RtngTool := FromRoutTool;
          RtngTool."Routing No." := RtngHeader."No.";
          RtngTool."Version Code" := ToVersionCode;
          RtngTool.INSERT;
        UNTIL FromRoutTool.NEXT = 0;

      FromRtngPersonnel.SETRANGE("Routing No.",FromRtngHeaderNo);
      FromRtngPersonnel.SETRANGE("Version Code",FromVersionCode);
      IF FromRtngPersonnel.FIND('-') THEN
        REPEAT
          RtngPersonnel := FromRtngPersonnel;
          RtngPersonnel."Routing No." := RtngHeader."No.";
          RtngPersonnel."Version Code" := ToVersionCode;
          RtngPersonnel.INSERT;
        UNTIL FromRtngPersonnel.NEXT = 0;

      FromRtngQltyMeas.SETRANGE("Routing No.",FromRtngHeaderNo);
      FromRtngQltyMeas.SETRANGE("Version Code",FromVersionCode);
      IF FromRtngQltyMeas.FIND('-') THEN
        REPEAT
          RtngQltyMeas := FromRtngQltyMeas;
          RtngQltyMeas."Routing No." := RtngHeader."No.";
          RtngQltyMeas."Version Code" := ToVersionCode;
          RtngQltyMeas.INSERT;
        UNTIL FromRtngQltyMeas.NEXT = 0;

      FromRtngComment.SETRANGE("Routing No.",FromRtngHeaderNo);
      FromRtngComment.SETRANGE("Version Code",FromVersionCode);
      IF FromRtngComment.FIND('-') THEN
        REPEAT
          RtngComment := FromRtngComment;
          RtngComment."Routing No." := RtngHeader."No.";
          RtngComment."Version Code" := ToVersionCode;
          RtngComment.INSERT;
        UNTIL FromRtngComment.NEXT = 0;
    END;

    PROCEDURE SelectCopyFromVersionList@2(VAR FromRtngVersion@1000 : Record 99000786);
    VAR
      RtngHeader@1001 : Record 99000763;
      OldRtngVersion@1002 : Record 99000786;
    BEGIN
      OldRtngVersion := FromRtngVersion;

      RtngHeader."No." := FromRtngVersion."Routing No.";
      IF FORM.RUNMODAL(0,FromRtngVersion) = ACTION::LookupOK THEN BEGIN
        IF OldRtngVersion.Status = OldRtngVersion.Status::Certified THEN
          ERROR(
            Text002,
            OldRtngVersion.FIELDCAPTION(Status),
            OldRtngVersion.TABLECAPTION,
            OldRtngVersion."Routing No.",
            OldRtngVersion."Version Code",
            OldRtngVersion.Status);
        CopyRouting(RtngHeader."No.",FromRtngVersion."Version Code",RtngHeader,OldRtngVersion."Version Code");
      END;
      FromRtngVersion := OldRtngVersion;
    END;

    BEGIN
    END.
  }
}
