OBJECT Codeunit 5837 Additional-Currency Management
{
  OBJECT-PROPERTIES
  {
    Date=17/12/01;
    Time=12:00:00;
    Version List=NAVW13.10;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      GLSetup@1000 : Record 98;
      Currency@1001 : Record 4;
      GLSetupRead@1002 : Boolean;
      CurrencyRead@1003 : Boolean;

    LOCAL PROCEDURE InitCodeunit@4() : Boolean;
    BEGIN
      IF NOT GLSetupRead THEN BEGIN
        GLSetup.GET;
        GLSetupRead := TRUE;
      END;
      IF GLSetup."Additional Reporting Currency" = '' THEN
        EXIT;
      IF NOT CurrencyRead THEN BEGIN
        Currency.GET(GLSetup."Additional Reporting Currency");
        Currency.TESTFIELD("Unit-Amount Rounding Precision");
        Currency.TESTFIELD("Amount Rounding Precision");
        CurrencyRead := TRUE;
      END;
      EXIT(TRUE);
    END;

    PROCEDURE CalcACYAmt@16(Amount@1000 : Decimal;PostingDate@1001 : Date;IsUnitAmount@1002 : Boolean) : Decimal;
    VAR
      CurrExchRate@1003 : Record 330;
    BEGIN
      IF NOT InitCodeunit THEN
        EXIT;
      EXIT(
        RoundACYAmt(
          CurrExchRate.ExchangeAmtLCYToFCY(
            PostingDate,GLSetup."Additional Reporting Currency",Amount,
            CurrExchRate.ExchangeRate(PostingDate,GLSetup."Additional Reporting Currency")),
            IsUnitAmount));
    END;

    PROCEDURE RoundACYAmt@1(UnroundedACYAmt@1000 : Decimal;IsUnitAmount@1001 : Boolean) : Decimal;
    VAR
      RndgPrec@1002 : Decimal;
    BEGIN
      IF NOT InitCodeunit THEN
        EXIT;
      IF IsUnitAmount THEN
        RndgPrec := Currency."Unit-Amount Rounding Precision"
      ELSE
        RndgPrec := Currency."Amount Rounding Precision";
      EXIT(ROUND(UnroundedACYAmt,RndgPrec));
    END;

    BEGIN
    END.
  }
}
