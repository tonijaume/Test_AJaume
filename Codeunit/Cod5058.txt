OBJECT Codeunit 5058 BankCont-Update
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      RMSetup@1000 : Record 5079;
      ContBusRel@1001 : Record 5054;
      Cont@1002 : Record 5050;
      OldCont@1003 : Record 5050;
      NoSeriesMgt@1004 : Codeunit 396;
      ContNo@1005 : Code[20];
      NoSeries@1006 : Code[10];
      SalespersonCode@1007 : Code[10];

    PROCEDURE OnInsert@3(VAR BankAcc@1000 : Record 270);
    BEGIN
      RMSetup.GET;
      IF RMSetup."Bus. Rel. Code for Bank Accs." = '' THEN
        EXIT;

      InsertNewContact(BankAcc,TRUE);
    END;

    PROCEDURE OnModify@1(VAR BankAcc@1000 : Record 270);
    VAR
      ChangeLogMgt@1001 : Codeunit 423;
      RecRef@1003 : RecordRef;
      xRecRef@1002 : RecordRef;
    BEGIN
      WITH ContBusRel DO BEGIN
        SETCURRENTKEY("Link to Table","No.");
        SETRANGE("Link to Table","Link to Table"::"Bank Account");
        SETRANGE("No.",BankAcc."No.");
        IF NOT FIND('-') THEN
          EXIT;
        Cont.GET("Contact No.");
        OldCont := Cont;
      END;

      ContNo := Cont."No.";
      NoSeries := Cont."No. Series";
      SalespersonCode := Cont."Salesperson Code";
      Cont.VALIDATE("E-Mail", BankAcc."E-Mail");
      Cont.TRANSFERFIELDS(BankAcc);
      Cont."No." := ContNo ;
      Cont."No. Series" := NoSeries;
      Cont."Salesperson Code" := SalespersonCode;
      Cont.VALIDATE(Name);
      Cont.OnModify(OldCont);
      Cont.MODIFY(TRUE);

      xRecRef.GETTABLE(OldCont);
      RecRef.GETTABLE(Cont);
      ChangeLogMgt.LogModification(RecRef,xRecRef);
    END;

    PROCEDURE OnDelete@2(VAR BankAcc@1000 : Record 270);
    BEGIN
      WITH ContBusRel DO BEGIN
        SETCURRENTKEY("Link to Table","No.");
        SETRANGE("Link to Table","Link to Table"::"Bank Account");
        SETRANGE("No.", BankAcc."No.");
        DELETEALL(TRUE);
      END;
    END;

    PROCEDURE InsertNewContact@4(VAR BankAcc@1000 : Record 270;LocalCall@1001 : Boolean);
    BEGIN
      IF NOT LocalCall THEN BEGIN
        RMSetup.GET;
        RMSetup.TESTFIELD("Bus. Rel. Code for Bank Accs.");
      END;

      WITH Cont DO BEGIN
        INIT;
        TRANSFERFIELDS(BankAcc);
        Cont.VALIDATE(Name);
        Cont.VALIDATE("E-Mail");
        "No." := '';
        "No. Series" := '';
        RMSetup.TESTFIELD("Contact Nos.");
        NoSeriesMgt.InitSeries(RMSetup."Contact Nos.",'',0D,"No.","No. Series");
        Type := Type::Company;
        TypeChange;
        SetSkipDefault(FALSE);
        INSERT(TRUE);
      END;

      WITH ContBusRel DO BEGIN
        INIT;
        "Contact No." := Cont."No.";
        "Business Relation Code" := RMSetup."Bus. Rel. Code for Bank Accs.";
        "Link to Table" := "Link to Table"::"Bank Account";
        "No." := BankAcc."No.";
        INSERT(TRUE);
      END;
    END;

    BEGIN
    END.
  }
}
