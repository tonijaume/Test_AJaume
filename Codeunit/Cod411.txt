OBJECT Codeunit 411 Dimension Buffer Management
{
  OBJECT-PROPERTIES
  {
    Date=12/01/06;
    Time=12:00:00;
    Version List=NAVW14.00.02;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      TempDimBuf@1000 : TEMPORARY Record 360;
      TempDimEntryBuf@1001 : TEMPORARY Record 373;
      NextDimBufNo@1004 : Integer;
      DimensionIDBuffer@1003 : TEMPORARY Record 353;
      LowerNextDimBufNo@1002 : Integer;

    PROCEDURE InsertDimensions@2(VAR DimBuf@1000 : Record 360) : Integer;
    VAR
      NewEntryNo@1001 : Integer;
    BEGIN
      IF DimBuf.FIND('-') THEN BEGIN
        TempDimBuf.RESET;
        IF TempDimBuf.FIND('+') THEN
          NewEntryNo := TempDimBuf."Entry No." + 1
        ELSE
          NewEntryNo := 1;
        InsertDimensionsUsingEntryNo(DimBuf,NewEntryNo);
        EXIT(NewEntryNo);
      END ELSE
        EXIT(0);
    END;

    PROCEDURE InsertDimensionsUsingEntryNo@6(VAR DimBuf@1000 : Record 360;EntryNo@1001 : Integer);
    VAR
      DimCount@1002 : Integer;
    BEGIN
      DimCount := DimBuf.COUNT;
      IF DimBuf.FIND('-') THEN
        REPEAT
          TempDimBuf.INIT;
          TempDimBuf := DimBuf;
          TempDimBuf."Entry No." := EntryNo;
          TempDimBuf."No. Of Dimensions" := DimCount;
          TempDimBuf.INSERT;
        UNTIL DimBuf.NEXT = 0;
    END;

    PROCEDURE FindDimensions@3(VAR DimBuf@1000 : Record 360) : Integer;
    VAR
      Found@1003 : Boolean;
      EndOfDimBuf@1004 : Boolean;
      EndOfTempDimBuf@1005 : Boolean;
      PrevEntryNo@1001 : Integer;
    BEGIN
      IF NOT DimBuf.FIND('-') THEN
        EXIT(0);

      TempDimBuf.RESET;
      TempDimBuf.SETCURRENTKEY("No. Of Dimensions");
      TempDimBuf.SETRANGE("No. Of Dimensions",DimBuf.COUNT);
      TempDimBuf.SETRANGE("Table ID",DimBuf."Table ID");
      TempDimBuf.SETRANGE("Dimension Code",DimBuf."Dimension Code");
      TempDimBuf.SETRANGE("Dimension Value Code",DimBuf."Dimension Value Code");
      IF NOT TempDimBuf.FIND('-') THEN BEGIN
        TempDimBuf.RESET;
        EXIT(0);
      END;
      IF TempDimBuf."No. Of Dimensions" = 1 THEN BEGIN
        TempDimBuf.RESET;
        EXIT(TempDimBuf."Entry No.");
      END;

      DimBuf.NEXT;
      WHILE (NOT EndOfTempDimBuf) AND (NOT Found) DO BEGIN
        PrevEntryNo := TempDimBuf."Entry No.";
        EndOfDimBuf := FALSE;
        TempDimBuf.SETFILTER("Entry No.",'>=%1',TempDimBuf."Entry No.");
        REPEAT
          TempDimBuf.SETRANGE("Dimension Code",DimBuf."Dimension Code");
          TempDimBuf.SETRANGE("Dimension Value Code",DimBuf."Dimension Value Code");
          EndOfTempDimBuf := NOT TempDimBuf.FIND('-');
          IF NOT EndOfTempDimBuf THEN
            EndOfDimBuf := DimBuf.NEXT = 0;
        UNTIL EndOfTempDimBuf OR EndOfDimBuf OR (PrevEntryNo <> TempDimBuf."Entry No.");
        IF EndOfDimBuf AND (PrevEntryNo = TempDimBuf."Entry No.") THEN
          Found := TRUE
        ELSE
          DimBuf.FIND('-');
      END;
      TempDimBuf.RESET;
      IF Found THEN
        EXIT(TempDimBuf."Entry No.")
      ELSE
        EXIT(0);
    END;

    PROCEDURE GetDimensions@4(EntryNo@1000 : Integer;VAR DimBuf@1001 : Record 360) : Boolean;
    BEGIN
      TempDimBuf.SETRANGE("Entry No.",EntryNo);
      IF NOT TempDimBuf.FIND('-') THEN
        EXIT(FALSE)
      ELSE
        REPEAT
          DimBuf.INIT;
          DimBuf := TempDimBuf;
          DimBuf.INSERT;
        UNTIL TempDimBuf.NEXT = 0;
      EXIT(TRUE);
    END;

    PROCEDURE DeleteAllDimensions@5();
    BEGIN
      TempDimBuf.RESET;
      TempDimBuf.DELETEALL;
    END;

    PROCEDURE CollectDimEntryNo@1(VAR SelectedDim@1000 : Record 369;TableID@1001 : Integer;EntryNo@1002 : Integer;ForgetDimEntryNo@1003 : Integer;DoCollect@1004 : Boolean;VAR DimEntryNo@1005 : Integer);
    VAR
      LedgEntryDim@1006 : Record 355;
      TempDimBuf@1007 : TEMPORARY Record 360;
    BEGIN
      IF SelectedDim.FIND('-') THEN BEGIN
        REPEAT
          IF LedgEntryDim.GET(TableID,EntryNo,SelectedDim."Dimension Code") THEN BEGIN
            TempDimBuf."Table ID" := LedgEntryDim."Table ID";
            TempDimBuf."Dimension Code" := LedgEntryDim."Dimension Code";
            TempDimBuf."Dimension Value Code" := LedgEntryDim."Dimension Value Code";
            TempDimBuf.INSERT;
          END;
        UNTIL SelectedDim.NEXT = 0;
        DimEntryNo := FindDimensions(TempDimBuf);
        IF DimEntryNo = 0 THEN
          DimEntryNo := InsertDimensions(TempDimBuf);
      END ELSE
        DimEntryNo := 0;

      IF (DimEntryNo <> ForgetDimEntryNo) AND DoCollect THEN BEGIN
        TempDimEntryBuf."No." := EntryNo;
        TempDimEntryBuf."Dimension Entry No." := DimEntryNo;
        TempDimEntryBuf.INSERT;
      END;
    END;

    PROCEDURE CollectBudgetDimEntryNo@11(VAR SelectedDim@1000 : Record 369;EntryNo@1001 : Integer;ForgetDimEntryNo@1002 : Integer;DoCollect@1003 : Boolean;VAR DimEntryNo@1004 : Integer);
    VAR
      GLBudgetDim@1005 : Record 361;
      TempDimBuf@1006 : TEMPORARY Record 360;
    BEGIN
      IF SelectedDim.FIND('-') THEN BEGIN
        REPEAT
          IF GLBudgetDim.GET(EntryNo,SelectedDim."Dimension Code") THEN BEGIN
            TempDimBuf."Table ID" := DATABASE::"G/L Budget Dimension";
            TempDimBuf."Dimension Code" := GLBudgetDim."Dimension Code";
            TempDimBuf."Dimension Value Code" := GLBudgetDim."Dimension Value Code";
            TempDimBuf.INSERT;
          END;
        UNTIL SelectedDim.NEXT = 0;
        DimEntryNo := FindDimensions(TempDimBuf);
        IF DimEntryNo = 0 THEN
          DimEntryNo := InsertDimensions(TempDimBuf);
      END ELSE
        DimEntryNo := 0;

      IF (DimEntryNo <> ForgetDimEntryNo) AND DoCollect THEN BEGIN
        TempDimEntryBuf."No." := EntryNo;
        TempDimEntryBuf."Dimension Entry No." := DimEntryNo;
        TempDimEntryBuf.INSERT;
      END;
    END;

    PROCEDURE FindFirstDimEntryNo@7(VAR DimEntryNo@1000 : Integer;VAR EntryNo@1001 : Integer) : Boolean;
    VAR
      Found@1002 : Boolean;
    BEGIN
      TempDimEntryBuf.SETCURRENTKEY("Dimension Entry No.");
      Found := TempDimEntryBuf.FIND('-');
      DimEntryNo := TempDimEntryBuf."Dimension Entry No.";
      EntryNo := TempDimEntryBuf."No.";
      EXIT(Found);
    END;

    PROCEDURE NextDimEntryNo@8(VAR DimEntryNo@1000 : Integer;VAR EntryNo@1001 : Integer) : Boolean;
    VAR
      Found@1002 : Boolean;
    BEGIN
      Found := TempDimEntryBuf.NEXT <> 0;
      DimEntryNo := TempDimEntryBuf."Dimension Entry No.";
      EntryNo := TempDimEntryBuf."No.";
      EXIT(Found);
    END;

    PROCEDURE DeleteAllDimEntryNo@9();
    BEGIN
      TempDimEntryBuf.DELETEALL;
    END;

    PROCEDURE CollectItemBudgetDimEntryNo@10(VAR SelectedDim@1004 : Record 369;EntryNo@1003 : Integer;ForgetDimEntryNo@1002 : Integer;DoCollect@1001 : Boolean;VAR DimEntryNo@1000 : Integer);
    VAR
      ItemBudgetDim@1006 : Record 7135;
      TempDimBuf@1005 : TEMPORARY Record 360;
    BEGIN
      IF SelectedDim.FIND('-') THEN BEGIN
        REPEAT
          IF ItemBudgetDim.GET(EntryNo,SelectedDim."Dimension Code") THEN BEGIN
            TempDimBuf."Table ID" := DATABASE::"Item Budget Dimension";
            TempDimBuf."Dimension Code" := ItemBudgetDim."Dimension Code";
            TempDimBuf."Dimension Value Code" := ItemBudgetDim."Dimension Value Code";
            TempDimBuf.INSERT;
          END;
        UNTIL SelectedDim.NEXT = 0;
        DimEntryNo := FindDimensions(TempDimBuf);
        IF DimEntryNo = 0 THEN
          DimEntryNo := InsertDimensions(TempDimBuf);
      END ELSE
        DimEntryNo := 0;

      IF (DimEntryNo <> ForgetDimEntryNo) AND DoCollect THEN BEGIN
        TempDimEntryBuf."No." := EntryNo;
        TempDimEntryBuf."Dimension Entry No." := DimEntryNo;
        TempDimEntryBuf.INSERT;
      END;
    END;

    PROCEDURE GetDimensionId@13(VAR Dimbuf@1000 : Record 360) : Integer;
    VAR
      NewDimensionComb@1001 : Boolean;
    BEGIN
      IF NOT Dimbuf.FINDFIRST THEN
        EXIT(0);

      IF NextDimBufNo = 0 THEN
        NextDimBufNo := 1;

      NewDimensionComb := FALSE;
      DimensionIDBuffer.ID := 0;
      REPEAT
        IF NewDimensionComb THEN
          InsertDimIdBuf(Dimbuf)
        ELSE IF NOT DimensionIDBuffer.GET(DimensionIDBuffer.ID,Dimbuf."Dimension Code",Dimbuf."Dimension Value Code") THEN BEGIN
          NewDimensionComb := TRUE;
          InsertDimIdBuf(Dimbuf);
        END;
      UNTIL Dimbuf.NEXT = 0;

      EXIT(DimensionIDBuffer.ID);
    END;

    PROCEDURE RetrieveDimensions@12(DimId@1000 : Integer;VAR DimBuf@1001 : Record 360);
    BEGIN
      DimBuf.RESET;
      DimBuf.DELETEALL;

      IF DimId = 0 THEN
        EXIT;

      DimensionIDBuffer.SETCURRENTKEY(ID);
      DimensionIDBuffer.SETRANGE(ID,DimId);
      REPEAT
        DimensionIDBuffer.FIND('-');
        DimBuf.INIT;
        DimBuf."Entry No." := DimId;
        DimBuf."Dimension Code" := DimensionIDBuffer."Dimension Code";
        DimBuf."Dimension Value Code" := DimensionIDBuffer."Dimension Value";
        DimBuf.INSERT;
        DimensionIDBuffer.SETRANGE(ID,DimensionIDBuffer."Parent ID");
      UNTIL DimensionIDBuffer."Parent ID" = 0;
    END;

    LOCAL PROCEDURE InsertDimIdBuf@15(VAR DimBuf@1000 : Record 360);
    BEGIN
      DimensionIDBuffer."Parent ID" := DimensionIDBuffer.ID;
      DimensionIDBuffer."Dimension Code" := DimBuf."Dimension Code";
      DimensionIDBuffer."Dimension Value" := DimBuf."Dimension Value Code";
      DimensionIDBuffer.ID := NextDimBufNo;
      NextDimBufNo += 1;
      DimensionIDBuffer.INSERT;
    END;

    BEGIN
    END.
  }
}
