OBJECT Codeunit 14 Gen. Jnl.-Show Entries
{
  OBJECT-PROPERTIES
  {
    Date=13/02/07;
    Time=12:00:00;
    Version List=NAVW15.00;
  }
  PROPERTIES
  {
    TableNo=81;
    OnRun=BEGIN
            CASE "Account Type" OF
              "Account Type"::"G/L Account":
                BEGIN
                  GLEntry.SETCURRENTKEY("G/L Account No.","Posting Date");
                  GLEntry.SETRANGE("G/L Account No.","Account No.");
                  IF GLEntry.FINDLAST THEN;
                  FORM.RUN(FORM::"General Ledger Entries",GLEntry);
                END;
              "Account Type"::Customer:
                BEGIN
                  CustLedgEntry.SETCURRENTKEY("Customer No.","Posting Date");
                  CustLedgEntry.SETRANGE("Customer No.","Account No.");
                  IF CustLedgEntry.FINDLAST THEN;
                  FORM.RUN(FORM::"Customer Ledger Entries",CustLedgEntry);
                END;
              "Account Type"::Vendor:
                BEGIN
                  VendLedgEntry.SETCURRENTKEY("Vendor No.","Posting Date");
                  VendLedgEntry.SETRANGE("Vendor No.","Account No.");
                  IF VendLedgEntry.FINDLAST THEN;
                  FORM.RUN(FORM::"Vendor Ledger Entries",VendLedgEntry);
                END;
              "Account Type"::"Bank Account":
                BEGIN
                  BankAccLedgEntry.SETCURRENTKEY("Bank Account No.","Posting Date");
                  BankAccLedgEntry.SETRANGE("Bank Account No.","Account No.");
                  IF BankAccLedgEntry.FINDLAST THEN;
                  FORM.RUN(FORM::"Bank Account Ledger Entries",BankAccLedgEntry);
                END;
              "Account Type"::"Fixed Asset":
                BEGIN
                  IF "FA Posting Type" <> "FA Posting Type"::Maintenance THEN BEGIN
                    FALedgEntry.SETCURRENTKEY("FA No.","Depreciation Book Code","FA Posting Date");
                    FALedgEntry.SETRANGE("FA No.","Account No.");
                    IF "Depreciation Book Code" <> '' THEN
                      FALedgEntry.SETRANGE("Depreciation Book Code","Depreciation Book Code");
                    IF FALedgEntry.FINDLAST THEN;
                    FORM.RUN(FORM::"FA Ledger Entries",FALedgEntry);
                  END ELSE BEGIN
                    MaintenanceLedgEntry.SETCURRENTKEY("FA No.","Depreciation Book Code","FA Posting Date");
                    MaintenanceLedgEntry.SETRANGE("FA No.","Account No.");
                    IF "Depreciation Book Code" <> '' THEN
                      MaintenanceLedgEntry.SETRANGE("Depreciation Book Code","Depreciation Book Code");
                    IF MaintenanceLedgEntry.FINDLAST THEN;
                    FORM.RUN(FORM::"Maintenance Ledger Entries",MaintenanceLedgEntry);
                  END;
                END;
              "Account Type"::"IC Partner":
                ERROR(Text001);
            END;
          END;

  }
  CODE
  {
    VAR
      GLEntry@1000 : Record 17;
      CustLedgEntry@1001 : Record 21;
      VendLedgEntry@1002 : Record 25;
      BankAccLedgEntry@1003 : Record 271;
      FALedgEntry@1004 : Record 5601;
      MaintenanceLedgEntry@1005 : Record 5625;
      Text001@1006 : TextConst 'ENU=Intercompany partners do not have ledger entries.;ESP=Las empresas vinculadas asociadas no tienen movimientos en el diario.';

    BEGIN
    END.
  }
}
