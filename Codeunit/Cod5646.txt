OBJECT Codeunit 5646 FAReclassJnlManagement
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    Permissions=TableData 5622=imd,
                TableData 5623=imd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=RECLASSIFY;ESP=RECLASIF.';
      Text001@1001 : TextConst 'ENU=FA Reclass. Journal;ESP=A/F Diario reclasif.';
      Text002@1002 : TextConst 'ENU=DEFAULT;ESP=GENERICO';
      Text003@1003 : TextConst 'ENU=Default Journal;ESP=Diario gen‚rico';
      OldFANo@1004 : Code[20];
      OldFANo2@1005 : Code[20];
      OpenFromBatch@1006 : Boolean;

    PROCEDURE TemplateSelection@4(FormID@1003 : Integer;VAR FAReclassJnlLine@1002 : Record 5624;VAR JnlSelected@1001 : Boolean);
    VAR
      FAReclassJnlTempl@1000 : Record 5622;
    BEGIN
      JnlSelected := TRUE;

      FAReclassJnlTempl.RESET;
      FAReclassJnlTempl.SETRANGE("Form ID",FormID);

      CASE FAReclassJnlTempl.COUNT OF
        0:
          BEGIN
            FAReclassJnlTempl.INIT;
            FAReclassJnlTempl.Name := Text000;
            FAReclassJnlTempl.Description := Text001;
            FAReclassJnlTempl.VALIDATE("Form ID");
            FAReclassJnlTempl.INSERT;
            COMMIT;
          END;
        1:
          FAReclassJnlTempl.FIND('-');
        ELSE
          JnlSelected := FORM.RUNMODAL(0,FAReclassJnlTempl) = ACTION::LookupOK;
      END;
      IF JnlSelected THEN BEGIN
        FAReclassJnlLine.FILTERGROUP := 2;
        FAReclassJnlLine.SETRANGE("Journal Template Name",FAReclassJnlTempl.Name);
        FAReclassJnlLine.FILTERGROUP := 0;
        IF OpenFromBatch THEN BEGIN
          FAReclassJnlLine."Journal Template Name" := '';
          FORM.RUN(FAReclassJnlTempl."Form ID",FAReclassJnlLine);
        END;

      END;
    END;

    PROCEDURE TemplateSelectionFromBatch@5(VAR FAReclassJnlBatch@1000 : Record 5623);
    VAR
      FAReclassJnlLine@1002 : Record 5624;
      FAReclassJnlTempl@1003 : Record 5622;
      JnlSelected@1001 : Boolean;
    BEGIN
      OpenFromBatch := TRUE;
      FAReclassJnlTempl.GET(FAReclassJnlBatch."Journal Template Name");
      FAReclassJnlTempl.TESTFIELD("Form ID");
      FAReclassJnlBatch.TESTFIELD(Name);

      FAReclassJnlLine.FILTERGROUP := 2;
      FAReclassJnlLine.SETRANGE("Journal Template Name",FAReclassJnlTempl.Name);
      FAReclassJnlLine.FILTERGROUP := 0;

      FAReclassJnlLine."Journal Template Name" := '';
      FAReclassJnlLine."Journal Batch Name" := FAReclassJnlBatch.Name;
      FORM.RUN(FAReclassJnlTempl."Form ID",FAReclassJnlLine);
    END;

    PROCEDURE OpenJournal@1(VAR CurrentJnlBatchName@1000 : Code[10];VAR FAReclassJnlLine@1001 : Record 5624);
    BEGIN
      CheckTemplateName(FAReclassJnlLine.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName);
      FAReclassJnlLine.FILTERGROUP := 2;
      FAReclassJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      FAReclassJnlLine.FILTERGROUP := 0;
    END;

    PROCEDURE OpenJnlBatch@12(VAR FAReclassJnlBatch@1000 : Record 5623);
    VAR
      FAReclassJnlTemplate@1002 : Record 5622;
      FAReclassJnlLine@1004 : Record 5624;
      FAReclassJnlBatch2@1001 : Record 5623;
      JnlSelected@1003 : Boolean;
    BEGIN
      IF FAReclassJnlBatch.GETFILTER("Journal Template Name") <> '' THEN
        EXIT;
      FAReclassJnlBatch.FILTERGROUP(2);
      IF FAReclassJnlBatch.GETFILTER("Journal Template Name") <> '' THEN BEGIN
        FAReclassJnlBatch.FILTERGROUP(0);
        EXIT;
      END;
      FAReclassJnlBatch.FILTERGROUP(0);

      IF NOT FAReclassJnlBatch.FIND('-') THEN BEGIN
        IF NOT FAReclassJnlTemplate.FIND('-') THEN
          TemplateSelection(0,FAReclassJnlLine,JnlSelected);
        IF FAReclassJnlTemplate.FIND('-') THEN
          CheckTemplateName(FAReclassJnlTemplate.Name,FAReclassJnlBatch.Name);
      END;
      FAReclassJnlBatch.FIND('-');
      JnlSelected := TRUE;
      IF FAReclassJnlBatch.GETFILTER("Journal Template Name") <> '' THEN
         FAReclassJnlTemplate.SETRANGE(Name,FAReclassJnlBatch.GETFILTER("Journal Template Name"));
      CASE FAReclassJnlTemplate.COUNT OF
        1:
          FAReclassJnlTemplate.FIND('-');
        ELSE
          JnlSelected := FORM.RUNMODAL(0,FAReclassJnlTemplate) = ACTION::LookupOK;
      END;
      IF NOT JnlSelected THEN
        ERROR('');

      FAReclassJnlBatch.FILTERGROUP(2);
      FAReclassJnlBatch.SETRANGE("Journal Template Name",FAReclassJnlTemplate.Name);
      FAReclassJnlBatch.FILTERGROUP(0);
    END;

    PROCEDURE CheckName@6(CurrentJnlBatchName@1000 : Code[10];VAR FAReclassJnlLine@1001 : Record 5624);
    VAR
      FAReclassJnlBatch@1002 : Record 5623;
    BEGIN
      FAReclassJnlBatch.GET(FAReclassJnlLine.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName);
    END;

    PROCEDURE CheckTemplateName@2(CurrentJnlTemplateName@1000 : Code[10];VAR CurrentJnlBatchName@1001 : Code[10]);
    VAR
      FAReclassJnlBatch@1002 : Record 5623;
    BEGIN
      FAReclassJnlBatch.SETRANGE("Journal Template Name",CurrentJnlTemplateName);
      IF NOT FAReclassJnlBatch.GET(CurrentJnlTemplateName,CurrentJnlBatchName) THEN BEGIN
        IF NOT FAReclassJnlBatch.FIND('-') THEN BEGIN
          FAReclassJnlBatch.INIT;
          FAReclassJnlBatch."Journal Template Name" := CurrentJnlTemplateName;
          FAReclassJnlBatch.Name := Text002;
          FAReclassJnlBatch.INSERT(TRUE);
          FAReclassJnlBatch.Description := Text003;
          FAReclassJnlBatch.MODIFY;
          COMMIT;
        END;
        CurrentJnlBatchName := FAReclassJnlBatch.Name;
      END;
    END;

    PROCEDURE SetName@8(CurrentJnlBatchName@1000 : Code[10];VAR FAReclassJnlLine@1001 : Record 5624);
    BEGIN
      FAReclassJnlLine.FILTERGROUP := 2;
      FAReclassJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      FAReclassJnlLine.FILTERGROUP := 0;
      IF FAReclassJnlLine.FIND('-') THEN;
    END;

    PROCEDURE LookupName@10(VAR CurrentJnlBatchName@1000 : Code[10];VAR FAReclassJnlLine@1001 : Record 5624) : Boolean;
    VAR
      FAReclassJnlBatch@1002 : Record 5623;
    BEGIN
      COMMIT;
      FAReclassJnlBatch."Journal Template Name" := FAReclassJnlLine.GETRANGEMAX("Journal Template Name");
      FAReclassJnlBatch.Name := FAReclassJnlLine.GETRANGEMAX("Journal Batch Name");
      FAReclassJnlBatch.FILTERGROUP(2);
      FAReclassJnlBatch.SETRANGE("Journal Template Name",FAReclassJnlBatch."Journal Template Name");
      FAReclassJnlBatch.FILTERGROUP(0);
      IF FORM.RUNMODAL(0,FAReclassJnlBatch) = ACTION::LookupOK THEN BEGIN
        CurrentJnlBatchName := FAReclassJnlBatch.Name;
        SetName(CurrentJnlBatchName,FAReclassJnlLine);
      END;
    END;

    PROCEDURE GetFAS@3(FAReclassJnlLine@1000 : Record 5624;VAR FADescription@1001 : Text[30];VAR FADescription2@1002 : Text[30]);
    VAR
      FA@1003 : Record 5600;
    BEGIN
      IF FAReclassJnlLine."FA No." <> OldFANo THEN BEGIN
        FADescription := '';
        IF FAReclassJnlLine."FA No." <> '' THEN
          IF FA.GET(FAReclassJnlLine."FA No.") THEN
            FADescription := FA.Description;
        OldFANo := FAReclassJnlLine."FA No.";
      END;
      IF FAReclassJnlLine."New FA No." <> OldFANo2 THEN BEGIN
        FADescription2 := '';
        IF FAReclassJnlLine."New FA No." <> '' THEN
          IF FA.GET(FAReclassJnlLine."New FA No.") THEN
            FADescription2 := FA.Description;
        OldFANo2 := FAReclassJnlLine."New FA No.";
      END;
    END;

    BEGIN
    END.
  }
}
