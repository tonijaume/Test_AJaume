OBJECT Codeunit 414 Release Sales Document
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    TableNo=36;
    OnRun=VAR
            SalesLine@1000 : Record 37;
            TempVATAmountLine0@1001 : TEMPORARY Record 290;
            TempVATAmountLine1@1002 : TEMPORARY Record 290;
            NotOnlyDropShipment@1003 : Boolean;
          BEGIN
            IF Status = Status::Released THEN
              EXIT;

            IF "Document Type" = "Document Type"::Quote THEN
              IF CheckCustomerCreated(TRUE) THEN
                GET("Document Type"::Quote,"No.")
              ELSE
                EXIT;

            TESTFIELD("Sell-to Customer No.");

            SalesLine.SETRANGE("Document Type","Document Type");
            SalesLine.SETRANGE("Document No.","No.");
            SalesLine.SETFILTER(Type,'>0');
            SalesLine.SETFILTER(Quantity,'<>0');
            IF NOT SalesLine.FIND('-') THEN
              ERROR(Text001,"Document Type","No.");
            InvtSetup.GET;
            IF InvtSetup."Location Mandatory" THEN BEGIN
              SalesLine.SETRANGE(Type,SalesLine.Type::Item);
              IF SalesLine.FINDSET THEN
                REPEAT
                  SalesLine.TESTFIELD("Location Code");
                UNTIL SalesLine.NEXT = 0;
              SalesLine.SETFILTER(Type,'>0');
            END;
            SalesLine.SETRANGE("Drop Shipment",FALSE);
            NotOnlyDropShipment := SalesLine.FINDFIRST;
            SalesLine.RESET;

            SalesSetup.GET;
            IF SalesSetup."Calc. Inv. Discount" THEN BEGIN
              CODEUNIT.RUN(CODEUNIT::"Sales-Calc. Discount",SalesLine);
              GET("Document Type","No.");
            END;

            IF TestPrepayment(Rec) AND ("Document Type" = "Document Type"::Order) THEN BEGIN
              Status := Status::"Pending Prepayment";
              MODIFY(TRUE);
              EXIT;
            END
            ELSE
              Status := Status::Released;

            SalesLine.SetSalesHeader(Rec);
            SalesLine.CalcVATAmountLines(0,Rec,SalesLine,TempVATAmountLine0);
            SalesLine.CalcVATAmountLines(1,Rec,SalesLine,TempVATAmountLine1);
            SalesLine.UpdateVATOnLines(0,Rec,SalesLine,TempVATAmountLine0);
            SalesLine.UpdateVATOnLines(1,Rec,SalesLine,TempVATAmountLine1);

            MODIFY(TRUE);

            IF NotOnlyDropShipment THEN
              IF "Document Type" IN ["Document Type"::Order,"Document Type"::"Return Order"] THEN
                WhseSalesRelease.Release(Rec);
          END;

  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=There is nothing to release for %1 %2.;ESP=No hay nada para lanzar para %1 %2.';
      SalesSetup@1002 : Record 311;
      InvtSetup@1000 : Record 313;
      WhseSalesRelease@1004 : Codeunit 5771;
      Text002@1005 : TextConst 'ENU=This document can only be released when the approval process is complete.;ESP=Este documento s¢lo se puede lanzar una vez completado el proceso de aprobaci¢n.';
      Text003@1003 : TextConst 'ENU=The approval process must be cancelled or completed to reopen this document.;ESP=El proceso de aprobaci¢n se debe cancelar o completar para volver a abrir este documento.';

    PROCEDURE Reopen@1(VAR SalesHeader@1000 : Record 36);
    VAR
      SalesLine@1001 : Record 37;
    BEGIN
      WITH SalesHeader DO BEGIN
        IF Status = Status::Open THEN
          EXIT;
        Status := Status::Open;
        SalesLine.SetSalesHeader(SalesHeader);
        SalesLine.SETRANGE("Document Type","Document Type");
        SalesLine.SETRANGE("Document No.","No.");
        SalesLine.SETFILTER(Type,'>0');
        SalesLine.SETFILTER(Quantity,'<>0');
        IF RECORDLEVELLOCKING THEN
          SalesLine.LOCKTABLE;
        IF SalesLine.FINDSET THEN
          REPEAT
            SalesLine.Amount := 0;
            SalesLine."Amount Including VAT" := 0;
            SalesLine."VAT Base Amount" := 0;
            SalesLine.InitOutstandingAmount;
            SalesLine.MODIFY;
          UNTIL SalesLine.NEXT = 0;
        SalesLine.RESET;
        MODIFY(TRUE);
        IF "Document Type" IN ["Document Type"::Order,"Document Type"::"Return Order"] THEN
          WhseSalesRelease.Reopen(SalesHeader);
      END;
    END;

    PROCEDURE PerformManualRelease@2(VAR SalesHeader@1002 : Record 36);
    VAR
      ApprovalEntry@1004 : Record 454;
      ApprovalManagement@1001 : Codeunit 439;
      ApprovedOnly@1005 : Boolean;
    BEGIN
      WITH SalesHeader DO BEGIN
        IF ApprovalManagement.CheckApprSalesDocument(SalesHeader) THEN BEGIN
          CASE Status OF
            Status::"Pending Approval":
              ERROR(Text002);
            Status::Released,Status::"Pending Prepayment":
              CODEUNIT.RUN(414,SalesHeader);
            Status::Open:
              BEGIN
                ApprovedOnly := TRUE;
                ApprovalEntry.SETCURRENTKEY("Table ID","Document Type","Document No.","Sequence No.");
                ApprovalEntry.SETRANGE("Table ID",DATABASE::"Sales Header");
                ApprovalEntry.SETRANGE("Document Type","Document Type");
                ApprovalEntry.SETRANGE("Document No.","No.");
                ApprovalEntry.SETFILTER(Status,'<>%1&<>%2',ApprovalEntry.Status::Rejected,ApprovalEntry.Status::Canceled);
                IF ApprovalEntry.FIND('-') THEN BEGIN
                  REPEAT
                    IF (ApprovedOnly = TRUE) AND (ApprovalEntry.Status <> ApprovalEntry.Status::Approved) THEN
                      ApprovedOnly := FALSE;
                  UNTIL ApprovalEntry.NEXT = 0;

                  IF ApprovedOnly = TRUE AND TestApprovalLimit(SalesHeader) THEN
                    CODEUNIT.RUN(414,SalesHeader)
                  ELSE
                    ERROR(Text002);
                END ELSE
                  ERROR(Text002);
              END;
          END;
        END ELSE
          CODEUNIT.RUN(414,SalesHeader);
      END;
    END;

    PROCEDURE PerformManualReopen@3(VAR SalesHeader@1002 : Record 36);
    VAR
      ApprovalManagement@1001 : Codeunit 439;
    BEGIN
      WITH SalesHeader DO BEGIN
        IF ApprovalManagement.CheckApprSalesDocument(SalesHeader) THEN BEGIN
          CASE Status OF
            Status::"Pending Approval":
              ERROR(Text003);
            Status::Open,Status::Released,Status::"Pending Prepayment":
              Reopen(SalesHeader);
          END;
        END ELSE
          Reopen(SalesHeader);
      END;
    END;

    PROCEDURE TestPrepayment@31(SalesHeader@1000 : Record 36) : Boolean;
    VAR
      SalesLines@1001 : Record 37;
    BEGIN
      SalesLines.SETRANGE("Document Type",SalesHeader."Document Type");
      SalesLines.SETRANGE("Document No.",SalesHeader."No.");
      SalesLines.SETFILTER("Prepmt. Line Amount",'<>%1',0);
      IF SalesLines.FIND('-') THEN BEGIN
        REPEAT
          IF SalesLines."Prepmt. Amt. Inv." <> SalesLines."Prepmt. Line Amount" THEN
            EXIT(TRUE);
        UNTIL SalesLines.NEXT = 0;
      END;
    END;

    PROCEDURE TestApprovalLimit@4(SalesHeader@1000 : Record 36) : Boolean;
    VAR
      UserSetup@1004 : Record 91;
      AppManagement@1001 : Codeunit 439;
      AppAmount@1002 : Decimal;
      AppAmountLCY@1003 : Decimal;
    BEGIN
      AppManagement.CalcSalesDocAmount(SalesHeader,AppAmount,AppAmountLCY);
      UserSetup.GET(USERID);
      IF UserSetup."Unlimited Sales Approval" THEN
        EXIT(TRUE)
      ELSE BEGIN
        IF AppAmountLCY > UserSetup."Sales Amount Approval Limit" THEN
          ERROR(Text002)
        ELSE
          EXIT(TRUE);
      END;
    END;

    BEGIN
    END.
  }
}
