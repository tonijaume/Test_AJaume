OBJECT Codeunit 99000812 PlanningWkshManagement
{
  OBJECT-PROPERTIES
  {
    Date=15/09/04;
    Time=12:00:00;
    Version List=NAVW14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Planning;ESP=Planif.';
      Text001@1001 : TextConst 'ENU=Planning Worksheet;ESP=Hoja planificaci¢n';
      Text002@1002 : TextConst 'ENU=RECURRING;ESP=PERIODICO';
      Text003@1003 : TextConst 'ENU=Recurring Planning Worksheet;ESP=Hoja planificaci¢n peri¢dica';
      Text004@1004 : TextConst 'ENU=DEFAULT;ESP=GENERICO';
      Text005@1005 : TextConst 'ENU=Default Worksheet;ESP=Hoja gen‚rica';
      LastReqLine@1006 : Record 246;

    PROCEDURE TemplateSelection@1(FormID@1004 : Integer;RecurringWksh@1000 : Boolean;VAR ReqLine@1003 : Record 246;VAR WkshSelected@1002 : Boolean);
    VAR
      ReqWkshTmpl@1001 : Record 244;
    BEGIN
      WkshSelected := TRUE;

      ReqWkshTmpl.RESET;
      ReqWkshTmpl.SETRANGE("Form ID",FormID);
      ReqWkshTmpl.SETRANGE(Recurring,RecurringWksh);
      CASE ReqWkshTmpl.COUNT OF
        0:
          BEGIN
            ReqWkshTmpl.INIT;
            ReqWkshTmpl.Recurring := RecurringWksh;
            IF NOT RecurringWksh THEN BEGIN
              ReqWkshTmpl.Name := Text000;
              ReqWkshTmpl.Description := Text001;
            END ELSE BEGIN
              ReqWkshTmpl.Name := Text002;
              ReqWkshTmpl.Description := Text003;
            END;
            ReqWkshTmpl.VALIDATE("Form ID");
            ReqWkshTmpl.INSERT;
            COMMIT;
          END;
        1:
          ReqWkshTmpl.FIND('-');
        ELSE
          WkshSelected := FORM.RUNMODAL(0,ReqWkshTmpl) = ACTION::LookupOK;
      END;
      IF WkshSelected THEN BEGIN
        ReqLine.FILTERGROUP(2);
        ReqLine.SETRANGE("Worksheet Template Name",ReqWkshTmpl.Name);
        ReqLine.FILTERGROUP(0);
      END;
    END;

    PROCEDURE OpenWorksheet@2(VAR CurrentWkshBatchName@1000 : Code[10];VAR ReqLine@1001 : Record 246);
    BEGIN
      CheckTemplateName(ReqLine.GETRANGEMAX("Worksheet Template Name"),CurrentWkshBatchName);
      ReqLine.FILTERGROUP(2);
      ReqLine.SETRANGE("Journal Batch Name",CurrentWkshBatchName);
      ReqLine.FILTERGROUP(0);
    END;

    PROCEDURE CheckTemplateName@3(CurrentWkshTemplateName@1000 : Code[10];VAR CurrentWkshBatchName@1001 : Code[10]);
    VAR
      ReqWkshTmpl@1002 : Record 244;
      ReqWkshName@1003 : Record 245;
    BEGIN
      ReqWkshName.SETRANGE("Worksheet Template Name",CurrentWkshTemplateName);
      IF NOT ReqWkshName.GET(CurrentWkshTemplateName,CurrentWkshBatchName) THEN BEGIN
        IF NOT ReqWkshName.FIND('-') THEN BEGIN
          ReqWkshTmpl.GET(CurrentWkshTemplateName);
          ReqWkshName.INIT;
          ReqWkshName."Worksheet Template Name" := ReqWkshTmpl.Name;
          ReqWkshName.Name := Text004;
          ReqWkshName.Description := Text005;
          ReqWkshName.INSERT;
          COMMIT;
        END;
        CurrentWkshBatchName := ReqWkshName.Name
      END;
    END;

    PROCEDURE CheckName@4(CurrentWkshBatchName@1000 : Code[10];VAR ReqLine@1001 : Record 246);
    VAR
      ReqWkshName@1002 : Record 245;
    BEGIN
      ReqWkshName.GET(ReqLine.GETRANGEMAX("Worksheet Template Name"),CurrentWkshBatchName);
    END;

    PROCEDURE SetName@5(CurrentWkshBatchName@1000 : Code[10];VAR ReqLine@1001 : Record 246);
    BEGIN
      ReqLine.FILTERGROUP(2);
      ReqLine.SETRANGE("Journal Batch Name",CurrentWkshBatchName);
      ReqLine.FILTERGROUP(0);
      IF ReqLine.FIND('-') THEN;
    END;

    PROCEDURE LookupName@6(VAR CurrentWkshBatchName@1000 : Code[10];VAR ReqLine@1001 : Record 246) : Boolean;
    VAR
      ReqWkshName@1002 : Record 245;
    BEGIN
      COMMIT;
      ReqWkshName."Worksheet Template Name" := ReqLine.GETRANGEMAX("Worksheet Template Name");
      ReqWkshName.Name := ReqLine.GETRANGEMAX("Journal Batch Name");
      ReqWkshName.FILTERGROUP(2);
      ReqWkshName.SETRANGE("Worksheet Template Name",ReqWkshName."Worksheet Template Name");
      ReqWkshName.FILTERGROUP(0);
      IF FORM.RUNMODAL(0,ReqWkshName) = ACTION::LookupOK THEN BEGIN
        CurrentWkshBatchName := ReqWkshName.Name;
        SetName(CurrentWkshBatchName,ReqLine);
      END;
    END;

    PROCEDURE GetDescriptionAndRcptName@7(VAR ReqLine@1000 : Record 246;VAR ItemDescription@1001 : Text[50];VAR RoutingDescription@1002 : Text[50]);
    VAR
      Item@1003 : Record 27;
      RtngHeader@1004 : Record 99000763;
    BEGIN
      IF ReqLine."No." = '' THEN
        ItemDescription := ''
      ELSE
        IF ReqLine."No." <> LastReqLine."No." THEN BEGIN
          IF Item.GET(ReqLine."No.") THEN
            ItemDescription := Item.Description
          ELSE
            ItemDescription := '';
        END;

      IF ReqLine."Routing No." = '' THEN
        RoutingDescription := ''
      ELSE
        IF ReqLine."Routing No." <> LastReqLine."Routing No." THEN BEGIN
          IF RtngHeader.GET(ReqLine."Routing No.") THEN
            RoutingDescription := RtngHeader.Description
          ELSE
            RoutingDescription := '';
        END;

      LastReqLine  := ReqLine;
    END;

    BEGIN
    END.
  }
}
