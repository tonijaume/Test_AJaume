OBJECT Codeunit 7030 Campaign Target Group Mgt
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      ContBusRel@1000 : Record 5054;
      SegLine@1001 : Record 5077;
      CampaignTargetGr@1002 : Record 7030;
      Text000@1003 : TextConst 'ENU=%1 %2 is now activated.;ESP=%1 %2 est  ahora activado.';
      Text001@1004 : TextConst 'ENU=%1 %2 is now deactivated.;ESP=%1 %2 est  ahora desactivado.';
      Text002@1005 : TextConst 'ENU=To activate the sales prices and/or line discounts, you must apply the relevant %1(s) to the %2 and place a check mark in the %3 field on the %1.;ESP=Para activar los precios venta o dtos. l¡nea, debe aplicar el %1 relevante al %2 y activar el campo %3 en el %1.';
      Campaign2@1009 : Record 5071;
      InteractLogEntry@1010 : Record 5065;
      Text004@1006 : TextConst 'ENU=There are no Sales Prices or Sales Line Discounts currently linked to this %1. Do you still want to activate?;ESP=No hay precios venta ni descuentos l¡nea unidos actualmente a este %1. ¨Quiere activarlo todav¡a?';
      Text006@1011 : TextConst 'ENU=Activating prices for the Contacts...\\;ESP=Activando precios para los Contactos...\\';
      Text007@1007 : TextConst 'ENU=Segment Lines  @1@@@@@@@@@@;ESP=L¡neas segmento  @1@@@@@@@@@@';
      Text008@1012 : TextConst 'ENU=Logged Segment Lines  @1@@@@@@@@@@;ESP=L¡neas segm. archiv.  @1@@@@@@@@@@';

    PROCEDURE ActivateCampaign@1(VAR Campaign@1000 : Record 5071);
    VAR
      SalesPrice@1002 : Record 7002;
      SalesLineDisc@1003 : Record 7004;
      Window@1005 : Dialog;
      Found@1001 : Boolean;
      Continue@1004 : Boolean;
      NoOfRecords@1006 : Integer;
      NoOfEntries@1009 : Integer;
      i@1007 : Integer;
    BEGIN
      SalesPrice.SETCURRENTKEY("Sales Type","Sales Code");
      SalesPrice.SETRANGE("Sales Type",SalesPrice."Sales Type"::Campaign);
      SalesPrice.SETRANGE("Sales Code",Campaign."No.");
      SalesLineDisc.SETCURRENTKEY("Sales Type","Sales Code");
      SalesLineDisc.SETRANGE("Sales Type",SalesLineDisc."Sales Type"::Campaign);
      SalesLineDisc.SETRANGE("Sales Code",Campaign."No.");
      IF NOT (SalesPrice.FIND('-') OR SalesLineDisc.FIND('-')) THEN BEGIN
        Continue := CONFIRM(Text004,TRUE,Campaign.TABLECAPTION);
        IF Continue = FALSE THEN
          EXIT;
      END;
      CampaignTargetGr.LOCKTABLE;
      Found := FALSE;

      WITH SegLine DO BEGIN
        SETCURRENTKEY("Campaign No.");
        SETRANGE("Campaign No.",Campaign."No.");
        SETRANGE("Campaign Target",TRUE);

        IF FIND('-') THEN BEGIN
          Found := TRUE;
          i := 0;
          Window.OPEN(
            Text006 +
            Text007);
          NoOfRecords := SegLine.COUNT;
          REPEAT
            i := i +1;
            AddSegLinetoTargetGr(SegLine);
            Window.UPDATE(1,ROUND(i / NoOfRecords * 10000,1));
          UNTIL SegLine.NEXT = 0;
          Window.CLOSE;
        END;
      END;

      WITH InteractLogEntry DO BEGIN
        SETCURRENTKEY("Campaign No.","Campaign Target");
        SETRANGE("Campaign No.",Campaign."No.");
        SETRANGE("Campaign Target",TRUE);
        SETRANGE(Postponed,FALSE);
        IF FIND('-') THEN BEGIN
          Found := TRUE;
          i := 0;
          Window.OPEN(
          Text006 +
          Text008);
          NoOfRecords := COUNT;
          REPEAT
            i := i + 1;
            AddInteractionLogEntry(InteractLogEntry);
            Window.UPDATE(1,ROUND(i / NoOfRecords * 10000,1));
          UNTIL NEXT = 0;
          Window.CLOSE;
        END;
      END;
      IF Found THEN BEGIN
        COMMIT;
        MESSAGE(Text000,Campaign.TABLECAPTION,Campaign."No.")
      END ELSE
        ERROR(Text002,SegLine.TABLECAPTION,Campaign.TABLECAPTION,SegLine.FIELDCAPTION(SegLine."Campaign Target"));
    END;

    PROCEDURE DeactivateCampaign@2(VAR Campaign@1000 : Record 5071;ShowMessage@1001 : Boolean);
    BEGIN
      CampaignTargetGr.LOCKTABLE;

      CampaignTargetGr.SETCURRENTKEY("Campaign No.");
      CampaignTargetGr.SETRANGE(CampaignTargetGr."Campaign No.",Campaign."No.");
      IF CampaignTargetGr.FIND('-') THEN
        REPEAT
         CampaignTargetGr.DELETE;
        UNTIL CampaignTargetGr.NEXT = 0;
      IF ShowMessage THEN
        MESSAGE(Text001,Campaign.TABLECAPTION,Campaign."No.");
    END;

    PROCEDURE AddSegLinetoTargetGr@3(SegLine@1001 : Record 5077);
    BEGIN
      WITH SegLine DO
        IF ("Campaign No." <> '') AND  "Campaign Target"  THEN BEGIN
          ContBusRel.SETCURRENTKEY("Link to Table","Contact No.");
          ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Customer);
          ContBusRel.SETRANGE("Contact No.",SegLine."Contact Company No.");
          IF ContBusRel.FIND('-') THEN
            InsertTargetGroup(CampaignTargetGr.Type::Customer,ContBusRel."No.","Campaign No.")
          ELSE
            InsertTargetGroup(
              CampaignTargetGr.Type::Contact,SegLine."Contact Company No.","Campaign No.");
          IF CampaignTargetGr.INSERT(TRUE) THEN;
        END;
    END;

    PROCEDURE DeleteSegfromTargetGr@5(SegLine@1001 : Record 5077);
    VAR
      SegLine2@1000 : Record 5077;
      Cont@1002 : Record 5050;
      SalesPrice@1003 : Record 7002;
      SalesLineDisc@1004 : Record 7004;
      Continue@1005 : Boolean;
    BEGIN
      WITH SegLine DO
        IF ("Campaign No." <> '') THEN BEGIN
          SegLine2.SETCURRENTKEY("Campaign No.","Contact Company No.","Campaign Target");
          SegLine2.SETRANGE("Campaign No.","Campaign No.");
          SegLine2.SETRANGE("Contact Company No.","Contact Company No.");
          SegLine2.SETRANGE("Campaign Target",TRUE);

          InteractLogEntry.SETCURRENTKEY("Campaign No.","Contact Company No.","Campaign Target");
          InteractLogEntry.SETRANGE("Campaign No.","Campaign No.");
          InteractLogEntry.SETRANGE("Contact Company No.","Contact Company No.");
          InteractLogEntry.SETRANGE("Campaign Target",TRUE);
          InteractLogEntry.SETRANGE(Postponed,FALSE);

          IF SegLine2.COUNT + InteractLogEntry.COUNT = 1 THEN BEGIN
            ContBusRel.SETCURRENTKEY("Link to Table","Contact No.");
            ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Customer);
            ContBusRel.SETRANGE("Contact No.","Contact Company No.");

            IF ContBusRel.FIND('-') THEN BEGIN
              IF CampaignTargetGr.GET(
                CampaignTargetGr.Type::Customer,ContBusRel."No.","Campaign No.")
              THEN
                CampaignTargetGr.DELETE;
            END ELSE
              IF CampaignTargetGr.GET(
                CampaignTargetGr.Type::Contact,"Contact No.","Campaign No.")
              THEN
                CampaignTargetGr.DELETE;
          END;
        END;
    END;

    PROCEDURE AddInteractionLogEntry@6(InteractionLogEntry@1000 : Record 5065);
    BEGIN
      WITH InteractionLogEntry DO
        IF ("Campaign No." <> '') AND  "Campaign Target"  THEN BEGIN
          ContBusRel.SETCURRENTKEY("Link to Table","Contact No.");
          ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Customer);
          ContBusRel.SETRANGE("Contact No.",InteractionLogEntry."Contact Company No.");
          IF ContBusRel.FIND('-') THEN
            InsertTargetGroup(CampaignTargetGr.Type::Customer,ContBusRel."No.","Campaign No.")
          ELSE
            InsertTargetGroup(
              CampaignTargetGr.Type::Contact,InteractionLogEntry."Contact Company No.","Campaign No.");
          IF CampaignTargetGr.INSERT(TRUE) THEN;
        END;
    END;

    PROCEDURE DeleteContfromTargetGr@7(InteractLogEntry@1000 : Record 5065);
    VAR
      InteractLogEntry2@1001 : Record 5065;
    BEGIN
      WITH InteractLogEntry DO
        IF ("Campaign No." <> '') THEN BEGIN
          InteractLogEntry2.SETCURRENTKEY("Campaign No.","Contact Company No.","Campaign Target");
          InteractLogEntry2.SETRANGE("Campaign No.","Campaign No.");
          InteractLogEntry2.SETRANGE("Contact Company No.","Contact Company No.");
          InteractLogEntry2.SETRANGE("Campaign Target",TRUE);
          InteractLogEntry2.SETRANGE(Postponed,FALSE);

          SegLine.SETCURRENTKEY("Campaign No.","Contact Company No.","Campaign Target");
          SegLine.SETRANGE("Campaign No.","Campaign No.");
          SegLine.SETRANGE("Contact Company No.","Contact Company No.");
          SegLine.SETRANGE("Campaign Target",TRUE);

          IF InteractLogEntry2.COUNT + InteractLogEntry.COUNT = 1 THEN BEGIN
            ContBusRel.SETCURRENTKEY("Link to Table","Contact No.");
            ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Customer);
            ContBusRel.SETRANGE("Contact No.","Contact Company No.");

            IF ContBusRel.FIND('-') THEN BEGIN
              IF CampaignTargetGr.GET(
                CampaignTargetGr.Type::Customer,ContBusRel."No.","Campaign No.")
              THEN
                CampaignTargetGr.DELETE;
            END ELSE
              IF CampaignTargetGr.GET(
                CampaignTargetGr.Type::Contact,"Contact No.","Campaign No.")
              THEN
                CampaignTargetGr.DELETE;
          END;
        END;
    END;

    PROCEDURE ConverttoCustomer@9(Contact@1000 : Record 5050;Customer@1002 : Record 18);
    VAR
      CampaignTargetGr2@1001 : Record 7030;
    BEGIN
      WITH Contact DO BEGIN
        CampaignTargetGr2.SETCURRENTKEY("No.");
        CampaignTargetGr2.SETRANGE("No.","No.");
        IF CampaignTargetGr2.FIND('-') THEN
        REPEAT
          InsertTargetGroup(
            CampaignTargetGr2.Type::Customer,Customer."No.",CampaignTargetGr2."Campaign No.");
          CampaignTargetGr2.DELETE;
        UNTIL CampaignTargetGr2.NEXT = 0;
      END;
    END;

    PROCEDURE ConverttoContact@8(Cust@1000 : Record 18;CompanyContNo@1001 : Code[20]);
    VAR
      CampaignTargetGr2@1002 : Record 7030;
    BEGIN
      WITH Cust DO BEGIN
        CampaignTargetGr2.SETRANGE("No.","No.");
        IF CampaignTargetGr2.FIND('-') THEN
        REPEAT
          InsertTargetGroup(
            CampaignTargetGr2.Type::Contact,CompanyContNo,CampaignTargetGr2."Campaign No.");
          CampaignTargetGr2.DELETE;
        UNTIL CampaignTargetGr2.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE InsertTargetGroup@4(Type@1000 : Option;No@1001 : Code[20];CampaignNo@1002 : Code[20]);
    BEGIN
      CampaignTargetGr.Type := Type;
      CampaignTargetGr."No." := No;
      CampaignTargetGr."Campaign No." := CampaignNo;
      IF CampaignTargetGr.INSERT THEN;
    END;

    BEGIN
    END.
  }
}
