OBJECT Codeunit 7319 Bin Content Create
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    TableNo=7338;
    OnRun=BEGIN
            BinCreateLine.COPY(Rec);
            Code;
            Rec := BinCreateLine;
          END;

  }
  CODE
  {
    VAR
      BinCreateLine@1000 : Record 7338;
      BinContent@1001 : Record 7302;
      Text000@1002 : TextConst 'ENU=Do you want to create the bin content?;ESP=¨Confirma que desea crear el contenido de la ubicaci¢n?';
      Text001@1003 : TextConst 'ENU=There is nothing to create.;ESP=No hay nada que crear.';
      Location@1004 : Record 14;

    PROCEDURE Code@1();
    BEGIN
      IF NOT CONFIRM(Text000,FALSE) THEN
        EXIT;

      IF BinCreateLine.FIND('-') THEN BEGIN
        BinCreateLine.TESTFIELD("Location Code");
        GetLocation(BinCreateLine."Location Code");
        REPEAT
          IF Location."Directed Put-away and Pick" THEN BEGIN
            BinCreateLine.TESTFIELD("Zone Code");
            BinCreateLine.TESTFIELD("Unit of Measure Code");
          END ELSE
            BinCreateLine.TESTFIELD("Zone Code",'');
          BinCreateLine.TESTFIELD("Bin Code");
          BinCreateLine.TESTFIELD("Item No.");
          BinCreate(BinCreateLine);
        UNTIL BinCreateLine.NEXT = 0;
        BinCreateLine.DELETEALL;
      END ELSE
        MESSAGE(Text001);
    END;

    PROCEDURE BinCreate@2(BinCreateLine2@1000 : Record 7338);
    BEGIN
      WITH BinCreateLine2 DO BEGIN
        IF EmptyLine THEN
          EXIT;
        BinContent.INIT;
        BinContent."Bin Code" := "Bin Code";
        BinContent."Location Code" := "Location Code";
        BinContent."Item No." := "Item No.";
        BinContent."Variant Code" := "Variant Code";
        BinContent."Unit of Measure Code" := "Unit of Measure Code";
        BinContent."Qty. per Unit of Measure" := "Qty. per Unit of Measure";
        BinContent.Fixed := Fixed;
        BinContent.Default := Default;
        IF Location."Directed Put-away and Pick" THEN BEGIN
          BinContent."Zone Code" := "Zone Code";
          BinContent."Bin Type Code" := "Bin Type Code";
          BinContent."Warehouse Class Code" := "Warehouse Class Code";
          BinContent."Block Movement" := "Block Movement";
          BinContent."Bin Ranking" := "Bin Ranking";
          BinContent."Cross-Dock Bin" := "Cross-Dock Bin";
          BinContent."Min. Qty." := "Min. Qty.";
          BinContent."Max. Qty." := "Max. Qty.";
        END;
        BinContent.INSERT(TRUE);
      END;
    END;

    LOCAL PROCEDURE GetLocation@3(LocationCode@1000 : Code[10]);
    BEGIN
      IF Location.Code <> LocationCode THEN
        Location.GET(LocationCode);
    END;

    BEGIN
    END.
  }
}
