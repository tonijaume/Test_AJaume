OBJECT Codeunit 5656 InsuranceJnlManagement
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    Permissions=TableData 5633=imd,
                TableData 5634=imd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=INSURANCE;ESP=SEGURO';
      Text001@1001 : TextConst 'ENU=Insurance Journal;ESP=Diario seguros';
      Text002@1002 : TextConst 'ENU=DEFAULT;ESP=GENERICO';
      Text003@1003 : TextConst 'ENU=Default Journal;ESP=Diario gen‚rico';
      OldInsuranceNo@1004 : Code[20];
      OldFANo@1005 : Code[20];
      OpenFromBatch@1006 : Boolean;

    PROCEDURE TemplateSelection@1(FormID@1003 : Integer;VAR InsuranceJnlLine@1002 : Record 5635;VAR JnlSelected@1001 : Boolean);
    VAR
      InsuranceJnlTempl@1000 : Record 5633;
    BEGIN
      JnlSelected := TRUE;

      InsuranceJnlTempl.RESET;
      InsuranceJnlTempl.SETRANGE("Form ID",FormID);

      CASE InsuranceJnlTempl.COUNT OF
        0:
          BEGIN
            InsuranceJnlTempl.INIT;
            InsuranceJnlTempl.Name := Text000;
            InsuranceJnlTempl.Description := Text001;
            InsuranceJnlTempl.VALIDATE("Form ID");
            InsuranceJnlTempl.INSERT;
            COMMIT;
          END;
        1:
          InsuranceJnlTempl.FIND('-');
        ELSE
          JnlSelected := FORM.RUNMODAL(0,InsuranceJnlTempl) = ACTION::LookupOK;
      END;
      IF JnlSelected THEN BEGIN
        InsuranceJnlLine.FILTERGROUP := 2;
        InsuranceJnlLine.SETRANGE("Journal Template Name",InsuranceJnlTempl.Name);
        InsuranceJnlLine.FILTERGROUP := 0;
        IF OpenFromBatch THEN BEGIN
          InsuranceJnlLine."Journal Template Name" := '';
          FORM.RUN(InsuranceJnlTempl."Form ID",InsuranceJnlLine);
        END;
      END;
    END;

    PROCEDURE TemplateSelectionFromBatch@10(VAR InsuranceJnlBatch@1000 : Record 5634);
    VAR
      InsuranceJnlLine@1002 : Record 5635;
      InsuranceJnlTempl@1003 : Record 5633;
      JnlSelected@1001 : Boolean;
    BEGIN
      OpenFromBatch := TRUE;
      InsuranceJnlTempl.GET(InsuranceJnlBatch."Journal Template Name");
      InsuranceJnlTempl.TESTFIELD("Form ID");
      InsuranceJnlBatch.TESTFIELD(Name);

      InsuranceJnlLine.FILTERGROUP := 2;
      InsuranceJnlLine.SETRANGE("Journal Template Name",InsuranceJnlTempl.Name);
      InsuranceJnlLine.FILTERGROUP := 0;

      InsuranceJnlLine."Journal Template Name" := '';
      InsuranceJnlLine."Journal Batch Name" := InsuranceJnlBatch.Name;
      FORM.RUN(InsuranceJnlTempl."Form ID",InsuranceJnlLine);
    END;

    PROCEDURE OpenJournal@2(VAR CurrentJnlBatchName@1000 : Code[10];VAR InsuranceJnlLine@1001 : Record 5635);
    BEGIN
      CheckTemplateName(InsuranceJnlLine.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName);
      InsuranceJnlLine.FILTERGROUP := 2;
      InsuranceJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      InsuranceJnlLine.FILTERGROUP := 0;
    END;

    PROCEDURE OpenJnlBatch@12(VAR InsuranceJnlBatch@1000 : Record 5634);
    VAR
      InsuranceJnlTemplate@1002 : Record 5633;
      InsuranceJnlLine@1004 : Record 5635;
      JnlSelected@1003 : Boolean;
    BEGIN
      IF InsuranceJnlBatch.GETFILTER("Journal Template Name") <> '' THEN
        EXIT;
      InsuranceJnlBatch.FILTERGROUP(2);
      IF InsuranceJnlBatch.GETFILTER("Journal Template Name") <> '' THEN BEGIN
        InsuranceJnlBatch.FILTERGROUP(0);
        EXIT;
      END;
      InsuranceJnlBatch.FILTERGROUP(0);

      IF NOT InsuranceJnlBatch.FIND('-') THEN BEGIN
        IF NOT InsuranceJnlTemplate.FIND('-') THEN
          TemplateSelection(0,InsuranceJnlLine,JnlSelected);
        IF InsuranceJnlTemplate.FIND('-') THEN
          CheckTemplateName(InsuranceJnlTemplate.Name,InsuranceJnlBatch.Name);
      END;
      InsuranceJnlBatch.FIND('-');
      JnlSelected := TRUE;
      IF InsuranceJnlBatch.GETFILTER("Journal Template Name") <> '' THEN
         InsuranceJnlTemplate.SETRANGE(Name,InsuranceJnlBatch.GETFILTER("Journal Template Name"));
      CASE InsuranceJnlTemplate.COUNT OF
        1:
          InsuranceJnlTemplate.FIND('-');
        ELSE
          JnlSelected := FORM.RUNMODAL(0,InsuranceJnlTemplate) = ACTION::LookupOK;
      END;
      IF NOT JnlSelected THEN
        ERROR('');

      InsuranceJnlBatch.FILTERGROUP(2);
      InsuranceJnlBatch.SETRANGE("Journal Template Name",InsuranceJnlTemplate.Name);
      InsuranceJnlBatch.FILTERGROUP(0);
    END;

    PROCEDURE CheckName@4(CurrentJnlBatchName@1000 : Code[10];VAR InsuranceJnlLine@1001 : Record 5635);
    VAR
      InsuranceJnlBatch@1002 : Record 5634;
    BEGIN
      InsuranceJnlBatch.GET(InsuranceJnlLine.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName);
    END;

    PROCEDURE SetName@5(CurrentJnlBatchName@1000 : Code[10];VAR InsuranceJnlLine@1001 : Record 5635);
    BEGIN
      InsuranceJnlLine.FILTERGROUP := 2;
      InsuranceJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      InsuranceJnlLine.FILTERGROUP := 0;
      IF InsuranceJnlLine.FIND('-') THEN;
    END;

    PROCEDURE LookupName@6(VAR CurrentJnlBatchName@1000 : Code[10];VAR InsuranceJnlLine@1001 : Record 5635) : Boolean;
    VAR
      InsuranceJnlBatch@1002 : Record 5634;
    BEGIN
      COMMIT;
      InsuranceJnlBatch."Journal Template Name" := InsuranceJnlLine.GETRANGEMAX("Journal Template Name");
      InsuranceJnlBatch.Name := InsuranceJnlLine.GETRANGEMAX("Journal Batch Name");
      InsuranceJnlBatch.FILTERGROUP(2);
      InsuranceJnlBatch.SETRANGE("Journal Template Name",InsuranceJnlBatch."Journal Template Name");
      InsuranceJnlBatch.FILTERGROUP(0);
      IF FORM.RUNMODAL(0,InsuranceJnlBatch) = ACTION::LookupOK THEN BEGIN
        CurrentJnlBatchName := InsuranceJnlBatch.Name;
        SetName(CurrentJnlBatchName,InsuranceJnlLine);
      END;
    END;

    PROCEDURE CheckTemplateName@3(CurrentJnlTemplateName@1000 : Code[10];VAR CurrentJnlBatchName@1001 : Code[10]);
    VAR
      InsuranceJnlBatch@1002 : Record 5634;
    BEGIN
      IF NOT InsuranceJnlBatch.GET(CurrentJnlTemplateName,CurrentJnlBatchName) THEN BEGIN
        InsuranceJnlBatch.SETRANGE("Journal Template Name",CurrentJnlTemplateName);
        IF NOT InsuranceJnlBatch.FIND('-') THEN BEGIN
          InsuranceJnlBatch.INIT;
          InsuranceJnlBatch."Journal Template Name" := CurrentJnlTemplateName;
          InsuranceJnlBatch.SetupNewBatch;
          InsuranceJnlBatch.Name := Text002;
          InsuranceJnlBatch.Description := Text003;
          InsuranceJnlBatch.INSERT(TRUE);
          COMMIT;
        END;
        CurrentJnlBatchName := InsuranceJnlBatch.Name;
      END;
    END;

    PROCEDURE GetDescriptions@7(InsuranceJnlLine@1000 : Record 5635;VAR InsuranceDescription@1001 : Text[30];VAR FADescription@1002 : Text[30]);
    VAR
      Insurance@1003 : Record 5628;
      FA@1004 : Record 5600;
    BEGIN
      IF InsuranceJnlLine."Insurance No." <> OldInsuranceNo THEN BEGIN
        InsuranceDescription := '';
        IF InsuranceJnlLine."Insurance No." <> '' THEN
          IF Insurance.GET(InsuranceJnlLine."Insurance No.") THEN
            InsuranceDescription := Insurance.Description;
        OldInsuranceNo := InsuranceJnlLine."Insurance No.";
      END;
      IF InsuranceJnlLine."FA No." <> OldFANo THEN BEGIN
        FADescription := '';
        IF InsuranceJnlLine."FA No." <> '' THEN
          IF FA.GET(InsuranceJnlLine."FA No.") THEN
            FADescription := FA.Description;
        OldFANo := FA."No.";
      END;
    END;

    BEGIN
    END.
  }
}
