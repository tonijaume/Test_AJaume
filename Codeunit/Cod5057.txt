OBJECT Codeunit 5057 VendCont-Update
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      RMSetup@1000 : Record 5079;
      ContBusRel@1001 : Record 5054;
      Cont@1002 : Record 5050;
      OldCont@1003 : Record 5050;
      NoSeriesMgt@1004 : Codeunit 396;
      ContNo@1005 : Code[20];
      NoSeries@1006 : Code[10];
      SalespersonCode@1007 : Code[10];

    PROCEDURE OnInsert@3(VAR Vend@1000 : Record 23);
    BEGIN
      RMSetup.GET;
      IF RMSetup."Bus. Rel. Code for Vendors" = '' THEN
        EXIT;

      InsertNewContact(Vend,TRUE);
    END;

    PROCEDURE OnModify@1(VAR Vend@1000 : Record 23);
    VAR
      ChangeLogMgt@1001 : Codeunit 423;
      RecRef@1003 : RecordRef;
      xRecRef@1002 : RecordRef;
    BEGIN
      WITH ContBusRel DO BEGIN
        SETCURRENTKEY("Link to Table","No.");
        SETRANGE("Link to Table","Link to Table"::Vendor);
        SETRANGE("No.",Vend."No.");
        IF NOT FIND('-') THEN
          EXIT;
        Cont.GET("Contact No.");
        OldCont := Cont;
      END;

      ContNo := Cont."No.";
      NoSeries := Cont."No. Series";
      SalespersonCode := Cont."Salesperson Code";
      Cont.VALIDATE("E-Mail",Vend."E-Mail");
      Cont.TRANSFERFIELDS(Vend);
      Cont."No." := ContNo ;
      Cont."No. Series" := NoSeries;
      Cont."Salesperson Code" := SalespersonCode;
      Cont.VALIDATE(Name);
      Cont.OnModify(OldCont);
      Cont.MODIFY(TRUE);

      xRecRef.GETTABLE(OldCont);
      RecRef.GETTABLE(Cont);
      ChangeLogMgt.LogModification(RecRef,xRecRef);
    END;

    PROCEDURE OnDelete@2(VAR Vend@1000 : Record 23);
    BEGIN
      WITH ContBusRel DO BEGIN
        SETCURRENTKEY("Link to Table","No.");
        SETRANGE("Link to Table","Link to Table"::Vendor);
        SETRANGE("No.", Vend."No.");
        DELETEALL(TRUE);
      END;
    END;

    PROCEDURE InsertNewContact@4(VAR Vend@1000 : Record 23;LocalCall@1001 : Boolean);
    BEGIN
      IF NOT LocalCall THEN BEGIN
        RMSetup.GET;
        RMSetup.TESTFIELD("Bus. Rel. Code for Vendors");
      END;

      WITH Cont DO BEGIN
        INIT;
        TRANSFERFIELDS(Vend);
        Cont.VALIDATE(Name);
        Cont.VALIDATE("E-Mail");
        "No." := '';
        "No. Series" := '';
        RMSetup.TESTFIELD("Contact Nos.");
        NoSeriesMgt.InitSeries(RMSetup."Contact Nos.",'',0D,"No.","No. Series");
        Type := Type::Company;
        TypeChange;
        SetSkipDefault(FALSE);
        INSERT(TRUE);
      END;

      WITH ContBusRel DO BEGIN
        INIT;
        "Contact No." := Cont."No.";
        "Business Relation Code" := RMSetup."Bus. Rel. Code for Vendors";
        "Link to Table" := "Link to Table"::Vendor;
        "No." := Vend."No.";
        INSERT(TRUE);
      END;
    END;

    PROCEDURE InsertNewContactPerson@5(VAR Vend@1001 : Record 23;LocalCall@1003 : Boolean);
    VAR
      ContComp@1000 : Record 5050;
      ContBusRel@1002 : Record 5054;
    BEGIN
      IF NOT LocalCall THEN BEGIN
        RMSetup.GET;
        RMSetup.TESTFIELD("Bus. Rel. Code for Vendors");
      END;

      ContBusRel.SETCURRENTKEY("Link to Table","No.");
      ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Vendor);
      ContBusRel.SETRANGE("No.",Vend."No.");
      IF ContBusRel.FIND('-') THEN
        IF ContComp.GET(ContBusRel."Contact No.") THEN
          WITH Cont DO BEGIN
            INIT;
            "No." := '';
            INSERT(TRUE);
            "Company No." := ContComp."No.";
            Type := Type::Person;
            VALIDATE(Name,Vend.Contact);
            InheritCompanyToPersonData(ContComp,FALSE);
            MODIFY(TRUE);
            Vend."Primary Contact No." := "No.";
          END
    END;

    BEGIN
    END.
  }
}
