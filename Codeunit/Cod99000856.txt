OBJECT Codeunit 99000856 Planning Transparency
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      TempInvProfileTrack@1000 : TEMPORARY Record 99000854;
      TempPlanningWarning@1016 : TEMPORARY Record 99000855;
      CurrReqLine@1013 : Record 246;
      CurrTemplateName@1002 : Code[10];
      CurrWorksheetName@1001 : Code[10];
      Text000@1003 : TextConst 'ENU=Undefined;ESP=Indefinido';
      Text001@1004 : TextConst 'ENU=Production Forecast;ESP=Previsi¢n producci¢n';
      Text002@1005 : TextConst 'ENU=Blanket Order;ESP=Pedido abierto';
      Text003@1006 : TextConst 'ENU=Safety Stock Quantity;ESP=Stock de seguridad';
      Text004@1007 : TextConst 'ENU=Reorder Point;ESP=Punto pedido';
      Text005@1008 : TextConst 'ENU=Maximum Inventory;ESP=Stock m ximo';
      Text006@1009 : TextConst 'ENU=Reorder Quantity;ESP=Cantidad a pedir';
      Text007@1014 : TextConst 'ENU=Maximum Order Quantity;ESP=Cantidad m xima pedido';
      Text008@1010 : TextConst 'ENU=Minimum Order Quantity;ESP=Cantidad m¡nima pedido';
      Text009@1011 : TextConst 'ENU=Order Multiple;ESP=M£ltiplos de pedido';
      Text010@1012 : TextConst 'ENU=Dampener (% of Lot Size);ESP=Amort. (% tama¤o lote)';
      Text011@1015 : TextConst 'ENU=Emergency Order;ESP=Orden emergencia';
      SequenceNo@1017 : Integer;

    PROCEDURE SetTemplAndWorksheet@2(TemplateName@1002 : Code[10];WorksheetName@1001 : Code[10]);
    BEGIN
      CurrTemplateName := TemplateName;
      CurrWorksheetName := WorksheetName;
    END;

    PROCEDURE FindReason@4(VAR DemandInvProfile@1000 : Record 99000853) : Integer;
    VAR
      SurplusType@1001 : 'None,Forecast,BlanketOrder,SafetyStock,ReorderPoint,MaxInventory,FixedOrderQty,MaxOrder,MinOrder,OrderMultiple,DampenerQty,PlanningFlexibility,Undefined';
    BEGIN
      CASE DemandInvProfile."Source Type" OF
        0:
          IF DemandInvProfile."Order Relation" = DemandInvProfile."Order Relation"::"Safety Stock" THEN
            SurplusType := SurplusType::SafetyStock
          ELSE
            IF DemandInvProfile."Order Relation" = DemandInvProfile."Order Relation"::"Reorder Point" THEN
              SurplusType := SurplusType::ReorderPoint
            ELSE
              SurplusType := SurplusType::Undefined;
        DATABASE::"Sales Line":
          IF DemandInvProfile."Source Order Status" = 4 THEN
            SurplusType := SurplusType::BlanketOrder;
        DATABASE::"Production Forecast Entry":
          SurplusType := SurplusType::Forecast;
        ELSE
          SurplusType := SurplusType::None;
      END;
      EXIT(SurplusType);
    END;

    PROCEDURE LogSurplus@1(SupplyLineNo@1000 : Integer;DemandLineNo@1001 : Integer;SourceType@1004 : Integer;SourceID@1005 : Code[20];Qty@1002 : Decimal;Reason@1003 : 'Surplus,Forecast,BlanketOrder,SafetyStock,ReorderPoint,MaxInventory,FixedOrderQty,MaxOrder,MinOrder,OrderMultiple,DampenerQty,PlanningFlexibility,Undefined,EmergencyOrder');
    VAR
      Priority@1007 : Integer;
    BEGIN
      IF (Qty = 0) OR (SupplyLineNo = 0) THEN
        EXIT;

      CASE Reason OF
        Reason::BlanketOrder:
          Priority := 1;
        Reason::Forecast:
          Priority := 1;
        Reason::SafetyStock:
          Priority := 1;
        Reason::ReorderPoint:
          Priority := 1;
        Reason::EmergencyOrder:
          Priority := 2;
        Reason::MaxInventory:
          Priority := 3;
        Reason::FixedOrderQty:
          Priority := 3;
        Reason::MaxOrder:
          Priority := 4;
        Reason::MinOrder:
          Priority := 5;
        Reason::OrderMultiple:
          Priority := 6;
        Reason::DampenerQty:
          Priority := 7;
        ELSE
          Reason := Reason::Undefined;
      END;

      IF Reason <> Reason::Undefined THEN BEGIN
        TempInvProfileTrack.INIT;
        TempInvProfileTrack.Priority := Priority;
        TempInvProfileTrack."Line No." := SupplyLineNo;
        TempInvProfileTrack."Demand Line No." := DemandLineNo;
        TempInvProfileTrack."Sequence No." := GetSequenceNo;
        TempInvProfileTrack."Surplus Type" := Reason;
        TempInvProfileTrack."Source Type" := SourceType;
        TempInvProfileTrack."Source ID" := SourceID;
        TempInvProfileTrack."Quantity Tracked" := Qty;
        TempInvProfileTrack.INSERT;
      END;
    END;

    PROCEDURE ModifyLogEntry@12(SupplyLineNo@1000 : Integer;DemandLineNo@1001 : Integer;SourceType@1004 : Integer;SourceID@1005 : Code[20];Qty@1002 : Decimal;Reason@1003 : 'Surplus,Forecast,BlanketOrder,SafetyStock,ReorderPoint,MaxInventory,FixedOrderQty,MaxOrder,MinOrder,OrderMultiple,DampenerQty,PlanningFlexibility,Undefined,EmergencyOrder');
    BEGIN
      IF (Qty = 0) OR (SupplyLineNo = 0) THEN
        EXIT;

      TempInvProfileTrack.SETRANGE("Line No.",SupplyLineNo);
      TempInvProfileTrack.SETRANGE("Demand Line No.",DemandLineNo);
      TempInvProfileTrack.SETRANGE("Surplus Type",Reason);
      TempInvProfileTrack.SETRANGE("Source Type",SourceType);
      TempInvProfileTrack.SETRANGE("Source ID",SourceID);
      IF TempInvProfileTrack.FINDLAST THEN BEGIN
        TempInvProfileTrack."Quantity Tracked" += Qty;
        TempInvProfileTrack.MODIFY;
      END;
      TempInvProfileTrack.RESET;
    END;

    PROCEDURE CleanLog@6(SupplyLineNo@1000 : Integer);
    BEGIN
      TempInvProfileTrack.SETRANGE("Line No.",SupplyLineNo);
      IF NOT TempInvProfileTrack.ISEMPTY THEN
        TempInvProfileTrack.DELETEALL;
      TempInvProfileTrack.SETRANGE("Line No.");

      TempPlanningWarning.SETRANGE("Worksheet Line No.",SupplyLineNo);
      IF NOT TempPlanningWarning.ISEMPTY THEN
        TempPlanningWarning.DELETEALL;
      TempPlanningWarning.SETRANGE("Worksheet Line No.");
    END;

    PROCEDURE PublishSurplus@3(VAR SupplyInvProfile@1002 : Record 99000853;VAR SKU@1006 : Record 5700;VAR ReqLine@1009 : Record 246;VAR ReservEntry@1014 : Record 337);
    VAR
      PlanningElement@1003 : Record 99000855;
      QtyTracked@1000 : Decimal;
      QtyRemaining@1001 : Decimal;
      QtyReorder@1004 : Decimal;
      QtyMin@1005 : Decimal;
      QtyRound@1010 : Decimal;
      DampenerQty@1008 : Decimal;
      OrderSizeParticipated@1011 : Boolean;
    BEGIN
      TempInvProfileTrack.SETRANGE("Line No.",SupplyInvProfile."Line No.");

      QtyRemaining := SurplusQty(ReqLine,ReservEntry);
      QtyTracked := SupplyInvProfile."Quantity (Base)" - QtyRemaining;
      IF (QtyRemaining > 0) OR NOT TempPlanningWarning.ISEMPTY THEN
        WITH TempInvProfileTrack DO BEGIN
          PlanningElement."Worksheet Template Name" := CurrTemplateName;
          PlanningElement."Worksheet Batch Name" := CurrWorksheetName;
          PlanningElement."Worksheet Line No." := SupplyInvProfile."Planning Line No.";
          IF QtyRemaining <= 0 THEN
            SETFILTER("Warning Level",'<>%1',0);
          IF FINDSET THEN BEGIN
            REPEAT
              SETRANGE(Priority,Priority);
              SETRANGE("Demand Line No.","Demand Line No.");
              PlanningElement.INIT;
              FINDLAST;
              PlanningElement."Track Quantity From" := QtyRemaining;
              PlanningElement."Warning Level" := "Warning Level";
              CASE Priority OF
                1:
                  BEGIN  // Anticipated demand
                    CALCSUMS("Quantity Tracked");
                    IF "Surplus Type" = "Surplus Type"::SafetyStock THEN BEGIN
                      PlanningElement."Parameter Value" := SKU."Safety Stock Quantity";
                      "Source ID" := SKU."Item No.";
                    END ELSE
                      IF "Surplus Type" = "Surplus Type"::ReorderPoint THEN BEGIN
                        PlanningElement."Parameter Value" := SKU."Reorder Point";
                        "Source ID" := SKU."Item No.";
                        "Quantity Tracked" := 0;
                      END;
                    PlanningElement."Untracked Quantity" := "Quantity Tracked";
                  END;
                2:
                  BEGIN  // Emergency Order
                    PlanningElement."Untracked Quantity" := "Quantity Tracked";
                  END;
                3:
                  BEGIN  // Order size
                    QtyReorder := "Quantity Tracked";
                    IF QtyTracked < QtyReorder THEN BEGIN
                      OrderSizeParticipated := TRUE;
                      PlanningElement."Untracked Quantity" := QtyReorder - QtyTracked;
                      CASE "Surplus Type" OF
                        "Surplus Type"::ReorderPoint:
                          PlanningElement."Parameter Value" := SKU."Reorder Point";
                        "Surplus Type"::FixedOrderQty:
                          PlanningElement."Parameter Value" := SKU."Reorder Quantity";
                        "Surplus Type"::MaxInventory:
                          PlanningElement."Parameter Value" := SKU."Maximum Inventory";
                      END;
                    END ELSE
                      OrderSizeParticipated := FALSE
                  END;
                4:
                  BEGIN // Maximum Order
                    IF OrderSizeParticipated THEN BEGIN
                      PlanningElement."Untracked Quantity" := "Quantity Tracked";
                      PlanningElement."Parameter Value" := SKU."Maximum Order Quantity";
                    END;
                  END;
                5:
                  BEGIN  // Minimum Order
                    QtyMin := "Quantity Tracked";
                    IF QtyTracked < QtyMin THEN
                      PlanningElement."Untracked Quantity" := QtyMin - QtyTracked;
                    PlanningElement."Parameter Value" := SKU."Minimum Order Quantity";
                  END;
                6:
                  BEGIN  // Rounding
                    QtyRound := SKU."Order Multiple"
                      - ROUND(SupplyInvProfile."Quantity (Base)",SKU."Order Multiple",'>')
                      + SupplyInvProfile."Quantity (Base)";
                    IF QtyRound > "Quantity Tracked" THEN
                      QtyRound := "Quantity Tracked";
                    IF QtyRound > QtyRemaining THEN
                      QtyRound := QtyRemaining;
                    PlanningElement."Untracked Quantity" := QtyRound;
                    PlanningElement."Parameter Value" := SKU."Order Multiple";
                  END;
                7:
                  BEGIN  // Dampener
                    DampenerQty := "Quantity Tracked";
                    IF DampenerQty < QtyRemaining THEN
                      PlanningElement."Untracked Quantity" := DampenerQty
                    ELSE
                      PlanningElement."Untracked Quantity" := QtyRemaining;
                    PlanningElement."Parameter Value" := DampenerQty;
                  END;
              END;
              IF (PlanningElement."Untracked Quantity" <> 0) OR
                 ("Surplus Type" = "Surplus Type"::ReorderPoint) OR
                 ("Warning Level" > 0)
              THEN BEGIN
                PlanningElement."Track Line No." += 1;
                PlanningElement."Item No." := SupplyInvProfile."Item No.";
                PlanningElement."Variant Code" := SupplyInvProfile."Variant Code";
                PlanningElement."Location Code" := SupplyInvProfile."Location Code";
                PlanningElement."Source Type" := "Source Type";
                PlanningElement."Source ID" := "Source ID";
                PlanningElement.Source := ShowSurplusReason("Surplus Type");
                QtyTracked += PlanningElement."Untracked Quantity";
                QtyRemaining -= PlanningElement."Untracked Quantity";
                PlanningElement."Track Quantity To" := QtyRemaining;
                TransferWarningSourceText(TempInvProfileTrack,PlanningElement);
                PlanningElement.INSERT;
              END;
              SETRANGE(Priority);
              SETRANGE("Demand Line No.");
            UNTIL (NEXT = 0);
          END;
          IF QtyRemaining > 0 THEN BEGIN // just in case that something by accident has not been captured
            PlanningElement.INIT;
            PlanningElement."Track Line No." += 1;
            PlanningElement."Item No." := SupplyInvProfile."Item No.";
            PlanningElement."Variant Code" := SupplyInvProfile."Variant Code";
            PlanningElement."Location Code" := SupplyInvProfile."Location Code";
            PlanningElement.Source := ShowSurplusReason("Surplus Type"::Undefined);
            PlanningElement."Track Quantity From" := QtyRemaining;
            PlanningElement."Untracked Quantity" := QtyRemaining;
            QtyTracked += PlanningElement."Untracked Quantity";
            QtyRemaining -= PlanningElement."Untracked Quantity";
            PlanningElement."Track Quantity To" := QtyRemaining;
            PlanningElement.INSERT;
          END;
        END;
      TempInvProfileTrack.SETRANGE("Line No.");
      TempInvProfileTrack.SETRANGE("Warning Level");
      CleanLog(SupplyInvProfile."Line No.");
    END;

    PROCEDURE SurplusQty@7(VAR ReqLine@1000 : Record 246;VAR ReservEntry@1001 : Record 337) : Decimal;
    VAR
      CrntReservEntry@1006 : Record 337;
      ReservEngineMgt@1003 : Codeunit 99000831;
      ReqLineReserve@1002 : Codeunit 99000833;
      QtyTracked1@1004 : Decimal;
      QtyTracked2@1005 : Decimal;
    BEGIN
      CrntReservEntry.COPY(ReservEntry);
      ReservEngineMgt.InitFilterAndSortingLookupFor(ReservEntry,FALSE);
      ReqLineReserve.FilterReservFor(ReservEntry,ReqLine);
      WITH ReservEntry DO BEGIN
        SETRANGE("Reservation Status","Reservation Status"::Surplus);
        IF FINDSET THEN
          REPEAT
            QtyTracked1 += "Quantity (Base)";
          UNTIL NEXT = 0;
        RESET;
        IF ReqLine."Action Message" > ReqLine."Action Message"::New THEN BEGIN
          CASE ReqLine."Ref. Order Type" OF
            ReqLine."Ref. Order Type"::Purchase:
              BEGIN
                SETRANGE("Source ID",ReqLine."Ref. Order No.");
                SETRANGE("Source Ref. No.",ReqLine."Ref. Line No.");
                SETRANGE("Source Type",DATABASE::"Purchase Line");
                SETRANGE("Source Subtype",1);
              END;
            ReqLine."Ref. Order Type"::"Prod. Order":
              BEGIN
                SETRANGE("Source ID",ReqLine."Ref. Order No.");
                SETRANGE("Source Type",DATABASE::"Prod. Order Line");
                SETRANGE("Source Subtype",ReqLine."Ref. Order Status");
                SETRANGE("Source Prod. Order Line",ReqLine."Ref. Line No.");
              END;
            ReqLine."Ref. Order Type"::Transfer:
              BEGIN
                SETRANGE("Source ID",ReqLine."Ref. Order No.");
                SETRANGE("Source Ref. No.",ReqLine."Ref. Line No.");
                SETRANGE("Source Type",DATABASE::"Transfer Line");
                SETRANGE("Source Subtype",1); // Inbound
                SETRANGE("Source Prod. Order Line",0);
              END;
          END;
          SETRANGE("Reservation Status","Reservation Status"::Surplus);
          IF FINDSET THEN
            REPEAT
              QtyTracked2 += "Quantity (Base)";
            UNTIL NEXT = 0;
          RESET;
        END;
      END;
      ReservEntry.COPY(CrntReservEntry);
      EXIT(QtyTracked1 + QtyTracked2);
    END;

    PROCEDURE ShowSurplusReason@5(SurplusType@1000 : 'None,Forecast,BlanketOrder,SafetyStock,ReorderPoint,MaxInventory,FixedOrderQty,MaxOrder,MinOrder,OrderMultiple,DampenerQty,PlanningFlexibility,Undefined,EmergencyOrder') ReturnText : Text[50];
    BEGIN
      CASE SurplusType OF
        SurplusType::Forecast:
          ReturnText := Text001;
        SurplusType::BlanketOrder:
          ReturnText := Text002;
        SurplusType::SafetyStock:
          ReturnText := Text003;
        SurplusType::ReorderPoint:
          ReturnText := Text004;
        SurplusType::MaxInventory:
          ReturnText := Text005;
        SurplusType::FixedOrderQty:
          ReturnText := Text006;
        SurplusType::MaxOrder:
          ReturnText := Text007;
        SurplusType::MinOrder:
          ReturnText := Text008;
        SurplusType::OrderMultiple:
          ReturnText := Text009;
        SurplusType::DampenerQty:
          ReturnText := Text010;
        SurplusType::EmergencyOrder:
          ReturnText := Text011;
        ELSE
          ReturnText := Text000;
      END;
    END;

    PROCEDURE SetCurrReqLine@8(VAR CurrentReqLine@1000 : Record 246);
    BEGIN
      CurrReqLine := CurrentReqLine;
    END;

    PROCEDURE DrillDownUntrackedQty@20(CaptionText@1002 : Text[80]);
    VAR
      PlanningElement@1000 : Record 99000855;
      SurplusTrackForm@1001 : Form 99000855;
    BEGIN
      WITH CurrReqLine DO BEGIN
        IF NOT ("Planning Line Origin" <> "Planning Line Origin"::" ") THEN // IsPlanning
          EXIT;

        PlanningElement.SETRANGE("Worksheet Template Name","Worksheet Template Name");
        PlanningElement.SETRANGE("Worksheet Batch Name","Journal Batch Name");
        PlanningElement.SETRANGE("Worksheet Line No.","Line No.");

        SurplusTrackForm.SETTABLEVIEW(PlanningElement);
        SurplusTrackForm.SetCaption(CaptionText);
        SurplusTrackForm.RUNMODAL;
      END;
    END;

    PROCEDURE ReqLineWarningLevel@40(ReqLine@1001 : Record 246) WarningLevel : Integer;
    VAR
      PlanningElement@1000 : Record 99000855;
    BEGIN
      WITH ReqLine DO BEGIN
        PlanningElement.SETRANGE("Worksheet Template Name","Worksheet Template Name");
        PlanningElement.SETRANGE("Worksheet Batch Name","Journal Batch Name");
        PlanningElement.SETRANGE("Worksheet Line No.","Line No.");
        PlanningElement.SETFILTER("Warning Level",'>%1',0);
        IF PlanningElement.FINDSET THEN
          REPEAT
            IF (PlanningElement."Warning Level" < WarningLevel) OR (WarningLevel = 0) THEN
              WarningLevel := PlanningElement."Warning Level";
          UNTIL PlanningElement.NEXT = 0;
      END;
    END;

    PROCEDURE LogWarning@10(SupplyLineNo@1004 : Integer;ReqLine@1001 : Record 246;WarningLevel@1002 : ',Emergency,Exception,Attention';Source@1003 : Text[200]) : Boolean;
    VAR
      PlanningElement@1000 : Record 99000855;
    BEGIN
      IF SupplyLineNo = 0 THEN
        WITH ReqLine DO BEGIN
          PlanningElement.SETRANGE("Worksheet Template Name","Worksheet Template Name");
          PlanningElement.SETRANGE("Worksheet Batch Name","Journal Batch Name");
          PlanningElement.SETRANGE("Worksheet Line No.","Line No.");

          IF NOT PlanningElement.FINDLAST THEN BEGIN
            PlanningElement."Worksheet Template Name" := "Worksheet Template Name";
            PlanningElement."Worksheet Batch Name" := "Journal Batch Name";
            PlanningElement."Worksheet Line No." := "Line No.";
          END;

          PlanningElement.INIT;
          PlanningElement."Track Line No." += 1;
          PlanningElement.Source := Source;
          PlanningElement."Warning Level" := WarningLevel;
          PlanningElement.INSERT;
        END
      ELSE
        WITH TempInvProfileTrack DO BEGIN
          INIT;
          "Line No." := SupplyLineNo;
          Priority := 10;
          "Sequence No." := GetSequenceNo;
          "Demand Line No." := 0;
          "Surplus Type" := 0;
          "Source Type" := 0;
          "Source ID" := '';
          "Quantity Tracked" := 0;
          "Warning Level" := WarningLevel;
          INSERT;
          TempPlanningWarning.INIT;
          TempPlanningWarning."Worksheet Template Name" := '';
          TempPlanningWarning."Worksheet Batch Name" := '';
          TempPlanningWarning."Worksheet Line No." := SupplyLineNo;
          TempPlanningWarning."Track Line No." := "Sequence No.";
          TempPlanningWarning.Source := Source;
          TempPlanningWarning.INSERT;
        END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE TransferWarningSourceText@11(FromInvProfileTrack@1000 : TEMPORARY Record 99000854;VAR ToPlanningElement@1001 : Record 99000855) : Text[250];
    BEGIN
      IF FromInvProfileTrack."Warning Level" = 0 THEN
        EXIT;
      IF TempPlanningWarning.GET('','',FromInvProfileTrack."Line No.",FromInvProfileTrack."Sequence No.") THEN BEGIN
        ToPlanningElement.Source := TempPlanningWarning.Source;
        TempPlanningWarning.DELETE;
      END;
    END;

    LOCAL PROCEDURE GetSequenceNo@9() : Integer;
    BEGIN
      SequenceNo := SequenceNo + 1;
      EXIT(SequenceNo);
    END;

    BEGIN
    END.
  }
}
