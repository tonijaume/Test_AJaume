OBJECT Codeunit 6812 EP XML DOM Management
{
  OBJECT-PROPERTIES
  {
    Date=25/05/05;
    Time=12:00:00;
    Version List=NAVW14.00.01;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      NormalCaseMode@1000 : Boolean;

    PROCEDURE AddElement@1(VAR XMLNode@1001 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 3.0:{F6D90F11-9C73-11D3-B32E-00C04F990BB4}:'Microsoft XML, v3.0'.DOMDocument";NodeName@1002 : Text[250];NodeText@1003 : Text[250];NameSpace@1004 : Text[250];VAR CreatedXMLNode@1005 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 3.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v3.0'.IXMLDOMNode") ExitStatus@1000 : Integer;
    VAR
      NewChildNode@1006 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 3.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v3.0'.IXMLDOMNode";
    BEGIN
      IF NOT NormalCaseMode THEN
        NodeName := UPPERCASE(NodeName);
      NewChildNode := XMLNode.ownerDocument.createNode('element', NodeName, NameSpace);

      IF ISCLEAR(NewChildNode) THEN BEGIN
        ExitStatus := 50;
        EXIT;
      END;

      IF NodeText <> '' THEN
        NewChildNode.text := NodeText;

      XMLNode.appendChild(NewChildNode);
      CreatedXMLNode := NewChildNode;

      ExitStatus := 0;
    END;

    PROCEDURE FindNode@3(XMLRootNode@1000 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 3.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v3.0'.IXMLDOMNode";NodePath@1001 : Text[250];VAR FoundXMLNode@1002 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 3.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v3.0'.IXMLDOMNode") : Boolean;
    BEGIN
      IF ISCLEAR(XMLRootNode) THEN
        EXIT(FALSE);

      IF NOT NormalCaseMode THEN
        NodePath := UPPERCASE(NodePath);
      FoundXMLNode := XMLRootNode.selectSingleNode(NodePath);

      IF ISCLEAR(FoundXMLNode) THEN
        EXIT(FALSE)
      ELSE
        EXIT(TRUE);
    END;

    PROCEDURE FindNodes@4(XMLRootNode@1000 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 3.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v3.0'.IXMLDOMNode";NodePath@1001 : Text[250];VAR ReturnedXMLNodeList@1002 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 3.0:{2933BF82-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v3.0'.IXMLDOMNodeList") : Boolean;
    BEGIN
      IF NOT NormalCaseMode THEN
        NodePath := UPPERCASE(NodePath);
      ReturnedXMLNodeList := XMLRootNode.selectNodes(NodePath);

      IF ISCLEAR(ReturnedXMLNodeList) THEN
        EXIT(FALSE)
      ELSE
        IF ReturnedXMLNodeList.length <= 0 THEN
          EXIT(FALSE)
        ELSE
          EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}
