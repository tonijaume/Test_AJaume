OBJECT Codeunit 272 Res. Jnl.-Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=08/06/01;
    Time=12:00:00;
    Version List=NAVW13.00;
  }
  PROPERTIES
  {
    TableNo=207;
    OnRun=BEGIN
            ResJnlLine.COPY(Rec);
            Code;
            Rec.COPY(ResJnlLine);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot be filtered when posting recurring journals;ESP=no puede contener un filtro cuando se registra un diario peri¢dico';
      Text001@1001 : TextConst 'ENU=Do you want to post the journal lines and print the posting report?;ESP=¨Confirma que desea registrar las l¡ns. de diario e imprimir el informe de reg.?';
      Text002@1002 : TextConst 'ENU=There is nothing to post.;ESP=No hay nada que registrar.';
      Text003@1003 : TextConst 'ENU=The journal lines were successfully posted.;ESP=Se han registrado correctamente las l¡neas del diario.';
      Text004@1004 : TextConst 'ENU="The journal lines were successfully posted. ";ESP="Se han registrado correctamente las l¡neas del diario. "';
      Text005@1005 : TextConst 'ENU=You are now in the %1 journal.;ESP=Se encuentra en el diario %1.';
      ResJnlTemplate@1006 : Record 206;
      ResJnlLine@1007 : Record 207;
      ResReg@1008 : Record 240;
      ResJnlPostBatch@1009 : Codeunit 213;
      TempJnlBatchName@1010 : Code[10];

    LOCAL PROCEDURE Code@1();
    BEGIN
      WITH ResJnlLine DO BEGIN
        ResJnlTemplate.GET("Journal Template Name");
        ResJnlTemplate.TESTFIELD("Posting Report ID");
        IF ResJnlTemplate.Recurring AND (GETFILTER("Posting Date") <> '') THEN
          FIELDERROR("Posting Date",Text000);

        IF NOT CONFIRM(Text001) THEN
          EXIT;

        TempJnlBatchName := "Journal Batch Name";

        ResJnlPostBatch.RUN(ResJnlLine);

        IF ResReg.GET("Line No.") THEN BEGIN
          ResReg.SETRECFILTER;
          REPORT.RUN(ResJnlTemplate."Posting Report ID",FALSE,FALSE,ResReg);
        END;

        IF "Line No." = 0 THEN
          MESSAGE(Text002)
        ELSE
          IF TempJnlBatchName = "Journal Batch Name" THEN
            MESSAGE(Text003)
          ELSE
            MESSAGE(
              Text004 +
              Text005,
              "Journal Batch Name");

        IF NOT FIND('=><') OR (TempJnlBatchName <> "Journal Batch Name") THEN BEGIN
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          FILTERGROUP(0);
          "Line No." := 1;
        END;
      END;
    END;

    BEGIN
    END.
  }
}
