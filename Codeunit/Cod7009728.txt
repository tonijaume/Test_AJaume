OBJECT Codeunit 7009728 Funciones Blank
{
  OBJECT-PROPERTIES
  {
    Date=02/10/12;
    Time=12:00:00;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1103355001 : TextConst 'ENU=Sure to delete all HPL %1;ESP=Seguro que desea borrar todo el HPL %1.';
      Text001@1103355002 : TextConst 'ENU=Processing erasure\;ESP=Procesando borrado\';
      Text002@1103355003 : TextConst 'ENU=Voucher.: #1#####   \;ESP=Bonos.: #1#####   \';
      Text003@1103355004 : TextConst 'ENU=Current: #2#####;ESP=Actual: #2#####';
      Text004@1103355005 : TextConst 'ENU=Difference code is going to be assigned \;ESP=Va a asignar un codigo de diferencia \';
      Text005@1103355006 : TextConst 'ENU=to all marked vouchers;ESP=a todos los bonos marcados';
      Text006@1103355007 : TextConst 'ENU=Massive assignment of Difference code                 \;ESP=Asignacion masiva de Codigo de Diferencia                 \';
      Text007@1103355008 : TextConst 'ENU=Code.....: #1########  \;ESP=Codigo.....: #1########  \';
      Text008@1103355009 : TextConst 'ENU=Description: #2##########################################################;ESP=Descripcion: #2##########################################################';
      Text009@1103355010 : TextConst 'ENU=Differences code %1 do not exist;ESP=No existe el codigo de diferencia %1.';
      Text010@1103355011 : TextConst 'ENU=Totalizing vouchers        \;ESP=Totalizacion bonos        \';
      Text011@1103355012 : TextConst 'ENU=TO Amount.....: #2##################\;ESP=Importe TO.....: #2##################\';
      Text012@1103355013 : TextConst 'ENU=Agency Amount: #1##################\;ESP=Importe Agencia: #1##################\';
      Text013@1103355014 : TextConst 'ENU=Difference.....: #3##################\;ESP=Diferencia.....: #3##################\';
      Text014@1103355015 : TextConst 'ENU=Wait 5 seconds'';ESP=Espere 5 segundos...';
      Text015@1103355016 : TextConst 'ENU=There are no booking summaries associated to this conciliation.;ESP=No existen resumenes de reserva asociados a esta conciliacion.';
      Text016@1103355017 : TextConst 'ENU=Would you like to return the filtered conciliations of HPL %1;ESP=Desea retroceder las conciliaciones filtradas del HPL %1';
      Text017@1103355018 : TextConst 'ENU=Retrocession ended.;ESP=Retrocesion finalizada.';
      ventana@1103355000 : Dialog;

    PROCEDURE borrar_hpl@1(par_datos@1103355000 : Record 7010004);
    VAR
      rBlank@1103355001 : Record 7010004;
      rBlank2@1103355002 : Record 7010004;
      rPrecioBlank@1103355003 : Record 7010072;
      IdUsuario@1103355004 : Code[10];
      num_hpl@1103355005 : Code[10];
      bono_actual@1103355006 : Integer;
      total_bonos@1103355007 : Integer;
      ventana@1103355008 : Dialog;
    BEGIN
      // borrar_hpl
      //
      IF NOT CONFIRM(STRSUBSTNO(Text000, par_datos."N§ HPL", TRUE)) THEN
        EXIT;
      rBlank.RESET;
      rBlank.SETCURRENTKEY("N§ HPL",
                           "N§ Linea");
      rBlank.SETRANGE("N§ HPL", par_datos."N§ HPL");
      IF rBlank.FIND('-') THEN BEGIN
        ventana.OPEN(Text001+
                     '\'+
                     Text002+
                     Text003);
        total_bonos := rBlank.COUNT;
        bono_actual := 0;
        ventana.UPDATE(1, total_bonos);
        REPEAT
          bono_actual := bono_actual + 1;
          ventana.UPDATE(2, bono_actual);
          rPrecioBlank.RESET;
          rPrecioBlank.SETRANGE(TourOperador, rBlank.TourOperador);
          rPrecioBlank.SETRANGE(Bono        , rBlank.Bono);
          rPrecioBlank.SETRANGE("Hotel TO"  , rBlank."Hotel TourOperador");
          rPrecioBlank.SETRANGE("N§ HPL"    , rBlank."N§ HPL");
          rPrecioBlank.SETRANGE("N§ Linea"  , rBlank."N§ Linea");
          IF rPrecioBlank.FIND('-') THEN
            rPrecioBlank.DELETEALL;

          rBlank2 := rBlank;
          rBlank2.DELETE;
        UNTIL rBlank.NEXT = 0;
      END;
      rBlank.RESET;
    END;

    PROCEDURE ver_precios_blank@2(par_conc@1103355000 : Record 7010070);
    VAR
      rBlank@1103355001 : Record 7010004;
      rPrecio@1103355002 : Record 7010072;
    BEGIN
      // ver_precios_blank
      //
      rBlank.RESET;
      rBlank.SETRANGE(TourOperador        , par_conc.TourOperador);
      rBlank.SETRANGE(Bono                , par_conc.Bono);
      rBlank.SETRANGE("Hotel TourOperador", par_conc."Hotel TO BLANK");
      rBlank.SETRANGE("N§ HPL"            , par_conc."N§ HPL");
      IF rBlank.FIND('-') THEN BEGIN
        rPrecio.RESET;
        rPrecio.SETRANGE(TourOperador, rBlank.TourOperador);
        rPrecio.SETRANGE(Bono        , rBlank.Bono);
        rPrecio.SETRANGE("Hotel TO"  , rBlank."Hotel TourOperador");
        rPrecio.SETRANGE("N§ HPL"    , rBlank."N§ HPL");
        rPrecio.SETRANGE("N§ Linea"  , rBlank."N§ Linea");
        FORM.RUNMODAL(FORM::"Tabla Precios BLANK", rPrecio);
      END;
      rBlank.RESET;
      rPrecio.RESET;
    END;

    PROCEDURE asigna_codigo_diferencia@3(VAR par_conc@1103355000 : Record 7010070);
    VAR
      ventana@1103355001 : Dialog;
      TextoDiferencia@1103355002 : Text[250];
      rDif@1103355003 : Record 7009706;
      rConc@1103355004 : Record 7010070;
      fTabDiferen@1103355005 : Form 7009706;
    BEGIN
      // asigna_codigo_diferencia
      //
      IF NOT CONFIRM(Text004+
                     Text005) THEN
        EXIT;

      {*
      ventana.OPEN(Text006+
                   '\'+
                   Text007+
                   Text008);
      ventana.INPUT(1, rDif.Codigo);
      IF rDif.Codigo = '' THEN BEGIN
        ventana.INPUT(2, TextoDiferencia);
        IF TextoDiferencia = '' THEN
          EXIT;
      END;
      ventana.CLOSE();

      IF (NOT rDif.GET(rDif.Codigo)) AND (rDif.Codigo <> '') THEN
        ERROR(Text009, rDif.Codigo);

      *}

      rDif.RESET;

      CLEAR(fTabDiferen);
      fTabDiferen.SETTABLEVIEW(rDif);
      fTabDiferen.LOOKUPMODE(TRUE);
      IF fTabDiferen.RUNMODAL <> ACTION::LookupOK THEN
        EXIT;
      fTabDiferen.GETRECORD(rDif);

      par_conc.MARKEDONLY(TRUE);
      IF par_conc.FIND('-') THEN
        REPEAT
          rConc := par_conc;
          rConc."Codigo diferencia" := rDif.Codigo;
          IF rDif.Codigo <> '' THEN
            rConc."Descripcion diferencia" := rDif.Descripcion
          ELSE
            rConc."Descripcion diferencia" := TextoDiferencia;
          rConc.MODIFY;
          rConc.MARK(FALSE);
        UNTIL par_conc.NEXT = 0;
      par_conc.MARKEDONLY(FALSE);
    END;

    PROCEDURE totalizar_conciliacion@4(VAR par_conc@1103355000 : Record 7010070);
    VAR
      importe_to@1103355001 : Decimal;
      importe_agencia@1103355002 : Decimal;
      importe_diferencia@1103355003 : Decimal;
      ventana@1103355004 : Dialog;
    BEGIN
      // totalizar_conciliacion
      //
      importe_agencia := 0;
      importe_to := 0;
      importe_diferencia := 0;
      IF par_conc.FIND('-') THEN BEGIN
        ventana.OPEN(Text010+
                     '\'+
                     Text011 +
                     Text012 +
                     Text013 +
                     Text014);
        REPEAT
          IF NOT par_conc."No Analisis" THEN BEGIN
            importe_agencia := importe_agencia + par_conc."Importe agencia";
            importe_diferencia := importe_diferencia + par_conc."Diferencia TTOO-Agencia";
          END;
          importe_to := importe_to + par_conc."Importe TTOO";

          // ----  AJS 21.09.2001  Hacemos los ROUND para visualizar los decimales deseados  ----

          ventana.UPDATE(1, ROUND(importe_agencia));
          ventana.UPDATE(2, ROUND(importe_to));

          // ----  FIN AJS 21.09.2001  Hacemos los ROUND para visualizar los decimales deseados  ----
        UNTIL par_conc.NEXT = 0;
      END;

      // ----  AJS 21.09.2001  Hacemos los ROUND para visualizar los decimales deseados  ----

      ventana.UPDATE(3, ROUND(importe_diferencia));
      SLEEP(5000);

      // ----  FIN AJS 21.09.2001  Hacemos los ROUND para visualizar los decimales deseados  ----
    END;

    PROCEDURE calculo_resumen@11(par_conc@1103355000 : Record 7010070);
    VAR
      rRes@1103355001 : Record 7009771;
      cCal@1103355002 : Codeunit 7009717;
    BEGIN
      // calculo_resumen
      //
      rRes.RESET;
      rRes.SETRANGE(Delegacion  , par_conc.Delegacion);
      rRes.SETRANGE("N§ Reserva", par_conc."N§ Reserva");
      IF rRes.FIND('+') THEN
        cCal.calculo_resumen(rRes, TRUE)
      ELSE
        ERROR(Text015);
      rRes.RESET;
    END;

    PROCEDURE RetrocedeConciliacion@5(VAR prConcil@1103355000 : Record 7010070;pwVerbose@1103355004 : Boolean);
    VAR
      lrBono@1103355001 : Record 7010013;
      lrBlank@1103355002 : Record 7010004;
      lrConcil@1103355003 : Record 7010070;
    BEGIN
      // RetrocedeConciliacion
      //

      IF pwVerbose THEN
        IF NOT CONFIRM(Text016, FALSE, prConcil."N§ HPL") THEN
          EXIT;

      lrBono.RESET;
      lrBono.SETCURRENTKEY("Tipo conciliacion",
                           "N§ HPL");

      lrBlank.RESET;
      lrBlank.SETCURRENTKEY("N§ HPL");

      lrConcil.RESET;
      lrConcil.SETCURRENTKEY("N§ HPL");

      IF prConcil.FIND('-') THEN BEGIN
        REPEAT

          lrBono.SETRANGE("N§ HPL", prConcil."N§ HPL");
          lrBono.SETRANGE(Bono    , prConcil.Bono);
          IF lrBono.FIND('-') THEN BEGIN
            lrBono.MODIFYALL("Tipo conciliacion" , lrBono."Tipo conciliacion"::"No concil");
            lrBono.MODIFYALL("Conciliacion BLANK", 0);
            lrBono.MODIFYALL("N§ HPL"            , '');
          END;

          lrBlank.SETRANGE("N§ HPL", prConcil."N§ HPL");
          lrBlank.SETRANGE(Bono    , prConcil.Bono);
          IF lrBlank.FIND('-') THEN
            lrBlank.MODIFYALL(Conciliado, FALSE);

          lrConcil := prConcil;
          lrConcil.DELETE;
        UNTIL prConcil.NEXT = 0;
      END;

      IF pwVerbose THEN
        MESSAGE(Text017);
    END;

    BEGIN
    END.
  }
}
