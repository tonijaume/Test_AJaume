OBJECT Codeunit 7316 Whse. Int. Put-away Release
{
  OBJECT-PROPERTIES
  {
    Date=13/02/07;
    Time=12:00:00;
    Version List=NAVW15.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1005 : TextConst 'ENU=There is nothing to release for %1 %2.;ESP=No hay nada para lanzar para %1 %2.';
      Text001@1000 : TextConst 'ENU=You have to delete all related Warehouse Worksheet Lines first before you can reopen the Whse. Internal Put-away.;ESP=Primero debe eliminar todas las l¡ns. hoja almac‚n relacionadas para reabrir la ubicac. interna almac‚n.';
      HideValidationDialog@1001 : Boolean;
      Text002@1002 : TextConst 'ENU=You have to delete all related Warehouse Activity Lines first before you can reopen the Whse. Internal Put-away.;ESP=Debe eliminar todas las l¡neas de actividad de almac‚n relacionadas para reabrir la ubicac. interna almac‚n.';

    PROCEDURE Release@1(WhseInternalPutAwayHeader@1000 : Record 7331);
    VAR
      Location@1004 : Record 14;
      WhsePutawayRqst@1003 : Record 7324;
      WhseInternalPutawayLine@1001 : Record 7332;
    BEGIN
      WITH WhseInternalPutAwayHeader DO BEGIN
        IF Status = Status::Released THEN
          EXIT;

        WhseInternalPutawayLine.SETRANGE("No.","No.");
        WhseInternalPutawayLine.SETFILTER(Quantity,'<>0');
        IF NOT WhseInternalPutawayLine.FIND('-') THEN
          ERROR(Text000,TABLECAPTION,"No.");

        IF "Location Code" <> '' THEN BEGIN
          Location.GET("Location Code");
          Location.TESTFIELD("Require Put-away");
        END ELSE
          CheckPutawayRequired("Location Code");

        REPEAT
          WhseInternalPutawayLine.TESTFIELD("Item No.");
          WhseInternalPutawayLine.TESTFIELD("Unit of Measure Code");
          IF Location."Directed Put-away and Pick" THEN
            WhseInternalPutawayLine.TESTFIELD("From Zone Code");
          IF Location."Bin Mandatory" THEN
            WhseInternalPutawayLine.TESTFIELD("From Bin Code");
        UNTIL WhseInternalPutawayLine.NEXT = 0;

        Status := Status::Released;
        MODIFY;

        CreateWhsePutawayRqst(WhseInternalPutAwayHeader);

        WhsePutawayRqst.SETRANGE(
          "Document Type",WhsePutawayRqst."Document Type"::"Internal Put-away");
        WhsePutawayRqst.SETRANGE("Document No.","No.");
        WhsePutawayRqst.SETRANGE(Status,Status::Open);
        WhsePutawayRqst.DELETEALL(TRUE);

        COMMIT;

      END;
    END;

    PROCEDURE Reopen@2(WhseInternalPutAwayHeader@1000 : Record 7331);
    VAR
      WhsePutawayRqst@1001 : Record 7324;
      WhseWkshLine@1002 : Record 7326;
      WhseActivLine@1003 : Record 5767;
    BEGIN
      WITH WhseInternalPutAwayHeader DO BEGIN
        IF Status = Status::Open THEN
          EXIT;

        WhseWkshLine.SETCURRENTKEY("Whse. Document Type","Whse. Document No.");
        WhseWkshLine.SETRANGE("Whse. Document Type",WhseWkshLine."Whse. Document Type"::"Internal Put-away");
        WhseWkshLine.SETRANGE("Whse. Document No.","No.");
        IF WhseWkshLine.FIND('-') THEN
          ERROR(Text001);

        WhseActivLine.SETCURRENTKEY("Whse. Document No.","Whse. Document Type","Activity Type");
        WhseActivLine.SETRANGE("Whse. Document No.","No.");
        WhseActivLine.SETRANGE("Whse. Document Type",WhseActivLine."Whse. Document Type"::"Internal Put-away");
        WhseActivLine.SETRANGE("Activity Type",WhseActivLine."Activity Type"::"Put-away");
        IF WhseActivLine.FIND('-') THEN
          ERROR(Text002);

        WhsePutawayRqst.SETRANGE("Document Type",WhsePutawayRqst."Document Type"::"Internal Put-away");
        WhsePutawayRqst.SETRANGE("Document No.","No.");
        WhsePutawayRqst.SETRANGE(Status,Status::Released);
        IF WhsePutawayRqst.FIND('-') THEN
          REPEAT
            WhsePutawayRqst.Status := Status::Open;
            WhsePutawayRqst.MODIFY;
          UNTIL WhsePutawayRqst.NEXT = 0;

        Status := Status::Open;
        MODIFY;
      END;
    END;

    LOCAL PROCEDURE CreateWhsePutawayRqst@3(VAR WhseInternalPutAwayHeader@1000 : Record 7331);
    VAR
      WhsePutawayRqst@1001 : Record 7324;
    BEGIN
      WITH WhseInternalPutAwayHeader DO BEGIN
        WhsePutawayRqst."Document Type" := WhsePutawayRqst."Document Type"::"Internal Put-away";
        WhsePutawayRqst."Document No." := "No.";
        WhsePutawayRqst.Status := Status;
        WhsePutawayRqst."Location Code" := "Location Code";
        WhsePutawayRqst."Zone Code" := "From Zone Code";
        WhsePutawayRqst."Bin Code" := "From Bin Code";
        "Document Status" := GetDocumentStatus(0);
        WhsePutawayRqst."Completely Put Away" :=
          "Document Status" = "Document Status"::"Completely Put Away";
        IF NOT WhsePutawayRqst.INSERT THEN
          WhsePutawayRqst.MODIFY;
      END;
    END;

    PROCEDURE SetHideValidationDialog@14(NewHideValidationDialog@1000 : Boolean);
    BEGIN
      HideValidationDialog := NewHideValidationDialog;
    END;

    BEGIN
    END.
  }
}
