OBJECT Codeunit 5055256 Liq. Jnl. - Post Line
{
  OBJECT-PROPERTIES
  {
    Date=13/02/07;
    Time=12:00:00;
    Version List=LIQEM5.00;
  }
  PROPERTIES
  {
    TableNo=5055256;
    Permissions=TableData 5055257=imd,
                TableData 5055258=imd;
    OnRun=BEGIN
            GLSetup.GET;
            TempJnlLineDim.RESET;
            TempJnlLineDim.DELETEALL;
            IF "Shortcut Dimension 1 Code" <> '' THEN BEGIN
              TempJnlLineDim."Table ID" := DATABASE::"Liq. Journal Line";
              TempJnlLineDim."Journal Template Name" := "Journal Template Name";
              TempJnlLineDim."Journal Batch Name" := "Journal Batch Name";
              TempJnlLineDim."Journal Line No." := "Line No.";
              TempJnlLineDim."Dimension Code" := GLSetup."Global Dimension 1 Code";
              TempJnlLineDim."Dimension Value Code" := "Shortcut Dimension 1 Code";
              TempJnlLineDim.INSERT;
            END;
            IF "Shortcut Dimension 2 Code" <> '' THEN BEGIN
              TempJnlLineDim."Table ID" := DATABASE::"Liq. Journal Line";
              TempJnlLineDim."Journal Template Name" := "Journal Template Name";
              TempJnlLineDim."Journal Batch Name" := "Journal Batch Name";
              TempJnlLineDim."Journal Line No." := "Line No.";
              TempJnlLineDim."Dimension Code" := GLSetup."Global Dimension 2 Code";
              TempJnlLineDim."Dimension Value Code" := "Shortcut Dimension 2 Code";
              TempJnlLineDim.INSERT;
            END;
            RunWithCheck(Rec,TempJnlLineDim);
          END;

  }
  CODE
  {
    VAR
      LiqJnlLine@1140000 : Record 5055256;
      LiqLedgEntry@1140001 : Record 5055257;
      LiqLedgEntry2@1140002 : Record 5055257;
      Liq@1140003 : Record 5055250;
      LiqReg@1140004 : Record 5055258;
      GenPostingSetup@1140005 : Record 252;
      TempJnlLineDim@1140006 : TEMPORARY Record 356;
      GLSetup@1140007 : Record 98;
      CheckLiqJnlLine@1140008 : Codeunit 5055255;
      DimMgt@1140009 : Codeunit 408;
      NextEntryNo@1140010 : Integer;
      LiqAcc@1140011 : Record 5055251;

    PROCEDURE GetResReg@1140000(VAR NewLiqRegister@1140000 : Record 5055258);
    BEGIN
      NewLiqRegister := LiqReg;
    END;

    PROCEDURE RunWithCheck@1140001(VAR LiqJnlLine2@1140000 : Record 5055256;VAR TempJnlLineDim2@1140001 : Record 356);
    BEGIN
      LiqJnlLine.COPY(LiqJnlLine2);
      TempJnlLineDim.RESET;
      TempJnlLineDim.DELETEALL;
      DimMgt.CopyJnlLineDimToJnlLineDim(TempJnlLineDim2,TempJnlLineDim);
      Code;
      LiqJnlLine2 := LiqJnlLine;
    END;

    LOCAL PROCEDURE Code@1140002();
    BEGIN
      WITH LiqJnlLine DO BEGIN
        IF EmptyLine THEN
          EXIT;

        CheckLiqJnlLine.RunCheck(LiqJnlLine,TempJnlLineDim);

        IF NextEntryNo = 0 THEN BEGIN
          LiqLedgEntry.LOCKTABLE;
          IF LiqLedgEntry.FIND('+') THEN
            NextEntryNo := LiqLedgEntry."Entry No.";
          NextEntryNo := NextEntryNo + 1;
        END;

        IF LiqReg."No." = 0 THEN BEGIN
          LiqReg.LOCKTABLE;
          IF (NOT LiqReg.FIND('+')) OR (LiqReg."To Entry No." <> 0) THEN BEGIN
            LiqReg.INIT;
            LiqReg."No." := LiqReg."No." + 1;
            LiqReg."From Entry No." := NextEntryNo;
            LiqReg."To Entry No." := NextEntryNo;
            LiqReg."Creation Date" := TODAY;
            LiqReg."Source Code" := "Source Code";
            LiqReg."Journal Batch Name" := "Journal Batch Name";
            LiqReg."User ID" := USERID;
            LiqReg.INSERT;
          END;
        END;
        LiqReg."To Entry No." := NextEntryNo;
        LiqReg.MODIFY;

        Liq.GET("Liquidity No.");
        IF "Liquidity Account No." <> '' THEN BEGIN
          LiqAcc.GET("Liquidity Account No.");
          LiqAcc.TESTFIELD(Blocked,FALSE);
        END;

        LiqLedgEntry.INIT;
        LiqLedgEntry."Source Code" := "Source Code";
        LiqLedgEntry."Journal Batch Name" := "Journal Batch Name";
        LiqLedgEntry."Reason Code" := "Reason Code";
        LiqLedgEntry."Gen. Bus. Posting Group" := "Gen. Bus. Posting Group";
        LiqLedgEntry."Gen. Prod. Posting Group" := "Gen. Prod. Posting Group";
        LiqLedgEntry."No. Series" := "Posting No. Series";
        LiqLedgEntry."Liquidity No." := "Liquidity No.";
        LiqLedgEntry."Due Date" :=  "Due Date";
        LiqLedgEntry."Document No." := "Document No.";
        LiqLedgEntry."Liquidity Account No." := "Liquidity Account No.";
        LiqLedgEntry."Source Type" := "Source Type";
        LiqLedgEntry.Description := Description;
        LiqLedgEntry."Currency Code" := "Currency Code";
        LiqLedgEntry."Amount (Currency)" := "Amount (Currency)";
        LiqLedgEntry."Discount %" := "Discount %";
        LiqLedgEntry."Payment Discount" := "Payment Discount";
        LiqLedgEntry."Associated Entry No." := LiqJnlLine."Associated Entry No.";
        LiqLedgEntry."Global Dimension 2 Code" := "Shortcut Dimension 2 Code";
        LiqLedgEntry."Global Dimension 1 Code" := "Shortcut Dimension 1 Code";
        LiqLedgEntry."Recurring Method" := "Recurring Method";
        LiqLedgEntry."Posting No. Series" := "Posting No. Series";
        LiqLedgEntry.Amount := Amount;
        LiqLedgEntry."Liquidity Date" := "Liquidity Date";
        LiqLedgEntry.Splitted := Splitted;
        LiqLedgEntry."Account Type" := "Account Type";
        LiqLedgEntry."Account Number" := "Account Number";

        WITH LiqLedgEntry DO BEGIN

          "Amount (Currency)" := ROUND("Amount (Currency)",0.00001);
          "Payment Discount" := ROUND("Payment Discount",0.00001);
          Amount := ROUND(Amount,0.00001);

          IF Description = Liq.Description THEN
            Description := '';
          "User ID" := USERID;
          "Entry No." := NextEntryNo;
        END;

        LiqLedgEntry.INSERT;

        DimMgt.MoveJnlLineDimToLedgEntryDim(
          TempJnlLineDim,DATABASE::"Liquidity Ledger Entry",LiqLedgEntry."Entry No.");

        NextEntryNo := NextEntryNo + 1;
      END;
    END;

    BEGIN
    END.
  }
}
