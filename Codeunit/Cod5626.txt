OBJECT Codeunit 5626 FA General Report
{
  OBJECT-PROPERTIES
  {
    Date=17/12/01;
    Time=12:00:00;
    Version List=NAVW13.10;
  }
  PROPERTIES
  {
    Permissions=TableData 5600=rm;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Posting Date Filter;ESP=Filtro fecha registro';
      Text001@1001 : TextConst 'ENU=You must specify the Starting Date and the Ending Date.;ESP=Se debe indicar la fecha inicial y final.';
      Text002@1002 : TextConst 'ENU=The Starting Date is later than the Ending Date.;ESP=La fecha inicial es posterior a la fecha final.';
      Text003@1003 : TextConst 'ENU=You must not specify closing dates.;ESP=No debe especificar las fechas de cierre.';
      Text004@1004 : TextConst 'ENU=You must specify the First Depreciation Date and the Last Depreciation Date.;ESP=Debe especificar la primera y £ltima fechas de amortizaci¢n.';
      Text005@1005 : TextConst 'ENU=The First Depreciation Date is later than the Last Depreciation Date.;ESP=La primera fecha de amortizaci¢n es posterior a la £ltima fecha de amortizaci¢n.';
      Text006@1006 : TextConst 'ENU=Sorting fixed assets;ESP=Ordenando activos fijos';
      FADeprBook@1007 : Record 5612;
      FALedgEntry@1008 : Record 5601;
      DepreciationCalc@1009 : Codeunit 5616;

    PROCEDURE GetLastDate@2(FANo@1000 : Code[20];PostingType@1001 : Integer;EndingDate@1002 : Date;DeprBookCode@1003 : Code[10];GLEntry@1004 : Boolean) : Date;
    VAR
      FirstLast@1005 : Text[1];
    BEGIN
      CLEARALL;
      IF PostingType = 0 THEN
        EXIT(0D);
      IF EndingDate = 0D THEN
        EndingDate := 31129999D;
      WITH FALedgEntry DO BEGIN
        RESET;
        IF GLEntry THEN BEGIN
          SETCURRENTKEY(
            "FA No.","Depreciation Book Code","FA Posting Category","FA Posting Type","Posting Date");
          SETRANGE("Depreciation Book Code",DeprBookCode);
          SETRANGE("FA No.",FANo);
          SETRANGE("FA Posting Category","FA Posting Category"::" ");
          SETRANGE("Posting Date",0D,EndingDate);
        END ELSE BEGIN
          DepreciationCalc.SetFAFilter(FALedgEntry,FANo,DeprBookCode,TRUE);
          SETRANGE("FA Posting Date",0D,EndingDate);
        END;
        FirstLast := '+';
        CASE PostingType OF
          FADeprBook.FIELDNO("Last Acquisition Cost Date"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Acquisition Cost");
          FADeprBook.FIELDNO("Last Depreciation Date"):
            SETRANGE("FA Posting Type","FA Posting Type"::Depreciation);
          FADeprBook.FIELDNO("Last Write-Down Date"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Write-Down");
          FADeprBook.FIELDNO("Last Appreciation Date"):
            SETRANGE("FA Posting Type","FA Posting Type"::Appreciation);
          FADeprBook.FIELDNO("Last Custom 1 Date"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Custom 1");
          FADeprBook.FIELDNO("Last Custom 2 Date"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Custom 2");
          FADeprBook.FIELDNO("Last Salvage Value Date"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Salvage Value");
          FADeprBook.FIELDNO("Acquisition Date"),
          FADeprBook.FIELDNO("G/L Acquisition Date"):
            BEGIN
              SETRANGE("FA Posting Type","FA Posting Type"::"Acquisition Cost");
              FirstLast:= '-';
            END;
          FADeprBook.FIELDNO("Disposal Date"):
            BEGIN
              SETRANGE("FA Posting Type","FA Posting Type"::"Proceeds on Disposal");
              FirstLast:= '-';
            END;
        END;

        IF FIND(FirstLast) THEN BEGIN
          IF GLEntry THEN
            EXIT("Posting Date")
          ELSE
            EXIT("FA Posting Date");
        END ELSE
          EXIT(0D);
      END;
    END;

    PROCEDURE CalcFAPostedAmount@1(FANo@1000 : Code[20];PostingType@1001 : Integer;Period@1002 : 'Before Starting Date,Net Change,at Ending Date';StartingDate@1003 : Date;EndingDate@1004 : Date;DeprBookCode@1005 : Code[10];BeforeAmount@1006 : Decimal;UntilAmount@1007 : Decimal;OnlyReclassified@1008 : Boolean;OnlyBookValue@1009 : Boolean) : Decimal;
    BEGIN
      CLEARALL;
      IF PostingType = 0 THEN
        EXIT(0);
      IF EndingDate = 0D THEN
        EndingDate := 31129999D;
      WITH FALedgEntry DO BEGIN
        CASE PostingType OF
          FADeprBook.FIELDNO("Book Value"):
            SETCURRENTKEY("FA No.","Depreciation Book Code","Part of Book Value");
          FADeprBook.FIELDNO("Depreciable Basis"):
            SETCURRENTKEY("FA No.","Depreciation Book Code","Part of Depreciable Basis");
          ELSE BEGIN
            SETCURRENTKEY(
              "FA No.","Depreciation Book Code",
              "FA Posting Category","FA Posting Type","FA Posting Date");
            SETRANGE("FA Posting Category","FA Posting Category"::" ");
          END;
        END;
        SETRANGE("FA No.",FANo);
        SETRANGE("Depreciation Book Code",DeprBookCode);
        IF OnlyReclassified THEN
          SETRANGE("Reclassification Entry",TRUE);
        IF OnlyBookValue THEN
          SETRANGE("Part of Book Value",TRUE);
        CASE PostingType OF
          FADeprBook.FIELDNO("Acquisition Cost"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Acquisition Cost");
          FADeprBook.FIELDNO(Depreciation):
            SETRANGE("FA Posting Type","FA Posting Type"::Depreciation);
          FADeprBook.FIELDNO("Write-Down"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Write-Down");
          FADeprBook.FIELDNO(Appreciation):
            SETRANGE("FA Posting Type","FA Posting Type"::Appreciation);
          FADeprBook.FIELDNO("Custom 1"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Custom 1");
          FADeprBook.FIELDNO("Custom 2"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Custom 2");
          FADeprBook.FIELDNO("Proceeds on Disposal"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Proceeds on Disposal");
          FADeprBook.FIELDNO("Gain/Loss"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Gain/Loss");
          FADeprBook.FIELDNO("Salvage Value"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Salvage Value");
          FADeprBook.FIELDNO("Book Value"):
            SETRANGE("Part of Book Value",TRUE);
          FADeprBook.FIELDNO("Depreciable Basis"):
            SETRANGE("Part of Depreciable Basis",TRUE);
        END;
        CASE Period OF
          Period::"Before Starting Date":
            SETRANGE("FA Posting Date",0D,StartingDate - 1);
          Period::"Net Change":
            SETRANGE("FA Posting Date",StartingDate,EndingDate);
          Period::"at Ending Date":
            SETRANGE("FA Posting Date",0D,EndingDate);
        END;
        CALCSUMS(Amount);

        IF (PostingType = FADeprBook.FIELDNO("Book Value")) OR
          (PostingType = FADeprBook.FIELDNO(Depreciation))
        THEN
          CASE Period OF
            Period::"Before Starting Date":
              Amount := Amount + BeforeAmount;
            Period::"Net Change":
              Amount := Amount - BeforeAmount + UntilAmount;
            Period::"at Ending Date":
              Amount := Amount + UntilAmount;
          END;
        EXIT(Amount);
      END;
    END;

    PROCEDURE CalcGLPostedAmount@5(FANo@1000 : Code[20];PostingType@1001 : Integer;Period@1002 : ' ,Disposal,Bal. Disposal';StartingDate@1003 : Date;EndingDate@1004 : Date;DeprBookCode@1005 : Code[10]) : Decimal;
    BEGIN
      CLEARALL;
      IF PostingType = 0 THEN
        EXIT(0);
      IF EndingDate = 0D THEN
        EndingDate := 31129999D;
      WITH FALedgEntry DO BEGIN
        SETCURRENTKEY(
          "FA No.","Depreciation Book Code",
          "FA Posting Category","FA Posting Type","Posting Date");
        SETRANGE("FA No.",FANo);
        SETRANGE("Depreciation Book Code",DeprBookCode);
        SETRANGE("FA Posting Category",Period);
        SETRANGE("Posting Date",StartingDate,EndingDate);
        CASE PostingType OF
          FADeprBook.FIELDNO("Acquisition Cost"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Acquisition Cost");
          FADeprBook.FIELDNO(Depreciation):
            SETRANGE("FA Posting Type","FA Posting Type"::Depreciation);
          FADeprBook.FIELDNO("Write-Down"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Write-Down");
          FADeprBook.FIELDNO(Appreciation):
            SETRANGE("FA Posting Type","FA Posting Type"::Appreciation);
          FADeprBook.FIELDNO("Custom 1"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Custom 1");
          FADeprBook.FIELDNO("Custom 2"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Custom 2");
          FADeprBook.FIELDNO("Proceeds on Disposal"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Proceeds on Disposal");
          FADeprBook.FIELDNO("Gain/Loss"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Gain/Loss");
          FADeprBook.FIELDNO("Book Value on Disposal"):
            SETRANGE("FA Posting Type","FA Posting Type"::"Book Value on Disposal");
        END;
        CALCSUMS(Amount);
        EXIT(Amount);
      END;
    END;

    PROCEDURE AppendFAPostingFilter@4(VAR FA@1000 : Record 5600;StartingDate@1001 : Date;EndingDate@1002 : Date);
    BEGIN
      IF (StartingDate = 0D) AND (EndingDate = 0D) THEN
        EXIT;
      IF StartingDate = 0D THEN
        FA.SETFILTER("FA Posting Date Filter",'..%1',EndingDate)
      ELSE IF EndingDate = 0D THEN
        FA.SETFILTER("FA Posting Date Filter",'%1..',StartingDate)
      ELSE
        FA.SETFILTER("FA Posting Date Filter",'%1..%2',StartingDate,EndingDate);
    END;

    PROCEDURE AppendPostingDateFilter@7(VAR FAFilter@1000 : Text[250];StartingDate@1001 : Date;EndingDate@1002 : Date);
    VAR
      PostingDateFilter@1003 : Text[50];
    BEGIN
      PostingDateFilter := STRSUBSTNO('%1: %2..%3',Text000,StartingDate,EndingDate);
      IF FAFilter = '' THEN
        FAFilter := PostingDateFilter
      ELSE
        FAFilter := FAFilter + STRSUBSTNO('%1 %2',',',PostingDateFilter);
    END;

    PROCEDURE ValidateDates@3(StartingDate@1000 : Date;EndingDate@1001 : Date);
    BEGIN
      IF (EndingDate = 0D) OR (StartingDate <= 01010000D) THEN
        ERROR(Text001);

      IF StartingDate > EndingDate THEN
        ERROR(Text002);

      IF (NORMALDATE(StartingDate) <> StartingDate) OR (NORMALDATE(EndingDate) <> EndingDate) THEN
        ERROR(Text003);
    END;

    PROCEDURE ValidateDeprDates@6(StartingDate@1000 : Date;EndingDate@1001 : Date);
    BEGIN
      IF (EndingDate = 0D) OR (StartingDate <= 01010000D) THEN
        ERROR(Text004);

      IF StartingDate > EndingDate THEN
        ERROR(Text005);

      IF (NORMALDATE(StartingDate) <> StartingDate) OR (NORMALDATE(EndingDate) <> EndingDate) THEN
        ERROR(Text003);
    END;

    PROCEDURE SetFAPostingGroup@8(VAR FA2@1000 : Record 5600;DeprBookCode@1001 : Code[10]);
    VAR
      FA@1002 : Record 5600;
      FADeprBook@1003 : Record 5612;
      Window@1004 : Dialog;
    BEGIN
      Window.OPEN(Text006);
      FA.LOCKTABLE;
      FA.COPY(FA2);
      FA.SETRANGE("FA Posting Group");
      IF FA.FIND('-') THEN
        REPEAT
          IF FADeprBook.GET(FA."No.",DeprBookCode) THEN BEGIN
            IF FA."FA Posting Group" <> FADeprBook."FA Posting Group" THEN BEGIN
              FA."FA Posting Group" :=  FADeprBook."FA Posting Group";
              FA.MODIFY;
            END;
          END;
        UNTIL FA.NEXT = 0;
      COMMIT;
      Window.CLOSE;
    END;

    BEGIN
    END.
  }
}
