OBJECT Codeunit 5055266 LiqAnalViewEntryToLiqEntries
{
  OBJECT-PROPERTIES
  {
    Date=13/02/07;
    Time=12:00:00;
    Version List=LIQEM5.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      AnalysisView@1140000 : Record 5055269;
      GLSetup@1140001 : Record 98;

    PROCEDURE GetLiqLedgEntries@1140000(VAR AnalysisViewEntry@1140000 : Record 5055271;VAR TempLiqLedgEntry@1140001 : Record 5055257);
    VAR
      LiqLedgEntry@1140002 : Record 5055257;
      TempLiqLedgEntry2@1140003 : TEMPORARY Record 5055257;
      AnalysisViewFilter@1140004 : Record 5055270;
      LedgEntryDim@1140005 : Record 355;
      LedgEntryDimBlanks@1140006 : Record 355;
      StartDate@1140007 : Date;
      EndDate@1140008 : Date;
      GlobalDimValue@1140009 : Code[20];
      MinEntryNo@1140010 : Integer;
      MaxEntryNo@1140011 : Integer;
      IncludeBlanks@1140012 : Boolean;
    BEGIN
      AnalysisView.GET(AnalysisViewEntry."Analysis View Code");

      IF AnalysisView."Date Compression" = AnalysisView."Date Compression"::None THEN BEGIN
        IF LiqLedgEntry.GET(AnalysisViewEntry."Entry No.") THEN BEGIN
          TempLiqLedgEntry := LiqLedgEntry;
          TempLiqLedgEntry.INSERT;
        END;
        EXIT;
      END;

      GLSetup.GET;
      TempLiqLedgEntry2.DELETEALL;

      StartDate := AnalysisViewEntry."Liquidity Date";
      EndDate := StartDate;

      WITH AnalysisView DO
        IF StartDate < "Starting Date" THEN
          StartDate := 0D
        ELSE
          IF (AnalysisViewEntry."Liquidity Date" = NORMALDATE(AnalysisViewEntry."Liquidity Date")) AND
             NOT ("Date Compression" IN ["Date Compression"::None,"Date Compression"::Day])
          THEN
            CASE "Date Compression" OF
              "Date Compression"::Week:
                EndDate := AnalysisViewEntry."Liquidity Date" + 6;
              "Date Compression"::Month:
                EndDate := CALCDATE('<+1M-1D>',AnalysisViewEntry."Liquidity Date");
              "Date Compression"::Quarter:
                EndDate := CALCDATE('<+3M-1D>',AnalysisViewEntry."Liquidity Date");
              "Date Compression"::Year:
                EndDate := CALCDATE('<+1Y-1D>',AnalysisViewEntry."Liquidity Date");
            END;

      WITH LiqLedgEntry DO BEGIN
        SETCURRENTKEY("Liquidity No.","Liquidity Account No.","Source Type","Liquidity Date");
        SETRANGE("Liquidity No.",AnalysisViewEntry."Liquidity No.");
        SETRANGE("Liquidity Account No.",AnalysisViewEntry."Liquidity Account No.");
        SETRANGE("Liquidity Date",StartDate,EndDate);

        IF GetGlobalDimValue(GLSetup."Global Dimension 1 Code",AnalysisViewEntry,GlobalDimValue) THEN
          LiqLedgEntry.SETRANGE("Global Dimension 1 Code",GlobalDimValue)
        ELSE
          IF AnalysisViewFilter.GET(AnalysisViewEntry."Analysis View Code",GLSetup."Global Dimension 1 Code")
          THEN
            LiqLedgEntry.SETFILTER("Global Dimension 1 Code",AnalysisViewFilter."Dimension Value Filter");

        IF GetGlobalDimValue(GLSetup."Global Dimension 2 Code",AnalysisViewEntry,GlobalDimValue) THEN
          LiqLedgEntry.SETRANGE("Global Dimension 2 Code",GlobalDimValue)
        ELSE
          IF AnalysisViewFilter.GET(AnalysisViewEntry."Analysis View Code",GLSetup."Global Dimension 2 Code")
          THEN
            LiqLedgEntry.SETFILTER("Global Dimension 2 Code",AnalysisViewFilter."Dimension Value Filter");

        IF FIND('-') THEN
          REPEAT
            IF DimEntryOK("Entry No.",AnalysisView."Dimension 1 Code",AnalysisViewEntry."Dimension 1 Value Code") AND
               DimEntryOK("Entry No.",AnalysisView."Dimension 2 Code",AnalysisViewEntry."Dimension 2 Value Code") AND
               DimEntryOK("Entry No.",AnalysisView."Dimension 3 Code",AnalysisViewEntry."Dimension 3 Value Code") AND
               DimEntryOK("Entry No.",AnalysisView."Dimension 4 Code",AnalysisViewEntry."Dimension 4 Value Code")
            THEN BEGIN
              TempLiqLedgEntry2 := LiqLedgEntry;
              TempLiqLedgEntry2.INSERT;
            END;
          UNTIL NEXT = 0;
      END;

      // Check for filters set...
      TempLiqLedgEntry2.SETCURRENTKEY("Entry No.");
      IF NOT TempLiqLedgEntry2.FIND('-') THEN
        EXIT;
      MinEntryNo := TempLiqLedgEntry2."Entry No.";
      TempLiqLedgEntry2.FIND('+');
      MaxEntryNo := TempLiqLedgEntry2."Entry No.";
      LedgEntryDim.SETRANGE("Table ID",DATABASE::"Liquidity Ledger Entry");
      LedgEntryDim.SETRANGE("Entry No.",MinEntryNo,MaxEntryNo);
      LedgEntryDim."Table ID" := DATABASE::"Liquidity Ledger Entry";
      WITH AnalysisViewFilter DO BEGIN
        SETRANGE("Analysis View Code",AnalysisViewEntry."Analysis View Code");
        IF FIND('-') THEN
          REPEAT
            IncludeBlanks := FilterIncludesBlanks(AnalysisViewFilter."Dimension Value Filter");
            LedgEntryDim.SETRANGE("Dimension Code",AnalysisViewFilter."Dimension Code");
            LedgEntryDim."Dimension Code" := AnalysisViewFilter."Dimension Code";
            LedgEntryDim.SETFILTER("Dimension Value Code",AnalysisViewFilter."Dimension Value Filter");

            IF TempLiqLedgEntry2.FIND('-') THEN
              REPEAT
                LedgEntryDim."Entry No." := TempLiqLedgEntry2."Entry No.";
                IF NOT LedgEntryDim.FIND THEN
                  IF IncludeBlanks THEN BEGIN
                    IF LedgEntryDimBlanks.GET(DATABASE::"Liquidity Ledger Entry",TempLiqLedgEntry2."Entry No.",
                                              AnalysisViewFilter."Dimension Code") THEN
                      TempLiqLedgEntry2.DELETE;
                  END ELSE
                    TempLiqLedgEntry2.DELETE;
              UNTIL TempLiqLedgEntry2.NEXT = 0;
          UNTIL NEXT = 0;
      END;
      TempLiqLedgEntry2.RESET;
      IF TempLiqLedgEntry2.FIND('-') THEN
        REPEAT
          TempLiqLedgEntry := TempLiqLedgEntry2;
          TempLiqLedgEntry.INSERT;
        UNTIL TempLiqLedgEntry2.NEXT = 0;
    END;

    LOCAL PROCEDURE DimEntryOK@1140001(EntryNo@1140000 : Integer;Dim@1140001 : Code[20];DimValue@1140002 : Code[20]) : Boolean;
    VAR
      LedgEntryDim@1140003 : Record 355;
    BEGIN
      IF Dim = '' THEN
        EXIT(TRUE);

      IF LedgEntryDim.GET(DATABASE::"Liquidity Ledger Entry",EntryNo,Dim) THEN
        EXIT(LedgEntryDim."Dimension Value Code" = DimValue)
      ELSE
        EXIT(DimValue = '');
    END;

    LOCAL PROCEDURE GetGlobalDimValue@1140002(GlobalDim@1140000 : Code[20];VAR AnalysisViewEntry@1140001 : Record 5055271;VAR GlobalDimValue@1140002 : Code[20]) : Boolean;
    VAR
      IsGlobalDim@1140003 : Boolean;
    BEGIN
      CASE GlobalDim OF
        AnalysisView."Dimension 1 Code":
          BEGIN
            IsGlobalDim := TRUE;
            GlobalDimValue := AnalysisViewEntry."Dimension 1 Value Code";
          END;
        AnalysisView."Dimension 2 Code":
          BEGIN
            IsGlobalDim := TRUE;
            GlobalDimValue := AnalysisViewEntry."Dimension 2 Value Code";
          END;
        AnalysisView."Dimension 3 Code":
          BEGIN
            IsGlobalDim := TRUE;
            GlobalDimValue := AnalysisViewEntry."Dimension 3 Value Code";
          END;
        AnalysisView."Dimension 4 Code":
          BEGIN
            IsGlobalDim := TRUE;
            GlobalDimValue := AnalysisViewEntry."Dimension 4 Value Code";
          END;
      END;
      EXIT(IsGlobalDim);
    END;

    LOCAL PROCEDURE FilterIncludesBlanks@1140003(NewFilter@1140000 : Code[250]) : Boolean;
    VAR
      TempDimBuf@1140001 : TEMPORARY Record 360;
    BEGIN
      WITH TempDimBuf DO BEGIN
        DELETEALL; // Necessary because of C/SIDE error
        INIT;
        INSERT;
        SETFILTER("Dimension Code",NewFilter);
        EXIT(FIND('-'));
      END;
    END;

    BEGIN
    END.
  }
}
