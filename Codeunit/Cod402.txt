OBJECT Codeunit 402 Purchase Header Apply
{
  OBJECT-PROPERTIES
  {
    Date=08/06/01;
    Time=12:00:00;
    Version List=NAVW13.00;
  }
  PROPERTIES
  {
    TableNo=38;
    OnRun=BEGIN
            PurchHeader.COPY(Rec);
            WITH PurchHeader DO BEGIN
              PayToVendorNo := "Pay-to Vendor No." ;
              VendLedgEntry.SETCURRENTKEY("Vendor No.",Open);
              VendLedgEntry.SETRANGE("Vendor No.",PayToVendorNo);
              VendLedgEntry.SETRANGE(Open,TRUE);
              IF "Applies-to ID" = '' THEN
                "Applies-to ID" := "No.";
              IF "Applies-to ID" = '' THEN
                ERROR(
                  Text000,
                  FIELDCAPTION("No."),FIELDCAPTION("Applies-to ID"));
              ApplyVendEntries.SetPurch(PurchHeader,VendLedgEntry,PurchHeader.FIELDNO("Applies-to ID"));
              ApplyVendEntries.SETRECORD(VendLedgEntry);
              ApplyVendEntries.SETTABLEVIEW(VendLedgEntry);
              ApplyVendEntries.LOOKUPMODE(TRUE);
              OK := ApplyVendEntries.RUNMODAL = ACTION::LookupOK;
              CLEAR(ApplyVendEntries);
              IF NOT OK THEN
                EXIT;
              VendLedgEntry.RESET;
              VendLedgEntry.SETCURRENTKEY("Vendor No.",Open);
              VendLedgEntry.SETRANGE("Vendor No.",PayToVendorNo);
              VendLedgEntry.SETRANGE(Open,TRUE);
              VendLedgEntry.SETRANGE("Applies-to ID","Applies-to ID");
              IF VendLedgEntry.FIND('-') THEN BEGIN
                "Applies-to Doc. Type" := 0;
                "Applies-to Doc. No." := '';
              END ELSE
                "Applies-to ID" := '';

              MODIFY;
            END;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You must specify %1 or %2.;ESP=Se debe especificar %1 o %2.';
      PurchHeader@1001 : Record 38;
      VendLedgEntry@1002 : Record 25;
      ApplyVendEntries@1003 : Form 233;
      PayToVendorNo@1004 : Code[20];
      OK@1005 : Boolean;

    BEGIN
    END.
  }
}
