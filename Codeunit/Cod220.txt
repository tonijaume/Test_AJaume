OBJECT Codeunit 220 Resource-Find Cost
{
  OBJECT-PROPERTIES
  {
    Date=21/05/03;
    Time=12:00:00;
    Version List=NAVW13.70;
  }
  PROPERTIES
  {
    TableNo=202;
    OnRun=BEGIN
            ResCost.COPY(Rec);
            WITH ResCost DO BEGIN
              ResCost2 := ResCost;

              FindResUnitCost(ResCost);

              CASE "Cost Type" OF
                "Cost Type"::Fixed: ;
                "Cost Type"::"% Extra":
                  BEGIN
                    ResCost2."Work Type Code" := '';
                    FindResUnitCost(ResCost2);
                    "Direct Unit Cost" := ResCost2."Direct Unit Cost" * (1 + "Direct Unit Cost" / 100);
                    "Unit Cost" := ResCost2."Unit Cost" * (1 + "Unit Cost" / 100);
                  END;
                "Cost Type"::"LCY Extra":
                  BEGIN
                    ResCost2."Work Type Code" := '';
                    FindResUnitCost(ResCost2);
                    "Direct Unit Cost" := ResCost2."Direct Unit Cost" + "Direct Unit Cost";
                    "Unit Cost" := ResCost2."Unit Cost" + "Unit Cost";
                  END;
              END;
            END;
            Rec := ResCost;
          END;

  }
  CODE
  {
    VAR
      ResCost@1000 : Record 202;
      ResCost2@1001 : Record 202;
      Res@1002 : Record 156;

    LOCAL PROCEDURE FindResUnitCost@1(VAR NearestResCost@1000 : Record 202);
    BEGIN
      WITH NearestResCost DO BEGIN
        IF GET(Type::Resource,Code,"Work Type Code") THEN
          EXIT;
        Res.GET(Code);
        IF GET(Type::"Group(Resource)",Res."Resource Group No.","Work Type Code") THEN
          EXIT;
        IF GET(Type::All,'',"Work Type Code") THEN
          EXIT;
        INIT;
        Code  := Res."No.";
        "Direct Unit Cost" := Res."Direct Unit Cost";
        "Unit Cost" := Res."Unit Cost";
      END;
    END;

    BEGIN
    END.
  }
}
