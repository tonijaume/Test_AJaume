OBJECT Codeunit 5639 FA Get Journal
{
  OBJECT-PROPERTIES
  {
    Date=08/06/01;
    Time=12:00:00;
    Version List=NAVW13.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You cannot duplicate using the current journal. Check the table %1.;ESP=No se puede duplicar utilizando el diario actual. Compruebe la tabla %1.';
      DeprBook@1001 : Record 5611;
      FAJnlSetup@1002 : Record 5605;
      FAJnlTemplate@1003 : Record 5619;
      FAJnlBatch@1004 : Record 5620;
      GenJnlTemplate@1005 : Record 80;
      GenJnlBatch@1006 : Record 232;
      InsuranceJnlTempl@1007 : Record 5633;
      InsuranceJnlBatch@1008 : Record 5634;
      TemplateName2@1009 : Code[10];
      BatchName2@1010 : Code[10];

    PROCEDURE JnlName@1(DeprBookCode@1000 : Code[10];BudgetAsset@1001 : Boolean;FAPostingType@1002 : 'Acquisition Cost,Depreciation,Write-Down,Appreciation,Custom 1,Custom 2,Disposal,Maintenance,Salvage Value';VAR GLIntegration@1003 : Boolean;VAR TemplateName@1004 : Code[10];VAR BatchName@1005 : Code[10]);
    VAR
      GLIntegration2@1006 : Boolean;
    BEGIN
      DeprBook.GET(DeprBookCode);
      IF NOT FAJnlSetup.GET(DeprBookCode,USERID) THEN
        FAJnlSetup.GET(DeprBookCode,'');
      GLIntegration2 := GLIntegration;
      GLIntegration := CalcGLIntegration(BudgetAsset,FAPostingType);
      BatchName2 := BatchName;
      TemplateName2 := TemplateName;
      IF GLIntegration THEN BEGIN
        FAJnlSetup.TESTFIELD("Gen. Jnl. Template Name");
        FAJnlSetup.TESTFIELD("Gen. Jnl. Batch Name");
        TemplateName := FAJnlSetup."Gen. Jnl. Template Name";
        BatchName := FAJnlSetup."Gen. Jnl. Batch Name";
        GenJnlTemplate.GET(TemplateName);
        GenJnlBatch.GET(TemplateName,BatchName);
      END ELSE BEGIN
        FAJnlSetup.TESTFIELD("FA Jnl. Batch Name");
        FAJnlSetup.TESTFIELD("FA Jnl. Template Name");
        TemplateName := FAJnlSetup."FA Jnl. Template Name";
        BatchName := FAJnlSetup."FA Jnl. Batch Name";
        FAJnlTemplate.GET(TemplateName);
        FAJnlBatch.GET(TemplateName,BatchName);
      END;
      IF (GLIntegration = GLIntegration2) AND
         (BatchName = BatchName2) AND
         (TemplateName = TemplateName2)
      THEN
        ERROR(Text000,FAJnlSetup.TABLECAPTION);
    END;

    PROCEDURE InsuranceJnlName@3(DeprBookCode@1000 : Code[10];VAR TemplateName@1001 : Code[10];VAR BatchName@1002 : Code[10]);
    BEGIN
      DeprBook.GET(DeprBookCode);
      IF NOT FAJnlSetup.GET(DeprBookCode,USERID) THEN
        FAJnlSetup.GET(DeprBookCode,'');
      FAJnlSetup.TESTFIELD("Insurance Jnl. Template Name");
      FAJnlSetup.TESTFIELD("Insurance Jnl. Batch Name");
      BatchName := FAJnlSetup."Insurance Jnl. Batch Name";
      TemplateName := FAJnlSetup."Insurance Jnl. Template Name";
      InsuranceJnlTempl.GET(TemplateName);
      InsuranceJnlBatch.GET(TemplateName,BatchName);
    END;

    PROCEDURE SetGenJnlRange@2(VAR GenJnlLine@1000 : Record 81;TemplateName@1001 : Code[10];BatchName@1002 : Code[10]);
    BEGIN
      WITH GenJnlLine DO BEGIN
        RESET;
        "Journal Template Name" := TemplateName;
        "Journal Batch Name" := BatchName;
        SETRANGE("Journal Template Name",TemplateName);
        SETRANGE("Journal Batch Name",BatchName);
        IF FIND('+') THEN ;
        INIT;
      END;
    END;

    PROCEDURE SetFAJnlRange@5(VAR FAJnlLine@1000 : Record 5621;TemplateName@1001 : Code[10];BatchName@1002 : Code[10]);
    BEGIN
      WITH FAJnlLine DO BEGIN
        RESET;
        "Journal Template Name" := TemplateName;
        "Journal Batch Name" := BatchName;
        SETRANGE("Journal Template Name",TemplateName);
        SETRANGE("Journal Batch Name",BatchName);
        IF FIND('+') THEN ;
        INIT;
      END;
    END;

    PROCEDURE SetInsuranceJnlRange@10(VAR InsuranceJnlLine@1000 : Record 5635;TemplateName@1001 : Code[10];BatchName@1002 : Code[10]);
    BEGIN
      WITH InsuranceJnlLine DO BEGIN
        RESET;
        "Journal Template Name" := TemplateName;
        "Journal Batch Name" := BatchName;
        SETRANGE("Journal Template Name",TemplateName);
        SETRANGE("Journal Batch Name",BatchName);
        IF FIND('+') THEN ;
        INIT;
      END;
    END;

    LOCAL PROCEDURE CalcGLIntegration@4(BudgetAsset@1000 : Boolean;FAPostingType@1001 : 'Acquisition Cost,Depreciation,Write-Down,Appreciation,Custom 1,Custom 2,Disposal,Maintenance,Salvage Value') : Boolean;
    BEGIN
      IF BudgetAsset THEN
        EXIT(FALSE);
      WITH DeprBook DO BEGIN
        CASE FAPostingType OF
          FAPostingType::"Acquisition Cost":
            EXIT("G/L Integration - Acq. Cost");
          FAPostingType::Depreciation:
            EXIT("G/L Integration - Depreciation");
          FAPostingType::"Write-Down":
            EXIT("G/L Integration - Write-Down");
          FAPostingType::Appreciation:
            EXIT("G/L Integration - Appreciation");
          FAPostingType::"Custom 1":
            EXIT("G/L Integration - Custom 1");
          FAPostingType::"Custom 2":
            EXIT("G/L Integration - Custom 2");
          FAPostingType::Disposal:
            EXIT("G/L Integration - Disposal");
          FAPostingType::Maintenance:
            EXIT("G/L Integration - Maintenance");
          FAPostingType::"Salvage Value":
            EXIT(FALSE);
        END;
      END;
    END;

    BEGIN
    END.
  }
}
