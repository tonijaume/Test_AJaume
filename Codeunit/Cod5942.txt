OBJECT Codeunit 5942 ServContractQuote-Tmpl. Upd.
{
  OBJECT-PROPERTIES
  {
    Date=13/02/07;
    Time=12:00:00;
    Version List=NAVW15.00;
  }
  PROPERTIES
  {
    TableNo=5965;
    OnRun=BEGIN
            ContractTemplate.RESET;
            IF NOT ContractTemplate.FIND('-') THEN
              EXIT;

            TESTFIELD("Contract No.");

            IF FORM.RUNMODAL(FORM::"Service Contract Template List",ContractTemplate) = ACTION::LookupOK THEN BEGIN
              ServContract := Rec;
              ServContract.Description := ContractTemplate.Description;
              ServContract.VALIDATE("Contract Group Code",ContractTemplate."Contract Group Code");
              ServContract.VALIDATE("Service Order Type",ContractTemplate."Service Order Type");
              ServContract.VALIDATE("Service Period",ContractTemplate."Default Service Period");
              ServContract.VALIDATE("Price Update Period",ContractTemplate."Price Update Period");
              ServContract.VALIDATE("Response Time (Hours)",ContractTemplate."Default Response Time (Hours)");
              ServContract.VALIDATE("Max. Labor Unit Price",ContractTemplate."Max. Labor Unit Price");
              ServContract.VALIDATE("Invoice after Service",ContractTemplate."Invoice after Service");
              ServContract.VALIDATE("Invoice Period",ContractTemplate."Invoice Period");
              ServContract.VALIDATE("Price Inv. Increase Code",ContractTemplate."Price Inv. Increase Code");
              ServContract.VALIDATE("Allow Unbalanced Amounts",ContractTemplate."Allow Unbalanced Amounts");
              ServContract.VALIDATE("Contract Lines on Invoice",ContractTemplate."Contract Lines on Invoice");
              ServContract.VALIDATE("Combine Invoices",ContractTemplate."Combine Invoices");
              ServContract.VALIDATE("Automatic Credit Memos",ContractTemplate."Automatic Credit Memos");
              ServContract.VALIDATE(Prepaid,ContractTemplate.Prepaid);
              ServContract.VALIDATE("Serv. Contract Acc. Gr. Code",ContractTemplate."Serv. Contract Acc. Gr. Code");
              ServContract."Template No." := ContractTemplate."No.";

              ServContract.CreateDim(
                DATABASE::"Service Contract Template",ContractTemplate."No.",
                0,'',0,'',0,'');
              WITH ServContract DO BEGIN
                CreateDim(
                  DATABASE::"Service Contract Template","Template No.",
                  DATABASE::Customer,"Bill-to Customer No.",
                  DATABASE::"Salesperson/Purchaser","Salesperson Code",
                  DATABASE::"Responsibility Center","Responsibility Center");
              END;

              ContractDisc.RESET;
              ContractDisc.SETRANGE("Contract Type",ServContract."Contract Type");
              ContractDisc.SETRANGE("Contract No.",ServContract."Contract No.");
              ContractDisc.DELETEALL;

              TemplateDisc.RESET;
              TemplateDisc.SETRANGE("Contract Type",TemplateDisc."Contract Type"::Template);
              TemplateDisc.SETRANGE("Contract No.",ContractTemplate."No.");
              IF TemplateDisc.FIND('-') THEN
                REPEAT
                  ContractDisc := TemplateDisc;
                  ContractDisc."Contract Type" := ServContract."Contract Type";
                  ContractDisc."Contract No." := ServContract."Contract No.";
                  ContractDisc.INSERT;
                UNTIL TemplateDisc.NEXT = 0;

              Rec := ServContract;
            END;
          END;

  }
  CODE
  {
    VAR
      ServContract@1000 : Record 5965;
      ContractTemplate@1001 : Record 5968;
      ContractDisc@1002 : Record 5972;
      TemplateDisc@1003 : Record 5972;
      DefDim@1004 : Record 352;
      ServContractDim@1005 : Record 389;
      DimMgt@1006 : Codeunit 408;

    BEGIN
    END.
  }
}
