OBJECT Codeunit 432 Consolidate
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01,NAVES6.00;
  }
  PROPERTIES
  {
    TableNo=220;
    Permissions=TableData 17=rimd;
    OnRun=VAR
            EmptyJnlLineDim@1002 : TEMPORARY Record 356;
            PreviousDate@1000 : Date;
            i@1001 : Integer;
            BalanceAmount@1003 : Decimal;
          BEGIN
            BusUnit := Rec;
            IF NORMALDATE(EndingDate) - NORMALDATE(StartingDate) + 1 > ARRAYLEN(RoundingResiduals) THEN
              ReportError(STRSUBSTNO(Text008,ARRAYLEN(RoundingResiduals)));

            IF ("Starting Date" <> 0D) OR ("Ending Date" <> 0D) THEN BEGIN
              IF "Starting Date" = 0D THEN
                ReportError(STRSUBSTNO(
                  Text033,FIELDCAPTION("Starting Date"),
                  FIELDCAPTION("Ending Date"),"Company Name"));
              IF "Ending Date" = 0D THEN
                ReportError(STRSUBSTNO(
                  Text033,FIELDCAPTION("Ending Date"),
                  FIELDCAPTION("Starting Date"),"Company Name"));
              IF "Starting Date" > "Ending Date" THEN
                ReportError(STRSUBSTNO(
                  Text032,FIELDCAPTION("Starting Date"),
                  FIELDCAPTION("Ending Date"),"Company Name"));
            END;

            ConsolidatingClosingDate :=
              (StartingDate = EndingDate) AND
              (StartingDate <> NORMALDATE(StartingDate));
            IF (StartingDate <> NORMALDATE(StartingDate)) AND
               (StartingDate <> EndingDate)
            THEN
              ReportError(Text030);

            ReadGLSetup;
            ClearInternals;
            Window.OPEN(Text001+Text002+Text003+Text004);
            Window.UPDATE(1,BusUnit.Code);

            IF NOT TestMode THEN BEGIN
              UpdatePhase(Text018);
              ClearPreviousConsolidation;
            END;

            IF ("Last Balance Currency Factor" <> 0) AND
               ("Balance Currency Factor" <> "Last Balance Currency Factor")
            THEN BEGIN
              UpdatePhase(Text019);
              UpdatePriorPeriodBalances;
            END;

            // Consolidate Current Entries
            UpdatePhase(Text020);
            CLEAR(GenJnlLine);
            GenJnlLine."Business Unit Code" := BusUnit.Code;
            GenJnlLine."Document No." := GLDocNo;
            GenJnlLine."Source Code" := ConsolidSourceCode;
            SubsidGLEntry.RESET;
            SubsidGLEntry.SETCURRENTKEY("G/L Account No.","Posting Date");
            SubsidGLEntry.SETRANGE("Posting Date",StartingDate,EndingDate);
            SubsidGLAcc.RESET;
            IF SubsidGLAcc.FIND('-') THEN
              REPEAT
                Window.UPDATE(3,SubsidGLAcc."No.");
                TestGLAccounts;
                TempGLEntry.RESET;
                TempGLEntry.DELETEALL;
                DimBufMgt.DeleteAllDimensions;
                SubsidGLEntry.SETRANGE("G/L Account No.",SubsidGLAcc."No.");
                PreviousDate := 0D;
                IF SubsidGLEntry.FIND('-') THEN
                  REPEAT
                    IF (SubsidGLEntry."Posting Date" <> NORMALDATE(SubsidGLEntry."Posting Date")) AND
                       NOT ConsolidatingClosingDate
                    THEN
                      ReportError(STRSUBSTNO(
                        Text031,
                        SubsidGLEntry.TABLECAPTION,
                        SubsidGLEntry.FIELDCAPTION("Posting Date"),
                        SubsidGLEntry."Posting Date"));
                    IF (SubsidGLAcc."Consol. Translation Method" = SubsidGLAcc."Consol. Translation Method"::"Historical Rate") AND
                       (SubsidGLEntry."Posting Date" <> PreviousDate)
                    THEN BEGIN
                      IF PreviousDate <> 0D THEN BEGIN
                        TempDimBufOut.RESET;
                        TempDimBufOut.DELETEALL;
                        IF TempGLEntry.FIND('-') THEN
                          REPEAT
                            IF NOT SkipAllDimensions THEN BEGIN
                              DimBufMgt.GetDimensions(TempGLEntry."Entry No.",TempDimBufOut);
                              TempDimBufOut.SETRANGE("Entry No.",TempGLEntry."Entry No.");
                            END;
                            CreateAndPostGenJnlLine(GenJnlLine,TempGLEntry,TempDimBufOut,TRUE);
                            CreateAndPostGenJnlLine(GenJnlLine,TempGLEntry,TempDimBufOut,FALSE);
                          UNTIL TempGLEntry.NEXT = 0;
                      END;
                      TempGLEntry.RESET;
                      TempGLEntry.DELETEALL;
                      DimBufMgt.DeleteAllDimensions;
                      PreviousDate := SubsidGLEntry."Posting Date";
                    END;
                    TempDimBufIn.RESET;
                    TempDimBufIn.DELETEALL;
                    IF NOT SkipAllDimensions THEN BEGIN
                      SubsidLedgEntryDim.SETRANGE("Entry No.",SubsidGLEntry."Entry No.");
                      IF SubsidLedgEntryDim.FIND('-') THEN
                        REPEAT
                          IF TempSelectedDim.GET('',0,0,'',SubsidLedgEntryDim."Dimension Code") THEN BEGIN
                            TempDimBufIn.INIT;
                            TempDimBufIn."Table ID" := DATABASE::"G/L Entry";
                            TempDimBufIn."Entry No." := SubsidGLEntry."Entry No.";
                            TempDimBufIn."Dimension Code" := SubsidLedgEntryDim."Dimension Code";
                            TempDimBufIn."Dimension Value Code" := SubsidLedgEntryDim."Dimension Value Code";
                            TempDimBufIn.INSERT;
                          END;
                        UNTIL SubsidLedgEntryDim.NEXT = 0;
                    END;
                    UpdateTempGLEntry(SubsidGLEntry);
                  UNTIL SubsidGLEntry.NEXT = 0;
                  TempDimBufOut.RESET;
                  TempDimBufOut.DELETEALL;
                  IF TempGLEntry.FIND('-') THEN
                    REPEAT
                      IF NOT SkipAllDimensions THEN BEGIN
                        DimBufMgt.GetDimensions(TempGLEntry."Entry No.",TempDimBufOut);
                        TempDimBufOut.SETRANGE("Entry No.",TempGLEntry."Entry No.");
                      END;
                      CreateAndPostGenJnlLine(GenJnlLine,TempGLEntry,TempDimBufOut,TRUE);
                      CreateAndPostGenJnlLine(GenJnlLine,TempGLEntry,TempDimBufOut,FALSE);
                    UNTIL TempGLEntry.NEXT = 0;
              UNTIL SubsidGLAcc.NEXT = 0;

            // Post balancing entries and adjustments
            UpdatePhase(Text025);

            FOR i := 1 TO NORMALDATE(EndingDate) - NORMALDATE(StartingDate) + 1 DO BEGIN
              IF ExchRateAdjAmounts[i] <> 0 THEN BEGIN
                GenJnlLine.Amount := ExchRateAdjAmounts[i];
                IF (BusUnit."Consolidation %" < 100) AND
                   (BusUnit."Consolidation %" > 0)
                THEN BEGIN
                  GenJnlLine.Amount := GenJnlLine.Amount * 100 / BusUnit."Consolidation %";
                  MinorExchRateAdjAmts[i] :=
                    MinorExchRateAdjAmts[i] - GenJnlLine.Amount + ExchRateAdjAmounts[i];
                END;
                IF GenJnlLine.Amount < 0 THEN BEGIN
                  BusUnit.TESTFIELD("Exch. Rate Gains Acc.");
                  GenJnlLine."Account No." := BusUnit."Exch. Rate Gains Acc.";
                END ELSE BEGIN
                  BusUnit.TESTFIELD("Exch. Rate Losses Acc.");
                  GenJnlLine."Account No." := BusUnit."Exch. Rate Losses Acc.";
                END;
                Window.UPDATE(3,GenJnlLine."Account No.");
                IF NOT ConsolidatingClosingDate THEN
                  GenJnlLine."Posting Date" := StartingDate + i - 1
                ELSE
                  GenJnlLine."Posting Date" := StartingDate;
                GenJnlLine.Description := STRSUBSTNO(Text015,WORKDATE);
                GenJnlPostLineTmp(GenJnlLine,EmptyJnlLineDim);
                RoundingResiduals[i] := RoundingResiduals[i] + GenJnlLine.Amount;
              END;
              IF CompExchRateAdjAmts[i] <> 0 THEN BEGIN
                GenJnlLine.Amount := CompExchRateAdjAmts[i];
                IF (BusUnit."Consolidation %" < 100) AND
                   (BusUnit."Consolidation %" > 0)
                THEN BEGIN
                  GenJnlLine.Amount := GenJnlLine.Amount * 100 / BusUnit."Consolidation %";
                  MinorExchRateAdjAmts[i] :=
                    MinorExchRateAdjAmts[i] - GenJnlLine.Amount + CompExchRateAdjAmts[i];
                END;
                IF GenJnlLine.Amount < 0 THEN BEGIN
                  BusUnit.TESTFIELD("Comp. Exch. Rate Gains Acc.");
                  GenJnlLine."Account No." := BusUnit."Comp. Exch. Rate Gains Acc.";
                END ELSE BEGIN
                  BusUnit.TESTFIELD("Comp. Exch. Rate Losses Acc.");
                  GenJnlLine."Account No." := BusUnit."Comp. Exch. Rate Losses Acc.";
                END;
                Window.UPDATE(3,GenJnlLine."Account No.");
                IF NOT ConsolidatingClosingDate THEN
                  GenJnlLine."Posting Date" := StartingDate + i - 1
                ELSE
                  GenJnlLine."Posting Date" := StartingDate;
                GenJnlLine.Description := STRSUBSTNO(Text027 + Text015,WORKDATE);
                GenJnlPostLineTmp(GenJnlLine,EmptyJnlLineDim);
                RoundingResiduals[i] := RoundingResiduals[i] + GenJnlLine.Amount;
              END;
              IF EqExchRateAdjAmts[i] <> 0 THEN BEGIN
                GenJnlLine.Amount := EqExchRateAdjAmts[i];
                IF (BusUnit."Consolidation %" < 100) AND
                   (BusUnit."Consolidation %" > 0)
                THEN BEGIN
                  GenJnlLine.Amount := GenJnlLine.Amount * 100 / BusUnit."Consolidation %";
                  MinorExchRateAdjAmts[i] :=
                    MinorExchRateAdjAmts[i] - GenJnlLine.Amount + EqExchRateAdjAmts[i];
                END;
                IF GenJnlLine.Amount < 0 THEN BEGIN
                  BusUnit.TESTFIELD("Equity Exch. Rate Gains Acc.");
                  GenJnlLine."Account No." := BusUnit."Equity Exch. Rate Gains Acc.";
                END ELSE BEGIN
                  BusUnit.TESTFIELD("Equity Exch. Rate Losses Acc.");
                  GenJnlLine."Account No." := BusUnit."Equity Exch. Rate Losses Acc.";
                END;
                Window.UPDATE(3,GenJnlLine."Account No.");
                IF NOT ConsolidatingClosingDate THEN
                  GenJnlLine."Posting Date" := StartingDate + i - 1
                ELSE
                  GenJnlLine."Posting Date" := StartingDate;
                GenJnlLine.Description := STRSUBSTNO(Text028 + Text015,WORKDATE);
                GenJnlPostLineTmp(GenJnlLine,EmptyJnlLineDim);
                RoundingResiduals[i] := RoundingResiduals[i] + GenJnlLine.Amount;
              END;
              IF MinorExchRateAdjAmts[i] <> 0 THEN BEGIN
                GenJnlLine.Amount := MinorExchRateAdjAmts[i];
                IF GenJnlLine.Amount < 0 THEN BEGIN
                  BusUnit.TESTFIELD("Minority Exch. Rate Gains Acc.");
                  GenJnlLine."Account No." := BusUnit."Minority Exch. Rate Gains Acc.";
                END ELSE BEGIN
                  BusUnit.TESTFIELD("Minority Exch. Rate Losses Acc");
                  GenJnlLine."Account No." := BusUnit."Minority Exch. Rate Losses Acc";
                END;
                Window.UPDATE(3,GenJnlLine."Account No.");
                GenJnlLine."Posting Date" := StartingDate + i - 1;
                GenJnlLine.Description := STRSUBSTNO(Text029 + Text015,WORKDATE);
                GenJnlPostLineTmp(GenJnlLine,EmptyJnlLineDim);
                RoundingResiduals[i] := RoundingResiduals[i] + GenJnlLine.Amount;
              END;
              IF RoundingResiduals[i] <> 0 THEN BEGIN
                GenJnlLine.Amount := -RoundingResiduals[i];
                BusUnit.TESTFIELD("Residual Account");
                GenJnlLine."Account No." := BusUnit."Residual Account";
                Window.UPDATE(3,GenJnlLine."Account No.");
                IF NOT ConsolidatingClosingDate THEN
                  GenJnlLine."Posting Date" := StartingDate + i - 1
                ELSE
                  GenJnlLine."Posting Date" := StartingDate;
                GenJnlLine.Description :=
                  COPYSTR(
                    STRSUBSTNO(Text016,WORKDATE,GenJnlLine.Amount),
                    1,MAXSTRLEN(GenJnlLine.Description));
                GenJnlPostLineTmp(GenJnlLine,EmptyJnlLineDim);
              END;
            END;

            IF NOT TestMode THEN BEGIN
              UpdatePhase(Text026);
              GenJnlPostLineFinally;
            END;
            Window.CLOSE;

            IF NOT TestMode THEN BEGIN
              BusUnit."Last Balance Currency Factor" := BusUnit."Balance Currency Factor";
              BusUnit.MODIFY;
            END;

            IF AnalysisViewEntriesDeleted THEN
              MESSAGE(Text005);
          END;

  }
  CODE
  {
    VAR
      BusUnit@1026 : Record 220;
      ConsolidGLAcc@1017 : Record 15;
      ConsolidGLEntry@1018 : Record 17;
      ConsolidLedgEntryDim@1019 : Record 355;
      ConsolidExchRate@1067 : Record 330;
      SubsidGLAcc@1000 : TEMPORARY Record 15;
      SubsidGLEntry@1001 : TEMPORARY Record 17;
      SubsidLedgEntryDim@1002 : TEMPORARY Record 355;
      SubsidExchRate@1003 : TEMPORARY Record 330;
      TempSelectedDim@1048 : TEMPORARY Record 369;
      GenJnlLine@1050 : Record 81;
      TempGenJnlLine@1054 : TEMPORARY Record 81;
      TempJnlLineDim@1055 : TEMPORARY Record 356;
      TempDimBufIn@1058 : TEMPORARY Record 360;
      TempDimBufOut@1057 : TEMPORARY Record 360;
      DimBufMgt@1059 : Codeunit 411;
      TempGLEntry@1060 : TEMPORARY Record 17;
      Window@1046 : Dialog;
      GLDocNo@1028 : Code[20];
      ProductVersion@1005 : Code[10];
      FormatVersion@1006 : Code[10];
      SubCompanyName@1007 : Text[30];
      CurrencyLCY@1008 : Code[10];
      CurrencyACY@1009 : Code[10];
      CurrencyPCY@1010 : Code[10];
      StoredCheckSum@1011 : Decimal;
      StartingDate@1013 : Date;
      EndingDate@1014 : Date;
      GlobalDim1Code@1020 : Code[20];
      GlobalDim2Code@1021 : Code[20];
      ConsolidSourceCode@1051 : Code[10];
      RoundingResiduals@1066 : ARRAY [500] OF Decimal;
      ExchRateAdjAmounts@1068 : ARRAY [500] OF Decimal;
      CompExchRateAdjAmts@1034 : ARRAY [500] OF Decimal;
      EqExchRateAdjAmts@1035 : ARRAY [500] OF Decimal;
      MinorExchRateAdjAmts@1037 : ARRAY [500] OF Decimal;
      DeletedAmounts@1022 : ARRAY [500] OF Decimal;
      DeletedDates@1023 : ARRAY [500] OF Date;
      DeletedIndex@1024 : Integer;
      MaxDeletedIndex@1025 : Integer;
      AnalysisViewEntriesDeleted@1027 : Boolean;
      Text000@1029 : TextConst 'ENU=Enter a document number.;ESP=Introduzca un n£mero de documento.';
      Text001@1030 : TextConst 'ENU=Consolidating companies...\\;ESP=Consolidando empresas.....\\';
      Text002@1031 : TextConst 'ENU=Business Unit Code   #1###################\;ESP=C¢digo empresa       #1###################\';
      Text003@1032 : TextConst 'ENU=Phase                #2############################\;ESP=Fase                 #2############################\';
      Text004@1033 : TextConst 'ENU=G/L Account No.      #3##################;ESP=N£mero de cuenta     #3##################';
      Text005@1071 : TextConst 'ENU=Analysis View Entries were deleted during the consolidation. An update is necessary.;ESP=Se han eliminado Movs. vista an lisis durante la consolidaci¢n. Es necesario actualizar.';
      Text006@1016 : TextConst 'ENU=There are more than %1 errors.;ESP=Hay m s de %1 errores.';
      Text008@1036 : TextConst 'ENU=The consolidation can include a maximum of %1 days.;ESP=La consolidaci¢n puede incluir %1 d¡as como m ximo.';
      Text010@1038 : TextConst 'ENU="Previously consolidated entries cannot be erased because this would cause the general ledger to be out of balance by an amount of %1. ";ESP="No es posible borrar entradas consolidadas con anterioridad, la contabilidad quedar¡a descuadrada por un importe de %1. "';
      Text011@1040 : TextConst 'ENU=" Check for manually posted G/L entries on %2 for posting across business units.";ESP=" Buscar movimientos contables registrados manualmente el %2 para registrarlos en las diferentes empresas."';
      Text013@1041 : TextConst 'ENU=%1 adjusted from %2 to %3 on %4;ESP=El %4, %1 pasa de %2 a %3';
      Text014@1042 : TextConst 'ENU=Adjustment of opening entries on %1;ESP=Ajuste de movs. pendientes el %1';
      Text015@1043 : TextConst 'ENU=Exchange rate adjustment on %1;ESP=Ajuste de tipo de cambio el %1';
      Text016@1044 : TextConst 'ENU=Posted %2 to residual account as of %1;ESP=Registrado %2 en cta. residual el %1';
      Text017@1045 : TextConst 'ENU=%1 at exchange rate %2 on %3;ESP=%1 con tipo de cambio %2 el %3';
      Text018@1047 : TextConst 'ENU=Clear Previous Consolidation;ESP=Borrar consolidaci¢n anterior';
      SkipAllDimensions@1049 : Boolean;
      Text019@1052 : TextConst 'ENU=Update Prior Period Balances;ESP=Actualizar saldos del periodo anterior';
      ConsolidatingClosingDate@1074 : Boolean;
      ExchRateAdjAmount@1053 : Decimal;
      HistoricalCurrencyFactor@1079 : Decimal;
      NextLineNo@1056 : Integer;
      Text020@1061 : TextConst 'ENU=Consolidate Current Data;ESP=Consolidar datos actuales';
      Text021@1062 : TextConst 'ENU="Within the Subsidiary (%5), there are two G/L Accounts: %1 and %4; which refer to the same %2, but with a different %3.";ESP="En la subsidiaria (%5) hay dos cuentas: %1 y %4; que hacen referencia al mismo valor de %2, pero su valor para %3 es distinto."';
      Text022@1063 : TextConst 'ENU=%1 %2, referenced by %5 %3 %4, does not exist in the consolidated %3 table.;ESP=%1 %2 (con referencia desde %5 %3 %4) no existe en la tabla consolidada %3.';
      Text023@1064 : TextConst 'ENU=%7 %1 %2 must have the same %3 as consolidated %1 %4. (%5 and %6, respectively);ESP=%3 debe ser igual en %7 %1 %2 y en la %1 consolidada %4. (%5 y %6, respectivamente)';
      Text024@1065 : TextConst 'ENU=%1 at %2 %3;ESP=%1 en %2 %3';
      Text025@1069 : TextConst 'ENU=Calculate Residual Entries;ESP=Calcular movimientos residuales';
      Text026@1070 : TextConst 'ENU=Post to General Ledger;ESP=Registrar en contabilidad';
      Text027@1039 : TextConst 'ENU="Composite ";ESP="Compuesto "';
      Text028@1072 : TextConst 'ENU="Equity ";ESP="Neto "';
      Text029@1073 : TextConst 'ENU="Minority ";ESP="Minoritario "';
      TestMode@1004 : Boolean;
      CurErrorIdx@1012 : Integer;
      ErrorText@1015 : ARRAY [500] OF Text[250];
      Text030@1076 : TextConst 'ENU=When using closing dates, the starting and ending dates must be the same.;ESP=Usando fech. cerradas, el principio y final de las fech. debe ser el mismo.';
      Text031@1075 : TextConst 'ENU=A %1 with %2 on a closing date (%3) was found while consolidating non-closing entries.;ESP=Al consolidar los movimientos no cerrados se encontr¢ un %1 con %2 en la fecha de cierre (%3).';
      Text032@1077 : TextConst 'ENU=The %1 is later than the %2 in company %3.;ESP=%1 es posterior al valor de %2 para la compa¤¡a %3.';
      Text033@1078 : TextConst 'ENU=%1 must not be empty when %2 is not empty, in company %3.;ESP=%1 no debe estar vac¡o si %2 no lo est  para la compa¤¡a %3.';

    PROCEDURE SetDocNo@15(NewDocNo@1000 : Code[20]);
    BEGIN
      GLDocNo := NewDocNo;
      IF GLDocNo = '' THEN
        ERROR(Text000);
    END;

    PROCEDURE SetSelectedDim@20(VAR SelectedDim@1000 : Record 369);
    BEGIN
      TempSelectedDim.RESET;
      TempSelectedDim.DELETEALL;
      SkipAllDimensions := NOT SelectedDim.FIND('-');
      IF NOT SkipAllDimensions THEN
        REPEAT
          TempSelectedDim := SelectedDim;
          TempSelectedDim."User ID" := '';
          TempSelectedDim."Object Type" := 0;
          TempSelectedDim."Object ID" := 0;
          TempSelectedDim.INSERT;
        UNTIL SelectedDim.NEXT = 0;
    END;

    PROCEDURE SetGlobals@1(NewProductVersion@1000 : Code[10];NewFormatVersion@1001 : Code[10];NewCompanyName@1002 : Text[30];NewCurrencyLCY@1003 : Code[10];NewCurrencyACY@1004 : Code[10];NewCurrencyPCY@1005 : Code[10];NewCheckSum@1006 : Decimal;NewStartingDate@1007 : Date;NewEndingDate@1008 : Date);
    BEGIN
      ProductVersion := NewProductVersion;
      FormatVersion := NewFormatVersion;
      SubCompanyName := NewCompanyName;
      CurrencyLCY := NewCurrencyLCY;
      CurrencyACY := NewCurrencyACY;
      CurrencyPCY := NewCurrencyPCY;
      StoredCheckSum := NewCheckSum;
      StartingDate := NewStartingDate;
      EndingDate := NewEndingDate;
    END;

    PROCEDURE InsertGLAccount@2(VAR NewGLAccount@1000 : Record 15);
    BEGIN
      SubsidGLAcc.INIT;
      SubsidGLAcc."No." := NewGLAccount."No.";
      SubsidGLAcc."Consol. Translation Method" := NewGLAccount."Consol. Translation Method";
      SubsidGLAcc."Consol. Debit Acc." := NewGLAccount."Consol. Debit Acc.";
      SubsidGLAcc."Consol. Credit Acc." := NewGLAccount."Consol. Credit Acc.";
      SubsidGLAcc.INSERT;
    END;

    PROCEDURE InsertGLEntry@3(VAR NewGLEntry@1000 : Record 17) : Integer;
    VAR
      NextEntryNo@1001 : Integer;
    BEGIN
      IF SubsidGLEntry.FIND('+') THEN
        NextEntryNo := SubsidGLEntry."Entry No." + 1
      ELSE
        NextEntryNo := 1;
      SubsidGLEntry.INIT;
      SubsidGLEntry."Entry No." := NextEntryNo;
      SubsidGLEntry."G/L Account No." := DELCHR(NewGLEntry."G/L Account No.", '=', ' ');
      SubsidGLEntry."Posting Date" := NewGLEntry."Posting Date";
      SubsidGLEntry."Debit Amount" := NewGLEntry."Debit Amount";
      SubsidGLEntry."Credit Amount" := NewGLEntry."Credit Amount";
      SubsidGLEntry."Add.-Currency Debit Amount" := NewGLEntry."Add.-Currency Debit Amount";
      SubsidGLEntry."Add.-Currency Credit Amount" := NewGLEntry."Add.-Currency Credit Amount";
      SubsidGLEntry.INSERT;
      EXIT(NextEntryNo);
    END;

    PROCEDURE InsertEntryDim@4(VAR NewEntryDim@1000 : Record 355);
    BEGIN
      SubsidLedgEntryDim.INIT;
      SubsidLedgEntryDim."Table ID" := DATABASE::"G/L Entry";
      SubsidLedgEntryDim."Entry No." := NewEntryDim."Entry No.";
      SubsidLedgEntryDim."Dimension Code" := NewEntryDim."Dimension Code";
      SubsidLedgEntryDim."Dimension Value Code" := NewEntryDim."Dimension Value Code";
      SubsidLedgEntryDim.INSERT;
    END;

    PROCEDURE InsertExchRate@5(VAR NewExchRate@1000 : Record 330);
    BEGIN
      SubsidExchRate.INIT;
      SubsidExchRate."Currency Code" := NewExchRate."Currency Code";
      SubsidExchRate."Starting Date" := NewExchRate."Starting Date";
      SubsidExchRate."Relational Currency Code" := NewExchRate."Relational Currency Code";
      SubsidExchRate."Exchange Rate Amount" := NewExchRate."Exchange Rate Amount";
      SubsidExchRate."Relational Exch. Rate Amount" := NewExchRate."Relational Exch. Rate Amount";
      SubsidExchRate.INSERT;
    END;

    PROCEDURE CalcCheckSum@6() CheckSum : Decimal;
    BEGIN
      CheckSum :=
        DateToDecimal(StartingDate) + DateToDecimal(EndingDate) +
        TextToDecimal(FormatVersion) + TextToDecimal(ProductVersion);
      SubsidGLAcc.RESET;
      IF SubsidGLAcc.FIND('-') THEN
        REPEAT
          CheckSum := CheckSum +
            TextToDecimal(SubsidGLAcc."No.") + TextToDecimal(SubsidGLAcc."Consol. Debit Acc.") +
            TextToDecimal(SubsidGLAcc."Consol. Credit Acc.");
        UNTIL SubsidGLAcc.NEXT = 0;
      SubsidGLEntry.RESET;
      IF SubsidGLEntry.FIND('-') THEN
        REPEAT
          CheckSum := CheckSum +
            SubsidGLEntry."Debit Amount" + SubsidGLEntry."Credit Amount" +
            SubsidGLEntry."Add.-Currency Debit Amount" + SubsidGLEntry."Add.-Currency Credit Amount" +
            DateToDecimal(SubsidGLEntry."Posting Date");
        UNTIL SubsidGLEntry.NEXT = 0;
    END;

    PROCEDURE ImportFromXML@7(FileName@1000 : Text[1024]);
    VAR
      Consolidation@1003 : XMLport 1;
      InputFile@1001 : File;
      InputStream@1002 : InStream;
    BEGIN
      InputFile.TEXTMODE(TRUE);
      InputFile.WRITEMODE(FALSE);
      InputFile.OPEN(FileName);

      InputFile.CREATEINSTREAM(InputStream);

      Consolidation.SETSOURCE(InputStream);
      Consolidation.IMPORT;
      InputFile.CLOSE;

      Consolidation.GetGLAccount(SubsidGLAcc);
      Consolidation.GetGLEntry(SubsidGLEntry);
      Consolidation.GetEntryDim(SubsidLedgEntryDim);
      Consolidation.GetExchRate(SubsidExchRate);
      Consolidation.GetGlobals(
        ProductVersion,FormatVersion,SubCompanyName,CurrencyLCY,CurrencyACY,CurrencyPCY,
        StoredCheckSum,StartingDate,EndingDate);

      SelectAllImportedDimensions;
    END;

    PROCEDURE ExportToXML@8(FileName@1000 : Text[250]);
    VAR
      Consolidation@1003 : XMLport 1;
      OutputFile@1001 : File;
      OutputStream@1002 : OutStream;
    BEGIN
      OutputFile.TEXTMODE(TRUE);
      OutputFile.WRITEMODE(TRUE);
      IF NOT ISSERVICETIER THEN OutputFile.QUERYREPLACE(TRUE);
      OutputFile.CREATE(FileName);

      OutputFile.CREATEOUTSTREAM(OutputStream);

      Consolidation.SetGlobals(SubCompanyName,CurrencyLCY,CurrencyACY,CurrencyPCY,StoredCheckSum,StartingDate,EndingDate);
      Consolidation.SetGLAccount(SubsidGLAcc);
      Consolidation.SetGLEntry(SubsidGLEntry);
      Consolidation.SetEntryDim(SubsidLedgEntryDim);
      Consolidation.SetExchRate(SubsidExchRate);

      Consolidation.SETDESTINATION(OutputStream);
      Consolidation.EXPORT;
      OutputFile.CLOSE;
    END;

    PROCEDURE GetGlobals@9(VAR ImpProductVersion@1008 : Code[10];VAR ImpFormatVersion@1007 : Code[10];VAR ImpCompanyName@1006 : Text[30];VAR ImpCurrencyLCY@1005 : Code[10];VAR ImpCurrencyACY@1004 : Code[10];VAR ImpCurrencyPCY@1003 : Code[10];VAR ImpCheckSum@1002 : Decimal;VAR ImpStartingDate@1001 : Date;VAR ImpEndingDate@1000 : Date);
    BEGIN
      ImpProductVersion := ProductVersion;
      ImpFormatVersion := FormatVersion;
      ImpCompanyName := SubCompanyName;
      ImpCurrencyLCY := CurrencyLCY;
      ImpCurrencyACY := CurrencyACY;
      ImpCurrencyPCY := CurrencyPCY;
      ImpCheckSum := StoredCheckSum;
      ImpStartingDate := StartingDate;
      ImpEndingDate := EndingDate;
    END;

    PROCEDURE SetTestMode@27(NewTestMode@1000 : Boolean);
    BEGIN
      TestMode := NewTestMode;
      CurErrorIdx := 0;
    END;

    PROCEDURE GetAccumulatedErrors@28(VAR NumErrors@1000 : Integer;VAR Errors@1001 : ARRAY [100] OF Text[250]);
    VAR
      Idx@1002 : Integer;
    BEGIN
      NumErrors := 0;
      CLEAR(Errors);
      FOR Idx := 1 TO CurErrorIdx DO BEGIN
        NumErrors := NumErrors + 1;
        Errors[NumErrors] := ErrorText[Idx];
        IF (Idx = ARRAYLEN(Errors)) AND (CurErrorIdx > Idx) THEN BEGIN
          COPYARRAY(ErrorText,ErrorText,ARRAYLEN(Errors) + 1);
          CurErrorIdx := CurErrorIdx - ARRAYLEN(Errors);
          EXIT;
        END;
      END;
      CurErrorIdx := 0;
      CLEAR(ErrorText);
    END;

    PROCEDURE SelectAllImportedDimensions@41();
    BEGIN
      // assume all dimensions that were imported were also selected.
      TempSelectedDim.RESET;
      TempSelectedDim.DELETEALL;
      IF SubsidLedgEntryDim.FIND('-') THEN
        REPEAT
          TempSelectedDim.INIT;
          TempSelectedDim."User ID" := '';
          TempSelectedDim."Object Type" := 0;
          TempSelectedDim."Object ID" := 0;
          TempSelectedDim."Dimension Code" := SubsidLedgEntryDim."Dimension Code";
          IF TempSelectedDim.INSERT THEN ;
        UNTIL SubsidLedgEntryDim.NEXT = 0;
      SkipAllDimensions := NOT TempSelectedDim.FIND('-');
    END;

    LOCAL PROCEDURE ReadGLSetup@10();
    VAR
      GLSetup@1000 : Record 98;
      SourceCodeSetup@1001 : Record 242;
    BEGIN
      GLSetup.GET;
      GlobalDim1Code := GLSetup."Global Dimension 1 Code";
      GlobalDim2Code := GLSetup."Global Dimension 2 Code";
      SourceCodeSetup.GET;
      ConsolidSourceCode := SourceCodeSetup.Consolidation;
    END;

    LOCAL PROCEDURE ClearInternals@23();
    BEGIN
      NextLineNo := 0;
      AnalysisViewEntriesDeleted := FALSE;
      TempGenJnlLine.RESET;
      TempGenJnlLine.DELETEALL;
      TempJnlLineDim.RESET;
      TempJnlLineDim.DELETEALL;
      TempDimBufOut.RESET;
      TempDimBufOut.DELETEALL;
      TempDimBufIn.RESET;
      TempDimBufIn.DELETEALL;
      CLEAR(RoundingResiduals);
      CLEAR(ExchRateAdjAmounts);
      CLEAR(CompExchRateAdjAmts);
      CLEAR(EqExchRateAdjAmts);
      CLEAR(MinorExchRateAdjAmts);
    END;

    LOCAL PROCEDURE UpdatePhase@16(PhaseText@1000 : Text[50]);
    BEGIN
      Window.UPDATE(2,PhaseText);
      Window.UPDATE(3,'');
    END;

    LOCAL PROCEDURE ClearPreviousConsolidation@14();
    VAR
      TempGLAccount@1000 : TEMPORARY Record 15;
      AnalysisView@1001 : Record 363;
      TempAnalysisView@1004 : TEMPORARY Record 363;
      AnalysisViewEntry@1002 : Record 365;
      AnalysisViewFound@1003 : Boolean;
    BEGIN
      ClearAmountArray;
      WITH ConsolidGLEntry DO BEGIN
        IF NOT
          SETCURRENTKEY("G/L Account No.","Business Unit Code","Global Dimension 1 Code","Global Dimension 2 Code","Posting Date")
        THEN
          SETCURRENTKEY("G/L Account No.","Business Unit Code","Posting Date");
        SETRANGE("Business Unit Code",BusUnit.Code);
        SETRANGE("Posting Date",StartingDate,EndingDate);
        IF FIND('-') THEN
          REPEAT
            UpdateAmountArray("Posting Date",Amount);
            Description := '';
            Amount := 0;
            "Debit Amount" := 0;
            "Credit Amount" := 0;
            "Additional-Currency Amount" := 0;
            "Add.-Currency Debit Amount" := 0;
            "Add.-Currency Credit Amount" := 0;
            MODIFY;
            IF "G/L Account No." <> TempGLAccount."No." THEN BEGIN
              Window.UPDATE(3,"G/L Account No.");
              TempGLAccount."No." := "G/L Account No.";
              TempGLAccount.INSERT;
            END;
          UNTIL NEXT = 0;
      END;
      CheckAmountArray;

      IF AnalysisView.FIND('-') THEN
        REPEAT
          AnalysisViewFound := FALSE;
          IF TempGLAccount.FIND('-') THEN
            REPEAT
              AnalysisViewEntry.SETRANGE("Analysis View Code",AnalysisView.Code);
              AnalysisViewEntry.SETRANGE("G/L Account No.",TempGLAccount."No.");
              IF AnalysisViewEntry.FIND('-') THEN BEGIN
                TempAnalysisView.Code := AnalysisViewEntry."Analysis View Code";
                TempAnalysisView.INSERT;
                AnalysisViewFound := TRUE;
              END;
            UNTIL (TempGLAccount.NEXT = 0) OR AnalysisViewFound;
        UNTIL AnalysisView.NEXT = 0;

      AnalysisViewEntry.RESET;
      IF TempAnalysisView.FIND('-') THEN
        REPEAT
          AnalysisView.GET(TempAnalysisView.Code);
          IF AnalysisView.Blocked THEN BEGIN
            AnalysisView."Refresh When Unblocked" := TRUE;
            AnalysisView.MODIFY;
          END ELSE BEGIN
            AnalysisViewEntry.SETRANGE("Analysis View Code",TempAnalysisView.Code);
            AnalysisViewEntry.DELETEALL;
            AnalysisView."Last Entry No." := 0;
            AnalysisView."Last Date Updated" := 0D;
            AnalysisView.MODIFY;
            AnalysisViewEntriesDeleted := TRUE;
          END;
        UNTIL TempAnalysisView.NEXT = 0;
    END;

    LOCAL PROCEDURE ClearAmountArray@11();
    BEGIN
      CLEAR(DeletedAmounts);
      CLEAR(DeletedDates);
      DeletedIndex := 0;
      MaxDeletedIndex := 0;
    END;

    LOCAL PROCEDURE UpdateAmountArray@12(PostingDate@1000 : Date;Amount@1001 : Decimal);
    VAR
      Top@1002 : Integer;
      Bottom@1003 : Integer;
      Middle@1004 : Integer;
      Found@1005 : Boolean;
      NotFound@1006 : Boolean;
      idx@1007 : Integer;
    BEGIN
      IF DeletedIndex = 0 THEN BEGIN
        DeletedIndex := 1;
        MaxDeletedIndex := 1;
        DeletedDates[DeletedIndex] := PostingDate;
        DeletedAmounts[DeletedIndex] := Amount;
      END ELSE IF PostingDate = DeletedDates[DeletedIndex] THEN
        DeletedAmounts[DeletedIndex] := DeletedAmounts[DeletedIndex] + Amount
      ELSE BEGIN
        Top := 0;
        Bottom := MaxDeletedIndex + 1;
        Found := FALSE;
        NotFound := FALSE;
        REPEAT
          Middle := (Top + Bottom) DIV 2;
          IF Bottom - Top <= 1 THEN
            NotFound := TRUE
          ELSE IF DeletedDates[Middle] > PostingDate THEN
            Bottom := Middle
          ELSE IF DeletedDates[Middle] < PostingDate THEN
            Top := Middle
          ELSE
            Found := TRUE;
        UNTIL Found OR NotFound;
        IF Found THEN BEGIN
          DeletedIndex := Middle;
          DeletedAmounts[DeletedIndex] := DeletedAmounts[DeletedIndex] + Amount;
        END ELSE BEGIN
          IF MaxDeletedIndex >= ARRAYLEN(DeletedDates) THEN
            ReportError(STRSUBSTNO(Text008,ARRAYLEN(DeletedDates)))
          ELSE
            MaxDeletedIndex := MaxDeletedIndex + 1;
          FOR idx := MaxDeletedIndex DOWNTO Bottom + 1 DO BEGIN
            DeletedAmounts[idx] := DeletedAmounts[idx - 1];
            DeletedDates[idx] := DeletedDates[idx - 1];
          END;
          DeletedIndex := Bottom;
          DeletedDates[DeletedIndex] := PostingDate;
          DeletedAmounts[DeletedIndex] := Amount;
        END;
      END;
    END;

    LOCAL PROCEDURE CheckAmountArray@13();
    VAR
      idx@1000 : Integer;
    BEGIN
      FOR idx := 1 TO MaxDeletedIndex DO
        IF DeletedAmounts[idx] <> 0 THEN
          ReportError(STRSUBSTNO(Text010 + Text011,DeletedAmounts[idx],DeletedDates[idx]));
    END;

    LOCAL PROCEDURE TestGLAccounts@18();
    VAR
      AccountToTest@1001 : Record 15;
    BEGIN
      // First test within the Subsidiary Chart of Accounts
      AccountToTest := SubsidGLAcc;
      IF AccountToTest.TranslationMethodConflict(SubsidGLAcc) THEN BEGIN
        IF SubsidGLAcc.GETFILTER("Consol. Debit Acc.") <> '' THEN
          ReportError(STRSUBSTNO(
            Text021,
            SubsidGLAcc."No.",
            SubsidGLAcc.FIELDCAPTION("Consol. Debit Acc."),
            SubsidGLAcc.FIELDCAPTION("Consol. Translation Method"),
            AccountToTest."No.",BusUnit.TABLECAPTION))
        ELSE
          ReportError(STRSUBSTNO(
            Text021,
            SubsidGLAcc."No.",
            SubsidGLAcc.FIELDCAPTION("Consol. Credit Acc."),
            SubsidGLAcc.FIELDCAPTION("Consol. Translation Method"),
            AccountToTest."No.",BusUnit.TABLECAPTION));
      END ELSE BEGIN
        SubsidGLAcc.RESET;
        SubsidGLAcc := AccountToTest;
        SubsidGLAcc.FIND('=');
      END;
      // Then, test for conflicts between subsidiary and parent (consolidated)
      IF SubsidGLAcc."Consol. Debit Acc." <> '' THEN BEGIN
        IF NOT ConsolidGLAcc.GET(SubsidGLAcc."Consol. Debit Acc.") THEN
          ReportError(STRSUBSTNO(
            Text022,
            SubsidGLAcc.FIELDCAPTION("Consol. Debit Acc."),SubsidGLAcc."Consol. Debit Acc.",
            SubsidGLAcc.TABLECAPTION,SubsidGLAcc."No.",BusUnit.TABLECAPTION));
        IF (SubsidGLAcc."Consol. Translation Method" <> ConsolidGLAcc."Consol. Translation Method") AND
           (BusUnit."File Format" <> BusUnit."File Format"::"Version 3.70 or Earlier (.txt)")
        THEN
          ReportError(STRSUBSTNO(
            Text023,
            SubsidGLAcc.TABLECAPTION,SubsidGLAcc."No.",
            SubsidGLAcc.FIELDCAPTION("Consol. Translation Method"),ConsolidGLAcc."No.",
            SubsidGLAcc."Consol. Translation Method",ConsolidGLAcc."Consol. Translation Method",
            BusUnit.TABLECAPTION));
      END;
      IF SubsidGLAcc."Consol. Debit Acc." = SubsidGLAcc."Consol. Credit Acc." THEN
        EXIT;
      IF SubsidGLAcc."Consol. Credit Acc." <> '' THEN BEGIN
        IF NOT ConsolidGLAcc.GET(SubsidGLAcc."Consol. Credit Acc.") THEN
          ReportError(STRSUBSTNO(
            Text022,
            SubsidGLAcc.FIELDCAPTION("Consol. Credit Acc."),SubsidGLAcc."Consol. Credit Acc.",
            SubsidGLAcc.TABLECAPTION,SubsidGLAcc."No.",BusUnit.TABLECAPTION));
        IF (SubsidGLAcc."Consol. Translation Method" <> ConsolidGLAcc."Consol. Translation Method") AND
           (BusUnit."File Format" <> BusUnit."File Format"::"Version 3.70 or Earlier (.txt)")
        THEN
          ReportError(STRSUBSTNO(
            Text023,
            SubsidGLAcc.TABLECAPTION,SubsidGLAcc."No.",
            SubsidGLAcc.FIELDCAPTION("Consol. Translation Method"),ConsolidGLAcc."No.",
            SubsidGLAcc."Consol. Translation Method",ConsolidGLAcc."Consol. Translation Method",
            BusUnit.TABLECAPTION));
      END;
    END;

    LOCAL PROCEDURE UpdatePriorPeriodBalances@21();
    VAR
      TempJnlDimLine2@1001 : TEMPORARY Record 356;
      idx@1002 : Integer;
      AdjustmentAmount@1000 : Decimal;
    BEGIN
      CLEAR(GenJnlLine);
      GenJnlLine."Business Unit Code" := BusUnit.Code;
      GenJnlLine."Document No." := GLDocNo;
      GenJnlLine."Source Code" := ConsolidSourceCode;

      BusUnit.TESTFIELD("Balance Currency Factor");
      BusUnit.TESTFIELD("Last Balance Currency Factor");
      ExchRateAdjAmount := 0;
      idx := NORMALDATE(EndingDate) - NORMALDATE(StartingDate) + 1;

      WITH ConsolidGLAcc DO BEGIN
        RESET;
        SETRANGE("Account Type","Account Type"::Posting);
        SETRANGE("Business Unit Filter",BusUnit.Code);
        SETRANGE("Date Filter",0D,EndingDate);
        SETRANGE("Income/Balance","Income/Balance"::"Balance Sheet");
        SETFILTER(
          "No.",'<>%1&<>%2&<>%3&<>%4&<>%5&<>%6&<>%7&<>%8&<>%9',
          BusUnit."Exch. Rate Losses Acc.",BusUnit."Exch. Rate Gains Acc.",
          BusUnit."Comp. Exch. Rate Gains Acc.",BusUnit."Comp. Exch. Rate Losses Acc.",
          BusUnit."Equity Exch. Rate Gains Acc.",BusUnit."Equity Exch. Rate Losses Acc.",
          BusUnit."Minority Exch. Rate Gains Acc.",BusUnit."Minority Exch. Rate Losses Acc",
          BusUnit."Residual Account");
        IF FIND('-') THEN
          REPEAT
            Window.UPDATE(3,"No.");
            CASE "Consol. Translation Method" OF
              "Consol. Translation Method"::"Average Rate (Manual)",
              "Consol. Translation Method"::"Closing Rate":
                // Post adjustment to existing balance to convert that balance to new Closing Rate
                IF SkipAllDimensions THEN BEGIN
                  CALCFIELDS("Debit Amount","Credit Amount");
                  IF "Debit Amount" <> 0 THEN
                    PostBalanceAdjustment("No.","Debit Amount");
                  IF "Credit Amount" <> 0 THEN
                    PostBalanceAdjustment("No.",-"Credit Amount");
                END ELSE BEGIN
                  TempGLEntry.RESET;
                  TempGLEntry.DELETEALL;
                  DimBufMgt.DeleteAllDimensions;
                  ConsolidGLEntry.RESET;
                  ConsolidGLEntry.SETCURRENTKEY("G/L Account No.","Posting Date");
                  ConsolidGLEntry.SETRANGE("G/L Account No.","No.");
                  ConsolidGLEntry.SETRANGE("Posting Date",0D,EndingDate);
                  ConsolidGLEntry.SETRANGE("Business Unit Code",BusUnit.Code);
                  ConsolidLedgEntryDim.SETRANGE("Table ID",DATABASE::"G/L Entry");
                  IF ConsolidGLEntry.FIND('-') THEN
                    REPEAT
                      TempDimBufIn.RESET;
                      TempDimBufIn.DELETEALL;
                      ConsolidLedgEntryDim.SETRANGE("Entry No.",ConsolidGLEntry."Entry No.");
                      IF ConsolidLedgEntryDim.FIND('-') THEN
                        REPEAT
                          IF TempSelectedDim.GET('',0,0,'',ConsolidLedgEntryDim."Dimension Code") THEN BEGIN
                            TempDimBufIn.INIT;
                            TempDimBufIn."Table ID" := DATABASE::"G/L Entry";
                            TempDimBufIn."Entry No." := ConsolidGLEntry."Entry No.";
                            TempDimBufIn."Dimension Code" := ConsolidLedgEntryDim."Dimension Code";
                            TempDimBufIn."Dimension Value Code" := ConsolidLedgEntryDim."Dimension Value Code";
                            TempDimBufIn.INSERT;
                          END;
                        UNTIL ConsolidLedgEntryDim.NEXT = 0;
                      UpdateTempGLEntry(ConsolidGLEntry);
                    UNTIL ConsolidGLEntry.NEXT = 0;
                  TempDimBufOut.RESET;
                  TempDimBufOut.DELETEALL;
                  IF TempGLEntry.FIND('-') THEN
                    REPEAT
                      DimBufMgt.GetDimensions(TempGLEntry."Entry No.",TempDimBufOut);
                      TempDimBufOut.SETRANGE("Entry No.",TempGLEntry."Entry No.");
                      IF TempGLEntry."Debit Amount" <> 0 THEN
                        PostBalanceAdjustment("No.",TempGLEntry."Debit Amount");
                      IF TempGLEntry."Credit Amount" <> 0 THEN
                        PostBalanceAdjustment("No.",-TempGLEntry."Credit Amount");
                    UNTIL TempGLEntry.NEXT = 0;
                END;
              "Consol. Translation Method"::"Historical Rate":
                // accumulate adjustment for historical accounts
                BEGIN
                  CALCFIELDS("Balance at Date");
                  AdjustmentAmount :=
                    ROUND(
                      ("Balance at Date" * BusUnit."Last Balance Currency Factor" / BusUnit."Balance Currency Factor") -
                      "Balance at Date");
                  ExchRateAdjAmounts[idx] := ExchRateAdjAmounts[idx] + AdjustmentAmount;
                END;
              "Consol. Translation Method"::"Composite Rate":
                // accumulate adjustment for composite accounts
                BEGIN
                  CALCFIELDS("Balance at Date");
                  AdjustmentAmount := 0;
                  CompExchRateAdjAmts[idx] := CompExchRateAdjAmts[idx] + AdjustmentAmount;
                END;
              "Consol. Translation Method"::"Equity Rate":
                // accumulate adjustment for equity accounts
                BEGIN
                  CALCFIELDS("Balance at Date");
                  AdjustmentAmount := 0;
                  EqExchRateAdjAmts[idx] := EqExchRateAdjAmts[idx] + AdjustmentAmount;
                END;
            END;
          UNTIL NEXT = 0;
      END;

      TempDimBufOut.RESET;
      TempDimBufOut.DELETEALL;
      TempJnlDimLine2.RESET;
      TempJnlDimLine2.DELETEALL;

      IF ExchRateAdjAmount <> 0 THEN BEGIN
        CLEAR(GenJnlLine);
        GenJnlLine."Business Unit Code" := BusUnit.Code;
        GenJnlLine."Document No." := GLDocNo;
        GenJnlLine."Source Code" := ConsolidSourceCode;
        GenJnlLine.Amount := -ExchRateAdjAmount;
        IF GenJnlLine.Amount < 0 THEN BEGIN
          BusUnit.TESTFIELD("Exch. Rate Gains Acc.");
          GenJnlLine."Account No." := BusUnit."Exch. Rate Gains Acc.";
        END ELSE BEGIN
          BusUnit.TESTFIELD("Exch. Rate Losses Acc.");
          GenJnlLine."Account No." := BusUnit."Exch. Rate Losses Acc.";
        END;
        Window.UPDATE(3,GenJnlLine."Account No.");
        GenJnlLine."Posting Date" := EndingDate;
        GenJnlLine.Description := STRSUBSTNO(Text014,WORKDATE);
        GenJnlPostLineTmp(GenJnlLine,TempJnlDimLine2);
      END;
    END;

    LOCAL PROCEDURE PostBalanceAdjustment@17(GLAccNo@1000 : Code[20];AmountToPost@1001 : Decimal);
    VAR
      TempJnlDimLine2@1002 : TEMPORARY Record 356;
    BEGIN
      GenJnlLine.Amount :=
        ROUND(
          (AmountToPost * BusUnit."Last Balance Currency Factor" / BusUnit."Balance Currency Factor") - AmountToPost);
      IF GenJnlLine.Amount <> 0 THEN BEGIN
        GenJnlLine."Account No." := GLAccNo;
        GenJnlLine."Posting Date" := EndingDate;
        GenJnlLine.Description :=
          COPYSTR(
            STRSUBSTNO(
              Text013,
              AmountToPost,
              ROUND(BusUnit."Last Balance Currency Factor",0.00001),
              ROUND(BusUnit."Balance Currency Factor",0.00001),
              WORKDATE),
            1,MAXSTRLEN(GenJnlLine.Description));
        IF TempDimBufOut.FIND('-') THEN
          REPEAT
            TempJnlDimLine2."Table ID" := DATABASE::"Gen. Journal Line";
            TempJnlDimLine2."Journal Template Name" := GenJnlLine."Journal Template Name";
            TempJnlDimLine2."Journal Batch Name" := GenJnlLine."Journal Batch Name";
            TempJnlDimLine2."Journal Line No." := GenJnlLine."Line No.";
            TempJnlDimLine2."Allocation Line No." := 0;
            TempJnlDimLine2."Dimension Code" := TempDimBufOut."Dimension Code";
            TempJnlDimLine2."Dimension Value Code" := TempDimBufOut."Dimension Value Code";
            TempJnlDimLine2.INSERT;
            IF TempDimBufOut."Dimension Code" = GlobalDim1Code THEN
              GenJnlLine."Shortcut Dimension 1 Code" := TempDimBufOut."Dimension Value Code";
            IF TempDimBufOut."Dimension Code" = GlobalDim2Code THEN
              GenJnlLine."Shortcut Dimension 2 Code" := TempDimBufOut."Dimension Value Code";
          UNTIL TempDimBufOut.NEXT = 0;
        GenJnlPostLineTmp(GenJnlLine,TempJnlDimLine2);
        ExchRateAdjAmount := ExchRateAdjAmount + GenJnlLine.Amount;
      END;
    END;

    LOCAL PROCEDURE UpdateTempGLEntry@30(VAR GLEntry@1000 : Record 17);
    VAR
      DimEntryNo@1001 : Integer;
      Found@1002 : Boolean;
    BEGIN
      DimEntryNo := DimBufMgt.FindDimensions(TempDimBufIn);
      Found := TempDimBufIn.FIND('-');
      IF Found AND (DimEntryNo = 0) THEN BEGIN
        TempGLEntry := GLEntry;
        TempGLEntry."Entry No." := DimBufMgt.InsertDimensions(TempDimBufIn);
        TempGLEntry.INSERT;
      END ELSE BEGIN
        IF TempGLEntry.GET(DimEntryNo) THEN BEGIN
          TempGLEntry.Amount := TempGLEntry.Amount + GLEntry.Amount;
          TempGLEntry."Debit Amount" := TempGLEntry."Debit Amount" + GLEntry."Debit Amount";
          TempGLEntry."Credit Amount" := TempGLEntry."Credit Amount" + GLEntry."Credit Amount";
          TempGLEntry."Additional-Currency Amount" := TempGLEntry."Additional-Currency Amount" + GLEntry."Additional-Currency Amount";
          TempGLEntry."Add.-Currency Debit Amount" := TempGLEntry."Add.-Currency Debit Amount" + GLEntry."Add.-Currency Debit Amount";
          TempGLEntry."Add.-Currency Credit Amount" :=
            TempGLEntry."Add.-Currency Credit Amount" + GLEntry."Add.-Currency Credit Amount";
          TempGLEntry.MODIFY;
        END ELSE BEGIN
          TempGLEntry := GLEntry;
          TempGLEntry."Entry No." := DimEntryNo;
          TempGLEntry.INSERT;
        END;
      END;
    END;

    LOCAL PROCEDURE CreateAndPostGenJnlLine@24(GenJnlLine@1000 : Record 81;VAR GLEntry@1001 : Record 17;VAR DimBuf@1002 : Record 360;PostDebit@1004 : Boolean);
    VAR
      TempJnlDimLine2@1007 : TEMPORARY Record 356;
      ConsolidAmount@1003 : Decimal;
      AmountToPost@1005 : Decimal;
      AdjustAmount@1009 : Decimal;
      ClosingAmount@1010 : Decimal;
      TranslationNeeded@1006 : Boolean;
      idx@1008 : Integer;
      OriginalTranslationMethod@1011 : Integer;
    BEGIN
      WITH GenJnlLine DO BEGIN
        IF PostDebit THEN BEGIN
          IF BusUnit."Data Source" = BusUnit."Data Source"::"Local Curr. (LCY)" THEN
            AmountToPost := GLEntry."Debit Amount"
          ELSE
            AmountToPost := GLEntry."Add.-Currency Debit Amount";
          "Account No." := SubsidGLAcc."Consol. Debit Acc.";
        END ELSE BEGIN
          IF BusUnit."Data Source" = BusUnit."Data Source"::"Local Curr. (LCY)" THEN
            AmountToPost := -GLEntry."Credit Amount"
          ELSE
            AmountToPost := -GLEntry."Add.-Currency Credit Amount";
          "Account No." := SubsidGLAcc."Consol. Credit Acc.";
        END;
        IF AmountToPost = 0 THEN
          EXIT;
        ConsolidGLAcc.GET("Account No.");

        OriginalTranslationMethod := SubsidGLAcc."Consol. Translation Method";
        IF SubsidGLAcc."Consol. Translation Method" = SubsidGLAcc."Consol. Translation Method"::"Average Rate (Manual)" THEN BEGIN
          IF ConsolidGLAcc."Income/Balance" = ConsolidGLAcc."Income/Balance"::"Balance Sheet" THEN
            SubsidGLAcc."Consol. Translation Method" := SubsidGLAcc."Consol. Translation Method"::"Closing Rate";
        END;

        ConsolidAmount := AmountToPost * BusUnit."Consolidation %" / 100;

        TranslationNeeded := (BusUnit."Currency Code" <> '');
        IF TranslationNeeded THEN
          IF BusUnit."Data Source" = BusUnit."Data Source"::"Add. Rep. Curr. (ACY)" THEN
            TranslationNeeded := (BusUnit."Currency Code" <> CurrencyACY);

        IF TranslationNeeded THEN BEGIN
          ClosingAmount :=
            ROUND(
              ConsolidExchRate.ExchangeAmtFCYToLCY(
                EndingDate,BusUnit."Currency Code",
                ConsolidAmount,BusUnit."Balance Currency Factor"));
          CASE SubsidGLAcc."Consol. Translation Method" OF
            SubsidGLAcc."Consol. Translation Method"::"Closing Rate":
              BEGIN
                Amount := ClosingAmount;
                Description :=
                  COPYSTR(
                    STRSUBSTNO(
                      Text017,
                      ConsolidAmount,ROUND(BusUnit."Balance Currency Factor",0.00001),EndingDate),
                    1,MAXSTRLEN(Description));
              END;
            SubsidGLAcc."Consol. Translation Method"::"Composite Rate",
            SubsidGLAcc."Consol. Translation Method"::"Equity Rate",
            SubsidGLAcc."Consol. Translation Method"::"Average Rate (Manual)":
              BEGIN
                Amount :=
                  ROUND(
                    ConsolidExchRate.ExchangeAmtFCYToLCY(
                      EndingDate,BusUnit."Currency Code",
                      ConsolidAmount,BusUnit."Income Currency Factor"));
                Description :=
                  COPYSTR(
                    STRSUBSTNO(
                      Text017,
                      ConsolidAmount,ROUND(BusUnit."Income Currency Factor",0.00001),EndingDate),
                    1,MAXSTRLEN(Description));
              END;
            SubsidGLAcc."Consol. Translation Method"::"Historical Rate":
              BEGIN
                Amount := TranslateUsingHistoricalRate(ConsolidAmount,GLEntry."Posting Date");
                Description :=
                  COPYSTR(
                    STRSUBSTNO(
                      Text017,
                      ConsolidAmount,ROUND(HistoricalCurrencyFactor,0.00001),GLEntry."Posting Date"),
                    1,MAXSTRLEN(Description));
              END;
          END;
        END ELSE BEGIN
          Amount := ROUND(ConsolidAmount);
          ClosingAmount := Amount;
          Description :=
            STRSUBSTNO(Text024,AmountToPost,BusUnit."Consolidation %",BusUnit.FIELDCAPTION("Consolidation %"));
        END;

        IF SubsidGLAcc."Consol. Translation Method" = SubsidGLAcc."Consol. Translation Method"::"Historical Rate" THEN
          "Posting Date" := GLEntry."Posting Date"
        ELSE
          "Posting Date" := EndingDate;
        idx := NORMALDATE("Posting Date") - NORMALDATE(StartingDate) + 1;

        IF DimBuf.FIND('-') THEN
          REPEAT
            TempJnlDimLine2."Table ID" := DATABASE::"Gen. Journal Line";
            TempJnlDimLine2."Journal Template Name" := "Journal Template Name";
            TempJnlDimLine2."Journal Batch Name" := "Journal Batch Name";
            TempJnlDimLine2."Journal Line No." := "Line No.";
            TempJnlDimLine2."Allocation Line No." := 0;
            TempJnlDimLine2."Dimension Code" := DimBuf."Dimension Code";
            TempJnlDimLine2."Dimension Value Code" := DimBuf."Dimension Value Code";
            TempJnlDimLine2.INSERT;
            IF DimBuf."Dimension Code" = GlobalDim1Code THEN
              "Shortcut Dimension 1 Code" := DimBuf."Dimension Value Code";
            IF DimBuf."Dimension Code" = GlobalDim2Code THEN
              "Shortcut Dimension 2 Code" := DimBuf."Dimension Value Code";
          UNTIL DimBuf.NEXT = 0;

        IF Amount <> 0 THEN
          GenJnlPostLineTmp(GenJnlLine,TempJnlDimLine2);
        TempJnlDimLine2.RESET;
        TempJnlDimLine2.DELETEALL;

        RoundingResiduals[idx] := RoundingResiduals[idx] + Amount;
        AdjustAmount := ClosingAmount - Amount;
        CASE SubsidGLAcc."Consol. Translation Method" OF
          SubsidGLAcc."Consol. Translation Method"::"Composite Rate":
            CompExchRateAdjAmts[idx] := CompExchRateAdjAmts[idx] + AdjustAmount;
          SubsidGLAcc."Consol. Translation Method"::"Equity Rate":
            EqExchRateAdjAmts[idx] := EqExchRateAdjAmts[idx] + AdjustAmount;
          ELSE
            ExchRateAdjAmounts[idx] := ExchRateAdjAmounts[idx] + AdjustAmount;
        END;
        SubsidGLAcc."Consol. Translation Method" := OriginalTranslationMethod;
      END;
    END;

    LOCAL PROCEDURE TranslateUsingHistoricalRate@26(AmountToTranslate@1000 : Decimal;DateToTranslate@1001 : Date) TranslatedAmount : Decimal;
    BEGIN
      IF BusUnit."Currency Exchange Rate Table" = BusUnit."Currency Exchange Rate Table"::"Local"
      THEN BEGIN
        ConsolidExchRate.RESET;
        ConsolidExchRate.SETRANGE("Currency Code",BusUnit."Currency Code");
        ConsolidExchRate.SETRANGE("Starting Date",0D,DateToTranslate);
        ConsolidExchRate.FIND('+');
        ConsolidExchRate.TESTFIELD("Exchange Rate Amount");
        ConsolidExchRate.TESTFIELD("Relational Exch. Rate Amount");
        ConsolidExchRate.TESTFIELD("Relational Currency Code",'');
        HistoricalCurrencyFactor :=
          ConsolidExchRate."Exchange Rate Amount" / ConsolidExchRate."Relational Exch. Rate Amount";
      END ELSE BEGIN
        SubsidExchRate.RESET;
        SubsidExchRate.SETRANGE("Starting Date",0D,DateToTranslate);
        SubsidExchRate.SETRANGE("Currency Code",CurrencyPCY);
        SubsidExchRate.FIND('+');
        SubsidExchRate.TESTFIELD("Exchange Rate Amount");
        SubsidExchRate.TESTFIELD("Relational Exch. Rate Amount");
        SubsidExchRate.TESTFIELD("Relational Currency Code",'');
        HistoricalCurrencyFactor :=
          SubsidExchRate."Relational Exch. Rate Amount" / SubsidExchRate."Exchange Rate Amount";
        IF BusUnit."Data Source" = BusUnit."Data Source"::"Add. Rep. Curr. (ACY)" THEN BEGIN
          SubsidExchRate.SETRANGE("Currency Code",CurrencyACY);
          SubsidExchRate.FIND('+');
          SubsidExchRate.TESTFIELD("Exchange Rate Amount");
          SubsidExchRate.TESTFIELD("Relational Exch. Rate Amount");
          SubsidExchRate.TESTFIELD("Relational Currency Code",'');
          HistoricalCurrencyFactor :=
            HistoricalCurrencyFactor * SubsidExchRate."Exchange Rate Amount" / SubsidExchRate."Relational Exch. Rate Amount";
        END;
      END;
      TranslatedAmount := ROUND(AmountToTranslate / HistoricalCurrencyFactor);
    END;

    LOCAL PROCEDURE GenJnlPostLineTmp@22(VAR GenJnlLine@1000 : Record 81;VAR JnlLineDim@1001 : Record 356);
    BEGIN
      NextLineNo := NextLineNo + 1;
      TempGenJnlLine := GenJnlLine;
      TempGenJnlLine."Line No." := NextLineNo;
      TempGenJnlLine."System-Created Entry" := TRUE;
      TempGenJnlLine.INSERT;
      IF JnlLineDim.FIND('-') THEN
        REPEAT
          TempJnlLineDim := JnlLineDim;
          TempJnlLineDim."Journal Line No." := NextLineNo;
          TempJnlLineDim.INSERT;
        UNTIL JnlLineDim.NEXT = 0;
    END;

    LOCAL PROCEDURE GenJnlPostLineFinally@29();
    VAR
      GenJnlPostLine@1000 : Codeunit 12;
    BEGIN
      TempGenJnlLine.SETCURRENTKEY("Journal Template Name","Journal Batch Name","Posting Date");
      TempJnlLineDim.RESET;
      TempJnlLineDim.SETRANGE("Table ID",DATABASE::"Gen. Journal Line");
      IF TempGenJnlLine.FIND('-') THEN
        REPEAT
          Window.UPDATE(3,TempGenJnlLine."Account No.");
          TempJnlLineDim.SETRANGE("Journal Template Name",TempGenJnlLine."Journal Template Name");
          TempJnlLineDim.SETRANGE("Journal Batch Name",TempGenJnlLine."Journal Batch Name");
          TempJnlLineDim.SETRANGE("Journal Line No.",TempGenJnlLine."Line No.");
          TempJnlLineDim.SETRANGE("Allocation Line No.",0);
          GenJnlPostLine.RunWithCheck(TempGenJnlLine,TempJnlLineDim);
        UNTIL TempGenJnlLine.NEXT = 0;
    END;

    LOCAL PROCEDURE TextToDecimal@19(Txt@1000 : Text[50]) Result : Decimal;
    VAR
      DecOnlyTxt@1001 : Text[50];
      Idx@1003 : Integer;
    BEGIN
      FOR Idx := 1 TO STRLEN(Txt) DO
        IF Txt[Idx] IN ['0','1','2','3','4','5','6','7','8','9'] THEN
          DecOnlyTxt := DecOnlyTxt + COPYSTR(Txt,Idx,1);
      IF DecOnlyTxt = '' THEN
        Result := 0
      ELSE
        EVALUATE(Result,DecOnlyTxt);
    END;

    LOCAL PROCEDURE DateToDecimal@25(Dt@1000 : Date) Result : Decimal;
    VAR
      Mon@1001 : Decimal;
      Day@1002 : Decimal;
      Yr@1003 : Decimal;
    BEGIN
      Day := DATE2DMY(Dt,1);
      Mon := DATE2DMY(Dt,2);
      Yr := DATE2DMY(Dt,3);
      Result := Yr * 100 + Mon + Day / 100;
    END;

    LOCAL PROCEDURE ReportError@31(ErrorMsg@1000 : Text[250]);
    BEGIN
      IF TestMode THEN BEGIN
        IF CurErrorIdx = ARRAYLEN(ErrorText) THEN
          ErrorText[CurErrorIdx] := STRSUBSTNO(Text006,ARRAYLEN(ErrorText))
        ELSE BEGIN
          CurErrorIdx := CurErrorIdx + 1;
          ErrorText[CurErrorIdx] := ErrorMsg;
        END;
      END ELSE
        ERROR(ErrorMsg);
    END;

    PROCEDURE GetNumSubsidGLAcc@32() : Integer;
    BEGIN
      SubsidGLAcc.RESET;
      EXIT(SubsidGLAcc.COUNT);
    END;

    PROCEDURE Get1stSubsidGLAcc@33(VAR GlAccount@1000 : Record 15) : Boolean;
    BEGIN
      SubsidGLAcc.RESET;
      IF SubsidGLAcc.FIND('-') THEN BEGIN
        GlAccount := SubsidGLAcc;
        IF TestMode THEN
          TestGLAccounts;
        EXIT(TRUE);
      END ELSE
        EXIT(FALSE);
    END;

    PROCEDURE GetNxtSubsidGLAcc@34(VAR GLAccount@1000 : Record 15) : Boolean;
    BEGIN
      IF SubsidGLAcc.NEXT <> 0 THEN BEGIN
        GLAccount := SubsidGLAcc;
        IF TestMode THEN
          TestGLAccounts;
        EXIT(TRUE);
      END ELSE
        EXIT(FALSE);
    END;

    PROCEDURE GetNumSubsidGLEntry@35() : Integer;
    BEGIN
      WITH SubsidGLEntry DO BEGIN
        RESET;
        SETCURRENTKEY("G/L Account No.","Posting Date");
        SETRANGE("G/L Account No.",SubsidGLAcc."No.");
        EXIT(COUNT);
      END;
    END;

    PROCEDURE Get1stSubsidGLEntry@36(VAR GLEntry@1000 : Record 17) : Boolean;
    BEGIN
      ConsolidatingClosingDate :=
        (StartingDate = EndingDate) AND
        (StartingDate <> NORMALDATE(StartingDate));
      IF (StartingDate <> NORMALDATE(StartingDate)) AND
         (StartingDate <> EndingDate)
      THEN
        ReportError(Text030);
      WITH SubsidGLEntry DO BEGIN
        RESET;
        SETCURRENTKEY("G/L Account No.","Posting Date");
        SETRANGE("G/L Account No.",SubsidGLAcc."No.");
        IF FIND('-') THEN BEGIN
          GLEntry := SubsidGLEntry;
          IF TestMode THEN BEGIN
            IF ("Posting Date" <> NORMALDATE("Posting Date")) AND
               NOT ConsolidatingClosingDate
            THEN
              ReportError(STRSUBSTNO(
                Text031,
                TABLECAPTION,
                FIELDCAPTION("Posting Date"),
                "Posting Date"));
          END;
          EXIT(TRUE);
        END ELSE
          EXIT(FALSE);
      END;
    END;

    PROCEDURE GetNxtSubsidGLEntry@37(VAR GLEntry@1000 : Record 17) : Boolean;
    BEGIN
      WITH SubsidGLEntry DO BEGIN
        IF NEXT <> 0 THEN BEGIN
          GLEntry := SubsidGLEntry;
          IF TestMode THEN BEGIN
            IF ("Posting Date" <> NORMALDATE("Posting Date")) AND
               NOT ConsolidatingClosingDate
            THEN
              ReportError(STRSUBSTNO(
                Text031,
                TABLECAPTION,
                FIELDCAPTION("Posting Date"),
                "Posting Date"));
          END;
          EXIT(TRUE);
        END ELSE
          EXIT(FALSE);
      END;
    END;

    PROCEDURE GetNumGenJnlLine@38() : Integer;
    BEGIN
      IF TestMode THEN
        EXIT(TempGenJnlLine.COUNT)
      ELSE
        EXIT(0);
    END;

    PROCEDURE Get1stGenJnlLine@39(VAR GenJnlLine@1000 : Record 81;VAR JnlLineDim@1001 : TEMPORARY Record 356) : Boolean;
    BEGIN
      IF TestMode THEN BEGIN
        JnlLineDim.DELETEALL;
        TempGenJnlLine.SETCURRENTKEY("Journal Template Name","Journal Batch Name","Posting Date");
        TempJnlLineDim.RESET;
        TempJnlLineDim.SETRANGE("Table ID",DATABASE::"Gen. Journal Line");
        IF TempGenJnlLine.FIND('-') THEN BEGIN
          GenJnlLine := TempGenJnlLine;
          TempJnlLineDim.SETRANGE("Journal Template Name",TempGenJnlLine."Journal Template Name");
          TempJnlLineDim.SETRANGE("Journal Batch Name",TempGenJnlLine."Journal Batch Name");
          TempJnlLineDim.SETRANGE("Journal Line No.",TempGenJnlLine."Line No.");
          TempJnlLineDim.SETRANGE("Allocation Line No.",0);
          IF TempJnlLineDim.FIND('-') THEN
            REPEAT
              JnlLineDim := TempJnlLineDim;
              JnlLineDim.INSERT;
            UNTIL TempJnlLineDim.NEXT = 0;
          EXIT(TRUE);
        END ELSE
          EXIT(FALSE);
      END ELSE
        EXIT(FALSE);
    END;

    PROCEDURE GetNxtGenJnlLine@40(VAR GenJnlLine@1000 : Record 81;VAR JnlLineDim@1001 : TEMPORARY Record 356) : Boolean;
    BEGIN
      IF TestMode THEN BEGIN
        JnlLineDim.DELETEALL;
        IF TempGenJnlLine.NEXT <> 0 THEN BEGIN
          GenJnlLine := TempGenJnlLine;
          TempJnlLineDim.SETRANGE("Journal Template Name",TempGenJnlLine."Journal Template Name");
          TempJnlLineDim.SETRANGE("Journal Batch Name",TempGenJnlLine."Journal Batch Name");
          TempJnlLineDim.SETRANGE("Journal Line No.",TempGenJnlLine."Line No.");
          TempJnlLineDim.SETRANGE("Allocation Line No.",0);
          IF TempJnlLineDim.FIND('-') THEN
            REPEAT
              JnlLineDim := TempJnlLineDim;
              JnlLineDim.INSERT;
            UNTIL TempJnlLineDim.NEXT = 0;
          EXIT(TRUE);
        END ELSE
          EXIT(FALSE);
      END ELSE
        EXIT(FALSE);
    END;

    PROCEDURE InsertHistoricGLAccount@1100000(VAR OldGLAccount@1100000 : Record 10721);
    BEGIN
      SubsidGLAcc.INIT;
      SubsidGLAcc."No." := OldGLAccount."No.";
      SubsidGLAcc."Consol. Translation Method" := OldGLAccount."Consol. Translation Method";
      SubsidGLAcc."Consol. Debit Acc." := OldGLAccount."Consol. Debit Acc.";
      SubsidGLAcc."Consol. Credit Acc." := OldGLAccount."Consol. Credit Acc.";
      SubsidGLAcc.INSERT;
    END;

    BEGIN
    END.
  }
}
