OBJECT Codeunit 5059 ProfileManagement
{
  OBJECT-PROPERTIES
  {
    Date=26/07/02;
    Time=12:00:00;
    Version List=NAVW13.60;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=GENERAL;ESP=GENERAL';
      Text001@1001 : TextConst 'ENU=General;ESP=General';
      Text002@1002 : TextConst 'ENU=No Profile Questionnaire is created for this Contact.;ESP=No se ha creado ning£n Cuestionario perfil para este Contacto.';
      ProfileQuestnHeaderTemp@1003 : TEMPORARY Record 5087;

    PROCEDURE FindLegalProfileQuestionnaire@5(Cont@1000 : Record 5050);
    VAR
      ProfileQuestnHeader@1001 : Record 5087;
      ContProfileAnswer@1002 : Record 5089;
      Valid@1003 : Boolean;
      ContBusRel@1004 : Record 5054;
    BEGIN
      ProfileQuestnHeaderTemp.DELETEALL;

      WITH ProfileQuestnHeader DO BEGIN
        RESET;
        IF FIND('-') THEN
          REPEAT
            Valid := TRUE;
            IF ("Contact Type" = "Contact Type"::Companies) AND
               (Cont.Type <> Cont.Type::Company)
            THEN
              Valid := FALSE;
            IF ("Contact Type" = "Contact Type"::People) AND
               (Cont.Type <> Cont.Type::Person)
            THEN
              Valid := FALSE;
            IF Valid AND ("Business Relation Code" <> '') THEN
              IF NOT ContBusRel.GET(Cont."Company No.","Business Relation Code") THEN
                Valid := FALSE;
            IF NOT Valid THEN BEGIN
              ContProfileAnswer.RESET;
              ContProfileAnswer.SETRANGE("Contact No.",Cont."No.");
              ContProfileAnswer.SETRANGE("Profile Questionnaire Code",Code);
              IF ContProfileAnswer.FIND('-') THEN
                Valid := TRUE;
            END;
            IF Valid THEN BEGIN
              ProfileQuestnHeaderTemp := ProfileQuestnHeader;
              ProfileQuestnHeaderTemp.INSERT;
            END;
          UNTIL NEXT = 0;
      END;
    END;

    PROCEDURE GetQuestionnaire@2() : Code[10];
    VAR
      ProfileQuestnHeader@1000 : Record 5087;
    BEGIN
      IF ProfileQuestnHeader.FIND('-') THEN
        EXIT(ProfileQuestnHeader.Code);

      ProfileQuestnHeader.INIT;
      ProfileQuestnHeader.Code := Text000;
      ProfileQuestnHeader.Description := Text001;
      ProfileQuestnHeader.INSERT;
      EXIT(ProfileQuestnHeader.Code);
    END;

    PROCEDURE ProfileQuestionnaireAllowed@4(Cont@1000 : Record 5050;ProfileQuestnHeaderCode@1001 : Code[10]) : Code[10];
    BEGIN
      FindLegalProfileQuestionnaire(Cont);

      IF ProfileQuestnHeaderTemp.GET(ProfileQuestnHeaderCode) THEN
        EXIT(ProfileQuestnHeaderCode)
      ELSE
        IF ProfileQuestnHeaderTemp.FIND('-') THEN
          EXIT(ProfileQuestnHeaderTemp.Code)
        ELSE
          ERROR(Text002,ProfileQuestnHeaderTemp.TABLECAPTION);
    END;

    PROCEDURE ShowContactQuestionnaireCard@7(Cont@1000 : Record 5050;ProfileQuestnLineCode@1002 : Code[10];ProfileQuestnLineLineNo@1004 : Integer);
    VAR
      ProfileQuestnLine@1003 : Record 5088;
      ContProfileAnswers@1001 : Form 5114;
    BEGIN
      ContProfileAnswers.SetParameters(Cont,ProfileQuestionnaireAllowed(Cont,''),ProfileQuestnLineCode,ProfileQuestnLineLineNo);
      IF ProfileQuestnHeaderTemp.GET(ProfileQuestnLineCode) THEN BEGIN
        ProfileQuestnLine.GET(ProfileQuestnLineCode,ProfileQuestnLineLineNo);
        ContProfileAnswers.SETRECORD(ProfileQuestnLine);
      END;
      ContProfileAnswers.RUNMODAL;
    END;

    PROCEDURE CheckName@10(CurrentQuestionsChecklistCode@1000 : Code[10];VAR Cont@1001 : Record 5050);
    BEGIN
      FindLegalProfileQuestionnaire(Cont);
      ProfileQuestnHeaderTemp.GET(CurrentQuestionsChecklistCode);
    END;

    PROCEDURE SetName@6(ProfileQuestnHeaderCode@1000 : Code[10];VAR ProfileQuestnLine@1001 : Record 5088;ContactProfileAnswerLine@1002 : Integer);
    BEGIN
      ProfileQuestnLine.FILTERGROUP := 2;
      ProfileQuestnLine.SETRANGE("Profile Questionnaire Code",ProfileQuestnHeaderCode);
      ProfileQuestnLine.FILTERGROUP := 0;
      IF ContactProfileAnswerLine = 0 THEN
        IF ProfileQuestnLine.FIND('-') THEN;
    END;

    PROCEDURE LookupName@8(VAR ProfileQuestnHeaderCode@1000 : Code[10];VAR ProfileQuestnLine@1001 : Record 5088;VAR Cont@1002 : Record 5050);
    BEGIN
      COMMIT;
      FindLegalProfileQuestionnaire(Cont);
      IF ProfileQuestnHeaderTemp.GET(ProfileQuestnHeaderCode) THEN;
      IF FORM.RUNMODAL(
        FORM::"Profile Questionnaire List", ProfileQuestnHeaderTemp) = ACTION::LookupOK
      THEN
        ProfileQuestnHeaderCode := ProfileQuestnHeaderTemp.Code;

      SetName(ProfileQuestnHeaderCode,ProfileQuestnLine,0);
    END;

    PROCEDURE ShowAnswerPoints@1(CurrProfileQuestnLine@1000 : Record 5088);
    VAR
      AnswerValues@1002 : Form 5172;
    BEGIN
      CurrProfileQuestnLine.SETRANGE("Profile Questionnaire Code",CurrProfileQuestnLine."Profile Questionnaire Code");
      FORM.RUNMODAL(FORM::"Answer Points",CurrProfileQuestnLine);
    END;

    BEGIN
    END.
  }
}
