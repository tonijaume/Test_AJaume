OBJECT Codeunit 7318 Bin Create
{
  OBJECT-PROPERTIES
  {
    Date=15/09/04;
    Time=12:00:00;
    Version List=NAVW14.00;
  }
  PROPERTIES
  {
    TableNo=7338;
    OnRun=BEGIN
            BinCreateLine.COPY(Rec);
            Code;
            Rec := BinCreateLine;
          END;

  }
  CODE
  {
    VAR
      BinCreateLine@1000 : Record 7338;
      Bin@1001 : Record 7354;
      Text000@1002 : TextConst 'ENU=Do you want to create the bin?;ESP=¨Confirma que desea crear la ubicaci¢n?';
      Text001@1003 : TextConst 'ENU=There is nothing to create.;ESP=No hay nada que crear.';
      Location@1004 : Record 14;

    PROCEDURE Code@1();
    BEGIN
      IF NOT CONFIRM(Text000,FALSE) THEN
        EXIT;

      IF BinCreateLine.FIND('-') THEN BEGIN
        REPEAT
          BinCreate(BinCreateLine);
        UNTIL BinCreateLine.NEXT = 0;
        BinCreateLine.DELETEALL;
      END ELSE
        MESSAGE(Text001);
    END;

    PROCEDURE BinCreate@2(BinCreateLine2@1000 : Record 7338);
    BEGIN
      WITH BinCreateLine2 DO BEGIN
        IF EmptyLine THEN
          EXIT;

        Bin.INIT;
        Bin.Code := "Bin Code";
        Bin.Description := Description;
        Bin."Location Code" := "Location Code";
        GetLocation("Location Code");
        IF Location."Directed Put-away and Pick" THEN BEGIN
          Bin."Zone Code" := "Zone Code";
          Bin."Bin Type Code" := "Bin Type Code";
          Bin."Warehouse Class Code" := "Warehouse Class Code";
          Bin."Block Movement" := "Block Movement";
          Bin."Special Equipment Code" := "Special Equipment Code";
          Bin."Bin Ranking" := "Bin Ranking";
          Bin."Maximum Cubage" := "Maximum Cubage";
          Bin."Maximum Weight" := "Maximum Weight";
          Bin."Cross-Dock Bin" := "Cross-Dock Bin";
        END;
        Bin.INSERT(TRUE);
      END;
    END;

    LOCAL PROCEDURE GetLocation@3(LocationCode@1000 : Code[10]);
    BEGIN
      IF LocationCode <> Location.Code THEN
        Location.GET(LocationCode);
    END;

    BEGIN
    END.
  }
}
