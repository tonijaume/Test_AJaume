OBJECT Codeunit 7010073 Cotizacion extras coste
{
  OBJECT-PROPERTIES
  {
    Date=02/10/12;
    Time=12:00:00;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      cVarias@1103355002 : Codeunit 7009720;
      cCotCom@1103355001 : Codeunit 7009913;
      cCalRes@1103355000 : Codeunit 7009707;
      Text001@1103355004 : TextConst 'ENU=EXTRAFIJOS;ESP=EXTRAFIJOS';
      Text002@1103355003 : TextConst 'ENU=EXTRAVARIA;ESP=EXTRAVARIA';

    PROCEDURE CacularExtras@1103355014(VAR prCabCot@1103355007 : Record 7009871;VAR prCabCal@1103355001 : TEMPORARY Record 7009735;VAR prHoteles@1103355010 : TEMPORARY Record 7009873;VAR prContratos@1103355012 : TEMPORARY Record 7009873;VAR prHabitaciones@1103355013 : TEMPORARY Record 7009873;VAR prPersonas@1103355011 : TEMPORARY Record 7009873;VAR prExtras@1103355014 : TEMPORARY Record 7009873);
    VAR
      lrPersoAux@1103355006 : TEMPORARY Record 7009873;
    BEGIN
      // CalcularExtras

      cCotCom.CopiarListaPersonas(prPersonas, lrPersoAux);

      CalcularExtrasFijos(prCabCal, prHabitaciones, prPersonas, lrPersoAux, DT2DATE(prCabCot."Fecha creacion"));
      CalcularExtrasVariables(prCabCal, prHabitaciones, prExtras, lrPersoAux, DT2DATE(prCabCot."Fecha creacion"));
    END;

    LOCAL PROCEDURE CalcularExtrasVariables@1103355006(VAR prCabCal@1103355006 : TEMPORARY Record 7009735;VAR prHabs@1103355000 : TEMPORARY Record 7009873;VAR prExtrasVar@1103355007 : TEMPORARY Record 7009873;VAR prPersosAux@1103355002 : Record 7009873;pwFechaVenta@1103355003 : Date);
    VAR
      lrPreCTMP@1103355004 : TEMPORARY Record 7009738;
      lrPersHab@1103355005 : TEMPORARY Record 2000000007;
    BEGIN
      // CalcularExtrasVariables

      prHabs.RESET;

      IF prHabs.FINDSET THEN BEGIN

        REPEAT

          IF prHabs."Aplicacion coste manual" IN [0,3] THEN BEGIN

            BuscarPrecios(prHabs, lrPreCTMP, pwFechaVenta, prHabs."Fecha fin", FALSE);
            cVarias.IniciarPeriodo(lrPersHab, prHabs."Fecha inicio", prHabs."Fecha fin");

            prExtrasVar.RESET;
            prExtrasVar.SETRANGE(Habitacion, prHabs.Habitacion);
            prExtrasVar.SETRANGE(Contrato  , prHabs.Contrato);

            IF prExtrasVar.FINDSET THEN BEGIN
              REPEAT

                BuscarExtraVariable(prCabCal, lrPreCTMP, prExtrasVar, lrPersHab, prPersosAux, pwFechaVenta);

              UNTIL prExtrasVar.NEXT = 0;
            END;

          END;

        UNTIL prHabs.NEXT = 0;

      END;
    END;

    LOCAL PROCEDURE CalcularExtrasFijos@1103355007(VAR prCab@1103355007 : TEMPORARY Record 7009735;VAR prHabs@1103355003 : TEMPORARY Record 7009873;VAR prPersos@1103355002 : TEMPORARY Record 7009873;VAR prPersosAux@1103355001 : Record 7009873;pwFechaVenta@1103355000 : Date);
    VAR
      lrPreCTMP@1103355005 : TEMPORARY Record 7009738;
      lrPersHab@1103355004 : TEMPORARY Record 2000000007;
      lrPersRes@1103355006 : TEMPORARY Record 2000000007;
    BEGIN
      // CalcularExtrasFijos

      prHabs.RESET;

      IF prHabs.FINDSET THEN BEGIN
        REPEAT
          IF prHabs."Aplicacion coste manual" IN [0,3] THEN BEGIN
            BuscarPrecios(prHabs, lrPreCTMP, pwFechaVenta, prHabs."Fecha fin", TRUE);
            cVarias.IniciarPeriodo(lrPersHab,prHabs."Fecha inicio", prHabs."Fecha fin");

            prPersos.RESET;
            prPersos.SETRANGE(Habitacion, prHabs.Habitacion);
            prPersos.SETRANGE(Contrato  , prHabs.Contrato);
            IF prPersos.FINDSET THEN BEGIN
              REPEAT

                FiltrarPrecios(lrPreCTMP, prPersos.Habitacion, prPersos.Regimen,
                               prPersos.Responsable, prPersos."N§ Persona", '');

                IF lrPreCTMP.FINDSET THEN BEGIN
                  REPEAT

                    IF ComprobarCriteriosAplicacion(prPersos, prPersosAux, lrPreCTMP, lrPersHab, lrPersRes, pwFechaVenta) THEN
                      AplicarExtra(prCab, prPersos, lrPreCTMP, lrPersHab, lrPersRes, Text001);

                  UNTIL lrPreCTMP.NEXT = 0;
                END;

              UNTIL prPersos.NEXT = 0;
            END;
          END;
        UNTIL prHabs.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE BuscarPrecios@1103355011(VAR prHabitacion@1103355004 : Record 7009873;VAR prPreC@1103355000 : TEMPORARY Record 7009738;pwFechaIni@1103355002 : Date;pwFechaFin@1103355003 : Date;pwAplicacionFija@1103355005 : Boolean);
    VAR
      lrPreC@1103355001 : Record 7009738;
    BEGIN
      // BuscarPrecios

      prPreC.RESET;
      prPreC.DELETEALL;

      lrPreC.RESET;
      lrPreC.SETCURRENTKEY(Delegacion   , "N§ Contrato", "Codigo Precio", Codigo,
                           "Fecha desde", "Fecha hasta", "Edad desde"   , "Edad hasta");

      lrPreC.SETRANGE ("N§ Contrato"    , prHabitacion.Contrato);
      lrPreC.SETRANGE ("Codigo Precio"  , lrPreC."Codigo Precio"::Extras);
      lrPreC.SETFILTER("Fecha desde"    , '<=%1', pwFechaFin);
      lrPreC.SETFILTER("Fecha hasta"    , '>=%1', pwFechaIni);
      lrPreC.SETRANGE ("Aplicacion fija", pwAplicacionFija);
      lrPreC.SETRANGE (Activo           , TRUE);
      IF lrPreC.FINDSET THEN BEGIN
        REPEAT
          prPreC := lrPreC;
          prPreC.INSERT;
        UNTIL lrPreC.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE FiltrarPrecios@1103355013(VAR prPreC@1103355001 : TEMPORARY Record 7009738;pwHabitacion@1103355000 : Code[10];pwRegimen@1103355002 : Code[10];pwResponsable@1103355006 : Boolean;pwNumPers@1103355003 : Integer;pwExtra@1103355004 : Code[10]);
    VAR
      lwResponsable@1103355005 : Integer;
    BEGIN
      // FiltrarPrecios

      prPreC.RESET;
      prPreC.SETCURRENTKEY(Delegacion   , "N§ Contrato", "Codigo Precio", Codigo,
                           "Fecha desde", "Fecha hasta", "Edad desde"   , "Edad hasta");

      prPreC.SETFILTER("Tipo Habitacion", '%1|%2', pwHabitacion                     , '');
      prPreC.SETFILTER("Tipo Regimen"   , '%1|%2', pwRegimen                        , '');
      prPreC.SETFILTER("Tipo Persona"   , '%1|%2', cCotCom.GetTipoPersona(pwNumPers), '');

      // los precios que no son por pax solo se aplican al responsable.
      IF NOT pwResponsable THEN
        prPreC.SETRANGE("Tipo Precio", prPreC."Tipo Precio"::Pax);

      IF pwExtra <> '' THEN
        prPreC.SETRANGE(Codigo, pwExtra);
    END;

    PROCEDURE BuscarExtraVariable@1103355015(VAR prCabCal@1103355000 : TEMPORARY Record 7009735;VAR prPreC@1103355001 : TEMPORARY Record 7009738;VAR prExtras@1103355002 : TEMPORARY Record 7009873;VAR prPerHab@1103355004 : TEMPORARY Record 2000000007;VAR prPersoAux@1103355003 : TEMPORARY Record 7009873;pwFechaVenta@1103355007 : Date);
    VAR
      lwCont@1103355006 : Integer;
      lrPerRes@1103355005 : TEMPORARY Record 2000000007;
      lrPerExtra@1103355008 : TEMPORARY Record 2000000007;
      lwDias@1103355009 : Integer;
    BEGIN
      // BuscarExtraVariable

      FiltrarPrecios(prPreC, prExtras.Habitacion, prExtras.Regimen,
                     prExtras.Responsable, prExtras."N§ Persona", prExtras.Extra);

      IF prPreC.FINDSET THEN BEGIN
        REPEAT

          cVarias.PeriodoVacio(lrPerRes);
          IF ComprobarCriteriosAplicacion(prExtras, prPersoAux, prPreC, prPerHab, lrPerRes, pwFechaVenta) THEN BEGIN
            cVarias.IniciarPeriodo(lrPerExtra,prExtras."Fecha inicio", prExtras."Fecha fin");
            AplicarExtra(prCabCal, prExtras, prPreC, lrPerExtra, lrPerRes, Text002);
          END;

        UNTIL prPreC.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE AplicarExtra@1103355012(VAR prCab@1103355010 : TEMPORARY Record 7009735;VAR prPerso@1103355000 : TEMPORARY Record 7009873;VAR prPreC@1103355001 : TEMPORARY Record 7009738;VAR prPeriodo@1103355002 : TEMPORARY Record 2000000007;VAR prPerRes@1103355003 : TEMPORARY Record 2000000007;pwCodLin@1103355012 : Code[10]);
    VAR
      lrLinCal@1103355011 : Record 7009736;
      lrPerAux@1103355004 : TEMPORARY Record 2000000007;
      lwDiasEstancia@1103355005 : Integer;
      lwCantidad@1103355006 : Decimal;
      lwImporte@1103355007 : Decimal;
      lwDia@1103355008 : Date;
      lwFiltroFecha@1103355009 : Text[1024];
      lwHotelFactura@1103355013 : Code[20];
    BEGIN
      // AplicarExtra

      IF prPreC.Porcentaje THEN BEGIN

        // Generamos el filtro de fechas que vamos a usar para calcular el porcentaje.
        CASE prPreC.Frecuencia OF

          // aplicacion £nica.
          0: lwFiltroFecha := FORMAT(prPeriodo."Period Start");

          // aplicacion diaria.
          1: lwFiltroFecha := STRSUBSTNO('%1..%2', prPeriodo."Period Start", prPeriodo."Period End");

          // aplicacion cada x dias.
          ELSE BEGIN
            lwDia := prPeriodo."Period Start";

            // podria fallar para estancias muy largas. (m s de 200 d¡as)
            WHILE (lwDia <= prPeriodo."Period End") DO BEGIN
              lwFiltroFecha := FORMAT(lwDia) + '|';
              lwDia := lwDia + prPreC.Frecuencia;
            END;

            lwFiltroFecha := DELCHR(lwFiltroFecha, '>', '|');

          END;
        END;

        IF lwFiltroFecha <> '' THEN
          lwImporte := cCotCom.CalcularPorcentajeCoste(prCab,prPreC, prPerso, lwFiltroFecha);

        lwCantidad := 1;

      END
      ELSE BEGIN

        // Aplicacion £nica.
        IF prPreC.Frecuencia = 0 THEN
          lwCantidad := 1
        // Aplicacion diaria / cada x d¡as
        ELSE BEGIN
          // Calculamos los dias de estancia.
          prPerRes.RESET;
          prPerRes.FINDFIRST;
          cVarias.InterPer(prPeriodo, prPerRes, lrPerAux);
          lwCantidad := ROUND(cVarias.GetDiasPeriodo(lrPerAux) / prPreC.Frecuencia, 0.01);
        END;

        IF prPreC."Tipo Precio" = prPreC."Tipo Precio"::Habitaci¢n THEN
          lwCantidad := lwCantidad * prPerso."N§ Habitaciones";

        lwImporte := prPreC.Importe;

      END;


      cCalRes.inserta_linea_calculo(lrLinCal, prCab.Usuario, pwCodLin, prPerso.Contrato,
                                    ROUND(lwCantidad * lwImporte), prPerso."N§ Persona", prPerso."Fecha inicio", prPreC.Codigo,
                                    ROUND(lwCantidad, 1), lwImporte, prPreC.Garantizable, prPreC."Importe HP");

      lwHotelFactura := prPerso.Hotel;

      IF prPreC."Hotel Factura" <> '' THEN
        lwHotelFactura := prPreC."Hotel Factura";

      cCalRes.fijos_linea_calculo  (lrLinCal, lrLinCal."Coste/Ingreso"::Coste, '', prPerso."N§ Persona",
                                    prPerso.Hotel, 0, lwHotelFactura, prPreC.Delegacion,
                                    prPreC."Validacion HP especial");
    END;

    LOCAL PROCEDURE ComprobarCriteriosAplicacion@1103355061(VAR prPersona@1103355000 : TEMPORARY Record 7009873;VAR prPersonas@1103355001 : TEMPORARY Record 7009873;VAR prPreC@1103355003 : TEMPORARY Record 7009738;VAR prPeriodos@1103355006 : TEMPORARY Record 2000000007;VAR prPerRes@1103355002 : TEMPORARY Record 2000000007;pwFechaVenta@1103355008 : Date) rvAplic : Boolean;
    VAR
      lwAcaba@1103355004 : Boolean;
      lwNumCrit@1103355005 : Integer;
    BEGIN
      // ComprobarCriteriosAplicacion

      rvAplic   := TRUE;
      lwAcaba   := FALSE;
      lwNumCrit := 0;

      WHILE rvAplic AND NOT lwAcaba DO BEGIN
        lwNumCrit += 1;
        CASE lwNumCrit OF

          1: rvAplic := ComprobarEdades    (prPersona, prPreC);
          2: rvAplic := ComprobarFechas    (prPreC, prPersona, pwFechaVenta, prPeriodos);
          3: rvAplic := ComprobarNumPersona(prPersona, prPreC, prPersonas);
          4: rvAplic := ComprobarHotelOfertaEspecial;
          5: rvAplic := ComprobarAplicaEstancia;

          ELSE
            lwAcaba := TRUE;
        END;
      END;

      cVarias.CopiaPeriodos(prPeriodos,prPerRes);
    END;

    LOCAL PROCEDURE ComprobarEdades@1103355000(VAR prPersona@1103355000 : TEMPORARY Record 7009873;VAR prPreC@1103355001 : TEMPORARY Record 7009738) : Boolean;
    VAR
      lwEdadHasta@1103355002 : Integer;
    BEGIN
      // ComprobarEdades
      // comprobamos si el pax entra dentro del rango de edades.

      IF prPreC."Edad hasta" = 0 THEN
        lwEdadHasta := 999
      ELSE
        lwEdadHasta := prPreC."Edad hasta";

      EXIT((prPersona.Edad >= prPreC."Edad desde") AND (prPersona.Edad <= lwEdadHasta));
    END;

    LOCAL PROCEDURE ComprobarNumPersona@1103355001(VAR prPersona@1103355000 : TEMPORARY Record 7009873;VAR prPreC@1103355001 : TEMPORARY Record 7009738;VAR prPersonas@1103355002 : TEMPORARY Record 7009873) : Boolean;
    BEGIN
      // ComprobarNumPersona
      //: Comprobamos que la posicion en la habitacion coincida con la indicada en el suplemento.
      //: En caso de que el precio tenga un rango de edades, la posicion es dentro de ese rango de edades.

      IF prPreC."Edad hasta" = 0 THEN
        EXIT((prPreC."N§ Persona" = 0) OR (prPersona.Posicion = prPreC."N§ Persona"))
      ELSE
        EXIT((prPreC."N§ Persona" = 0) OR
             (GetNumPersonaxEdad(prPersona, prPersonas, prPreC."Edad desde", prPreC."Edad hasta") = prPreC."N§ Persona"));
    END;

    LOCAL PROCEDURE GetNumPersonaxEdad@1103355025(VAR prPersona@1103355000 : TEMPORARY Record 7009873;VAR prPersonas@1103355001 : TEMPORARY Record 7009873;pwEdadDesde@1103355003 : Integer;pwEdadHasta@1103355004 : Integer) rvNum : Integer;
    VAR
      lwAcaba@1103355002 : Boolean;
    BEGIN
      // GetNumPersonaxEdad

      prPersonas.RESET;
      prPersonas.SETCURRENTKEY("Id cotizacion", Edad);

      lwAcaba := FALSE;
      rvNum   := 0;

      IF prPersonas.FINDLAST THEN BEGIN
        REPEAT
          IF (prPersonas.Edad >= pwEdadDesde) AND (prPersonas.Edad <= pwEdadHasta) THEN
            rvNum += 1;
          lwAcaba :=  (prPersona."N§ Persona" = prPersonas."N§ Persona") OR (prPersonas.NEXT(-1) = 0);
        UNTIL lwAcaba;
      END;

      IF prPersona."N§ Habitaciones" <> 0 THEN
        rvNum := ROUND(rvNum / prPersona."N§ Habitaciones", 1);

      IF rvNum = 0 THEN
        rvNum := 1;
    END;

    LOCAL PROCEDURE ComprobarFechas@1103355002(VAR prPreC@1103355001 : TEMPORARY Record 7009738;VAR prPersona@1103355000 : TEMPORARY Record 7009873;pwFechaVenta@1103355002 : Date;VAR prPeriodos@1103355003 : TEMPORARY Record 2000000007) rvRes : Boolean;
    VAR
      lrPerPrecio@1103355005 : TEMPORARY Record 2000000007;
    BEGIN
      // ComprobarFechas

      CASE prPreC."Tipo Fechas" OF

        prPreC."Tipo Fechas"::"Fecha venta"          :
          EXIT(((pwFechaVenta >= prPreC."Fecha desde") AND (pwFechaVenta <= prPreC."Fecha hasta")));

        prPreC."Tipo Fechas"::"Fecha entrada"        :
          EXIT(((prPeriodos."Period Start" >= prPreC."Fecha desde") AND (prPeriodos."Period End" <= prPreC."Fecha hasta")));

        prPreC."Tipo Fechas"::"Fecha estancia"       :
          EXIT(((prPeriodos."Period Start" >= prPreC."Fecha desde") AND (prPeriodos."Period End" <= prPreC."Fecha hasta")));

        prPreC."Tipo Fechas"::"Fecha venta+entrada"  :
          EXIT(((pwFechaVenta >= prPreC."Fecha desde") AND (pwFechaVenta <= prPreC."Fecha hasta")) AND
               ((prPeriodos."Period Start" >= prPreC."2¦ Fecha desde") AND (prPeriodos."Period Start" <= prPreC."2¦ Fecha hasta")));

        prPreC."Tipo Fechas"::"Fecha venta+estancia" :
          EXIT(((pwFechaVenta >= prPreC."Fecha desde") AND (pwFechaVenta <= prPreC."Fecha hasta")) AND
               ((prPeriodos."Period Start" >= prPreC."2¦ Fecha desde") AND (prPeriodos."Period Start" <= prPreC."2¦ Fecha hasta")));

        prPreC."Tipo Fechas"::"Fecha salida"         :
          EXIT(((prPeriodos."Period Start" >= prPreC."Fecha desde") AND (prPeriodos."Period Start" <= prPreC."Fecha hasta")));

        prPreC."Tipo Fechas"::"Fecha venta+salida"   :
          EXIT(((pwFechaVenta >= prPreC."Fecha desde") AND (pwFechaVenta <= prPreC."Fecha hasta")) AND
               ((prPeriodos."Period Start" >= prPreC."2¦ Fecha desde") AND (prPeriodos."Period Start" <= prPreC."2¦ Fecha hasta")));

        prPreC  ."Tipo Fechas"::"Fecha entrada+salida" :
          EXIT(((prPeriodos."Period Start" >= prPreC."Fecha desde") AND (prPeriodos."Period Start" <= prPreC."Fecha hasta")) AND
               ((prPeriodos."Period Start" >= prPreC."2¦ Fecha desde") AND (prPeriodos."Period Start" <= prPreC."2¦ Fecha hasta")));

      END;
    END;

    LOCAL PROCEDURE ComprobarHotelOfertaEspecial@1103355016() : Boolean;
    BEGIN
      // ComprobarHotelOfertaEspecial
      // Para cotizaciones no se puede comprobar.

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ComprobarAplicaEstancia@1103355017() : Boolean;
    BEGIN
      // ComprobarAplicaEstancia
      // Para cotizaciones no se puede comprobar.

      EXIT(TRUE);
    END;

    BEGIN
    {

      ## AJF 29/06/09

        .- (09/00319) cambio c¢digos de hotel de code10 a code20.
    }
    END.
  }
}
