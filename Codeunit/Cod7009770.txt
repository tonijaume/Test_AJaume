OBJECT Codeunit 7009770 Genera mov. dato analitico
{
  OBJECT-PROPERTIES
  {
    Date=02/10/12;
    Time=12:00:00;
    Modified=Yes;
    Version List=AIC2009,DA;
  }
  PROPERTIES
  {
    TableNo=7009911;
    OnRun=VAR
            MovDato@1103355001 : Record 7009914;
            DimDato@1103355002 : Record 7009913;
            FiltDato@1103355004 : Record 7009912;
            RecRef@1103355000 : RecordRef;
            FRef@1103355003 : FieldRef;
            OK@1103355005 : Boolean;
          BEGIN
            TESTFIELD("Id. Tabla");
            TESTFIELD("Id. Campo fecha");
            TESTFIELD("Id. Campo valor");
            RecRef.OPEN("Id. Tabla", FALSE, "Empresa origen");
            RecRef.CURRENTKEYINDEX("Id. Clave");

            FRef := RecRef.FIELD("Id. Campo fecha");
            IF GETFILTER("Filtro fecha") <> '' THEN
              FRef.SETFILTER(GETFILTER("Filtro fecha"));

            FiltDato.SETRANGE("Cod. dato analitico", Codigo);
            IF FiltDato.FIND('-') THEN
              REPEAT
                FRef := RecRef.FIELD(FiltDato."Id. campo");
                FRef.SETFILTER(FiltDato.Filtro);
              UNTIL FiltDato.NEXT = 0;

            IF WindowUpdate.UpdateNoOfRecords(RecRef.COUNT, Descripcion) THEN
              IF RecRef.FIND('-') THEN BEGIN
                MovDato.RESET;
                IF GETFILTER("Filtro fecha") <> '' THEN BEGIN
                  MovDato.SETCURRENTKEY(Fecha);
                  MovDato.SETFILTER(Fecha, GETFILTER("Filtro fecha"));
                END;
                MovDato.SETRANGE("Cod. dato analitico", Codigo);
                MovDato.DELETEALL;
                MovDato.RESET;
                MovDato."Cod. dato analitico" := Codigo;

                REPEAT
                  WindowUpdate.Update;

                  MovDato.Fecha := RecRef.FIELD("Id. Campo fecha").VALUE;
                  IF MovDato.Fecha > "Ult. fecha actualizada" THEN
                    "Ult. fecha actualizada" := MovDato.Fecha;

                  CASE "Agrupacion por fecha" OF
                    "Agrupacion por fecha"::Semanal: MovDato.Fecha := CALCDATE(Text001,MovDato.Fecha);
                    "Agrupacion por fecha"::Mensual: MovDato.Fecha := CALCDATE(Text002, MovDato.Fecha);
                    "Agrupacion por fecha"::Trismestral: MovDato.Fecha := CALCDATE(Text003, MovDato.Fecha);
                    "Agrupacion por fecha"::Anual: MovDato.Fecha := CALCDATE(Text004, MovDato.Fecha);
                  END;
                  MovDato."Cod. dimension 1" := GetDimensionValue(RecRef,"Id. Campo dimension 1","Formato dimension 1",Rec,0);
                  MovDato."Cod. dimension 2" := GetDimensionValue(RecRef,"Id. Campo dimension 2","Formato dimension 2",Rec,1);
                  MovDato."Cod. dimension 3" := GetDimensionValue(RecRef,"Id. Campo dimension 3","Formato dimension 3",Rec,2);
                  MovDato."Cod. dimension 4" := GetDimensionValue(RecRef,"Id. Campo dimension 4","Formato dimension 4",Rec,3);

                  Field.GET(RecRef.NUMBER, "Id. Campo valor");
                  IF Field.Class = Field.Class::FlowField THEN
                    OK := RecRef.FIELD("Id. Campo valor").CALCFIELD;
                  MovDato.Valor := RecRef.FIELD("Id. Campo valor").VALUE;
                  UpdateMovDato(MovDato);
                UNTIL RecRef.NEXT = 0;

                "Fecha Ult. actualizacion" := TODAY;
                MODIFY;
              END;
          END;

  }
  CODE
  {
    VAR
      Text001@1103355000 : TextConst 'ENU=CW;ESP=PS';
      Text002@1103355001 : TextConst 'ENU=PM;ESP=PM;ITA=PM';
      Text003@1103355002 : TextConst 'ENU=CQ;ESP=PT;FRA=PT';
      Text004@1103355003 : TextConst 'ENU=CY;ESP=PA;FRA=PA';
      Text005@1103355004 : TextConst 'ENU=Dimension created in process;ESP=Dimension creada en proceso';
      Field@1103355005 : Record 2000000041;
      WindowUpdate@1103355007 : Codeunit 7009771;
      Text006@1103355006 : TextConst 'ENU=Name;ESP=Nombre;FRA=Pr‚nom;ITA=Nome;PTG=Nome';
      Text007@1103355008 : TextConst 'ENU=Description;ESP=Descripci¢n;FRA=Description;ITA=Descrizione;PTG=Descri‡Æo';

    PROCEDURE UpdateMovDato@1103355015(par_MovDato@1103355000 : Record 7009914);
    VAR
      MovDato@1103355001 : Record 7009914;
    BEGIN
      IF par_MovDato.Valor = 0 THEN
        EXIT;

      MovDato.SETCURRENTKEY(Fecha,"Cod. dimension 1","Cod. dimension 2","Cod. dimension 3","Cod. dimension 4");
      MovDato.SETRANGE("Cod. dato analitico", par_MovDato."Cod. dato analitico");
      MovDato.SETRANGE(Fecha, par_MovDato.Fecha);
      MovDato.SETRANGE("Cod. dimension 1", par_MovDato."Cod. dimension 1");
      MovDato.SETRANGE("Cod. dimension 2", par_MovDato."Cod. dimension 2");
      MovDato.SETRANGE("Cod. dimension 3", par_MovDato."Cod. dimension 3");
      MovDato.SETRANGE("Cod. dimension 4", par_MovDato."Cod. dimension 4");
      IF MovDato.FIND('-') THEN BEGIN
        MovDato.Valor += par_MovDato.Valor;
        IF MovDato.Valor = 0 THEN
          MovDato.DELETE
        ELSE
          MovDato.MODIFY;
      END
      ELSE BEGIN
        //UpdateDimDato(par_MovDato."Cod. dato analitico", 0, par_MovDato."Cod. dimension 1");
        //UpdateDimDato(par_MovDato."Cod. dato analitico", 1, par_MovDato."Cod. dimension 2");
        //UpdateDimDato(par_MovDato."Cod. dato analitico", 2, par_MovDato."Cod. dimension 3");
        //UpdateDimDato(par_MovDato."Cod. dato analitico", 3, par_MovDato."Cod. dimension 4");
        MovDato.RESET;
        MovDato.SETRANGE("Cod. dato analitico", par_MovDato."Cod. dato analitico");
        IF MovDato.FIND('+') THEN
          par_MovDato."No. mov." := MovDato."No. mov." + 1
        ELSE
          par_MovDato."No. mov." := 1;
        par_MovDato.INSERT;
      END;
    END;

    PROCEDURE GetDimensionValue@1103355004(VAR RecRef@1103355002 : RecordRef;DimFieldID@1103355000 : Integer;FieldFormat@1103355001 : Text[30];AnalyticData@1103355005 : Record 7009911;DimNo@1103355006 : Integer) DimCode : Code[20];
    VAR
      DimDato@1103355003 : Record 7009913;
      RecRef2@1103355007 : RecordRef;
      FRef@1103355008 : FieldRef;
      DimValue@1103355004 : Text[100];
      i@1103355009 : Integer;
    BEGIN
      IF DimFieldID = 0 THEN
        EXIT('');

      Field.GET(RecRef.NUMBER, DimFieldID);
      IF Field.Class = Field.Class::FlowField THEN
        IF NOT RecRef.FIELD(DimFieldID).CALCFIELD THEN
          EXIT('');

      IF Field.Type IN [Field.Type::Text,Field.Type::Code] THEN
        DimValue := RecRef.FIELD(DimFieldID).VALUE
      ELSE IF FieldFormat <> '' THEN
        DimValue := FORMAT(RecRef.FIELD(DimFieldID).VALUE, MAXSTRLEN(DimCode), FieldFormat)
      ELSE
        DimValue := STRSUBSTNO('%1', RecRef.FIELD(DimFieldID).VALUE);
      DimCode := COPYSTR(DimValue,1,MAXSTRLEN(DimCode));

      IF NOT DimDato.GET(AnalyticData.Codigo, DimNo, DimCode) THEN BEGIN
        IF Field.RelationTableNo <> 0 THEN BEGIN
          RecRef2.OPEN(Field.RelationTableNo, FALSE, AnalyticData."Empresa origen");
          IF Field.RelationFieldNo <> 0 THEN
            FRef := RecRef2.FIELD(Field.RelationFieldNo)
          ELSE
            FRef := RecRef2.FIELDINDEX(1);
          FRef.SETRANGE(RecRef.FIELD(DimFieldID).VALUE);
          IF RecRef2.FIND('-') THEN BEGIN
            i := 0;
            REPEAT
              i += 1;
              IF RecRef2.FIELDINDEX(i).CAPTION IN [Text006,Text007] THEN
                DimDato.Descripcion := COPYSTR(FORMAT(RecRef2.FIELDINDEX(i).VALUE),1,MAXSTRLEN(DimDato.Descripcion));
            UNTIL (i = RecRef2.FIELDCOUNT) OR (DimDato.Descripcion <> '');
          END;
        END;
        DimDato."Cod. dato analitico" := AnalyticData.Codigo;
        DimDato.Dimension := DimNo;
        DimDato.Codigo := DimCode;
        IF DimDato.Descripcion = '' THEN
          DimDato.Descripcion := Text005;
        DimDato.INSERT;
      END;
    END;

    BEGIN
    END.
  }
}
