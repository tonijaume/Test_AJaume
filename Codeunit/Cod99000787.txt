OBJECT Codeunit 99000787 Create Prod. Order Lines
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    Permissions=TableData 27=r,
                TableData 36=r,
                TableData 37=r,
                TableData 5406=rimd,
                TableData 5407=rimd,
                TableData 5408=r,
                TableData 99000765=rim,
                TableData 99000774=r,
                TableData 5405=r;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Item@1000 : Record 27;
      SalesHeader@1002 : Record 36;
      SalesLine@1003 : Record 37;
      FamilyLine@1004 : Record 99000774;
      MfgSetup@1005 : Record 99000765;
      ProdOrder@1006 : Record 5405;
      ProdOrderLine@1007 : Record 5406;
      ProdOrderLine2@1008 : Record 5406;
      ProdOrderComp@1009 : Record 5407;
      ReservEntry@1010 : Record 337;
      TempOldProdOrderComp@1011 : TEMPORARY Record 5407;
      ReserveSalesLine@1012 : Codeunit 99000832;
      ReserveProdOrderComp@1013 : Codeunit 99000838;
      CalcProdOrder@1014 : Codeunit 99000773;
      NextProdOrderLineNo@1015 : Integer;
      InsertNew@1016 : Boolean;
      SalesLineIsSet@1001 : Boolean;

    PROCEDURE CheckStructure@8(Status@1000 : Option;ProdOrderNo@1001 : Code[20];Direction@1002 : 'Forward,Backward';MultiLevel@1003 : Boolean);
    BEGIN
      ProdOrder.GET(Status,ProdOrderNo);
      ProdOrderLine.SETRANGE(Status,Status);
      ProdOrderLine.SETRANGE("Prod. Order No.",ProdOrderNo);
      IF ProdOrderLine.FIND('+') THEN
        NextProdOrderLineNo := ProdOrderLine."Line No." + 10000
      ELSE
        NextProdOrderLineNo := 10000;

      CheckMultiLevelStructure(Direction,MultiLevel);
    END;

    PROCEDURE Copy@5(ProdOrder2@1000 : Record 5405;Direction@1001 : 'Forward,Backward';VariantCode@1002 : Code[10]);
    BEGIN
      MfgSetup.GET;

      ProdOrder2.TESTFIELD("Source No.");
      ProdOrder2.TESTFIELD("Starting Time");
      ProdOrder2.TESTFIELD("Starting Date");
      ProdOrder2.TESTFIELD("Ending Time");
      ProdOrder2.TESTFIELD("Ending Date");
      IF Direction = Direction::Backward THEN
        ProdOrder2.TESTFIELD("Due Date");

      ProdOrder := ProdOrder2;

      ProdOrderLine.LOCKTABLE;
      ProdOrderLine.SETRANGE(Status,ProdOrder.Status);
      ProdOrderLine.SETRANGE("Prod. Order No.",ProdOrder."No.");
      ProdOrderLine.DELETEALL(TRUE);

      NextProdOrderLineNo := 10000;

      InsertNew := FALSE;
      CASE ProdOrder."Source Type" OF
        ProdOrder."Source Type"::Item:
          BEGIN
            Item.GET(ProdOrder."Source No.");
            InitProdOrderLine;
            ProdOrderLine."Variant Code" := VariantCode;
            ProdOrderLine.Description := ProdOrder.Description;
            ProdOrderLine."Description 2" := ProdOrder."Description 2";
            ProdOrderLine.VALIDATE(Quantity,ProdOrder.Quantity);
            ProdOrderLine.UpdateDatetime;
            IF SalesLineIsSet THEN
              CopyDimFromSalesLine(SalesLine,ProdOrderLine);
            ProdOrderLine.INSERT;
          END;
        ProdOrder."Source Type"::Family:
          CopyFromFamily;
        ProdOrder."Source Type"::"Sales Header":
          BEGIN
            InsertNew := TRUE;
            IF ProdOrder.Status <> ProdOrder.Status::Simulated THEN
              SalesHeader.GET(SalesHeader."Document Type"::Order,ProdOrder."Source No.")
            ELSE
              SalesHeader.GET(SalesHeader."Document Type"::Quote,ProdOrder."Source No.");
            CopyFromSalesOrder;
          END;
      END;

      ProcessProdOrderLines(Direction);
      CheckMultiLevelStructure(Direction,TRUE);
    END;

    LOCAL PROCEDURE CopyFromFamily@3();
    VAR
      Family@1000 : Record 99000773;
    BEGIN
      Family.GET(ProdOrder."Source No.");
      FamilyLine.SETCURRENTKEY("Low-Level Code");
      FamilyLine.SETRANGE("Family No.",ProdOrder."Source No.");

      IF FamilyLine.FINDSET THEN
        REPEAT
          IF FamilyLine."Item No." <> '' THEN BEGIN
            Item.GET(FamilyLine."Item No.");

            InitProdOrderLine;
            ProdOrderLine."Variant Code" := '';
            ProdOrderLine.Description := FamilyLine.Description;
            ProdOrderLine."Description 2" := FamilyLine."Description 2";
            ProdOrderLine.VALIDATE("Unit of Measure Code",FamilyLine."Unit of Measure Code");
            ProdOrderLine.VALIDATE(Quantity,FamilyLine.Quantity * ProdOrder.Quantity);
            ProdOrderLine."Routing No." := Family."Routing No.";
            ProdOrderLine."Routing Reference No." := 0;
            ProdOrderLine.UpdateDatetime;
            InsertProdOrderLine;
          END;
        UNTIL FamilyLine.NEXT = 0;
    END;

    LOCAL PROCEDURE CopyFromSalesOrder@1();
    VAR
      SalesPlanLine@1000 : TEMPORARY Record 99000800;
      LeadTimeMgt@1001 : Codeunit 5404;
    BEGIN
      SalesLine.SETRANGE("Document Type",SalesHeader."Document Type");
      SalesLine.SETRANGE("Document No.",SalesHeader."No.");
      IF SalesLine.FINDSET THEN
        REPEAT
          SalesLine.CALCFIELDS("Reserved Quantity");
          IF (SalesLine.Type = SalesLine.Type::Item) AND
             (SalesLine."No." <> '') AND
             ((SalesLine."Outstanding Quantity" - SalesLine."Reserved Quantity") <> 0)
          THEN BEGIN
            Item.GET(SalesLine."No.");
            IF Item."Replenishment System" = Item."Replenishment System"::"Prod. Order" THEN BEGIN
              SalesPlanLine.INIT;
              SalesPlanLine."Sales Order No." := SalesLine."Document No.";
              SalesPlanLine."Sales Order Line No." := SalesLine."Line No.";
              SalesPlanLine."Item No." := SalesLine."No.";
              SalesPlanLine."Low-Level Code" := Item."Low-Level Code";
              SalesPlanLine.INSERT;
            END;
          END;
        UNTIL SalesLine.NEXT = 0;

      SalesPlanLine.SETCURRENTKEY("Low-Level Code");
      IF SalesPlanLine.FINDSET THEN
        REPEAT
          SalesLine.GET(
            SalesHeader."Document Type",
            SalesPlanLine."Sales Order No.",
            SalesPlanLine."Sales Order Line No.");
          SalesLine.CALCFIELDS("Reserved Quantity");
          Item.GET(SalesLine."No.");

          InitProdOrderLine;
          ProdOrderLine."Variant Code" := SalesLine."Variant Code";
          ProdOrderLine.Description := SalesLine.Description;
          ProdOrderLine."Description 2" := SalesLine."Description 2";
          SalesLine.CALCFIELDS("Reserved Qty. (Base)");
          ProdOrderLine.VALIDATE("Quantity (Base)",SalesLine."Outstanding Qty. (Base)" - SalesLine."Reserved Qty. (Base)");
          ProdOrderLine."Location Code" := SalesLine."Location Code";
          ProdOrderLine."Bin Code" := SalesLine."Bin Code";
          ProdOrderLine."Due Date" := SalesLine."Shipment Date";
          ProdOrderLine."Ending Date" :=
            LeadTimeMgt.PlannedEndingDate(
              ProdOrderLine."Item No.",
              ProdOrderLine."Location Code",
              '',
              ProdOrderLine."Due Date",
              '',
              2);
          ProdOrderLine.VALIDATE("Ending Date");

          InsertProdOrderLine;

          IF SalesLine."Document Type" = SalesLine."Document Type"::Order THEN BEGIN // Not simulated
            ReserveSalesLine.SetBinding(ReservEntry.Binding::"Order-to-Order");
            ProdOrderLine.CALCFIELDS("Reserved Qty. (Base)");
            ReserveSalesLine.CreateReservationSetFrom(
              DATABASE::"Prod. Order Line",
              ProdOrderLine.Status,
              ProdOrderLine."Prod. Order No.",
              '',
              ProdOrderLine."Line No.",0,
              ProdOrderLine."Variant Code",
              ProdOrderLine."Location Code",'','',
              ProdOrderLine."Qty. per Unit of Measure");
            ReserveSalesLine.CreateReservation(
              SalesLine,
              ProdOrderLine.Description,
              ProdOrderLine."Ending Date",
              ProdOrderLine."Remaining Qty. (Base)" - ProdOrderLine."Reserved Qty. (Base)",
              '','');
          END;
          CopyDimFromSalesLine(SalesLine,ProdOrderLine);
        UNTIL (SalesPlanLine.NEXT = 0);
    END;

    LOCAL PROCEDURE InitProdOrderLine@2();
    BEGIN
      ProdOrderLine.INIT;
      ProdOrderLine.Status := ProdOrder.Status;
      ProdOrderLine."Prod. Order No." := ProdOrder."No.";
      ProdOrderLine."Line No." := NextProdOrderLineNo;
      ProdOrderLine."Routing Reference No." := ProdOrderLine."Line No.";
      ProdOrderLine.VALIDATE("Item No.",Item."No.");
      ProdOrderLine."Location Code" := ProdOrder."Location Code";
      ProdOrderLine."Shortcut Dimension 1 Code" := ProdOrder."Shortcut Dimension 1 Code";
      ProdOrderLine."Shortcut Dimension 2 Code" := ProdOrder."Shortcut Dimension 2 Code";
      IF ProdOrder."Bin Code" <> '' THEN
        ProdOrderLine."Bin Code" := ProdOrder."Bin Code"
      ELSE
        ProdOrderLine.VALIDATE("Location Code");
      ProdOrderLine."Scrap %" := Item."Scrap %";
      ProdOrderLine."Due Date" := ProdOrder."Due Date";
      ProdOrderLine."Starting Date" := ProdOrder."Starting Date";
      ProdOrderLine."Starting Time" := ProdOrder."Starting Time";
      ProdOrderLine."Ending Date" := ProdOrder."Ending Date";
      ProdOrderLine."Ending Time" := ProdOrder."Ending Time";
      ProdOrderLine."Planning Level Code" := 0;
      ProdOrderLine."Inventory Posting Group" := Item."Inventory Posting Group";
      ProdOrderLine.UpdateDatetime;
      ProdOrderLine.VALIDATE("Unit Cost");

      NextProdOrderLineNo := NextProdOrderLineNo + 10000;
    END;

    LOCAL PROCEDURE InsertProdOrderLine@4() : Boolean;
    VAR
      ProdOrderLine3@1000 : Record 5406;
    BEGIN
      ProdOrderLine3 := ProdOrderLine;
      ProdOrderLine3.SETRANGE(Status,ProdOrderLine.Status);
      ProdOrderLine3.SETRANGE("Prod. Order No.",ProdOrderLine."Prod. Order No.");
      ProdOrderLine3.SETRANGE("Item No.",ProdOrderLine."Item No.");
      ProdOrderLine3.SETRANGE("Variant Code",ProdOrderLine."Variant Code");

      IF (NOT InsertNew) AND ProdOrderLine3.FINDFIRST THEN BEGIN
        CopyProdOrderCompToTemp(ProdOrderLine3);
        ProdOrderLine3.VALIDATE(Quantity,ProdOrderLine3.Quantity + ProdOrderLine.Quantity);
        AdjustDateAndTime(ProdOrderLine3,ProdOrderLine."Due Date",ProdOrderLine."Ending Date",ProdOrderLine."Ending Time");

        IF ProdOrderLine3."Planning Level Code" < ProdOrderLine."Planning Level Code" THEN BEGIN
          ProdOrderLine3."Planning Level Code" := ProdOrderLine."Planning Level Code";
          UpdateCompPlanningLevel(ProdOrderLine3);
        END;

        ProdOrderLine3.MODIFY;
        ProdOrderLine := ProdOrderLine3;
        EXIT(FALSE);
      END ELSE BEGIN
        ProdOrderLine.INSERT;
        EXIT(TRUE);
      END;
    END;

    LOCAL PROCEDURE ProcessProdOrderLines@7(Direction@1000 : 'Forward,Backward');
    BEGIN
      ProdOrderLine.SETRANGE(Status,ProdOrder.Status);
      ProdOrderLine.SETRANGE("Prod. Order No.",ProdOrder."No.");

      IF ProdOrderLine.FINDSET(TRUE) THEN
        REPEAT
          CalcProdOrder.Calculate(ProdOrderLine,Direction,TRUE,TRUE,TRUE);
        UNTIL ProdOrderLine.NEXT = 0;
    END;

    LOCAL PROCEDURE CheckMultiLevelStructure@6(Direction@1000 : 'Forward,Backward';MultiLevel@1001 : Boolean);
    VAR
      MultiLevelStructureCreated@1002 : Boolean;
    BEGIN
      ProdOrderComp.SETCURRENTKEY(Status,"Prod. Order No.","Prod. Order Line No.","Item Low-Level Code");
      ProdOrderComp.SETRANGE(Status,ProdOrder.Status);
      ProdOrderComp.SETRANGE("Prod. Order No.",ProdOrder."No.");
      ProdOrderComp.SETFILTER("Planning Level Code",'>0');
      ProdOrderComp.SETFILTER("Item No.",'<>%1','');
      IF ProdOrderComp.FINDSET(TRUE) THEN
        REPEAT
          MultiLevelStructureCreated :=
            MultiLevelStructureCreated OR
            CheckMakeOrderLine(ProdOrderComp,ProdOrderLine,Direction,MultiLevel);
        UNTIL ProdOrderComp.NEXT = 0;
      IF MultiLevelStructureCreated THEN
        ReserveMultiLevelStructure(ProdOrderComp);
    END;

    PROCEDURE CheckMakeOrderLine@15(VAR ProdOrderComp@1000 : Record 5407;VAR ProdOrderLine@1001 : Record 5406;Direction@1002 : 'Forward,Backward';MultiLevel@1003 : Boolean) : Boolean;
    VAR
      ParentItem@1004 : Record 27;
      Inserted@1006 : Boolean;
    BEGIN
      ProdOrderLine2.GET(ProdOrderComp.Status,ProdOrderComp."Prod. Order No.",ProdOrderComp."Prod. Order Line No.");
      ParentItem.GET(ProdOrderLine2."Item No.");
      IF ParentItem."Manufacturing Policy" <> ParentItem."Manufacturing Policy"::"Make-to-Order" THEN
        EXIT(FALSE);

      Item.GET(ProdOrderComp."Item No.");
      IF Item."Replenishment System" <> Item."Replenishment System"::"Prod. Order" THEN
        EXIT(FALSE);

      IF (ParentItem."Manufacturing Policy" = ParentItem."Manufacturing Policy"::"Make-to-Order") AND
         (Item."Manufacturing Policy" = Item."Manufacturing Policy"::"Make-to-Order")
      THEN BEGIN
        InitProdOrderLine;
        ProdOrderLine.VALIDATE("Unit of Measure Code",ProdOrderComp."Unit of Measure Code");
        ProdOrderLine."Qty. per Unit of Measure" := ProdOrderComp."Qty. per Unit of Measure";
        ProdOrderLine."Variant Code" := ProdOrderComp."Variant Code";
        ProdOrderLine."Location Code" := ProdOrderComp."Location Code";
        ProdOrderLine."Bin Code" := ProdOrderComp."Bin Code";
        ProdOrderLine.Description := ProdOrderComp.Description;
        ProdOrderLine."Description 2" := Item."Description 2";
        ProdOrderComp.CALCFIELDS("Reserved Quantity");
        ProdOrderLine.VALIDATE(Quantity,ProdOrderComp."Expected Quantity" - ProdOrderComp."Reserved Quantity");
        IF ProdOrderLine."Quantity (Base)" = 0 THEN
          EXIT(FALSE);
        ProdOrderLine."Planning Level Code" := ProdOrderComp."Planning Level Code";
        ProdOrderLine."Due Date" := ProdOrderComp."Due Date";
        ProdOrderLine."Ending Date" := ProdOrderComp."Due Date";
        ProdOrderLine."Ending Time" := ProdOrderComp."Due Time";
        ProdOrderLine.UpdateDatetime;
        // this InsertNew is responsible for controlling if same POLine is added up or new POLine is created
        InsertNew := InsertNew AND (ProdOrderComp."Planning Level Code" > 1);
        Inserted := InsertProdOrderLine;
        IF MultiLevel THEN
          IF Inserted THEN
            CalcProdOrder.Calculate(ProdOrderLine,Direction::Backward,TRUE,TRUE,TRUE)
          ELSE BEGIN
            CalcProdOrder.Recalculate(ProdOrderLine,Direction::Backward);
            IF ProdOrderLine."Line No." < ProdOrderComp."Prod. Order Line No." THEN
              UpdateProdOrderLine(ProdOrderLine,Direction);
          END
        ELSE
          EXIT(FALSE);
        ProdOrderComp."Supplied-by Line No." := ProdOrderLine."Line No.";
        ProdOrderComp.MODIFY;
      END ELSE
        EXIT(FALSE);
      EXIT(TRUE);
    END;

    PROCEDURE ReserveMultiLevelStructure@9(VAR ProdOrderComp2@1000 : Record 5407);
    VAR
      ProdOrderComp3@1001 : Record 5407;
      ProdOrderLine3@1002 : Record 5406;
    BEGIN
      ProdOrderComp3.COPY(ProdOrderComp2);
      ProdOrderComp3.SETCURRENTKEY(Status,"Prod. Order No.","Prod. Order Line No.","Due Date");
      ProdOrderComp3.SETFILTER("Supplied-by Line No.",'<>0');
      IF ProdOrderComp3.FIND('+') THEN
        REPEAT
          IF ProdOrderLine3.GET(
               ProdOrderComp3.Status,ProdOrderComp3."Prod. Order No.",ProdOrderComp3."Supplied-by Line No.")
          THEN BEGIN
            ReserveProdOrderComp.SetBinding(ReservEntry.Binding::"Order-to-Order");
            ProdOrderComp3.CALCFIELDS("Reserved Qty. (Base)");
            ReserveProdOrderComp.CreateReservationSetFrom(
              DATABASE::"Prod. Order Line",
              ProdOrderLine3.Status,
              ProdOrderLine3."Prod. Order No.",'',
              ProdOrderLine3."Line No.",0,
              ProdOrderLine3."Variant Code",
              ProdOrderLine3."Location Code",'','',
              ProdOrderLine3."Qty. per Unit of Measure");
            ReserveProdOrderComp.CreateReservation(
              ProdOrderComp3,
              ProdOrderLine3.Description,
              ProdOrderLine3."Ending Date",
              ProdOrderComp3."Remaining Qty. (Base)" - ProdOrderComp3."Reserved Qty. (Base)",
              '','');
          END;
        UNTIL ProdOrderComp3.NEXT(-1) = 0;
    END;

    PROCEDURE CopyDimFromSalesLine@10(SalesLine@1000 : Record 37;VAR ProdOrderLine@1001 : Record 5406);
    VAR
      DocDim@1002 : Record 357;
      ProdDim@1003 : Record 358;
      DimMgt@1004 : Codeunit 408;
    BEGIN
      DocDim.SETRANGE("Table ID",DATABASE::"Sales Line");
      DocDim.SETRANGE("Document Type",SalesLine."Document Type");
      DocDim.SETRANGE("Document No.",SalesLine."Document No.");
      DocDim.SETRANGE("Line No.",SalesLine."Line No.");
      IF DocDim.FINDSET THEN BEGIN
        ProdDim.SETRANGE("Table ID",DATABASE::"Prod. Order Line");
        ProdDim.SETRANGE("Document Status",ProdOrderLine.Status);
        ProdDim.SETRANGE("Document No.",ProdOrderLine."Prod. Order No.");
        ProdDim.SETRANGE("Document Line No.",ProdOrderLine."Line No.");
        ProdDim.SETRANGE("Line No.",0);
        ProdDim.DELETEALL;

        ProdDim."Table ID" := DATABASE::"Prod. Order Line";
        ProdDim."Document Status" := ProdOrderLine.Status;
        ProdDim."Document No." := ProdOrderLine."Prod. Order No.";
        ProdDim."Document Line No." := ProdOrderLine."Line No.";
        ProdDim."Line No." := 0;

        REPEAT
          ProdDim."Dimension Code" := DocDim."Dimension Code";
          ProdDim."Dimension Value Code" := DocDim."Dimension Value Code";
          ProdDim.INSERT;
          CASE DimMgt.GlobalDimNo(DocDim."Dimension Code") OF
            1:
              ProdOrderLine."Shortcut Dimension 1 Code" := DocDim."Dimension Value Code";
            2:
              ProdOrderLine."Shortcut Dimension 2 Code" := DocDim."Dimension Value Code";
          END;
        UNTIL DocDim.NEXT = 0;
      END;
    END;

    PROCEDURE SetSalesLine@11(SalesLine2@1000 : Record 37);
    BEGIN
      SalesLine := SalesLine2;
      SalesLineIsSet := TRUE;
    END;

    LOCAL PROCEDURE AdjustDateAndTime@12(VAR ProdOrderLine3@1000 : Record 5406;DueDate@1001 : Date;EndingDate@1002 : Date;EndingTime@1003 : Time);
    BEGIN
      IF ProdOrderLine3."Due Date" > DueDate THEN
        ProdOrderLine3."Due Date" := DueDate;

      IF ProdOrderLine3."Ending Date" > EndingDate THEN BEGIN
        ProdOrderLine3."Ending Date" := EndingDate;
        ProdOrderLine3."Ending Time" := EndingTime;
      END ELSE
        IF (ProdOrderLine3."Ending Date" = EndingDate) AND
           (ProdOrderLine3."Ending Time" > EndingTime)
        THEN
          ProdOrderLine3."Ending Time" := EndingTime;
      ProdOrderLine3.UpdateDatetime;
    END;

    LOCAL PROCEDURE UpdateCompPlanningLevel@17(ProdOrderLine3@1000 : Record 5406);
    VAR
      ProdOrderComp3@1001 : Record 5407;
    BEGIN
      // update planning level code of component
      ProdOrderComp3.SETRANGE(Status,ProdOrderLine3.Status);
      ProdOrderComp3.SETRANGE("Prod. Order No.",ProdOrderLine3."Prod. Order No.");
      ProdOrderComp3.SETRANGE("Prod. Order Line No.",ProdOrderLine3."Line No.");
      ProdOrderComp3.SETFILTER("Planning Level Code",'>0');
      IF ProdOrderComp3.FINDSET(TRUE) THEN
        REPEAT
          ProdOrderComp3."Planning Level Code" := ProdOrderLine3."Planning Level Code" + 1;
          ProdOrderComp3.MODIFY;
        UNTIL ProdOrderComp3.NEXT = 0;
    END;

    LOCAL PROCEDURE UpdateProdOrderLine@13(VAR ProdOrderLine@1000 : Record 5406;Direction@1003 : 'Forward,Backward');
    VAR
      ProdOrderLine3@1002 : Record 5406;
      ProdOrderComp3@1001 : Record 5407;
    BEGIN
      ProdOrderComp3.SETRANGE(Status,ProdOrderLine.Status);
      ProdOrderComp3.SETRANGE("Prod. Order No.",ProdOrderLine."Prod. Order No.");
      ProdOrderComp3.SETRANGE("Prod. Order Line No.",ProdOrderLine."Line No.");
      IF ProdOrderComp3.FINDSET THEN
        REPEAT
          ProdOrderLine3.COPYFILTERS(ProdOrderLine);
          ProdOrderLine3.SETRANGE("Item No.",ProdOrderComp3."Item No.");
          ProdOrderLine3.SETRANGE("Variant Code",ProdOrderComp3."Variant Code");
          IF ProdOrderLine3.FINDFIRST THEN BEGIN
            ProdOrderComp3.CALCFIELDS("Reserved Quantity");
            TempOldProdOrderComp.GET(ProdOrderComp3.Status,ProdOrderComp3."Prod. Order No.",
              ProdOrderComp3."Prod. Order Line No.",ProdOrderComp3."Line No.");
            ProdOrderLine3.VALIDATE(Quantity,
              ProdOrderLine3.Quantity - TempOldProdOrderComp."Expected Quantity" +
              ProdOrderComp3."Expected Quantity" - ProdOrderComp3."Reserved Quantity");
            IF ProdOrderLine3."Planning Level Code" < ProdOrderComp3."Planning Level Code" THEN
              ProdOrderLine3."Planning Level Code" := ProdOrderComp3."Planning Level Code";
            AdjustDateAndTime(ProdOrderLine3,ProdOrderComp3."Due Date",ProdOrderComp3."Due Date",ProdOrderComp3."Due Time");
            UpdateCompPlanningLevel(ProdOrderLine3);
            CalcProdOrder.Recalculate(ProdOrderLine3,Direction::Backward);
            ProdOrderLine3.MODIFY;
          END;
        UNTIL ProdOrderComp3.NEXT = 0;
      TempOldProdOrderComp.DELETEALL;
    END;

    LOCAL PROCEDURE CopyProdOrderCompToTemp@14(ProdOrderLine3@1000 : Record 5406);
    VAR
      ProdOrderComp2@1001 : Record 5407;
    BEGIN
      TempOldProdOrderComp.DELETEALL;
      ProdOrderComp2.SETRANGE(Status,ProdOrderLine3.Status);
      ProdOrderComp2.SETRANGE("Prod. Order No.",ProdOrderLine3."Prod. Order No.");
      ProdOrderComp2.SETRANGE("Prod. Order Line No.",ProdOrderLine3."Line No.");
      IF ProdOrderComp2.FINDSET THEN
        REPEAT
          TempOldProdOrderComp := ProdOrderComp2;
          TempOldProdOrderComp.INSERT;
        UNTIL ProdOrderComp2.NEXT = 0;
    END;

    BEGIN
    END.
  }
}
