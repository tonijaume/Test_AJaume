OBJECT Codeunit 7305 Whse. Jnl.-B.Register
{
  OBJECT-PROPERTIES
  {
    Date=26/07/02;
    Time=12:00:00;
    Version List=NAVW13.60;
  }
  PROPERTIES
  {
    TableNo=7310;
    OnRun=BEGIN
            WhseJnlBatch.COPY(Rec);
            Code;
            Rec.COPY(WhseJnlBatch);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to register the journals?;ESP=Confirma que desea registrar los diarios?';
      Text001@1001 : TextConst 'ENU=The journals were successfully registered.;ESP=Se han registrado los diarios.';
      Text002@1002 : TextConst 'ENU="It was not possible to register all of the journals. ";ESP="No fue posible registrar todos los diarios. "';
      Text003@1003 : TextConst 'ENU=The journals that were not successfully registered are now marked.;ESP=Los diarios que no se han podido registrar, aparecen marcados.';
      WhseJnlTemplate@1004 : Record 7309;
      WhseJnlBatch@1005 : Record 7310;
      WhseJnlLine@1006 : Record 7311;
      WhseJnlRegisterBatch@1007 : Codeunit 7304;
      JnlWithErrors@1008 : Boolean;

    LOCAL PROCEDURE Code@1();
    BEGIN
      WITH WhseJnlBatch DO BEGIN
        WhseJnlTemplate.GET("Journal Template Name");
        WhseJnlTemplate.TESTFIELD("Force Registering Report",FALSE);

        IF NOT CONFIRM(Text000,FALSE) THEN
          EXIT;

        FIND('-');
        REPEAT
          WhseJnlLine."Journal Template Name" := "Journal Template Name";
          WhseJnlLine."Journal Batch Name" := Name;
          WhseJnlLine."Location Code" := WhseJnlBatch."Location Code";
          WhseJnlLine."Line No." := 10000;
          CLEAR(WhseJnlRegisterBatch);
          IF WhseJnlRegisterBatch.RUN(WhseJnlLine) THEN
            MARK(FALSE)
          ELSE BEGIN
            MARK(TRUE);
            JnlWithErrors := TRUE;
          END;
        UNTIL NEXT = 0;

        IF NOT JnlWithErrors THEN
          MESSAGE(Text001)
        ELSE
          MESSAGE(
            Text002 +
            Text003);

        IF NOT FIND('=><') THEN BEGIN
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Location Code","Location Code");
          FILTERGROUP(0);
          Name := '';
        END;
      END;
    END;

    BEGIN
    END.
  }
}
