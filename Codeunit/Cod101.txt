OBJECT Codeunit 101 Cust. Entry-SetAppl.ID
{
  OBJECT-PROPERTIES
  {
    Date=13/02/07;
    Time=12:00:00;
    Version List=NAVW15.00,NAVES4.00;
  }
  PROPERTIES
  {
    Permissions=TableData 21=imd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text1100100@1100001 : TextConst 'ENU=%1 cannot be applied, since it is included in a bill group.;ESP=%1 no se puede liquidar, ya que est  incluido en una remesa.';
      Text1100101@1100002 : TextConst 'ENU=" Remove it from its bill group and try again.";ESP=" B¢rrelo de la remesa e int‚ntelo de nuevo."';
      ApplicationManagement@1100004 : Codeunit 1;
      Doc@1100000 : Record 7000002;
      CustEntryApplID@1000 : Code[20];

    PROCEDURE SetApplId@1(VAR CustLedgEntry@1000 : Record 21;ApplyingCustLedgEntry@1003 : Record 21;AppliedAmount@1004 : Decimal;PmtDiscAmount@1002 : Decimal;AppliesToID@1001 : Code[20]);
    BEGIN
      CustLedgEntry.LOCKTABLE;
      IF CustLedgEntry.FIND('-') THEN BEGIN
        // Make Applies-to ID
        IF CustLedgEntry."Applies-to ID" <> '' THEN
          CustEntryApplID := ''
        ELSE BEGIN
          CustEntryApplID := AppliesToID;
          IF CustEntryApplID = '' THEN BEGIN
            CustEntryApplID := USERID;
            IF CustEntryApplID = '' THEN
              CustEntryApplID := '***';
          END;
        END;

        // Set Applies-to ID
        REPEAT
          CustLedgEntry.TESTFIELD(Open,TRUE);
          IF CustLedgEntry."Document Situation" = CustLedgEntry."Document Situation"::"Posted BG/PO" THEN
            ERROR(
              Text1100100,
              CustLedgEntry.Description);
          IF (((CustLedgEntry."Document Type" = CustLedgEntry."Document Type"::Bill) OR
             (CustLedgEntry."Document Type" = CustLedgEntry."Document Type"::Invoice))) AND
             ApplicationManagement.AccessToCartera THEN
            IF Doc.GET(Doc.Type::Receivable,CustLedgEntry."Entry No.") THEN
              IF Doc."Bill Gr./Pmt. Order No." <> '' THEN
                ERROR(
                  Text1100100 +
                  Text1100101,
                  CustLedgEntry.Description);

          CustLedgEntry."Applies-to ID" := CustEntryApplID;
          IF CustLedgEntry."Applies-to ID" = '' THEN BEGIN
            CustLedgEntry."Accepted Pmt. Disc. Tolerance" := FALSE;
            CustLedgEntry."Accepted Payment Tolerance" := 0;
          END;
          // Set Amount to Apply
          IF ((CustLedgEntry."Amount to Apply" <> 0) AND (CustEntryApplID = '')) OR
            (CustEntryApplID = '')
          THEN
            CustLedgEntry."Amount to Apply" := 0
          ELSE
            IF CustLedgEntry."Amount to Apply" = 0 THEN BEGIN
              CustLedgEntry.CALCFIELDS("Remaining Amount");
              CustLedgEntry."Amount to Apply" := CustLedgEntry."Remaining Amount"
            END;

          IF CustLedgEntry."Entry No." = ApplyingCustLedgEntry."Entry No." THEN
            CustLedgEntry."Applying Entry" := ApplyingCustLedgEntry."Applying Entry";
          CustLedgEntry.MODIFY;
        UNTIL CustLedgEntry.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}
