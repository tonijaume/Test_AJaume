OBJECT Codeunit 5055250 LiqJournalManagment
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=LIQEM6.00;
  }
  PROPERTIES
  {
    Permissions=TableData 206=imd,
                TableData 236=imd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text1140000@1140000 : TextConst 'ENU=Liquidity;ESP=Liquidez';
      Text1140001@1140001 : TextConst 'ENU=Liquidity Journals;ESP=Diarios liquidez';
      Text1140002@1140002 : TextConst 'ENU=RECURR.;ESP=PERIàD.';
      Text1140003@1140003 : TextConst 'ENU=Recurr. Liquidity Journal;ESP=Diario peri¢dico liquidez';
      Text1140004@1140004 : TextConst 'ENU=DEFAULT;ESP=GENERICO';
      Text1140005@1140005 : TextConst 'ENU=Default Journal;ESP=Diario gen‚rico';
      OldLiqNo@1140006 : Code[20];
      OldLiqAccNo@1140007 : Code[20];
      OpenFromBatch@1140008 : Boolean;

    PROCEDURE TemplateSelection@1140000(FormID@1140000 : Integer;RecurringJnl@1140001 : Boolean;VAR LiqJnlLine@1140002 : Record 5055256;VAR JnlSelected@1140003 : Boolean);
    VAR
      LiqJnlTemplate@1140004 : Record 5055254;
    BEGIN
      JnlSelected := TRUE;

      LiqJnlTemplate.RESET;
      IF NOT OpenFromBatch THEN
        LiqJnlTemplate.SETRANGE("Form ID", FormID);
      LiqJnlTemplate.SETRANGE(Recurring,RecurringJnl);

      CASE LiqJnlTemplate.COUNT OF
        0:
          BEGIN
            LiqJnlTemplate.INIT;
            LiqJnlTemplate.Recurring := RecurringJnl;
            IF NOT RecurringJnl THEN BEGIN
              LiqJnlTemplate.Name := Text1140000;
              LiqJnlTemplate.Description := Text1140001;
            END ELSE BEGIN
              LiqJnlTemplate.Name := Text1140002;
              LiqJnlTemplate.Description := Text1140003;
            END;
            LiqJnlTemplate.VALIDATE("Form ID");
            LiqJnlTemplate.INSERT;
            COMMIT;
          END;
        1:
          LiqJnlTemplate.FIND('-');
        ELSE
          JnlSelected := FORM.RUNMODAL(0,LiqJnlTemplate) = ACTION::LookupOK;
      END;
      IF JnlSelected THEN BEGIN
        LiqJnlLine.FILTERGROUP := 2;
        LiqJnlLine.SETRANGE("Journal Template Name",LiqJnlTemplate.Name);
        LiqJnlLine.FILTERGROUP := 0;
        IF OpenFromBatch THEN BEGIN
          LiqJnlLine."Journal Template Name" := '';
          FORM.RUN(LiqJnlTemplate."Form ID",LiqJnlLine);
        END;
      END;
    END;

    PROCEDURE TemplateSelectionFromBatch@1140010(VAR LiquidityJnlBatch@1140000 : Record 5055255);
    VAR
      LiqJnlLine@1140001 : Record 5055256;
      JnlSelected@1140002 : Boolean;
    BEGIN
      OpenFromBatch := TRUE;
      LiquidityJnlBatch.CALCFIELDS(Recurring);
      LiqJnlLine."Journal Batch Name" := LiquidityJnlBatch.Name;
      TemplateSelection(0,LiquidityJnlBatch.Recurring,LiqJnlLine,JnlSelected);
    END;

    PROCEDURE OpenJnl@1140001(VAR CurrentJnlBatchName@1140000 : Code[10];VAR LiqJnlLine@1140001 : Record 5055256);
    BEGIN
      CheckTemplateName(LiqJnlLine.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName);
      LiqJnlLine.FILTERGROUP := 2;
      LiqJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      LiqJnlLine.FILTERGROUP := 0;
    END;

    PROCEDURE OpenJnlBatch@1140012(VAR LiquidityJnlBatch@1140000 : Record 5055255);
    VAR
      CopyOfLiquidityJnlBatch@1140001 : Record 5055255;
      LiqJnlTemplate@1140002 : Record 5055254;
    BEGIN
      CopyOfLiquidityJnlBatch := LiquidityJnlBatch;
      IF NOT LiquidityJnlBatch.FIND('-') THEN BEGIN
        IF LiqJnlTemplate.FIND('-') THEN
          CheckTemplateName(LiqJnlTemplate.Name,LiquidityJnlBatch.Name);
        LiqJnlTemplate.SETRANGE(Recurring,TRUE);
        IF LiqJnlTemplate.FIND('-') THEN
          CheckTemplateName(LiqJnlTemplate.Name,LiquidityJnlBatch.Name);
        LiqJnlTemplate.SETRANGE(Recurring);

        IF LiquidityJnlBatch.FIND('-') THEN;
          CopyOfLiquidityJnlBatch := LiquidityJnlBatch;
      END;
      LiquidityJnlBatch := CopyOfLiquidityJnlBatch;
    END;

    PROCEDURE CheckTemplateName@1140002(CurrentJnlTemplateName@1140000 : Code[10];VAR CurrentJnlBatchName@1140001 : Code[10]);
    VAR
      LiqJnlName@1140002 : Record 5055255;
    BEGIN
      LiqJnlName.SETRANGE("Journal Template Name",CurrentJnlTemplateName);
      IF NOT LiqJnlName.GET(CurrentJnlTemplateName,CurrentJnlBatchName) THEN BEGIN
        IF NOT LiqJnlName.FIND('-') THEN BEGIN
          LiqJnlName.INIT;
          LiqJnlName."Journal Template Name" := CurrentJnlTemplateName;
          LiqJnlName.SetupNewBatch;
          LiqJnlName.Name := Text1140004;
          LiqJnlName.Description := Text1140005;
          LiqJnlName.INSERT(TRUE);
          COMMIT;
        END;
        CurrentJnlBatchName := LiqJnlName.Name;
      END;
    END;

    PROCEDURE CheckName@1140003(CurrentJnlBatchName@1140000 : Code[10];VAR LiqJnlLine@1140001 : Record 5055256);
    VAR
      LiqJnlName@1140002 : Record 5055255;
    BEGIN
      LiqJnlName.GET(LiqJnlLine.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName);
    END;

    PROCEDURE SetName@1140004(CurrentJnlBatchName@1140000 : Code[10];VAR LiqJnlLine@1140001 : Record 5055256);
    BEGIN
      LiqJnlLine.FILTERGROUP := 2;
      LiqJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      LiqJnlLine.FILTERGROUP :=  0;
      IF LiqJnlLine.FIND('-') THEN;
    END;

    PROCEDURE LookupName@1140005(VAR CurrentJnlBatchName@1140000 : Code[10];VAR LiqJnlLine@1140001 : Record 5055256) : Boolean;
    VAR
      LiqJnlName@1140002 : Record 5055255;
    BEGIN
      COMMIT;
      LiqJnlName."Journal Template Name" := LiqJnlLine.GETRANGEMAX("Journal Template Name");
      LiqJnlName.Name := LiqJnlLine.GETRANGEMAX("Journal Batch Name");
      LiqJnlName.FILTERGROUP := 2;
      LiqJnlName.SETRANGE("Journal Template Name",LiqJnlName."Journal Template Name");
      LiqJnlName.FILTERGROUP :=  0;
      IF FORM.RUNMODAL(0,LiqJnlName) = ACTION::LookupOK THEN BEGIN
        CurrentJnlBatchName := LiqJnlName.Name;
        SetName(CurrentJnlBatchName,LiqJnlLine);
      END;
    END;

    PROCEDURE GetLiq@1140006(LiqNo@1140000 : Code[20];VAR LiqName@1140001 : Text[50]);
    VAR
      Liq@1140002 : Record 5055250;
    BEGIN
      IF LiqNo <> OldLiqNo THEN BEGIN
        LiqName := '';
        IF LiqNo <> '' THEN
          IF Liq.GET(LiqNo) THEN
            LiqName := Liq.Description;
        OldLiqNo := LiqNo;
      END;
    END;

    PROCEDURE GetLiqAcc@1140007(LiqAccNo@1140000 : Code[20];VAR LiqAccName@1140001 : Text[30]);
    VAR
      LiqAcc@1140002 : Record 5055251;
    BEGIN
      IF LiqAccNo <> OldLiqAccNo THEN BEGIN
        LiqAccName := '';
        IF LiqAccNo <> '' THEN
          IF LiqAcc.GET(LiqAccNo) THEN
            LiqAccName := LiqAcc.Name;
        OldLiqAccNo := LiqAccNo;
      END;
    END;

    BEGIN
    END.
  }
}
