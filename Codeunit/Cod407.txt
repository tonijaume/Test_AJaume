OBJECT Codeunit 407 Adjust Gen. Journal Balance
{
  OBJECT-PROPERTIES
  {
    Date=08/06/01;
    Time=12:00:00;
    Version List=NAVW13.00;
  }
  PROPERTIES
  {
    TableNo=81;
    OnRun=VAR
            GenJnlLine@1000 : Record 81;
            PrevGenJnlLine@1001 : Record 81;
            CorrectionEntry@1002 : Boolean;
            TotalAmountLCY@1003 : Decimal;
          BEGIN
            TempCurrTotalBuffer.DELETEALL;
            GenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
            GenJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");

            TempJnlLineDim[1].RESET;
            TempJnlLineDim[1].DELETEALL;

            JnlLineDim.SETRANGE("Table ID",DATABASE::"Gen. Journal Line");
            JnlLineDim.SETRANGE("Journal Template Name","Journal Template Name");
            JnlLineDim.SETRANGE("Journal Batch Name","Journal Batch Name");

            WITH GenJnlLine DO BEGIN
              IF NOT FIND('-') THEN
                EXIT;
              PrevGenJnlLine := GenJnlLine;
              CorrectionEntry := TRUE;
              REPEAT
                JnlLineDim.SETRANGE("Journal Line No.",GenJnlLine."Line No.");
                DimMgt.CopyJnlLineDimToJnlLineDim(JnlLineDim,TempJnlLineDim[1]);
                IF ("Posting Date" <> PrevGenJnlLine."Posting Date") OR
                   ("Document No." <> PrevGenJnlLine."Document No.")
                THEN BEGIN
                  IF CheckCurrBalance AND (TotalAmountLCY <> 0) THEN BEGIN
                    PrevGenJnlLine.Correction := CorrectionEntry;
                    InsertCorrectionLines(GenJnlLine,PrevGenJnlLine);
                  END;
                  TotalAmountLCY := 0;
                  TempCurrTotalBuffer.DELETEALL;
                  CorrectionEntry := TRUE;
                  PrevGenJnlLine := GenJnlLine;
                END;
                TotalAmountLCY := TotalAmountLCY + "Amount (LCY)";
                IF "Bal. Account No." = '' THEN BEGIN
                  IF TempCurrTotalBuffer.GET("Currency Code") THEN BEGIN
                    TempCurrTotalBuffer."Total Amount" :=
                      TempCurrTotalBuffer."Total Amount" + Amount;
                    TempCurrTotalBuffer."Total Amount (LCY)" :=
                      TempCurrTotalBuffer."Total Amount (LCY)" + "Amount (LCY)";
                    TempCurrTotalBuffer.MODIFY;
                  END ELSE BEGIN
                    TempCurrTotalBuffer."Currency Code" := "Currency Code";
                    TempCurrTotalBuffer."Total Amount" := Amount;
                    TempCurrTotalBuffer."Total Amount (LCY)" := "Amount (LCY)";
                    TempCurrTotalBuffer.INSERT;
                  END;
                  CorrectionEntry := CorrectionEntry AND Correction;
                END;

                IF "Document Type" <> PrevGenJnlLine."Document Type" THEN
                  "Document Type" := 0;
                IF "Business Unit Code" <> PrevGenJnlLine."Business Unit Code" THEN
                  "Business Unit Code" := '';
                IF "Reason Code" <> PrevGenJnlLine."Reason Code" THEN
                  "Reason Code" := '';
                IF "Recurring Method" <> PrevGenJnlLine."Recurring Method" THEN
                  "Recurring Method" := 0;
                IF "Recurring Frequency" <> PrevGenJnlLine."Recurring Frequency" THEN
                  EVALUATE("Recurring Frequency",'<>');

                IF GenJnlLine."Line No." <> PrevGenJnlLine."Line No." THEN BEGIN
                  TempJnlLineDim[1].SETRANGE("Journal Line No.","Line No.");
                  IF TempJnlLineDim[1].FIND('-') THEN
                    REPEAT
                      IF NOT TempJnlLineDim[2].GET(
                        TempJnlLineDim[1]."Table ID",TempJnlLineDim[1]."Journal Template Name",
                        TempJnlLineDim[1]."Journal Batch Name",PrevGenJnlLine."Line No.",
                        0,TempJnlLineDim[1]."Dimension Code")
                      THEN
                        TempJnlLineDim[1].DELETE
                      ELSE
                        IF TempJnlLineDim[1]."Dimension Value Code" <> TempJnlLineDim[2]."Dimension Value Code" THEN
                          TempJnlLineDim[1].DELETE;
                    UNTIL TempJnlLineDim[1].NEXT = 0;
                END;

                PrevGenJnlLine := GenJnlLine;
              UNTIL NEXT = 0;

              CLEAR(PrevGenJnlLine);

              IF CheckCurrBalance AND (TotalAmountLCY <> 0) THEN BEGIN
                Correction := CorrectionEntry;
                InsertCorrectionLines(PrevGenJnlLine,GenJnlLine);
              END;
            END;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=The program cannot find a key between line number %1 and line number %2.;ESP=El sistema no puede encontrar una clave entre la l¡nea No. %1 y la No. %2';
      Text002@1001 : TextConst 'ENU=Rounding correction for %1;ESP=Correci¢n redondeo para %1';
      TempCurrTotalBuffer@1002 : TEMPORARY Record 332;
      JnlLineDim@1003 : Record 356;
      TempJnlLineDim@1004 : ARRAY [2] OF TEMPORARY Record 356;
      DimMgt@1005 : Codeunit 408;

    PROCEDURE CheckCurrBalance@1() : Boolean;
    VAR
      InBalance@1000 : Boolean;
    BEGIN
      InBalance := TRUE;
      IF TempCurrTotalBuffer.FIND('-') THEN
        REPEAT
          InBalance := InBalance AND (TempCurrTotalBuffer."Total Amount" = 0)
        UNTIL (NOT InBalance) OR (TempCurrTotalBuffer.NEXT = 0);
      EXIT (InBalance);
    END;

    LOCAL PROCEDURE InsertCorrectionLines@8(VAR GenJnlLine2@1000 : Record 81;VAR PrevGenJnlLine2@1001 : Record 81);
    VAR
      Currency@1002 : Record 4;
      NewGenJnlLine@1003 : Record 81;
    BEGIN
      NewGenJnlLine := PrevGenJnlLine2;

      TempCurrTotalBuffer.SETFILTER("Currency Code",'<>%1','');
      TempCurrTotalBuffer.SETRANGE("Total Amount",0);

      IF TempCurrTotalBuffer.FIND('-') THEN
        REPEAT
          Currency.GET(TempCurrTotalBuffer."Currency Code");
          Currency.TESTFIELD("Conv. LCY Rndg. Debit Acc.");
          Currency.TESTFIELD("Conv. LCY Rndg. Credit Acc.");
          WITH NewGenJnlLine DO BEGIN
            INIT;
            IF GenJnlLine2."Line No." = 0 THEN
              "Line No." := "Line No." + 10000
            ELSE
              IF GenJnlLine2."Line No." >= "Line No." + 2 THEN
                "Line No." := ("Line No." + GenJnlLine2."Line No.") DIV 2
              ELSE
                ERROR(
                  STRSUBSTNO(
                    Text000,
                    PrevGenJnlLine2."Line No.",
                    GenJnlLine2."Line No."));
            "Document Type" := PrevGenJnlLine2."Document Type";
            "Account Type" := "Account Type"::"G/L Account";
            Correction := PrevGenJnlLine2.Correction;
            IF Correction XOR (TempCurrTotalBuffer."Total Amount (LCY)" <= 0) THEN
              VALIDATE("Account No.",Currency."Conv. LCY Rndg. Debit Acc.")
            ELSE
              VALIDATE("Account No.",Currency."Conv. LCY Rndg. Credit Acc.");
            "Posting Date" := PrevGenJnlLine2."Posting Date";
            "Document No." := PrevGenJnlLine2."Document No.";
            Description := STRSUBSTNO(Text002,TempCurrTotalBuffer."Currency Code");
            VALIDATE(Amount,-TempCurrTotalBuffer."Total Amount (LCY)");
            "Source Code" := PrevGenJnlLine2."Source Code";
            "Business Unit Code" := PrevGenJnlLine2."Business Unit Code";
            "Reason Code" := PrevGenJnlLine2."Reason Code";
            "Recurring Method" := PrevGenJnlLine2."Recurring Method";
            "Recurring Frequency" := PrevGenJnlLine2."Recurring Frequency";
            "Posting No. Series" := PrevGenJnlLine2."Posting No. Series";
            IF TempCurrTotalBuffer."Total Amount (LCY)" <> 0 THEN BEGIN
              INSERT;
              WITH TempJnlLineDim[1] DO BEGIN
                SETRANGE("Journal Line No.",PrevGenJnlLine2."Line No.");
                IF FIND('-') THEN
                  REPEAT
                    IF JnlLineDim.GET(
                      "Table ID","Journal Template Name","Journal Batch Name",NewGenJnlLine."Line No.",0,"Dimension Code")
                    THEN
                      JnlLineDim.DELETE;
                    JnlLineDim := TempJnlLineDim[1];
                    JnlLineDim."Journal Line No." := NewGenJnlLine."Line No.";
                    JnlLineDim.INSERT(TRUE);
                  UNTIL NEXT = 0;
              END;
            END;
          END;
        UNTIL TempCurrTotalBuffer.NEXT = 0;
    END;

    BEGIN
    END.
  }
}
