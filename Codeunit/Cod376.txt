OBJECT Codeunit 376 Check Entry Set Recon.-No.
{
  OBJECT-PROPERTIES
  {
    Date=08/06/01;
    Time=12:00:00;
    Version List=NAVW13.00;
  }
  PROPERTIES
  {
    Permissions=TableData 271=rm,
                TableData 272=rm;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot be %1;ESP=no puede ser %1';
      BankAccLedgEntry@1001 : Record 271;

    PROCEDURE ToggleReconNo@5(VAR CheckLedgEntry@1000 : Record 272;VAR BankAccReconLine@1001 : Record 274;VAR ChangeAmount@1002 : Boolean);
    BEGIN
      BankAccLedgEntry.LOCKTABLE;
      CheckLedgEntry.LOCKTABLE;
      BankAccReconLine.LOCKTABLE;
      BankAccReconLine.FIND;
      IF CheckLedgEntry."Statement No." = '' THEN BEGIN
        SetReconNo(CheckLedgEntry,BankAccReconLine);
        BankAccReconLine."Applied Amount" := BankAccReconLine."Applied Amount" - CheckLedgEntry.Amount;
        BankAccReconLine."Applied Entries" := BankAccReconLine."Applied Entries" + 1;
      END ELSE BEGIN
        RemoveReconNo(CheckLedgEntry,BankAccReconLine,TRUE);
        BankAccReconLine."Applied Amount" := BankAccReconLine."Applied Amount" + CheckLedgEntry.Amount;
        BankAccReconLine."Applied Entries" := BankAccReconLine."Applied Entries" - 1;
      END;
      IF BankAccReconLine."Applied Entries" = 1 THEN
        BankAccReconLine."Check No." := CheckLedgEntry."Check No."
      ELSE
        BankAccReconLine."Check No." := '';
      IF ChangeAmount THEN
        BankAccReconLine.VALIDATE("Statement Amount",BankAccReconLine."Applied Amount")
      ELSE
        BankAccReconLine.VALIDATE("Statement Amount");
      BankAccReconLine.MODIFY;
    END;

    PROCEDURE SetReconNo@2(VAR CheckLedgEntry@1000 : Record 272;VAR BankAccReconLine@1001 : Record 274);
    BEGIN
      CheckLedgEntry.TESTFIELD(Open,TRUE);
      IF (CheckLedgEntry."Statement Status" <> CheckLedgEntry."Statement Status"::Open) AND
         (CheckLedgEntry."Statement Status" <>
          CheckLedgEntry."Statement Status"::"Check Entry Applied")
      THEN
        CheckLedgEntry.FIELDERROR(
          "Statement Status",STRSUBSTNO(Text000,CheckLedgEntry."Statement Status"));
      CheckLedgEntry.TESTFIELD("Statement No.",'');
      CheckLedgEntry.TESTFIELD("Statement Line No.",0);
      IF NOT (CheckLedgEntry."Entry Status" IN
              [CheckLedgEntry."Entry Status"::Posted,CheckLedgEntry."Entry Status"::"Financially Voided"])
      THEN
        CheckLedgEntry.FIELDERROR(
          "Entry Status",STRSUBSTNO(Text000,CheckLedgEntry."Entry Status"));
      CheckLedgEntry.TESTFIELD("Bank Account No.",BankAccReconLine."Bank Account No.");
      CheckLedgEntry."Statement Status" := CheckLedgEntry."Statement Status"::"Check Entry Applied";
      CheckLedgEntry."Statement No." := BankAccReconLine."Statement No.";
      CheckLedgEntry."Statement Line No." := BankAccReconLine."Statement Line No.";
      CheckLedgEntry.MODIFY;

      BankAccLedgEntry.GET(CheckLedgEntry."Bank Account Ledger Entry No.");
      BankAccLedgEntry.TESTFIELD(Open,TRUE);
      IF (BankAccLedgEntry."Statement Status" <> BankAccLedgEntry."Statement Status"::Open) AND
         (BankAccLedgEntry."Statement Status" <>
          BankAccLedgEntry."Statement Status"::"Check Entry Applied")
      THEN
        BankAccLedgEntry.FIELDERROR(
          "Statement Status",STRSUBSTNO(Text000,BankAccLedgEntry."Statement Status"));
      BankAccLedgEntry.TESTFIELD("Statement No.",'');
      BankAccLedgEntry.TESTFIELD("Statement Line No.",0);
      BankAccLedgEntry."Statement Status" :=
        BankAccLedgEntry."Statement Status"::"Check Entry Applied";
      BankAccLedgEntry."Statement No." := '';
      BankAccLedgEntry."Statement Line No." := 0;
      BankAccLedgEntry.MODIFY;
    END;

    PROCEDURE RemoveReconNo@3(VAR CheckLedgEntry@1000 : Record 272;VAR BankAccReconLine@1001 : Record 274;Test@1002 : Boolean);
    VAR
      CheckLedgEntry2@1003 : Record 272;
    BEGIN
      CheckLedgEntry.TESTFIELD(Open,TRUE);
      IF Test THEN BEGIN
        CheckLedgEntry.TESTFIELD(
          "Statement Status",CheckLedgEntry."Statement Status"::"Check Entry Applied");
        CheckLedgEntry.TESTFIELD("Statement No.",BankAccReconLine."Statement No.");
        CheckLedgEntry.TESTFIELD("Statement Line No.",BankAccReconLine."Statement Line No.");
      END;
      CheckLedgEntry.TESTFIELD("Bank Account No.",BankAccReconLine."Bank Account No.");
      CheckLedgEntry."Statement Status" := CheckLedgEntry."Statement Status"::Open;
      CheckLedgEntry."Statement No." := '';
      CheckLedgEntry."Statement Line No." := 0;
      CheckLedgEntry.MODIFY;

      CheckLedgEntry2.RESET;
      CheckLedgEntry2.SETCURRENTKEY("Bank Account Ledger Entry No.");
      CheckLedgEntry2.SETRANGE(
        "Bank Account Ledger Entry No.",CheckLedgEntry."Bank Account Ledger Entry No.");
      CheckLedgEntry2.SETRANGE(
        "Statement Status",CheckLedgEntry."Statement Status"::"Check Entry Applied");
      IF NOT CheckLedgEntry2.FIND('-') THEN BEGIN
        BankAccLedgEntry.GET(CheckLedgEntry."Bank Account Ledger Entry No.");
        BankAccLedgEntry.TESTFIELD(Open,TRUE);
        IF Test THEN BEGIN
          BankAccLedgEntry.TESTFIELD(
            "Statement Status",BankAccLedgEntry."Statement Status"::"Check Entry Applied");
          BankAccLedgEntry.TESTFIELD("Statement No.",'');
          BankAccLedgEntry.TESTFIELD("Statement Line No.",0);
        END;
        BankAccLedgEntry."Statement Status" := BankAccLedgEntry."Statement Status"::Open;
        BankAccLedgEntry."Statement No." := '';
        BankAccLedgEntry."Statement Line No." := 0;
        BankAccLedgEntry.MODIFY;
      END;
    END;

    BEGIN
    END.
  }
}
