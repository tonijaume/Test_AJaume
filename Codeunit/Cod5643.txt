OBJECT Codeunit 5643 FA Reclass. Transfer Batch
{
  OBJECT-PROPERTIES
  {
    Date=08/06/01;
    Time=12:00:00;
    Version List=NAVW13.00;
  }
  PROPERTIES
  {
    TableNo=5624;
    Permissions=TableData 5623=imd;
    OnRun=BEGIN
            FAReclassJnlLine.COPY(Rec);
            Code;
            Rec := FAReclassJnlLine;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot exceed %1 characters;ESP=no puede superar %1 caracteres';
      Text001@1001 : TextConst 'ENU=Journal Batch Name    #1##########\\;ESP=Nombre secci¢n diario #1##########\\';
      Text002@1002 : TextConst 'ENU=Checking lines        #2######\;ESP=Comprobando l¡neas    #2######\';
      Text003@1003 : TextConst 'ENU=Posting lines         #3###### @4@@@@@@@@@@@@@;ESP=Registrando l¡neas    #3###### @4@@@@@@@@@@@@@';
      FAReclassJnlLine@1004 : Record 5624;
      FAReclassJnlTempl@1005 : Record 5622;
      FAReclassJnlBatch@1006 : Record 5623;
      FAReclassJnlLine2@1007 : Record 5624;
      FAReclassJnlLine3@1008 : Record 5624;
      FAReclassCheckLine@1009 : Codeunit 5641;
      FAReclassTransferLine@1010 : Codeunit 5642;
      Window@1011 : Dialog;
      OneFAReclassDone@1012 : Boolean;
      LineCounter@1013 : Integer;
      StartLineNo@1014 : Integer;
      NoOfRecords@1015 : Integer;

    PROCEDURE Code@1();
    BEGIN
      WITH FAReclassJnlLine DO BEGIN
        SETRANGE("Journal Template Name","Journal Template Name");
        SETRANGE("Journal Batch Name","Journal Batch Name");
        IF RECORDLEVELLOCKING THEN
          LOCKTABLE;

        FAReclassJnlTempl.GET("Journal Template Name");
        FAReclassJnlBatch.GET("Journal Template Name","Journal Batch Name");
        IF STRLEN(INCSTR(FAReclassJnlBatch.Name)) > MAXSTRLEN(FAReclassJnlBatch.Name) THEN
          FAReclassJnlBatch.FIELDERROR(
            Name,
            STRSUBSTNO(
              Text000,
              MAXSTRLEN(FAReclassJnlBatch.Name)));

        IF NOT FIND('=><') THEN BEGIN
          "Line No." := 0;
          EXIT;
        END;

        Window.OPEN(
          Text001 +
          Text002 +
          Text003);
        Window.UPDATE(1,"Journal Batch Name");

        LineCounter := 0;
        StartLineNo := "Line No.";
        REPEAT
          LineCounter := LineCounter + 1;
          Window.UPDATE(2,LineCounter);
          FAReclassCheckLine.RUN(FAReclassJnlLine);
          IF NEXT = 0 THEN
            FIND('-');
        UNTIL "Line No." = StartLineNo;
        NoOfRecords := LineCounter;

        LineCounter := 0;
        OneFAReclassDone := FALSE;
        SETCURRENTKEY("Journal Template Name","Journal Batch Name","FA Posting Date");
        FIND('-');
        REPEAT
          LineCounter := LineCounter + 1;
          Window.UPDATE(3,LineCounter);
          Window.UPDATE(4,ROUND(LineCounter / NoOfRecords * 10000,1));
          FAReclassTransferLine.FAReclassLine(FAReclassJnlLine,OneFAReclassDone);
        UNTIL NEXT = 0;

        INIT;
        IF OneFAReclassDone THEN
          "Line No." := 1
        ELSE
          "Line No." := 0;

        IF OneFAReclassDone THEN BEGIN
          IF NOT RECORDLEVELLOCKING THEN
            LOCKTABLE(TRUE,TRUE);
          FAReclassJnlLine2.COPYFILTERS(FAReclassJnlLine);
          FAReclassJnlLine2.SETFILTER("FA No.",'<>%1','');
          IF FAReclassJnlLine2.FIND('+') THEN; // Remember the last line
          DELETEALL;

          FAReclassJnlLine3.SETRANGE("Journal Template Name","Journal Template Name");
          FAReclassJnlLine3.SETRANGE("Journal Batch Name","Journal Batch Name");
          IF NOT FAReclassJnlLine3.FIND('+') THEN
            IF INCSTR("Journal Batch Name") <> '' THEN BEGIN
              FAReclassJnlBatch.GET("Journal Template Name","Journal Batch Name");
              FAReclassJnlBatch.DELETE;
              FAReclassJnlBatch.Name := INCSTR("Journal Batch Name");
              IF FAReclassJnlBatch.INSERT THEN;
              "Journal Batch Name" := FAReclassJnlBatch.Name;
            END;

          FAReclassJnlLine3.SETRANGE("Journal Batch Name","Journal Batch Name");
          IF NOT FAReclassJnlLine3.FIND('+') THEN BEGIN
            FAReclassJnlLine3.INIT;
            FAReclassJnlLine3."Journal Template Name" := "Journal Template Name";
            FAReclassJnlLine3."Journal Batch Name" := "Journal Batch Name";
            FAReclassJnlLine3."Line No." := 10000;
            FAReclassJnlBatch.GET("Journal Template Name","Journal Batch Name");
            FAReclassJnlLine3."Posting Date" := FAReclassJnlLine2."Posting Date";
            FAReclassJnlLine3.INSERT;
          END;
        END;

        COMMIT;
        CLEAR(FAReclassCheckLine);
        CLEAR(FAReclassTransferLine);
      END;
    END;

    BEGIN
    END.
  }
}
