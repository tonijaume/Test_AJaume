OBJECT Codeunit 5638 FAJnlManagement
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    Permissions=TableData 5619=imd,
                TableData 5620=imd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=ASSETS;ESP=ACTIVOS';
      Text001@1001 : TextConst 'ENU=Fixed Asset Journal;ESP=A/F Diario activos';
      Text002@1002 : TextConst 'ENU=Recurring;ESP=Peri¢dico';
      Text003@1003 : TextConst 'ENU=Recurring Fixed Asset Journal;ESP=Diario peri¢dico activos';
      Text004@1004 : TextConst 'ENU=DEFAULT;ESP=GENERICO';
      Text005@1005 : TextConst 'ENU=Default Journal;ESP=Diario gen‚rico';
      OldFANo@1006 : Code[20];
      OpenFromBatch@1007 : Boolean;

    PROCEDURE TemplateSelection@1(FormID@1004 : Integer;RecurringJnl@1000 : Boolean;VAR FAJnlLine@1003 : Record 5621;VAR JnlSelected@1002 : Boolean);
    VAR
      FAJnlTemplate@1001 : Record 5619;
    BEGIN
      JnlSelected := TRUE;

      FAJnlTemplate.RESET;
      IF NOT OpenFromBatch THEN
        FAJnlTemplate.SETRANGE("Form ID",FormID);
      FAJnlTemplate.SETRANGE(Recurring,RecurringJnl);

      CASE FAJnlTemplate.COUNT OF
        0:
          BEGIN
            FAJnlTemplate.INIT;
            FAJnlTemplate.Recurring := RecurringJnl;
            IF NOT RecurringJnl THEN BEGIN
              FAJnlTemplate.Name := Text000;
              FAJnlTemplate.Description := Text001;
            END ELSE BEGIN
              FAJnlTemplate.Name := Text002;
              FAJnlTemplate.Description := Text003;
            END;
            FAJnlTemplate.VALIDATE("Form ID");
            FAJnlTemplate.INSERT;
            COMMIT;
          END;
        1:
          FAJnlTemplate.FIND('-');
        ELSE
          JnlSelected := FORM.RUNMODAL(0,FAJnlTemplate) = ACTION::LookupOK;
      END;
      IF JnlSelected THEN BEGIN
        FAJnlLine.FILTERGROUP := 2;
        FAJnlLine.SETRANGE("Journal Template Name",FAJnlTemplate.Name);
        FAJnlLine.FILTERGROUP := 0;
        IF OpenFromBatch THEN BEGIN
          FAJnlLine."Journal Template Name" := '';
          FORM.RUN(FAJnlTemplate."Form ID",FAJnlLine);
        END;
      END;
    END;

    PROCEDURE TemplateSelectionFromBatch@10(VAR FAJnlBatch@1000 : Record 5620);
    VAR
      FAJnlLine@1002 : Record 5621;
      FAJnlTemplate@1003 : Record 5619;
      JnlSelected@1001 : Boolean;
    BEGIN
      OpenFromBatch := TRUE;
      FAJnlTemplate.GET(FAJnlBatch."Journal Template Name");
      FAJnlTemplate.TESTFIELD("Form ID");
      FAJnlBatch.TESTFIELD(Name);

      FAJnlLine.FILTERGROUP := 2;
      FAJnlLine.SETRANGE("Journal Template Name",FAJnlTemplate.Name);
      FAJnlLine.FILTERGROUP := 0;

      FAJnlLine."Journal Template Name" := '';
      FAJnlLine."Journal Batch Name" := FAJnlBatch.Name;
      FORM.RUN(FAJnlTemplate."Form ID",FAJnlLine);
    END;

    PROCEDURE OpenJournal@5(VAR CurrentJnlBatchName@1000 : Code[10];VAR FAJnlLine@1001 : Record 5621);
    BEGIN
      CheckTemplateName(FAJnlLine.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName);
      FAJnlLine.FILTERGROUP := 2;
      FAJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      FAJnlLine.FILTERGROUP := 0;
    END;

    PROCEDURE OpenJnlBatch@12(VAR FAJnlBatch@1000 : Record 5620);
    VAR
      FAJnlTemplate@1002 : Record 5619;
      FAJnlLine@1004 : Record 5621;
      JnlSelected@1003 : Boolean;
    BEGIN
      IF FAJnlBatch.GETFILTER("Journal Template Name") <> '' THEN
        EXIT;
      FAJnlBatch.FILTERGROUP(2);
      IF FAJnlBatch.GETFILTER("Journal Template Name") <> '' THEN BEGIN
        FAJnlBatch.FILTERGROUP(0);
        EXIT;
      END;
      FAJnlBatch.FILTERGROUP(0);

      IF NOT FAJnlBatch.FIND('-') THEN BEGIN
        IF NOT FAJnlTemplate.FIND('-') THEN
          TemplateSelection(0,FALSE,FAJnlLine,JnlSelected);
        IF FAJnlTemplate.FIND('-') THEN
          CheckTemplateName(FAJnlTemplate.Name,FAJnlBatch.Name);
        FAJnlTemplate.SETRANGE(Recurring,TRUE);
        IF NOT FAJnlTemplate.FIND('-') THEN
          TemplateSelection(0,TRUE,FAJnlLine,JnlSelected);
        IF FAJnlTemplate.FIND('-') THEN
          CheckTemplateName(FAJnlTemplate.Name,FAJnlBatch.Name);
        FAJnlTemplate.SETRANGE(Recurring);
      END;
      FAJnlBatch.FIND('-');
      JnlSelected := TRUE;
      FAJnlBatch.CALCFIELDS(Recurring);
      FAJnlTemplate.SETRANGE(Recurring,FAJnlBatch.Recurring);
      IF FAJnlBatch.GETFILTER("Journal Template Name") <> '' THEN
         FAJnlTemplate.SETRANGE(Name,FAJnlBatch.GETFILTER("Journal Template Name"));
      CASE FAJnlTemplate.COUNT OF
        1:
          FAJnlTemplate.FIND('-');
        ELSE
          JnlSelected := FORM.RUNMODAL(0,FAJnlTemplate) = ACTION::LookupOK;
      END;
      IF NOT JnlSelected THEN
        ERROR('');

      FAJnlBatch.FILTERGROUP(2);
      FAJnlBatch.SETRANGE("Journal Template Name",FAJnlTemplate.Name);
      FAJnlBatch.FILTERGROUP(0);
    END;

    PROCEDURE CheckName@4(CurrentJnlBatchName@1000 : Code[10];VAR FAJnlLine@1001 : Record 5621);
    VAR
      FAJnlBatch@1002 : Record 5620;
    BEGIN
      FAJnlBatch.GET(FAJnlLine.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName);
    END;

    PROCEDURE SetName@6(CurrentJnlBatchName@1000 : Code[10];VAR FAJnlLine@1001 : Record 5621);
    BEGIN
      FAJnlLine.FILTERGROUP := 2;
      FAJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      FAJnlLine.FILTERGROUP := 0;
      IF FAJnlLine.FIND('-') THEN;
    END;

    PROCEDURE LookupName@7(VAR CurrentJnlBatchName@1000 : Code[10];VAR FAJnlLine@1001 : Record 5621) : Boolean;
    VAR
      FAJnlBatch@1002 : Record 5620;
    BEGIN
      COMMIT;
      FAJnlBatch."Journal Template Name" := FAJnlLine.GETRANGEMAX("Journal Template Name");
      FAJnlBatch.Name := FAJnlLine.GETRANGEMAX("Journal Batch Name");
      FAJnlBatch.FILTERGROUP(2);
      FAJnlBatch.SETRANGE("Journal Template Name",FAJnlBatch."Journal Template Name");
      FAJnlBatch.FILTERGROUP(0);
      IF FORM.RUNMODAL(0,FAJnlBatch) = ACTION::LookupOK THEN BEGIN
        CurrentJnlBatchName := FAJnlBatch.Name;
        SetName(CurrentJnlBatchName,FAJnlLine);
      END;
    END;

    LOCAL PROCEDURE CheckTemplateName@3(CurrentJnlTemplateName@1000 : Code[10];VAR CurrentJnlBatchName@1001 : Code[10]);
    VAR
      FAJnlBatch@1002 : Record 5620;
    BEGIN
      IF NOT FAJnlBatch.GET(CurrentJnlTemplateName,CurrentJnlBatchName) THEN BEGIN
        FAJnlBatch.SETRANGE("Journal Template Name",CurrentJnlTemplateName);
        IF NOT FAJnlBatch.FIND('-') THEN BEGIN
          FAJnlBatch.INIT;
          FAJnlBatch."Journal Template Name" := CurrentJnlTemplateName;
          FAJnlBatch.SetupNewBatch;
          FAJnlBatch.Name := Text004;
          FAJnlBatch.Description := Text005;
          FAJnlBatch.INSERT(TRUE);
          COMMIT;
        END;
        CurrentJnlBatchName := FAJnlBatch.Name;
      END;
    END;

    PROCEDURE GetFA@2(FANo@1000 : Code[20];VAR FADescription@1001 : Text[30]);
    VAR
      FA@1002 : Record 5600;
    BEGIN
      IF FANo <> OldFANo THEN BEGIN
        FADescription := '';
        IF FANo <> '' THEN
          IF FA.GET(FANo) THEN
            FADescription := FA.Description;
        OldFANo := FANo;
      END;
    END;

    BEGIN
    END.
  }
}
