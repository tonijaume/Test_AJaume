OBJECT Codeunit 428 IC Mapping
{
  OBJECT-PROPERTIES
  {
    Date=15/09/04;
    Time=12:00:00;
    Version List=NAVW14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE MapAccounts@1(ICGLAcc@1002 : Record 410);
    VAR
      GlAcc@1001 : Record 15;
    BEGIN
      GlAcc.SETRANGE(GlAcc."No.",ICGLAcc."No.");
      IF GlAcc.FIND('-') AND (ICGLAcc."Account Type" = GlAcc."Account Type") THEN BEGIN
        ICGLAcc."Map-to G/L Acc. No." := GlAcc."No.";
        ICGLAcc.MODIFY;
      END;
    END;

    PROCEDURE MapIncomingICDimensions@2(ICDimension@1004 : Record 411);
    VAR
      Dimension@1001 : Record 348;
      DimensionValue@1003 : Record 349;
      ICDimensionValue@1000 : Record 412;
    BEGIN
      Dimension.SETRANGE(Dimension.Code,ICDimension.Code);
      IF Dimension.FIND('-') THEN BEGIN
        ICDimension."Map-to Dimension Code" := Dimension.Code;
        ICDimension.MODIFY;
        ICDimensionValue.SETRANGE(ICDimensionValue."Dimension Code",ICDimension.Code);
        IF ICDimensionValue.FIND('-') THEN BEGIN
          ICDimensionValue.MODIFYALL("Map-to Dimension Code",ICDimension."Map-to Dimension Code");
        IF ICDimensionValue.FIND('-') THEN
          REPEAT
            IF ICDimensionValue."Map-to Dimension Value Code" = '' THEN BEGIN
              DimensionValue.SETRANGE(DimensionValue.Code,ICDimensionValue.Code);
              IF DimensionValue.FIND('-') AND
                 (DimensionValue."Dimension Value Type" = ICDimensionValue."Dimension Value Type")
              THEN BEGIN
                ICDimensionValue."Map-to Dimension Value Code" := DimensionValue.Code;
                ICDimensionValue.MODIFY;
              END;
            END;
          UNTIL ICDimensionValue.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE MapOutgoingICDimensions@3(Dimension@1000 : Record 348);
    VAR
      ICDimension@1001 : Record 411;
      ICDimensionValue@1003 : Record 412;
      DimensionValue@1002 : Record 349;
    BEGIN
      ICDimension.SETRANGE(ICDimension.Code,Dimension.Code);
      IF ICDimension.FIND('-') THEN BEGIN
        Dimension."Map-to IC Dimension Code" := ICDimension.Code;
        Dimension.MODIFY;
        DimensionValue.SETRANGE(DimensionValue."Dimension Code",Dimension.Code);
        IF DimensionValue.FIND('-') THEN BEGIN
          DimensionValue.MODIFYALL("Map-to IC Dimension Code",Dimension."Map-to IC Dimension Code");
        IF DimensionValue.FIND('-') THEN
          REPEAT
            IF DimensionValue."Map-to IC Dimension Value Code" = '' THEN BEGIN
              ICDimensionValue.SETRANGE(ICDimensionValue.Code,DimensionValue.Code);
              IF ICDimensionValue.FIND('-') AND
                 (DimensionValue."Dimension Value Type" = ICDimensionValue."Dimension Value Type")
              THEN BEGIN
                DimensionValue."Map-to IC Dimension Value Code" := ICDimensionValue.Code;
                DimensionValue.MODIFY;
              END;
            END;
          UNTIL DimensionValue.NEXT = 0;
        END;
      END;
    END;

    BEGIN
    END.
  }
}
