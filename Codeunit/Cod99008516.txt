OBJECT Codeunit 99008516 BizTalk XML DOM Management
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=No temporary filename could be found.;ESP=No se ha podido encontrar el archivo temporal.';
      Text001@1001 : TextConst 'ENU=The environmental variable TEMP is empty. Please fill in a temporary file location .;ESP=La variable de entorno TEMP est  vac¡a. Especifique una ubicaci¢n temporal para los archivos.';

    PROCEDURE AddElement@1(VAR DOMNode@1001 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 3.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v3.0'.IXMLDOMNode";Name@1002 : Text[250];Value@1003 : Text[250];NameSpace@1004 : Text[250];VAR CreatedDOMNode@1005 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 3.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v3.0'.IXMLDOMNode") Status@1000 : Integer;
    VAR
      TempElement@1006 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 3.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v3.0'.IXMLDOMNode";
    BEGIN
      TempElement := DOMNode.ownerDocument.createNode('element', Name, NameSpace);
      IF ISCLEAR(TempElement) THEN BEGIN
        Status := 50;
        EXIT(Status)
      END;
      IF Value <> '' THEN
        TempElement.text := Value;
      DOMNode.appendChild(TempElement);
      CreatedDOMNode := TempElement;
      CLEAR(TempElement);
      Status := 0;
    END;

    PROCEDURE AddAttribute@2(VAR DOMNode@1001 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 3.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v3.0'.IXMLDOMNode";Name@1002 : Text[250];Value@1003 : Text[250]) Status@1000 : Integer;
    VAR
      TempAttribute@1004 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 3.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v3.0'.IXMLDOMNode";
    BEGIN
      TempAttribute := DOMNode.ownerDocument.createAttribute(Name);
      IF ISCLEAR(TempAttribute) THEN BEGIN
        Status := 60;
        EXIT(Status)
      END;
      IF Value <> '' THEN
        TempAttribute.nodeValue := Value;
      DOMNode.attributes.setNamedItem(TempAttribute);
      CLEAR(TempAttribute);
    END;

    PROCEDURE FindNode@3(RootNode@1020 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 3.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v3.0'.IXMLDOMNode";NodePath@1000 : Text[250];VAR ReturnedNode@2030 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 3.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v3.0'.IXMLDOMNode") : Boolean;
    BEGIN
      ReturnedNode := RootNode.selectSingleNode(NodePath);
      IF ISCLEAR(ReturnedNode) THEN
        EXIT(FALSE)
      ELSE
        EXIT(TRUE);
    END;

    PROCEDURE FindNodes@4(RootNode@3021 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 3.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v3.0'.IXMLDOMNode";NodePath@1000 : Text[250];VAR ReturnedNodeList@4022 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 3.0:{2933BF82-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v3.0'.IXMLDOMNodeList") : Boolean;
    BEGIN
      ReturnedNodeList := RootNode.selectNodes(NodePath);
      IF ISCLEAR(ReturnedNodeList) THEN
        EXIT(FALSE)
      ELSE
        EXIT(TRUE);
    END;

    PROCEDURE AddPicture@5(VAR DOMNode@1001 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 3.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v3.0'.IXMLDOMNode";Name@1002 : Text[250];OutboundProductCatalogLine@1003 : Record 99008510;NameSpace@1004 : Text[250];VAR CreatedDOMNode@1005 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 3.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v3.0'.IXMLDOMNode") Status@1000 : Integer;
    VAR
      Base64@1006 : Automation "{244B7B21-2FBE-4189-A6A8-B8E90E6F83B5} 6.0:{9CD92F3E-9F7C-4EE3-A5D0-724296F8790F}:Unknown Automation Server.Unknown Class";
      FileName@1007 : Text[250];
    BEGIN
      Status := AddElement(DOMNode,Name,'',NameSpace,CreatedDOMNode);
      IF  Status > 0 THEN
        EXIT(Status)
      ELSE BEGIN
        IF NOT CREATE(Base64,TRUE,FALSE) THEN
          EXIT(281);
        OutboundProductCatalogLine.CALCFIELDS(Picture);
        IF OutboundProductCatalogLine.Picture.HASVALUE THEN BEGIN
          FileName := GetTempFileName;
          OutboundProductCatalogLine.Picture.EXPORT(FileName);
          Status := Base64.Encode(FileName,CreatedDOMNode);
          IF Status = 0 THEN
            FILE.ERASE(FileName);
          CLEAR(Base64);
        END;
      END;
      EXIT(Status);
    END;

    PROCEDURE GetPicture@6(VAR InboundProductCatalogLine@1000 : Record 99008505;DOMNode@1001 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 3.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v3.0'.IXMLDOMNode") : Integer;
    VAR
      Base64@1002 : Automation "{244B7B21-2FBE-4189-A6A8-B8E90E6F83B5} 6.0:{9CD92F3E-9F7C-4EE3-A5D0-724296F8790F}:Unknown Automation Server.Unknown Class";
      FileName@1003 : Text[250];
    BEGIN
      IF NOT CREATE(Base64,TRUE,FALSE) THEN
        EXIT(281);
      FileName := GetTempFileName;
      IF Base64.Decode(FileName,DOMNode) = 0 THEN BEGIN
        InboundProductCatalogLine.Picture.IMPORT(FileName);
        FILE.ERASE(FileName);
      END;
      CLEAR(Base64);
      EXIT;
    END;

    LOCAL PROCEDURE GetTempFileName@7() : Text[250];
    VAR
      RBAutoMgt@1004 : Codeunit 419;
      FileName@1000 : Text[250];
      TempFileName@1001 : Text[250];
      NoOfTries@1002 : Integer;
      FoundFileName@1003 : Boolean;
    BEGIN
      FileName := RBAutoMgt.EnvironFileName('','');
      IF FileName = '' THEN
        ERROR(Text001);
      FileName := FileName + '\NFPicture.bmp';
      FoundFileName := FALSE;
      NoOfTries := 0;
      REPEAT
        BEGIN
          IF NoOfTries > 10 THEN
            ERROR(Text000);
          TempFileName := FileName + FORMAT(NoOfTries);
          IF FILE.EXISTS(TempFileName) THEN
            NoOfTries := NoOfTries + 1
          ELSE
            FoundFileName := TRUE;
        END;
      UNTIL FoundFileName;
      EXIT(TempFileName);
    END;

    BEGIN
    END.
  }
}
