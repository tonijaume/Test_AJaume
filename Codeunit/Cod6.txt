OBJECT Codeunit 6 Fiscal Year-Close
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW13.00,NAVES6.00.01;
  }
  PROPERTIES
  {
    TableNo=50;
    OnRun=BEGIN
            AccountingPeriod.COPY(Rec);
            Code;
            Rec := AccountingPeriod;
          END;

  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=You must create a new fiscal year before you can close the old year.;ESP=Antes de cerrar un ejercicio hay que crear el siguiente.';
      Text002@1001 : TextConst 'ENU="This function closes the fiscal year from %1 to %2. ";ESP="Esta funci¢n cierra el ejercicio comprendido entre %1 y %2. "';
      Text003@1002 : TextConst 'ENU=Once the fiscal year is closed it cannot be opened again, and the periods in the fiscal year cannot be changed.\\;ESP=Cuando se ha cerrado un ejercicio no se pueden cambiar los periodos que lo componen ni se puede volver a abrir.\\';
      Text004@1003 : TextConst 'ENU=Do you want to close the fiscal year?;ESP=¨Confirma que desea cerrar el ejercicio?';
      AccountingPeriod@1004 : Record 50;
      AccountingPeriod2@1005 : Record 50;
      AccountingPeriod3@1006 : Record 50;
      FiscalYearStartDate@1007 : Date;
      FiscalYearEndDate@1008 : Date;
      HidePrintDialog@1100000 : Boolean;

    LOCAL PROCEDURE Code@1();
    BEGIN
      WITH AccountingPeriod DO BEGIN
        AccountingPeriod2.SETRANGE(Closed,FALSE);
        AccountingPeriod2.FIND('-');

        FiscalYearStartDate := AccountingPeriod2."Starting Date";
        AccountingPeriod := AccountingPeriod2;
        TESTFIELD("New Fiscal Year",TRUE);

        AccountingPeriod2.SETRANGE("New Fiscal Year",TRUE);
        IF AccountingPeriod2.FIND('>') THEN BEGIN
          FiscalYearEndDate := CALCDATE('<-1D>',AccountingPeriod2."Starting Date");

          AccountingPeriod3 := AccountingPeriod2;
          AccountingPeriod2.SETRANGE("New Fiscal Year");
          AccountingPeriod2.FIND('<');
        END ELSE
          ERROR(Text001);

        IF HidePrintDialog THEN BEGIN
          IF FiscalYearStartDate = 010111D THEN
            EXIT;
        END ELSE
          IF NOT
            CONFIRM(
              Text002 +
              Text003 +
              Text004,FALSE,
              FiscalYearStartDate,FiscalYearEndDate)
          THEN
            EXIT;

        RESET;

        SETRANGE("Starting Date",FiscalYearStartDate,AccountingPeriod2."Starting Date");
        MODIFYALL(Closed,TRUE);

        SETRANGE("Starting Date",FiscalYearStartDate,AccountingPeriod3."Starting Date");
        MODIFYALL("Date Locked",TRUE);

        RESET;
      END;
    END;

    PROCEDURE SetHidePrintDialog@1100100(NewHidePrintDialog@1100000 : Boolean);
    BEGIN
      HidePrintDialog := NewHidePrintDialog;
    END;

    BEGIN
    END.
  }
}
