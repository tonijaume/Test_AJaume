OBJECT Codeunit 6800 EP Functions
{
  OBJECT-PROPERTIES
  {
    Date=25/05/05;
    Time=12:00:00;
    Version List=NAVW14.00.01;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=A template with %1 %2 cannot be found.;ESP=No se puede encontrar una plantilla con %1 %2.';
      Text002@1001 : TextConst 'ENU=No tables have been declared for template %1.;ESP=No se declararon tablas para la plantilla %1.';
      Text003@1000000000 : TextConst 'ENU=Actions found for template %1. Are these to be transferred?;ESP=Se encontraron acciones para la plantilla %1. ¨Desea transferirlas?';

    PROCEDURE InsertGroupReqTypeTabs@1000000001(VAR GroupReqType@1000000000 : Record 6805);
    VAR
      GroupReqTypeTable@1000000007 : Record 6811;
      GroupReqTypeTableTab@1000000001 : Record 6806;
      GroupReqTypeTableTabFields@1000000005 : Record 6807;
      GrpRTTableTabFieldsSortKeys@1000000012 : Record 6822;
      GroupReqTypeActions@1000000009 : Record 6809;
      GrpReqTypeActionHeadCreateCrit@1000000014 : Record 6824;
      ReqTypeTable@1000000002 : Record 6811;
      ReqTypeTableTab@1000000003 : Record 6806;
      ReqTypeTableTabFields@1000000004 : Record 6807;
      ReqTypeTableTabFieldsSortKeys@1000000011 : Record 6822;
      ReqTypeActions@1000000008 : Record 6809;
      ReqTypeActionHeadCreateCrit@1000000013 : Record 6824;
      SrcGroupReqType@1000000015 : Record 6805;
      GrpReqTypeActionFilter@1000000016 : Record 6813;
      SrcGrpReqTypeActionFilter@1000000017 : Record 6813;
      GrpReqTypeLookup@1000000018 : Record 6827;
      SrcGrpReqTypeLookup@1000000019 : Record 6827;
      GrpReqTypeLookupCond@1000000020 : Record 6828;
      SrcGrpReqTypeLookupCond@1000000021 : Record 6828;
      GrpReqTypeLookupMapping@1000000022 : Record 6829;
      SrcGrpReqTypeLookupMapping@1000000023 : Record 6829;
      GrpReqTypeConnHeadLine@1000000024 : Record 6815;
      SrcGrpReqTypeConnHeadLine@1000000025 : Record 6815;
      GrpReqTypeTableFilterFields@1000000026 : Record 6832;
      SrcGrpReqTypeTableFilterFields@1000000027 : Record 6832;
      GrpReqTypeTableFilterKeys@1000000028 : Record 6833;
      SrcGrpReqTypeTableFilterKeys@1000000029 : Record 6833;
      NewLineNo@1000 : Integer;
    BEGIN
      IF GroupReqType.Code = '' THEN
        EXIT;

      // copy values into table 6805
      SrcGroupReqType.RESET;
      SrcGroupReqType.SETRANGE(Code, GroupReqType.Code);
      SrcGroupReqType.SETRANGE("Group Code", '');
      IF NOT SrcGroupReqType.FIND('-') THEN
        ERROR(Text001, GroupReqType.FIELDCAPTION(Code),GroupReqType.Code);
      GroupReqType.Description := SrcGroupReqType.Description;
      GroupReqType.Editable := SrcGroupReqType.Editable;
      GroupReqType.Type := SrcGroupReqType.Type;
      GroupReqType."Defined by System" := SrcGroupReqType."Defined by System";
      GroupReqType."Caption ID" := SrcGroupReqType."Caption ID";
      GroupReqType.Caption := SrcGroupReqType.Caption;

      IF SrcGroupReqType.Type = SrcGroupReqType.Type::System THEN
        EXIT;

      // copy values into table 6811
      ReqTypeTable.RESET;
      ReqTypeTable.SETRANGE("WP Request Code",GroupReqType.Code);
      ReqTypeTable.SETRANGE("Group Code",'');
      IF NOT ReqTypeTable.FIND('-') THEN
        ERROR(Text002, GroupReqType.Code);

      REPEAT
        GroupReqTypeTable.INIT;
        GroupReqTypeTable.TRANSFERFIELDS(ReqTypeTable);
        GroupReqTypeTable."Group Code" := GroupReqType."Group Code";
        GroupReqTypeTable.INSERT;

        // Transfer Actions
        ReqTypeActions.RESET;
        ReqTypeActions.SETRANGE("Group Code",'');
        ReqTypeActions.SETRANGE("WP Request Code",GroupReqTypeTable."WP Request Code");
        ReqTypeActions.SETRANGE("Table No.",GroupReqTypeTable."Table No.");
        ReqTypeActions.SETRANGE("Header or Line",GroupReqTypeTable."Header or Line");
        IF ReqTypeActions.FIND('-') THEN BEGIN
          IF CONFIRM(STRSUBSTNO(Text003,ReqTypeActions."Header or Line"),TRUE) THEN BEGIN
            REPEAT
              GroupReqTypeActions.INIT;
              GroupReqTypeActions.TRANSFERFIELDS(ReqTypeActions);
              GroupReqTypeActions."Group Code" := GroupReqType."Group Code";
              GroupReqTypeActions.INSERT;

              // transfer head criteria
              ReqTypeActionHeadCreateCrit.RESET;
              ReqTypeActionHeadCreateCrit.SETRANGE("Group Code",'');
              ReqTypeActionHeadCreateCrit.SETRANGE("WP Request Code",ReqTypeActions."WP Request Code");
              ReqTypeActionHeadCreateCrit.SETRANGE("Table No.",ReqTypeActions."Table No.");
              ReqTypeActionHeadCreateCrit.SETRANGE("Header or Line",ReqTypeActions."Header or Line");
              ReqTypeActionHeadCreateCrit.SETRANGE("Action Type",ReqTypeActions.Type);
              ReqTypeActionHeadCreateCrit.SETRANGE(Position,ReqTypeActions.Position);
              IF ReqTypeActionHeadCreateCrit.FIND('-') THEN BEGIN
                REPEAT
                  GrpReqTypeActionHeadCreateCrit.INIT;
                  GrpReqTypeActionHeadCreateCrit.TRANSFERFIELDS(ReqTypeActionHeadCreateCrit);
                  GrpReqTypeActionHeadCreateCrit."Group Code" := GroupReqTypeActions."Group Code";
                  GrpReqTypeActionHeadCreateCrit.INSERT(TRUE);
                UNTIL ReqTypeActionHeadCreateCrit.NEXT = 0;
              END;

              // transfer filter keys and fields
              SrcGrpReqTypeActionFilter.RESET;
              SrcGrpReqTypeActionFilter.SETRANGE("Group Code", '');
              SrcGrpReqTypeActionFilter.SETRANGE("WP Request Code", ReqTypeActions."WP Request Code");
              SrcGrpReqTypeActionFilter.SETRANGE("Table No.", ReqTypeActions."Table No.");
              SrcGrpReqTypeActionFilter.SETRANGE("Header or Line",ReqTypeActions."Header or Line");
              SrcGrpReqTypeActionFilter.SETRANGE("Action Type",ReqTypeActions.Type);
              SrcGrpReqTypeActionFilter.SETRANGE(Position,ReqTypeActions.Position);
              IF SrcGrpReqTypeActionFilter.FIND('-') THEN BEGIN
                REPEAT
                  GrpReqTypeActionFilter.INIT;
                  GrpReqTypeActionFilter.TRANSFERFIELDS(SrcGrpReqTypeActionFilter);
                  GrpReqTypeActionFilter."Group Code" := GroupReqTypeActions."Group Code";
                  GrpReqTypeActionFilter.INSERT(TRUE);
                UNTIL SrcGrpReqTypeActionFilter.NEXT = 0;
              END;

            UNTIL ReqTypeActions.NEXT = 0;
          END;
        END;

        ReqTypeTableTab.RESET;
        ReqTypeTableTab.SETRANGE("WP Request Code",GroupReqType.Code);
        ReqTypeTableTab.SETRANGE("Group Code",'');
        ReqTypeTableTab.SETRANGE("Table No.",ReqTypeTable."Table No.");
        IF ReqTypeTableTab.FIND('-') THEN BEGIN
          REPEAT
            GroupReqTypeTableTab.INIT;
            GroupReqTypeTableTab.TRANSFERFIELDS(ReqTypeTableTab);
            GroupReqTypeTableTab."Group Code" := GroupReqType."Group Code";
            GroupReqTypeTableTab.INSERT;

            // transfer fields
            ReqTypeTableTabFields.RESET;
            ReqTypeTableTabFields.SETRANGE("WP Request Code",ReqTypeTableTab."WP Request Code");
            ReqTypeTableTabFields.SETRANGE("Group Code",'');
            ReqTypeTableTabFields.SETRANGE("Table No.",ReqTypeTableTab."Table No.");
            ReqTypeTableTabFields.SETRANGE("Tab Code",ReqTypeTableTab.Code);
            IF ReqTypeTableTabFields.FIND('-') THEN BEGIN
              NewLineNo := 0;
              REPEAT
                NewLineNo := NewLineNo + 10000;
                GroupReqTypeTableTabFields.INIT;
                GroupReqTypeTableTabFields.TRANSFERFIELDS(ReqTypeTableTabFields);
                GroupReqTypeTableTabFields."Group Code" := GroupReqTypeTableTab."Group Code";
                GroupReqTypeTableTabFields.INSERT;

                // transfer sort keys
                ReqTypeTableTabFieldsSortKeys.RESET;
                ReqTypeTableTabFieldsSortKeys.SETRANGE("WP Request Code",ReqTypeTableTabFields."WP Request Code");
                ReqTypeTableTabFieldsSortKeys.SETRANGE("Group Code",'');
                ReqTypeTableTabFieldsSortKeys.SETRANGE("Table No.",ReqTypeTableTabFields."Table No.");
                ReqTypeTableTabFieldsSortKeys.SETRANGE("Tab Code",ReqTypeTableTabFields."Tab Code");
                ReqTypeTableTabFieldsSortKeys.SETRANGE("Field Line No.",ReqTypeTableTabFields."Line No.");
                IF ReqTypeTableTabFieldsSortKeys.FIND('-') THEN BEGIN
                  REPEAT
                    GrpRTTableTabFieldsSortKeys.INIT;
                    GrpRTTableTabFieldsSortKeys.TRANSFERFIELDS(ReqTypeTableTabFieldsSortKeys);
                    GrpRTTableTabFieldsSortKeys."Group Code" := GroupReqTypeTableTab."Group Code";
                    GrpRTTableTabFieldsSortKeys.INSERT(TRUE);
                  UNTIL ReqTypeTableTabFieldsSortKeys.NEXT = 0;
                END;

                // transfer lookups
                SrcGrpReqTypeLookup.RESET;
                SrcGrpReqTypeLookup.SETRANGE("WP Request Code",ReqTypeTableTabFields."WP Request Code");
                SrcGrpReqTypeLookup.SETRANGE("Group Code",'');
                SrcGrpReqTypeLookup.SETRANGE("Table No.",ReqTypeTableTabFields."Table No.");
                SrcGrpReqTypeLookup.SETRANGE("Tab Code",ReqTypeTableTabFields."Tab Code");
                SrcGrpReqTypeLookup.SETRANGE("Line No.",ReqTypeTableTabFields."Line No.");
                IF SrcGrpReqTypeLookup.FIND('-') THEN BEGIN
                  REPEAT
                    GrpReqTypeLookup.INIT;
                    GrpReqTypeLookup.TRANSFERFIELDS(SrcGrpReqTypeLookup);
                    GrpReqTypeLookup."Group Code" := GroupReqTypeTableTab."Group Code";
                    GrpReqTypeLookup.INSERT(TRUE);

                    // transfer related lookup conditions
                    SrcGrpReqTypeLookupCond.RESET;
                    SrcGrpReqTypeLookupCond.SETRANGE("WP Request Code",GrpReqTypeLookup."WP Request Code");
                    SrcGrpReqTypeLookupCond.SETRANGE("Group Code",'');
                    SrcGrpReqTypeLookupCond.SETRANGE("Table No.",GrpReqTypeLookup."Table No.");
                    SrcGrpReqTypeLookupCond.SETRANGE("Tab Code",GrpReqTypeLookup."Tab Code");
                    SrcGrpReqTypeLookupCond.SETRANGE("Line No.",GrpReqTypeLookup."Line No.");
                    SrcGrpReqTypeLookupCond.SETRANGE("Lookup WP Request Code", GrpReqTypeLookup."Lookup WP Request Code");
                    IF SrcGrpReqTypeLookupCond.FIND('-') THEN BEGIN
                      REPEAT
                        GrpReqTypeLookupCond.INIT;
                        GrpReqTypeLookupCond.TRANSFERFIELDS(SrcGrpReqTypeLookupCond);
                        GrpReqTypeLookupCond."Group Code" := GrpReqTypeLookup."Group Code";
                        GrpReqTypeLookupCond.INSERT(TRUE);
                      UNTIL SrcGrpReqTypeLookupCond.NEXT = 0;
                    END;

                    // transfer related lookup field mappings
                    SrcGrpReqTypeLookupMapping.RESET;
                    SrcGrpReqTypeLookupMapping.SETRANGE("WP Request Code",GrpReqTypeLookup."WP Request Code");
                    SrcGrpReqTypeLookupMapping.SETRANGE("Group Code",'');
                    SrcGrpReqTypeLookupMapping.SETRANGE("Table No.",GrpReqTypeLookup."Table No.");
                    SrcGrpReqTypeLookupMapping.SETRANGE("Tab Code",GrpReqTypeLookup."Tab Code");
                    SrcGrpReqTypeLookupMapping.SETRANGE("Line No.",GrpReqTypeLookup."Line No.");
                    SrcGrpReqTypeLookupMapping.SETRANGE("Lookup WP Request Code", GrpReqTypeLookup."Lookup WP Request Code");
                    IF SrcGrpReqTypeLookupMapping.FIND('-') THEN BEGIN
                      REPEAT
                        GrpReqTypeLookupMapping.INIT;
                        GrpReqTypeLookupMapping.TRANSFERFIELDS(SrcGrpReqTypeLookupMapping);
                        GrpReqTypeLookupMapping."Group Code" := GrpReqTypeLookup."Group Code";
                        GrpReqTypeLookupMapping.INSERT(TRUE);
                      UNTIL SrcGrpReqTypeLookupMapping.NEXT = 0;
                    END;

                  UNTIL SrcGrpReqTypeLookup.NEXT = 0;
                END;
              UNTIL ReqTypeTableTabFields.NEXT = 0;
            END;

          UNTIL ReqTypeTableTab.NEXT = 0;
        END;

        // Transfer Head-Line Connection
        SrcGrpReqTypeConnHeadLine.RESET;
        SrcGrpReqTypeConnHeadLine.SETRANGE("WP Request Code",GroupReqType.Code);
        SrcGrpReqTypeConnHeadLine.SETRANGE("Group Code",'');
        SrcGrpReqTypeConnHeadLine.SETRANGE("Header Table No.",ReqTypeTable."Table No.");
        IF SrcGrpReqTypeConnHeadLine.FIND('-') THEN BEGIN
          REPEAT
            GrpReqTypeConnHeadLine.INIT;
            GrpReqTypeConnHeadLine.TRANSFERFIELDS(SrcGrpReqTypeConnHeadLine);
            GrpReqTypeConnHeadLine."Group Code" := GroupReqType."Group Code";
            GrpReqTypeConnHeadLine.INSERT(TRUE);
          UNTIL SrcGrpReqTypeConnHeadLine.NEXT = 0;
        END;

        // Transfer Connection to other webparts (filters)
        SrcGrpReqTypeTableFilterFields.RESET;
        SrcGrpReqTypeTableFilterFields.SETRANGE("WP Request Code",GroupReqType.Code);
        SrcGrpReqTypeTableFilterFields.SETRANGE("Group Code",'');
        SrcGrpReqTypeTableFilterFields.SETRANGE("Table No.",ReqTypeTable."Table No.");
        IF SrcGrpReqTypeTableFilterFields.FIND('-') THEN BEGIN
          REPEAT
            GrpReqTypeTableFilterFields.INIT;
            GrpReqTypeTableFilterFields.TRANSFERFIELDS(SrcGrpReqTypeTableFilterFields);
            GrpReqTypeTableFilterFields."Group Code" := GroupReqType."Group Code";
            GrpReqTypeTableFilterFields.INSERT(TRUE);
          UNTIL SrcGrpReqTypeTableFilterFields.NEXT = 0;
        END;

        // Transfer Connection to other webparts (keys)
        SrcGrpReqTypeTableFilterKeys.RESET;
        SrcGrpReqTypeTableFilterKeys.SETRANGE("WP Request Code",GroupReqType.Code);
        SrcGrpReqTypeTableFilterKeys.SETRANGE("Group Code",'');
        SrcGrpReqTypeTableFilterKeys.SETRANGE("Table No.",ReqTypeTable."Table No.");
        IF SrcGrpReqTypeTableFilterKeys.FIND('-') THEN BEGIN
          REPEAT
            GrpReqTypeTableFilterKeys.INIT;
            GrpReqTypeTableFilterKeys.TRANSFERFIELDS(SrcGrpReqTypeTableFilterKeys);
            GrpReqTypeTableFilterKeys."Group Code" := GroupReqType."Group Code";
            GrpReqTypeTableFilterKeys.INSERT(TRUE);
          UNTIL SrcGrpReqTypeTableFilterKeys.NEXT = 0;
        END;

      UNTIL ReqTypeTable.NEXT = 0;
    END;

    BEGIN
    END.
  }
}
