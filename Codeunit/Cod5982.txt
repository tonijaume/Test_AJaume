OBJECT Codeunit 5982 Service-Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=27/08/13;
    Time=12:00:00;
    Modified=Yes;
    Version List=TFS288091;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1002 : TextConst 'ENU=&Ship,&Invoice,Ship &and Invoice,Ship and &Consume;ESM=&Enviar,&Facturar,Enviar &y facturar,Enviar y &consumir';
      Text001@1001 : TextConst 'ENU=Do you want to post and print the %1?;ESM=¨Confirma que desea registrar e imprimir el/la %1?';
      ServiceHeader@1003 : Record 5900;
      ServShptHeader@1004 : Record 5990;
      ServInvHeader@1005 : Record 5992;
      ServCrMemoHeader@1006 : Record 5994;
      ReportSelection@1008 : Record 77;
      ServicePost@1009 : Codeunit 5980;
      EInvoiceMgt@1020000 : Codeunit 7009772;
      Selection@1010 : Integer;
      Ship@1011 : Boolean;
      Consume@1007 : Boolean;
      Invoice@1000 : Boolean;

    PROCEDURE PostDocument@5(VAR Rec@1000 : Record 5900);
    VAR
      DummyServLine@1001 : TEMPORARY Record 5902;
    BEGIN
      ServiceHeader.COPY(Rec);
      Code(DummyServLine);
      Rec := ServiceHeader;
    END;

    PROCEDURE PostDocumentWithLines@4(VAR Rec@1000 : Record 5900;VAR PassedServLine@1001 : Record 5902);
    BEGIN
      ServiceHeader.COPY(Rec);
      Code(PassedServLine);
      Rec := ServiceHeader;
    END;

    LOCAL PROCEDURE Code@2(VAR PassedServLine@1002 : Record 5902);
    BEGIN
      WITH ServiceHeader DO BEGIN
        EInvoiceMgt.EDocPrintValidation(0,"No.");
        CASE "Document Type" OF
          "Document Type"::Order:
            BEGIN
              Selection := STRMENU(Text000,3);
              IF Selection = 0 THEN
                EXIT;
              Ship := Selection IN [1,3,4];
              Consume := Selection IN [4];
              Invoice := Selection IN [2,3];
            END
          ELSE
            IF NOT CONFIRM(Text001,FALSE,"Document Type") THEN
              EXIT;
        END;
        ServicePost.PostWithLines(ServiceHeader,PassedServLine,Ship,Consume,Invoice);
        GetReport(ServiceHeader);
        COMMIT;
      END;
    END;

    PROCEDURE GetReport@3(VAR ServiceHeader@1000 : Record 5900);
    BEGIN
      WITH ServiceHeader DO BEGIN
        CASE "Document Type" OF
          "Document Type"::Order:
            BEGIN
              IF Ship THEN BEGIN
                ServShptHeader."No." := "Last Shipping No.";
                ServShptHeader.SETRECFILTER;
                PrintReport(ReportSelection.Usage::"SM.Shipment");
              END;
              IF Invoice THEN BEGIN
                ServInvHeader."No." := "Last Posting No.";
                ServInvHeader.SETRECFILTER;
                PrintReport(ReportSelection.Usage::"SM.Invoice");
              END;
            END;
          "Document Type"::Invoice:
            BEGIN
              IF "Last Posting No." = '' THEN
                ServInvHeader."No." := "No."
              ELSE
                ServInvHeader."No." := "Last Posting No.";
              ServInvHeader.SETRECFILTER;
              PrintReport(ReportSelection.Usage::"SM.Invoice");
            END;
          "Document Type"::"Credit Memo":
            BEGIN
              IF "Last Posting No." = '' THEN
                ServCrMemoHeader."No." := "No."
              ELSE
                ServCrMemoHeader."No." := "Last Posting No.";
              ServCrMemoHeader.SETRECFILTER;
              PrintReport(ReportSelection.Usage::"SM.Credit Memo");
            END;
        END;
      END;
    END;

    LOCAL PROCEDURE PrintReport@1(ReportUsage@1000 : Integer);
    BEGIN
      ReportSelection.RESET;
      ReportSelection.SETRANGE(Usage,ReportUsage);
      ReportSelection.FIND('-');
      REPEAT
        ReportSelection.TESTFIELD("Report ID");
        CASE ReportUsage OF
          ReportSelection.Usage::"SM.Invoice":
            REPORT.RUN(ReportSelection."Report ID",FALSE,FALSE,ServInvHeader);
          ReportSelection.Usage::"SM.Credit Memo":
            REPORT.RUN(ReportSelection."Report ID",FALSE,FALSE,ServCrMemoHeader);
          ReportSelection.Usage::"SM.Shipment":
            REPORT.RUN(ReportSelection."Report ID",FALSE,FALSE,ServShptHeader);
        END;
      UNTIL ReportSelection.NEXT = 0;
    END;

    BEGIN
    END.
  }
}
