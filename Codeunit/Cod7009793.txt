OBJECT Codeunit 7009793 Sincronizar comercializadora
{
  OBJECT-PROPERTIES
  {
    Date=26/05/15;
    Time=15:59:58;
    Modified=Yes;
    Version List=AIC2009,COM;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      wOperacion@1000000000 : 'Insert,Modify,Delete,Rename';
      Text001@1000000001 : TextConst 'ENU=Sincronization problem, check the information in the offshore company.;ESP=Problema de sincronizacion, revise la informacion en la empresa comercializadora.';

    PROCEDURE SincronizaTabla@1000000000(VAR prRecRef@1000000000 : RecordRef;VAR prxRecRef@1000000001 : RecordRef;pwOperacion@1000000002 : Integer;pwCompanyCOM@1000000003 : Text[30]);
    VAR
      lrRefEmp@1000000010 : RecordRef;
      lrRefEmpx@1000000009 : RecordRef;
      lwSaltar@1000000005 : Boolean;
      lwKeyRef@1000000004 : KeyRef;
    BEGIN
      // SincronizaTabla

      IF pwCompanyCOM <> COMPANYNAME THEN
      BEGIN

        lrRefEmp.OPEN(prRecRef.NUMBER, FALSE, pwCompanyCOM);

        IF lrRefEmp.RECORDLEVELLOCKING THEN
          lrRefEmp.LOCKTABLE
        ELSE
          lrRefEmp.LOCKTABLE(FALSE,FALSE);

        _TransfiereCampos(prRecRef, lrRefEmp, pwCompanyCOM);

        CASE pwOperacion OF
          wOperacion::Insert :
          BEGIN
            lwSaltar := lrRefEmp.FIND;

            IF lwSaltar THEN
            BEGIN
              _TransfiereCampos(prRecRef, lrRefEmp, pwCompanyCOM);

              lwSaltar := NOT lrRefEmp.MODIFY;
            END
            ELSE
              lwSaltar := NOT lrRefEmp.INSERT;

            IF lwSaltar THEN
              MESSAGE(Text001);

          END;

          wOperacion::Modify :
          BEGIN
            lwSaltar := NOT lrRefEmp.FIND;

            IF NOT lwSaltar THEN
            BEGIN
              _TransfiereCampos(prRecRef, lrRefEmp, pwCompanyCOM);

              lwSaltar := NOT lrRefEmp.MODIFY;
            END
            ELSE
              lwSaltar := NOT lrRefEmp.INSERT;

            IF lwSaltar THEN
              MESSAGE(Text001);
          END;

          wOperacion::Delete :
          BEGIN
            lwSaltar := NOT lrRefEmp.FIND;

            IF NOT lwSaltar THEN
            BEGIN
              _TransfiereCampos(prRecRef, lrRefEmp, pwCompanyCOM);

              lwSaltar := NOT lrRefEmp.DELETE;
            END;

            IF lwSaltar THEN
              MESSAGE(Text001);
          END;

          wOperacion::Rename :
          BEGIN
            lrRefEmpx.OPEN(prxRecRef.NUMBER, FALSE, pwCompanyCOM);

            IF lrRefEmpx.RECORDLEVELLOCKING THEN
              lrRefEmpx.LOCKTABLE
            ELSE
              lrRefEmpx.LOCKTABLE(FALSE,FALSE);

            _TransfiereCampos(prxRecRef, lrRefEmpx, pwCompanyCOM);

            lwSaltar := NOT lrRefEmpx.FIND;

            lwSaltar := lrRefEmp.FIND;

            _TransfiereCampos(prRecRef, lrRefEmp, pwCompanyCOM);

            IF NOT lwSaltar THEN
            BEGIN

              // No se puede renombrar directamente, la funci¢n rename no
              // deja saltarse el trigger, y entra en bucle hasta que salta la pila
              lwSaltar := NOT lrRefEmpx.DELETE;
              lwSaltar := NOT lrRefEmp.INSERT;
            END;

            IF lwSaltar THEN
              MESSAGE(Text001);

            lrRefEmpx.CLOSE;

          END;
        END;

        lrRefEmp.CLOSE;
      END;
    END;

    LOCAL PROCEDURE _TransfiereCampos@1000000001(VAR prRefDe@1000000002 : RecordRef;VAR prRefA@1000000001 : RecordRef;VAR pwEmpresa@1000000000 : Text[30]);
    VAR
      lwFieldRefA@1000000005 : FieldRef;
      lwFieldRefDe@1000000004 : FieldRef;
      lwIndex@1000000003 : Integer;
    BEGIN
      // _TransfiereCampos

      FOR lwIndex := 1 TO prRefDe.FIELDCOUNT DO
      BEGIN
        lwFieldRefDe := prRefDe.FIELDINDEX(lwIndex);
        lwFieldRefA  := prRefA.FIELDINDEX(lwIndex);
        lwFieldRefA.VALUE := lwFieldRefDe.VALUE;
      END;
    END;

    PROCEDURE OpInsert@1103355005() : Integer;
    BEGIN
      // OpInsert

      EXIT(wOperacion::Insert);
    END;

    PROCEDURE OpModify@1103355006() : Integer;
    BEGIN
      // OpModify

      EXIT(wOperacion::Modify);
    END;

    PROCEDURE OpDelete@1103355007() : Integer;
    BEGIN
      // OpDelete

      EXIT(wOperacion::Delete);
    END;

    PROCEDURE OpRename@1103355008() : Integer;
    BEGIN
      // OpRename

      EXIT(wOperacion::Rename);
    END;

    PROCEDURE SincronizaDiario@1000000002(pwDiario@1000000000 : Code[10];pwSeccion@1000000001 : Code[10];pwDocumento@1000000002 : Code[20];pwTouroperador@1000000003 : Code[10]);
    VAR
      lrLinDiario@1000000004 : Record 81;
      lrLinDiario2@1000000006 : Record 81;
      lrTO@1000000005 : Record 7009717;
      lwNoLinea@1000000007 : Integer;
    BEGIN
      // SincronizaDiario

      lrLinDiario.RESET;
      lrLinDiario.SETRANGE("Journal Template Name", pwDiario);
      lrLinDiario.SETRANGE("Journal Batch Name"   , pwSeccion);
      IF pwDocumento <> '' THEN
        lrLinDiario.SETRANGE("Document No.", pwDocumento);
      IF lrLinDiario.FINDSET(TRUE) THEN
      BEGIN
        //. Obtenemos el touroperador para ver su empresa comercializadora
        lrTO.GET(pwTouroperador);
        IF lrTO."Empresa comercializadora" = '' THEN
          EXIT;

        //. Buscamos el £ltimo numero de linea en la empresa destino
        lrLinDiario2.RESET;
        lrLinDiario2.CHANGECOMPANY(lrTO."Empresa comercializadora");
        lrLinDiario2.SETRANGE("Journal Template Name", pwDiario);
        lrLinDiario2.SETRANGE("Journal Batch Name"   , pwSeccion);
        IF lrLinDiario2.FINDLAST THEN
          lwNoLinea := lrLinDiario2."Line No."
        ELSE
          lwNoLinea := 0;

        //. Copiamos las lineas a la empresa comercializadora
        REPEAT
          lwNoLinea += 10000;

          lrLinDiario2.TRANSFERFIELDS(lrLinDiario);
          lrLinDiario2."Line No." := lwNoLinea;
          lrLinDiario2.INSERT;
        UNTIL lrLinDiario.NEXT = 0;
        lrLinDiario.DELETEALL;
      END;
    END;

    PROCEDURE SincronizaDocCompra@1000000003(pwTipoDocumento@1000000000 : Integer;pwNumDocumento@1000000001 : Code[20];pwTouroperador@1000000008 : Code[10]);
    VAR
      lrTO@1000000009 : Record 7009717;
      lrDocCompra@1000000002 : Record 38;
      lrLinCompra@1000000003 : Record 39;
      lrDocDim@1000000004 : Record 357;
      lrComent@1000000005 : Record 43;
      lrRecRef@1000000006 : RecordRef;
      lrxRecRef@1000000007 : RecordRef;
    BEGIN
      // SincronizaDocCompra

      IF lrDocCompra.GET(pwTipoDocumento, pwNumDocumento) THEN
      BEGIN
        //. Obtenemos el touroperador para ver su empresa comercializadora
        lrTO.GET(pwTouroperador);
        IF lrTO."Empresa comercializadora" = '' THEN
          EXIT;

        //. Pasamos la cabecera de factura
        lrRecRef.GETTABLE(lrDocCompra);
        lrxRecRef.GETTABLE(lrDocCompra);
        SincronizaTabla(lrRecRef, lrxRecRef, 0, lrTO."Empresa comercializadora");

        //. Pasamos las lineas de factura
        lrLinCompra.RESET;
        lrLinCompra.SETRANGE("Document Type", lrDocCompra."Document Type");
        lrLinCompra.SETRANGE("Document No." , lrDocCompra."No.");
        IF lrLinCompra.FINDSET THEN
        BEGIN
          REPEAT
            lrRecRef.GETTABLE(lrLinCompra);
            lrxRecRef.GETTABLE(lrLinCompra);
            SincronizaTabla(lrRecRef, lrxRecRef, 0, lrTO."Empresa comercializadora");
          UNTIL lrLinCompra.NEXT = 0;
          lrLinCompra.DELETEALL;
        END;

        //. Pasamos las dimensiones
        lrDocDim.RESET;
        lrDocDim.SETRANGE("Document Type", lrDocCompra."Document Type");
        lrDocDim.SETRANGE("Document No." , lrDocCompra."No.");
        lrDocDim.SETRANGE("Table ID"     , 38, 39);
        IF lrDocDim.FINDSET THEN
        BEGIN
          REPEAT
            lrRecRef.GETTABLE(lrDocDim);
            lrxRecRef.GETTABLE(lrDocDim);
            SincronizaTabla(lrRecRef, lrxRecRef, 0, lrTO."Empresa comercializadora");
          UNTIL lrDocDim.NEXT = 0;
          lrDocDim.DELETEALL;
        END;

        //. Pasamos los comentarios de factura
        lrComent.RESET;
        lrComent.SETRANGE("Document Type", lrDocCompra."Document Type");
        lrComent.SETRANGE("No."          , lrDocCompra."No.");
        IF lrComent.FINDSET THEN
        BEGIN
          REPEAT
            lrRecRef.GETTABLE(lrComent);
            lrxRecRef.GETTABLE(lrComent);
            SincronizaTabla(lrRecRef, lrxRecRef, 0, lrTO."Empresa comercializadora");
          UNTIL lrComent.NEXT = 0;
          lrComent.DELETEALL;
        END;

        //. Borramos el doc compra
        lrDocCompra.DELETE;
      END;
    END;

    BEGIN
    {
      // Para usar la sincronizaci¢n hacemos esto en cada tabla:

      lrRecRef.GETTABLE(Rec);
      lrxRecRef.GETTABLE(xRec);
      cSinc2.Sincroniza(lrRecRef, lrxRecRef, cSinc2.Opxxx);

      (lrRecRef y lrxRecRef son Recordrefs)
      (cSinc2.Opxxx puede ser OpInsert/OpModify/OpDelete/OpRename dependiendo del trigger)

      $000 AJS 12032015 CNT-CAR-14096, creada para poder sincronizar tablas con la empresa comercializadora

      $001 AJS 07052015 CNT-OC-15134, nueva funcion SincronizaDiario para pasar los asientos de FPR a la empresa comercializadora

      $002 AJS 08052015 CNT-OC-15134, nueva funcion SincronizaDocCompra para pasar las facturas de compra a la empresa comercializadora
    }
    END.
  }
}
