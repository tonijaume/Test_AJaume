OBJECT Codeunit 243 Item Jnl.-B.Post
{
  OBJECT-PROPERTIES
  {
    Date=08/06/01;
    Time=12:00:00;
    Version List=NAVW13.00;
  }
  PROPERTIES
  {
    TableNo=233;
    OnRun=BEGIN
            ItemJnlBatch.COPY(Rec);
            Code;
            Rec.COPY(ItemJnlBatch);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to post the journals?;ESP=Confirma que desea registrar los diarios?';
      Text001@1001 : TextConst 'ENU=The journals were successfully posted.;ESP=Se han registrado correctamente los diarios.';
      Text002@1002 : TextConst 'ENU="It was not possible to post all of the journals. ";ESP="No ha sido posible registrar todos los diarios. "';
      Text003@1003 : TextConst 'ENU=The journals that were not successfully posted are now marked.;ESP=Los diarios que no se han registrado estn marcados.';
      ItemJnlTemplate@1004 : Record 82;
      ItemJnlBatch@1005 : Record 233;
      ItemJnlLine@1006 : Record 83;
      ItemJnlPostBatch@1007 : Codeunit 23;
      JnlWithErrors@1008 : Boolean;

    LOCAL PROCEDURE Code@1();
    BEGIN
      WITH ItemJnlBatch DO BEGIN
        ItemJnlTemplate.GET("Journal Template Name");
        ItemJnlTemplate.TESTFIELD("Force Posting Report",FALSE);

        IF NOT CONFIRM(Text000,FALSE) THEN
          EXIT;

        FIND('-');
        REPEAT
          ItemJnlLine."Journal Template Name" := "Journal Template Name";
          ItemJnlLine."Journal Batch Name" := Name;
          ItemJnlLine."Line No." := 1;
          CLEAR(ItemJnlPostBatch);
          IF ItemJnlPostBatch.RUN(ItemJnlLine) THEN
            MARK(FALSE)
          ELSE BEGIN
            MARK(TRUE);
            JnlWithErrors := TRUE;
          END;
        UNTIL NEXT = 0;

        IF NOT JnlWithErrors THEN
          MESSAGE(Text001)
        ELSE
          MESSAGE(
            Text002 +
            Text003);

        IF NOT FIND('=><') THEN BEGIN
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          FILTERGROUP(0);
          Name := '';
        END;
      END;
    END;

    BEGIN
    END.
  }
}
