OBJECT Codeunit 250 BOMJnlManagement
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    Permissions=TableData 88=imd,
                TableData 234=imd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=BOM;ESP=LM';
      Text001@1001 : TextConst 'ENU=BOM Journal;ESP=Diario L.M.';
      Text002@1002 : TextConst 'ENU=RECURRING;ESP=PERIODICO';
      Text003@1003 : TextConst 'ENU=Recurring BOM Journal;ESP=Diario L.M. peri¢dico';
      Text004@1004 : TextConst 'ENU=DEFAULT;ESP=GENERICO';
      Text005@1005 : TextConst 'ENU=Default Journal;ESP=Diario gen‚rico';
      OpenFromBatch@1006 : Boolean;

    PROCEDURE TemplateSelection@1(FormID@1004 : Integer;RecurringJnl@1000 : Boolean;VAR BOMJnlLine@1003 : Record 89;VAR JnlSelected@1002 : Boolean);
    VAR
      BOMJnlTemplate@1001 : Record 88;
    BEGIN
      JnlSelected := TRUE;

      BOMJnlTemplate.RESET;
      BOMJnlTemplate.SETRANGE("Form ID",FormID);
      BOMJnlTemplate.SETRANGE(Recurring,RecurringJnl);

      CASE BOMJnlTemplate.COUNT OF
        0:
          BEGIN
            BOMJnlTemplate.INIT;
            BOMJnlTemplate.Recurring := RecurringJnl;
            IF NOT RecurringJnl THEN BEGIN
              BOMJnlTemplate.Name := Text000;
              BOMJnlTemplate.Description := Text001;
            END ELSE BEGIN
              BOMJnlTemplate.Name := Text002;
              BOMJnlTemplate.Description := Text003;
            END;
            BOMJnlTemplate.VALIDATE("Form ID");
            BOMJnlTemplate.INSERT;
            COMMIT;
          END;
        1:
          BOMJnlTemplate.FIND('-');
        ELSE
          JnlSelected := FORM.RUNMODAL(0,BOMJnlTemplate) = ACTION::LookupOK;
      END;
      IF JnlSelected THEN BEGIN
        BOMJnlLine.FILTERGROUP := 2;
        BOMJnlLine.SETRANGE("Journal Template Name",BOMJnlTemplate.Name);
        BOMJnlLine.FILTERGROUP := 0;
        IF OpenFromBatch THEN BEGIN
          BOMJnlLine."Journal Template Name" := '';
          FORM.RUN(BOMJnlTemplate."Form ID",BOMJnlLine);
        END;
      END;
    END;

    PROCEDURE TemplateSelectionFromBatch@10(VAR BOMJnlBatch@1000 : Record 234);
    VAR
      BOMJnlLine@1002 : Record 89;
      BOMJnlTemplate@1003 : Record 88;
      JnlSelected@1001 : Boolean;
    BEGIN
      OpenFromBatch := TRUE;
      BOMJnlTemplate.GET(BOMJnlBatch."Journal Template Name");
      BOMJnlTemplate.TESTFIELD("Form ID");
      BOMJnlBatch.TESTFIELD(Name);

      BOMJnlLine.FILTERGROUP := 2;
      BOMJnlLine.SETRANGE("Journal Template Name",BOMJnlTemplate.Name);
      BOMJnlLine.FILTERGROUP := 0;

      BOMJnlLine."Journal Template Name" := '';
      BOMJnlLine."Journal Batch Name" := BOMJnlBatch.Name;
      FORM.RUN(BOMJnlTemplate."Form ID",BOMJnlLine);
    END;

    PROCEDURE OpenJnl@2(VAR CurrentJnlBatchName@1000 : Code[10];VAR BOMJnlLine@1001 : Record 89);
    BEGIN
      CheckTemplateName(BOMJnlLine.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName);
      BOMJnlLine.FILTERGROUP := 2;
      BOMJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      BOMJnlLine.FILTERGROUP := 0;
    END;

    PROCEDURE OpenJnlBatch@12(VAR BOMJnlBatch@1000 : Record 234);
    VAR
      BOMJnlTemplate@1002 : Record 88;
      BOMJnlLine@1004 : Record 89;
      BOMJnlBatch2@1001 : Record 234;
      JnlSelected@1003 : Boolean;
    BEGIN
      IF BOMJnlBatch.GETFILTER("Journal Template Name") <> '' THEN
        EXIT;
      BOMJnlBatch.FILTERGROUP(2);
      IF BOMJnlBatch.GETFILTER("Journal Template Name") <> '' THEN BEGIN
        BOMJnlBatch.FILTERGROUP(0);
        EXIT;
      END;
      BOMJnlBatch.FILTERGROUP(0);

      IF NOT BOMJnlBatch.FIND('-') THEN BEGIN
        IF NOT BOMJnlTemplate.FIND('-') THEN
          TemplateSelection(0,FALSE,BOMJnlLine,JnlSelected);
        IF BOMJnlTemplate.FIND('-') THEN
          CheckTemplateName(BOMJnlTemplate.Name,BOMJnlBatch.Name);
        BOMJnlTemplate.SETRANGE(Recurring,TRUE);
        IF NOT BOMJnlTemplate.FIND('-') THEN
          TemplateSelection(0,TRUE,BOMJnlLine,JnlSelected);
        IF BOMJnlTemplate.FIND('-') THEN
          CheckTemplateName(BOMJnlTemplate.Name,BOMJnlBatch.Name);
        BOMJnlTemplate.SETRANGE(Recurring);
      END;
      BOMJnlBatch.FIND('-');
      JnlSelected := TRUE;
      BOMJnlBatch.CALCFIELDS(Recurring);
      BOMJnlTemplate.SETRANGE(Recurring,BOMJnlBatch.Recurring);
      IF BOMJnlBatch.GETFILTER("Journal Template Name") <> '' THEN
         BOMJnlTemplate.SETRANGE(Name,BOMJnlBatch.GETFILTER("Journal Template Name"));
      CASE BOMJnlTemplate.COUNT OF
        1:
          BOMJnlTemplate.FIND('-');
        ELSE
          JnlSelected := FORM.RUNMODAL(0,BOMJnlTemplate) = ACTION::LookupOK;
      END;
      IF NOT JnlSelected THEN
        ERROR('');

      BOMJnlBatch.FILTERGROUP(2);
      BOMJnlBatch.SETRANGE("Journal Template Name",BOMJnlTemplate.Name);
      BOMJnlBatch.FILTERGROUP(0);
    END;

    PROCEDURE CheckTemplateName@3(CurrentJnlTemplateName@1000 : Code[10];VAR CurrentJnlBatchName@1001 : Code[10]);
    VAR
      BOMJnlBatch@1002 : Record 234;
    BEGIN
      BOMJnlBatch.SETRANGE("Journal Template Name",CurrentJnlTemplateName);
      IF NOT BOMJnlBatch.GET(CurrentJnlTemplateName,CurrentJnlBatchName) THEN BEGIN
        IF NOT BOMJnlBatch.FIND('-') THEN BEGIN
          BOMJnlBatch.INIT;
          BOMJnlBatch."Journal Template Name" := CurrentJnlTemplateName;
          BOMJnlBatch.SetupNewBatch;
          BOMJnlBatch.Name := Text004;
          BOMJnlBatch.Description := Text005;
          BOMJnlBatch.INSERT(TRUE);
          COMMIT;
        END;
        CurrentJnlBatchName := BOMJnlBatch.Name
      END;
    END;

    PROCEDURE CheckName@4(CurrentJnlBatchName@1000 : Code[10];VAR BOMJnlLine@1001 : Record 89);
    VAR
      BOMJnlBatch@1002 : Record 234;
    BEGIN
      BOMJnlBatch.GET(BOMJnlLine.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName);
    END;

    PROCEDURE SetName@5(CurrentJnlBatchName@1000 : Code[10];VAR BOMJnlLine@1001 : Record 89);
    BEGIN
      BOMJnlLine.FILTERGROUP := 2;
      BOMJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      BOMJnlLine.FILTERGROUP := 0;
      IF BOMJnlLine.FIND('-') THEN;
    END;

    PROCEDURE LookupName@6(VAR CurrentJnlBatchName@1000 : Code[10];VAR BomJnlLine@1001 : Record 89) : Boolean;
    VAR
      BOMJnlBatch@1002 : Record 234;
    BEGIN
      COMMIT;
      BOMJnlBatch."Journal Template Name" := BomJnlLine.GETRANGEMAX("Journal Template Name");
      BOMJnlBatch.Name := BomJnlLine.GETRANGEMAX("Journal Batch Name");
      BOMJnlBatch.FILTERGROUP(2);
      BOMJnlBatch.SETRANGE("Journal Template Name",BOMJnlBatch."Journal Template Name");
      BOMJnlBatch.FILTERGROUP(0);
      IF FORM.RUNMODAL(0,BOMJnlBatch) = ACTION::LookupOK THEN BEGIN
        CurrentJnlBatchName := BOMJnlBatch.Name;
        SetName(CurrentJnlBatchName,BomJnlLine)
      END;
    END;

    BEGIN
    END.
  }
}
