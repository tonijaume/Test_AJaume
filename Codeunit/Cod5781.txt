OBJECT Codeunit 5781 Whse. Validate Source Header
{
  OBJECT-PROPERTIES
  {
    Date=21/05/03;
    Time=12:00:00;
    Version List=NAVW13.70;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to change %1 in all related records in warehouse accordingly?;ESP=¨Desea cambiar %1 en todos los registros relacionados con almac‚n?';
      Text001@1002 : TextConst 'ENU=The update has been interrupted to respect the warning.;ESP=Se ha interrumpido la actualizaci¢n para respetar la advertencia.';

    PROCEDURE SalesHeaderVerifyChange@1(VAR NewSalesHeader@1000 : Record 36;VAR OldSalesHeader@1001 : Record 36);
    VAR
      SalesLine@1002 : Record 37;
    BEGIN
      WITH NewSalesHeader DO BEGIN
        IF "Shipping Advice" = OldSalesHeader."Shipping Advice" THEN
          EXIT;
        IF NOT CONFIRM(Text000,FALSE,FIELDCAPTION("Shipping Advice")) THEN
          ERROR(Text001);

        SalesLine.RESET;
        SalesLine.SETRANGE(SalesLine."Document Type",OldSalesHeader."Document Type");
        SalesLine.SETRANGE(SalesLine."Document No.",OldSalesHeader."No.");
        IF SalesLine.FIND('-') THEN
          REPEAT
            ChangeWhseLines(
               DATABASE::"Sales Line",
               SalesLine."Document Type",
               SalesLine."Document No.",
               SalesLine."Line No.",
               0,
               "Shipping Advice");
          UNTIL SalesLine.NEXT = 0;
      END;
    END;

    PROCEDURE TransHeaderVerifyChange@2(VAR NewTransHeader@1000 : Record 5740;VAR OldTransHeader@1001 : Record 5740);
    VAR
      TransLine@1002 : Record 5741;
    BEGIN
      WITH NewTransHeader DO BEGIN
        IF "Shipping Advice" = OldTransHeader."Shipping Advice" THEN
          EXIT;
        IF NOT CONFIRM(Text000,FALSE,FIELDCAPTION("Shipping Advice")) THEN
          ERROR(Text001);

        TransLine.RESET;
        TransLine.SETRANGE(TransLine."Document No.",OldTransHeader."No.");
        IF TransLine.FIND('-') THEN
          REPEAT
            ChangeWhseLines(
               DATABASE::"Transfer Line",
               0, // Outbound Transfer
               TransLine."Document No.",
               TransLine."Line No.",
               0,
               "Shipping Advice");
          UNTIL TransLine.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE ChangeWhseLines@6(SourceType@1000 : Integer;SourceSubType@1001 : Option;SourceNo@1002 : Code[20];SourceLineNo@1003 : Integer;SourceSublineNo@1004 : Integer;ShipAdvice@1005 : Integer);
    VAR
      WhseActivLine@1010 : Record 5767;
      WhseShptLine@1008 : Record 7321;
      WhseWkshLine@1007 : Record 7326;
    BEGIN
      WhseShptLine.SETCURRENTKEY(
        "Source Type","Source Subtype","Source No.","Source Line No.");
      WhseShptLine.SETRANGE("Source Type",SourceType);
      WhseShptLine.SETRANGE("Source Subtype",SourceSubType);
      WhseShptLine.SETRANGE("Source No.",SourceNo);
      WhseShptLine.SETRANGE("Source Line No.",SourceLineNo);
      IF WhseShptLine.FIND('-') THEN
        REPEAT
          WhseShptLine."Shipping Advice" := ShipAdvice;
          WhseShptLine.MODIFY;
        UNTIL WhseShptLine.NEXT = 0;

      WhseActivLine.SETCURRENTKEY(
        "Source Type","Source Subtype","Source No.","Source Line No.","Source Subline No.");
      WhseActivLine.SETRANGE("Source Type",SourceType);
      WhseActivLine.SETRANGE("Source Subtype",SourceSubType);
      WhseActivLine.SETRANGE("Source No.",SourceNo);
      WhseActivLine.SETRANGE("Source Line No.",SourceLineNo);
      WhseActivLine.SETRANGE("Source Subline No.",SourceSublineNo);
      IF WhseActivLine.FIND('-') THEN
        REPEAT
          WhseActivLine."Shipping Advice" := ShipAdvice;
          WhseActivLine.MODIFY;
        UNTIL WhseActivLine.NEXT = 0;

      WhseWkshLine.RESET;
      WhseWkshLine.SETCURRENTKEY(
        "Source Type","Source Subtype","Source No.","Source Line No.");
      WhseWkshLine.SETRANGE("Source Type",SourceType);
      WhseWkshLine.SETRANGE("Source Subtype",SourceSubType);
      WhseWkshLine.SETRANGE("Source No.",SourceNo);
      WhseWkshLine.SETRANGE("Source Line No.",SourceLineNo);
      IF WhseWkshLine.FIND('-') THEN
        REPEAT
          WhseWkshLine."Shipping Advice" := ShipAdvice;
          WhseWkshLine.MODIFY;
        UNTIL WhseWkshLine.NEXT = 0;
    END;

    BEGIN
    END.
  }
}
