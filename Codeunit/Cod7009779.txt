OBJECT Codeunit 7009779 Control uso informes
{
  OBJECT-PROPERTIES
  {
    Date=06/03/13;
    Time=10:45:52;
    Modified=Yes;
    Version List=AIC2009,INFORMES;
  }
  PROPERTIES
  {
    SingleInstance=Yes;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      rUsoInformeTMP@1100217000 : TEMPORARY Record 7009863;
      rUsoInforme@1100217001 : Record 7009863;

    PROCEDURE UsoInforme@1100217000(ReportID@1100217000 : Integer);
    BEGIN
      // UsoInforme

      IF NOT rUsoInformeTMP.GET(COMPANYNAME, ReportID) THEN
      BEGIN
        rUsoInformeTMP.INIT;
        rUsoInformeTMP.Empresa      := COMPANYNAME;
        rUsoInformeTMP."Report ID"  := ReportID;
        rUsoInformeTMP."Primer uso" := CURRENTDATETIME;
        rUsoInformeTMP.INSERT;
      END;

      rUsoInformeTMP."Ultimo uso"     := CURRENTDATETIME;
      rUsoInformeTMP."Ultimo usuario" := USERID;
      rUsoInformeTMP."Numero usos"    += 1;
      rUsoInformeTMP.MODIFY;
    END;

    PROCEDURE SalvarUsoInforme@1100217001();
    BEGIN
      // SalvarUsoInforme

      IF rUsoInformeTMP.FINDSET THEN
      BEGIN
        REPEAT
          IF NOT rUsoInforme.GET(rUsoInformeTMP.Empresa, rUsoInformeTMP."Report ID") THEN
          BEGIN
            rUsoInforme.INIT;
            rUsoInforme.Empresa      := rUsoInformeTMP.Empresa;
            rUsoInforme."Report ID"  := rUsoInformeTMP."Report ID";
            rUsoInforme."Primer uso" := rUsoInformeTMP."Primer uso";
            rUsoInforme.INSERT;
          END;

          rUsoInforme."Ultimo uso"     := rUsoInformeTMP."Ultimo uso";
          rUsoInforme."Ultimo usuario" := rUsoInformeTMP."Ultimo usuario";
          rUsoInforme."Numero usos"    += rUsoInformeTMP."Numero usos";
          rUsoInforme.MODIFY;
        UNTIL rUsoInformeTMP.NEXT = 0;
      END;

      rUsoInformeTMP.DELETEALL;
    END;

    BEGIN
    END.
  }
}
