OBJECT Codeunit 99008505 Purchase Receipt-Accept
{
  OBJECT-PROPERTIES
  {
    Date=15/09/04;
    Time=12:00:00;
    Version List=NAVW14.00;
  }
  PROPERTIES
  {
    TableNo=99008502;
    OnRun=VAR
            InboundPurchaseDocumentHeader@1000 : Record 99008502;
          BEGIN
            InboundPurchaseDocumentHeader := Rec;
            IF NOT Validation(InboundPurchaseDocumentHeader) THEN
              EXIT;
            IF UpdatePurchaseDocument(InboundPurchaseDocumentHeader) AND NOT HideDialogBox THEN
              MESSAGE(Text000 + Text001,InboundPurchaseDocumentHeader."Corresp. Accepted Document No.");
            Rec := InboundPurchaseDocumentHeader;
          END;

  }
  CODE
  {
    VAR
      Text000@1003 : TextConst 'ENU="The referenced purchase order has been ";ESP="Se ha actualizado el pedido de compra referido "';
      Text001@1004 : TextConst 'ENU=updated with the BizTalk purchase receipt.;ESP=con el albar n de compra BizTalk.';
      Text002@1005 : TextConst 'ENU=A BizTalk partner with the number %1 does not exist.;ESP=No existe un socio BizTalk con el n£mero %1.';
      Text003@1006 : TextConst 'ENU=A purchase order has already been updated with a similar BizTalk purchase receipt.;ESP=Ya se ha actualizado un pedido de compra a partir de un albar n de compra BizTalk similar.';
      Text004@1007 : TextConst 'ENU=A posted purchase receipt that is based on a similar BizTalk purchase receipt exists.;ESP=Ya existe un albar n de compra registrado basado en un albar n de compra BizTalk similar.';
      Text005@1008 : TextConst 'ENU=The referenced purchase order cannot be updated because it already contains a vendor shipment number.;ESP=No se puede actualizar el pedido de compra referido porque ya tiene un n£mero de env¡o de proveedor.';
      Text006@1009 : TextConst 'ENU=The referenced purchase order does not exist.;ESP=El pedido de compra referido no existe.';
      Text007@1010 : TextConst 'ENU=No reference to a purchase order exists.;ESP=No existe ninguna referencia a un pedido de compra.';
      Text009@1012 : TextConst 'ENU=The currency code on the inbound purchase document header does not map to any currency specified for the Partner.;ESP=El c¢digo de divisa en la cabecera del documento de compra saliente no tiene equivalencia con ninguna divisa especificada para el socio.';
      Text011@1014 : TextConst 'ENU=Commerce Gateway does not support the acceptance of a %1 whose %2 is %3;ESP=Commerce Gateway no permite aceptar un documento %1 en el que %2 es %3';
      Text013@1016 : TextConst 'ENU=There is a discrepancy between the inbound quantity and the quantity to receive;ESP=Existe discrepancia entre la cantidad entrada y la cantidad a recibir';
      Text014@1017 : TextConst 'ENU=" on the referenced purchase order line in line number %1.";ESP=" en la l¡nea n£mero %1 del pedido de compra referido."';
      Text015@1018 : TextConst 'ENU=There is a discrepancy between the inbound receipt lines and the referenced order lines.;ESP=Existen discrepancias entre las l¡neas del albar n entrada y las l¡neas del pedido referido.';
      Text019@1022 : TextConst 'ENU=A vendor item number is not defined for item number %1;ESP=No se ha definido un n£mero de producto proveedor para el producto n£mero %1.';
      Text020@1023 : TextConst 'ENU=The corresponding vendor item number %1 in quote line no. %2 \;ESP=No se puede encontrar el n£mero producto proveedor %1 correspondiente en la l¡nea de oferta n£m %2 \';
      Text021@1024 : TextConst 'ENU=could not be found in the BizTalk purchase receipt;ESP=en la l¡nea de oferta n§. %2 en el albar n de compra BizTalk.';
      Text022@1025 : TextConst 'ENU=A common item number is not defined for item number %1;ESP=No se definido un n£mero producto com£n  para el producto n£mero %1.';
      Text023@1026 : TextConst 'ENU=The corresponding common item number %1 in quote line no. %2 \;ESP=No se puede encontrar el n£mero producto com£n %1 correspondiente en la l¡nea de oferta n£m %2 \';
      BizTalkPartnerSetup@1000 : Record 99008513;
      PurchOrderHeader@1001 : Record 38;
      ReferencedFound@1002 : Boolean;
      HideDialogBox@1013 : Boolean;

    PROCEDURE Validation@1(VAR InboundPurchaseDocumentHeader@1000 : Record 99008502) : Boolean;
    VAR
      PostedPurchaseHeader@1001 : Record 120;
      BizTalkPartnerCurrencyMap@1002 : Record 99008532;
    BEGIN
      BizTalkPartnerSetup.SETCURRENTKEY("Partner Type","Partner No.");
      BizTalkPartnerSetup.SETRANGE("Partner Type", BizTalkPartnerSetup."Partner Type"::Vendor);
      BizTalkPartnerSetup.SETRANGE("Partner No.", InboundPurchaseDocumentHeader."Buy-from Vendor No.");
      IF NOT BizTalkPartnerSetup.FIND('-') THEN
        ERROR(Text002,InboundPurchaseDocumentHeader."Buy-from Vendor No.");

      IF InboundPurchaseDocumentHeader."Vendor Shipment No." <> '' THEN BEGIN
        PurchOrderHeader.SETCURRENTKEY("Document Type","Buy-from Vendor No.");
        PurchOrderHeader.SETRANGE("Document Type",PurchOrderHeader."Document Type"::Order);
        PurchOrderHeader.SETRANGE("Buy-from Vendor No.",InboundPurchaseDocumentHeader."Buy-from Vendor No.");
        PurchOrderHeader.SETRANGE("Vendor Shipment No.",InboundPurchaseDocumentHeader."Vendor Shipment No.");
        IF PurchOrderHeader.FIND('-') THEN
          ERROR(Text003);

        PostedPurchaseHeader.SETRANGE("Buy-from Vendor No.",InboundPurchaseDocumentHeader."Buy-from Vendor No.");
        PostedPurchaseHeader.SETRANGE("Vendor Shipment No.",InboundPurchaseDocumentHeader."Vendor Shipment No.");
        IF PostedPurchaseHeader.FIND('-') THEN
          ERROR(Text004);
      END;

      PurchOrderHeader.RESET;
      IF InboundPurchaseDocumentHeader."Reference Order No." <> '' THEN BEGIN
        PurchOrderHeader.SETCURRENTKEY("Document Type","Buy-from Vendor No.");
        PurchOrderHeader.SETRANGE("Document Type",PurchOrderHeader."Document Type"::Order);
        PurchOrderHeader.SETRANGE("Buy-from Vendor No.",InboundPurchaseDocumentHeader."Buy-from Vendor No.");
        PurchOrderHeader.SETRANGE("No.",InboundPurchaseDocumentHeader."Reference Order No.");
        IF PurchOrderHeader.FIND('-') THEN  BEGIN
          ReferencedFound := TRUE;
          IF PurchOrderHeader."Vendor Shipment No." <> '' THEN
            ERROR(Text005);
        END ELSE
          ERROR(Text006);
      END ELSE
        ERROR(Text007);

      IF InboundPurchaseDocumentHeader."Currency Code" <> '' THEN BEGIN
        IF BizTalkPartnerCurrencyMap.GET(
          BizTalkPartnerSetup."BizTalk Partner No.",InboundPurchaseDocumentHeader."Currency Code") THEN
          InboundPurchaseDocumentHeader."Currency Code" := BizTalkPartnerCurrencyMap."Currency Code"
        ELSE
          ERROR(Text009);
      END;

      EXIT(TRUE);
    END;

    PROCEDURE UpdatePurchaseDocument@4(VAR InboundPurchaseDocumentHeader@1000 : Record 99008502) : Boolean;
    VAR
      InboundPurchaseDocumentLine@1001 : Record 99008503;
      PurchOrderLine@1002 : Record 39;
      AuxCommentPurchLine@1009 : Record 39;
      BizTalkManagement@1003 : Codeunit 99008508;
      ItemNo@1004 : Code[20];
      LineNo@1005 : Code[4];
      PreviousItemNo@1008 : Code[20];
      PreviousLineNo@1007 : Integer;
      LastLineNo@1010 : Integer;
      IgnoreComments@1006 : Boolean;
    BEGIN
      PurchOrderHeader.VALIDATE("BizTalk Purchase Receipt",TRUE);
      PurchOrderHeader.VALIDATE("Vendor Shipment No.",InboundPurchaseDocumentHeader."Vendor Shipment No.");
      PurchOrderHeader.VALIDATE("Currency Code",InboundPurchaseDocumentHeader."Currency Code");

      CheckLines(InboundPurchaseDocumentHeader);

      PurchOrderLine.SETCURRENTKEY("Document Type","Document No.");
      PurchOrderLine.SETRANGE("Document Type", PurchOrderHeader."Document Type");
      PurchOrderLine.SETRANGE("Document No.", PurchOrderHeader."No.");

      CASE BizTalkPartnerSetup."Purchase Item Number Type" OF
        BizTalkPartnerSetup."Purchase Item Number Type"::"Internal Item No.":
          InboundPurchaseDocumentLine.SETCURRENTKEY("Inbound Document No.","No.");
        BizTalkPartnerSetup."Purchase Item Number Type"::"Vendor Item No.":
          InboundPurchaseDocumentLine.SETCURRENTKEY("Inbound Document No.","Vendor Item No.");
        BizTalkPartnerSetup."Purchase Item Number Type"::"Common Item No.":
          InboundPurchaseDocumentLine.SETCURRENTKEY("Inbound Document No.","Common Item No.");
      END;
      InboundPurchaseDocumentLine.SETRANGE("Inbound Document No.", InboundPurchaseDocumentHeader."Inbound Document No.");

      IgnoreComments := BizTalkPartnerSetup."Update Purchase Comments" =
        BizTalkPartnerSetup."Update Purchase Comments"::"Ignore New and Modified";
      IF NOT IgnoreComments THEN BEGIN
        PurchOrderLine.SETRANGE(Type,PurchOrderLine.Type::" ");
        PurchOrderLine.DELETEALL;
        PurchOrderLine.SETRANGE(Type);
        AuxCommentPurchLine.COPYFILTERS(PurchOrderLine);
        IF AuxCommentPurchLine.FIND('+') THEN
          LastLineNo := AuxCommentPurchLine."Line No." + 10000;
      END;

      LineNo := '0';
      IF InboundPurchaseDocumentLine.FIND('-') THEN
        REPEAT
          LineNo := INCSTR(LineNo);
          CASE InboundPurchaseDocumentLine.Type OF
            InboundPurchaseDocumentLine.Type::" ":
              IF NOT IgnoreComments THEN BEGIN
                AuxCommentPurchLine.INIT;
                AuxCommentPurchLine."Document Type" := PurchOrderHeader."Document Type";
                AuxCommentPurchLine."Document No." := PurchOrderHeader."No.";
                AuxCommentPurchLine."Line No." := LastLineNo;
                AuxCommentPurchLine.Description := InboundPurchaseDocumentLine.Description;
                AuxCommentPurchLine.INSERT;
                LastLineNo := LastLineNo + 10000;
              END;
            InboundPurchaseDocumentLine.Type::Item:
              BEGIN
                CASE BizTalkPartnerSetup."Purchase Item Number Type" OF
                  BizTalkPartnerSetup."Purchase Item Number Type"::"Internal Item No.":
                    BEGIN
                      InboundPurchaseDocumentHeader.TESTFIELD("No.");
                      ItemNo := InboundPurchaseDocumentLine."No.";
                    END;
                  BizTalkPartnerSetup."Purchase Item Number Type"::"Vendor Item No.":
                    BEGIN
                      InboundPurchaseDocumentHeader.TESTFIELD("Buy-from Vendor No.");
                      InboundPurchaseDocumentLine.TESTFIELD("Vendor Item No.");
                      ItemNo := BizTalkManagement.ConvertVendorItemNo(
                        InboundPurchaseDocumentLine."Vendor Item No.",
                        InboundPurchaseDocumentHeader."Buy-from Vendor No.");
                    END;
                  BizTalkPartnerSetup."Purchase Item Number Type"::"Common Item No.":
                    BEGIN
                      InboundPurchaseDocumentLine.TESTFIELD("Common Item No.");
                      ItemNo := BizTalkManagement.ConvertCommonItemNo(
                        InboundPurchaseDocumentLine."Common Item No.");
                    END;
                END;
                IF PreviousItemNo <> ItemNo THEN
                  PreviousLineNo := 0;
                PreviousItemNo := ItemNo;

                PurchOrderLine.SETRANGE("No.",ItemNo);
                PurchOrderLine.SETFILTER("Line No.",'>%1',PreviousLineNo);
                IF PurchOrderLine.FIND('-') THEN BEGIN
                  IF InboundPurchaseDocumentLine.Quantity <> PurchOrderLine."Qty. to Receive" THEN
                      ERROR(Text013 + Text014,LineNo);
                END ELSE
                  ERROR(Text015);
                PreviousLineNo := PurchOrderLine."Line No.";
              END;
            ELSE
              ERROR(Text011,PurchOrderLine.TABLECAPTION,PurchOrderLine.FIELDCAPTION(Type),FORMAT(PurchOrderLine.Type));
          END;
        UNTIL InboundPurchaseDocumentLine.NEXT = 0;

      InboundPurchaseDocumentHeader.Status := InboundPurchaseDocumentHeader.Status::Accepted;
      InboundPurchaseDocumentHeader."Date Processed" := TODAY;
      InboundPurchaseDocumentHeader."Time Processed" := TIME;
      InboundPurchaseDocumentHeader."Corresp. Accepted Document No." := PurchOrderHeader."No.";
      InboundPurchaseDocumentHeader.MODIFY;
      PurchOrderHeader.MODIFY;
      EXIT(TRUE);
    END;

    PROCEDURE CheckLines@3(InboundPurchaseDocumentHeader@1000 : Record 99008502);
    VAR
      InboundPurchaseDocumentLine@1001 : Record 99008503;
      PurchOrderLine@1002 : Record 39;
      BizTalkManagement@1003 : Codeunit 99008508;
      LineNo@1004 : Code[4];
      ItemNo@1005 : Code[20];
      VendorItemNo@1006 : Text[20];
      CommonItemNo@1007 : Code[20];
    BEGIN
      LineNo := '0';
      PurchOrderLine.SETRANGE("Document Type",PurchOrderHeader."Document Type");
      PurchOrderLine.SETRANGE("Document No.", PurchOrderHeader."No.");
      PurchOrderLine.FIND('-');
      REPEAT
        LineNo := INCSTR(LineNo);
        CASE PurchOrderLine.Type OF
          PurchOrderLine.Type::" ":;
          PurchOrderLine.Type::Item:
            BEGIN
              InboundPurchaseDocumentLine.SETCURRENTKEY("Inbound Document No.");
              InboundPurchaseDocumentLine.SETRANGE("Inbound Document No.", InboundPurchaseDocumentHeader."Inbound Document No.");
              CASE BizTalkPartnerSetup."Purchase Item Number Type" OF
                BizTalkPartnerSetup."Purchase Item Number Type"::"Internal Item No.":
                  InboundPurchaseDocumentLine.SETRANGE("No.",PurchOrderLine."No.");
                BizTalkPartnerSetup."Purchase Item Number Type"::"Vendor Item No.":
                  BEGIN
                    VendorItemNo := BizTalkManagement.GetVendorItemNumber(
                      PurchOrderLine."No.",PurchOrderHeader."Buy-from Vendor No.");
                    IF VendorItemNo = '' THEN
                      ERROR(Text019,PurchOrderLine."No.");
                    InboundPurchaseDocumentLine.SETRANGE("Vendor Item No.",VendorItemNo);
                    IF NOT InboundPurchaseDocumentLine.FIND('-') THEN
                      ERROR(Text020 + Text021,VendorItemNo,LineNo);
                  END;
                BizTalkPartnerSetup."Purchase Item Number Type"::"Common Item No.":
                  BEGIN
                    CommonItemNo := BizTalkManagement.GetCommonItemNumber(PurchOrderLine."No.");
                    IF CommonItemNo = '' THEN
                      ERROR(Text022,PurchOrderLine."No.");
                    InboundPurchaseDocumentLine.SETRANGE("Common Item No.",CommonItemNo);
                    IF NOT InboundPurchaseDocumentLine.FIND('-') THEN
                      ERROR(Text023 + Text021,CommonItemNo,LineNo);
                  END;
              END;
            END;
          ELSE
            ERROR(Text011,PurchOrderLine.TABLECAPTION,PurchOrderLine.FIELDCAPTION(Type),FORMAT(PurchOrderLine.Type));
        END;
      UNTIL PurchOrderLine.NEXT = 0;
    END;

    PROCEDURE SetHideDialogBox@5(NewHideDialogBox@1000 : Boolean);
    BEGIN
      HideDialogBox := NewHideDialogBox;
    END;

    BEGIN
    END.
  }
}
