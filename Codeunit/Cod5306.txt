OBJECT Codeunit 5306 Outlook Synch. Export Schema
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    TableNo=472;
    OnRun=BEGIN
            JQEntry := Rec;
            SortedEntitiesBuffer.RESET;
            SortedEntitiesBuffer.DELETEALL;

            OSynchNAVMgt.GetSortedEntities("User ID",SortedEntitiesBuffer,TRUE);
            SendMappingScheme("User ID");
            Rec := JQEntry;
          END;

  }
  CODE
  {
    VAR
      JQEntry@1010 : Record 472;
      OSynchEntity@1005 : Record 5300;
      OSynchField@1004 : Record 5304;
      SortedEntitiesBuffer@1000 : TEMPORARY Record 5306;
      OSynchSetupDetail@1006 : Record 5310;
      OSynchNAVMgt@1001 : Codeunit 5301;
      OSynchTypeConversion@1007 : Codeunit 5302;
      XMLWriter@1002 : Automation "{CAF2CEDD-2636-48EC-AE94-3A2E78BDCAA4} 1.0:{25FBE079-487E-44F2-8352-23B354C2AD0E}:Unknown Automation Server.Unknown Class";
      Text001@1003 : TextConst 'ENU=The synchronization failed because the synchronization data could not be sent from Microsoft Dynamics NAV. Try again later and if the problem persists contact your system administrator.;ESP=La sincronizaci¢n no se pudo realizar porque los datos de sincronizaci¢n no se pudieron enviar desde Microsoft Dynamics NAV. Vuelva a intentarlo m s tarde y, si el problema persiste, p¢ngase en contacto con el administrador del sistema.';
      Text002@1008 : TextConst 'ENU=The synchronization failed because the %1 user has no entities to synchronize. Try again later and if the problem persists contact your system administrator.;ESP=Error en la sincronizaci¢n: el usuario %1 no tiene objetos que sincronizar. Vuelva a intentarlo m s tarde y, si el problema persiste, p¢ngase en contacto con el administrador del sistema.';
      Text003@1009 : TextConst 'ENU=The synchronization failed because the synchronization data from Microsoft Outlook cannot be processed. Try again later and if the problem persists contact your system administrator.;ESP=La sincronizaci¢n no se pudo realizar porque los datos de sincronizaci¢n de Microsoft Outlook no se pudieron procesar. Vuelva a intentarlo m s tarde y, si el problema persiste, p¢ngase en contacto con el administrador del sistema.';

    PROCEDURE SendMappingScheme@36(UserID@1002 : Code[20]);
    VAR
      XMLTextReader@1003 : Automation "{CAF2CEDD-2636-48EC-AE94-3A2E78BDCAA4} 1.0:{8FC8A753-24F6-495F-98BB-E975695CA4C9}:Unknown Automation Server.Unknown Class";
      TempDateTime@1007 : DateTime;
      OutlookCheckSum@1005 : Text[250];
      CurrentCheckSum@1006 : Text[250];
    BEGIN
      IF NOT SortedEntitiesBuffer.FIND('-') THEN
        ERROR(Text002,UserID);

      CLEAR(XMLTextReader);
      CREATE(XMLTextReader);
      TempDateTime := OSynchTypeConversion.LocalDT2UTC(CURRENTDATETIME);

      IF NOT OSynchNAVMgt.GetXMLFromJQEntry(XMLTextReader,JQEntry) THEN
        ERROR(Text003);

      IF ISCLEAR(XMLWriter) THEN
        CREATE(XMLWriter);

      XMLWriter.WriteStartDocument;
      XMLWriter.WriteStartElement('Schema');
      XMLWriter.WriteAttribute('StartSynchTime',FORMAT(OSynchTypeConversion.SetDateTimeFormat(TempDateTime)));

      REPEAT
        OSynchEntity.GET(SortedEntitiesBuffer.Name);
        IF OSynchEntity."Outlook Item" <> '' THEN
          ComposeSynchEntityNode(OSynchEntity,UserID);
      UNTIL SortedEntitiesBuffer.NEXT = 0;

      XMLWriter.WriteEndElement;
      XMLWriter.WriteEndDocument;

      OutlookCheckSum := GetOutlookSchemaCheckSum(XMLTextReader);
      IF OutlookCheckSum <> '' THEN
        CurrentCheckSum := GetCurrentSchemaCheckSum(XMLWriter);

      IF (OutlookCheckSum <> '') AND (OutlookCheckSum = CurrentCheckSum) THEN
        PutCurrentSchemaCheckSum(XMLWriter,CurrentCheckSum,TempDateTime);

      IF NOT OSynchNAVMgt.PutXMLToJQResponse(JQEntry,XMLWriter) THEN BEGIN
        CLEAR(XMLWriter);
        ERROR(Text001);
      END;

      CLEAR(XMLWriter);
    END;

    LOCAL PROCEDURE ComposeSynchEntityNode@35(OSynchEntity1@1000 : Record 5300;UserID@1003 : Code[20]);
    VAR
      OSynchEntityElement@1002 : Record 5301;
      TempOSynchField@1004 : TEMPORARY Record 5304;
      OSynchUserSetup@1001 : Record 5305;
      IntVar@1005 : Integer;
    BEGIN
      IF ISCLEAR(XMLWriter) THEN
        EXIT;

      OSynchEntity1.TESTFIELD(Code);
      OSynchUserSetup.GET(UserID,OSynchEntity1.Code);
      IntVar := OSynchUserSetup."Synch. Direction";

      XMLWriter.WriteStartElement('SynchEntity');
      XMLWriter.WriteAttribute('Code',OSynchEntity1.Code);
      XMLWriter.WriteAttribute('Description',OSynchEntity1.Description);
      XMLWriter.WriteAttribute('OutlookItemType',OSynchEntity1."Outlook Item");
      XMLWriter.WriteAttribute('SynchDirection',FORMAT(IntVar));

      OSynchField.RESET;
      OSynchField.SETRANGE("Synch. Entity Code",OSynchEntity1.Code);
      OSynchField.SETRANGE("Element No.",0);
      OSynchField.SETFILTER("Outlook Object",'<>%1','');
      OSynchField.SETFILTER("Outlook Property",'<>%1','');
      IF OSynchField.FIND('-') THEN
        REPEAT
          TempOSynchField.RESET;
          TempOSynchField.SETRANGE("Synch. Entity Code",OSynchField."Synch. Entity Code");
          TempOSynchField.SETRANGE("Element No.",OSynchField."Element No.");
          TempOSynchField.SETRANGE("Outlook Property",OSynchField."Outlook Property");
          IF NOT TempOSynchField.FIND('-') THEN BEGIN
            TempOSynchField.INIT;
            TempOSynchField := OSynchField;
            TempOSynchField.INSERT;
          END;
        UNTIL OSynchField.NEXT = 0;

      TempOSynchField.RESET;
      IF TempOSynchField.FIND('-') THEN
        REPEAT
          ComposeFieldNode(TempOSynchField);
        UNTIL TempOSynchField.NEXT = 0;

      OSynchSetupDetail.RESET;
      OSynchSetupDetail.SETCURRENTKEY("Table No.");
      OSynchSetupDetail.SETRANGE("Synch. Entity Code",OSynchEntity1.Code);
      OSynchSetupDetail.SETRANGE("User ID",UserID);
      IF OSynchSetupDetail.FIND('-') THEN
        REPEAT
          OSynchEntityElement.GET(OSynchSetupDetail."Synch. Entity Code",OSynchSetupDetail."Element No.");
          IF OSynchEntityElement."Outlook Collection" <> '' THEN
            ComposeCollectionNode(OSynchEntityElement);
        UNTIL OSynchSetupDetail.NEXT = 0;

      XMLWriter.WriteEndElement;
    END;

    LOCAL PROCEDURE ComposeFieldNode@34(OSynchFieldIn@1000 : Record 5304);
    VAR
      Field@1001 : Record 2000000041;
      RecRef@1003 : RecordRef;
      FldRef@1002 : FieldRef;
    BEGIN
      IF ISCLEAR(XMLWriter) THEN
        EXIT;

      OSynchFieldIn.TESTFIELD("Synch. Entity Code");
      OSynchFieldIn.TESTFIELD("Line No.");

      XMLWriter.WriteStartElement('Field');
      IF OSynchFieldIn."Outlook Property" <> '' THEN
        XMLWriter.WriteAttribute('Name',OSynchFieldIn."Outlook Property");

      IF OSynchFieldIn."Read-Only Status" <> OSynchFieldIn."Read-Only Status"::" " THEN BEGIN
        RecRef.GETTABLE(OSynchFieldIn);
        FldRef := RecRef.FIELD(OSynchFieldIn.FIELDNO("Read-Only Status"));

        XMLWriter.WriteAttribute('Read-OnlyStatus',SELECTSTR(OSynchFieldIn."Read-Only Status" + 1,FldRef.OPTIONSTRING));
        RecRef.CLOSE
      END;

      IF OSynchFieldIn."User-Defined" THEN
        XMLWriter.WriteAttribute('User-Defined',OSynchTypeConversion.SetBoolFormat(OSynchFieldIn."User-Defined"));

      IF OSynchFieldIn."Search Field" THEN
        XMLWriter.WriteAttribute('SearchKey',OSynchTypeConversion.SetBoolFormat(OSynchFieldIn."Search Field"));

      IF OSynchFieldIn."Table No." = 0 THEN
        Field.GET(OSynchFieldIn."Master Table No.",OSynchFieldIn."Field No.")
      ELSE
        Field.GET(OSynchFieldIn."Table No.",OSynchFieldIn."Field No.");

      IF Field.Type = Field.Type::BLOB THEN
        XMLWriter.WriteAttribute('Base64',OSynchTypeConversion.SetBoolFormat(TRUE));

      XMLWriter.WriteEndElement;
    END;

    LOCAL PROCEDURE ComposeCollectionNode@33(OSynchEntityElementIn@1000 : Record 5301);
    VAR
      TempOSynchField@1002 : TEMPORARY Record 5304;
      OSynchDependency@1001 : Record 5311;
    BEGIN
      IF ISCLEAR(XMLWriter) THEN
        EXIT;

      OSynchEntityElementIn.TESTFIELD("Synch. Entity Code");
      OSynchEntityElementIn.TESTFIELD("Element No.");

      XMLWriter.WriteStartElement('Collection');
      XMLWriter.WriteAttribute('Name',OSynchEntityElementIn."Outlook Collection");

      OSynchField.RESET;
      OSynchField.SETRANGE("Synch. Entity Code",OSynchEntityElementIn."Synch. Entity Code");
      OSynchField.SETRANGE("Element No.",OSynchEntityElementIn."Element No.");
      OSynchField.SETFILTER("Outlook Object",'<>%1','');
      OSynchField.SETFILTER("Outlook Property",'<>%1','');
      IF OSynchField.FIND('-') THEN
        REPEAT
          TempOSynchField.RESET;
          TempOSynchField.SETRANGE("Synch. Entity Code",OSynchField."Synch. Entity Code");
          TempOSynchField.SETRANGE("Element No.",OSynchField."Element No.");
          TempOSynchField.SETRANGE("Outlook Property",OSynchField."Outlook Property");
          IF NOT TempOSynchField.FIND('-') THEN BEGIN
            TempOSynchField.INIT;
            TempOSynchField := OSynchField;
            TempOSynchField.INSERT;
          END;
        UNTIL OSynchField.NEXT = 0;

      TempOSynchField.RESET;
      IF TempOSynchField.FIND('-') THEN
        REPEAT
          ComposeFieldNode(TempOSynchField);
        UNTIL TempOSynchField.NEXT = 0;

      OSynchDependency.RESET;
      OSynchDependency.SETRANGE("Synch. Entity Code",TempOSynchField."Synch. Entity Code");
      OSynchDependency.SETRANGE("Element No.",TempOSynchField."Element No.");
      IF OSynchDependency.FIND('-') THEN BEGIN
        TempOSynchField.SETRANGE("Search Field",TRUE);
        IF TempOSynchField.FIND('-') THEN
          REPEAT
            ComposeSearchFieldNode(TempOSynchField,OSynchDependency);
          UNTIL TempOSynchField.NEXT = 0;
      END;

      XMLWriter.WriteEndElement;
    END;

    LOCAL PROCEDURE ComposeSearchFieldNode@32(OSynchFieldIn@1000 : Record 5304;OSynchDependency@1004 : Record 5311);
    VAR
      OSynchField1@1002 : Record 5304;
      SearchKeyBuffer@1001 : TEMPORARY Record 5306;
    BEGIN
      IF ISCLEAR(XMLWriter) THEN
        EXIT;

      XMLWriter.WriteStartElement('SearchKey');
      XMLWriter.WriteAttribute('Field',OSynchFieldIn."Outlook Property");

      IF OSynchDependency.FIND('-') THEN
        REPEAT
          OSynchField.RESET;
          OSynchField.SETRANGE("Synch. Entity Code",OSynchFieldIn."Synch. Entity Code");
          OSynchField.SETRANGE("Element No.",OSynchFieldIn."Element No.");
          OSynchField.SETRANGE("Outlook Property",OSynchFieldIn."Outlook Property");
          IF OSynchField.FIND('-') THEN
            REPEAT
              OSynchEntity.GET(OSynchDependency."Depend. Synch. Entity Code");
              OSynchField1.RESET;
              OSynchField1.SETRANGE("Synch. Entity Code",OSynchEntity.Code);
              OSynchField1.SETRANGE("Element No.",0);
              IF OSynchField."Table No." = 0 THEN
                IF OSynchField."Master Table No." = OSynchEntity."Table No." THEN
                  OSynchField1.SETRANGE("Table No.",0)
                ELSE
                  OSynchField1.SETRANGE("Table No.",OSynchField."Master Table No.")
              ELSE
                IF OSynchField."Table No." = OSynchEntity."Table No." THEN
                  OSynchField1.SETRANGE("Table No.",0)
                ELSE
                  OSynchField1.SETRANGE("Table No.",OSynchField."Table No.");

              OSynchField1.SETRANGE("Field No.",OSynchField."Field No.");
              IF OSynchField1.FIND('-') THEN BEGIN
                SearchKeyBuffer.RESET;
                SearchKeyBuffer.SETRANGE(Name,OSynchEntity.Code);
                IF NOT SearchKeyBuffer.FIND('-') THEN BEGIN
                  XMLWriter.WriteStartElement('Entity');
                  XMLWriter.WriteAttribute('Name',OSynchEntity.Code);
                  XMLWriter.WriteAttribute('Field',OSynchField1."Outlook Property");
                  XMLWriter.WriteEndElement;

                  SearchKeyBuffer.INIT;
                  SearchKeyBuffer."Entry No." := SearchKeyBuffer."Entry No." + 1;
                  SearchKeyBuffer.Name := OSynchEntity.Code;
                  SearchKeyBuffer.INSERT;
                END;
              END;
            UNTIL OSynchField.NEXT = 0;
        UNTIL OSynchDependency.NEXT = 0;

      XMLWriter.WriteEndElement;
    END;

    LOCAL PROCEDURE GetOutlookSchemaCheckSum@1(XmlTextReader@1000 : Automation "{CAF2CEDD-2636-48EC-AE94-3A2E78BDCAA4} 1.0:{8FC8A753-24F6-495F-98BB-E975695CA4C9}:Unknown Automation Server.Unknown Class") CheckSum : Text[250];
    VAR
      Container@1003 : Automation "{CAF2CEDD-2636-48EC-AE94-3A2E78BDCAA4} 1.0:{2884F16D-75BF-4E3D-A1B7-8FFF455F37ED}:Unknown Automation Server.Unknown Class";
      RootIterator@1002 : Text[38];
    BEGIN
      IF ISCLEAR(XmlTextReader) THEN
        ERROR(Text003);

      IF XmlTextReader.RootLocalName <> 'Schema' THEN
        ERROR(Text003);

      IF XmlTextReader.SelectElements(RootIterator,'child::CheckSum') > 0 THEN BEGIN
        CLEAR(Container);
        CREATE(Container);
        Container := XmlTextReader.GetLongText(RootIterator);
        Container.MoveFirst;
        Container.ReadNext(250,CheckSum);
      END;
    END;

    LOCAL PROCEDURE GetCurrentSchemaCheckSum@37(VAR XMLTextWriter@1003 : Automation "{CAF2CEDD-2636-48EC-AE94-3A2E78BDCAA4} 1.0:{25FBE079-487E-44F2-8352-23B354C2AD0E}:Unknown Automation Server.Unknown Class") CheckSum : Text[250];
    VAR
      XmlTextReader@1002 : Automation "{CAF2CEDD-2636-48EC-AE94-3A2E78BDCAA4} 1.0:{8FC8A753-24F6-495F-98BB-E975695CA4C9}:Unknown Automation Server.Unknown Class";
      HashCalculator@1007 : Automation "{CAF2CEDD-2636-48EC-AE94-3A2E78BDCAA4} 1.0:{44380697-014C-4AFE-8F9E-F81725E51A2F}:Unknown Automation Server.Unknown Class";
      Container@1000 : Automation "{CAF2CEDD-2636-48EC-AE94-3A2E78BDCAA4} 1.0:{2884F16D-75BF-4E3D-A1B7-8FFF455F37ED}:Unknown Automation Server.Unknown Class";
    BEGIN
      CREATE(XmlTextReader);
      CREATE(HashCalculator);
      CREATE(Container);

      XmlTextReader.Initialize(XMLTextWriter);
      Container := XmlTextReader.ToLongText;
      CheckSum := HashCalculator.CalculateFromLongTextBase64(Container);

      CLEAR(HashCalculator);
      CLEAR(XmlTextReader);
      CLEAR(Container);
    END;

    LOCAL PROCEDURE PutCurrentSchemaCheckSum@3(VAR XMLTextWriter@1003 : Automation "{CAF2CEDD-2636-48EC-AE94-3A2E78BDCAA4} 1.0:{25FBE079-487E-44F2-8352-23B354C2AD0E}:Unknown Automation Server.Unknown Class";CheckSumText@1002 : Text[250];StartDateTime@1000 : DateTime);
    VAR
      TempOSynchLink@1001 : TEMPORARY Record 5302;
      EntryIDContainer@1005 : Automation "{CAF2CEDD-2636-48EC-AE94-3A2E78BDCAA4} 1.0:{2884F16D-75BF-4E3D-A1B7-8FFF455F37ED}:Unknown Automation Server.Unknown Class";
      OutStrm@1004 : OutStream;
    BEGIN
      CLEAR(XMLTextWriter);
      CLEAR(EntryIDContainer);

      CREATE(XMLTextWriter);
      CREATE(EntryIDContainer);

      XMLTextWriter.WriteStartDocument;
      XMLTextWriter.WriteStartElement('Schema');
      XMLWriter.WriteAttribute('StartSynchTime',FORMAT(OSynchTypeConversion.SetDateTimeFormat(StartDateTime)));

      XMLTextWriter.WriteStartElement('CheckSum');
      IF CheckSumText <> '' THEN BEGIN
        CLEAR(TempOSynchLink);
        CLEAR(OutStrm);
        TempOSynchLink.RESET;
        TempOSynchLink.INIT;
        TempOSynchLink."Outlook Entry ID".CREATEOUTSTREAM(OutStrm);
        IF OutStrm.WRITETEXT(CheckSumText,STRLEN(CheckSumText)) > 0 THEN BEGIN
          TempOSynchLink.CALCFIELDS("Outlook Entry ID");
          IF TempOSynchLink.GetEntryID(EntryIDContainer) THEN
            XMLTextWriter.WriteLongText(EntryIDContainer);
        END;
      END;
      XMLTextWriter.WriteEndElement;
      XMLTextWriter.WriteEndElement;
    END;

    BEGIN
    END.
  }
}
