OBJECT Codeunit 412 Common Dialog Management
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text003@1003 : TextConst 'ENU=Text Files (*.txt)|*.txt|All Files (*.*)|*.*;ESP=Fich. texto (*.txt)|*.txt|Todos fich. (*.*)|*.*';
      Text004@1004 : TextConst 'ENU=Microsoft Excel Files (*.xl*)|*.xl*|All Files (*.*)|*.*;ESP=Fich. Microsoft Excel (*.xl*)|*.xl*|Todos fich. (*.*)|*.*';
      Text005@1005 : TextConst 'ENU=Word Documents (*.doc)|*.doc|All Files (*.*)|*.*;ESP=Documentos Word (*.doc)|*.doc|Todos fich. (*.*)|*.*';
      Text006@1006 : TextConst 'ENU=XML files (*.xml)|*.xml|All Files (*.*)|*.*;ESP=Archivos XML (*.xml)|*.xml|Todos los archivos (*.*)|*.*';
      Text007@1007 : TextConst 'ENU=HTM files (*.htm)|*.htm|All Files (*.*)|*.*;ESP=Archivos HTM (*.htm)|*.htm|Todos los archivos (*.*)|*.*';
      Text008@1008 : TextConst 'ENU=XML Schema Files (*.xsd)|*.xsd|All Files (*.*)|*.*;ESP=Archivos de esquema XML (*.xsd)|*.xsd|Todos los archivos (*.*)|*.*';
      Text009@1009 : TextConst 'ENU=XSL Transform Files(*.xslt)|*.xslt|All Files (*.*)|*.*;ESP=Archivos de transformaci¢n XSL (*.xslt)|*.xslt|Todos los archivos (*.*)|*.*';

    PROCEDURE OpenFile@1(WindowTitle@1000 : Text[50];DefaultFileName@1001 : Text[1024];DefaultFileType@1002 : ' ,Text,Excel,Word,Custom,Xml,Htm,Xsd,Xslt';FilterString@1003 : Text[250];Action@1004 : 'Open,Save') : Text[260];
    VAR
      CommonDialogControl@1005 : OCX "{F9043C85-F6F2-101A-A3C9-08002B2F49FB}:'Microsoft Common Dialog Control, version 6.0'";
      Filter@1007 : Text[255];
    BEGIN
      IF DefaultFileType = DefaultFileType::Custom THEN BEGIN
        GetDefaultFileType(DefaultFileName,DefaultFileType);
        Filter := FilterString;
      END ELSE
        Filter := GetFilterString(DefaultFileType);

      CommonDialogControl.MaxFileSize := 2048;
      CommonDialogControl.FileName := DefaultFileName;
      CommonDialogControl.DialogTitle := WindowTitle;
      CommonDialogControl.Filter := Filter;
      CommonDialogControl.InitDir := DefaultFileName;

      IF Action = Action::Open THEN
        CommonDialogControl.ShowOpen
      ELSE
        CommonDialogControl.ShowSave;
      EXIT(CommonDialogControl.FileName);
    END;

    PROCEDURE OpenFileWithName@2(DefaultFileName@1001 : Text[1024]) : Text[260];
    VAR
      DefaultFileType@1002 : ' ,Text,Excel,Word,Custom,Xml,Htm,Xsd,Xslt';
      FilterString@1003 : Text[250];
      Action@1004 : 'Open,Save';
    BEGIN
      GetDefaultFileType(DefaultFileName,DefaultFileType);
      IF DefaultFileType = DefaultFileType::Custom THEN
        FilterString := Text003;

      EXIT(OpenFile('',DefaultFileName,DefaultFileType,FilterString,Action::Open));
    END;

    LOCAL PROCEDURE GetDefaultFileType@3(DefaultFileName@1001 : Text[1024];VAR DefaultFileType@1002 : ' ,Text,Excel,Word,Custom,Xml,Htm,Xsd,Xslt');
    BEGIN
      CASE TRUE OF
        CheckFileNameForFileType(DefaultFileName,'.DOC'):
          DefaultFileType := DefaultFileType::Word;
        CheckFileNameForFileType(DefaultFileName,'.XLS'):
          DefaultFileType := DefaultFileType::Excel;
        CheckFileNameForFileType(DefaultFileName,'.TXT'):
          DefaultFileType := DefaultFileType::Custom;
        CheckFileNameForFileType(DefaultFileName,'.XML'):
          DefaultFileType := DefaultFileType::Xml;
        CheckFileNameForFileType(DefaultFileName,'.HTM'):
          DefaultFileType := DefaultFileType::Htm;
        CheckFileNameForFileType(DefaultFileName,'.XSD'):
          DefaultFileType := DefaultFileType::Xsd;
        CheckFileNameForFileType(DefaultFileName,'.XSLT'):
          DefaultFileType := DefaultFileType::Xslt;
        ELSE
          DefaultFileType := DefaultFileType::Custom;
      END;
    END;

    LOCAL PROCEDURE CheckFileNameForFileType@4(DefaultFileName@1001 : Text[1024];FileExtension@1002 : Text[5]) : Boolean;
    VAR
      Position@1003 : Integer;
    BEGIN
      Position := STRPOS(UPPERCASE(DefaultFileName),FileExtension);
      EXIT((Position > 0) AND (Position - 1 = STRLEN(DefaultFileName) - STRLEN(FileExtension)));
    END;

    PROCEDURE GetFilterString@5(FileType@1000 : ' ,Text,Excel,Word,Custom,Xml,Htm,Xsd,Xslt') : Text[255];
    BEGIN
      CASE FileType OF
        FileType::Text:
          EXIT(Text003);
        FileType::Excel:
          EXIT(Text004);
        FileType::Word:
          EXIT(Text005);
        FileType::Xml:
          EXIT(Text006);
        FileType::Htm:
          EXIT(Text007);
        FileType::Xsd:
          EXIT(Text008);
        FileType::Xslt:
          EXIT(Text009);
        FileType::Custom:
          EXIT('');
      END;
    END;

    BEGIN
    END.
  }
}
