OBJECT XMLport 11 IC Dimension Import/Export
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=IC Dimension Import/Export;
               ESP=Importaci¢n/Exportaci¢n de dimensi¢n IC];
    Format/Evaluate=XML Format/Evaluate;
    OnPreXMLport=VAR
                   ICDim2@1000 : Record 411;
                   ICDimVal2@1001 : Record 412;
                 BEGIN
                   TempICDim.DELETEALL;
                   TempICDimVal.DELETEALL;

                   IF ICDim2.FIND('-') THEN
                     REPEAT
                       TempICDim := ICDim2;
                       TempICDim.INSERT;
                     UNTIL ICDim2.NEXT = 0;

                   IF ICDimVal2.FIND('-') THEN
                     REPEAT
                       TempICDimVal := ICDimVal2;
                       TempICDimVal.INSERT;
                     UNTIL ICDimVal2.NEXT = 0;
                 END;

    OnPostXMLport=VAR
                    OrgICDim@1000 : Record 411;
                    OrgICDimVal@1002 : Record 412;
                    MsgTxt@1001 : Text[1024];
                  BEGIN
                    IF XMLInbound THEN BEGIN
                      IF TempICDimVal.FIND('-') THEN
                        REPEAT
                          Deleted[2] := Deleted[2] + 1;
                          OrgICDimVal.GET(TempICDimVal."Dimension Code",TempICDimVal.Code);
                          OrgICDimVal.DELETE;
                        UNTIL TempICDimVal.NEXT = 0;
                      IF TempICDim.FIND('-') THEN
                        REPEAT
                          Deleted[1] := Deleted[1] + 1;
                          OrgICDim.GET(TempICDim.Code);
                          OrgICDim.DELETE;
                        UNTIL TempICDim.NEXT = 0;

                      Inserted[1] := Inserted[1] + Inserted[2];
                      Modified[1] := Modified[1] + Modified[2];
                      Deleted[1] := Deleted[1] + Deleted[2];

                      IF Inserted[1] > 0 THEN
                        IF Inserted[1] = 1 THEN
                          MsgTxt := STRSUBSTNO(Text001,Inserted[1])
                        ELSE
                          MsgTxt := STRSUBSTNO(Text002,Inserted[1]);

                      IF Modified[1] > 0 THEN BEGIN
                        IF MsgTxt <> '' THEN
                          MsgTxt := MsgTxt + '\';
                        IF Modified[1] = 1 THEN
                          MsgTxt := MsgTxt + STRSUBSTNO(Text003,Modified[1])
                        ELSE
                          MsgTxt := MsgTxt + STRSUBSTNO(Text004,Modified[1]);
                      END;

                      IF Deleted[1] > 0 THEN BEGIN
                        IF MsgTxt <> '' THEN
                          MsgTxt := MsgTxt + '\';
                        IF Deleted[1] = 1 THEN
                          MsgTxt := MsgTxt + STRSUBSTNO(Text005,Deleted[1])
                        ELSE
                          MsgTxt := MsgTxt + STRSUBSTNO(Text006,Deleted[1]);
                      END;

                      IF Inserted[1] + Deleted[1] + Modified[1] = 0 THEN
                        MsgTxt := Text000;

                      MESSAGE(MsgTxt);
                    END;
                  END;

  }
  ELEMENTS
  {
    { [{8524A015-7B03-4616-B7ED-DF755D9743DB}];  ;ICDimensions        ;Element ;Text     }

    { [{E4461354-5312-40F1-A247-BDD47F13F46A}];1 ;ICDim               ;Element ;Table   ;
                                                  VariableName=ICDim;
                                                  SourceTable=Table411;
                                                  Import::OnBeforeInsertRecord=VAR
                                                                                 OrgICDim@1000 : Record 411;
                                                                               BEGIN
                                                                                 XMLInbound := TRUE;

                                                                                 IF TempICDim.GET(ICDim.Code) THEN BEGIN
                                                                                   IF (ICDim.Name <> TempICDim.Name) OR (ICDim.Blocked <> TempICDim.Blocked) THEN
                                                                                     Modified[1] := Modified[1] + 1;
                                                                                   ICDim."Map-to Dimension Code" := TempICDim."Map-to Dimension Code";
                                                                                   OrgICDim.GET(ICDim.Code);
                                                                                   OrgICDim.DELETE;
                                                                                   TempICDim.DELETE;
                                                                                 END ELSE
                                                                                   Inserted[1] := Inserted[1] + 1;
                                                                               END;
                                                                                }

    { [{7FD30CCA-3F17-4C85-BA3B-208887A132B9}];2 ;Code                ;Attribute;Field  ;
                                                  DataType=Code;
                                                  SourceField=ICDim::Code }

    { [{19736BB9-0048-4FD2-A5BC-71298E64E0CC}];2 ;Name                ;Attribute;Field  ;
                                                  DataType=Text;
                                                  SourceField=ICDim::Name }

    { [{19A541D4-F864-48F5-81A7-C4783B9E9BCC}];2 ;Blocked             ;Attribute;Field  ;
                                                  DataType=Boolean;
                                                  SourceField=ICDim::Blocked }

    { [{B4CF53F3-D956-4244-9D99-FF99B46CA65B}];2 ;ICDimVal            ;Element ;Table   ;
                                                  VariableName=ICDimVal;
                                                  SourceTable=Table412;
                                                  LinkFields=Field1=FIELD(Field1);
                                                  LinkTable=ICDim;
                                                  Import::OnBeforeInsertRecord=VAR
                                                                                 OrgICDimVal@1000 : Record 412;
                                                                               BEGIN
                                                                                 XMLInbound := TRUE;

                                                                                 IF TempICDimVal.GET(ICDimVal."Dimension Code",ICDimVal.Code) THEN BEGIN
                                                                                   IF (ICDimVal.Name <> TempICDimVal.Name) OR
                                                                                      (ICDimVal."Dimension Value Type" <> TempICDimVal."Dimension Value Type") OR
                                                                                      (ICDimVal.Blocked <> TempICDimVal.Blocked)
                                                                                   THEN
                                                                                     Modified[2] := Modified[2] + 1;
                                                                                   ICDimVal."Map-to Dimension Code" := TempICDimVal."Map-to Dimension Code";
                                                                                   ICDimVal."Map-to Dimension Value Code" := TempICDimVal."Map-to Dimension Value Code";
                                                                                   OrgICDimVal.GET(ICDimVal."Dimension Code",ICDimVal.Code);
                                                                                   OrgICDimVal.DELETE;
                                                                                   TempICDimVal.DELETE;
                                                                                 END ELSE
                                                                                   Inserted[2] := Inserted[2] + 1;
                                                                               END;
                                                                                }

    { [{38605CEA-CC70-47D8-AAF5-D4BACDBCF9BE}];3 ;DimCode             ;Attribute;Field  ;
                                                  DataType=Code;
                                                  SourceField=ICDimVal::Dimension Code }

    { [{9FD32A7E-8E8C-499A-81E5-C38347C94827}];3 ;Code                ;Attribute;Field  ;
                                                  DataType=Code;
                                                  SourceField=ICDimVal::Code }

    { [{D93AF4F9-F634-48D7-B149-665782D4812C}];3 ;Name                ;Attribute;Field  ;
                                                  DataType=Text;
                                                  SourceField=ICDimVal::Name }

    { [{44841DED-9C5E-4F76-AB72-1310E7801CD1}];3 ;DimValType          ;Attribute;Field  ;
                                                  DataType=Option;
                                                  SourceField=ICDimVal::Dimension Value Type }

    { [{AF61B7A1-16BA-4BC2-A98B-E835578CDBA8}];3 ;Blocked             ;Attribute;Field  ;
                                                  DataType=Boolean;
                                                  SourceField=ICDimVal::Blocked }

    { [{5CF17052-F8C2-4372-BA66-6C3D25AF2991}];3 ;Indentation         ;Attribute;Field  ;
                                                  DataType=Integer;
                                                  SourceField=ICDimVal::Indentation }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      TempICDim@1004 : TEMPORARY Record 411;
      TempICDimVal@1012 : TEMPORARY Record 412;
      XMLInbound@1003 : Boolean;
      Inserted@1002 : ARRAY [2] OF Integer;
      Deleted@1001 : ARRAY [2] OF Integer;
      Modified@1000 : ARRAY [2] OF Integer;
      Text000@1011 : TextConst 'ENU=There were no changes.;ESP=No hubo cambios.';
      Text001@1010 : TextConst 'ENU=%1 record was added.;ESP=Se agreg¢ %1 registro.';
      Text002@1009 : TextConst 'ENU=%1 records were added.;ESP=Se agregaron %1 registros.';
      Text003@1008 : TextConst 'ENU=%1 record was updated.;ESP=Se actualiz¢ %1 registro.';
      Text004@1007 : TextConst 'ENU=%1 records were updated.;ESP=Se actualizaron %1 registros.';
      Text005@1006 : TextConst 'ENU=%1 record was deleted.;ESP=Se ha eliminado registro %1.';
      Text006@1005 : TextConst 'ENU=%1 records were deleted.;ESP=Se han eliminado %1 registros.';

    BEGIN
    END.
  }
}
