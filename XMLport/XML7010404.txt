OBJECT XMLport 7010404 Export polizas SAT
{
  OBJECT-PROPERTIES
  {
    Date=19/11/14;
    Time=18:51:33;
    Version List=AIC2009,CNT-CAR-14090;
  }
  PROPERTIES
  {
  }
  ELEMENTS
  {
    { [{418521DC-4D36-47C9-8747-8A54234C4DC1}];  ;rootPolizas         ;Element ;Text     }

    { [{12ECCDC4-F2A8-4C40-BDED-C4E211AB1C79}];1 ;Polizas             ;Element ;Text     }

    { [{37517AF7-C292-49CC-9FE9-54A191386CC0}];2 ;Poliza              ;Element ;Table   ;
                                                  SourceTable=Table45;
                                                  SourceTableView=SORTING(Field10700);
                                                  Export::OnPreXMLItem=BEGIN
                                                                         "G/L Register".SETRANGE("G/L Register"."Posting Date",vFecIni,vFecFin);
                                                                       END;

                                                  Export::OnAfterGetRecord=VAR
                                                                             rMov@1000000000 : Record 17;
                                                                             rMovBan@1000000001 : Record 271;
                                                                             rMovPro@1000000002 : Record 25;
                                                                             rMovCli@1000000003 : Record 21;
                                                                           BEGIN
                                                                             TipoCab := '1';

                                                                             CLEAR(NumeroPoliza);
                                                                             CLEAR(Descripcion);
                                                                             CLEAR(Divisa);
                                                                             CLEAR(NumeroFactura);

                                                                             // Buscamos si es pago, cobro u otros
                                                                             TipoPoliza := text003;
                                                                             rMov.RESET;
                                                                             rMov.SETCURRENTKEY("Document Date","Document Type","Source Type","Source No.");
                                                                             rMov.SETRANGE(rMov."Entry No.", "G/L Register"."From Entry No.", "G/L Register"."To Entry No.");
                                                                             IF rMov.FINDFIRST THEN BEGIN
                                                                               IF rMov."Document Type" = rMov."Document Type"::Payment THEN BEGIN
                                                                                 rMov.SETRANGE(rMov."Source Type", rMov."Source Type"::Customer);
                                                                                 IF rMov.FINDFIRST THEN
                                                                                   TipoPoliza := text001;
                                                                                 rMov.SETRANGE(rMov."Source Type", rMov."Source Type"::Vendor);
                                                                                 IF rMov.FINDFIRST THEN
                                                                                   TipoPoliza := text002;
                                                                               END;
                                                                               NumeroPoliza := rMov."Document No.";
                                                                               Descripcion := rMov.Description;

                                                                               // Buscamos el mov banco
                                                                               rMovBan.RESET;
                                                                               rMovBan.SETCURRENTKEY(rMovBan."Transaction No.");
                                                                               rMovBan.SETRANGE(rMovBan."Transaction No.", "G/L Register"."No.");
                                                                               IF rMovBan.FINDFIRST THEN BEGIN
                                                                                 Divisa := rMovBan."Currency Code";
                                                                               END;

                                                                               // Buscamos el tipo de cambio
                                                                               rMovCli.RESET;
                                                                               rMovCli.SETCURRENTKEY(rMovCli."Transaction No.");
                                                                               rMovCli.SETRANGE(rMovCli."Transaction No.", "G/L Register"."No.");
                                                                               IF rMovCli.FINDFIRST THEN BEGIN
                                                                                 TipoCambio := FORMAT(1/rMovCli."Adjusted Currency Factor");
                                                                               END;
                                                                               rMovPro.RESET;
                                                                               rMovPro.SETCURRENTKEY(rMovPro."Transaction No.");
                                                                               rMovPro.SETRANGE(rMovPro."Transaction No.", "G/L Register"."No.");
                                                                               IF rMovPro.FINDFIRST THEN BEGIN
                                                                                 TipoCambio := FORMAT(1/rMovPro."Adjusted Currency Factor");
                                                                                 NumeroFactura := rMovPro."External Document No.";
                                                                               END;
                                                                             END;
                                                                           END;
                                                                            }

    { [{90FF0C73-5062-4D34-9FD0-1151F9B73CF6}];3 ;Tipo                ;Element ;Text    ;
                                                  VariableName=TipoCab }

    { [{2898C3D4-B20A-4295-AF63-8C681CB61530}];3 ;TipoPoliza          ;Element ;Text    ;
                                                  VariableName=TipoPoliza }

    { [{865505CD-A895-4C7C-874F-4BB6E23A446C}];3 ;NumeroPoliza        ;Element ;Text    ;
                                                  VariableName=NumeroPoliza }

    { [{653705C4-4504-4EA6-A6D0-33ADBBD9DA70}];3 ;Fecha               ;Element ;Field   ;
                                                  DataType=Date;
                                                  SourceField=G/L Register::Posting Date }

    { [{4D928486-7B0B-4C4D-A8A0-C30DDFE50904}];3 ;Concepto            ;Element ;Text    ;
                                                  VariableName=Descripcion }

    { [{80B8B980-1E22-49E6-AF77-E7F3F4BD3127}];3 ;Detalles            ;Element ;Text     }

    { [{A52EF5E5-D713-427B-BD27-B025FAAF0649}];4 ;Detalle             ;Element ;Table   ;
                                                  SourceTable=Table17;
                                                  SourceTableView=SORTING(Field52)
                                                                  ORDER(Ascending);
                                                  LinkFields=Field52=FIELD(Field1);
                                                  LinkTable=G/L Register;
                                                  Export::OnAfterGetRecord=BEGIN
                                                                             TipoDet := '2';
                                                                           END;
                                                                            }

    { [{A89B110C-03E8-4586-912E-2B64AFC10F0A}];5 ;Tipo                ;Element ;Text    ;
                                                  VariableName=TipoDet }

    { [{A4850FCE-681A-4EFE-A42A-11BC741B3D10}];5 ;Cuenta              ;Element ;Field   ;
                                                  DataType=Text;
                                                  SourceField=G/L Entry::G/L Account No. }

    { [{8F658B71-975C-478C-B011-420E82A110C1}];5 ;Concepto            ;Element ;Field   ;
                                                  DataType=Text;
                                                  SourceField=G/L Entry::Description }

    { [{522C299D-6D99-4D19-B520-F97621EDF059}];5 ;Debe                ;Element ;Field   ;
                                                  DataType=Decimal;
                                                  SourceField=G/L Entry::Debit Amount }

    { [{A3D70BB2-92D7-44BB-8FBC-B2080A09D78B}];5 ;Haber               ;Element ;Field   ;
                                                  DataType=Decimal;
                                                  SourceField=G/L Entry::Credit Amount }

    { [{C22940EA-8479-4A56-BA05-B34BB39CB20D}];5 ;Moneda              ;Element ;Text    ;
                                                  VariableName=Divisa;
                                                  DataType=Decimal }

    { [{E8663D5E-65DC-43A3-B8A4-AF4E8F7F57DD}];5 ;TipoCambio          ;Element ;Text    ;
                                                  VariableName=TipoCambio;
                                                  DataType=Decimal }

    { [{8AD34B1B-657E-4537-BA26-7CA7DB67D064}];3 ;MovsBanco           ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Export::OnBeforePassVariable=BEGIN
                                                                                 IF TipoPoliza <> text002 THEN
                                                                                   currXMLport.SKIP;
                                                                               END;
                                                                                }

    { [{8336FD3C-10DB-4EF0-B8C7-F7D56875084C}];4 ;MovBanco            ;Element ;Table   ;
                                                  SourceTable=Table271;
                                                  SourceTableView=SORTING(Field53);
                                                  LinkFields=Field53=FIELD(Field1);
                                                  LinkTable=G/L Register;
                                                  MinOccurs=Zero;
                                                  Export::OnPreXMLItem=BEGIN
                                                                         //IF TipoPoliza <> text002 THEN
                                                                         //  currXMLport.SKIP;
                                                                       END;

                                                  Export::OnAfterGetRecord=VAR
                                                                             rMovChe@1000000000 : Record 272;
                                                                             rBan@1000000001 : Record 270;
                                                                             rVend@1000000002 : Record 23;
                                                                             rVenBan@1000000003 : Record 288;
                                                                           BEGIN
                                                                             TipoBanco := '3';

                                                                             // Buscamos el mov cheque para mirar el tipo movimiento
                                                                             rMovChe.RESET;
                                                                             rMovChe.SETCURRENTKEY("Bank Account Ledger Entry No.");
                                                                             rMovChe.SETRANGE("Bank Account Ledger Entry No.", "Bank Account Ledger Entry"."Entry No.");
                                                                             IF rMovChe.FINDFIRST THEN BEGIN
                                                                               TipoMov := FORMAT(rMovChe."Bank Payment Type");
                                                                               ChequeBancoDestino := rMovChe."Document No.";
                                                                             END ELSE BEGIN
                                                                               TipoMov := text004;
                                                                             END;

                                                                             // Buscamos el nombre del banco
                                                                             rBan.RESET;
                                                                             IF rBan.GET("Bank Account Ledger Entry"."Bank Account No.") THEN BEGIN
                                                                               BancoOrigen := rBan.Name;
                                                                               CuentaOrigen := rBan."CCC No.";
                                                                             END ELSE BEGIN
                                                                               BancoOrigen := '';
                                                                               CuentaOrigen := '';
                                                                             END;

                                                                             CIFProveedor := '';
                                                                             NombreProveedor := '';
                                                                             CuentaDestino := '';
                                                                             ChequeBancoDestino := '';
                                                                             CIFProvFactura := '';

                                                                             // Buscamos el proveedor
                                                                             IF "Bank Account Ledger Entry"."Bal. Account Type" = "Bank Account Ledger Entry"."Bal. Account Type"::Vendor THEN BEGIN
                                                                               rVend.RESET;
                                                                               IF rVend.GET("Bank Account Ledger Entry"."Bal. Account No.") THEN BEGIN
                                                                                 CIFProveedor := rVend."VAT Registration No.";
                                                                                 NombreProveedor := rVend.Name;
                                                                                 CIFProvFactura := rVend."VAT Registration No.";
                                                                                 rVenBan.RESET;
                                                                                 rVenBan.SETRANGE(rVenBan."Vendor No.",rVend."No.");
                                                                                 IF rVenBan.FINDFIRST THEN BEGIN
                                                                                   ChequeBancoDestino := rVenBan.Name;
                                                                                   CuentaDestino := rVenBan."CCC No.";
                                                                                 END;
                                                                               END;
                                                                             END;
                                                                           END;
                                                                            }

    { [{EFB5A8FB-36D7-46B8-9771-ED4A488F9D63}];5 ;Tipo                ;Element ;Text    ;
                                                  VariableName=TipoBanco }

    { [{6360CD28-3DF4-4E91-A7B7-0149486DB8F2}];5 ;TipoMov             ;Element ;Text    ;
                                                  VariableName=TipoMov }

    { [{B3E76FDD-52AB-42BF-B595-2CAC5A078976}];5 ;BancoOrigen         ;Element ;Text    ;
                                                  VariableName=BancoOrigen }

    { [{63CEEF15-118D-4FB9-987F-D7B4DFA19732}];5 ;CuentaOrigen        ;Element ;Text     }

    { [{324583B8-2315-4682-92F9-DE37A8A7A0BC}];5 ;Fecha               ;Element ;Field   ;
                                                  DataType=Date;
                                                  SourceField=Bank Account Ledger Entry::Posting Date }

    { [{D3178A96-99ED-4FE0-B622-84EFD147BA4F}];5 ;Monto               ;Element ;Field   ;
                                                  DataType=Decimal;
                                                  SourceField=Bank Account Ledger Entry::Amount }

    { [{E2753061-26F5-43DE-841C-3547D10ED306}];5 ;RFCProveedor        ;Element ;Text    ;
                                                  VariableName=CIFProveedor }

    { [{91BB65EF-6758-47EE-A1BA-045329CB2903}];5 ;NombreProveedor     ;Element ;Text     }

    { [{33533CFA-414B-46F9-BD5D-15C088FD226D}];5 ;ChequeBancoDestino  ;Element ;Text     }

    { [{C691F29D-68C0-4CC1-8739-E63FD920E620}];5 ;CuentaDestino       ;Element ;Text     }

    { [{2BBB2B02-35ED-4CB7-9181-6EC7350A88E2}];3 ;FacturaPagada       ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Export::OnBeforePassVariable=BEGIN
                                                                                 IF TipoPoliza <> text002 THEN
                                                                                   currXMLport.SKIP;
                                                                               END;
                                                                                }

    { [{388FE74D-4546-4263-A954-7DB6868C71B7}];4 ;Tipo                ;Element ;Text    ;
                                                  VariableName=TipoFac;
                                                  Export::OnBeforePassVariable=BEGIN
                                                                                 TipoFac := '4';
                                                                               END;
                                                                                }

    { [{5B53AA2B-09CB-4CFD-8E1E-566F10CA67E7}];4 ;RFC                 ;Element ;Text    ;
                                                  VariableName=CIFProvFactura }

    { [{2F17EF87-4BB6-42C1-81D7-F0A64CB18169}];4 ;NumeroFactura       ;Element ;Text     }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      vFecIni@1000000001 : Date;
      vFecFin@1000000000 : Date;
      text001@1000000002 : TextConst 'ESP=Ingresos';
      text002@1000000003 : TextConst 'ESP=Egresos';
      text003@1000000004 : TextConst 'ESP=Diario';
      text004@1000000005 : TextConst 'ESP=Transferencia';

    PROCEDURE SetFiltros@1000000000(pFecIni@1000000000 : Date;pFecFin@1000000001 : Date);
    BEGIN
      vFecIni := pFecIni;
      vFecFin := pFecFin;
    END;

    BEGIN
    {
      ARM 17/11/2014 CNT-CAR-14090  Exportar P¢lizas
    }
    END.
  }
}
