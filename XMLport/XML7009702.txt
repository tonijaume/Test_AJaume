OBJECT XMLport 7009702 Excursions - Master data
{
  OBJECT-PROPERTIES
  {
    Date=13/07/15;
    Time=21:06:59;
    Modified=Yes;
    Version List=AIC2009,APP 2.0;
  }
  PROPERTIES
  {
    Format/Evaluate=XML Format/Evaluate;
    UseDefaultNamespace=Yes;
  }
  ELEMENTS
  {
    { [{BDDEFF37-8EBB-41CD-9FA5-B8A0B2ACEF6E}];  ;Destinations        ;Element ;Text     }

    { [{33A7DC9F-4844-4D36-BD00-751674A2097A}];1 ;Destination         ;Element ;Table   ;
                                                  VariableName=rCompany;
                                                  SourceTable=Table2000000006;
                                                  MinOccurs=Zero;
                                                  Export::OnPreXMLItem=BEGIN
                                                                         IF NOT wAllDestinations THEN
                                                                           rCompany.SETRANGE(Name, COMPANYNAME);
                                                                       END;

                                                  Export::OnAfterGetRecord=BEGIN
                                                                             //. Busca la divisa de venta en parametros
                                                                             rPar.CHANGECOMPANY(rCompany.Name);
                                                                             IF NOT rPar.FINDFIRST THEN
                                                                               CLEAR(rPar);
                                                                             wDivisaVenta := rPar."Divisa venta excursiones";
                                                                             //+$011 <
                                                                             rCurrency.CHANGECOMPANY(rCompany.Name);
                                                                             IF rCurrency.GET(wDivisaVenta) THEN
                                                                             BEGIN
                                                                               IF rCurrency."Codigo ISO" <> '' THEN
                                                                                 wDivisaVenta := rCurrency."Codigo ISO";
                                                                             END;
                                                                             //+$011 >

                                                                             //. Buscamos la tabla de parametros de codigo de persona
                                                                             rParPer.CHANGECOMPANY(rCompany.Name);
                                                                             IF NOT rParPer.FINDFIRST THEN
                                                                               CLEAR(rParPer);

                                                                             //. El maestro de suplementos tiene que apuntar a la empresa que exportamos
                                                                             rSupCode.CHANGECOMPANY(rCompany.Name);
                                                                           END;
                                                                            }

    { [{695D2C36-1C5C-4F2B-B0CF-909E7680B6AA}];2 ;Dest_Code           ;Attribute;Field  ;
                                                  DataType=Text;
                                                  SourceField=rCompany::Name }

    { [{10210B3F-1A04-42A7-9620-B9DECDA1469D}];2 ;Dest_CurrencyCode   ;Attribute;Text   ;
                                                  VariableName=wDivisaVenta }

    { [{0CFF298A-CC80-4961-A319-E876860AE391}];2 ;Excursions          ;Element ;Table   ;
                                                  VariableName=rExcursion;
                                                  SourceTable=Table7009710;
                                                  SourceTableView=WHERE(Field5=CONST(2),
                                                                        Field50=CONST(No));
                                                  MinOccurs=Zero;
                                                  Export::OnPreXMLItem=BEGIN
                                                                         rExcursion.CHANGECOMPANY(rCompany.Name);
                                                                         IF wCodExcursions <> '' THEN
                                                                         BEGIN
                                                                           wExcursionCode := _GetConversionAtennea(0, wCodTTOO, wCodExcursions); //+$008
                                                                           rExcursion.SETRANGE(Codigo, wExcursionCode);
                                                                         END;
                                                                       END;

                                                  Export::OnAfterGetRecord=VAR
                                                                             rCfgPub@1000000000 : Record 7010268;
                                                                           BEGIN
                                                                             // $004
                                                                             rCfgPub.RESET;
                                                                             rCfgPub.CHANGECOMPANY(rCompany.Name); //+$005
                                                                             rCfgPub.SETFILTER(TourOperador , '%1|%2', wCodTTOO, '');
                                                                             rCfgPub.SETRANGE (Excursion    , rExcursion.Codigo);
                                                                             rCfgPub.SETFILTER("Fecha desde", '<=%1', wDate);
                                                                             rCfgPub.SETFILTER("Fecha hasta", '>=%1', wDate);
                                                                             IF NOT rCfgPub.FINDFIRST THEN
                                                                               currXMLport.SKIP;

                                                                             wChildAllowed  := _CheckChildsAllowed(rExcursion.Codigo); //+$013
                                                                             IF wCodExcursions = '' THEN
                                                                               wExcursionCode := _GetConversionTouroperador(0, wCodTTOO, rExcursion.Codigo)
                                                                             ELSE
                                                                               wExcursionCode := wCodExcursions; //+$008
                                                                           END;
                                                                            }

    { [{77AA6B87-B8D4-46D1-AF2D-2ED76C81986D}];3 ;Exc_Code            ;Attribute;Text   ;
                                                  VariableName=wExcursionCode;
                                                  DataType=Code }

    { [{3E0A134D-CCDE-4A68-BAF8-00A52230D5AA}];3 ;Exc_Description     ;Attribute;Field  ;
                                                  DataType=Text;
                                                  SourceField=rExcursion::Descripcion }

    { [{C1278875-BB39-42F5-83A9-21D6D1D59721}];3 ;Exc_Sale_Per_Vehicle;Attribute;Field  ;
                                                  DataType=Boolean;
                                                  SourceField=rExcursion::Venta x vehiculo }

    { [{6AA8F714-BC58-42D7-9EB8-8E9657C2A1A2}];3 ;Exc_ChildrenAllowed ;Attribute;Text   ;
                                                  VariableName=wChildAllowed }

    { [{00A09EE5-9893-44F4-BAAD-BEC4F862527B}];3 ;Supplements         ;Element ;Table   ;
                                                  VariableName=rSuplemento;
                                                  SourceTable=Table7010148;
                                                  SourceTableView=WHERE(Field1=CONST(1),
                                                                        Field63=CONST(Yes));
                                                  LinkFields=Field3=FIELD(Field1);
                                                  LinkTable=rExcursion;
                                                  Export::OnPreXMLItem=BEGIN
                                                                         rSuplemento.CHANGECOMPANY(rCompany.Name);
                                                                         IF rPar."Busca tarifa por fecha venta" THEN  //+$007
                                                                         BEGIN
                                                                           rSuplemento.SETFILTER("Fecha Desde", '<=%1', TODAY);
                                                                           rSuplemento.SETFILTER("Fecha Hasta", '>=%1', TODAY);
                                                                         END
                                                                         ELSE
                                                                         BEGIN
                                                                           rSuplemento.SETFILTER("Fecha Desde", '<=%1', wDate);
                                                                           rSuplemento.SETFILTER("Fecha Hasta", '>=%1', wDate);
                                                                         END;
                                                                       END;

                                                  Export::OnAfterGetRecord=BEGIN
                                                                             rSupCode.CHANGECOMPANY(rCompany.Name);
                                                                             IF NOT rSupCode.GET(rSuplemento.Codigo) THEN
                                                                               wSupName := '';
                                                                           END;
                                                                            }

    { [{74692812-A135-4AD3-ADBE-59FE1B71930A}];4 ;Sup_Code            ;Attribute;Field  ;
                                                  DataType=Code;
                                                  SourceField=rSuplemento::Codigo }

    { [{B0F86418-9B10-4E1E-AA9F-5DA6C976D4AB}];4 ;Sup_Name            ;Attribute;Text   ;
                                                  VariableName=wSupName }

    { [{8E6CDA97-4F52-44B1-A638-E53700149AF0}];4 ;Sup_AdultPrice      ;Attribute;Field  ;
                                                  DataType=Decimal;
                                                  SourceField=rSuplemento::Precio Adulto }

    { [{1F0BB9A9-8FFB-40F9-B114-4974CEA34493}];4 ;Sup_ChildPrice      ;Attribute;Field  ;
                                                  DataType=Decimal;
                                                  SourceField=rSuplemento::Precio Ni¤o }

    { [{41E6F690-595B-4350-8F1C-963894F95BDD}];4 ;Sup_IsPercentage    ;Attribute;Field  ;
                                                  DataType=Boolean;
                                                  SourceField=rSuplemento::% Porcentaje }

    { [{CD8985AA-2753-4B86-8A6D-39A0AAAF5A90}];4 ;Sup_IsCumulative    ;Attribute;Field  ;
                                                  DataType=Boolean;
                                                  SourceField=rSuplemento::Acumula al precio }

    { [{87BF8B96-4644-4CC2-9EBA-63F109A8F2CD}];3 ;Schedule            ;Element ;Table   ;
                                                  VariableName=rCalendar;
                                                  SourceTable=Table7010254;
                                                  LinkFields=Field1=FIELD(Field1);
                                                  LinkTable=rExcursion;
                                                  Export::OnPreXMLItem=BEGIN
                                                                         rCalendar.CHANGECOMPANY(rCompany.Name);
                                                                         rCalendar.SETFILTER("Fecha inicio", '<=%1', wDate);
                                                                         rCalendar.SETFILTER("Fecha final" , '>=%1', wDate);
                                                                       END;

                                                  Export::OnAfterGetRecord=VAR
                                                                             rCfgPub@1000000000 : Record 7010268;
                                                                           BEGIN
                                                                             // $004
                                                                             rCfgPub.RESET;
                                                                             rCfgPub.CHANGECOMPANY(rCompany.Name); //+$005
                                                                             rCfgPub.SETFILTER(TourOperador     ,'%1|%2',wCodTTOO,'');
                                                                             rCfgPub.SETRANGE (Excursion        , rExcursion.Codigo);
                                                                             rCfgPub.SETRANGE ("Turno Excursion", rCalendar.Turno);
                                                                             rCfgPub.SETFILTER("Fecha desde"    , '<=%1',wDate);
                                                                             rCfgPub.SETFILTER("Fecha hasta"    , '>=%1',wDate);
                                                                             IF NOT rCfgPub.FINDFIRST THEN
                                                                               currXMLport.SKIP;
                                                                           END;
                                                                            }

    { [{95BBA1B6-DCF3-4997-AA96-04850DB97F62}];4 ;Sched_Turn          ;Attribute;Field  ;
                                                  DataType=Integer;
                                                  SourceField=rCalendar::Turno }

    { [{1F824F60-7A57-4FF5-963C-11D2B6375E9A}];4 ;Sched_DayStart      ;Attribute;Field  ;
                                                  DataType=Date;
                                                  SourceField=rCalendar::Fecha inicio }

    { [{6C4C8B8F-A4A7-41AA-81C3-19A43DA48CC1}];4 ;Sched_DayEnd        ;Attribute;Field  ;
                                                  DataType=Date;
                                                  SourceField=rCalendar::Fecha final }

    { [{4BFE6F62-53E3-482D-8DD0-B5B61D88AF83}];4 ;Sched_StartTime     ;Attribute;Field  ;
                                                  DataType=Time;
                                                  SourceField=rCalendar::Hora inicio }

    { [{91542353-1362-445F-8DB4-3A7D65C6EBBB}];4 ;Sched_Monday        ;Attribute;Field  ;
                                                  DataType=Boolean;
                                                  SourceField=rCalendar::Lunes }

    { [{B3990274-5024-4502-AA23-3529E789EBF4}];4 ;Sched_Tuesday       ;Attribute;Field  ;
                                                  DataType=Boolean;
                                                  SourceField=rCalendar::Martes }

    { [{B9A9FC70-BD8F-44FE-BACB-0F91268DEA00}];4 ;Sched_Wednesday     ;Attribute;Field  ;
                                                  DataType=Boolean;
                                                  SourceField=rCalendar::Miercoles }

    { [{58E40066-AF7F-48C9-985A-04443FBD3C2A}];4 ;Sched_Thursday      ;Attribute;Field  ;
                                                  DataType=Boolean;
                                                  SourceField=rCalendar::Jueves }

    { [{C5B4F883-F591-425C-8C2F-1C35E5C449E9}];4 ;Sched_Friday        ;Attribute;Field  ;
                                                  DataType=Boolean;
                                                  SourceField=rCalendar::Viernes }

    { [{2FBC262F-63E8-4C57-B1FC-ACCB84BED6E8}];4 ;Sched_Saturday      ;Attribute;Field  ;
                                                  DataType=Boolean;
                                                  SourceField=rCalendar::Sabado }

    { [{01EB753D-A23E-4591-ADAB-4A6BF702B351}];4 ;Sched_Sunday        ;Attribute;Field  ;
                                                  DataType=Boolean;
                                                  SourceField=rCalendar::Domingo }

    { [{8CF3BA52-2035-47C2-B9A5-3AA5FC47371E}];4 ;Pickups             ;Element ;Table   ;
                                                  VariableName=rPickup;
                                                  SourceTable=Table7010184;
                                                  LinkFields=Field3=FIELD(Field1),
                                                             Field5=FIELD(Field23);
                                                  LinkTable=rCalendar;
                                                  Export::OnPreXMLItem=BEGIN
                                                                         rPickup.CHANGECOMPANY(rCompany.Name);
                                                                         rPickup.SETFILTER("Hora recogida", '<>%1', 0T);
                                                                       END;

                                                  Export::OnAfterGetRecord=BEGIN
                                                                             _GetZonePrice;

                                                                             wHotelCode := _GetConversionTouroperador(1, wCodTTOO, rPickup.Hotel); //+$008
                                                                           END;
                                                                            }

    { [{C9ADEC49-9213-4050-B314-E930C0414FD0}];5 ;Pick_HotelCode      ;Attribute;Text   ;
                                                  VariableName=wHotelCode;
                                                  DataType=Code }

    { [{F62113CC-5271-40A7-92FA-28CC99EB77B8}];5 ;Pick_HotelName      ;Attribute;Field  ;
                                                  DataType=Text;
                                                  SourceField=rPickup::Nombre hotel }

    { [{98FAFB8E-EFE7-47F0-A745-1624D2C82669}];5 ;Pick_Time           ;Attribute;Field  ;
                                                  DataType=Time;
                                                  SourceField=rPickup::Hora recogida }

    { [{11D320E9-1393-43CE-8D9C-C857F77E00B8}];5 ;Pick_AdultPrice     ;Attribute;Text   ;
                                                  VariableName=wAdultPrice }

    { [{20DA5E62-CFD2-4477-99F9-453DE561C1B4}];5 ;Pick_ChildPrice     ;Attribute;Text   ;
                                                  VariableName=wChildPrice }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      wCodExcursions@1100217002 : Code[10];
      wCodTTOO@1000000000 : Code[10];
      wAllDestinations@1100217001 : Boolean;
      wDate@1100217000 : Date;
      rPar@1100217003 : Record 7009700;
      rSupCode@1100217004 : Record 7010143;
      rParPer@1100217005 : Record 7009701;
      rCurrency@1000000003 : Record 4;
      wAdultPriceDec@1100217006 : Decimal;
      wChildPriceDec@1100217007 : Decimal;
      Text007@1000000002 : TextConst 'ENU=Wrong excursion code;ESP=Codigo de excursion erroneo';
      Text008@1000000001 : TextConst 'ENU=Wrong hotel code;ESP=Codigo de hotel erroneo';

    PROCEDURE PassParameters@1100217000(pwCodExcursions@1100217000 : Code[10];pwCodTTOO@1000000000 : Code[10];pwAllDestinations@1100217001 : Boolean;pwDate@1100217002 : Date);
    BEGIN
      // PassParameters

      wCodExcursions   := pwCodExcursions;
      wAllDestinations := pwAllDestinations;
      wDate            := pwDate;
      wCodTTOO         := pwCodTTOO; // $004
    END;

    LOCAL PROCEDURE _GetZonePrice@1100217006();
    VAR
      lrHotel@1100217000 : Record 7009724;
      lrPrecio@1100217001 : Record 7010148;
    BEGIN
      // _GetZonePrice

      wAdultPriceDec := 0;
      wChildPriceDec := 0;

      wAdultPrice := '';
      wChildPrice := '';
      lrHotel.CHANGECOMPANY(rCompany.Name);
      IF NOT lrHotel.GET(rPickup.Hotel) THEN
        EXIT;

      //. Buscamos el precio base para el adulto
      lrPrecio.RESET;
      lrPrecio.CHANGECOMPANY(rCompany.Name);
      lrPrecio.SETRANGE (Delegacion   , rPar.Delegacion);
      lrPrecio.SETRANGE (Excursion    , rExcursion.Codigo);
      lrPrecio.SETRANGE ("Tipo precio", lrPrecio."Tipo precio"::"Precio Base");
      IF rPar."Busca tarifa por fecha venta" THEN //+$007
      BEGIN
        lrPrecio.SETFILTER("Fecha Desde", '<=%1', TODAY);
        lrPrecio.SETFILTER("Fecha Hasta", '>=%1', TODAY);
      END
      ELSE
      BEGIN
        lrPrecio.SETFILTER("Fecha Desde", '<=%1', wDate);
        lrPrecio.SETFILTER("Fecha Hasta", '>=%1', wDate);
      END;
      lrPrecio.SETRANGE (Codigo       , rParPer."Codigo Adulto");
      lrPrecio.SETRANGE (Activo       , TRUE);
      IF lrPrecio.FINDFIRST THEN
        wAdultPriceDec := lrPrecio.Precio;

      //. Buscamos el precio base para el adulto
      lrPrecio.SETRANGE (Codigo       , rParPer."Codigo Ni¤o");
      IF lrPrecio.FINDFIRST THEN
        wChildPriceDec := lrPrecio.Precio;

      //. Buscamos si la zona tiene un suplemento
      lrPrecio.SETRANGE ("Tipo precio", lrPrecio."Tipo precio"::"Suplemento Zona");
      lrPrecio.SETRANGE (Codigo       , lrHotel."Zona Fisica");
      IF lrPrecio.FINDFIRST THEN
      BEGIN
        wAdultPriceDec += lrPrecio."Precio Adulto";
        wChildPriceDec += lrPrecio."Precio Ni¤o";
      END;

      wAdultPrice := FORMAT(wAdultPriceDec);
      wChildPrice := FORMAT(wChildPriceDec);
    END;

    LOCAL PROCEDURE _GetConversionTouroperador@1000000004(pwTipoConversion@1000000000 : Integer;pwTouroperador@1000000001 : Code[10];pwCodigo@1000000002 : Code[20]) : Code[20];
    VAR
      lrExcursion@1000000003 : Record 7009710;
      lrHotel@1000000004 : Record 7009724;
      lrConversion@1000000005 : Record 7010271;
    BEGIN
      // _GetCodigoTouroperador

      //. @TipoConversion, 0: Excursion, 1: Hotel

      //. Filtramos sobre la tabla de conversion especial para la API
      lrConversion.RESET;
      lrConversion.CHANGECOMPANY(rCompany.Name);
      lrConversion.SETRANGE("Tipo conversion", pwTipoConversion);
      lrConversion.SETRANGE(Touroperador     , pwTouroperador);
      lrConversion.SETRANGE("Codigo Atennea" , pwCodigo);

      CASE pwTipoConversion OF
        0:
        BEGIN
          //. Si no lo encontramos en la tabla de conversi¢n vamos a devolver nuestros propios codigos
          IF NOT lrConversion.FINDFIRST THEN
            EXIT(pwCodigo)
          ELSE
            EXIT(lrConversion."Codigo externo")
        END;
        1:
        BEGIN
          //. Si no lo encontramos en la tabla de conversion, devolvermos nuestro codigo de hotel
          IF NOT lrConversion.FINDFIRST THEN
            EXIT(pwCodigo)
          ELSE
            EXIT(lrConversion."Codigo externo");
        END;
      END;
    END;

    LOCAL PROCEDURE _GetConversionAtennea@1000000000(pwTipoConversion@1000000000 : Integer;pwTouroperador@1000000001 : Code[10];pwCodigo@1000000002 : Code[20]) : Code[20];
    VAR
      lrExcursion@1000000003 : Record 7009710;
      lrHotel@1000000004 : Record 7009724;
      lrConversion@1000000005 : Record 7010271;
    BEGIN
      // _GetConversion

      //. @TipoConversion, 0: Excursion, 1: Hotel

      //. Filtramos sobre la tabla de conversion especial para la API
      lrConversion.RESET;
      lrConversion.CHANGECOMPANY(rCompany.Name);
      lrConversion.SETRANGE("Tipo conversion", pwTipoConversion);
      lrConversion.SETRANGE(Touroperador     , pwTouroperador);
      lrConversion.SETRANGE("Codigo externo" , pwCodigo);

      CASE pwTipoConversion OF
        0:
        BEGIN
          //. Si no lo encontramos en la tabla de conversi¢n vamos a ver si estan utilizando nuestros propios codigos
          IF NOT lrConversion.FINDFIRST THEN
          BEGIN
            IF NOT lrExcursion.GET(pwCodigo) THEN
              EXIT('')
            ELSE
              EXIT(lrExcursion.Codigo);
          END
          ELSE
            EXIT(lrConversion."Codigo Atennea");
        END;
        1:
        BEGIN
          //. Si no lo encontramos en la tabla de conversion, buscaremos en nuestra tabla de hoteles
          IF NOT lrConversion.FINDFIRST THEN
          BEGIN
            IF NOT lrHotel.GET(pwCodigo) THEN
              ERROR(Text008)
            ELSE
              EXIT(lrHotel.Hotel);
          END
          ELSE
            EXIT(lrConversion."Codigo Atennea");
        END;
      END;
    END;

    LOCAL PROCEDURE _CheckChildsAllowed@1000000001(pwExcursion@1000000002 : Code[20]) : Text[30];
    VAR
      lrTarifa@1000000000 : Record 7010147;
      lrPrecio@1000000001 : Record 7010148;
    BEGIN

      //. Buscamos el precio base para el ni¤o
      lrPrecio.RESET;
      lrPrecio.CHANGECOMPANY(rCompany.Name);
      lrPrecio.SETRANGE (Delegacion   , rPar.Delegacion);
      lrPrecio.SETRANGE (Excursion    , pwExcursion);
      lrPrecio.SETRANGE ("Tipo precio", lrPrecio."Tipo precio"::"Precio Base");
      IF rPar."Busca tarifa por fecha venta" THEN //+$007
      BEGIN
        lrPrecio.SETFILTER("Fecha Desde", '<=%1', TODAY);
        lrPrecio.SETFILTER("Fecha Hasta", '>=%1', TODAY);
      END
      ELSE
      BEGIN
        lrPrecio.SETFILTER("Fecha Desde", '<=%1', wDate);
        lrPrecio.SETFILTER("Fecha Hasta", '>=%1', wDate);
      END;
      lrPrecio.SETRANGE (Codigo, rParPer."Codigo Ni¤o");
      lrPrecio.SETRANGE (Activo, TRUE);
      IF lrPrecio.FINDFIRST THEN
        EXIT('TRUE')
      ELSE
        EXIT('FALSE');
    END;

    BEGIN
    {
      $001 AJS 07052014 Exportamos solo los hoteles con hora de recogida

      $002 AJS 11052014 Exportar solo los suplementos que esten marcados

      $003 AJS 27062014 CNT-CAR-14073, Incluir el campo rExcursion."Venta x vehiculo"

      $004 ARM 28082014 CNT-CAR-14073 Solo se exportar n las excursiones publicadas para el TTOO o con TTOO en blanco

      $005 AJS 16102014 La tabla de excursiones publicadas va por empresa, hay que hacer changecompany

      $006 AJS 28102014 Las recogidas pasan a depender del turno/calendario

      $007 AJS 28102014 Hay que tener en cuenta si las tarifas van por fecha venta o fecha servicio

      $008 AJS 30102014 Tratamos de retornar el codigo de excursion del touroperador

      $009 AJS 10122014 Traducir el codigo de excursion

      $010 AJS 12012015 Agregar dos nodos para enviar la fecha de inicio/final de cada turno

      $011 AJS 13072015 Exportamos el codigo ISO de la divisa
    }
    END.
  }
}
