OBJECT XMLport 7009710 PIMS Information
{
  OBJECT-PROPERTIES
  {
    Date=22/06/15;
    Time=16:57:38;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    Format/Evaluate=XML Format/Evaluate;
    UseDefaultNamespace=Yes;
  }
  ELEMENTS
  {
    { [{4F4881E2-2CF0-4502-9CB3-A37F290AE24B}];  ;Root                ;Element ;Text     }

    { [{E17C1265-C721-4E20-AEC0-F00045E40AD9}];1 ;NAVCompany          ;Element ;Table   ;
                                                  VariableName=dsEmpresa;
                                                  SourceTable=Table2000000006;
                                                  Export::OnAfterGetRecord=BEGIN
                                                                             _CountryInfo;
                                                                             IF NOT rPar."Empresa Delegacion" THEN
                                                                               currXMLport.SKIP;
                                                                           END;
                                                                            }

    { [{58CF742E-5690-4FA9-A11C-E0F8B80D7D79}];2 ;CompanyCode         ;Attribute;Field  ;
                                                  DataType=Text;
                                                  SourceField=dsEmpresa::Name }

    { [{7C9EE353-CCBD-4D62-8093-6A0E94A58834}];2 ;CountryISOCode      ;Attribute;Text    }

    { [{A26039DB-1237-465E-AF57-28D16C88439C}];2 ;CountryName         ;Attribute;Text    }

    { [{9D3C2CC3-0F59-466E-8FE3-1C5D04867C77}];2 ;SaleCurrencyISOCode ;Attribute;Text    }

    { [{14C56575-8FEE-4828-B0C3-BA1B17D7133A}];2 ;Destination         ;Element ;Table   ;
                                                  VariableName=dsOficinaVenta;
                                                  SourceTable=Table7009720;
                                                  Export::OnPreXMLItem=BEGIN
                                                                         dsOficinaVenta.CHANGECOMPANY(dsEmpresa.Name);
                                                                       END;
                                                                        }

    { [{F1A78DAD-BFD8-4521-9508-8DBC42CF0963}];3 ;DestinationCode     ;Attribute;Field  ;
                                                  DataType=Code;
                                                  SourceField=dsOficinaVenta::Codigo }

    { [{4B47BCF3-E0F3-494F-94DF-550E413F28DC}];3 ;DestinationName     ;Attribute;Field  ;
                                                  DataType=Text;
                                                  SourceField=dsOficinaVenta::Nombre }

    { [{CE6A73E4-4DB5-4211-AAE9-F48B2E20AEF7}];2 ;CommercialExcursion ;Element ;Table   ;
                                                  VariableName=dsCodigoCom;
                                                  SourceTable=Table7010276 }

    { [{4E6896CA-FBE3-41D0-BF29-3ABD534E3343}];3 ;CommercialCode      ;Attribute;Field  ;
                                                  DataType=Code;
                                                  SourceField=dsCodigoCom::Codigo }

    { [{D0E8BDF3-7DF9-49B6-A857-99BD08B50CAA}];3 ;CommercialDescription;Attribute;Field ;
                                                  DataType=Text;
                                                  SourceField=dsCodigoCom::Descripcion }

    { [{CC319C4A-4E7F-4B3B-8F8E-63371F22EFDF}];3 ;Excursion           ;Element ;Table   ;
                                                  VariableName=dsProd;
                                                  SourceTable=Table7009710;
                                                  SourceTableView=SORTING(Field1)
                                                                  WHERE(Field5=CONST(2),
                                                                        Field50=CONST(No));
                                                  LinkFields=Field50003=FIELD(Field1);
                                                  LinkTable=dsCodigoCom;
                                                  Export::OnPreXMLItem=BEGIN
                                                                         dsProd.CHANGECOMPANY(dsEmpresa.Name);
                                                                       END;

                                                  Export::OnAfterGetRecord=BEGIN
                                                                             IF NOT _GetTarifa(dsProd.Codigo) THEN
                                                                               currXMLport.SKIP;

                                                                             ExcDuration := FORMAT(dsProd.Duracion);
                                                                           END;
                                                                            }

    { [{64C6EBDE-21BC-47BD-9E9F-1BD6C08DFEC2}];4 ;Exc_Code            ;Attribute;Field  ;
                                                  DataType=Code;
                                                  SourceField=dsProd::Codigo }

    { [{65CC609C-5C89-4FF9-983B-7EF2A87F8450}];4 ;Exc_Description     ;Attribute;Field  ;
                                                  DataType=Text;
                                                  SourceField=dsProd::Descripcion }

    { [{C1757A44-31E1-4E80-B6D6-395BE7482E97}];4 ;Exc_Duration        ;Attribute;Text   ;
                                                  VariableName=ExcDuration;
                                                  DataType=Option }

    { [{5D7BE8B7-9753-4E7C-8C9C-11706F19F2EB}];4 ;Exc_Occupancy       ;Attribute;Field  ;
                                                  DataType=Integer;
                                                  SourceField=dsProd::Capacidad x defecto }

    { [{8A4960EE-62FE-4AFB-A3E6-B34F529D399A}];4 ;Exc_SalesOffice     ;Attribute;Field  ;
                                                  DataType=Code;
                                                  SourceField=dsProd::Oficina }

    { [{3B5A7149-9821-401A-9D7E-57071C307A70}];4 ;Exc_MinChildAge     ;Attribute;Field  ;
                                                  DataType=Integer;
                                                  SourceField=dsProd::Edad ni¤o desde }

    { [{FCE3574F-9DC2-48D6-B98B-143BAC616547}];4 ;Schedule            ;Element ;Table   ;
                                                  VariableName=dsCalendario;
                                                  SourceTable=Table7010254;
                                                  LinkFields=Field1=FIELD(Field1);
                                                  LinkTable=dsProd;
                                                  Export::OnPreXMLItem=BEGIN
                                                                         dsCalendario.CHANGECOMPANY(dsEmpresa.Name);
                                                                         dsCalendario.SETFILTER("Fecha final", '>=%1', TODAY);
                                                                       END;
                                                                        }

    { [{FCE6FF5A-94E9-4989-A36E-BACBB90E0FC0}];5 ;Sch_StartDate       ;Attribute;Field  ;
                                                  DataType=Date;
                                                  SourceField=dsCalendario::Fecha inicio }

    { [{EACEC788-675D-489C-B32D-1C15D6FC8148}];5 ;Sch_EndDate         ;Attribute;Field  ;
                                                  DataType=Date;
                                                  SourceField=dsCalendario::Fecha final }

    { [{D25AE883-34D6-415C-8E0B-D431BA72F074}];5 ;Sch_StarTime        ;Attribute;Field  ;
                                                  DataType=Time;
                                                  SourceField=dsCalendario::Hora inicio }

    { [{3E5816B7-288F-4F87-BD4D-9B36EB5BAB81}];5 ;Sch_Monday          ;Attribute;Field  ;
                                                  DataType=Boolean;
                                                  SourceField=dsCalendario::Lunes }

    { [{A007C874-2A7A-4030-8851-F915E05DAAB1}];5 ;Sch_Tuesday         ;Attribute;Field  ;
                                                  DataType=Boolean;
                                                  SourceField=dsCalendario::Martes }

    { [{18DF2D45-EA8B-4965-841C-D6462538F6A6}];5 ;Sch_Wednesday       ;Attribute;Field  ;
                                                  DataType=Boolean;
                                                  SourceField=dsCalendario::Miercoles }

    { [{1E7D4A67-C421-4F60-A7B6-19ADEBC887B0}];5 ;Sch_Thursday        ;Attribute;Field  ;
                                                  DataType=Boolean;
                                                  SourceField=dsCalendario::Jueves }

    { [{BF140394-E3AF-4EA6-A0F0-A3B3311E6D7E}];5 ;Sch_Friday          ;Attribute;Field  ;
                                                  DataType=Boolean;
                                                  SourceField=dsCalendario::Viernes }

    { [{F38A5956-81FA-4758-92A1-D1BAF23C689E}];5 ;Sch_Saturday        ;Attribute;Field  ;
                                                  DataType=Boolean;
                                                  SourceField=dsCalendario::Sabado }

    { [{4C422113-544F-4C87-B0ED-F47E010F8723}];5 ;Sch_Sunday          ;Attribute;Field  ;
                                                  DataType=Boolean;
                                                  SourceField=dsCalendario::Domingo }

    { [{AE37D05B-6B9C-4B54-A8F9-9717A0658CD6}];5 ;PickUp              ;Element ;Table   ;
                                                  VariableName=dsRecogidas;
                                                  SourceTable=Table7010184;
                                                  SourceTableView=SORTING(Field3,Field5,Field7);
                                                  LinkFields=Field3=FIELD(Field1),
                                                             Field5=FIELD(Field23);
                                                  LinkTable=dsCalendario;
                                                  Export::OnPreXMLItem=BEGIN
                                                                         dsRecogidas.CHANGECOMPANY(dsEmpresa.Name);
                                                                         dsRecogidas.SETFILTER("Hora recogida", '<>%1', 0T);
                                                                       END;

                                                  Export::OnAfterGetRecord=BEGIN
                                                                             _HotelInfo;
                                                                           END;
                                                                            }

    { [{C6AF274E-A7BC-4EBD-A83C-FB2C23E13B5D}];6 ;Pick_HotelCode      ;Attribute;Text    }

    { [{BE632FF8-F61C-4A5A-BD25-E23815E9AEE3}];6 ;Pick_HotelName      ;Attribute;Text    }

    { [{BD57E292-2276-478D-8553-B627DAF600E6}];6 ;Pick_ZoneCode       ;Attribute;Text    }

    { [{8F533440-D0D4-4351-B5AB-E3B8B3000A68}];6 ;Pick_ZoneName       ;Attribute;Text    }

    { [{14E0721F-AF77-4505-AE2E-7F0C6A52611A}];6 ;Pick_Time           ;Attribute;Field  ;
                                                  DataType=Time;
                                                  SourceField=dsRecogidas::Hora recogida }

    { [{7AA485BA-20EB-4696-9AE6-8AAB16FED25D}];6 ;Pick_AdultSurcharge ;Attribute;Text    }

    { [{EB3B59D7-207D-4A1F-B955-862E2F154820}];6 ;Pick_ChildSurcharge ;Attribute;Text    }

    { [{27CB48DE-C145-4923-AE96-DAF545595BC8}];4 ;Price               ;Element ;Table   ;
                                                  VariableName=dsPrecios;
                                                  SourceTable=Table7010148;
                                                  SourceTableView=SORTING(Field2,Field1,Field3,Field5,Field6)
                                                                  WHERE(Field1=CONST(0));
                                                  LinkFields=Field3=FIELD(Field1);
                                                  LinkTable=dsProd;
                                                  Export::OnPreXMLItem=BEGIN
                                                                         dsPrecios.CHANGECOMPANY(dsEmpresa.Name);
                                                                         dsPrecios.SETRANGE ("ID Tarifa"  , rTarifa."ID Tarifa");
                                                                         dsPrecios.SETFILTER("Fecha Hasta", '>=%1', TODAY);
                                                                       END;
                                                                        }

    { [{9042421F-DB85-4D3B-9F09-8C4ADBED53A4}];5 ;Price_PersonType    ;Attribute;Field  ;
                                                  DataType=Code;
                                                  SourceField=dsPrecios::Codigo }

    { [{EE003361-1DE4-4BB2-83F2-5AD28B039442}];5 ;Price_StartDate     ;Attribute;Field  ;
                                                  DataType=Date;
                                                  SourceField=dsPrecios::Fecha Desde }

    { [{63ADBD33-BF8B-41C0-B181-F51D3B6DCA97}];5 ;Price_EndDate       ;Attribute;Field  ;
                                                  DataType=Date;
                                                  SourceField=dsPrecios::Fecha Hasta }

    { [{2E5F7B83-5C08-4B95-9290-AD5108DFD90D}];5 ;Price_Price         ;Attribute;Field  ;
                                                  DataType=Decimal;
                                                  SourceField=dsPrecios::Precio }

    { [{62CDE070-8876-40CD-8545-1E95DA8544A9}];5 ;Price_ExemptedPrice ;Attribute;Field  ;
                                                  DataType=Decimal;
                                                  SourceField=dsPrecios::Precio exento }

    { [{D020FB13-647A-48E0-8157-82E20C258421}];5 ;Price_CancelExpenses;Attribute;Field  ;
                                                  DataType=Decimal;
                                                  SourceField=dsPrecios::Importe Gastos Anulacion }

    { [{9D96D20F-EFA2-4F80-9C3C-30F7037CFD6C}];3 ;Commission          ;Element ;Table   ;
                                                  VariableName=dsComisiones;
                                                  SourceTable=Table7010142;
                                                  SourceTableView=SORTING(Field33,Field37,Field7,Field9,Field70)
                                                                  WHERE(Field33=FILTER(4),
                                                                        Field70=CONST(No));
                                                  Export::OnPreXMLItem=BEGIN
                                                                         dsComisiones.CHANGECOMPANY(dsEmpresa.Name);
                                                                         dsComisiones.SETFILTER("Fecha final contrato", '>=%1', TODAY);
                                                                       END;

                                                  Export::OnAfterGetRecord=BEGIN
                                                                             _TouroperatorInfo;
                                                                           END;
                                                                            }

    { [{8C314AED-6BBC-4A81-8F07-04CE4816CE61}];4 ;Com_ContractID      ;Attribute;Field  ;
                                                  DataType=Code;
                                                  SourceField=dsComisiones::N§ Contrato compra }

    { [{22C4C851-0263-4721-91CB-5623B046D165}];4 ;Com_TouroperatorCode;Attribute;Field  ;
                                                  DataType=Code;
                                                  SourceField=dsComisiones::TourOperador }

    { [{1E943F85-A021-47C7-8154-B2A13916A3C3}];4 ;Com_TouroperatorName;Attribute;Text    }

    { [{40FC833A-280C-4A71-8FF1-449CB2C3D991}];4 ;Com_StartDate       ;Attribute;Field  ;
                                                  DataType=Date;
                                                  SourceField=dsComisiones::Fecha inicio contrato }

    { [{663189AE-FACF-4F11-B4DB-612DB73902E6}];4 ;Com_EndDate         ;Attribute;Field  ;
                                                  DataType=Date;
                                                  SourceField=dsComisiones::Fecha final contrato }

    { [{3210F9E9-557E-4A0A-80EC-7BE6A0EF64C5}];4 ;Com_GenericCommission;Attribute;Text   }

    { [{FF7FDBD1-79C4-4499-ACCC-249D3708E4B4}];4 ;Com_PaymentTerms    ;Attribute;Text    }

    { [{65B05BE2-595E-420B-A969-B718304CAD70}];4 ;ExcursionCommission ;Element ;Table   ;
                                                  VariableName=dsPrecioComision;
                                                  SourceTable=Table7010153;
                                                  LinkFields=Field1=FIELD(Field1),
                                                             Field3=FIELD(Field3);
                                                  LinkTable=dsComisiones;
                                                  Export::OnPreXMLItem=BEGIN
                                                                         dsPrecioComision.CHANGECOMPANY(dsEmpresa.Name);
                                                                         dsPrecioComision.SETFILTER(Excursion    , '<>%1', '');
                                                                         dsPrecioComision.SETFILTER("Fecha hasta", '>=%1', TODAY);
                                                                       END;
                                                                        }

    { [{9A221B04-0DAD-4A59-810A-5055AE27AC25}];5 ;Com_ExcursionCode   ;Attribute;Field  ;
                                                  DataType=Code;
                                                  SourceField=dsPrecioComision::Excursion }

    { [{1114B98D-70A1-4EDE-BD51-5231AEABE0B2}];5 ;Com_ExcursionCommission;Attribute;Field;
                                                  DataType=Decimal;
                                                  SourceField=dsPrecioComision::Porcentaje Comision }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      rTarifa@1000000000 : Record 7010147;
      rPar@1000000001 : Record 7009700;

    LOCAL PROCEDURE _CountryInfo@1000000001();
    VAR
      lrCompanyInfo@1000000001 : Record 79;
      lrCountry@1000000000 : Record 9;
      lrCurrency@1000000003 : Record 4;
    BEGIN
      // _CountryInfo

      CountryName         := '';
      CountryISOCode      := '';
      SaleCurrencyISOCode := '';

      lrCompanyInfo.CHANGECOMPANY(dsEmpresa.Name);
      IF NOT lrCompanyInfo.FINDFIRST THEN
        currXMLport.SKIP;

      lrCountry.CHANGECOMPANY(dsEmpresa.Name);
      IF NOT lrCountry.GET(lrCompanyInfo."Country/Region Code") THEN
        currXMLport.SKIP;

      CountryName    := lrCountry.Name;
      CountryISOCode := lrCountry."Codigo ISO";

      rPar.CHANGECOMPANY(dsEmpresa.Name);
      IF rPar.FINDFIRST THEN
        IF NOT rPar."Empresa Delegacion" THEN
          CLEAR(rPar);

      lrCurrency.CHANGECOMPANY(dsEmpresa.Name);
      IF lrCurrency.GET(rPar."Divisa venta excursiones") THEN
        SaleCurrencyISOCode := lrCurrency."Codigo ISO";
    END;

    LOCAL PROCEDURE _HotelInfo@1000000007();
    VAR
      lrHotel@1000000000 : Record 7009724;
      lrZonaFisica@1000000001 : Record 7009713;
      lrConversion@1000000002 : Record 7009725;
      lrPrecio@1000000003 : Record 7010148;
      lrZonaPrecio@1000000004 : Record 7010149;
    BEGIN
      // _HotelInfo

      //. Buscamos el hotel para la zona fisica

      lrHotel.CHANGECOMPANY(dsEmpresa.Name);
      IF NOT lrHotel.GET(dsRecogidas.Hotel) THEN
        CLEAR(lrHotel);

      lrZonaFisica.CHANGECOMPANY(dsEmpresa.Name);
      IF NOT lrZonaFisica.GET(lrHotel."Zona Fisica") THEN
        CLEAR(lrZonaFisica);

      lrConversion.CHANGECOMPANY(dsEmpresa.Name);
      lrConversion.RESET;
      lrConversion.SETRANGE(TourOperador    , 'SWG');
      lrConversion.SETRANGE("Hotel estancia", dsRecogidas.Hotel);
      IF NOT lrConversion.FINDFIRST THEN
        CLEAR(lrConversion);

      Pick_HotelCode := lrConversion."Hotel TO";
      Pick_HotelName := lrHotel.Nombre;
      Pick_ZoneCode  := lrZonaFisica."Zona Fisica";
      Pick_ZoneName  := lrZonaFisica.Descripcion;

      //. Buscamos si la zona fisica esta dentro de alguna zona de suplementos

      lrZonaPrecio.CHANGECOMPANY(dsEmpresa.Name);

      lrZonaPrecio.RESET;
      lrZonaPrecio.SETRANGE("ID Tarifa"    , rTarifa."ID Tarifa");
      lrZonaPrecio.SETRANGE("Tipo Contrato", lrZonaPrecio."Tipo Contrato"::Venta);
      lrZonaPrecio.SETRANGE(Excursion      , rTarifa.Excursion);
      lrZonaPrecio.SETRANGE("Zona Fisica"  , lrZonaFisica."Zona Fisica");
      IF NOT lrZonaPrecio.FINDFIRST THEN
        CLEAR(lrZonaPrecio);

      Pick_AdultSurcharge := '';
      Pick_ChildSurcharge := '';

      IF lrZonaPrecio.Zona <> '' THEN
      BEGIN
        //. Buscamos si esta zona tiene algun suplemento
        lrPrecio.CHANGECOMPANY(dsEmpresa.Name);

        lrPrecio.RESET;
        lrPrecio.SETRANGE (Delegacion   , rTarifa.Delegacion);
        lrPrecio.SETRANGE ("ID Tarifa"  , rTarifa."ID Tarifa");
        lrPrecio.SETRANGE ("Tipo precio", lrPrecio."Tipo precio"::"Suplemento Zona");
        lrPrecio.SETRANGE (Codigo       , lrZonaPrecio.Zona);
        lrPrecio.SETFILTER("Fecha Hasta", '>=%1', TODAY);
        IF NOT lrPrecio.FINDFIRST THEN
          CLEAR(lrPrecio);

        Pick_AdultSurcharge := FORMAT(lrPrecio."Precio Adulto");
        Pick_ChildSurcharge := FORMAT(lrPrecio."Precio Ni¤o");
      END;
    END;

    LOCAL PROCEDURE _GetTarifa@1000000010(pwExcursion@1000000000 : Code[10]) : Boolean;
    BEGIN
      // _GetTarifa

      rTarifa.CHANGECOMPANY(dsEmpresa.Name);

      rTarifa.RESET;
      rTarifa.SETRANGE (Excursion     , pwExcursion);
      rTarifa.SETFILTER("Fecha Inicio", '<=%1', TODAY);
      rTarifa.SETFILTER("Fecha Final" , '>=%1', TODAY);
      IF NOT rTarifa.FINDFIRST THEN
      BEGIN
        CLEAR(rTarifa);
        EXIT(FALSE);
      END
      ELSE
        EXIT(TRUE);
    END;

    LOCAL PROCEDURE _TouroperatorInfo@1000000006();
    VAR
      lrTouroperador@1000000000 : Record 7009717;
      lrPrecio@1000000001 : Record 7010153;
      lrTOSetup@1000000002 : Record 7009705;
      lrCustomer@1000000003 : Record 18;
      lrPaymentTerm@1000000004 : Record 3;
    BEGIN
      // _TouroperatorInfo

      lrTouroperador.CHANGECOMPANY(dsEmpresa.Name);
      IF NOT lrTouroperador.GET(dsComisiones.TourOperador) THEN
        CLEAR(lrTouroperador);

      Com_TouroperatorName := lrTouroperador.Nombre;

      //. Buscamos los terminos de pago del cliente
      lrTOSetup.CHANGECOMPANY(dsEmpresa.Name);
      IF NOT lrTOSetup.GET(rPar.Delegacion, dsComisiones.TourOperador) THEN
        CLEAR(lrTOSetup);

      lrCustomer.CHANGECOMPANY(dsEmpresa.Name);
      IF NOT lrCustomer.GET(lrTOSetup."Cliente credito excursiones") THEN
        CLEAR(lrCustomer);

      lrPaymentTerm.CHANGECOMPANY(dsEmpresa.Name);
      IF NOT lrPaymentTerm.GET(lrCustomer."Payment Terms Code") THEN
        CLEAR(lrPaymentTerm);
      Com_PaymentTerms := FORMAT(lrPaymentTerm."Due Date Calculation");


      //. Buscamos si el touroperador tiene una comisi¢n generica
      lrPrecio.CHANGECOMPANY(dsEmpresa.Name);

      lrPrecio.RESET;
      lrPrecio.SETRANGE (Delegacion   , dsComisiones.Delegacion);
      lrPrecio.SETRANGE ("N§ Contrato", dsComisiones."N§ Contrato compra");
      lrPrecio.SETFILTER("Fecha hasta", '>=%1', TODAY);
      lrPrecio.SETRANGE (Excursion    , '');
      IF NOT lrPrecio.FINDFIRST THEN
        CLEAR(lrPrecio);

      Com_GenericCommission := FORMAT(lrPrecio."Porcentaje Comision");
    END;

    BEGIN
    {
      $000 AJS 11052015 Este XMLPort exporta la informaci¢n de todas las empresas por lo que hay que recordar el CHANGECOMPANY de
                        todas las tablas que se empleen

           Las variables de las tablas que se exportan llevaran ds (DataSource) el resto como siempre £nicamente la r (Record)

      $001 AJS 02062015 A¤adido el nodo dsProd."Edad ni¤o desde" para informar de la edad minima permitida para los ni¤os

      $002 AJS 05062015 Eliminada la informacion de proveedores porque no es necesaria para Sunwing (A. Cocco)

      $003 AJS 22062015 Nueva tabla de agrupacion comercial
    }
    END.
  }
}
