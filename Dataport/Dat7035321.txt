OBJECT Dataport 7035321 TOUROPERADOR EN SUBZONA
{
  OBJECT-PROPERTIES
  {
    Date=16/10/09;
    Time=12:34:51;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    TransactionType=Update;
    CaptionML=ESP=TOUROPERADOR EN SUBZONA;
    FieldStartDelimiter=<None>;
    FieldEndDelimiter=<None>;
    RecordSeparator=<CR/LF>;
    OnInitDataport=VAR
                     lcFunciones@1100244000 : Codeunit 7035320;
                   BEGIN
                      CurrDataport.FILENAME := lcFunciones.NombreArchivo(4,3)
                   END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table7035321;
        OnPreDataItem=VAR
                        lrHistorico@1100244000 : Record 7035333;
                      BEGIN

                        lrHistorico.RESET;
                        lrHistorico.SETRANGE(Tipo,lrHistorico.Tipo::Reserva);
                        IF lrHistorico.FINDLAST THEN
                          wNFichero := lrHistorico."No Importacion";
                      END;

        OnAfterImportRecord=VAR
                              lrTTOOEnSub2@1100244000 : Record 7035321;
                            BEGIN
                              "N§Fichero" := wNFichero;

                              // Si cambia la descripcion de un TTTOO eliminamos el codigo Finbus y al final de
                              // la importacion generaremos un mensaje de advertencia

                              IF lrTTOOEnSub2.GET("Codigo de Cliente") THEN BEGIN
                                IF (lrTTOOEnSub2.Nombre <> Nombre) THEN BEGIN
                                  rTTOOTemp.COPY(lrTTOOEnSub2);
                                  rTTOOTemp.INSERT;
                                  "Codigo FinBus" := '';
                                  MODIFY;
                                END;
                              END
                              ELSE BEGIN
                                rTTOOTemp.INIT;
                                rTTOOTemp."Codigo de Cliente" := "Codigo de Cliente";
                                rTTOOTemp.INSERT;
                              END;
                            END;

        OnPostDataItem=VAR
                         lrTTOOEnSub@1100244000 : Record 7035321;
                         lrTexto@1100244001 : Text[100];
                         lwArchivo@1100244002 : File;
                       BEGIN
                         IF rTTOOTemp.FINDSET THEN BEGIN
                           MESSAGE(Text000+
                                   Text001);
                           IF NOT lwArchivo.CREATE(Text002) THEN
                             EXIT;
                           lwArchivo.TEXTMODE(TRUE);
                           CLEAR(lrTexto);
                           lwArchivo.WRITE(Text003);
                           lwArchivo.WRITE('');
                           lwArchivo.WRITE('');
                           lwArchivo.WRITE(PADSTR(Text004,10,' ') + PADSTR(Text005,32,' ') + PADSTR(Text006,32,' ')) ;
                           lwArchivo.WRITE(PADSTR('',74,'-'));
                           REPEAT
                             IF NOT lrTTOOEnSub.GET(rTTOOTemp."Codigo de Cliente") THEN
                               lrTTOOEnSub.INIT;
                             CLEAR(lrTexto);
                             lrTexto := PADSTR(rTTOOTemp."Codigo de Cliente",10,' ');
                             lrTexto += PADSTR(rTTOOTemp.Nombre,32,' ');
                             lrTexto += PADSTR(lrTTOOEnSub.Nombre,32,' ');
                             lwArchivo.WRITE(lrTexto);
                           UNTIL rTTOOTemp.NEXT =0  ;
                           lwArchivo.CLOSE;
                           //SHELL(Text007,Text002);
                           HYPERLINK(Text002);
                         END;
                       END;

      }
      FIELDS
      {
        {      ;     ;"Tipo Touroperador"  }
        {      ;     ;"Codigo de Cliente"  }
        {      ;     ;Nombre               }
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=9020;
      Height=3410;
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      Text000@1100244002 : TextConst 'ESP=Se han creado nuevos codigos Touroperador En Subzona \';
      Text001@1100244003 : TextConst 'ESP=Los que han cambiado de nombre, se han dejado con el codigo Finbus en blanco \';
      Text002@1100244004 : TextConst 'ESP=c:\TTOOenSUB.txt';
      Text003@1100244005 : TextConst 'ESP=CAMBIOS EN LA TABLA DE TOUROPERADORES EN SUBZONA';
      Text004@1100244006 : TextConst 'ESP=CODIGO';
      Text005@1100244007 : TextConst 'ESP=NOMBRE ANTERIOR';
      Text006@1100244008 : TextConst 'ESP=NOMBRE ACTUAL';
      Text007@1100244009 : TextConst 'ESP=NOTEPAD.EXE';
      wNFichero@1100244000 : Integer;
      rTTOOTemp@1100244001 : TEMPORARY Record 7035321;

    PROCEDURE Setfichero@1100244000(VAR pwNFichero@1100244001 : Integer);
    BEGIN
      // Setfichero
      // Asigna el numero de fichero
      wNFichero:= pwNFichero;
    END;

    BEGIN
    END.
  }
}
