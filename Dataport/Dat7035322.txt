OBJECT Dataport 7035322 HOTEL TRASLADOS
{
  OBJECT-PROPERTIES
  {
    Date=30/04/10;
    Time=11:53:12;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    CaptionML=ESP=HOTEL TRASLADOS;
    FieldStartDelimiter=<None>;
    FieldEndDelimiter=<None>;
    RecordSeparator=<CR/LF>;
    OnInitDataport=VAR
                     lcFunciones@1100244000 : Codeunit 7035320;
                   BEGIN
                     CurrDataport.FILENAME := lcFunciones.NombreArchivo(5,3)
                   END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table7035322;
        DataItemTableView=SORTING(Codigo);
        OnPreDataItem=VAR
                        lrHistorico@1100244000 : Record 7035333;
                      BEGIN
                        lrHistorico.RESET;
                        lrHistorico.SETRANGE(Tipo,lrHistorico.Tipo::Reserva);
                        IF lrHistorico.FIND ('+') THEN
                          wNFichero := lrHistorico."No Importacion";
                      END;

        OnAfterImportRecord=VAR
                              lrHotel2@1100244000 : Record 7035322;
                            BEGIN
                              "N§Fichero"   := wNFichero;
                              "Cod.Oficina" := wZonaOficina + wSubZonaOficina;

                              // Si cambia la descripcion de un hotel eliminamos el codigo Finbus y al final de
                              // la importacion generaremos un mensaje de advertencia

                              IF lrHotel2.GET(Codigo) THEN BEGIN
                                IF (lrHotel2.Nombre <> Nombre) THEN BEGIN
                                  rHotelTemp.COPY(lrHotel2);
                                  rHotelTemp.INSERT;

                                  "Codigo FinBus" := '';
                                  MODIFY;
                                END;
                              END
                              ELSE BEGIN
                                rHotelTemp.INIT;
                                rHotelTemp.Codigo := Codigo;
                                rHotelTemp.INSERT;

                                //"Codigo FinBus":= '';
                                //MODIFY;
                              END;
                            END;

        OnPostDataItem=VAR
                         lrTexto@1100244000 : Text[100];
                         lfFormHoteles@1100244001 : Form 7035322;
                         lwArchivo@1100244002 : File;
                         lrHotel@1100244003 : Record 7035322;
                       BEGIN

                         IF rHotelTemp.FIND('-') THEN BEGIN
                           MESSAGE(Text000);
                           IF NOT lwArchivo.CREATE(Text001) THEN
                             EXIT;
                           lwArchivo.TEXTMODE(TRUE);
                           CLEAR(lrTexto);
                           lwArchivo.WRITE(Text002);
                           lwArchivo.WRITE('');
                           lwArchivo.WRITE('');
                           lwArchivo.WRITE(PADSTR(Text003,10,' ') + PADSTR(Text004,32,' ') + PADSTR(Text005,32,' ')) ;
                           lwArchivo.WRITE(PADSTR('',74,'-'));
                           REPEAT
                             IF NOT lrHotel.GET(rHotelTemp.Codigo) THEN
                               lrHotel.INIT;
                             CLEAR(lrTexto);
                             lrTexto := PADSTR(rHotelTemp.Codigo,10,' ');
                             lrTexto += PADSTR(rHotelTemp.Nombre,32,' ');
                             lrTexto += PADSTR(lrHotel.Nombre,32,' ');
                             lwArchivo.WRITE(lrTexto);
                           UNTIL rHotelTemp.NEXT =0  ;
                           lwArchivo.CLOSE;
                           //SHELL(Text006,Text001);
                           HYPERLINK(Text001);
                         END;
                       END;

      }
      FIELDS
      {
        {      ;     ;wZonaOficina         }
        {      ;     ;wSubZonaOficina      }
        {      ;     ;Codigo               }
        {      ;     ;Nombre               }
        {      ;     ;Orden_Visualizacion_Llegadas }
        {      ;     ;Orden_Visualizacion_Salidas }
        {      ;     ;"Zona Traslados2"    }
        {      ;     ;"Subzona Traslados"  }
        {      ;     ;wDesconocido         }
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=9020;
      Height=3410;
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      Text000@1100244004 : TextConst 'ESP=Los siguiente codigos de Hotel han cambiado de nombre,\ se ha dejado el codigo Finbus en blanco \';
      Text001@1100244005 : TextConst 'ESP=c:\HotelesCambiados.txt';
      Text002@1100244006 : TextConst 'ESP=CAMBIOS EN LA TABLA DE HOTELES';
      Text003@1100244007 : TextConst 'ESP=CODIGO';
      Text004@1100244008 : TextConst 'ESP=NOMBRE ANTERIOR';
      Text005@1100244009 : TextConst 'ESP=NOMBRE ACTUAL';
      Text006@1100244010 : TextConst 'ESP=NOTEPAD.EXE';
      wNFichero@1100244000 : Integer;
      rHotelTemp@1100244001 : TEMPORARY Record 7035322;
      wZonaOficina@1100244002 : Code[2];
      wSubZonaOficina@1100244003 : Code[1];
      wDesconocido@1103355000 : Code[3];

    PROCEDURE Setfichero@1100244000(VAR pwNFichero@1100244001 : Integer);
    BEGIN
      // Setfichero
      // Asigna el numero de fichero
      wNFichero:= pwNFichero;
    END;

    BEGIN
    {
      ## AJS 30.04.2010

        .- Agregado nuevo campo alfanumerico de 3 caractres al final
    }
    END.
  }
}
