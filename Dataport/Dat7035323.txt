OBJECT Dataport 7035323 SUBZONA_TRASLADOS
{
  OBJECT-PROPERTIES
  {
    Date=16/10/09;
    Time=12:34:52;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    CaptionML=ESP=SUBZONA_TRASLADOS;
    FieldStartDelimiter=<None>;
    FieldEndDelimiter=<None>;
    RecordSeparator=<CR/LF>;
    OnInitDataport=VAR
                     lcFunciones@1100244000 : Codeunit 7035320;
                   BEGIN
                     CurrDataport.FILENAME := lcFunciones.NombreArchivo(6,3)
                   END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table7035323;
        OnPreDataItem=VAR
                        lrHistorico@1100244000 : Record 7035333;
                      BEGIN
                        lrHistorico.RESET;
                        lrHistorico.SETRANGE(Tipo,lrHistorico.Tipo::Reserva);
                        IF lrHistorico.FINDLAST THEN
                          wNFichero := lrHistorico."No Importacion";
                      END;

        OnAfterImportRecord=VAR
                              lrSubzona2@1100244000 : Record 7035323;
                            BEGIN
                              "N§Fichero"   := wNFichero;
                              "Cod.Oficina" := wZonaOficina + wSubZonaOficina;

                              // Si cambia la descripcion de una Subzona eliminamos el codigo Finbus y al final de
                              // la importacion generaremos un mensaje de advertencia

                              IF lrSubzona2.GET("Subzona Traslados","Zona Traslados","Cod.Oficina") THEN BEGIN
                                IF (lrSubzona2.Nombre <> Nombre) THEN BEGIN
                                  rSubzonaTemp.COPY(lrSubzona2);
                                  rSubzonaTemp.INSERT;
                                  "Codigo Finbus" := '';
                                  MODIFY;
                                END;
                              END
                              ELSE BEGIN
                                rSubzonaTemp.INIT;
                                rSubzonaTemp."Subzona Traslados" := "Subzona Traslados";
                                rSubzonaTemp."Zona Traslados"    := "Zona Traslados";
                                rSubzonaTemp."Cod.Oficina"       := "Cod.Oficina";
                                rSubzonaTemp.INSERT;
                              END;
                            END;

        OnPostDataItem=VAR
                         lrTexto@1100244000 : Text[100];
                         lwArchivo@1100244001 : File;
                         lrSubzona@1100244002 : Record 7035323;
                       BEGIN
                         IF rSubzonaTemp.FINDSET THEN BEGIN
                           MESSAGE(Text000+
                                   Text001);
                           IF NOT lwArchivo.CREATE(Text002) THEN
                             EXIT;
                           lwArchivo.TEXTMODE(TRUE);
                           CLEAR(lrTexto);
                           lwArchivo.WRITE(Text003);
                           lwArchivo.WRITE('');
                           lwArchivo.WRITE('');
                           lwArchivo.WRITE(PADSTR(Text004,10,' ') + PADSTR(Text005,10,' ') + PADSTR(Text006,10,' ') +
                                            PADSTR(Text007,22,' ') + PADSTR(Text008,22,' ')) ;
                           lwArchivo.WRITE(PADSTR('',74,'-'));
                           REPEAT
                             IF NOT lrSubzona.GET(rSubzonaTemp."Subzona Traslados",rSubzonaTemp."Zona Traslados",rSubzonaTemp."Cod.Oficina") THEN
                               lrSubzona.INIT;
                             CLEAR(lrTexto);
                             lrTexto := PADSTR(FORMAT(lrSubzona."Subzona Traslados") ,10,' ');
                             lrTexto += PADSTR(lrSubzona."Zona Traslados"   ,10,' ');
                             lrTexto += PADSTR(lrSubzona."Cod.Oficina"      ,10,' ');
                             lrTexto += PADSTR(rSubzonaTemp. Nombre,22,' ');
                             lrTexto += PADSTR(lrSubzona.Nombre,22,' ');
                             lwArchivo.WRITE(lrTexto);
                           UNTIL rSubzonaTemp.NEXT =0  ;
                           lwArchivo.CLOSE;
                           //SHELL(Text009,Text002);
                           HYPERLINK(Text002);
                         END;
                       END;

      }
      FIELDS
      {
        {      ;     ;wZonaOficina         }
        {      ;     ;wSubZonaOficina      }
        {      ;     ;"Zona Traslados"    ;OnBeforeEvaluateField=BEGIN
                                                                   Text := wZonaOficina + wSubZonaOficina + Text;
                                                                 END;
                                                                  }
        {      ;     ;"Subzona Traslados"  }
        {      ;     ;Nombre               }
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=9020;
      Height=3410;
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      Text000@1100244004 : TextConst 'ESP=Se han creado nuevos codigos de Subzona Traslados \';
      Text001@1100244005 : TextConst 'ESP=Los que han cambiado de nombre, se han dejado con el codigo Finbus en blanco \';
      Text002@1100244006 : TextConst 'ESP=SUBZONATRAS.txt';
      Text003@1100244007 : TextConst 'ESP=CAMBIOS EN LA TABLA DE SUBZONA TRASLADOS';
      Text004@1100244008 : TextConst 'ESP=SUBZONA';
      Text005@1100244009 : TextConst 'ESP=ZONA';
      Text006@1100244010 : TextConst 'ESP=OFICINA';
      Text007@1100244011 : TextConst 'ESP=NOMBRE ANTERIOR';
      Text008@1100244012 : TextConst 'ESP=NOMBRE ACTUAL';
      Text009@1100244013 : TextConst 'ESP=NOTEPAD.EXE';
      wNFichero@1100244000 : Integer;
      rSubzonaTemp@1100244001 : TEMPORARY Record 7035323;
      wZonaOficina@1100244002 : Code[2];
      wSubZonaOficina@1100244003 : Code[1];

    PROCEDURE Setfichero@1100244000(VAR pwNFichero@1100244001 : Integer);
    BEGIN
      // Setfichero
      // Asigna el numero de fichero
      wNFichero:= pwNFichero;
    END;

    BEGIN
    END.
  }
}
