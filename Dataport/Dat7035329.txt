OBJECT Dataport 7035329 ZONA TRASLADOS
{
  OBJECT-PROPERTIES
  {
    Date=16/10/09;
    Time=12:34:52;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    CaptionML=ESP=ZONA TRASLADOS;
    FieldStartDelimiter=<None>;
    FieldEndDelimiter=<None>;
    RecordSeparator=<CR/LF>;
    OnPreDataport=VAR
                    lcFunciones@1100244000 : Codeunit 7035320;
                  BEGIN
                    CurrDataport.FILENAME := lcFunciones.NombreArchivo(7,3)
                  END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table7035329;
        OnPreDataItem=VAR
                        lrHistorico@1100244000 : Record 7035333;
                      BEGIN
                        lrHistorico.RESET;
                        lrHistorico.SETRANGE(Tipo,lrHistorico.Tipo::Reserva);
                        IF lrHistorico.FINDLAST THEN
                          wNFichero := lrHistorico."No Importacion";
                      END;

        OnAfterImportRecord=VAR
                              lrZona2@1100244000 : Record 7035329;
                            BEGIN
                              "N§Fichero" := wNFichero;

                              // Si cambia la descripcion de un TTTOO al final de
                              // la importacion generaremos un mensaje de advertencia

                              IF lrZona2.GET("Zona Traslados") THEN BEGIN
                                IF (lrZona2.Nombre <> Nombre) THEN BEGIN
                                  rZonaTemp.COPY(lrZona2);
                                  rZonaTemp.INSERT;
                                  MODIFY;
                                END;
                              END
                              ELSE BEGIN
                                rZonaTemp.INIT;
                                rZonaTemp."Zona Traslados" := "Zona Traslados";
                                rZonaTemp.INSERT;
                              END;
                            END;

        OnPostDataItem=VAR
                         lrTexto@1100244000 : Text[100];
                         lwArchivo@1100244001 : File;
                         lrZona@1100244002 : Record 7035329;
                       BEGIN
                         IF rZonaTemp.FINDSET THEN BEGIN
                           MESSAGE(Text000);
                           IF NOT lwArchivo.CREATE(Text001) THEN
                             EXIT;
                           lwArchivo.TEXTMODE(TRUE);
                           CLEAR(lrTexto);
                           lwArchivo.WRITE(Text002);
                           lwArchivo.WRITE('');
                           lwArchivo.WRITE('');
                           lwArchivo.WRITE(PADSTR(Text003,10,' ') +
                                                  PADSTR(Text004,31,' ') + PADSTR(Text005,31,' ')) ;
                           lwArchivo.WRITE(PADSTR('',74,'-'));
                           REPEAT
                             IF NOT lrZona.GET(rZonaTemp."Zona Traslados") THEN
                               lrZona.INIT;
                             CLEAR(lrTexto);
                             lrTexto := PADSTR(rZonaTemp."Zona Traslados",10,' ');
                             lrTexto += PADSTR(rZonaTemp.Nombre,31,' ');
                             lrTexto += PADSTR(lrZona.Nombre,31,' ');
                             lwArchivo.WRITE(lrTexto);
                           UNTIL rZonaTemp.NEXT =0  ;
                           lwArchivo.CLOSE;
                           //SHELL(Text006,Text001);
                           HYPERLINK(Text001);
                         END;
                       END;

      }
      FIELDS
      {
        {      ;     ;wZonaTras            }
        {      ;     ;wSubzonaTras         }
        {      ;     ;"Zona Traslados"    ;OnBeforeEvaluateField=BEGIN
                                                                   Text := wZonaTras + wSubzonaTras + Text;
                                                                 END;
                                                                  }
        {      ;     ;Nombre               }
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=9020;
      Height=3410;
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      Text000@1100244004 : TextConst 'ESP=Se han creado nuevos codigos de Zona Traslados';
      Text001@1100244005 : TextConst 'ESP=Zona Traslados.txt';
      Text002@1100244006 : TextConst 'ESP=CAMBIOS EN LA TABLA DE ZONAS DE TRASLADO';
      Text003@1100244007 : TextConst 'ESP=CODIGO';
      Text004@1100244008 : TextConst 'ESP=NOMBRE ANTERIOR';
      Text005@1100244009 : TextConst 'ESP=NOMBRE ACTUAL';
      Text006@1100244010 : TextConst 'ESP=NOTEPAD.EXE';
      wNFichero@1100244000 : Integer;
      rZonaTemp@1100244001 : TEMPORARY Record 7035329;
      wZonaTras@1100244002 : Code[2];
      wSubzonaTras@1100244003 : Code[1];

    PROCEDURE Setfichero@1100244000(VAR pwNFichero@1100244001 : Integer);
    BEGIN
      // Setfichero
      // Asigna el numero de fichero
      wNFichero:= pwNFichero;
    END;

    BEGIN
    END.
  }
}
