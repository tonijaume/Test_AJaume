OBJECT Report 7000098 Settle Docs. in Post. Bill Gr.
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVES6.00.01;
  }
  PROPERTIES
  {
    Permissions=TableData 21=imd,
                TableData 25=imd,
                TableData 45=m,
                TableData 7000003=imd,
                TableData 7000004=imd,
                TableData 7000006=imd,
                TableData 7000007=imd;
    CaptionML=[ENU=Settle Docs. in Post. Bill Gr.;
               ESP=Liq. docs. en remesa regis.];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   PostingDate := WORKDATE;
                 END;

    OnPreReport=BEGIN
                  GLSetup.GET;
                END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table7000003;
        DataItemTableView=SORTING(Bill Gr./Pmt. Order No.,Status,Category Code,Redrawn,Due Date)
                          WHERE(Status=CONST(Open));
        DataItemVarName=PostedDoc;
        OnPreDataItem=BEGIN
                        DocPost.CheckPostingDate(PostingDate);

                        SourceCodeSetup.GET;
                        SourceCode := SourceCodeSetup."Cartera Journal";
                        DocCount :=0;
                        SumLCYAmt := 0;
                        GenJnlLineNextNo := 0;
                        ExistVATEntry := FALSE;

                        Window.OPEN(
                          Text1100000);
                      END;

        OnAfterGetRecord=VAR
                           FromJnl@1100001 : Boolean;
                         BEGIN
                           IsRedrawn := CarteraManagement.CheckFromRedrawnDoc(PostedDoc."No.");

                           PostedBillGr.GET(PostedDoc."Bill Gr./Pmt. Order No.");
                           BankAcc.GET(PostedBillGr."Bank Account No.");
                           Delay := BankAcc."Delay for Notices";

                           IF DueOnly AND (PostingDate < "Due Date" + Delay) THEN
                             CurrReport.SKIP;

                           DocCount := DocCount + 1;
                           Window.UPDATE(1,DocCount);

                           WITH GenJnlLine DO BEGIN
                             GenJnlLineNextNo := GenJnlLineNextNo + 10000;
                             CLEAR(GenJnlLine);
                             INIT;
                             "Line No." := GenJnlLineNextNo;
                             "Posting Date" := PostingDate;
                             "Document Type" := "Document Type"::Payment;
                             "Document No." := PostedBillGr."No.";
                             "Reason Code" := PostedBillGr."Reason Code";
                             VALIDATE("Account Type","Account Type"::Customer);
                             CustLedgEntry.GET(PostedDoc."Entry No.");

                             IF GLSetup."Unrealized VAT" AND (PostedDoc."Document Type" = PostedDoc."Document Type"::Bill) THEN BEGIN
                               FromJnl := FALSE;
                               IF PostedDoc."From Journal" THEN
                                 FromJnl := TRUE;
                               ExistsNoRealVAT := GenJnlPostLine.CustFindVATSetup(VATPostingSetup,CustLedgEntry,FromJnl)
                             END;

                             VALIDATE("Account No.",CustLedgEntry."Customer No.");
                             IF PostedDoc."Document Type" = PostedDoc."Document Type"::Bill THEN
                               Description := COPYSTR(
                                 STRSUBSTNO(Text1100001,PostedDoc."Document No.",PostedDoc."No."),
                                 1,MAXSTRLEN(Description))
                             ELSE
                               Description := COPYSTR(
                                 STRSUBSTNO(Text1100002,PostedDoc."Document No."),
                                 1,MAXSTRLEN(Description));
                             VALIDATE("Currency Code",PostedDoc."Currency Code");
                             IF GenJnlPostLine.CheckCalcPmtDiscGenJnlCust(GenJnlLine,CustLedgEntry,0,FALSE) THEN
                               VALIDATE(Amount,-PostedDoc."Remaining Amount" + CustLedgEntry."Remaining Pmt. Disc. Possible")
                             ELSE
                               VALIDATE(Amount,-PostedDoc."Remaining Amount");
                             "Applies-to Doc. Type" := CustLedgEntry."Document Type";
                             "Applies-to Doc. No." := CustLedgEntry."Document No.";
                             "Applies-to Bill No." := CustLedgEntry."Bill No.";
                             "Source Code" := SourceCode;
                             "System-Created Entry" := TRUE;
                             "Shortcut Dimension 1 Code" := CustLedgEntry."Global Dimension 1 Code";
                             "Shortcut Dimension 2 Code" := CustLedgEntry."Global Dimension 2 Code";
                             INSERT;
                             CarteraDimMgt.CustInsertJnlLinDimension(GenJnlLine,CustLedgEntry,TRUE);
                             SumLCYAmt := SumLCYAmt + "Amount (LCY)";
                           END;

                           IF ("Document Type" = "Document Type"::Bill) AND
                              GLSetup."Unrealized VAT" AND
                              ExistsNoRealVAT AND
                              (NOT IsRedrawn) THEN BEGIN
                             CustLedgEntry.CALCFIELDS("Remaining Amount","Remaining Amt. (LCY)");
                             CarteraManagement.CustUnrealizedVAT2(
                               CustLedgEntry,
                               CustLedgEntry."Remaining Amt. (LCY)",
                               GenJnlLine,
                               ExistVATEntry,
                               FirstVATEntryNo,
                               LastVATEntryNo,
                               NoRealVATBuffer,
                               FromJnl);

                             TempCurrCode := PostedDoc."Currency Code";
                             PostedDoc."Currency Code" := '';

                             IF NoRealVATBuffer.FIND('-') THEN BEGIN
                               REPEAT
                                 BEGIN
                                   InsertGenJournalLine(
                                     GenJnlLine."Account Type"::"G/L Account",
                                     NoRealVATBuffer.Account,
                                     NoRealVATBuffer.Amount,
                                     PostedDoc."Global Dimension 1 Code",
                                     PostedDoc."Global Dimension 2 Code");
                                   InsertGenJournalLine(
                                     GenJnlLine."Account Type"::"G/L Account",
                                     NoRealVATBuffer."Balance Account",
                                     -NoRealVATBuffer.Amount,
                                     PostedDoc."Global Dimension 1 Code",
                                     PostedDoc."Global Dimension 2 Code");
                                 END;
                               UNTIL NoRealVATBuffer.NEXT = 0;
                               NoRealVATBuffer.DELETEALL;
                             END;
                             PostedDoc."Currency Code" := TempCurrCode;
                           END;

                           IF GenJnlPostLine.CheckCalcPmtDiscGenJnlCust(GenJnlLine,CustLedgEntry,0,FALSE) THEN
                             CalcBankAccount("No.","Remaining Amount" - CustLedgEntry."Remaining Pmt. Disc. Possible",CustLedgEntry."Entry No.")
                           ELSE
                             CalcBankAccount("No.","Remaining Amount",CustLedgEntry."Entry No.");

                           GroupAmount := GroupAmount + "Remaining Amount";
                           CustLedgEntry."Document Status" := CustLedgEntry."Document Status"::Honored;
                           CustLedgEntry.MODIFY;


                           IF  (PostedBillGr."Dealing Type" = PostedBillGr."Dealing Type"::Discount)  AND
                               (PostedBillGr.Factoring <> PostedBillGr.Factoring::" ") THEN BEGIN
                             DiscFactLiabs(PostedDoc);

                           END;
                         END;

        OnPostDataItem=VAR
                         CustLedgEntry2@1100000 : Record 21;
                         JnlLineDim@1100001 : Record 356;
                         PostedDoc2@1170000000 : Record 7000003;
                       BEGIN
                         IF (DocCount = 0) OR (GroupAmount = 0) THEN BEGIN
                           IF DueOnly THEN
                             ERROR(
                               Text1100003 +
                               Text1100004)
                           ELSE
                             ERROR(
                               Text1100003 +
                               Text1100005);
                         END;
                         IF BankAccPostBuffer.FIND('-') THEN
                         REPEAT
                           CustLedgEntry2.GET(BankAccPostBuffer."Entry No.");
                           PostedDoc2.GET(0,CustLedgEntry2."Entry No.");
                           PostedBillGr.GET(PostedDoc2."Bill Gr./Pmt. Order No.");
                           BankAcc.GET(PostedBillGr."Bank Account No.");
                           IF PostedBillGr.Factoring = PostedBillGr.Factoring::" " THEN BEGIN
                             GenJnlLineNextNo := GenJnlLineNextNo + 10000;
                             WITH GenJnlLine DO BEGIN
                               CLEAR(GenJnlLine);
                               INIT;
                               "Line No." := GenJnlLineNextNo;
                               "Posting Date" := PostingDate;
                               "Document Type" := "Document Type"::Payment;
                               "Document No." := PostedBillGr."No.";
                               "Reason Code" := PostedBillGr."Reason Code";
                               IF PostedBillGr."Dealing Type" = PostedBillGr."Dealing Type"::Discount THEN BEGIN
                                 BankAcc.TESTFIELD("Bank Acc. Posting Group");
                                 BankAccPostingGr.GET(BankAcc."Bank Acc. Posting Group");
                                 VALIDATE("Account Type","Account Type"::"G/L Account");
                                 BankAccPostingGr.TESTFIELD("Liabs. for Disc. Bills Acc.");
                                 VALIDATE("Account No.",BankAccPostingGr."Liabs. for Disc. Bills Acc.");
                               END ELSE BEGIN
                                 VALIDATE("Account Type",GenJnlLine."Account Type"::"Bank Account");
                                 VALIDATE("Account No.",BankAcc."No.");
                               END;
                               Description := COPYSTR(STRSUBSTNO(Text1100006,PostedBillGr."No."),1,MAXSTRLEN(Description));
                               VALIDATE("Currency Code",PostedBillGr."Currency Code");
                               VALIDATE(Amount,BankAccPostBuffer.Amount);
                               "Source Code" := SourceCode;
                               "System-Created Entry" := TRUE;
                               GenJnlLine."Shortcut Dimension 1 Code" := BankAccPostBuffer."Global Dimension 1 Code";
                               GenJnlLine."Shortcut Dimension 2 Code" := BankAccPostBuffer."Global Dimension 2 Code";
                               INSERT;
                               CarteraDimMgt.CustInsertJnlLinDimension(GenJnlLine,CustLedgEntry2,TRUE);
                               SumLCYAmt := SumLCYAmt + "Amount (LCY)";
                             END;
                           END; // ELSE
                             // FactBankAccounting;
                         UNTIL BankAccPostBuffer.NEXT = 0;

                         IF PostedBillGr.Factoring <> PostedBillGr.Factoring::" " THEN
                           FactBankAccounting;

                         IF PostedBillGr."Currency Code" <> '' THEN BEGIN
                           IF SumLCYAmt <> 0 THEN BEGIN
                             Currency.SETFILTER(Code,PostedBillGr."Currency Code");
                             Currency.FIND('-');
                             IF SumLCYAmt > 0 THEN BEGIN
                               Currency.TESTFIELD("Residual Gains Account");
                               Acct := Currency."Residual Gains Account";
                             END ELSE BEGIN
                               Currency.TESTFIELD("Residual Losses Account");
                               Acct := Currency."Residual Losses Account";
                             END;
                             GenJnlLineNextNo := GenJnlLineNextNo + 10000;
                             WITH GenJnlLine DO BEGIN
                               CLEAR(GenJnlLine);
                               INIT;
                               "Line No." := GenJnlLineNextNo;
                               "Posting Date" := PostingDate;
                               "Document No." := PostedBillGr."No.";
                               "Reason Code" := PostedBillGr."Reason Code";
                               VALIDATE("Account Type","Account Type"::"G/L Account");
                               VALIDATE("Account No.",Acct);
                               Description := Text1100007;
                               VALIDATE("Currency Code",'');
                               VALIDATE(Amount,-SumLCYAmt);
                               "Source Code" := SourceCode;
                               "System-Created Entry" := TRUE;
                               GenJnlLine."Shortcut Dimension 1 Code" := BankAcc."Global Dimension 1 Code";
                               GenJnlLine."Shortcut Dimension 2 Code" := BankAcc."Global Dimension 2 Code";
                               INSERT;
                               CarteraDimMgt.CustInsertJnlLinDimension(GenJnlLine,CustLedgEntry2,TRUE);
                             END;
                           END;
                         END;
                         IF GenJnlLine.FIND('-') THEN
                           REPEAT
                             GenJnlLine2 := GenJnlLine;
                             GenJnlPostLine.SetFromSettlement(TRUE);
                             JnlLineDim.SETRANGE("Table ID",DATABASE::"Gen. Journal Line");
                             JnlLineDim.SETRANGE("Journal Template Name",GenJnlLine2."Journal Template Name");
                             JnlLineDim.SETRANGE("Journal Batch Name",GenJnlLine2."Journal Batch Name");
                             JnlLineDim.SETRANGE("Journal Line No.",GenJnlLine2."Line No.");
                             GenJnlPostLine.RunWithCheck(GenJnlLine2,JnlLineDim);
                             IF PostedBillGr.GET(GenJnlLine."Document No.") THEN
                               DocPost.CloseBillGroupIfEmpty(PostedBillGr,PostingDate);
                           UNTIL GenJnlLine.NEXT = 0;

                         Window.CLOSE;

                         IF ExistVATEntry THEN BEGIN
                           GLReg.FIND('+');
                           GLReg."From VAT Entry No." := FirstVATEntryNo;
                           GLReg."To VAT Entry No." := LastVATEntryNo;
                           GLReg.MODIFY;
                         END;

                         COMMIT;

                         IF NOT HidePrintDialog THEN
                           MESSAGE(Text1100008,DocCount,GroupAmount);
                       END;

      }
      SECTIONS
      {
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=5060;
      Height=990;
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 5   ;TextBox      ;3410 ;0    ;1650 ;440  ;CaptionML=[ENU=Posting Date;
                                                              ESP=Fecha registro];
                                                   NotBlank=Yes;
                                                   SourceExpr=PostingDate }
      { 6   ;CheckBox     ;3410 ;550  ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Due bills only;
                                                              ESP=S¢lo docs. vencidos];
                                                   SourceExpr=DueOnly }
      { 13  ;Label        ;0    ;0    ;3300 ;440  ;ParentControl=5 }
      { 7   ;Label        ;0    ;550  ;3300 ;440  ;ParentControl=6 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones] }

      { 5   ;2   ;Field     ;
                  CaptionML=[ENU=Posting Date;
                             ESP=Fecha registro];
                  NotBlank=Yes;
                  SourceExpr=PostingDate }

      { 6   ;2   ;Field     ;
                  CaptionML=[ENU=Due bills only;
                             ESP=S¢lo docs. vencidos];
                  SourceExpr=DueOnly }

    }
  }
  CODE
  {
    VAR
      Text1100000@1000 : TextConst 'ENU=Settling receivable documents         #1######;ESP=Liquidando documentos a cobrar        #1######';
      Text1100001@1001 : TextConst 'ENU=Bill settlement %1/%2;ESP=Liq. efecto %1/%2';
      Text1100002@1002 : TextConst 'ENU=Document settlement %1;ESP=Liq. documento %1';
      Text1100003@1003 : TextConst 'ENU=No receivable documents have been found that can be settled.;ESP=No hay documentos a cobrar que liquidar.';
      Text1100004@1004 : TextConst 'ENU=Please check that the selection is not empty and at least one receivable documents is open and due.;ESP=Compruebe que ha seleccionado al menos un documento a cobrar pdte. y vencido.';
      Text1100005@1005 : TextConst 'ENU=Please check that the selection is not empty and at least one receivable documents is open.;ESP=Compruebe que ha seleccionado al menos un documento a cobrar pendiente.';
      Text1100006@1006 : TextConst 'ENU=Bill Group settlement %1;ESP=Liquidaci¢n remesa %1';
      Text1100007@1007 : TextConst 'ENU=Residual adjust generated by rounding Amount;ESP=Ajuste por redondeo';
      Text1100008@1008 : TextConst 'ENU=%1 receivable documents totaling %2 have been settled.;ESP=Se han liquidado %1 documentos a cobrar por un importe de %2.';
      Text1100009@1009 : TextConst 'ENU=Bill Group settlement %1 Customer No. %2;ESP=Liquida remesa %1 cliente n§ %2';
      Text1100010@1010 : TextConst 'ENU=Receivable document settlement %1/%2;ESP=Liq. documento a cobrar %1/%2';
      SourceCodeSetup@1100000 : Record 242;
      PostedBillGr@1100001 : Record 7000006;
      GenJnlLine@1100002 : TEMPORARY Record 81;
      GenJnlLine2@1100003 : Record 81;
      CustLedgEntry@1100004 : Record 21;
      BankAccPostingGr@1100005 : Record 277;
      BankAcc@1100006 : Record 270;
      CurrExchRate@1100007 : Record 330;
      Currency@1100008 : Record 4;
      GLSetup@1100009 : Record 98;
      GLReg@1100010 : Record 45;
      VATPostingSetup@1100011 : Record 325;
      DocPost@1100012 : Codeunit 7000006;
      CarteraManagement@1100013 : Codeunit 7000000;
      GenJnlPostLine@1100014 : Codeunit 12;
      CarteraDimMgt@1100033 : Codeunit 7000011;
      BankAccPostBuffer@1100015 : TEMPORARY Record 7000012;
      NoRealVATBuffer@1100035 : TEMPORARY Record 7000012;
      Window@1100016 : Dialog;
      PostingDate@1100017 : Date;
      DueOnly@1100018 : Boolean;
      Delay@1100019 : Decimal;
      SourceCode@1100020 : Code[10];
      Acct@1100021 : Code[20];
      DocCount@1100022 : Integer;
      GroupAmount@1100023 : Decimal;
      GenJnlLineNextNo@1100024 : Integer;
      SumLCYAmt@1100025 : Decimal;
      TotalDisctdAmt@1100026 : Decimal;
      VATEntryNo@1100027 : Integer;
      ExistVATEntry@1100028 : Boolean;
      IsRedrawn@1100029 : Boolean;
      FirstVATEntryNo@1100030 : Integer;
      LastVATEntryNo@1100031 : Integer;
      TempCurrCode@1100032 : Code[10];
      ExistsNoRealVAT@1100034 : Boolean;
      HidePrintDialog@1100100 : Boolean;

    PROCEDURE DiscFactLiabs@1(PostedDoc2@1100000 : Record 7000003);
    VAR
      DisctedAmt@1100001 : Decimal;
      Currency2@1100002 : Record 4;
      RoundingPrec@1100003 : Decimal;
    BEGIN
      DisctedAmt := 0;
      GenJnlLineNextNo := GenJnlLineNextNo + 10000;

      IF PostedDoc2."Currency Code" <> '' THEN BEGIN
        Currency2.GET(PostedDoc2."Currency Code");
        RoundingPrec := Currency2."Amount Rounding Precision";
      END ELSE
        RoundingPrec := GLSetup."Amount Rounding Precision";

      DisctedAmt := ROUND(DocPost.FindDisctdAmt(
        PostedDoc2."Remaining Amount",
        PostedDoc2."Account No.",
        PostedDoc2."Bank Account No."),RoundingPrec);

      CalcBankAccount(PostedDoc2."No.",-DisctedAmt,CustLedgEntry."Entry No.");
      TotalDisctdAmt := TotalDisctdAmt + DisctedAmt;

        WITH GenJnlLine DO BEGIN
          CLEAR(GenJnlLine);
          INIT;
          "Line No." := GenJnlLineNextNo;
          "Posting Date" := PostingDate;
          "Document Type" := "Document Type"::Payment;
          "Document No." := PostedBillGr."No.";
          "Reason Code" := PostedBillGr."Reason Code";
          BankAcc.TESTFIELD("Bank Acc. Posting Group");
          BankAccPostingGr.GET(BankAcc."Bank Acc. Posting Group");
          VALIDATE("Account Type","Account Type"::"G/L Account");
          BankAccPostingGr.TESTFIELD("Liabs. for Factoring Acc.");
          VALIDATE("Account No.",BankAccPostingGr."Liabs. for Factoring Acc.");
          Description := COPYSTR(
            STRSUBSTNO(Text1100009,
            PostedBillGr."No.",
            PostedDoc2."Account No."),1,MAXSTRLEN(Description));
          VALIDATE("Currency Code",PostedBillGr."Currency Code");
          VALIDATE(Amount,DisctedAmt);
          "Source Code" := SourceCode;
          "System-Created Entry" := TRUE;
          GenJnlLine."Shortcut Dimension 1 Code" := PostedDoc2."Global Dimension 1 Code";
          GenJnlLine."Shortcut Dimension 2 Code" := PostedDoc2."Global Dimension 2 Code";

          INSERT;
          CarteraDimMgt.CustInsertJnlLinDimension(GenJnlLine,CustLedgEntry,TRUE);
          SumLCYAmt := SumLCYAmt + "Amount (LCY)";
        END;
    END;

    PROCEDURE FactBankAccounting@11();
    VAR
      CustLedgEntry2@1100000 : Record 21;
    BEGIN
      CASE TRUE OF
        PostedDoc."Dealing Type" = PostedDoc."Dealing Type"::Discount:
          BEGIN
            IF BankAccPostBuffer.FIND('-') THEN
            REPEAT
              GenJnlLineNextNo := GenJnlLineNextNo + 10000;
              CustLedgEntry2.GET(BankAccPostBuffer."Entry No.");
              WITH GenJnlLine DO BEGIN
                CLEAR(GenJnlLine);
                INIT;
                "Line No." := GenJnlLineNextNo;
                "Posting Date" := PostingDate;
                "Document Type" := "Document Type"::Payment;
                "Document No." := PostedBillGr."No.";
                "Reason Code" := PostedBillGr."Reason Code";
                BankAcc.TESTFIELD("Bank Acc. Posting Group");
                BankAccPostingGr.GET(BankAcc."Bank Acc. Posting Group");
                VALIDATE("Account Type",GenJnlLine."Account Type"::"Bank Account");
                VALIDATE("Account No.",BankAcc."No.");
                Description := COPYSTR(STRSUBSTNO(Text1100006,PostedBillGr."No."),1,MAXSTRLEN(Description));
                VALIDATE("Currency Code",PostedBillGr."Currency Code");
             //   VALIDATE(Amount,GroupAmount - TotalDisctdAmt);
                VALIDATE(Amount,BankAccPostBuffer.Amount);
                "Source Code" := SourceCode;
                "System-Created Entry" := TRUE;
                GenJnlLine."Shortcut Dimension 1 Code" := BankAccPostBuffer."Global Dimension 1 Code";
                GenJnlLine."Shortcut Dimension 2 Code" := BankAccPostBuffer."Global Dimension 2 Code";
                INSERT;
                CarteraDimMgt.CustInsertJnlLinDimension(GenJnlLine,CustLedgEntry2,TRUE);
                SumLCYAmt := SumLCYAmt + "Amount (LCY)";
              END;
            UNTIL BankAccPostBuffer.NEXT = 0;
          END;
        ELSE
          BEGIN
            IF BankAccPostBuffer.FIND('-') THEN
            REPEAT
              GenJnlLineNextNo := GenJnlLineNextNo + 10000;
              CustLedgEntry2.GET(BankAccPostBuffer."Entry No.");
              WITH GenJnlLine DO BEGIN
                CLEAR(GenJnlLine);
                INIT;
                "Line No." := GenJnlLineNextNo;
                "Posting Date" := PostingDate;
                "Document Type" := "Document Type"::Payment;
                "Document No." := PostedBillGr."No.";
                "Reason Code" := PostedBillGr."Reason Code";
                BankAcc.TESTFIELD("Bank Acc. Posting Group");
                BankAccPostingGr.GET(BankAcc."Bank Acc. Posting Group");
                VALIDATE("Account Type",GenJnlLine."Account Type"::"Bank Account");
                VALIDATE("Account No.",BankAcc."No.");
                Description := COPYSTR(STRSUBSTNO(Text1100006,PostedBillGr."No."),1,MAXSTRLEN(Description));
                VALIDATE("Currency Code",PostedBillGr."Currency Code");
                VALIDATE(Amount,BankAccPostBuffer.Amount);
                "Source Code" := SourceCode;
                "System-Created Entry" := TRUE;
                GenJnlLine."Shortcut Dimension 1 Code" := BankAccPostBuffer."Global Dimension 1 Code";
                GenJnlLine."Shortcut Dimension 2 Code" := BankAccPostBuffer."Global Dimension 2 Code";
                INSERT;
                CarteraDimMgt.CustInsertJnlLinDimension(GenJnlLine,CustLedgEntry2,TRUE);
                SumLCYAmt := SumLCYAmt + "Amount (LCY)";
              END;
            UNTIL BankAccPostBuffer.NEXT = 0;
          END;
      END;
    END;

    LOCAL PROCEDURE InsertGenJournalLine@4(AccType@1100000 : Integer;AccNo@1100001 : Code[20];Amount2@1100002 : Decimal;Dep@1100003 : Code[20];Proj@1100004 : Code[20]);
    BEGIN
      GenJnlLineNextNo :=  GenJnlLineNextNo + 10000;

      WITH GenJnlLine DO BEGIN
        CLEAR(GenJnlLine);
        INIT;
        "Line No." := GenJnlLineNextNo;
        "Posting Date" := PostingDate;
        "Document Type" := "Document Type"::Payment;
        "Document No." := PostedBillGr."No.";
        "Reason Code" := PostedBillGr."Reason Code";
        "Account Type" := AccType;
        "Account No." := AccNo;
        Description := COPYSTR(
          STRSUBSTNO(Text1100010,PostedDoc."Document No.",PostedDoc."No."),
          1,MAXSTRLEN(Description));
        VALIDATE("Currency Code",PostedDoc."Currency Code");
        VALIDATE(Amount,-Amount2);
        "Applies-to Doc. Type" := CustLedgEntry."Document Type";
        "Applies-to Doc. No." := '';
        "Applies-to Bill No." := CustLedgEntry."Bill No.";
        "Source Code" := SourceCode;
        "System-Created Entry" := TRUE;
        "Shortcut Dimension 1 Code" := Dep;
        "Shortcut Dimension 2 Code" := Proj;
        SumLCYAmt := SumLCYAmt + "Amount (LCY)";
        INSERT;
        CarteraDimMgt.CustInsertJnlLinDimension(GenJnlLine,CustLedgEntry,TRUE);
      END;
    END;

    PROCEDURE CalcBankAccount@5(BankAcc2@1100000 : Code[20];Amount2@1100001 : Decimal;EntryNo@1100002 : Integer);
    BEGIN
      IF BankAccPostBuffer.GET(BankAcc2,'',EntryNo) THEN BEGIN
        BankAccPostBuffer.Amount := BankAccPostBuffer.Amount + Amount2;
        BankAccPostBuffer.MODIFY;
      END ELSE BEGIN
        BankAccPostBuffer.INIT;
        BankAccPostBuffer.Account := BankAcc2;
        BankAccPostBuffer.Amount := Amount2;
        BankAccPostBuffer."Entry No." := EntryNo;
        BankAccPostBuffer."Global Dimension 1 Code" := CustLedgEntry."Global Dimension 1 Code";
        BankAccPostBuffer."Global Dimension 2 Code" := CustLedgEntry."Global Dimension 2 Code";
        BankAccPostBuffer.INSERT;
      END;
    END;

    PROCEDURE SetHidePrintDialog@1100100(NewHidePrintDialog@1100000 : Boolean);
    BEGIN
      HidePrintDialog := NewHidePrintDialog;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
