OBJECT Report 5181 Relocate Attachments
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Relocate Attachments;
               ESP=Reorganizar anexos];
    UseReqForm=No;
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   IF NOT CONFIRM(Text000,TRUE) THEN
                     CurrReport.QUIT;
                 END;

    OnPreReport=BEGIN
                  RMSetup.GET;
                  NoOfRecords := Attachment.COUNTAPPROX;
                  Window.OPEN(Text001);
                END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table5062;
        DataItemTableView=SORTING(No.);
        OnAfterGetRecord=VAR
                           FromDiskFileName@1000 : Text[250];
                         BEGIN
                           LineCount := LineCount + 1;
                           Window.UPDATE(1,ROUND(LineCount / NoOfRecords * 10000,1));

                           // Copy DiskFile to DiskFile
                           IF ("Storage Type" = "Storage Type"::"Disk File") AND
                              (RMSetup."Attachment Storage Type" = RMSetup."Attachment Storage Type"::"Disk File")
                           THEN BEGIN
                             RMSetup.TESTFIELD("Attachment Storage Location");
                             IF "Storage Pointer" <> RMSetup."Attachment Storage Location" THEN BEGIN
                               FromDiskFileName := ConstDiskFileName;
                               "Storage Pointer" := RMSetup."Attachment Storage Location";
                               MODIFY;
                               FILE.COPY(FromDiskFileName,ConstDiskFileName);
                               COMMIT;
                               FILE.ERASE(FromDiskFileName);
                             END;
                             CurrReport.SKIP;
                           END;

                           // Export Embedded Blob to Diskfile
                           IF ("Storage Type" = "Storage Type"::Embedded) AND
                              (RMSetup."Attachment Storage Type" = RMSetup."Attachment Storage Type"::"Disk File")
                           THEN BEGIN
                             RMSetup.TESTFIELD("Attachment Storage Location");
                             CALCFIELDS(Attachment);
                             IF Attachment.HASVALUE THEN BEGIN
                               "Storage Type" := "Storage Type"::"Disk File";
                               "Storage Pointer" := RMSetup."Attachment Storage Location";
                               Attachment.EXPORT(ConstDiskFileName); // Export blob to UNC location
                               CLEAR(Attachment);
                               MODIFY;
                               COMMIT;
                               CurrReport.SKIP;
                             END;
                           END;

                           // Import DiskFile to Embedded Blob
                           IF ("Storage Type" = "Storage Type"::"Disk File") AND
                              (RMSetup."Attachment Storage Type" = RMSetup."Attachment Storage Type"::Embedded)
                           THEN BEGIN
                             FromDiskFileName := ConstDiskFileName;
                             Attachment.IMPORT(FromDiskFileName); // Import file from UNC location
                             "Storage Type" := "Storage Type"::Embedded;
                             "Storage Pointer" := '';
                             MODIFY;
                             COMMIT;
                             FILE.ERASE(FromDiskFileName);
                             CurrReport.SKIP;
                           END;
                         END;

      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=9020;
      Height=3410;
    }
    CONTROLS
    {
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      RMSetup@1000 : Record 5079;
      Text000@1001 : TextConst 'ENU=Do you want to relocate existing attachments?;ESP=¨Confirma que desea reorganizar los anexos existentes?';
      Window@1002 : Dialog;
      Text001@1004 : TextConst 'ENU=Relocating attachments @1@@@@@@@@@@@@@;ESP=Reorganizando anexos   @1@@@@@@@@@@@@@';
      NoOfRecords@1005 : Integer;
      LineCount@1006 : Integer;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
