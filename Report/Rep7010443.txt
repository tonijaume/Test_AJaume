OBJECT Report 7010443 Contrato Precios x Plazas
{
  OBJECT-PROPERTIES
  {
    Date=16/10/09;
    Time=12:34:44;
    Version List=TRANSFER,INV;
  }
  PROPERTIES
  {
    CaptionML=ESP=Contrato Precios x Plazas;
    LeftMargin=1000;
    OnInitReport=BEGIN
                   // Indicamos aqui el numero m ximo de columas que tiene el report
                   wNoColum:=7;
                   rPar.FINDFIRST;
                 END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table7010403;
        DataItemTableView=SORTING(Garaje,N§ Contrato);
        NewPagePerRecord=Yes;
        OnAfterGetRecord=BEGIN
                           CreaTemporales;
                         END;

        ReqFilterFields=Garaje,N§ Contrato;
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Header;
            PrintOnEveryPage=Yes;
            SectionWidth=18300;
            SectionHeight=2538;
            OnPostSection=BEGIN
                            wImprimeCab:= TRUE;
                          END;

          }
          CONTROLS
          {
            { 1103350023;Label  ;0    ;0    ;7500 ;423  ;HorzAlign=Left;
                                                         FontSize=14;
                                                         FontBold=Yes;
                                                         CaptionML=ESP=Listado de Tarifas }
            { 1103350027;TextBox;0    ;423  ;7500 ;423  ;FontBold=Yes;
                                                         SourceExpr=COMPANYNAME }
            { 1103350028;Shape  ;0    ;2115 ;18300;423  ;ShapeStyle=HorzLine }
            { 1103350030;TextBox;5850 ;1269 ;2250 ;423  ;VertAlign=Bottom;
                                                         FontSize=8;
                                                         SourceExpr=Garaje }
            { 1103350031;Label  ;3900 ;1269 ;1800 ;423  ;ParentControl=1103350030;
                                                         VertAlign=Bottom;
                                                         FontSize=8;
                                                         FontBold=Yes;
                                                         MultiLine=Yes;
                                                         LeaderDots=Yes }
            { 1103350032;TextBox;5850 ;1692 ;2250 ;423  ;VertAlign=Bottom;
                                                         FontSize=8;
                                                         SourceExpr="N§ Contrato" }
            { 1103350033;Label  ;3900 ;1692 ;1800 ;423  ;ParentControl=1103350032;
                                                         VertAlign=Bottom;
                                                         FontSize=8;
                                                         FontBold=Yes;
                                                         MultiLine=Yes;
                                                         LeaderDots=Yes }
            { 1103350034;TextBox;10800;1269 ;1500 ;423  ;VertAlign=Bottom;
                                                         FontSize=8;
                                                         SourceExpr=Cliente }
            { 1103350035;Label  ;8550 ;1269 ;2100 ;423  ;ParentControl=1103350034;
                                                         VertAlign=Bottom;
                                                         FontSize=8;
                                                         FontBold=Yes;
                                                         MultiLine=Yes;
                                                         LeaderDots=Yes }
            { 1103350036;TextBox;10800;1692 ;1500 ;423  ;VertAlign=Bottom;
                                                         FontSize=8;
                                                         SourceExpr=Touroperador }
            { 1103350037;Label  ;8550 ;1692 ;2100 ;423  ;ParentControl=1103350036;
                                                         VertAlign=Bottom;
                                                         FontSize=8;
                                                         FontBold=Yes;
                                                         MultiLine=Yes;
                                                         LeaderDots=Yes }
            { 1103350038;TextBox;1950 ;1269 ;1500 ;423  ;VertAlign=Bottom;
                                                         FontSize=8;
                                                         CaptionML=ESP=Fecha inicio;
                                                         SourceExpr="Fecha inicio contrato" }
            { 1103350039;Label  ;0    ;1269 ;1800 ;423  ;ParentControl=1103350038;
                                                         HorzAlign=Left;
                                                         VertAlign=Bottom;
                                                         FontSize=8;
                                                         FontBold=Yes;
                                                         MultiLine=Yes;
                                                         LeaderDots=Yes }
            { 1103350040;TextBox;1950 ;1692 ;1500 ;423  ;VertAlign=Bottom;
                                                         FontSize=8;
                                                         CaptionML=ESP=Fecha final;
                                                         SourceExpr="Fecha final contrato" }
            { 1103350041;Label  ;0    ;1692 ;1800 ;423  ;ParentControl=1103350040;
                                                         HorzAlign=Left;
                                                         VertAlign=Bottom;
                                                         FontSize=8;
                                                         FontBold=Yes;
                                                         MultiLine=Yes;
                                                         LeaderDots=Yes }
            { 1103350042;TextBox;12450;1269 ;5850 ;423  ;VertAlign=Bottom;
                                                         FontSize=8;
                                                         SourceExpr=nom_cliente }
            { 1103350043;TextBox;12450;1692 ;5850 ;423  ;VertAlign=Bottom;
                                                         FontSize=8;
                                                         SourceExpr=nom_touroperador }
          }
           }
        { PROPERTIES
          {
            SectionType=Footer;
            PrintOnEveryPage=Yes;
            PlaceInBottom=Yes;
            SectionWidth=18300;
            SectionHeight=846;
            OnPostSection=BEGIN
                            wImprimeCab:= TRUE;
                          END;

          }
          CONTROLS
          {
            { 1103350044;TextBox;4800 ;423  ;3150 ;423  ;HorzAlign=Right;
                                                         SourceExpr=FORMAT(TODAY,0,4) }
            { 1103350045;TextBox;8100 ;423  ;2250 ;423  ;HorzAlign=Right;
                                                         SourceExpr=USERID }
            { 1103350046;Label  ;16950;423  ;900  ;423  ;ParentControl=1103350047 }
            { 1103350047;TextBox;17850;423  ;450  ;423  ;CaptionML=ESP=P gina;
                                                         SourceExpr=CurrReport.PAGENO }
            { 1103350048;Shape  ;0    ;0    ;18300;423  ;ShapeStyle=HorzLine }
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=1;
        DataItemTable=Table2000000026;
        DataItemTableView=SORTING(Number);
        OnPreDataItem=BEGIN
                        rTPrecios.SETCURRENTKEY(Garaje,"N§ Contrato","Tipo precio","Codigo servicio","Zona desde",
                                               "Zona hasta","Fecha desde","Fecha hasta","Tipo Vehiculo","Rango desde","Rango hasta");

                        rTRangos.RESET;
                        rTRangos.SETCURRENTKEY(Garaje,"N§ Contrato","Tipo precio","Codigo servicio","Zona desde",
                                               "Zona hasta","Fecha desde","Fecha hasta","Tipo Vehiculo","Rango desde","Rango hasta");

                        rTZonas.RESET;
                        rTZonas.SETCURRENTKEY(Garaje,"N§ Contrato","Tipo precio","Codigo servicio","Zona desde",
                                               "Zona hasta","Fecha desde","Fecha hasta","Tipo Vehiculo","Rango desde","Rango hasta");

                        rTZonas.FINDSET;

                        CLEAR(wCodSerAnt);
                        CLEAR(wVuelta);
                      END;

        OnAfterGetRecord=VAR
                           lwCont@1103350000 : Integer;
                           lwPrec@1103350001 : ARRAY [2] OF Decimal;
                         BEGIN
                           // Rellena el encabezamiento
                           wImprimeCab:= (wCodSerAnt <> rTZonas."Codigo servicio");
                           IF wCodSerAnt <> rTZonas."Codigo servicio" THEN BEGIN
                             EstableceVueltas;

                             wCodSerAnt := rTZonas."Codigo servicio";
                             EstableceRangos(wCodSerAnt);
                           END;

                           CLEAR(wPrecios);

                           rTRangos.SETRANGE("Codigo servicio", rTZonas."Codigo servicio");
                           IF rTRangos.FINDSET THEN BEGIN
                             CLEAR(lwCont);
                             IF wVuelta > 1 THEN BEGIN // Avanzamos los rangos necesarios
                               REPEAT
                                 lwCont+=1;
                               UNTIL (lwCont >= wNoColum * (wVuelta-1)) OR (rTRangos.NEXT=0);
                               CLEAR(lwCont);
                             END;
                             REPEAT
                               lwCont+=1;
                               rTPrecios.RESET;
                               rTPrecios.SETRANGE("Codigo servicio", rTZonas."Codigo servicio");
                               rTPrecios.SETRANGE("Zona desde"     , rTZonas."Zona desde");
                               rTPrecios.SETRANGE("Zona hasta"     , rTZonas."Zona hasta");
                               rTPrecios.SETRANGE("Rango desde"    , rTRangos."Rango desde");
                               rTPrecios.SETRANGE("Rango hasta"    , rTRangos."Rango hasta");
                               IF rTPrecios.FINDSET THEN BEGIN
                                 CASE rTPrecios."Tipo aplicacion" OF
                                   rTPrecios."Tipo aplicacion"::Vehiculo: wPrecios[lwCont] := FORMAT(rTPrecios."Precio Vehiculo (DL)");
                                   rTPrecios."Tipo aplicacion"::Pax : BEGIN  // Dividimos entre Adultos y Ni¤os
                                     CLEAR(lwPrec);
                                     lwPrec[1] := rTPrecios."Precio Adulto (DL)";
                                     lwPrec[2] := rTPrecios."Precio Ni¤o (DL)";
                                     // Si el precio de adulto y ni¤o es igual solo mostramos uno
                                     wPrecios[lwCont] += FORMAT(lwPrec[1]);
                                     IF (lwPrec[1] <> lwPrec[2]) AND (lwPrec[2]<>0) THEN BEGIN
                                       IF wPrecios[lwCont] <> '' THEN
                                         wPrecios[lwCont] += ' - ';
                                       wPrecios[lwCont] += STRSUBSTNO(Text50000,lwPrec[2]);
                                     END;
                                   END;
                                 END;
                               END;
                             UNTIL (lwCont >= wNoColum) OR (rTRangos.NEXT=0);
                           END;

                           wMinimos:= rTZonas."Minimos Zona";
                           wZonas  := STRSUBSTNO('%1 - %2',rTZonas."Zona desde" , rTZonas."Zona hasta");
                           // Si no hay m s zonas paramos el bucle
                           // JPT 24/03/06 No podemos hacer un break ya que no aparece el ultimo registro. Hacemos un setrange
                           IF rTZonas.NEXT=0 THEN BEGIN
                             IF wVuelta <> wNumVueltas THEN
                               CLEAR(wCodSerAnt)
                             ELSE
                               SETRANGE(Number, Number);
                               //CurrReport.BREAK;
                           END;
                         END;

      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=18300;
            SectionHeight=0;
          }
          CONTROLS
          {
          }
           }
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=18300;
            SectionHeight=1692;
            OnPreSection=BEGIN
                           CurrReport.SHOWOUTPUT(wImprimeCab);
                         END;

          }
          CONTROLS
          {
            { 1103350001;TextBox;4650 ;846  ;1950 ;423  ;HorzAlign=Center;
                                                         FontSize=9;
                                                         FontBold=Yes;
                                                         SourceExpr=wRangos[1] }
            { 1103350002;TextBox;6600 ;846  ;1950 ;423  ;HorzAlign=Center;
                                                         FontSize=9;
                                                         FontBold=Yes;
                                                         SourceExpr=wRangos[2] }
            { 1103350003;TextBox;8550 ;846  ;1950 ;423  ;HorzAlign=Center;
                                                         FontSize=9;
                                                         FontBold=Yes;
                                                         SourceExpr=wRangos[3] }
            { 1103350004;TextBox;10500;846  ;1950 ;423  ;HorzAlign=Center;
                                                         FontSize=9;
                                                         FontBold=Yes;
                                                         SourceExpr=wRangos[4] }
            { 1103350005;TextBox;12450;846  ;1950 ;423  ;HorzAlign=Center;
                                                         FontSize=9;
                                                         FontBold=Yes;
                                                         SourceExpr=wRangos[5] }
            { 1103350022;TextBox;14400;846  ;1950 ;423  ;HorzAlign=Center;
                                                         FontSize=9;
                                                         FontBold=Yes;
                                                         SourceExpr=wRangos[6] }
            { 1103350024;TextBox;16350;846  ;1950 ;423  ;HorzAlign=Center;
                                                         FontSize=9;
                                                         FontBold=Yes;
                                                         SourceExpr=wRangos[7] }
            { 1103350025;TextBox;0    ;423  ;8550 ;423  ;FontSize=9;
                                                         FontBold=Yes;
                                                         SourceExpr=NombreCodSer(wCodSerAnt) }
            { 1103350026;Shape  ;0    ;1269 ;18300;423  ;ShapeStyle=HorzLine }
            { 1103350049;Label  ;3750 ;846  ;750  ;423  ;ParentControl=1103350029;
                                                         HorzAlign=Center;
                                                         VertAlign=Bottom;
                                                         FontSize=9;
                                                         FontBold=Yes;
                                                         MultiLine=Yes;
                                                         CaptionML=ESP=Min. }
          }
           }
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=18300;
            SectionHeight=423;
          }
          CONTROLS
          {
            { 1103350000;TextBox;0    ;0    ;3600 ;423  ;SourceExpr=wZonas }
            { 1103350006;TextBox;4650 ;0    ;1950 ;423  ;HorzAlign=Center;
                                                         SourceExpr=wPrecios[1] }
            { 1103350007;TextBox;6600 ;0    ;1950 ;423  ;HorzAlign=Center;
                                                         SourceExpr=wPrecios[2] }
            { 1103350008;TextBox;8550 ;0    ;1950 ;423  ;HorzAlign=Center;
                                                         SourceExpr=wPrecios[3] }
            { 1103350009;TextBox;10500;0    ;1950 ;423  ;HorzAlign=Center;
                                                         SourceExpr=wPrecios[4] }
            { 1103350010;TextBox;12450;0    ;1950 ;423  ;HorzAlign=Center;
                                                         SourceExpr=wPrecios[5] }
            { 1103350011;TextBox;14400;0    ;1950 ;423  ;HorzAlign=Center;
                                                         SourceExpr=wPrecios[6] }
            { 1103350012;TextBox;16350;0    ;1950 ;423  ;HorzAlign=Center;
                                                         SourceExpr=wPrecios[7] }
            { 1103350029;TextBox;3750 ;0    ;750  ;423  ;HorzAlign=Center;
                                                         CaptionML=ESP=Minim.;
                                                         DecimalPlaces=0:2;
                                                         BlankZero=Yes;
                                                         SourceExpr=wMinimos }
          }
           }
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=9020;
      Height=3410;
    }
    CONTROLS
    {
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      Text50000@1103355000 : TextConst 'ESP=N %1';
      rPar@1103350017 : Record 7010311;
      rTRangos@1103350000 : TEMPORARY Record 7010405;
      rTPrecios@1103350001 : TEMPORARY Record 7010405;
      rTZonas@1103350008 : TEMPORARY Record 7010405;
      rTZonas2@1103350010 : TEMPORARY Record 7010405;
      wVentana@1103350002 : Dialog;
      wZonas@1103350003 : Text[100];
      wPrecios@1103350004 : ARRAY [10] OF Text[30];
      wRangos@1103350005 : ARRAY [10] OF Text[30];
      wMinimos@1103350015 : Decimal;
      wCodSerAnt@1103350006 : Code[10];
      Tex0001@1103350007 : TextConst 'ESP=El contrato %1 tiene m s de %2 rangos de precios para %3, Los dem s No se van a imprimir';
      wNoColum@1103350009 : Integer;
      wVuelta@1103350011 : Integer;
      wNumVueltas@1103350012 : Integer;
      Tex0002@1103350013 : TextConst 'ESP=No se ha encontrado ning£n precio para el contrato %1';
      wImprimeCab@1103350014 : Boolean;
      Tex0003@1103350016 : TextConst 'ESP=Creando Temporal\@1@@@@@@@@@@';
      wCambiaPag@1103350018 : Boolean;

    PROCEDURE CreaTemporales@1103350001();
    VAR
      lrPreciosVta@1103350000 : Record 7010405;
      lwOrden@1103350001 : Integer;
      lwNContrato@1103350002 : Code[10];
      lwTotal@1103350003 : Integer;
      lwCont@1103350004 : Integer;
    BEGIN
      // CreaTemporales
      // Crea los archivos temporales

      CLEAR(rTRangos);
      rTRangos.SETCURRENTKEY(Garaje,"N§ Contrato","Tipo precio","Codigo servicio","Zona desde",
                             "Zona hasta","Fecha desde","Fecha hasta","Tipo Vehiculo","Rango desde","Rango hasta");
      IF rTRangos.FINDFIRST THEN
        rTRangos.DELETEALL;

      CLEAR(rTZonas);
      rTZonas.SETCURRENTKEY(Garaje,"N§ Contrato","Tipo precio","Codigo servicio","Zona desde",
                             "Zona hasta","Fecha desde","Fecha hasta","Tipo Vehiculo","Rango desde","Rango hasta");
      IF rTZonas.FINDFIRST THEN
        rTZonas.DELETEALL;

      CLEAR(rTPrecios);
      IF rTPrecios.FINDFIRST THEN
        rTPrecios.DELETEALL;

      IF "Contrato venta"."Basado en contrato n§" = '' THEN
        lwNContrato := "Contrato venta"."N§ Contrato"
      ELSE
        lwNContrato := "Contrato venta"."Basado en contrato n§";

      CLEAR(lrPreciosVta);
      lrPreciosVta.SETRANGE(Garaje        , "Contrato venta".Garaje);
      lrPreciosVta.SETRANGE("N§ Contrato" , lwNContrato);
      lrPreciosVta.SETRANGE("Tipo precio" , lrPreciosVta."Tipo precio"::"Precio Base");
      IF lrPreciosVta.FINDSET THEN BEGIN
        CLEAR(lwOrden);
        wVentana.OPEN(Tex0003);
        CLEAR(lwCont);
        lwTotal := lrPreciosVta.COUNT;
        REPEAT
          rTPrecios:= lrPreciosVta;
          rTPrecios.INSERT;

          rTRangos.SETRANGE("Rango desde"     , lrPreciosVta."Rango desde");
          rTRangos.SETRANGE("Rango hasta"     , lrPreciosVta."Rango hasta");
          rTRangos.SETRANGE("Codigo servicio" , lrPreciosVta."Codigo servicio");
          IF NOT rTRangos.FINDSET THEN BEGIN
            rTRangos."Rango desde"     := lrPreciosVta."Rango desde";
            rTRangos."Rango hasta"     := lrPreciosVta."Rango hasta";
            rTRangos."Codigo servicio" := lrPreciosVta."Codigo servicio";
            lwOrden+=1;
            rTRangos.Orden := lwOrden;
            rTRangos.INSERT;
          END;

          rTZonas.SETRANGE("Zona desde"      , lrPreciosVta."Zona desde");
          rTZonas.SETRANGE("Zona hasta"      , lrPreciosVta."Zona hasta");
          rTZonas.SETRANGE("Codigo servicio" , lrPreciosVta."Codigo servicio");
          IF NOT rTZonas.FINDSET THEN BEGIN
            rTZonas."Zona desde"      := lrPreciosVta."Zona desde";
            rTZonas."Zona hasta"      := lrPreciosVta."Zona hasta";
            rTZonas."Codigo servicio" := lrPreciosVta."Codigo servicio";
            lwOrden+=1;
            rTZonas.Orden := lwOrden;
            rTZonas.INSERT;
          END;
          IF (lrPreciosVta."Minimos Zona" <> 0) AND (rTZonas."Minimos Zona" < lrPreciosVta."Minimos Zona") THEN BEGIN
            rTZonas."Minimos Zona"  := lrPreciosVta."Minimos Zona";
            rTZonas.MODIFY;
          END;
          lwCont +=1;
          wVentana.UPDATE(1,ROUND(lwCont/lwTotal*10000,1));
        UNTIL lrPreciosVta.NEXT=0;
        wVentana.CLOSE;
      END ELSE
        MESSAGE(Tex0002,"Contrato venta"."N§ Contrato");
    END;

    PROCEDURE EstableceRangos@1103350000(pwCodSer@1103350000 : Code[10]) : Integer;
    VAR
      lwCont@1103350002 : Integer;
      lwTotalRangos@1103350001 : Integer;
      lwOk@1103350003 : Boolean;
    BEGIN
      // EstableceRangos
      // Establece los rangos y los marca en cabecera. Estable la cantidad de vueltas que se dar  para ese codigo de servicio
      // Por si hay m s rangos que columas
      // wNumVueltas: Numero de vueltas que se necesitan para ese codigo de servicio (en virtud de los rangos)

      CLEAR(wRangos);
      CLEAR(lwTotalRangos);
      rTRangos.SETRANGE("Codigo servicio", pwCodSer);
      IF rTRangos.FINDSET THEN BEGIN
        CLEAR(lwCont);
        lwTotalRangos:=  rTRangos.COUNT;
        IF wVuelta > 1 THEN BEGIN // Avanzamos los rangos necesarios
          REPEAT
            lwCont+=1;
          UNTIL (lwCont >= wNoColum * (wVuelta-1)) OR (rTRangos.NEXT =0);
          CLEAR(lwCont);
        END;
        REPEAT
          lwCont+=1;
          wRangos[lwCont]:= STRSUBSTNO('%1 - %2', rTRangos."Rango desde",rTRangos."Rango hasta");
        UNTIL (lwCont >= wNoColum) OR (rTRangos.NEXT =0);

        wNumVueltas := lwTotalRangos DIV wNoColum;
        IF lwTotalRangos MOD wNoColum <> 0 THEN
          wNumVueltas +=1;

        IF wNumVueltas = 0 THEN // Minimo siempre una vuelta
          wNumVueltas:= 1;
      END;
    END;

    PROCEDURE EstableceVueltas@1103350007();
    BEGIN
      // EstableceVueltas
      // Si se cambia de vuelta pone el puntero de Zonas donde estaba anteriormente
      // wVuelta: Numero de vuelta que se ejecuta

      IF wVuelta < wNumVueltas THEN BEGIN
        wVuelta +=1;
        rTZonas.COPY(rTZonas2);
      END
      ELSE BEGIN
        rTZonas2.COPY(rTZonas);
        wVuelta:=1;
      END;
    END;

    PROCEDURE NombreCodSer@1103350021(pwCod@1103350000 : Code[10]) : Text[30];
    VAR
      lrCodSer@1103350001 : Record 7010320;
    BEGIN
      // NombreCodSer
      // Devuelve el nombre del codigo de servicio

      CLEAR(lrCodSer);
      IF lrCodSer.GET(pwCod) THEN
        EXIT(lrCodSer.Descripcion)
      ELSE
        EXIT(pwCod);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
