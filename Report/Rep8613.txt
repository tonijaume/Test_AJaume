OBJECT Report 8613 Create Item Journal Lines
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create Item Journal Lines;
               ESP=Crear l¡neas diario producto];
    ProcessingOnly=Yes;
    OnPostReport=BEGIN
                   MESSAGE(Text004);
                 END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table27;
        DataItemTableView=SORTING(No.);
        OnPreDataItem=BEGIN
                        IF JournalTemplate = '' THEN
                          ERROR(Text001);

                        IF BatchName = '' THEN
                          ERROR(Text002);

                        IF TemplateCode = '' THEN
                          ERROR(Text003);

                        ItemJnlLine.SETRANGE("Journal Template Name",JournalTemplate);
                        ItemJnlLine.SETRANGE("Journal Batch Name",BatchName);
                        IF ItemJnlLine.FIND('+') THEN
                          LineNo := ItemJnlLine."Line No." + 10000
                        ELSE
                          LineNo := 10000;

                        ItemJnlBatch.GET(JournalTemplate,BatchName);
                        StdItemJnl.GET(JournalTemplate,TemplateCode);
                      END;

        OnAfterGetRecord=VAR
                           StdItemJnlLine@1000 : Record 753;
                         BEGIN
                           ItemJnlLine.INIT;
                           IF GetStandardJournalLine THEN BEGIN
                             Initialize(StdItemJnl,ItemJnlBatch.Name);

                             StdItemJnlLine.SETRANGE("Journal Template Name",StdItemJnl."Journal Template Name");
                             StdItemJnlLine.SETRANGE("Standard Journal Code",StdItemJnl.Code);
                             IF StdItemJnlLine.FIND('-') THEN
                               REPEAT
                                 CopyItemJnlFromStdJnl(StdItemJnlLine,ItemJnlLine);
                                 ItemJnlLine.VALIDATE("Entry Type",EntryTypes);
                                 ItemJnlLine.VALIDATE("Item No.","No.");

                                 IF PostingDate <> 0D THEN
                                   ItemJnlLine.VALIDATE("Posting Date",PostingDate);

                                 IF DocumentDate <> 0D THEN BEGIN
                                   ItemJnlLine.VALIDATE("Posting Date",DocumentDate);
                                   ItemJnlLine."Posting Date" := PostingDate;
                                 END;

                                 IF NOT ItemJnlLine.INSERT(TRUE) THEN
                                   ItemJnlLine.MODIFY(TRUE);
                               UNTIL StdItemJnlLine.NEXT = 0;
                           END ELSE BEGIN
                             ItemJnlLine.VALIDATE("Journal Template Name",ItemJnlLine.GETFILTER(ItemJnlLine."Journal Template Name"));
                             ItemJnlLine.VALIDATE("Journal Batch Name",BatchName);
                             ItemJnlLine."Line No." := LineNo;
                             LineNo := LineNo + 10000;

                             ItemJnlLine.VALIDATE("Entry Type",EntryTypes);
                             ItemJnlLine.VALIDATE("Item No.","No.");

                             IF PostingDate <> 0D THEN
                               ItemJnlLine.VALIDATE("Posting Date",PostingDate);

                             IF DocumentDate <> 0D THEN BEGIN
                               ItemJnlLine.VALIDATE("Posting Date",DocumentDate);
                               ItemJnlLine."Posting Date" := PostingDate;
                             END;

                             IF NOT ItemJnlLine.INSERT(TRUE) THEN
                               ItemJnlLine.MODIFY(TRUE);
                           END;
                         END;

        ReqFilterFields=No.,Statistics Group,Vendor No.,Blocked;
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=9020;
      Height=3850;
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 2   ;TextBox      ;3850 ;0    ;1700 ;440  ;CaptionML=[ENU=Entry Type;
                                                              ESP=Tipo movimiento];
                                                   OptionCaptionML=[ENU=Purchase,Sale,Positive Adjmt.,Negative Adjmt.;
                                                                    ESP=Compra,Venta,Ajuste positivo,Ajuste negativo];
                                                   SourceExpr=EntryTypes }
      { 3   ;Label        ;0    ;0    ;3740 ;440  ;ParentControl=2 }
      { 1   ;Label        ;0    ;550  ;3740 ;440  ;ParentControl=4 }
      { 4   ;TextBox      ;3850 ;550  ;1700 ;440  ;CaptionML=[ENU=Posting Date;
                                                              ESP=Fecha registro];
                                                   SourceExpr=PostingDate }
      { 5   ;Label        ;0    ;1100 ;3740 ;440  ;ParentControl=6 }
      { 6   ;TextBox      ;3850 ;1100 ;1700 ;440  ;CaptionML=[ENU=Document Date;
                                                              ESP=Fecha emisi¢n documento];
                                                   SourceExpr=DocumentDate }
      { 7   ;Label        ;0    ;2200 ;3740 ;440  ;ParentControl=8 }
      { 8   ;TextBox      ;3850 ;2200 ;1700 ;440  ;CaptionML=[ENU=Batch Name;
                                                              ESP=Nombre secci¢n];
                                                   SourceExpr=BatchName;
                                                   OnLookup=VAR
                                                              ItemJnlBatches@1000 : Form 262;
                                                            BEGIN
                                                              IF JournalTemplate <> '' THEN BEGIN
                                                                ItemJnlBatch.SETRANGE("Journal Template Name",JournalTemplate);
                                                                ItemJnlBatches.SETTABLEVIEW(ItemJnlBatch);
                                                              END;

                                                              ItemJnlBatches.LOOKUPMODE := TRUE;
                                                              ItemJnlBatches.EDITABLE := FALSE;
                                                              IF ItemJnlBatches.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                ItemJnlBatches.GETRECORD(ItemJnlBatch);
                                                                BatchName := ItemJnlBatch.Name;
                                                              END;
                                                            END;
                                                             }
      { 9   ;Label        ;0    ;2750 ;3740 ;440  ;ParentControl=10 }
      { 10  ;TextBox      ;3850 ;2750 ;1700 ;440  ;CaptionML=[ENU=Standard Item Journal;
                                                              ESP=Diario productos est ndar];
                                                   SourceExpr=TemplateCode;
                                                   TableRelation="Standard Item Journal".Code;
                                                   OnLookup=VAR
                                                              StdItemJnl1@1001 : Record 752;
                                                              StdItemJnls@1000 : Form 753;
                                                            BEGIN
                                                              IF JournalTemplate <> '' THEN BEGIN
                                                                StdItemJnl1.SETRANGE("Journal Template Name",JournalTemplate);
                                                                StdItemJnls.SETTABLEVIEW(StdItemJnl1);
                                                              END;

                                                              StdItemJnls.LOOKUPMODE := TRUE;
                                                              StdItemJnls.EDITABLE := FALSE;
                                                              IF StdItemJnls.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                StdItemJnls.GETRECORD(StdItemJnl1);
                                                                TemplateCode := StdItemJnl1.Code;
                                                              END;
                                                            END;
                                                             }
      { 11  ;Label        ;0    ;1650 ;3740 ;440  ;ParentControl=12 }
      { 12  ;TextBox      ;3850 ;1650 ;1700 ;440  ;CaptionML=[ENU=Journal Template;
                                                              ESP=Libro diario];
                                                   SourceExpr=JournalTemplate;
                                                   TableRelation="Gen. Journal Batch".Name;
                                                   OnLookup=VAR
                                                              ItemJnlTemplates@1000 : Form 102;
                                                              ItemJnlTemplate@1001 : Record 82;
                                                            BEGIN
                                                              ItemJnlTemplate.SETRANGE(Type,ItemJnlTemplate.Type::Item);
                                                              ItemJnlTemplate.SETRANGE(Recurring,FALSE);
                                                              ItemJnlTemplates.SETTABLEVIEW(ItemJnlTemplate);

                                                              ItemJnlTemplates.LOOKUPMODE := TRUE;
                                                              ItemJnlTemplates.EDITABLE := FALSE;
                                                              IF ItemJnlTemplates.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                ItemJnlTemplates.GETRECORD(ItemJnlTemplate);
                                                                JournalTemplate := ItemJnlTemplate.Name;
                                                              END;
                                                            END;
                                                             }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones] }

      { 2   ;2   ;Field     ;
                  CaptionML=[ENU=Entry Type;
                             ESP=Tipo movimiento];
                  OptionCaptionML=[ENU=Purchase,Sale,Positive Adjmt.,Negative Adjmt.;
                                   ESP=Compra,Venta,Ajuste positivo,Ajuste negativo];
                  SourceExpr=EntryTypes }

      { 4   ;2   ;Field     ;
                  CaptionML=[ENU=Posting Date;
                             ESP=Fecha registro];
                  SourceExpr=PostingDate }

      { 6   ;2   ;Field     ;
                  CaptionML=[ENU=Document Date;
                             ESP=Fecha emisi¢n documento];
                  SourceExpr=DocumentDate }

      { 12  ;2   ;Field     ;
                  CaptionML=[ENU=Journal Template;
                             ESP=Libro diario];
                  SourceExpr=JournalTemplate;
                  TableRelation="Gen. Journal Batch".Name;
                  OnLookup=VAR
                             ItemJnlTemplates@1000 : Form 102;
                             ItemJnlTemplate@1001 : Record 82;
                           BEGIN
                             ItemJnlTemplate.SETRANGE(Type,ItemJnlTemplate.Type::Item);
                             ItemJnlTemplate.SETRANGE(Recurring,FALSE);
                             ItemJnlTemplates.SETTABLEVIEW(ItemJnlTemplate);

                             ItemJnlTemplates.LOOKUPMODE := TRUE;
                             ItemJnlTemplates.EDITABLE := FALSE;
                             IF ItemJnlTemplates.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               ItemJnlTemplates.GETRECORD(ItemJnlTemplate);
                               JournalTemplate := ItemJnlTemplate.Name;
                             END;
                           END;
                            }

      { 8   ;2   ;Field     ;
                  CaptionML=[ENU=Batch Name;
                             ESP=Nombre secci¢n];
                  SourceExpr=BatchName;
                  OnLookup=VAR
                             ItemJnlBatches@1000 : Form 262;
                           BEGIN
                             IF JournalTemplate <> '' THEN BEGIN
                               ItemJnlBatch.SETRANGE("Journal Template Name",JournalTemplate);
                               ItemJnlBatches.SETTABLEVIEW(ItemJnlBatch);
                             END;

                             ItemJnlBatches.LOOKUPMODE := TRUE;
                             ItemJnlBatches.EDITABLE := FALSE;
                             IF ItemJnlBatches.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               ItemJnlBatches.GETRECORD(ItemJnlBatch);
                               BatchName := ItemJnlBatch.Name;
                             END;
                           END;
                            }

      { 10  ;2   ;Field     ;
                  CaptionML=[ENU=Standard Item Journal;
                             ESP=Diario productos est ndar];
                  SourceExpr=TemplateCode;
                  TableRelation="Standard Item Journal".Code;
                  OnLookup=VAR
                             StdItemJnl1@1001 : Record 752;
                             StdItemJnls@1000 : Form 753;
                           BEGIN
                             IF JournalTemplate <> '' THEN BEGIN
                               StdItemJnl1.SETRANGE("Journal Template Name",JournalTemplate);
                               StdItemJnls.SETTABLEVIEW(StdItemJnl1);
                             END;

                             StdItemJnls.LOOKUPMODE := TRUE;
                             StdItemJnls.EDITABLE := FALSE;
                             IF StdItemJnls.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               StdItemJnls.GETRECORD(StdItemJnl1);
                               TemplateCode := StdItemJnl1.Code;
                             END;
                           END;
                            }

    }
  }
  CODE
  {
    VAR
      StdItemJnl@1007 : Record 752;
      ItemJnlBatch@1006 : Record 233;
      LastItemJnlLine@1012 : Record 83;
      ItemJnlLine@1011 : Record 83;
      JnlLineDimFrom@1009 : Record 356;
      ItemJnlMgt@1013 : Codeunit 240;
      EntryTypes@1000 : 'Purchase,Sale,Positive Adjmt.,Negative Adjmt.';
      PostingDate@1001 : Date;
      DocumentDate@1002 : Date;
      BatchName@1003 : Code[20];
      TemplateCode@1004 : Code[20];
      LineNo@1008 : Integer;
      JournalTemplate@1015 : Text[10];
      Text001@1018 : TextConst 'ENU=Item Journal Template name is blank.;ESP=El nombre del libro diario producto est  vac¡o.';
      Text002@1017 : TextConst 'ENU=Item Journal Batch name is blank.;ESP=El nombre de la secci¢n diario producto est  vac¡o.';
      Text003@1016 : TextConst 'ENU=Standard Item Journal code is blank.;ESP=El c¢digo diario productos est ndar est  vac¡o.';
      Text004@1005 : TextConst 'ENU=Item Journal lines are successfully created.;ESP=Las l¡neas del diario productos se crearon correctamente.';

    PROCEDURE GetStandardJournalLine@3() : Boolean;
    VAR
      StdItemJnlLine@1000 : Record 753;
    BEGIN
      IF TemplateCode = '' THEN
        EXIT;
      StdItemJnlLine.SETRANGE("Journal Template Name",StdItemJnl."Journal Template Name");
      StdItemJnlLine.SETRANGE("Standard Journal Code",StdItemJnl.Code);
      EXIT(StdItemJnlLine.FIND('-'));
    END;

    PROCEDURE Initialize@2(StdItemJnl@1000 : Record 752;JnlBatchName@1001 : Code[10]);
    BEGIN
      ItemJnlLine."Journal Template Name" := StdItemJnl."Journal Template Name";
      ItemJnlLine."Journal Batch Name" := JnlBatchName;
      ItemJnlLine.SETRANGE("Journal Template Name",StdItemJnl."Journal Template Name");
      ItemJnlLine.SETRANGE("Journal Batch Name",JnlBatchName);

      LastItemJnlLine.SETRANGE("Journal Template Name",StdItemJnl."Journal Template Name");
      LastItemJnlLine.SETRANGE("Journal Batch Name",JnlBatchName);

      IF LastItemJnlLine.FIND('+') THEN;
    END;

    PROCEDURE CopyItemJnlFromStdJnl@7(StdItemJnlLine@1000 : Record 753;VAR ItemJnlLine@1001 : Record 83);
    BEGIN
      ItemJnlLine.INIT;
      ItemJnlLine."Line No." := 0;
      ItemJnlLine.SetUpNewLine(LastItemJnlLine);
      IF LastItemJnlLine."Line No." <> 0 THEN
        ItemJnlLine."Line No." := LastItemJnlLine."Line No." + 10000
      ELSE
        ItemJnlLine."Line No." := 10000;


      ItemJnlLine.TRANSFERFIELDS(StdItemJnlLine,FALSE);

      IF (ItemJnlLine."Item No." <> '') AND (ItemJnlLine."Unit Amount" = 0)  THEN
        ItemJnlLine.RecalculateUnitAmount;

      IF (ItemJnlLine."Entry Type" = ItemJnlLine."Entry Type"::Output) AND
        (ItemJnlLine."Value Entry Type" <> ItemJnlLine."Value Entry Type"::Revaluation)
      THEN
        ItemJnlLine."Invoiced Quantity" := 0
      ELSE
        ItemJnlLine."Invoiced Quantity" := ItemJnlLine.Quantity;
      ItemJnlLine.TESTFIELD("Qty. per Unit of Measure");
      ItemJnlLine."Invoiced Qty. (Base)" := ROUND(ItemJnlLine."Invoiced Quantity" * ItemJnlLine."Qty. per Unit of Measure",0.00001);

      ItemJnlLine.INSERT(TRUE);
      CopyStdItemJnlLineDims(StdItemJnlLine,ItemJnlLine);

      LastItemJnlLine := ItemJnlLine;
    END;

    LOCAL PROCEDURE CopyStdItemJnlLineDims@5(StdItemJnlLine@1001 : Record 753;ItemJnlLine1@1000 : Record 83);
    VAR
      DimMgt@1004 : Codeunit 408;
    BEGIN
      JnlLineDimFrom.SETRANGE("Table ID",DATABASE::"Standard Item Journal Line");
      JnlLineDimFrom.SETRANGE("Journal Template Name",StdItemJnlLine."Journal Template Name");
      JnlLineDimFrom.SETRANGE("Journal Batch Name",StdItemJnlLine."Standard Journal Code");
      JnlLineDimFrom.SETRANGE("Journal Line No.",StdItemJnlLine."Line No.");

      DimMgt.DeleteJnlLineDim(DATABASE::"Item Journal Line",ItemJnlLine1."Journal Template Name",
        ItemJnlLine1."Journal Batch Name",ItemJnlLine1."Line No.",0);

      DimMgt.MoveJnlLineDimToJnlLineDim(JnlLineDimFrom,DATABASE::"Item Journal Line",
        ItemJnlLine1."Journal Template Name",ItemJnlLine1."Journal Batch Name",ItemJnlLine1."Line No.");
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
