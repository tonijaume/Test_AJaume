OBJECT Report 7131 Import Item Budget from Excel
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Import Item Budget from Excel;
               ESP=Importar de Excel ppto. prod.];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF ToItemBudgetName = '' THEN
                    ERROR(Text000);

                  IF NOT ItemBudgetName.GET(AnalysisArea,ToItemBudgetName) THEN BEGIN
                    IF NOT CONFIRM(Text001,FALSE,ItemBudgetName.TABLECAPTION,ToItemBudgetName) THEN
                      CurrReport.BREAK;
                    ItemBudgetName."Analysis Area" := AnalysisArea;
                    ItemBudgetName.Name := ToItemBudgetName;
                    ItemBudgetName.INSERT;
                  END ELSE BEGIN
                    IF ItemBudgetName.Blocked THEN
                      ERROR(Text002,ItemBudgetEntry.FIELDCAPTION("Budget Name"),ToItemBudgetName);
                    IF NOT CONFIRM(
                      Text003,FALSE,
                      LOWERCASE(FORMAT(SELECTSTR(ImportOption + 1,Text010))),
                      ItemBudgetEntry.FIELDCAPTION("Budget Name"),ToItemBudgetName)
                    THEN
                      CurrReport.BREAK;
                  END;

                  ExcelBuf.LOCKTABLE;
                  ItemBudgetBuf.LOCKTABLE;

                  ReadExcelSheet;
                  AnalyseData;
                END;

    OnPostReport=BEGIN
                   ExcelBuf.DELETEALL;
                   ItemBudgetBuf.DELETEALL;
                 END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table7136;
        DataItemTableView=SORTING(Item No.,Source Type,Source No.,Location Code,Global Dimension 1 Code,Global Dimension 2 Code,Budget Dimension 1 Code,Budget Dimension 2 Code,Budget Dimension 3 Code,Date);
        DataItemVarName=ItemBudgetBuf;
        OnPreDataItem=BEGIN
                        RecNo := 0;

                        IF ImportOption = ImportOption::"Replace entries" THEN BEGIN
                          ItemBudgetEntry.SETRANGE("Analysis Area",AnalysisArea);
                          ItemBudgetEntry.SETRANGE("Budget Name",ToItemBudgetName);
                          ItemBudgetEntry.DELETEALL(TRUE);
                        END;

                        ItemBudgetEntry.RESET;
                        IF ItemBudgetEntry.FIND('+') THEN
                          EntryNo := ItemBudgetEntry."Entry No." + 1
                        ELSE
                          EntryNo := 1;
                      END;

        OnAfterGetRecord=BEGIN
                           RecNo := RecNo + 1;
                           ItemBudgetEntry.INIT;
                           ItemBudgetEntry.VALIDATE("Entry No.",EntryNo);
                           ItemBudgetEntry.VALIDATE("Analysis Area",AnalysisArea);
                           ItemBudgetEntry.VALIDATE("Budget Name",ToItemBudgetName);
                           ItemBudgetEntry.VALIDATE("Item No.","Item No.");
                           ItemBudgetEntry.VALIDATE("Location Code","Location Code");
                           ItemBudgetEntry.VALIDATE(Date,Date);
                           ItemBudgetEntry.VALIDATE(Description,Description);
                           ItemBudgetEntry.VALIDATE("Source Type","Source Type");
                           ItemBudgetEntry.VALIDATE("Source No.","Source No.");
                           ItemBudgetEntry.VALIDATE("Sales Amount","Sales Amount");
                           ItemBudgetEntry.VALIDATE(Quantity,Quantity);
                           ItemBudgetEntry.VALIDATE("Cost Amount","Cost Amount");
                           ItemBudgetEntry.VALIDATE("Global Dimension 1 Code","Global Dimension 1 Code");
                           ItemBudgetEntry.VALIDATE("Global Dimension 2 Code","Global Dimension 2 Code");
                           ItemBudgetEntry.VALIDATE("Budget Dimension 1 Code","Budget Dimension 1 Code");
                           ItemBudgetEntry.VALIDATE("Budget Dimension 2 Code","Budget Dimension 2 Code");
                           ItemBudgetEntry.VALIDATE("Budget Dimension 3 Code","Budget Dimension 3 Code");
                           ItemBudgetEntry.VALIDATE("User ID",USERID);
                           ItemBudgetEntry.INSERT(TRUE);
                           EntryNo := EntryNo + 1;
                         END;

        OnPostDataItem=BEGIN
                         IF RecNo > 0 THEN
                           MESSAGE(Text004,ItemBudgetEntry.TABLECAPTION,RecNo);
                       END;

      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=9130;
      Height=4290;
      SaveValues=Yes;
      OnOpenForm=VAR
                   ItemBudgetName@1000 : Record 7132;
                 BEGIN
                   Description := Text005 + FORMAT(WORKDATE);
                   IF NOT ItemBudgetName.GET(AnalysisArea,ToItemBudgetName) THEN
                     ToItemBudgetName := '';

                   ValueType := ValueTypeHidden;
                   RequestOptionsForm.SalesValueType.VISIBLE := AnalysisArea = AnalysisArea::Sales;
                   RequestOptionsForm.PurchValueType.VISIBLE := AnalysisArea = AnalysisArea::Purchase;
                 END;

    }
    CONTROLS
    {
      { 1   ;TextBox      ;3630 ;550  ;5500 ;440  ;CaptionML=[ENU=Workbook File Name;
                                                              ESP=Nombre fichero libro];
                                                   SourceExpr=FileName;
                                                   OnAssistEdit=BEGIN
                                                                  UploadFile;
                                                                END;

                                                   OnAfterValidate=BEGIN
                                                                     UploadFile;
                                                                   END;
                                                                    }
      { 2   ;Label        ;220  ;550  ;3300 ;440  ;ParentControl=1 }
      { 3   ;TextBox      ;3630 ;1100 ;5500 ;440  ;CaptionML=[ENU=Worksheet Name;
                                                              ESP=Nombre hoja];
                                                   SourceExpr=SheetName;
                                                   OnAssistEdit=VAR
                                                                  ExcelBuf@1001 : Record 370;
                                                                BEGIN
                                                                  IF ISSERVICETIER THEN
                                                                    SheetName := ExcelBuf.SelectSheetsName(UploadedFileName)
                                                                  ELSE
                                                                    SheetName := ExcelBuf.SelectSheetsName(FileName);
                                                                END;
                                                                 }
      { 4   ;Label        ;220  ;1100 ;3300 ;440  ;ParentControl=3 }
      { 5   ;TextBox      ;3630 ;3300 ;5500 ;440  ;CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n];
                                                   SourceExpr=Description }
      { 6   ;Label        ;220  ;3300 ;3300 ;440  ;ParentControl=5 }
      { 9   ;TextBox      ;3630 ;2200 ;2750 ;440  ;ValidateTableRelation=No;
                                                   CaptionML=[ENU=Budget Name;
                                                              ESP=Nombre ppto.];
                                                   SourceExpr=ToItemBudgetName;
                                                   OnLookup=VAR
                                                              ItemBudgetName@1002 : Record 7132;
                                                            BEGIN
                                                              ItemBudgetName.FILTERGROUP := 2;
                                                              ItemBudgetName.SETRANGE("Analysis Area",AnalysisArea);
                                                              ItemBudgetName.FILTERGROUP := 0;
                                                              IF FORM.RUNMODAL(FORM::"Item Budget Names",ItemBudgetName) = ACTION::LookupOK THEN BEGIN
                                                                Text := ItemBudgetName.Name;
                                                                EXIT(TRUE);
                                                              END;
                                                            END;
                                                             }
      { 10  ;Label        ;220  ;2200 ;3300 ;440  ;ParentControl=9 }
      { 11  ;Label        ;0    ;1650 ;3300 ;440  ;LeaderDots=No;
                                                   CaptionML=[ENU=Import to;
                                                              ESP=Importar a] }
      { 12  ;Label        ;0    ;0    ;3300 ;440  ;LeaderDots=No;
                                                   CaptionML=[ENU=Import from;
                                                              ESP=Importar de] }
      { 7   ;TextBox      ;3630 ;2750 ;2750 ;440  ;CaptionML=[ENU=Option;
                                                              ESP=Opci¢n];
                                                   OptionCaptionML=[ENU=Replace entries,Add entries;
                                                                    ESP=Reemplazar movs.,A¤adir movs.];
                                                   SourceExpr=ImportOption }
      { 8   ;Label        ;220  ;2750 ;3300 ;440  ;ParentControl=7 }
      { 13  ;TextBox      ;3630 ;3850 ;2750 ;440  ;Name=SalesValueType;
                                                   CaptionML=[ENU=Import Value as;
                                                              ESP=Importar valor como];
                                                   OptionCaptionML=[ENU=Sales Amount,COGS Amount,Quantity;
                                                                    ESP=Importe venta,Coste ventas,Cantidad];
                                                   OptionString=Sales Amount,COGS Amount,Quantity;
                                                   SourceExpr=ValueType }
      { 14  ;Label        ;220  ;3850 ;3300 ;440  ;ParentControl=13 }
      { 15  ;TextBox      ;3630 ;3850 ;2750 ;440  ;Name=PurchValueType;
                                                   CaptionML=[ENU=Import Value as;
                                                              ESP=Importar valor como];
                                                   OptionCaptionML=[ENU=,Cost Amount,Quantity;
                                                                    ESP=,Importe coste,Cantidad];
                                                   OptionString=,Cost Amount,Quantity;
                                                   SourceExpr=ValueType }
      { 16  ;Label        ;220  ;3850 ;3300 ;440  ;ParentControl=15 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnInit=BEGIN
               PurchValueTypeVisible := TRUE;
               SalesValueTypeVisible := TRUE;
             END;

      OnOpenPage=VAR
                   ItemBudgetName@1000 : Record 7132;
                 BEGIN
                   Description := Text005 + FORMAT(WORKDATE);
                   IF NOT ItemBudgetName.GET(AnalysisArea,ToItemBudgetName) THEN
                     ToItemBudgetName := '';

                   ValueType := ValueTypeHidden;
                   SalesValueTypeVisible := AnalysisArea = AnalysisArea::Sales;
                   PurchValueTypeVisible := AnalysisArea = AnalysisArea::Purchase;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones] }

      { 12  ;2   ;Group     ;
                  CaptionML=[ENU=Import from;
                             ESP=Importar de] }

      { 1   ;3   ;Field     ;
                  CaptionML=[ENU=Workbook File Name;
                             ESP=Nombre fichero libro];
                  SourceExpr=FileName;
                  OnValidate=BEGIN
                               FileNameOnAfterValidate;
                             END;

                  OnAssistEdit=BEGIN
                                 UploadFile;
                               END;
                                }

      { 3   ;3   ;Field     ;
                  CaptionML=[ENU=Worksheet Name;
                             ESP=Nombre hoja];
                  SourceExpr=SheetName;
                  OnAssistEdit=VAR
                                 ExcelBuf@1001 : Record 370;
                               BEGIN
                                 IF ISSERVICETIER THEN
                                   SheetName := ExcelBuf.SelectSheetsName(UploadedFileName)
                                 ELSE
                                   SheetName := ExcelBuf.SelectSheetsName(FileName);
                               END;
                                }

      { 11  ;2   ;Group     ;
                  CaptionML=[ENU=Import to;
                             ESP=Importar a] }

      { 9   ;3   ;Field     ;
                  CaptionML=[ENU=Budget Name;
                             ESP=Nombre ppto.];
                  SourceExpr=ToItemBudgetName;
                  OnLookup=VAR
                             ItemBudgetName@1002 : Record 7132;
                           BEGIN
                             ItemBudgetName.FILTERGROUP := 2;
                             ItemBudgetName.SETRANGE("Analysis Area",AnalysisArea);
                             ItemBudgetName.FILTERGROUP := 0;
                             IF FORM.RUNMODAL(FORM::"Item Budget Names",ItemBudgetName) = ACTION::LookupOK THEN BEGIN
                               Text := ItemBudgetName.Name;
                               EXIT(TRUE);
                             END;
                           END;
                            }

      { 7   ;3   ;Field     ;
                  CaptionML=[ENU=Option;
                             ESP=Opci¢n];
                  OptionCaptionML=[ENU=Replace entries,Add entries;
                                   ESP=Reemplazar movs.,A¤adir movs.];
                  SourceExpr=ImportOption }

      { 5   ;3   ;Field     ;
                  CaptionML=[ENU=Description;
                             ESP=Descripci¢n];
                  SourceExpr=Description }

      { 15  ;3   ;Field     ;
                  Name=PurchValueType;
                  CaptionML=[ENU=Import Value as;
                             ESP=Importar valor como];
                  OptionCaptionML=[ENU=,Cost Amount,Quantity;
                                   ESP=,Importe coste,Cantidad];
                  SourceExpr=ValueType;
                  Visible=PurchValueTypeVisible }

      { 13  ;3   ;Field     ;
                  Name=SalesValueType;
                  CaptionML=[ENU=Import Value as;
                             ESP=Importar valor como];
                  OptionCaptionML=[ENU=Sales Amount,COGS Amount,Quantity;
                                   ESP=Importe venta,Coste ventas,Cantidad];
                  SourceExpr=ValueType;
                  Visible=SalesValueTypeVisible }

    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You must specify a budget name to import to.;ESP=Debe especif. nombre de ppto. para importar a.';
      Text001@1001 : TextConst 'ENU=Do you want to create %1 %2.;ESP=Confirma que desea crear %1 %2.';
      Text002@1002 : TextConst 'ENU=%1 %2 is blocked. You cannot import entries.;ESP=%1 %2 est  bloqueado. No puede importar movs.';
      Text003@1003 : TextConst 'ENU=Are you sure you want to %1 for %2 %3.;ESP=Est  seguro que quiere el %1 para %2 %3.';
      Text004@1004 : TextConst 'ENU=%1 table has been successfully updated with %2 entries.;ESP=%1 tabla se ha actualizado correctamente con %2 movs.';
      Text005@1005 : TextConst 'ENU="Imported from Excel ";ESP="Importado de Excel "';
      Text006@1006 : TextConst 'ENU=Import Excel File;ESP=Importar fich. Excel';
      Text007@1023 : TextConst 'ENU=Table Data;ESP=Datos de la tabla';
      Text008@1022 : TextConst 'ENU=Show as Lines;ESP=Muestra como l¡neas';
      Text009@1009 : TextConst 'ENU=Show as Columns;ESP=Muestra como columnas';
      Text010@1010 : TextConst 'ENU=Replace entries,Add entries;ESP=Reemplazar movs.,A¤adir movs.';
      Text011@1011 : TextConst 'ENU=The text %1 can only be specified once in the Excel worksheet.;ESP=El texto %1 s¢lo se puede especificar una vez en la hoja de Excel.';
      Text012@1017 : TextConst 'ENU=The filters specified by worksheet must be placed in the lines before the table.;ESP=Los filtros especificados por la hoja deben ubicarse en las l¡neas anteriores a la tabla.';
      Text013@1015 : TextConst 'ENU=Date Filter;ESP=Filtro fecha';
      ExcelBuf@1027 : Record 370;
      GLSetup@1034 : Record 98;
      ItemBudgetName@1053 : Record 7132;
      ItemBudgetEntry@1013 : Record 7134;
      ItemBudgetManagement@1024 : Codeunit 7130;
      Window@1040 : Dialog;
      FileName@1037 : Text[250];
      UploadedFileName@1028 : Text[1024];
      SheetName@1038 : Text[250];
      Description@1041 : Text[50];
      ToItemBudgetName@1039 : Code[10];
      RecNo@1016 : Integer;
      EntryNo@1012 : Integer;
      ImportOption@1054 : 'Replace entries,Add entries';
      AnalysisArea@1048 : 'Sales,Purchase,Inventory';
      ValueType@1057 : 'Sales Amount,COGS / Cost Amount,Quantity';
      ValueTypeHidden@1026 : 'Sales Amount,COGS / Cost Amount,Quantity';
      GlSetupRead@1029 : Boolean;
      Text014@1014 : TextConst 'ENU=Customer Filter;ESP=Filtro cliente';
      Text015@1021 : TextConst 'ENU=Vendor Filter;ESP=Filtro proveedor';
      Text016@1008 : TextConst 'ENU=Analyzing Data...\\;ESP=Analizar Datos...\\';
      Text017@1007 : TextConst 'ENU=Item Filter;ESP=Filtro producto';
      Text018@1018 : TextConst 'ENU=%1 is not a valid dimension value.;ESP=%1 no es un valor de dimensi¢n aceptable.';
      Text019@1020 : TextConst 'ENU=%1 is not a valid line definition.;ESP=%1 no es una def. de l¡nea v lida.';
      Text020@1019 : TextConst 'ENU=%1 is not a valid column definition.;ESP=%1 no es una def. de columna v lida.';
      Text021@1025 : TextConst 'ENU=You must specify a dimension value in row %1, column %2.;ESP=Especifique un valor de dimensi¢n en la fila %1, columna %2.';
      SalesValueTypeVisible@19059405 : Boolean INDATASET;
      PurchValueTypeVisible@19069465 : Boolean INDATASET;

    LOCAL PROCEDURE ReadExcelSheet@1();
    BEGIN
      IF ISSERVICETIER THEN
        IF UploadedFileName = '' THEN
          UploadFile
        ELSE
          FileName := UploadedFileName;

      ExcelBuf.OpenBook(FileName,SheetName);
      ExcelBuf.ReadSheet;
    END;

    LOCAL PROCEDURE AnalyseData@2();
    VAR
      DateFilter@1003 : Text[30];
      LineDimCode@1014 : Text[30];
      ColumnDimCode@1013 : Text[30];
      ItemFilter@1004 : Code[250];
      LocationFilter@1017 : Code[10];
      GlobalDim1Filter@1009 : Code[250];
      GlobalDim2Filter@1008 : Code[250];
      BudgetDim1Filter@1007 : Code[250];
      BudgetDim2Filter@1006 : Code[250];
      BudgetDim3Filter@1005 : Code[250];
      SourceNoFilter@1010 : Code[250];
      CurrLineDimValue@1000 : Code[20];
      CurrColumnDimValue@1001 : Code[20];
      TotalRecNo@1002 : Integer;
      HeaderRowNo@1011 : Integer;
      SourceTypeFilter@1012 : ' ,Customer,Vendor,Item';
      LineDimOption@1016 : 'Item,Customer,Vendor,Period,Location,Global Dimension 1,Global Dimension 2,Budget Dimension 1,Budget Dimension 2,Budget Dimension 3';
      ColumnDimOption@1015 : 'Item,Customer,Vendor,Period,Location,Global Dimension 1,Global Dimension 2,Budget Dimension 1,Budget Dimension 2,Budget Dimension 3';
    BEGIN
      Window.OPEN(Text016 + '@1@@@@@@@@@@@@@@@@@@@@@@@@@\');
      TotalRecNo := ExcelBuf.COUNT;

      ItemBudgetBuf.DELETEALL;

      IF ExcelBuf.FIND('-') THEN
        REPEAT
          RecNo := RecNo + 1;
          Window.UPDATE(1,ROUND(RecNo / TotalRecNo * 10000,1));
          CASE TRUE OF
            STRPOS(ExcelBuf."Cell Value as Text",Text007) <> 0:
              BEGIN
                IF HeaderRowNo = 0 THEN
                  HeaderRowNo := ExcelBuf."Row No."
                ELSE
                  ERROR(STRSUBSTNO(Text011,Text007));

                ConvertFiltersToValue(
                  DateFilter,
                  ItemFilter,
                  GlobalDim1Filter,
                  GlobalDim2Filter,
                  BudgetDim1Filter,
                  BudgetDim2Filter,
                  BudgetDim3Filter,
                  SourceNoFilter,
                  SourceTypeFilter);

                IF ItemBudgetManagement.DimCodeNotAllowed(LineDimCode,ItemBudgetName) THEN
                  ERROR(Text019,LineDimCode);

                IF ItemBudgetManagement.DimCodeNotAllowed(ColumnDimCode,ItemBudgetName) THEN
                  ERROR(Text020,ColumnDimCode);

                ItemBudgetManagement.SetLineAndColDim(
                  ItemBudgetName,
                  LineDimCode,
                  LineDimOption,
                  ColumnDimCode,
                  ColumnDimOption);
              END;
            STRPOS(ExcelBuf."Cell Value as Text",IsGlobalDimFilter(1)) <> 0:
              BEGIN
                CheckFilterRowNo(HeaderRowNo);
                ExcelBuf.SETRANGE("Row No.",ExcelBuf."Row No.");
                IF ExcelBuf.NEXT <> 0 THEN
                  GlobalDim1Filter := ExcelBuf."Cell Value as Text";
                ExcelBuf.SETRANGE("Row No.");
              END;
            STRPOS(ExcelBuf."Cell Value as Text",IsGlobalDimFilter(2)) <> 0:
              BEGIN
                CheckFilterRowNo(HeaderRowNo);
                ExcelBuf.SETRANGE("Row No.",ExcelBuf."Row No.");
                IF ExcelBuf.NEXT <> 0 THEN
                  GlobalDim2Filter := ExcelBuf."Cell Value as Text";
                ExcelBuf.SETRANGE("Row No.");
              END;
            STRPOS(ExcelBuf."Cell Value as Text",IsBudgetDimFilter(1)) <> 0:
              BEGIN
                CheckFilterRowNo(HeaderRowNo);
                ExcelBuf.SETRANGE("Row No.",ExcelBuf."Row No.");
                IF ExcelBuf.NEXT <> 0 THEN
                  BudgetDim1Filter := ExcelBuf."Cell Value as Text";
                ExcelBuf.SETRANGE("Row No.");
              END;
            STRPOS(ExcelBuf."Cell Value as Text",IsBudgetDimFilter(2)) <> 0:
              BEGIN
                CheckFilterRowNo(HeaderRowNo);
                ExcelBuf.SETRANGE("Row No.",ExcelBuf."Row No.");
                IF ExcelBuf.NEXT <> 0 THEN
                  BudgetDim2Filter := ExcelBuf."Cell Value as Text";
                ExcelBuf.SETRANGE("Row No.");
              END;
            STRPOS(ExcelBuf."Cell Value as Text",IsBudgetDimFilter(3)) <> 0:
              BEGIN
                CheckFilterRowNo(HeaderRowNo);
                ExcelBuf.SETRANGE("Row No.",ExcelBuf."Row No.");
                IF ExcelBuf.NEXT <> 0 THEN
                  BudgetDim3Filter := ExcelBuf."Cell Value as Text";
                ExcelBuf.SETRANGE("Row No.");
              END;
            STRPOS(ExcelBuf."Cell Value as Text",Text017) <> 0:
              BEGIN
                CheckFilterRowNo(HeaderRowNo);
                ExcelBuf.SETRANGE("Row No.",ExcelBuf."Row No.");
                IF ExcelBuf.NEXT <> 0 THEN
                  ItemFilter := ExcelBuf."Cell Value as Text";
                ExcelBuf.SETRANGE("Row No.");
              END;
            STRPOS(ExcelBuf."Cell Value as Text",Text013) <> 0:
              BEGIN
                CheckFilterRowNo(HeaderRowNo);
                ExcelBuf.SETRANGE("Row No.",ExcelBuf."Row No.");
                IF ExcelBuf.NEXT <> 0 THEN
                  DateFilter := ExcelBuf."Cell Value as Text";
                ExcelBuf.SETRANGE("Row No.");
              END;
            STRPOS(ExcelBuf."Cell Value as Text",Text014) <> 0:
              BEGIN
                CheckFilterRowNo(HeaderRowNo);
                ExcelBuf.SETRANGE("Row No.",ExcelBuf."Row No.");
                IF ExcelBuf.NEXT <> 0 THEN BEGIN
                  SourceTypeFilter := SourceTypeFilter::Customer;
                  SourceNoFilter := ExcelBuf."Cell Value as Text";
                END;
                ExcelBuf.SETRANGE("Row No.");
              END;
            STRPOS(ExcelBuf."Cell Value as Text",Text015) <> 0:
              BEGIN
                CheckFilterRowNo(HeaderRowNo);
                ExcelBuf.SETRANGE("Row No.",ExcelBuf."Row No.");
                IF ExcelBuf.NEXT <> 0 THEN BEGIN
                  SourceTypeFilter := SourceTypeFilter::Vendor;
                  SourceNoFilter := ExcelBuf."Cell Value as Text";
                END;
                ExcelBuf.SETRANGE("Row No.");
              END;
            STRPOS(ExcelBuf."Cell Value as Text",Text008) <> 0:
              BEGIN
                CheckFilterRowNo(HeaderRowNo);
                ExcelBuf.SETRANGE("Row No.",ExcelBuf."Row No.");
                IF ExcelBuf.NEXT <> 0 THEN
                  LineDimCode := ExcelBuf."Cell Value as Text";
                ExcelBuf.SETRANGE("Row No.");
              END;
            STRPOS(ExcelBuf."Cell Value as Text",Text009) <> 0:
              BEGIN
                CheckFilterRowNo(HeaderRowNo);
                ExcelBuf.SETRANGE("Row No.",ExcelBuf."Row No.");
                IF ExcelBuf.NEXT <> 0 THEN
                  ColumnDimCode := ExcelBuf."Cell Value as Text";
                ExcelBuf.SETRANGE("Row No.");
              END;
            (ExcelBuf."Row No." > HeaderRowNo) AND (HeaderRowNo <> 0):
              BEGIN
                CurrLineDimValue := ExcelBuf."Cell Value as Text";
                ExcelBuf.SETRANGE("Row No.",ExcelBuf."Row No.");
                WHILE ExcelBuf.NEXT <> 0 DO BEGIN
                  CurrColumnDimValue := GetCurrColumnDimValue(ExcelBuf."Column No.",HeaderRowNo);
                  ExchangeFiltersWithDimValue(
                    CurrLineDimValue,
                    CurrColumnDimValue,
                    LineDimOption,
                    ColumnDimOption,
                    DateFilter,
                    ItemFilter,
                    LocationFilter,
                    GlobalDim1Filter,
                    GlobalDim2Filter,
                    BudgetDim1Filter,
                    BudgetDim2Filter,
                    BudgetDim3Filter,
                    SourceNoFilter,
                    SourceTypeFilter);

                  ItemBudgetBuf.INIT;
                  ItemBudgetBuf."Item No." := ItemFilter;
                  IF SourceTypeFilter <> 0 THEN
                    ItemBudgetBuf."Source Type" := SourceTypeFilter
                  ELSE
                    ItemBudgetBuf."Source Type":=LineDimOption;
                  ItemBudgetBuf."Source No." := SourceNoFilter;
                  ItemBudgetBuf."Location Code" := LocationFilter;
                  ItemBudgetBuf."Global Dimension 1 Code" := GlobalDim1Filter;
                  ItemBudgetBuf."Global Dimension 2 Code" := GlobalDim2Filter;
                  ItemBudgetBuf."Budget Dimension 1 Code" := BudgetDim1Filter;
                  ItemBudgetBuf."Budget Dimension 2 Code" := BudgetDim2Filter;
                  ItemBudgetBuf."Budget Dimension 3 Code" := BudgetDim3Filter;
                  EVALUATE(ItemBudgetBuf.Date,DateFilter);
                  CASE ValueType OF
                    ValueType::"Sales Amount":
                      EVALUATE(ItemBudgetBuf."Sales Amount",ExcelBuf."Cell Value as Text");
                    ValueType::"COGS / Cost Amount":
                      EVALUATE(ItemBudgetBuf."Cost Amount",ExcelBuf."Cell Value as Text");
                    ValueType::Quantity:
                      EVALUATE(ItemBudgetBuf.Quantity,ExcelBuf."Cell Value as Text");
                  END;
                  ItemBudgetBuf.INSERT;
                END;
                ExcelBuf.SETRANGE("Row No.");
              END;
          END;
        UNTIL ExcelBuf.NEXT = 0;

      Window.CLOSE;
    END;

    LOCAL PROCEDURE ExchangeFiltersWithDimValue@10(CurrLineDimValue@1000 : Code[20];CurrColumnDimValue@1001 : Code[20];LineDimOption@1003 : 'Item,Customer,Vendor,Period,Location,Global Dimension 1,Global Dimension 2,Budget Dimension 1,Budget Dimension 2,Budget Dimension 3';ColumnDimOption@1002 : 'Item,Customer,Vendor,Period,Location,Global Dimension 1,Global Dimension 2,Budget Dimension 1,Budget Dimension 2,Budget Dimension 3';VAR DateFilter@1012 : Text[30];VAR ItemFilter@1011 : Code[250];VAR LocationFilter@1013 : Code[10];VAR GlobalDim1Filter@1010 : Code[250];VAR GlobalDim2Filter@1009 : Code[250];VAR BudgetDim1Filter@1008 : Code[250];VAR BudgetDim2Filter@1007 : Code[250];VAR BudgetDim3Filter@1006 : Code[250];VAR SourceNoFilter@1005 : Code[250];VAR SourceTypeFilter@1004 : ' ,Customer,Vendor,Item');
    BEGIN
      CASE LineDimOption OF
        LineDimOption::Item: ItemFilter := CurrLineDimValue;
        LineDimOption::Customer:
          BEGIN
            SourceNoFilter := CurrLineDimValue;
            IF SourceTypeFilter = SourceTypeFilter::" " THEN
              SourceTypeFilter := SourceTypeFilter::Customer;
          END;
        LineDimOption::Vendor:
          BEGIN
            SourceNoFilter := CurrLineDimValue;
            IF SourceTypeFilter = SourceTypeFilter::" " THEN
              SourceTypeFilter := SourceTypeFilter::Vendor;
          END;

        LineDimOption::Period: DateFilter := CurrLineDimValue;
        LineDimOption::Location: LocationFilter := CurrColumnDimValue;
        LineDimOption::"Global Dimension 1": GlobalDim1Filter := CurrLineDimValue;
        LineDimOption::"Global Dimension 2": GlobalDim2Filter := CurrLineDimValue;
        LineDimOption::"Budget Dimension 1": BudgetDim1Filter := CurrLineDimValue;
        LineDimOption::"Budget Dimension 2": BudgetDim2Filter := CurrLineDimValue;
        LineDimOption::"Budget Dimension 3": BudgetDim3Filter := CurrLineDimValue;
        ELSE
          ERROR(Text018,CurrLineDimValue);
      END;

      CASE ColumnDimOption OF
        ColumnDimOption::Item: ItemFilter := CurrColumnDimValue;
        ColumnDimOption::Customer:
          BEGIN
            SourceNoFilter := CurrColumnDimValue;
            IF SourceTypeFilter = SourceTypeFilter::" " THEN
              SourceTypeFilter := SourceTypeFilter::Customer;
          END;
        ColumnDimOption::Vendor:
          BEGIN
            SourceNoFilter := CurrColumnDimValue;
            IF SourceTypeFilter = SourceTypeFilter::" " THEN
              SourceTypeFilter := SourceTypeFilter::Vendor;
          END;
        ColumnDimOption::Period: DateFilter := CurrColumnDimValue;
        ColumnDimOption::Location: LocationFilter := CurrColumnDimValue;
        ColumnDimOption::"Global Dimension 1": GlobalDim1Filter := CurrColumnDimValue;
        ColumnDimOption::"Global Dimension 2": GlobalDim2Filter := CurrColumnDimValue;
        ColumnDimOption::"Budget Dimension 1": BudgetDim1Filter := CurrColumnDimValue;
        ColumnDimOption::"Budget Dimension 2": BudgetDim2Filter := CurrColumnDimValue;
        ColumnDimOption::"Budget Dimension 3": BudgetDim3Filter := CurrColumnDimValue;
        ELSE
          ERROR(Text018,CurrColumnDimValue);
      END;
    END;

    LOCAL PROCEDURE GetCurrColumnDimValue@7(ColNo@1000 : Integer;HeaderRowNo@1002 : Integer) : Code[20];
    VAR
      ExcelBuf2@1001 : Record 370;
    BEGIN
      IF NOT ExcelBuf2.GET(HeaderRowNo,ColNo) THEN
        ERROR(Text021,HeaderRowNo,ColNo);
      EXIT(ExcelBuf2."Cell Value as Text");
    END;

    LOCAL PROCEDURE ConvertFiltersToValue@13(VAR DateFilter@1005 : Text[30];VAR ItemFilter@1006 : Code[250];VAR GlobalDim1Filter@1011 : Code[250];VAR GlobalDim2Filter@1010 : Code[250];VAR BudgetDim1Filter@1009 : Code[250];VAR BudgetDim2Filter@1008 : Code[250];VAR BudgetDim3Filter@1007 : Code[250];VAR SourceNoFilter@1012 : Code[250];VAR SourceTypeFilter@1013 : ' ,Customer,Vendor,Item');
    VAR
      Item@1000 : Record 27;
      Calendar@1001 : Record 2000000007;
      Cust@1002 : Record 18;
      Vend@1003 : Record 23;
      DimValue@1004 : Record 349;
      CurrDate@1014 : Date;
    BEGIN
      IF ItemFilter <> '' THEN BEGIN
        Item.SETFILTER("No.",ItemFilter);
        ItemFilter := Item.GETRANGEMIN("No.");
        Item.GET(ItemFilter);
      END;

      IF DateFilter <> '' THEN BEGIN
        Calendar.SETFILTER("Period Start",DateFilter);
        DateFilter := FORMAT(Calendar.GETRANGEMIN("Period Start"));
        EVALUATE(CurrDate,DateFilter);
        Calendar.GET(Calendar."Period Type"::Date,CurrDate);
      END;

      IF SourceNoFilter <> '' THEN BEGIN
        CASE SourceTypeFilter OF
          SourceTypeFilter::Customer:
            BEGIN
              Cust.SETFILTER("No.",SourceNoFilter);
              SourceNoFilter := Cust.GETRANGEMIN("No.");
              Cust.GET(SourceNoFilter);
            END;
          SourceTypeFilter::Vendor:
            BEGIN
              Vend.SETFILTER("No.",SourceNoFilter);
              SourceNoFilter := Vend.GETRANGEMIN("No.");
              Vend.GET(SourceNoFilter);
            END;
        END;
      END;

      GetGLSetup;
      IF GlobalDim1Filter <> '' THEN BEGIN
        DimValue.SETFILTER(Code,GlobalDim1Filter);
        GlobalDim1Filter := DimValue.GETRANGEMIN(Code);
        DimValue.GET(GLSetup."Global Dimension 1 Code",GlobalDim1Filter);
      END;

      IF GlobalDim2Filter <> '' THEN BEGIN
        DimValue.SETFILTER(Code,GlobalDim2Filter);
        GlobalDim2Filter := DimValue.GETRANGEMIN(Code);
        DimValue.GET(GLSetup."Global Dimension 2 Code",GlobalDim2Filter);
      END;

      IF BudgetDim1Filter <> '' THEN BEGIN
        DimValue.SETFILTER(Code,BudgetDim1Filter);
        BudgetDim1Filter := DimValue.GETRANGEMIN(Code);
        DimValue.GET(ItemBudgetName."Budget Dimension 1 Code",BudgetDim1Filter);
      END;

      IF BudgetDim2Filter <> '' THEN BEGIN
        DimValue.SETFILTER(Code,BudgetDim2Filter);
        BudgetDim2Filter := DimValue.GETRANGEMIN(Code);
        DimValue.GET(ItemBudgetName."Budget Dimension 2 Code",BudgetDim2Filter);
      END;

      IF BudgetDim3Filter <> '' THEN BEGIN
        DimValue.SETFILTER(Code,BudgetDim3Filter);
        BudgetDim3Filter := DimValue.GETRANGEMIN(Code);
        DimValue.GET(ItemBudgetName."Budget Dimension 3 Code",BudgetDim3Filter);
      END;
    END;

    LOCAL PROCEDURE CheckFilterRowNo@12(HeaderRowNo@1000 : Integer);
    BEGIN
      IF (HeaderRowNo <> 0) AND (ExcelBuf."Row No." > HeaderRowNo) THEN
        ERROR(Text012);
    END;

    LOCAL PROCEDURE IsGlobalDimFilter@3(DimNo@1000 : Integer) : Text[30];
    VAR
      Dim@1001 : Record 348;
    BEGIN
      GetGLSetup;
      CASE DimNo OF
        1: IF Dim.GET(GLSetup."Global Dimension 1 Code") THEN;
        2: IF Dim.GET(GLSetup."Global Dimension 2 Code") THEN;
      END;
      EXIT(Dim."Filter Caption");
    END;

    LOCAL PROCEDURE IsBudgetDimFilter@11(DimNo@1000 : Integer) : Text[30];
    VAR
      Dim@1001 : Record 348;
    BEGIN
      CASE DimNo OF
        1: IF Dim.GET(ItemBudgetName."Budget Dimension 1 Code") THEN;
        2: IF Dim.GET(ItemBudgetName."Budget Dimension 2 Code") THEN;
        3: IF Dim.GET(ItemBudgetName."Budget Dimension 3 Code") THEN;
      END;
      EXIT(Dim."Filter Caption");
    END;

    LOCAL PROCEDURE GetGLSetup@5();
    BEGIN
      IF NOT GlSetupRead THEN
        GLSetup.GET;
      GlSetupRead := TRUE;
    END;

    PROCEDURE SetProperties@6(NewToItemBudgetName@1000 : Code[10];NewAnalysisArea@1001 : Integer;NewValueType@1002 : Integer);
    BEGIN
      ToItemBudgetName := NewToItemBudgetName;
      AnalysisArea := NewAnalysisArea;
      ValueTypeHidden := NewValueType;
    END;

    PROCEDURE UploadFile@4();
    VAR
      CommonDialogMgt@1000 : Codeunit 412;
    BEGIN
      UploadedFileName := CommonDialogMgt.OpenFile(Text006,'',2,'',0);
      FileName := UploadedFileName;
    END;

    LOCAL PROCEDURE FileNameOnAfterValidate@19040090();
    BEGIN
      UploadFile;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
