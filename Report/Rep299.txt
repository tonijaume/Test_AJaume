OBJECT Report 299 Delete Invoiced Sales Orders
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Delete Invoiced Sales Orders;
               ESP=Eliminar peds. venta factdos.];
    ProcessingOnly=Yes;
  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table36;
        DataItemTableView=SORTING(Document Type,No.)
                          WHERE(Document Type=CONST(Order));
        ReqFilterHeadingML=[ENU=Sales Order;
                            ESP=Pedido venta];
        OnPreDataItem=BEGIN
                        Window.OPEN(Text000);
                      END;

        OnAfterGetRecord=VAR
                           ReserveSalesLine@1000 : Codeunit 99000832;
                         BEGIN
                           Window.UPDATE(1,"No.");

                           AllLinesDeleted := TRUE;
                           DocDim.RESET;
                           DocDim.SETRANGE("Document Type","Document Type");
                           DocDim.SETRANGE("Document No.","No.");
                           ItemChargeAssgntSales.RESET;
                           ItemChargeAssgntSales.SETRANGE("Document Type","Document Type");
                           ItemChargeAssgntSales.SETRANGE("Document No.","No.");
                           SalesOrderLine.RESET;
                           SalesOrderLine.SETRANGE("Document Type","Document Type");
                           SalesOrderLine.SETRANGE("Document No.","No.");
                           SalesOrderLine.SETFILTER("Quantity Invoiced",'<>0');
                           IF SalesOrderLine.FIND('-') THEN BEGIN
                             SalesOrderLine.SETRANGE("Quantity Invoiced");
                             SalesOrderLine.SETFILTER("Outstanding Quantity",'<>0');
                             IF NOT SalesOrderLine.FIND('-') THEN BEGIN
                               SalesOrderLine.SETRANGE("Outstanding Quantity");
                               SalesOrderLine.SETFILTER("Qty. Shipped Not Invoiced",'<>0');
                               IF NOT SalesOrderLine.FIND('-') THEN BEGIN
                                 SalesOrderLine.LOCKTABLE;
                                 IF NOT SalesOrderLine.FIND('-') THEN BEGIN
                                   SalesOrderLine.SETRANGE("Qty. Shipped Not Invoiced");
                                   DocDim.SETRANGE("Table ID",DATABASE::"Sales Line");
                                   IF SalesOrderLine.FIND('-') THEN
                                     REPEAT
                                       SalesOrderLine.CALCFIELDS("Qty. Assigned");
                                       IF (SalesOrderLine."Qty. Assigned" = SalesOrderLine."Quantity Invoiced") OR
                                          (SalesOrderLine.Type <> SalesOrderLine.Type::"Charge (Item)")
                                       THEN BEGIN
                                         DocDim.SETRANGE("Line No.",SalesOrderLine."Line No.");
                                         DocDim.DELETEALL;
                                         IF SalesOrderLine.Type = SalesOrderLine.Type::"Charge (Item)" THEN BEGIN
                                           ItemChargeAssgntSales.SETRANGE("Document Line No.",SalesOrderLine."Line No.");
                                           ItemChargeAssgntSales.DELETEALL;
                                         END;
                                         IF SalesOrderLine.HASLINKS THEN
                                           SalesOrderLine.DELETELINKS;
                                         SalesOrderLine.DELETE;
                                       END ELSE
                                         AllLinesDeleted := FALSE;

                                     UNTIL SalesOrderLine.NEXT = 0;

                                   IF AllLinesDeleted THEN BEGIN
                                     SalesPost.DeleteHeader(
                                       "Sales Header",SalesShptHeader,SalesInvHeader,SalesCrMemoHeader,ReturnRcptHeader,
                                       PrepmtSalesInvHeader,PrepmtSalesCrMemoHeader);

                                     ReserveSalesLine.DeleteInvoiceSpecFromHeader("Sales Header");

                                     SalesCommentLine.SETRANGE("Document Type","Document Type");
                                     SalesCommentLine.SETRANGE("No.","No.");
                                     SalesCommentLine.DELETEALL;

                                     DocDim.SETRANGE("Table ID",DATABASE::"Sales Header");
                                     DocDim.SETRANGE("Line No.",0);
                                     DocDim.DELETEALL;

                                     WhseRequest.SETRANGE("Source Type",DATABASE::"Sales Line");
                                     WhseRequest.SETRANGE("Source Subtype","Document Type");
                                     WhseRequest.SETRANGE("Source No.","No.");
                                     WhseRequest.DELETEALL(TRUE);

                                     IF HASLINKS THEN
                                       DELETELINKS;
                                     DELETE;
                                   END;
                                   COMMIT;
                                 END;
                               END;
                             END;
                           END;
                         END;

        ReqFilterFields=No.,Sell-to Customer No.,Bill-to Customer No.;
      }
      SECTIONS
      {
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Processing sales orders #1##########;ESP=Procesando pedidos venta  #1##########';
      SalesOrderLine@1001 : Record 37;
      SalesShptHeader@1009 : Record 110;
      SalesInvHeader@1010 : Record 112;
      SalesCrMemoHeader@1011 : Record 114;
      ReturnRcptHeader@1012 : Record 6660;
      PrepmtSalesInvHeader@1013 : Record 112;
      PrepmtSalesCrMemoHeader@1014 : Record 114;
      SalesCommentLine@1002 : Record 44;
      DocDim@1003 : Record 357;
      ItemChargeAssgntSales@1007 : Record 5809;
      WhseRequest@1008 : Record 5765;
      SalesPost@1005 : Codeunit 80;
      Window@1004 : Dialog;
      AllLinesDeleted@1006 : Boolean;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
