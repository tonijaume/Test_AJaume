OBJECT Report 6002 Delete Service Document Log
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Delete Service Document Log;
               ESP=Eliminar log documento servicio];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   OnPostReportStatus := FALSE;
                 END;

    OnPostReport=BEGIN
                   OnPostReportStatus := TRUE;
                 END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table5912;
        DataItemTableView=SORTING(Change Date);
        OnPreDataItem=BEGIN
                        CounterTotal := COUNT;
                        IF ProcessDeletedOnly THEN BEGIN
                          IF NOT HideConfirmationDlg THEN
                            IF NOT CONFIRM(Text006) THEN
                              CurrReport.BREAK;
                          Window.OPEN(Text007 + Text008 + Text009);
                          EXIT;
                        END;
                        IF CounterTotal = 0 THEN BEGIN
                          IF NOT HideConfirmationDlg THEN
                            MESSAGE(Text000);
                          CurrReport.BREAK;
                        END;
                        IF NOT HideConfirmationDlg THEN
                          IF NOT CONFIRM(
                            STRSUBSTNO(
                              Text001 +
                              Text002,
                              CounterTotal,TABLECAPTION),
                            FALSE)
                          THEN
                            ERROR(Text003);

                        DELETEALL;
                        LogEntryDeleted := CounterTotal;
                      END;

        OnAfterGetRecord=BEGIN
                           IF NOT ProcessDeletedOnly THEN
                             CurrReport.BREAK;

                           ServHeader.RESET;
                           IF (("Document Type" = "Document Type"::Order) OR
                               ("Document Type" = "Document Type"::Invoice) OR
                               ("Document Type" = "Document Type"::"Credit Memo") OR
                               ("Document Type" = "Document Type"::Quote)) AND NOT ServHeader.GET("Document Type","Document No.") OR
                              ("Document Type" = "Document Type"::Shipment) AND NOT ServShptHeader.GET("Document No.") OR
                              ("Document Type" = "Document Type"::"Posted Invoice") AND NOT ServInvHeader.GET("Document No.") OR
                              ("Document Type" = "Document Type"::"Posted Credit Memo") AND NOT ServCrMemoHeader.GET("Document No.")
                           THEN BEGIN
                             ServOrdLog.RESET;
                             ServOrdLog.SETRANGE("Document Type","Document Type");
                             ServOrdLog.SETRANGE("Document No.","Document No.");
                             LogEntryFiltered := ServOrdLog.COUNT;

                             LogEntryDeleted := LogEntryDeleted + LogEntryFiltered;
                             LogEntryProcessed := LogEntryProcessed + LogEntryFiltered;
                             ServOrdLog.DELETEALL;

                             Window.UPDATE(2,LogEntryDeleted)
                           END ELSE
                             LogEntryProcessed := LogEntryProcessed + 1;

                           Window.UPDATE(1,LogEntryProcessed);
                           Window.UPDATE(3,ROUND(LogEntryProcessed / CounterTotal * 10000,1));
                         END;

        OnPostDataItem=BEGIN
                         IF NOT HideConfirmationDlg THEN
                           IF LogEntryDeleted > 1 THEN
                             MESSAGE(Text004,LogEntryDeleted)
                           ELSE
                             MESSAGE(Text005,LogEntryDeleted);
                       END;

        ReqFilterFields=Change Date,Document Type,Document No.;
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=9020;
      Height=3410;
    }
    CONTROLS
    {
      { 2   ;CheckBox     ;7040 ;0    ;440  ;440  ;ShowCaption=No;
                                                   SourceExpr=ProcessDeletedOnly }
      { 3   ;Label        ;0    ;0    ;6930 ;440  ;ParentControl=2;
                                                   CaptionML=[ENU=Delete Log Entries for Deleted Documents Only;
                                                              ESP=Eliminar movs. log s¢lo en documentos eliminados] }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones] }

      { 2   ;2   ;Field     ;
                  CaptionML=[ENU=Delete Log Entries for Deleted Documents Only;
                             ESP=Eliminar movs. log s¢lo en documentos eliminados];
                  SourceExpr=ProcessDeletedOnly }

    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=There is nothing to delete.;ESP=No hay nada que eliminar.';
      Text001@1001 : TextConst 'ENU=%1 %2 record(s) will be deleted.\\;ESP=%1 %2 registro(s) ser n eliminados.\\';
      Text002@1002 : TextConst 'ENU=Do you want to continue?;ESP=¨Confirma que desea continuar?';
      Text003@1003 : TextConst 'ENU=No records deleted.;ESP=Ning£n registro eliminado.';
      Text004@1004 : TextConst 'ENU=%1 records were deleted.;ESP=Se han eliminado %1 registros.';
      Text005@1005 : TextConst 'ENU=%1 record was deleted.;ESP=Se ha eliminado registro %1.';
      ServHeader@1017 : Record 5900;
      ServShptHeader@1018 : Record 5990;
      ServInvHeader@1019 : Record 5992;
      ServCrMemoHeader@1021 : Record 5994;
      ServOrdLog@1016 : Record 5912;
      Window@1009 : Dialog;
      LogEntryProcessed@1015 : Integer;
      LogEntryDeleted@1010 : Integer;
      LogEntryFiltered@1020 : Integer;
      CounterTotal@1006 : Integer;
      ProcessDeletedOnly@1007 : Boolean;
      HideConfirmationDlg@1008 : Boolean;
      Text006@1014 : TextConst 'ENU=Do you want to delete the service order log entries for deleted service orders?;ESP=¨Desea eliminar los movs. log pedido servicio para pedidos serv.?';
      Text007@1013 : TextConst 'ENU=Log entries processed: #1######\\;ESP=Movs. log procesados:  #1######\\';
      Text008@1012 : TextConst 'ENU=Log entries deleted:   #2######\\;ESP=Movs. log eliminados:  #2######\\';
      Text009@1011 : TextConst 'ENU=@3@@@@@@@@@@@@@@@@@@@@@@@@@@@@@;ESP=@3@@@@@@@@@@@@@@@@@@@@@@@@@@@@@';
      OnPostReportStatus@1022 : Boolean;

    PROCEDURE SetHideConfirmationDlg@17(HideDlg@1000 : Boolean);
    BEGIN
      HideConfirmationDlg := HideDlg;
    END;

    PROCEDURE SetProcessDeletedOnly@1(DeletedOnly@1000 : Boolean);
    BEGIN
      ProcessDeletedOnly := DeletedOnly;
    END;

    PROCEDURE GetServDocLog@2(VAR ServDocLog@1000 : Record 5912);
    BEGIN
      ServDocLog.COPY("Service Document Log");
    END;

    PROCEDURE GetOnPostReportStatus@3() : Boolean;
    BEGIN
      EXIT(OnPostReportStatus AND NOT ProcessDeletedOnly);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
