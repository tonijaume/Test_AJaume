OBJECT Report 6665 Batch Post Purch. Ret. Orders
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Batch Post Purch. Ret. Orders;
               ESP=Reg. lotes pedidos devoluci¢n];
    ProcessingOnly=Yes;
  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table38;
        DataItemTableView=SORTING(Document Type,No.)
                          WHERE(Document Type=CONST(Return Order));
        ReqFilterHeadingML=[ENU=Purchase Return Order;
                            ESP=Devoluci¢n compra];
        OnPreDataItem=BEGIN
                        IF ReplacePostingDate AND (PostingDateReq = 0D) THEN
                          ERROR(Text000);

                        ApprovalTemplate.SETCURRENTKEY(ApprovalTemplate."Table ID",ApprovalTemplate."Document Type",ApprovalTemplate.Enabled);
                        ApprovalTemplate.SETRANGE("Table ID",DATABASE::"Purchase Header");
                        ApprovalTemplate.SETRANGE("Document Type","Purchase Header"."Document Type"::"Return Order");
                        ApprovalTemplate.SETRANGE(Enabled,TRUE);
                        IF ApprovalTemplate.FINDFIRST THEN
                          "Purchase Header".SETRANGE(Status,"Purchase Header".Status::Released);

                        CounterTotal := COUNT;
                        Window.OPEN(Text001);
                      END;

        OnAfterGetRecord=BEGIN
                           IF CalcInvDisc THEN
                             CalculateInvoiceDiscount;

                           Counter := Counter + 1;
                           Window.UPDATE(1,"No.");
                           Window.UPDATE(2,ROUND(Counter / CounterTotal * 10000,1));
                           "Purchase Header".Ship := ShipReq;
                           "Purchase Header".Invoice := InvReq;
                           CLEAR(PurchPost);
                           PurchPost.SetPostingDate(ReplacePostingDate,ReplaceDocumentDate,PostingDateReq);
                           IF PurchPost.RUN("Purchase Header") THEN BEGIN
                             CounterOK := CounterOK + 1;
                             IF MARKEDONLY THEN
                               MARK(FALSE);
                           END;
                         END;

        OnPostDataItem=BEGIN
                         Window.CLOSE;
                         MESSAGE(Text002,CounterOK,CounterTotal);
                       END;

        ReqFilterFields=No.,Status;
      }
      SECTIONS
      {
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=5060;
      Height=3300;
      SaveValues=Yes;
      OnOpenForm=BEGIN
                   PurchSetup.GET;
                   CalcInvDisc := PurchSetup."Calc. Inv. Discount";
                 END;

    }
    CONTROLS
    {
      { 1   ;TextBox      ;3410 ;1210 ;1650 ;440  ;CaptionML=[ENU=Posting Date;
                                                              ESP=Fecha registro];
                                                   SourceExpr=PostingDateReq }
      { 21  ;Label        ;0    ;1210 ;3300 ;440  ;ParentControl=1 }
      { 2   ;CheckBox     ;3410 ;1760 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Replace Posting Date;
                                                              ESP=Reemplazar fecha reg.];
                                                   SourceExpr=ReplacePostingDate;
                                                   OnValidate=BEGIN
                                                                IF ReplacePostingDate THEN
                                                                  MESSAGE(Text003);
                                                              END;
                                                               }
      { 22  ;Label        ;0    ;1760 ;3300 ;440  ;ParentControl=2 }
      { 3   ;CheckBox     ;3410 ;2310 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Replace Document Date;
                                                              ESP=Reemplazar fecha documento];
                                                   SourceExpr=ReplaceDocumentDate }
      { 4   ;Label        ;0    ;2310 ;3300 ;440  ;ParentControl=3 }
      { 5   ;CheckBox     ;3410 ;2860 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Calc. Inv. Discount;
                                                              ESP=Calc. dto. factura];
                                                   SourceExpr=CalcInvDisc;
                                                   OnValidate=BEGIN
                                                                PurchSetup.GET;
                                                                PurchSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
                                                              END;
                                                               }
      { 6   ;Label        ;0    ;2860 ;3300 ;440  ;ParentControl=5 }
      { 7   ;CheckBox     ;3410 ;550  ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Invoice;
                                                              ESP=Factura];
                                                   SourceExpr=InvReq }
      { 8   ;Label        ;0    ;550  ;3300 ;440  ;ParentControl=7 }
      { 9   ;CheckBox     ;3410 ;0    ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Ship;
                                                              ESP=Env¡o];
                                                   SourceExpr=ShipReq }
      { 10  ;Label        ;0    ;0    ;3300 ;440  ;ParentControl=9 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   PurchSetup.GET;
                   CalcInvDisc := PurchSetup."Calc. Inv. Discount";
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones] }

      { 9   ;2   ;Field     ;
                  CaptionML=[ENU=Ship;
                             ESP=Env¡o];
                  SourceExpr=ShipReq }

      { 7   ;2   ;Field     ;
                  CaptionML=[ENU=Invoice;
                             ESP=Factura];
                  SourceExpr=InvReq }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Posting Date;
                             ESP=Fecha registro];
                  SourceExpr=PostingDateReq }

      { 2   ;2   ;Field     ;
                  CaptionML=[ENU=Replace Posting Date;
                             ESP=Reemplazar fecha reg.];
                  SourceExpr=ReplacePostingDate;
                  OnValidate=BEGIN
                               IF ReplacePostingDate THEN
                                 MESSAGE(Text003);
                             END;
                              }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Replace Document Date;
                             ESP=Reemplazar fecha documento];
                  SourceExpr=ReplaceDocumentDate }

      { 5   ;2   ;Field     ;
                  CaptionML=[ENU=Calc. Inv. Discount;
                             ESP=Calc. dto. factura];
                  SourceExpr=CalcInvDisc;
                  OnValidate=BEGIN
                               PurchSetup.GET;
                               PurchSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
                             END;
                              }

    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Please enter the posting date.;ESP=Introduzca la fecha registro.';
      Text001@1001 : TextConst 'ENU=Posting return orders  #1########## @2@@@@@@@@@@@@@;ESP=Registrando devoluciones  #1########## @2@@@@@@@@@@@@@';
      Text002@1002 : TextConst 'ENU=%1 return orders out of a total of %2 have now been posted.;ESP=%1 devoluciones de un total de %2 han sido registrados.';
      PurchLine@1003 : Record 39;
      PurchSetup@1004 : Record 312;
      ApprovalTemplate@1018 : Record 464;
      PurchCalcDisc@1005 : Codeunit 70;
      PurchPost@1006 : Codeunit 90;
      Window@1007 : Dialog;
      PostingDateReq@1008 : Date;
      CounterTotal@1009 : Integer;
      Counter@1010 : Integer;
      CounterOK@1011 : Integer;
      ShipReq@1012 : Boolean;
      InvReq@1013 : Boolean;
      ReplacePostingDate@1014 : Boolean;
      ReplaceDocumentDate@1015 : Boolean;
      CalcInvDisc@1016 : Boolean;
      Text003@1017 : TextConst 'ENU=The exchange rate associated with the new posting date on the purchase header will not apply to the purchase lines.;ESP=El tipo de cambio asociado con la nueva fecha de registro indicada en el encabezado de compra no se aplicar  a las l¡neas de compra.';

    PROCEDURE CalculateInvoiceDiscount@1();
    BEGIN
      PurchLine.RESET;
      PurchLine.SETRANGE("Document Type","Purchase Header"."Document Type");
      PurchLine.SETRANGE("Document No.","Purchase Header"."No.");
      IF PurchLine.FIND('-') THEN
        IF PurchCalcDisc.RUN(PurchLine) THEN BEGIN
          "Purchase Header".GET("Purchase Header"."Document Type","Purchase Header"."No.");
          COMMIT;
        END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
