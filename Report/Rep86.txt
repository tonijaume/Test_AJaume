OBJECT Report 86 Adjust Add. Reporting Currency
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    Permissions=TableData 17=im,
                TableData 32=m,
                TableData 45=im,
                TableData 169=m,
                TableData 254=m,
                TableData 355=imd,
                TableData 5802=m;
    CaptionML=[ENU=Adjust Add. Reporting Currency;
               ESP=Ajust. divisa adicional];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   IF GLSetup."Additional Reporting Currency" = '' THEN
                     GLSetup.GET;
                 END;

    OnPreReport=VAR
                  GenJnlLine@1000 : Record 81;
                BEGIN
                  Currency.GET(GLSetup."Additional Reporting Currency");
                  Currency.TESTFIELD("Amount Rounding Precision");
                  Currency.TESTFIELD("Unit-Amount Rounding Precision");
                  Currency.TESTFIELD("Residual Gains Account");
                  ResidualGLAcc.GET(Currency."Residual Gains Account");
                  ResidualGLAcc.TESTFIELD(Blocked,FALSE);
                  ResidualGLAcc.TESTFIELD("Account Type",ResidualGLAcc."Account Type"::Posting);
                  Currency.TESTFIELD("Residual Losses Account");
                  ResidualGLAcc.GET(Currency."Residual Losses Account");
                  ResidualGLAcc.TESTFIELD(Blocked,FALSE);
                  ResidualGLAcc.TESTFIELD("Account Type",ResidualGLAcc."Account Type"::Posting);
                  SourceCodeSetup.GET;
                  SourceCodeSetup.TESTFIELD("Adjust Add. Reporting Currency");

                  IF DocumentNo = '' THEN
                    ERROR(
                      Text000,GenJnlLine.FIELDCAPTION("Document No."));
                  IF RetainedEarningsGLAcc."No." = '' THEN
                    ERROR(Text001);
                END;

    OnPostReport=BEGIN
                   ReportIsExecuted := TRUE;
                 END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table254;
        DataItemTableView=SORTING(Type,Closed)
                          WHERE(Type=FILTER(Purchase..Sale));
        OnPreDataItem=VAR
                        GLSetup@1000 : Record 98;
                      BEGIN
                        Window.OPEN(
                          Text002);
                        IF "VAT Entry".COUNT > 0 THEN
                          VATEntryStep := 10000 * 100000 DIV "VAT Entry".COUNT;

                        GLSetup.GET;
                        IF NOT GLSetup."Unrealized VAT" THEN
                          SETRANGE(Closed,FALSE);
                      END;

        OnAfterGetRecord=BEGIN
                           VATEntryCount := VATEntryCount + VATEntryStep;
                           IF VATEntryOldCount DIV 100000 <> VATEntryCount DIV 100000 THEN BEGIN
                             Window.UPDATE(1,VATEntryCount DIV 100000);
                             VATEntryOldCount := VATEntryCount;
                           END;

                           "Additional-Currency Amount" := ExchangeAmtLCYToFCY("Posting Date",Amount,FALSE);
                           "Additional-Currency Base" := ExchangeAmtLCYToFCY("Posting Date",Base,FALSE);
                           "Add.-Currency Unrealized Amt." := ExchangeAmtLCYToFCY("Posting Date","Unrealized Amount",FALSE);
                           "Add.-Currency Unrealized Base" := ExchangeAmtLCYToFCY("Posting Date","Unrealized Base",FALSE);
                           "Add.-Curr. Rem. Unreal. Amount" := ExchangeAmtLCYToFCY("Posting Date","Remaining Unrealized Amount",FALSE);
                           "Add.-Curr. Rem. Unreal. Base" := ExchangeAmtLCYToFCY("Posting Date","Remaining Unrealized Base",FALSE);
                           MODIFY;
                         END;

        OnPostDataItem=BEGIN
                         Window.CLOSE;
                       END;

      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemTable=Table17;
        DataItemTableView=SORTING(Entry No.);
        OnPreDataItem=BEGIN
                        Window.OPEN(
                          Text003 +
                          Text004);
                      END;

        OnAfterGetRecord=VAR
                           GLAccNo@1000 : Code[20];
                         BEGIN
                           IF OldGLEntry."Posting Date" < "Posting Date" THEN BEGIN
                             Window.UPDATE(1,"Posting Date");
                             OldGLEntry := "G/L Entry";
                           END;
                           "Additional-Currency Amount" := ExchangeAmtLCYToFCY("Posting Date",Amount,FALSE);
                           IF "Debit Amount" <> 0 THEN BEGIN
                             "Add.-Currency Debit Amount" := "Additional-Currency Amount";
                             "Add.-Currency Credit Amount" := 0;
                           END ELSE BEGIN
                             "Add.-Currency Debit Amount" := 0;
                             "Add.-Currency Credit Amount" := -"Additional-Currency Amount";
                           END;
                           MODIFY;

                           TotalAddCurrAmount := TotalAddCurrAmount + "Additional-Currency Amount";
                           TotalAmount := TotalAmount + Amount;
                           IF TotalAmount = 0 THEN BEGIN
                             IF TotalAddCurrAmount <> 0 THEN BEGIN
                               IF TotalAddCurrAmount < 0 THEN
                                 GLAccNo := Currency."Residual Losses Account"
                               ELSE
                                 GLAccNo := Currency."Residual Gains Account";
                               InsertGLEntry(
                                 "Posting Date","Document Date","Document Type","Document No.",GLAccNo,
                                 "Journal Batch Name","Reason Code",-TotalAddCurrAmount);
                               TotalAddCurrAmount := 0;
                             END;
                           END;

                           IF "Entry No." = LastEntryNo THEN
                             CurrReport.BREAK;

                           IF "Posting Date" = CLOSINGDATE("Posting Date") THEN BEGIN
                             TmpCloseIncomeStatementBuffer."Closing Date" := "Posting Date";
                             TmpCloseIncomeStatementBuffer."G/L Account No." := "G/L Account No.";
                             IF TmpCloseIncomeStatementBuffer.INSERT THEN;
                           END;
                         END;

        OnPostDataItem=BEGIN
                         IF TmpCloseIncomeStatementBuffer.FIND('-') THEN
                           REPEAT
                             IF IsAccountingPeriodClosingDate(
                                  TmpCloseIncomeStatementBuffer."Closing Date")
                             THEN BEGIN
                               CheckCombination(TmpCloseIncomeStatementBuffer);

                               CLEAR(TmpCloseIncomeStatementBuffer3);
                               TmpCloseIncomeStatementBuffer3."Closing Date" := "Posting Date";
                               TmpCloseIncomeStatementBuffer3."G/L Account No." := Currency."Residual Gains Account";
                               IF TmpCloseIncomeStatementBuffer3.INSERT THEN;
                               TmpCloseIncomeStatementBuffer3."Closing Date" := "Posting Date";
                               TmpCloseIncomeStatementBuffer3."G/L Account No." := Currency."Residual Losses Account";
                               IF TmpCloseIncomeStatementBuffer3.INSERT THEN;
                             END;
                           UNTIL TmpCloseIncomeStatementBuffer.NEXT = 0;

                           IF TmpCloseIncomeStatementBuffer3.FIND('-') THEN
                             REPEAT
                               CheckCombination(TmpCloseIncomeStatementBuffer3);
                             UNTIL TmpCloseIncomeStatementBuffer3.NEXT = 0;

                         IF GLReg."To Entry No." <> 0 THEN
                           GLReg.INSERT;
                         Window.CLOSE;
                       END;

      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemTable=Table5802;
        DataItemTableView=SORTING(Item No.);
        OnPreDataItem=BEGIN
                        Window.OPEN(
                          Text011 +
                          Text006);
                      END;

        OnAfterGetRecord=BEGIN
                           IF OldValueEntry."Item No." <> "Item No." THEN BEGIN
                             Window.UPDATE(1,"Item No.");
                             OldValueEntry := "Value Entry";
                           END;

                           "Cost per Unit (ACY)" := ExchangeAmtLCYToFCY("Posting Date","Cost per Unit",TRUE);
                           "Cost Amount (Actual) (ACY)" := ExchangeAmtLCYToFCY("Posting Date","Cost Amount (Actual)",FALSE);
                           "Cost Amount (Expected) (ACY)" := ExchangeAmtLCYToFCY("Posting Date","Cost Amount (Expected)",FALSE);
                           "Cost Amount (Non-Invtbl.)(ACY)" := ExchangeAmtLCYToFCY("Posting Date","Cost Amount (Non-Invtbl.)",FALSE);
                           "Cost Posted to G/L (ACY)" := ExchangeAmtLCYToFCY("Posting Date","Cost Posted to G/L",FALSE);

                           MODIFY;
                         END;

        OnPostDataItem=BEGIN
                         Window.CLOSE;
                       END;

      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemTable=Table169;
        DataItemTableView=SORTING(Job No.,Posting Date);
        OnPreDataItem=BEGIN
                        Window.OPEN(
                          Text007 +
                          Text008);
                      END;

        OnAfterGetRecord=BEGIN
                           IF OldJobLedgEntry."Job No." <> "Job No." THEN BEGIN
                             Window.UPDATE(1,"Job No.");
                             OldJobLedgEntry := "Job Ledger Entry";
                           END;

                           "Additional-Currency Total Cost" := ExchangeAmtLCYToFCY("Posting Date","Total Cost (LCY)",FALSE);
                           "Add.-Currency Total Price" := ExchangeAmtLCYToFCY("Posting Date","Total Price (LCY)",FALSE);
                           MODIFY;
                         END;

      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemTable=Table5406;
        DataItemTableView=SORTING(Status,Prod. Order No.,Line No.);
        OnPreDataItem=BEGIN
                        Window.OPEN(
                          Text99000004 +
                          Text99000002);
                      END;

        OnAfterGetRecord=BEGIN
                           IF OldProdOrderLine."Prod. Order No." <> "Prod. Order No." THEN BEGIN
                             Window.UPDATE(1,"Prod. Order No.");
                             OldProdOrderLine := "Prod. Order Line";
                           END;

                           "Cost Amount (ACY)" := ExchangeAmtLCYToFCY(WORKDATE,"Cost Amount",FALSE);
                           "Unit Cost (ACY)" := ExchangeAmtLCYToFCY(WORKDATE,"Unit Cost",TRUE);
                           "Currency Factor" := CurrExchRate.ExchangeRate(WORKDATE,GLSetup."Additional Reporting Currency");
                           MODIFY;
                         END;

      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=6160;
      Height=1760;
      OnOpenForm=BEGIN
                   GLSetup.TESTFIELD("Additional Reporting Currency");
                   GLSetup2 := GLSetup;
                   CurrencyFactor := CurrExchRate.ExchangeRate(WORKDATE,GLSetup."Additional Reporting Currency");
                 END;

    }
    CONTROLS
    {
      { 7   ;TextBox      ;3410 ;0    ;1650 ;440  ;Editable=Yes;
                                                   CaptionML=[ENU=Additional Reporting Currency;
                                                              ESP=Informaci¢n divisa adicional];
                                                   SourceExpr=GLSetup."Additional Reporting Currency";
                                                   OnAssistEdit=BEGIN
                                                                  ChangeExchangeRate.SetParameter(GLSetup."Additional Reporting Currency",CurrencyFactor,WORKDATE);
                                                                  IF ChangeExchangeRate.RUNMODAL = ACTION::OK THEN BEGIN
                                                                    CurrencyFactor := ChangeExchangeRate.GetParameter;
                                                                  END;
                                                                  CLEAR(ChangeExchangeRate);
                                                                END;

                                                   OnAfterValidate=BEGIN
                                                                     GLSetup := GLSetup2;
                                                                   END;
                                                                    }
      { 8   ;Label        ;0    ;0    ;3300 ;440  ;ParentControl=7 }
      { 1   ;TextBox      ;3410 ;660  ;2750 ;440  ;CaptionML=[ENU=Document No.;
                                                              ESP=N§ documento];
                                                   SourceExpr=DocumentNo }
      { 2   ;Label        ;0    ;660  ;3300 ;440  ;ParentControl=1 }
      { 4   ;TextBox      ;3410 ;1320 ;2750 ;440  ;CaptionML=[ENU=Retained Earnings Acc.;
                                                              ESP=Cta. ajtes. red. div. adic];
                                                   SourceExpr=RetainedEarningsGLAcc."No.";
                                                   TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                IF RetainedEarningsGLAcc."No." <> '' THEN BEGIN
                                                                  RetainedEarningsGLAcc.FIND;
                                                                  RetainedEarningsGLAcc.CheckGLAcc;
                                                                END;
                                                              END;
                                                               }
      { 5   ;Label        ;0    ;1320 ;3300 ;440  ;ParentControl=4 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   GLSetup.TESTFIELD("Additional Reporting Currency");
                   GLSetup2 := GLSetup;
                   CurrencyFactor := CurrExchRate.ExchangeRate(WORKDATE,GLSetup."Additional Reporting Currency");
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones] }

      { 7   ;2   ;Field     ;
                  CaptionML=[ENU=Additional Reporting Currency;
                             ESP=Informaci¢n divisa adicional];
                  SourceExpr=GLSetup."Additional Reporting Currency";
                  Editable=TRUE;
                  OnValidate=BEGIN
                               GLSetupAdditionalReportingCurr;
                             END;

                  OnAssistEdit=BEGIN
                                 ChangeExchangeRate.SetParameter(GLSetup."Additional Reporting Currency",CurrencyFactor,WORKDATE);
                                 IF ChangeExchangeRate.RUNMODAL = ACTION::OK THEN BEGIN
                                   CurrencyFactor := ChangeExchangeRate.GetParameter;
                                 END;
                                 CLEAR(ChangeExchangeRate);
                               END;
                                }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Document No.;
                             ESP=N§ documento];
                  SourceExpr=DocumentNo }

      { 4   ;2   ;Field     ;
                  CaptionML=[ENU=Retained Earnings Acc.;
                             ESP=Cta. ajtes. red. div. adic];
                  SourceExpr=RetainedEarningsGLAcc."No.";
                  TableRelation="G/L Account";
                  OnValidate=BEGIN
                               IF RetainedEarningsGLAcc."No." <> '' THEN BEGIN
                                 RetainedEarningsGLAcc.FIND;
                                 RetainedEarningsGLAcc.CheckGLAcc;
                               END;
                             END;
                              }

    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Please enter a %1.;ESP=Por favor, teclee %1.';
      Text001@1001 : TextConst 'ENU=Please enter Retained Earnings Account No.;ESP=Inserte, cta. retenci¢n bf§';
      Text002@1002 : TextConst 'ENU=Processing VAT Entries @1@@@@@@@@@@\;ESP=Procesando movs. IVA   @1@@@@@@@@@@\';
      Text003@1003 : TextConst 'ENU=Processing G/L Entries...\\;ESP=Procesando movs. contabilidad...\\';
      Text004@1004 : TextConst 'ENU=Posting Date #1##########\;ESP=Fecha reg.   #1##########\';
      Text006@1005 : TextConst 'ENU=Item No. #1##########\;ESP=N§ prod. #1##########\';
      Text007@1006 : TextConst 'ENU=Processing Job Ledger Entries...\\;ESP=Procesando movs. proyecto...\\';
      Text008@1007 : TextConst 'ENU=Job No. #1##########\;ESP=N§ proy #1##########\';
      Text010@1008 : TextConst 'ENU=Residual caused by rounding of %1;ESP=Ajuste residual generado por el redondeo de %1';
      Text011@1009 : TextConst 'ENU=Processing Value Entries...\\;ESP=Procesar movs. valor...\\';
      Text99000002@1011 : TextConst 'ENU=Prod.Order No. #1##########\;ESP=N§ orden prod. #1##########\';
      Text99000004@1013 : TextConst 'ENU=Processing Finished Prod. Order Lines...\\;ESP=Procesando l¡ns. ord. prod. terminada...\\';
      GLSetup@1014 : Record 98;
      GLSetup2@1015 : Record 98;
      SourceCodeSetup@1016 : Record 242;
      OldGLEntry@1017 : Record 17;
      OldValueEntry@1018 : Record 5802;
      OldJobLedgEntry@1019 : Record 169;
      OldProdOrderLine@1022 : Record 5406;
      CurrExchRate@1023 : Record 330;
      Currency@1024 : Record 4;
      GLEntry2@1025 : Record 17;
      GLReg@1026 : Record 45;
      AccountingPeriod@1027 : Record 50;
      TmpCloseIncomeStatementBuffer@1028 : TEMPORARY Record 347;
      TmpCloseIncomeStatementBuffer3@1029 : TEMPORARY Record 347;
      GLEntry3@1030 : Record 17;
      RetainedEarningsGLAcc@1031 : Record 15;
      ResidualGLAcc@1020 : Record 15;
      ChangeExchangeRate@1032 : Form 511;
      CurrencyFactor@1033 : Decimal;
      Window@1034 : Dialog;
      TotalAddCurrAmount@1035 : Decimal;
      TotalAmount@1036 : Decimal;
      NextEntryNo@1037 : Integer;
      LastEntryNo@1038 : Integer;
      NextTransactionNo@1039 : Integer;
      FiscalYearStartDate@1040 : Date;
      ReportIsExecuted@1041 : Boolean;
      VATEntryCount@1042 : Integer;
      VATEntryOldCount@1043 : Integer;
      VATEntryStep@1044 : Integer;
      FiscalYearStartDate2@1045 : Date;
      FiscalYearEndDate2@1046 : Date;
      LastDateChecked@1047 : Date;
      LastFiscalYearStartDate@1048 : Date;
      LastFiscalYearEndDate@1049 : Date;
      LastIsAccPeriodClosingDate@1050 : Boolean;
      DocumentNo@1051 : Code[20];

    PROCEDURE SetAddCurr@2(AddCurr@1000 : Code[10]);
    BEGIN
      GLSetup."Additional Reporting Currency" := AddCurr;
    END;

    PROCEDURE IsExecuted@3() : Boolean;
    BEGIN
      EXIT(ReportIsExecuted);
    END;

    LOCAL PROCEDURE ExchangeAmtLCYToFCY@1(PostingDate@1003 : Date;Amount@1000 : Decimal;IsUnitAmount@1001 : Boolean) : Decimal;
    VAR
      AmtRndgPrec@1002 : Decimal;
    BEGIN
      IF IsUnitAmount THEN
        AmtRndgPrec := Currency."Unit-Amount Rounding Precision"
      ELSE
        AmtRndgPrec := Currency."Amount Rounding Precision";
      EXIT(
        ROUND(
          CurrExchRate.ExchangeAmtLCYToFCY(
            PostingDate,GLSetup."Additional Reporting Currency",Amount,
            CurrExchRate.ExchangeRate(PostingDate,GLSetup."Additional Reporting Currency")),
           AmtRndgPrec));
    END;

    PROCEDURE IsAccountingPeriodClosingDate@5(Date@1000 : Date) : Boolean;
    VAR
      AccountingPeriod@1001 : Record 50;
      OK@1002 : Boolean;
    BEGIN
      IF Date <> LastDateChecked THEN BEGIN
        OK := AccountingPeriod.GET(NORMALDATE(Date) + 1);
        IF OK THEN
          OK := AccountingPeriod."New Fiscal Year";
        IF OK THEN BEGIN
          LastFiscalYearEndDate := CLOSINGDATE(Date);
          AccountingPeriod.SETRANGE("New Fiscal Year",TRUE);
          OK := AccountingPeriod.FIND('<');
          LastFiscalYearStartDate := AccountingPeriod."Starting Date";
          LastIsAccPeriodClosingDate := TRUE;
        END ELSE BEGIN
          LastIsAccPeriodClosingDate := FALSE;
          LastFiscalYearStartDate := 0D;
          LastFiscalYearEndDate := 0D;
        END;
      END;
      FiscalYearStartDate2 := LastFiscalYearStartDate;
      FiscalYearEndDate2 := LastFiscalYearEndDate;
      EXIT(LastIsAccPeriodClosingDate);
    END;

    PROCEDURE CheckCombination@7(CloseIncomeStmtBuffer2@1000 : Record 347);
    BEGIN
      CLEAR(GLEntry3);
      GLEntry3.SETCURRENTKEY("G/L Account No.","Posting Date");
      GLEntry3.SETRANGE("G/L Account No.",CloseIncomeStmtBuffer2."G/L Account No.");
      GLEntry3.SETRANGE("Posting Date",FiscalYearStartDate2,FiscalYearEndDate2);

      GLEntry3.CALCSUMS(Amount);
      IF GLEntry3.Amount = 0 THEN BEGIN
        GLEntry3.CALCSUMS("Additional-Currency Amount");
        IF GLEntry3."Additional-Currency Amount" <> 0 THEN BEGIN
          InsertGLEntry(
            FiscalYearEndDate2,FiscalYearEndDate2,GLEntry3."Document Type"::" ",DocumentNo,
            CloseIncomeStmtBuffer2."G/L Account No.",
            '','',-GLEntry3."Additional-Currency Amount");

          InsertGLEntry(
            FiscalYearEndDate2,FiscalYearEndDate2,GLEntry3."Document Type"::" ",DocumentNo,
            RetainedEarningsGLAcc."No.",
            '','',GLEntry3."Additional-Currency Amount");
        END;
      END;
    END;

    PROCEDURE InsertGLEntry@10(PostingDate@1000 : Date;DocumentDate@1001 : Date;DocumentType@1002 : Integer;DocumentNo@1003 : Code[20];GLAccountNo@1004 : Code[20];JnlBatchName@1005 : Code[10];ReasonCode@1006 : Code[10];AddCurrAmount@1007 : Decimal);
    BEGIN
      IF NextEntryNo = 0 THEN BEGIN
        IF GLEntry2.FIND('+') THEN BEGIN
          LastEntryNo := GLEntry2."Entry No.";
          NextEntryNo := GLEntry2."Entry No." + 1;
          NextTransactionNo := GLEntry2."Transaction No." + 1;
        END;

        AccountingPeriod.RESET;
        AccountingPeriod.SETCURRENTKEY(Closed);
        AccountingPeriod.SETRANGE(Closed,FALSE);
        AccountingPeriod.FIND('-');
        FiscalYearStartDate := AccountingPeriod."Starting Date";

        GLReg.LOCKTABLE;
        IF GLReg.FIND('+') THEN
          GLReg."No." := GLReg."No." + 1
        ELSE
          GLReg."No." := 1;
        GLReg.INIT;
        GLReg."From Entry No." := NextEntryNo;
        GLReg."From VAT Entry No." := 0;
        GLReg."To VAT Entry No." := 0;
        GLReg."Creation Date" := TODAY;
        GLReg."Source Code" := SourceCodeSetup."Adjust Add. Reporting Currency";
        GLReg."Journal Batch Name" := '';
        GLReg."User ID" := USERID;
      END ELSE
        NextEntryNo := NextEntryNo + 1;

      GLEntry2.INIT;
      GLEntry2."Posting Date" := PostingDate;
      GLEntry2."Document Date" := DocumentDate;
      GLEntry2."Document Type" := DocumentType;
      GLEntry2."Document No." := DocumentNo;
      GLEntry2."External Document No." := '';
      GLEntry2.Description :=
        COPYSTR(
          STRSUBSTNO(
            Text010,
            GLEntry2.FIELDCAPTION("Additional-Currency Amount")),
          1,MAXSTRLEN(GLEntry2.Description));
      GLEntry2."Source Code" := SourceCodeSetup."Adjust Add. Reporting Currency";
      GLEntry2."Source Type" := 0;
      GLEntry2."Source No." := '';
      GLEntry2."Job No." := '';
      GLEntry2.Quantity := 0;
      GLEntry2."Journal Batch Name" := JnlBatchName;
      GLEntry2."Reason Code" := ReasonCode;
      GLEntry2."Entry No." := NextEntryNo;
      GLEntry2."Transaction No." := NextTransactionNo;
      GLEntry2."G/L Account No." := GLAccountNo;
      GLEntry2.Amount := 0;
      GLEntry2."User ID" := USERID;
      GLEntry2."No. Series" := '';
      GLEntry2."System-Created Entry" := TRUE;
      GLEntry2."Prior-Year Entry" := GLEntry2."Posting Date" < FiscalYearStartDate;
      GLEntry2."Additional-Currency Amount" := AddCurrAmount;
      IF GLEntry2."Additional-Currency Amount" > 0 THEN BEGIN
        GLEntry2."Add.-Currency Debit Amount" := GLEntry2."Additional-Currency Amount";
        GLEntry2."Add.-Currency Credit Amount" := 0;
      END ELSE BEGIN
        GLEntry2."Add.-Currency Debit Amount" := 0;
        GLEntry2."Add.-Currency Credit Amount" := -GLEntry2."Additional-Currency Amount";
      END;
      GLEntry2.INSERT;

      GLReg."To Entry No." := GLEntry2."Entry No.";
    END;

    PROCEDURE InitializeRequest@4(NewDocumentNo@1000 : Code[20];NewRetainedEarningsGLAccNo@1001 : Code[20]);
    BEGIN
      DocumentNo := NewDocumentNo;
      RetainedEarningsGLAcc."No." := NewRetainedEarningsGLAccNo;
      GLSetup2 := GLSetup;
      CurrencyFactor := CurrExchRate.ExchangeRate(WORKDATE,GLSetup."Additional Reporting Currency");
    END;

    LOCAL PROCEDURE GLSetupAdditionalReportingCurr@19052006();
    BEGIN
      GLSetup := GLSetup2;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
