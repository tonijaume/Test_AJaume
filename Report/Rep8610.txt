OBJECT Report 8610 Create G/L Acc. Journal Lines
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create G/L Acc. Journal Lines;
               ESP=Crear l¡neas diario cuentas];
    ProcessingOnly=Yes;
    OnPostReport=BEGIN
                   MESSAGE(Text004);
                 END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table15;
        DataItemTableView=SORTING(No.);
        OnPreDataItem=BEGIN
                        IF JournalTemplate = '' THEN
                          ERROR(Text001);

                        IF BatchName = '' THEN
                          ERROR(Text002);

                        IF TemplateCode = '' THEN
                          ERROR(Text003);

                        GenJnlLine.SETRANGE("Journal Template Name",JournalTemplate);
                        GenJnlLine.SETRANGE("Journal Batch Name",BatchName);
                        IF GenJnlLine.FIND('+') THEN
                          LineNo := GenJnlLine."Line No." + 10000
                        ELSE
                          LineNo := 10000;

                        GenJnlBatch.GET(JournalTemplate,BatchName);
                        StdGenJournal.GET(JournalTemplate,TemplateCode);
                      END;

        OnAfterGetRecord=VAR
                           StdGenJournalLine@1000 : Record 751;
                         BEGIN
                           GenJnlLine.INIT;
                           IF GetStandardJournalLine() THEN BEGIN
                             Initialize(StdGenJournal,GenJnlBatch.Name);

                             StdGenJournalLine.SETRANGE("Journal Template Name",StdGenJournal."Journal Template Name");
                             StdGenJournalLine.SETRANGE("Standard Journal Code",StdGenJournal.Code);
                             IF StdGenJournalLine.FIND('-') THEN
                               REPEAT
                                 CopyGenJnlFromStdJnl(StdGenJournalLine,GenJnlLine);
                                 GenJnlLine.VALIDATE("Document Type",DocumentTypes);
                                 GenJnlLine.VALIDATE("Account Type",GenJnlLine."Account Type"::"G/L Account");
                                 GenJnlLine.VALIDATE("Account No.","G/L Account"."No.");
                                 GenJnlLine.VALIDATE("Bal. Account No.",GenJnlBatch."Bal. Account No.");
                                 IF PostingDate <> 0D THEN
                                   GenJnlLine.VALIDATE("Posting Date",PostingDate);
                                 GenJnlLine.MODIFY(TRUE);
                               UNTIL StdGenJournalLine.NEXT = 0;
                           END ELSE BEGIN
                             GenJnlLine.VALIDATE("Journal Template Name",JournalTemplate);
                             GenJnlLine.VALIDATE("Journal Batch Name",BatchName);
                             GenJnlLine."Line No." := LineNo;
                             LineNo := LineNo + 10000;

                             GenJnlLine.VALIDATE("Document Type",DocumentTypes);
                             GenJnlLine.VALIDATE("Account Type",GenJnlLine."Account Type"::"G/L Account");
                             GenJnlLine.VALIDATE("Account No.","G/L Account"."No.");
                             GenJnlLine.VALIDATE("Bal. Account No.",GenJnlBatch."Bal. Account No.");

                             IF PostingDate <> 0D THEN
                               GenJnlLine.VALIDATE("Posting Date",PostingDate);

                             IF NOT GenJnlLine.INSERT(TRUE) THEN
                               GenJnlLine.MODIFY(TRUE);
                           END;
                         END;

        ReqFilterFields=No.,Account Type,Blocked,Direct Posting,No. 2;
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=9020;
      Height=3410;
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 2   ;TextBox      ;3850 ;0    ;1700 ;440  ;CaptionML=[ENU=Document Type;
                                                              ESP=Tipo documento];
                                                   OptionCaptionML=[ENU=" ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund";
                                                                    ESP=" ,Pago,Factura,Abono,Docs. inter‚s,Recordatorio,Reembolso"];
                                                   SourceExpr=DocumentTypes }
      { 3   ;Label        ;0    ;0    ;3740 ;440  ;ParentControl=2 }
      { 1   ;Label        ;0    ;550  ;3740 ;440  ;ParentControl=4 }
      { 4   ;TextBox      ;3850 ;550  ;1700 ;440  ;CaptionML=[ENU=Posting Date;
                                                              ESP=Fecha registro];
                                                   SourceExpr=PostingDate }
      { 7   ;Label        ;0    ;1650 ;3740 ;440  ;ParentControl=8 }
      { 8   ;TextBox      ;3850 ;1650 ;1700 ;440  ;CaptionML=[ENU=Batch Name;
                                                              ESP=Nombre secci¢n];
                                                   SourceExpr=BatchName;
                                                   TableRelation="Gen. Journal Batch".Name;
                                                   OnLookup=VAR
                                                              GenJnlBatches@1001 : Form 251;
                                                            BEGIN
                                                              IF JournalTemplate <> '' THEN BEGIN
                                                                GenJnlBatch.SETRANGE("Journal Template Name",JournalTemplate);
                                                                GenJnlBatches.SETTABLEVIEW(GenJnlBatch);
                                                              END;

                                                              GenJnlBatches.LOOKUPMODE := TRUE;
                                                              GenJnlBatches.EDITABLE := FALSE;
                                                              IF GenJnlBatches.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                GenJnlBatches.GETRECORD(GenJnlBatch);
                                                                BatchName := GenJnlBatch.Name;
                                                              END;
                                                            END;
                                                             }
      { 9   ;Label        ;0    ;2200 ;3740 ;440  ;ParentControl=10 }
      { 10  ;TextBox      ;3850 ;2200 ;1650 ;440  ;CaptionML=[ENU=Standard General Journal;
                                                              ESP=Diario general est ndar];
                                                   SourceExpr=TemplateCode;
                                                   TableRelation="Standard General Journal".Code;
                                                   OnLookup=VAR
                                                              StdGenJournal1@1005 : Record 750;
                                                              StdGenJnls@1000 : Form 750;
                                                            BEGIN
                                                              IF JournalTemplate <> '' THEN BEGIN
                                                                StdGenJournal1.SETRANGE("Journal Template Name",JournalTemplate);
                                                                StdGenJnls.SETTABLEVIEW(StdGenJournal1);
                                                              END;

                                                              StdGenJnls.LOOKUPMODE := TRUE;
                                                              StdGenJnls.EDITABLE := FALSE;
                                                              IF StdGenJnls.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                StdGenJnls.GETRECORD(StdGenJournal1);
                                                                TemplateCode := StdGenJournal1.Code;
                                                              END;
                                                            END;
                                                             }
      { 5   ;Label        ;0    ;1100 ;3740 ;440  ;ParentControl=6 }
      { 6   ;TextBox      ;3850 ;1100 ;1700 ;440  ;CaptionML=[ENU=Journal Template;
                                                              ESP=Libro diario];
                                                   SourceExpr=JournalTemplate;
                                                   TableRelation="Gen. Journal Batch".Name;
                                                   OnLookup=VAR
                                                              GenJnlTemplates@1000 : Form 101;
                                                              GenJnlTemplate@1001 : Record 80;
                                                            BEGIN
                                                              GenJnlTemplate.SETRANGE(Type,GenJnlTemplate.Type::General);
                                                              GenJnlTemplate.SETRANGE(Recurring,FALSE);
                                                              GenJnlTemplates.SETTABLEVIEW(GenJnlTemplate);

                                                              GenJnlTemplates.LOOKUPMODE := TRUE;
                                                              GenJnlTemplates.EDITABLE := FALSE;
                                                              IF GenJnlTemplates.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                GenJnlTemplates.GETRECORD(GenJnlTemplate);
                                                                JournalTemplate := GenJnlTemplate.Name;
                                                              END;
                                                            END;
                                                             }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones] }

      { 2   ;2   ;Field     ;
                  CaptionML=[ENU=Document Type;
                             ESP=Tipo documento];
                  OptionCaptionML=[ENU=" ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund";
                                   ESP=" ,Pago,Factura,Abono,Docs. inter‚s,Recordatorio,Reembolso"];
                  SourceExpr=DocumentTypes }

      { 4   ;2   ;Field     ;
                  CaptionML=[ENU=Posting Date;
                             ESP=Fecha registro];
                  SourceExpr=PostingDate }

      { 6   ;2   ;Field     ;
                  CaptionML=[ENU=Journal Template;
                             ESP=Libro diario];
                  SourceExpr=JournalTemplate;
                  TableRelation="Gen. Journal Batch".Name;
                  OnLookup=VAR
                             GenJnlTemplates@1000 : Form 101;
                             GenJnlTemplate@1001 : Record 80;
                           BEGIN
                             GenJnlTemplate.SETRANGE(Type,GenJnlTemplate.Type::General);
                             GenJnlTemplate.SETRANGE(Recurring,FALSE);
                             GenJnlTemplates.SETTABLEVIEW(GenJnlTemplate);

                             GenJnlTemplates.LOOKUPMODE := TRUE;
                             GenJnlTemplates.EDITABLE := FALSE;
                             IF GenJnlTemplates.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               GenJnlTemplates.GETRECORD(GenJnlTemplate);
                               JournalTemplate := GenJnlTemplate.Name;
                             END;
                           END;
                            }

      { 8   ;2   ;Field     ;
                  CaptionML=[ENU=Batch Name;
                             ESP=Nombre secci¢n];
                  SourceExpr=BatchName;
                  TableRelation="Gen. Journal Batch".Name;
                  OnLookup=VAR
                             GenJnlBatches@1001 : Form 251;
                           BEGIN
                             IF JournalTemplate <> '' THEN BEGIN
                               GenJnlBatch.SETRANGE("Journal Template Name",JournalTemplate);
                               GenJnlBatches.SETTABLEVIEW(GenJnlBatch);
                             END;

                             GenJnlBatches.LOOKUPMODE := TRUE;
                             GenJnlBatches.EDITABLE := FALSE;
                             IF GenJnlBatches.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               GenJnlBatches.GETRECORD(GenJnlBatch);
                               BatchName := GenJnlBatch.Name;
                             END;
                           END;
                            }

      { 10  ;2   ;Field     ;
                  CaptionML=[ENU=Standard General Journal;
                             ESP=Diario general est ndar];
                  SourceExpr=TemplateCode;
                  TableRelation="Standard General Journal".Code;
                  OnLookup=VAR
                             StdGenJournal1@1005 : Record 750;
                             StdGenJnls@1000 : Form 750;
                           BEGIN
                             IF JournalTemplate <> '' THEN BEGIN
                               StdGenJournal1.SETRANGE("Journal Template Name",JournalTemplate);
                               StdGenJnls.SETTABLEVIEW(StdGenJournal1);
                             END;

                             StdGenJnls.LOOKUPMODE := TRUE;
                             StdGenJnls.EDITABLE := FALSE;
                             IF StdGenJnls.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               StdGenJnls.GETRECORD(StdGenJournal1);
                               TemplateCode := StdGenJournal1.Code;
                             END;
                           END;
                            }

    }
  }
  CODE
  {
    VAR
      StdGenJournal@1007 : Record 750;
      GenJnlBatch@1006 : Record 232;
      GenJnlLine@1005 : Record 81;
      GenJnlLineTemp@1011 : TEMPORARY Record 81;
      LastGenJnlLine@1002 : Record 81;
      JnlLineDimFrom@1010 : Record 356;
      DocumentTypes@1000 : ' ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund';
      PostingDate@1001 : Date;
      BatchName@1003 : Code[20];
      TemplateCode@1004 : Code[20];
      LineNo@1008 : Integer;
      Text001@1014 : TextConst 'ENU=Gen. Journal Template name is blank.;ESP=El nombre del libro diario general est  vac¡o.';
      Text002@1009 : TextConst 'ENU=Gen. Journal Batch name is blank.;ESP=El nombre de la secci¢n diario general est  vac¡o.';
      JournalTemplate@1012 : Text[10];
      Text003@1015 : TextConst 'ENU=Standard Gen. Journal code is blank.;ESP=C¢digo diario general est ndar est  vac¡o.';
      Text004@1016 : TextConst 'ENU=General Journal lines are successfully created.;ESP=Las l¡neas del diario general se crearon correctamente.';

    PROCEDURE GetStandardJournalLine@3() : Boolean;
    VAR
      StdGenJounalLine@1000 : Record 751;
    BEGIN
      IF TemplateCode = '' THEN
        EXIT;
      StdGenJounalLine.SETRANGE("Journal Template Name",StdGenJournal."Journal Template Name");
      StdGenJounalLine.SETRANGE("Standard Journal Code",StdGenJournal.Code);
      EXIT(StdGenJounalLine.FIND('-'))
    END;

    PROCEDURE Initialize@15(VAR StdGenJnl@1001 : Record 750;JnlBatchName@1000 : Code[10]);
    BEGIN
      GenJnlLine."Journal Template Name" := StdGenJnl."Journal Template Name";
      GenJnlLine."Journal Batch Name" := JnlBatchName;
      GenJnlLine.SETRANGE("Journal Template Name",StdGenJnl."Journal Template Name");
      GenJnlLine.SETRANGE("Journal Batch Name",JnlBatchName);

      LastGenJnlLine.SETRANGE("Journal Template Name",StdGenJnl."Journal Template Name");
      LastGenJnlLine.SETRANGE("Journal Batch Name",JnlBatchName);

      IF LastGenJnlLine.FIND('+') THEN;

      GenJnlBatch.SETRANGE("Journal Template Name",StdGenJnl."Journal Template Name");
      GenJnlBatch.SETRANGE(Name,JnlBatchName);

      IF GenJnlBatch.FIND('-') THEN;
    END;

    PROCEDURE CopyGenJnlFromStdJnl@1(StdGenJnlLine@1001 : Record 751;VAR GenJnlLine@1002 : Record 81);
    VAR
      GenJnlManagement@1000 : Codeunit 230;
      Balance@1007 : Decimal;
      TotalBalance@1006 : Decimal;
      ShowBalance@1005 : Boolean;
      ShowTotalBalance@1004 : Boolean;
    BEGIN
      GenJnlLine.INIT;
      GenJnlLine."Line No." := 0;
      GenJnlManagement.CalcBalance(GenJnlLine,LastGenJnlLine,Balance,TotalBalance,ShowBalance,ShowTotalBalance);
      GenJnlLine.SetUpNewLine(LastGenJnlLine,Balance,TRUE);
      IF LastGenJnlLine."Line No." <> 0 THEN
        GenJnlLine."Line No." := LastGenJnlLine."Line No." + 10000
      ELSE
        GenJnlLine."Line No." := 10000;

      GenJnlLine.TRANSFERFIELDS(StdGenJnlLine,FALSE);
      GenJnlLine.UpdateLineBalance;
      GenJnlLine.VALIDATE("Currency Code");

      IF GenJnlLine."VAT Prod. Posting Group" <> '' THEN
        GenJnlLine.VALIDATE("VAT Prod. Posting Group");
      IF (GenJnlLine."VAT %" <> 0) AND GenJnlBatch."Allow VAT Difference" THEN
        GenJnlLine.VALIDATE("VAT Amount",StdGenJnlLine."VAT Amount");
      GenJnlLine.VALIDATE(GenJnlLine."Bal. VAT Prod. Posting Group");

      IF GenJnlBatch."Allow VAT Difference" THEN
        GenJnlLine.VALIDATE("Bal. VAT Amount",StdGenJnlLine."Bal. VAT Amount");
      GenJnlLine.INSERT(TRUE);

      CopyStdGenJnlLineDims(StdGenJnlLine,GenJnlLine);

      LastGenJnlLine := GenJnlLine;
    END;

    LOCAL PROCEDURE CopyStdGenJnlLineDims@2(StdGenJnlLine1@1001 : Record 751;GenJnlLine1@1000 : Record 81);
    VAR
      DimMgt@1003 : Codeunit 408;
    BEGIN
      JnlLineDimFrom.SETRANGE("Table ID",DATABASE::"Standard General Journal Line");
      JnlLineDimFrom.SETRANGE("Journal Template Name",StdGenJnlLine1."Journal Template Name");
      JnlLineDimFrom.SETRANGE("Journal Batch Name",StdGenJnlLine1."Standard Journal Code");
      JnlLineDimFrom.SETRANGE("Journal Line No.",StdGenJnlLine1."Line No.");

      DimMgt.DeleteJnlLineDim(DATABASE::"Gen. Journal Line",GenJnlLine1."Journal Template Name",
        GenJnlLine1."Journal Batch Name",GenJnlLine1."Line No.",0);

      DimMgt.MoveJnlLineDimToJnlLineDim(JnlLineDimFrom,DATABASE::"Gen. Journal Line",
        GenJnlLine1."Journal Template Name",GenJnlLine1."Journal Batch Name",GenJnlLine1."Line No.");
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
