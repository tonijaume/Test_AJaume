OBJECT Report 7000090 Payment order - Export N34
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVES6.00.01;
  }
  PROPERTIES
  {
    Permissions=TableData 7000020=m;
    CaptionML=[ENU=Payment order - Export N34;
               ESP=Orden pago - Exportar N34];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   CompanyInfo.GET();
                   CompanyInfo.TESTFIELD("VAT Registration No.");
                   VATRegNo := INSSTR(PADSTR('',MAXSTRLEN(VATRegNo) - STRLEN(CompanyInfo."VAT Registration No."),' '),
                   CompanyInfo."VAT Registration No.", 1 + MAXSTRLEN(VATRegNo) - STRLEN(CompanyInfo."VAT Registration No."));
                 END;

    OnPreReport=BEGIN

                  OutFile.TEXTMODE := TRUE;
                  OutFile.WRITEMODE := TRUE;
                  IF ISSERVICETIER THEN BEGIN
                    ExternalFile := RBMgt.EnvironFileName('','');
                    ToFile := Text10702;
                  END;
                  OutFile.CREATE(ExternalFile);
                END;

    OnPostReport=BEGIN
                   TotalReg := TotalReg + 1;
                   CASE TRUE OF
                     LCY = LCY::Euro:
                       IF IsEuro THEN
                         DocAmount := EuroAmount(TotalAmount)
                       ELSE
                         DocAmount := CONVERTSTR(FORMAT(TotalAmount,12,Text1100000),' ','0');
                     LCY = LCY::Other:
                       IF IsEuro THEN
                         DocAmount := EuroAmount(TotalAmount)
                       ELSE
                         DocAmount := CONVERTSTR(FORMAT(TotalAmount,12,Text1100000),' ','0');
                   END;

                   OutText := '08' + RegisterString + VATRegNo + PADSTR('',15,' ') +
                     DocAmount +
                     (PADSTR('',8 - STRLEN(FORMAT(TotalDocVend)),'0') + FORMAT(TotalDocVend)) +
                     (PADSTR('',10 - STRLEN(FORMAT(TotalReg)),'0') + FORMAT(TotalReg)) +
                      PADSTR('',13,' ');
                   OutFile.WRITE(OutText);

                   OutFile.CLOSE;
                   IF ISSERVICETIER THEN
                     DOWNLOAD(ExternalFile,'','C:',Text10701,ToFile);
                 END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table7000020;
        OnPreDataItem=BEGIN
                        GLSetup.GET;
                        IF CheckErrors THEN
                          Relat := '1'
                        ELSE
                          Relat := '0';

                        "Payment Order".FIND('-');
                        IsEuro := DocMisc.GetRegisterCode("Payment Order"."Currency Code",RegisterCode,RegisterString);
                        IF RegisterCode <> 0 THEN
                          RegisterString := '56'
                        ELSE
                          RegisterString := '06';
                      END;

        OnAfterGetRecord=BEGIN
                           TESTFIELD("Bank Account No.");
                           BankAcc.GET("Bank Account No.");


                           CCCBankNo := BankAcc."CCC Bank No.";
                           CCCBankNo := PADSTR('',MAXSTRLEN(CCCBankNo) - STRLEN(CCCBankNo),'0') + CCCBankNo;

                           CCCBankBranchNo := BankAcc."CCC Bank Branch No.";
                           CCCBankBranchNo := PADSTR('',MAXSTRLEN(CCCBankBranchNo) - STRLEN(CCCBankBranchNo),'0') + CCCBankBranchNo;

                           CCCAccNo := BankAcc."CCC Bank Account No.";
                           CCCAccNo := PADSTR('',MAXSTRLEN(CCCAccNo) - STRLEN(CCCAccNo),'0') + CCCAccNo;

                           CCCControlDigits := BankAcc."CCC Control Digits";
                           CCCControlDigits := PADSTR('',MAXSTRLEN(CCCControlDigits) - STRLEN(CCCControlDigits),'0') + CCCControlDigits;

                           IF "Posting Date" = 0D THEN
                             PostDate := PADSTR('',6,'0')
                           ELSE
                             PostDate := FORMAT("Posting Date",0,Text1100002);

                           OutText := '03' + RegisterString + VATRegNo + PADSTR('',12,' ') + '001' +
                             FORMAT(DeliveryDate,0,Text1100002) + PostDate +
                             CCCBankNo + CCCBankBranchNo + CCCAccNo + Relat + PADSTR('',3,' ') + CCCControlDigits + PADSTR('',7,' ');
                           OutFile.WRITE(OutText);

                           TotalReg := TotalReg + 1;

                           OutText := '03' + RegisterString + VATRegNo + PADSTR('',12,' ') + '002' +
                             PADSTR(CompanyInfo.Name,36,' ') + PADSTR('',7,' ');
                           OutFile.WRITE(OutText);

                           TotalReg := TotalReg + 1;

                           OutText := '03' + RegisterString + VATRegNo + PADSTR('',12,' ') + '003' +
                             PADSTR(CompanyInfo.Address,36,' ') + PADSTR('',7,' ');
                           OutFile.WRITE(OutText);

                           TotalReg := TotalReg + 1;

                           OutText := '03' + RegisterString + VATRegNo + PADSTR('',12,' ') + '004' +
                             PADSTR(CompanyInfo."Post Code" + ' ' + CompanyInfo.City,36,' ') + PADSTR('',7,' ');
                           OutFile.WRITE(OutText);

                           TotalReg := TotalReg + 1;
                         END;

        ReqFilterFields=No.;
      }
      SECTIONS
      {
      }
       }
    { PROPERTIES
      {
        DataItemIndent=1;
        DataItemTable=Table7000002;
        DataItemTableView=SORTING(Type,Bill Gr./Pmt. Order No.,Category Code,Currency Code,Accepted,Due Date)
                          ORDER(Ascending)
                          WHERE(Type=CONST(Payable));
        OnAfterGetRecord=BEGIN
                           TESTFIELD("Payment Method Code");
                           DocType2 := DocMisc.DocType2("Payment Method Code");

                           TESTFIELD("Account No.");
                           Vendor.GET("Account No.");

                           VATRegVend := Vendor."VAT Registration No.";
                           VATRegVend := PADSTR('',MAXSTRLEN(VATRegVend) - STRLEN(VATRegVend),' ') + VATRegVend;
                           RmgAmount := PADSTR('',MAXSTRLEN(RmgAmount) - STRLEN(RmgAmount),'0') + RmgAmount;


                           CASE TRUE OF
                             LCY = LCY::Euro:
                               IF IsEuro THEN BEGIN
                                 TotalAmount := TotalAmount + "Remaining Amount";
                                 RmgAmount := EuroAmount ("Remaining Amount");
                               END ELSE  BEGIN
                                 RmgAmount := FORMAT("Remaining Amount" * 100,12,Text1100000);
                                 TotalAmount := TotalAmount + "Remaining Amount";
                               END;
                             LCY = LCY::Other:
                               IF IsEuro THEN  BEGIN
                                 TotalAmount := TotalAmount + "Remaining Amount";
                                 RmgAmount := EuroAmount ("Remaining Amount");
                               END ELSE BEGIN
                                 RmgAmount := FORMAT("Remaining Amount" * 100,12,Text1100000);
                                 TotalAmount := TotalAmount + "Remaining Amount" * 100;
                               END;
                           END;

                           IF DocType2 = '4' THEN BEGIN
                             VendBankAccCode := "Cust./Vendor Bank Acc. Code";
                             IF VendBankAcc.GET("Account No.",VendBankAccCode) THEN BEGIN
                               VendCCCAccNo := VendBankAcc."CCC Bank Account No.";
                               VendCCCControlDigits := VendBankAcc."CCC Control Digits";
                               Vendor2 := VendBankAcc."CCC Bank No.";
                               VendCCCBankBranchNo := VendBankAcc."CCC Bank Branch No.";
                             END ELSE BEGIN
                               VendCCCAccNo := '';
                               VendCCCControlDigits := '';
                               Vendor2 := '';
                               VendCCCBankBranchNo := '';
                             END;
                           END ELSE BEGIN
                             TESTFIELD("Cust./Vendor Bank Acc. Code");
                             VendBankAccCode := "Cust./Vendor Bank Acc. Code";
                             VendBankAcc.GET("Account No.",VendBankAccCode);
                             VendCCCAccNo := VendBankAcc."CCC Bank Account No.";
                             VendCCCControlDigits := VendBankAcc."CCC Control Digits";
                             Vendor2 := VendBankAcc."CCC Bank No.";
                             VendCCCBankBranchNo := VendBankAcc."CCC Bank Branch No.";
                             IF (Vendor2 = '') OR (VendCCCBankBranchNo = '') OR
                              (VendCCCControlDigits = '') OR (VendCCCAccNo = '')
                             THEN
                               ERROR(Text1100003,VendBankAcc."Vendor No.");
                           END;

                           VendCCCAccNo := PADSTR('',MAXSTRLEN(VendCCCAccNo) - STRLEN(VendCCCAccNo),'0') + VendCCCAccNo;
                           VendCCCControlDigits := PADSTR('',MAXSTRLEN(VendCCCControlDigits) - STRLEN(VendCCCControlDigits),'0')
                             + VendCCCControlDigits;

                           IF IsEuro THEN
                             IF DocType2 = '4' THEN
                               DocType := '57'
                             ELSE BEGIN
                               DocType := '56';
                               VendBankAcc.TESTFIELD("CCC Bank Account No.");
                             END
                           ELSE
                             IF DocType2 = '4' THEN
                               DocType := '07'
                             ELSE BEGIN
                               DocType := '06';
                               VendBankAcc.TESTFIELD("CCC Bank Account No.");
                             END;

                           OutText := '06' + DocType + VATRegNo + VATRegVend + '010' + CONVERTSTR(RmgAmount,' ','0') +
                             CONVERTSTR(PADSTR(Vendor2,4,' '),' ','0') + CONVERTSTR(PADSTR(VendCCCBankBranchNo,4,' '),' ','0') +
                             CONVERTSTR(PADSTR(VendCCCAccNo,10,' '),' ','0') +
                             '1' + '9' + PADSTR('',2,' ') + PADSTR(VendCCCControlDigits,2,' ') + PADSTR('',7,' ');
                           OutFile.WRITE(OutText);

                           TotalReg := TotalReg + 1;
                           TotalDocVend := TotalDocVend + 1;

                           OutText := '06' + DocType + VATRegNo + VATRegVend + '011' + PADSTR(Vendor.Name,36,' ')
                             + PADSTR('',7,' ');
                           OutFile.WRITE(OutText);
                           TotalReg := TotalReg + 1;
                           IF (DocType = '06') OR (DocType = '56') THEN BEGIN
                             OutText := '06' + DocType + VATRegNo + VATRegVend + '012' + PADSTR(Vendor.Address,36,' ')
                                        + PADSTR('',7,' ');
                             OutFile.WRITE(OutText);

                             TotalReg := TotalReg + 1;

                             IF Vendor."Address 2" <> '' THEN BEGIN
                               OutText := '06' + DocType + VATRegNo + VATRegVend + '013' + PADSTR(Vendor."Address 2",36,' ')
                                 + PADSTR('',7,' ');
                               OutFile.WRITE(OutText);

                               TotalReg := TotalReg + 1;
                             END;

                             OutText := '06' + DocType + VATRegNo + VATRegVend + '014' + PADSTR(Vendor."Post Code" + ' ' + Vendor.City,36,' ')
                               + PADSTR('',7,' ');
                             OutFile.WRITE(OutText);

                             TotalReg := TotalReg + 1;
                           END;
                         END;

        DataItemLink=Bill Gr./Pmt. Order No.=FIELD(No.);
      }
      SECTIONS
      {
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=8910;
      Height=1760;
      SaveValues=Yes;
      OnOpenForm=BEGIN
                   IF ExternalFile = '' THEN
                     ExternalFile := 'C:\' + Text10702;
                   IF DeliveryDate = 0D THEN
                     DeliveryDate := TODAY;
                   RequestOptionsForm.FileNameControl.VISIBLE := NOT ISSERVICETIER;
                 END;

    }
    CONTROLS
    {
      { 1   ;TextBox      ;3410 ;0    ;5500 ;440  ;Name=FileNameControl;
                                                   CaptionML=[ENU=External file;
                                                              ESP=Archivo externo];
                                                   SourceExpr=ExternalFile;
                                                   OnAssistEdit=BEGIN
                                                                  ExternalFile := CommonDialogMgt.OpenFile(Text10700,ExternalFile,4,Text10701,1);
                                                                END;
                                                                 }
      { 2   ;Label        ;0    ;0    ;3300 ;440  ;ParentControl=1 }
      { 4   ;Label        ;0    ;660  ;3300 ;440  ;ParentControl=5 }
      { 5   ;CheckBox     ;3410 ;660  ;440  ;440  ;Name=crelation;
                                                   ShowCaption=No;
                                                   CaptionML=[ENU=Relation;
                                                              ESP=Relaci¢n];
                                                   SourceExpr=CheckErrors }
      { 3   ;Label        ;0    ;1320 ;3300 ;440  ;ParentControl=6;
                                                   CaptionML=[ENU=Delivery Date;
                                                              ESP=Fecha de emisi¢n] }
      { 6   ;TextBox      ;3410 ;1320 ;1650 ;440  ;CaptionML=[ENU=Delivery Date;
                                                              ESP=Fecha de emisi¢n];
                                                   SourceExpr=DeliveryDate }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnInit=BEGIN
               FileNameControlVisible := TRUE;
             END;

      OnOpenPage=BEGIN
                   IF ExternalFile = '' THEN
                     ExternalFile := 'C:\' + Text10702;
                   IF DeliveryDate = 0D THEN
                     DeliveryDate := TODAY;
                   FileNameControlVisible := NOT ISSERVICETIER;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones] }

      { 1   ;2   ;Field     ;
                  Name=FileNameControl;
                  CaptionML=[ENU=External file;
                             ESP=Archivo externo];
                  SourceExpr=ExternalFile;
                  Visible=FileNameControlVisible;
                  OnAssistEdit=BEGIN
                                 ExternalFile := CommonDialogMgt.OpenFile(Text10700,ExternalFile,4,Text10701,1);
                               END;
                                }

      { 5   ;2   ;Field     ;
                  Name=crelation;
                  CaptionML=[ENU=Relation;
                             ESP=Relaci¢n];
                  SourceExpr=CheckErrors }

      { 6   ;2   ;Field     ;
                  CaptionML=[ENU=Delivery Date;
                             ESP=Fecha de emisi¢n];
                  SourceExpr=DeliveryDate }

    }
  }
  CODE
  {
    VAR
      Text1100000@1000 : TextConst 'ENU=<Integer>;ESP=<Integer>';
      Text1100002@1002 : TextConst 'ENU=<Day,2><month,2><year>;ESP=<Day,2><month,2><year>';
      Text1100003@1003 : TextConst 'ENU=Some data from the Bank Account of Vendor %1 are missing.;ESP=Falta alguna informaci¢n sobre el banco del proveedor %1.';
      CompanyInfo@1100000 : Record 79;
      Vendor@1100001 : Record 23;
      BankAcc@1100002 : Record 270;
      VendBankAcc@1100003 : Record 288;
      PaymentMethod@1100004 : Record 289;
      GLSetup@1100005 : Record 98;
      DocMisc@1100006 : Codeunit 7000007;
      OutFile@1100007 : File;
      ExternalFile@1100008 : Text[1024];
      CheckErrors@1100009 : Boolean;
      VATRegNo@1100010 : Text[10];
      CCCBankNo@1100011 : Text[4];
      VATRegVend@1100012 : Text[12];
      VendCCCBankBranchNo@1100013 : Text[4];
      CCCBankBranchNo@1100014 : Text[4];
      VendCCCControlDigits@1100015 : Text[2];
      CCCControlDigits@1100016 : Text[2];
      CCCAccNo@1100017 : Text[10];
      VendCCCAccNo@1100018 : Text[10];
      Vendor2@1100019 : Text[4];
      TotalReg@1100020 : Decimal;
      TotalDocVend@1100021 : Decimal;
      TotalAmount@1100022 : Decimal;
      OutText@1100023 : Text[85];
      DeliveryDate@1100024 : Date;
      Relat@1100025 : Text[1];
      DocType2@1100026 : Code[10];
      DocType@1100027 : Text[2];
      RmgAmount@1100028 : Text[12];
      VendBankAccCode@1100029 : Code[20];
      CurrCode@1100030 : Text[3];
      PostDate@1100031 : Text[6];
      LCY@1100032 : 'Euro,Other';
      IsEuro@1100033 : Boolean;
      RegisterCode@1100034 : Integer;
      RegisterString@1100035 : Text[2];
      DocAmount@1100036 : Text[12];
      RBMgt@1100037 : Codeunit 419;
      ToFile@1100038 : Text[1024];
      CommonDialogMgt@1100039 : Codeunit 412;
      Text10700@1100042 : TextConst 'ENU=Export to file;ESP=Exportar a archivo';
      Text10701@1100041 : TextConst 'ENU=ASC Files (*.asc)|*.asc|All Files (*.*)|*.*;ESP=Archivos ASC (*.asc)|*.asc|Todos los archivos (*.*)|*.*';
      Text10702@1100040 : TextConst 'ENU=ORDENPAGO.ASC;ESP=ORDENPAGO.ASC';
      FileNameControlVisible@19024830 : Boolean INDATASET;

    PROCEDURE EuroAmount@3(Amount@1100000 : Decimal) : Text[12];
    VAR
      TextAmount@1100001 : Text[15];
    BEGIN
      TextAmount := CONVERTSTR(FORMAT(Amount),' ','0');
      IF STRPOS(TextAmount,',') = 0 THEN
        TextAmount := TextAmount + '00'
      ELSE BEGIN
        IF STRLEN(COPYSTR(TextAmount,STRPOS(TextAmount,','),STRLEN(TextAmount))) = 2 THEN
          TextAmount := TextAmount + '0';
        TextAmount := DELCHR(TextAmount,'=',',');
      END;
      IF STRPOS(TextAmount,'.') = 0 THEN
        TextAmount := TextAmount
      ELSE
        TextAmount := DELCHR(TextAmount,'=','.');

      WHILE STRLEN(TextAmount) < 12 DO
        TextAmount := '0' + TextAmount;

      EXIT(TextAmount);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
