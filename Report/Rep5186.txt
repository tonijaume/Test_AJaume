OBJECT Report 5186 Remove Contacts
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Remove Contacts;
               ESP=Eliminar contactos];
    UseReqForm=No;
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  ItemFilters := "Value Entry".HASFILTER;

                  SegCriteriaManagement.InsertCriteriaAction(
                    "Segment Header".GETFILTER("No."),MainReportNo,
                    FALSE,FALSE,FALSE,FALSE,EntireCompanies);
                  SegCriteriaManagement.InsertCriteriaFilter(
                    "Segment Header".GETFILTER("No."),DATABASE::Contact,
                    Contact.GETFILTERS,Contact.GETVIEW(FALSE));
                  SegCriteriaManagement.InsertCriteriaFilter(
                    "Segment Header".GETFILTER("No."),DATABASE::"Contact Profile Answer",
                    "Contact Profile Answer".GETFILTERS,"Contact Profile Answer".GETVIEW(FALSE));
                  SegCriteriaManagement.InsertCriteriaFilter(
                    "Segment Header".GETFILTER("No."),DATABASE::"Contact Mailing Group",
                    "Contact Mailing Group".GETFILTERS,"Contact Mailing Group".GETVIEW(FALSE));
                  SegCriteriaManagement.InsertCriteriaFilter(
                    "Segment Header".GETFILTER("No."),DATABASE::"Interaction Log Entry",
                    "Interaction Log Entry".GETFILTERS,"Interaction Log Entry".GETVIEW(FALSE));
                  SegCriteriaManagement.InsertCriteriaFilter(
                    "Segment Header".GETFILTER("No."),DATABASE::"Contact Job Responsibility","Contact Job Responsibility".GETFILTERS,
                    "Contact Job Responsibility".GETVIEW(FALSE));
                  SegCriteriaManagement.InsertCriteriaFilter(
                    "Segment Header".GETFILTER("No."),DATABASE::"Contact Industry Group",
                    "Contact Industry Group".GETFILTERS,"Contact Industry Group".GETVIEW(FALSE));
                  SegCriteriaManagement.InsertCriteriaFilter(
                    "Segment Header".GETFILTER("No."),DATABASE::"Contact Business Relation",
                    "Contact Business Relation".GETFILTERS,"Contact Business Relation".GETVIEW(FALSE));
                  SegCriteriaManagement.InsertCriteriaFilter(
                    "Segment Header".GETFILTER("No."),DATABASE::"Value Entry",
                    "Value Entry".GETFILTERS,"Value Entry".GETVIEW(FALSE));
                END;

    OnPostReport=BEGIN
                   IF EntireCompanies THEN
                     AddPeople;

                   UpdateSegLines;
                 END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table5076;
        DataItemTableView=SORTING(No.);
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=1;
        DataItemTable=Table5077;
        DataItemTableView=SORTING(Segment No.,Line No.);
        OnAfterGetRecord=BEGIN
                           RecordNo := RecordNo + 1;
                           IF RecordNo = 1 THEN BEGIN
                             OldTime := TIME;
                             CASE MainReportNo OF
                               REPORT::"Remove Contacts - Reduce": Window.OPEN(Text000);
                               REPORT::"Remove Contacts - Refine": Window.OPEN(Text001);
                             END;
                             NoOfRecords := COUNT;
                           END;
                           NewTime := TIME;
                           IF (NewTime - OldTime > 100) OR (NewTime < OldTime) THEN BEGIN
                             NewProgress := ROUND(RecordNo / NoOfRecords * 100,1);
                             IF NewProgress <> OldProgress THEN BEGIN
                               Window.UPDATE(1,NewProgress * 100);
                               OldProgress := NewProgress;
                             END;
                             OldTime := TIME;
                           END;
                         END;

        DataItemLink=Segment No.=FIELD(No.);
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=2;
        DataItemTable=Table5050;
        DataItemTableView=SORTING(No.);
        OnPreDataItem=BEGIN
                        FILTERGROUP(4);
                        SETRANGE("Company No.","Segment Line"."Contact Company No.");
                        IF NOT EntireCompanies THEN
                          SETRANGE("No.","Segment Line"."Contact No.");
                        FILTERGROUP(0);
                      END;

        OnAfterGetRecord=BEGIN
                           IF EntireCompanies THEN BEGIN
                             IF TempCheckCont.GET(Contact."No.") THEN
                               CurrReport.SKIP;
                             TempCheckCont := Contact;
                             TempCheckCont.INSERT;
                           END;

                           ContactOK := TRUE;
                         END;

        ReqFilterFields=No.,Search Name,Type,Salesperson Code,Post Code,Country/Region Code,Territory Code;
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=3;
        DataItemTable=Table5089;
        ReqFilterHeadingML=[ENU=Profile;
                            ESP=Perfil];
        OnPreDataItem=BEGIN
                        IF ContactOK AND (GETFILTERS<>'') THEN
                          ContactOK := FALSE
                        ELSE
                          CurrReport.BREAK;
                      END;

        OnAfterGetRecord=BEGIN
                           ContactOK := TRUE;
                           CurrReport.BREAK;
                         END;

        DataItemLink=Contact No.=FIELD(No.);
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=3;
        DataItemTable=Table5056;
        DataItemTableView=SORTING(Contact No.,Mailing Group Code);
        ReqFilterHeadingML=[ENU=Mailing Group;
                            ESP=Grupo correo];
        OnPreDataItem=BEGIN
                        IF ContactOK AND (GETFILTERS<>'') THEN
                          ContactOK := FALSE
                        ELSE
                          CurrReport.BREAK;
                      END;

        OnAfterGetRecord=BEGIN
                           ContactOK := TRUE;
                           CurrReport.BREAK;
                         END;

        ReqFilterFields=Mailing Group Code;
        DataItemLink=Contact No.=FIELD(No.);
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=3;
        DataItemTable=Table5065;
        DataItemTableView=SORTING(Contact Company No.,Contact No.,Date);
        OnPreDataItem=BEGIN
                        IF ContactOK AND (GETFILTERS<>'') THEN
                          ContactOK := FALSE
                        ELSE
                          CurrReport.BREAK;
                      END;

        OnAfterGetRecord=BEGIN
                           ContactOK := TRUE;
                           CurrReport.BREAK;
                         END;

        ReqFilterFields=Date,Segment No.,Campaign No.,Evaluation,Interaction Template Code,Salesperson Code;
        DataItemLink=Contact Company No.=FIELD(Company No.),
                     Contact No.=FIELD(No.);
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=3;
        DataItemTable=Table5067;
        DataItemTableView=SORTING(Contact No.,Job Responsibility Code);
        ReqFilterHeadingML=[ENU=Job Responsibility;
                            ESP=Responsabilidad cargo];
        OnPreDataItem=BEGIN
                        IF ContactOK AND (GETFILTERS<>'') THEN
                          ContactOK := FALSE
                        ELSE
                          CurrReport.BREAK;
                      END;

        OnAfterGetRecord=BEGIN
                           ContactOK := TRUE;
                           CurrReport.BREAK;
                         END;

        ReqFilterFields=Job Responsibility Code;
        DataItemLink=Contact No.=FIELD(No.);
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=3;
        DataItemTable=Table5058;
        DataItemTableView=SORTING(Contact No.,Industry Group Code);
        ReqFilterHeadingML=[ENU=Industry Group;
                            ESP=Grupo industria];
        OnPreDataItem=BEGIN
                        IF ContactOK AND (GETFILTERS<>'') THEN
                          ContactOK := FALSE
                        ELSE
                          CurrReport.BREAK;
                      END;

        OnAfterGetRecord=BEGIN
                           ContactOK := TRUE;
                           CurrReport.BREAK;
                         END;

        ReqFilterFields=Industry Group Code;
        DataItemLink=Contact No.=FIELD(Company No.);
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=3;
        DataItemTable=Table5054;
        DataItemTableView=SORTING(Contact No.,Business Relation Code);
        ReqFilterHeadingML=[ENU=Business Relation;
                            ESP=Relaci¢n negocio];
        OnPreDataItem=BEGIN
                        IF ContactOK AND ((GETFILTERS<>'') OR ItemFilters) THEN
                          ContactOK := FALSE
                        ELSE
                          CurrReport.BREAK;
                      END;

        OnAfterGetRecord=BEGIN
                           SkipItemLedgerEntry := FALSE;
                           IF NOT ItemFilters THEN BEGIN
                             ContactOK := TRUE;
                             SkipItemLedgerEntry := TRUE;
                             CurrReport.BREAK;
                           END;
                         END;

        ReqFilterFields=Business Relation Code;
        DataItemLink=Contact No.=FIELD(Company No.);
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=4;
        DataItemTable=Table5802;
        DataItemTableView=SORTING(Source Type,Source No.,Item No.,Posting Date);
        OnPreDataItem=BEGIN
                        IF SkipItemLedgerEntry THEN
                          CurrReport.BREAK;

                        CASE "Contact Business Relation"."Link to Table" OF
                          "Contact Business Relation"."Link to Table"::Customer: BEGIN
                            "Value Entry".SETRANGE("Source Type","Value Entry"."Source Type"::Customer);
                            "Value Entry".SETRANGE("Source No.","Contact Business Relation"."No.");
                          END;
                          "Contact Business Relation"."Link to Table"::Vendor: BEGIN
                            "Value Entry".SETRANGE("Source Type","Value Entry"."Source Type"::Vendor);
                            "Value Entry".SETRANGE("Source No.","Contact Business Relation"."No.");
                          END ELSE
                            CurrReport.BREAK;
                        END;
                      END;

        OnAfterGetRecord=BEGIN
                           ContactOK := TRUE;
                           CurrReport.BREAK;
                         END;

        ReqFilterFields=Item No.,Variant Code,Posting Date,Inventory Posting Group;
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=3;
        DataItemTable=Table2000000026;
        DataItemTableView=SORTING(Number)
                          WHERE(Number=CONST(1));
        OnAfterGetRecord=BEGIN
                           IF ContactOK THEN
                             InsertContact(Contact);
                         END;

      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=9020;
      Height=3410;
    }
    CONTROLS
    {
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Reducing Contacts @1@@@@@@@@@@@@@;ESP=Reduc. contactos  @1@@@@@@@@@@@@@';
      Text001@1001 : TextConst 'ENU=Refining Contacts @1@@@@@@@@@@@@@;ESP=Redef. contactos  @1@@@@@@@@@@@@@';
      TempCont@1002 : TEMPORARY Record 5050;
      TempCont2@1003 : TEMPORARY Record 5050;
      TempCheckCont@1004 : TEMPORARY Record 5050;
      Cont@1005 : Record 5050;
      SegLine@1006 : Record 5077;
      SegmentHistoryMgt@1007 : Codeunit 5061;
      SegCriteriaManagement@1008 : Codeunit 5062;
      MainReportNo@1009 : Integer;
      ItemFilters@1010 : Boolean;
      ContactOK@1011 : Boolean;
      EntireCompanies@1012 : Boolean;
      SkipItemLedgerEntry@1013 : Boolean;
      Window@1014 : Dialog;
      NoOfRecords@1015 : Integer;
      RecordNo@1016 : Integer;
      OldTime@1017 : Time;
      NewTime@1018 : Time;
      OldProgress@1019 : Integer;
      NewProgress@1020 : Integer;

    PROCEDURE SetOptions@4(CalledFromReportNo@1000 : Integer;OptionEntireCompanies@1001 : Boolean);
    BEGIN
      MainReportNo := CalledFromReportNo;
      EntireCompanies := OptionEntireCompanies;
    END;

    PROCEDURE InsertContact@9(VAR CheckedCont@1000 : Record 5050);
    BEGIN
      TempCont := CheckedCont;
      IF TempCont.INSERT THEN;
    END;

    PROCEDURE AddPeople@2();
    BEGIN
      TempCont.RESET;
      IF TempCont.FIND('-') THEN
        REPEAT
          IF TempCont."Company No." <> '' THEN BEGIN
            Cont.SETCURRENTKEY("Company No.");
            Cont.SETRANGE("Company No.",TempCont."Company No.");
            IF Cont.FIND('-') THEN
              REPEAT
                TempCont2 := Cont;
                IF TempCont2.INSERT THEN;
              UNTIL Cont.NEXT = 0
          END ELSE BEGIN
            TempCont2 := TempCont;
            TempCont2.INSERT;
          END;
        UNTIL TempCont.NEXT = 0;

      TempCont.DELETEALL;
      IF TempCont2.FIND('-') THEN
        REPEAT
          TempCont := TempCont2;
          TempCont.INSERT;
        UNTIL TempCont2.NEXT = 0;
      TempCont2.DELETEALL;
    END;

    PROCEDURE UpdateSegLines@1();
    BEGIN
      SegLine.RESET;
      SegLine.SETRANGE("Segment No.","Segment Header"."No.");
      IF SegLine.FIND('-') THEN
        REPEAT
          CASE MainReportNo OF
            REPORT::"Remove Contacts - Reduce": BEGIN
              IF TempCont.GET(SegLine."Contact No.") THEN BEGIN
                SegLine.DELETE(TRUE);
                SegmentHistoryMgt.DeleteLine(
                  SegLine."Segment No.",SegLine."Contact No.",SegLine."Line No.");
              END;
            END;
            REPORT::"Remove Contacts - Refine":
              IF NOT TempCont.GET(SegLine."Contact No.") THEN BEGIN
                SegLine.DELETE(TRUE);
                SegmentHistoryMgt.DeleteLine(
                  SegLine."Segment No.",SegLine."Contact No.",SegLine."Line No.");
              END;
          END;
        UNTIL SegLine.NEXT = 0;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
