OBJECT Report 7000086 Batch Settl. Posted Bill Grs.
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVES6.00;
  }
  PROPERTIES
  {
    Permissions=TableData 21=imd,
                TableData 25=imd,
                TableData 45=m,
                TableData 7000003=imd,
                TableData 7000004=imd,
                TableData 7000006=imd,
                TableData 7000007=imd;
    CaptionML=[ENU=Batch Settl. Posted Bill Grs.;
               ESP=Liq. por lotes remesas regis.];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   PostingDate := WORKDATE;
                 END;

    OnPreReport=BEGIN
                  GLSetup.GET;
                END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table7000006;
        DataItemTableView=SORTING(No.)
                          ORDER(Ascending);
        DataItemVarName=PostedBillGr;
        OnPreDataItem=BEGIN
                        DocPost.CheckPostingDate(PostingDate);

                        SourceCodeSetup.GET;
                        SourceCode := SourceCodeSetup."Cartera Journal";

                        GroupAmountLCY := 0;
                        BillGrCount := 0;
                        DocCount := 0;
                        TotalDocCount := 0;
                        TotalBillGr := PostedBillGr.COUNT;
                        Window.OPEN(
                          Text1100000 +
                          Text1100001 +
                          Text1100002);
                      END;

        OnAfterGetRecord=BEGIN
                           BillGrCount := BillGrCount + 1;
                           Window.UPDATE(1,ROUND(BillGrCount / TotalBillGr * 10000,1));
                           Window.UPDATE(2,STRSUBSTNO('%1',"No."));
                           Window.UPDATE(3,0);
                           GroupAmount := 0;

                           TotalDisctdAmt := 0;
                         END;

        OnPostDataItem=BEGIN
                         Window.CLOSE;

                         COMMIT;

                         MESSAGE(
                           Text1100003,
                           TotalDocCount,BillGrCount,GroupAmountLCY);
                       END;

      }
      SECTIONS
      {
      }
       }
    { PROPERTIES
      {
        DataItemIndent=1;
        DataItemTable=Table7000003;
        DataItemTableView=SORTING(Bill Gr./Pmt. Order No.,Status,Category Code,Redrawn,Due Date)
                          WHERE(Status=CONST(Open),
                                Type=CONST(Receivable));
        DataItemVarName=PostedDoc;
        OnPreDataItem=BEGIN
                        SumLCYAmt := 0;
                        GenJnlLineNextNo := 0;
                        TotalDoc := PostedDoc.COUNT;
                        ExistVATEntry := FALSE;
                      END;

        OnAfterGetRecord=VAR
                           FromJnl@1100001 : Boolean;
                         BEGIN
                           IsRedrawn := CarteraManagement.CheckFromRedrawnDoc(PostedDoc."No.");
                           BankAcc.GET(PostedBillGr."Bank Account No.");
                           Delay := BankAcc."Delay for Notices";

                           IF DueOnly AND (PostingDate < "Due Date" + Delay) THEN
                             CurrReport.SKIP;

                           TotalDocCount := TotalDocCount + 1;
                           DocCount := DocCount + 1;
                           Window.UPDATE(3,ROUND(DocCount / TotalDoc * 10000,1));
                           Window.UPDATE(4,STRSUBSTNO('%1 %2',"Document Type","Document No."));


                           WITH GenJnlLine DO BEGIN
                             GenJnlLineNextNo := GenJnlLineNextNo + 10000;
                             CLEAR(GenJnlLine);
                             INIT;
                             "Line No." := GenJnlLineNextNo;
                             "Posting Date" := PostingDate;
                             "Document Type" := "Document Type"::Payment;
                             "Document No." := PostedBillGr."No.";
                             VALIDATE("Account Type","Account Type"::Customer);
                             CustLedgEntry.GET(PostedDoc."Entry No.");
                             IF PostedDoc."Document Type" = PostedDoc."Document Type"::Bill THEN BEGIN
                               Description := COPYSTR(
                                 STRSUBSTNO(Text1100004,PostedDoc."Document No.",PostedDoc."No."),
                                 1,MAXSTRLEN(Description));
                               IF GLSetup."Unrealized VAT" THEN BEGIN
                                 FromJnl := FALSE;
                                 IF PostedDoc."From Journal" THEN
                                   FromJnl := TRUE;
                                 ExistsNoRealVAT := GenJnlPostLine.CustFindVATSetup(VATPostingSetup,CustLedgEntry,FromJnl);
                               END;
                             END ELSE
                               Description := COPYSTR(
                                 STRSUBSTNO(Text1100005,PostedDoc."Document No."),
                                 1,MAXSTRLEN(Description));
                             VALIDATE("Account No.",CustLedgEntry."Customer No.");
                             VALIDATE("Currency Code",PostedDoc."Currency Code");
                             VALIDATE(Amount,-PostedDoc."Remaining Amount");
                             "Applies-to Doc. Type" := CustLedgEntry."Document Type";
                             "Applies-to Doc. No." := CustLedgEntry."Document No.";
                             "Applies-to Bill No." := CustLedgEntry."Bill No.";
                             "Source Code" := SourceCode;
                             "System-Created Entry" := TRUE;
                             "Shortcut Dimension 1 Code" := CustLedgEntry."Global Dimension 1 Code";
                             "Shortcut Dimension 2 Code" := CustLedgEntry."Global Dimension 2 Code";
                             INSERT;
                             CarteraDimMgt.CustInsertJnlLinDimension(GenJnlLine,CustLedgEntry,TRUE);
                             SumLCYAmt := SumLCYAmt + "Amount (LCY)";
                           END;

                           IF ("Document Type" = "Document Type"::Bill) AND
                              GLSetup."Unrealized VAT" AND
                              ExistVATEntry AND
                              (NOT IsRedrawn) THEN BEGIN
                             CustLedgEntry.CALCFIELDS("Remaining Amount","Remaining Amt. (LCY)");
                             CarteraManagement.CustUnrealizedVAT2(
                               CustLedgEntry,
                               CustLedgEntry."Remaining Amt. (LCY)",
                               GenJnlLine,
                               ExistVATEntry,
                               FirstVATEntryNo,
                               LastVATEntryNo,
                               NoRealVATBuffer,
                               FromJnl);

                               IF NoRealVATBuffer.FIND('-') THEN BEGIN
                                 REPEAT
                                   BEGIN
                                     InsertGenJournalLine(
                                       GenJnlLine."Account Type"::"G/L Account",
                                       NoRealVATBuffer.Account,
                                       NoRealVATBuffer.Amount);
                                     InsertGenJournalLine(
                                       GenJnlLine."Account Type"::"G/L Account",
                                       NoRealVATBuffer."Balance Account",
                                       -NoRealVATBuffer.Amount);
                                   END;
                                 UNTIL NoRealVATBuffer.NEXT = 0;
                                 NoRealVATBuffer.DELETEALL;
                               END;
                           END;

                           GroupAmount := GroupAmount + "Remaining Amount";
                           GroupAmountLCY := GroupAmountLCY + "Remaining Amt. (LCY)";
                           CustLedgEntry."Document Status" := CustLedgEntry."Document Status"::Honored;
                           CustLedgEntry.MODIFY;

                           IF BGPOPostBuffer.GET('','',CustLedgEntry."Entry No.") THEN BEGIN
                             BGPOPostBuffer.Amount := BGPOPostBuffer.Amount + "Remaining Amount";
                             BGPOPostBuffer.MODIFY;
                           END ELSE BEGIN
                             BGPOPostBuffer.INIT;
                             BGPOPostBuffer."Entry No." := CustLedgEntry."Entry No.";
                             BGPOPostBuffer.Amount := BGPOPostBuffer.Amount + "Remaining Amount";
                             BGPOPostBuffer.INSERT;
                           END;


                           IF  (PostedBillGr."Dealing Type" = PostedBillGr."Dealing Type"::Discount)  AND
                               (PostedBillGr.Factoring <> PostedBillGr.Factoring::" ") THEN BEGIN
                             DiscFactLiabs(PostedDoc);
                           END;
                         END;

        OnPostDataItem=VAR
                         CustLedgEntry2@1100001 : Record 21;
                         JnlLineDim@1100000 : Record 356;
                       BEGIN
                         IF (DocCount = 0) OR (GroupAmount = 0) THEN
                           CurrReport.SKIP;

                         IF PostedBillGr.Factoring = PostedBillGr.Factoring::" " THEN BEGIN

                           WITH GenJnlLine DO BEGIN
                             CustLedgEntry2.GET(BGPOPostBuffer."Entry No.");
                             CLEAR(GenJnlLine);
                             INIT;
                             GenJnlLineNextNo := GenJnlLineNextNo + 10000;
                             "Line No." := GenJnlLineNextNo;
                             "Posting Date" := PostingDate;
                             "Document Type" := "Document Type"::Payment;
                             "Document No." := PostedBillGr."No.";
                             IF "Dealing Type" = "Dealing Type"::Discount THEN BEGIN
                               BankAcc.TESTFIELD("Bank Acc. Posting Group");
                               BankAccPostingGr.GET(BankAcc."Bank Acc. Posting Group");
                               VALIDATE("Account Type","Account Type"::"G/L Account");
                               BankAccPostingGr.TESTFIELD("Liabs. for Disc. Bills Acc.");
                               VALIDATE("Account No.",BankAccPostingGr."Liabs. for Disc. Bills Acc.");
                             END ELSE BEGIN
                               VALIDATE("Account Type",GenJnlLine."Account Type"::"Bank Account");
                               VALIDATE("Account No.",BankAcc."No.");
                             END;
                             Description := COPYSTR(STRSUBSTNO(Text1100009,PostedBillGr."No."),1,MAXSTRLEN(Description));
                             VALIDATE("Currency Code",PostedBillGr."Currency Code");
                             VALIDATE(Amount,GroupAmount);
                             "Source Code" := SourceCode;
                             "System-Created Entry" := TRUE;
                             "Shortcut Dimension 1 Code" := CustLedgEntry."Global Dimension 1 Code";
                             "Shortcut Dimension 2 Code" := CustLedgEntry."Global Dimension 2 Code";
                             INSERT;
                             CarteraDimMgt.CustInsertJnlLinDimension(GenJnlLine,CustLedgEntry2,TRUE);
                             SumLCYAmt := SumLCYAmt + "Amount (LCY)";
                           END;
                         END ELSE
                           FactBankAccounting;

                         IF PostedBillGr."Currency Code" <> '' THEN BEGIN
                           IF SumLCYAmt <> 0 THEN BEGIN
                             Currency.SETFILTER(Code,PostedBillGr."Currency Code");
                             Currency.FIND('-');
                             IF SumLCYAmt > 0 THEN BEGIN
                               Currency.TESTFIELD("Residual Gains Account");
                               Acct := Currency."Residual Gains Account";
                             END ELSE BEGIN
                               Currency.TESTFIELD("Residual Losses Account");
                               Acct := Currency."Residual Losses Account";
                             END;
                             GenJnlLineNextNo := GenJnlLineNextNo + 10000;
                             WITH GenJnlLine DO BEGIN
                               CLEAR(GenJnlLine);
                               INIT;
                               "Line No." := GenJnlLineNextNo;
                               "Posting Date" := PostingDate;
                               "Document Type" := "Document Type"::Payment;
                               "Document No." := PostedBillGr."No.";
                               VALIDATE("Account Type","Account Type"::"G/L Account");
                               VALIDATE("Account No.",Acct);
                               Description := Text1100010;
                               VALIDATE("Currency Code",'');
                               VALIDATE(Amount,-SumLCYAmt);
                               "Source Code" := SourceCode;
                               "System-Created Entry" := TRUE;
                               "Shortcut Dimension 1 Code" := BankAcc."Global Dimension 1 Code";
                               "Shortcut Dimension 2 Code" := BankAcc."Global Dimension 2 Code";
                               INSERT;
                               CarteraDimMgt.CustInsertJnlLinDimension(GenJnlLine,CustLedgEntry2,TRUE);
                             END;
                           END;
                         END;
                         IF GenJnlLine.FIND('-') THEN
                           REPEAT
                             GenJnlLine2 := GenJnlLine;
                             GenJnlPostLine.SetFromSettlement(TRUE);
                             JnlLineDim.SETRANGE("Table ID",DATABASE::"Gen. Journal Line");
                             JnlLineDim.SETRANGE("Journal Template Name",GenJnlLine2."Journal Template Name");
                             JnlLineDim.SETRANGE("Journal Batch Name",GenJnlLine2."Journal Batch Name");
                             JnlLineDim.SETRANGE("Journal Line No.",GenJnlLine2."Line No.");
                             GenJnlPostLine.RunWithCheck(GenJnlLine2,JnlLineDim);
                           UNTIL GenJnlLine.NEXT = 0;
                         GenJnlLine.DELETEALL;

                         DocPost.CloseBillGroupIfEmpty(PostedBillGr,PostingDate);

                         IF ExistVATEntry THEN BEGIN
                           GLReg.FIND('+');
                           GLReg."From VAT Entry No." := FirstVATEntryNo;
                           GLReg."To VAT Entry No." := LastVATEntryNo;
                           GLReg.MODIFY;
                         END;
                       END;

        DataItemLink=Bill Gr./Pmt. Order No.=FIELD(No.);
      }
      SECTIONS
      {
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=5060;
      Height=990;
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 5   ;TextBox      ;3410 ;0    ;1650 ;440  ;CaptionML=[ENU=Posting Date;
                                                              ESP=Fecha registro];
                                                   NotBlank=Yes;
                                                   SourceExpr=PostingDate }
      { 6   ;CheckBox     ;3410 ;550  ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Due bills only;
                                                              ESP=S¢lo docs. vencidos];
                                                   SourceExpr=DueOnly }
      { 13  ;Label        ;0    ;0    ;3300 ;440  ;ParentControl=5 }
      { 7   ;Label        ;0    ;550  ;3300 ;440  ;ParentControl=6 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones] }

      { 5   ;2   ;Field     ;
                  CaptionML=[ENU=Posting Date;
                             ESP=Fecha registro];
                  NotBlank=Yes;
                  SourceExpr=PostingDate }

      { 6   ;2   ;Field     ;
                  CaptionML=[ENU=Due bills only;
                             ESP=S¢lo docs. vencidos];
                  SourceExpr=DueOnly }

    }
  }
  CODE
  {
    VAR
      Text1100000@1000 : TextConst 'ENU=Settling           @1@@@@@@@@@@@@@@@@@@@@@@@\\;ESP=Liquidando         @1@@@@@@@@@@@@@@@@@@@@@@@\\';
      Text1100001@1001 : TextConst 'ENU=Bill Groups        #2######  @3@@@@@@@@@@@@@\;ESP=Remesas            #2######  @3@@@@@@@@@@@@@\';
      Text1100002@1002 : TextConst 'ENU=Receiv. Documents  #4######;ESP=Docs. a cobrar     #4######';
      Text1100003@1003 : TextConst 'ENU=%1 Documents in %2 Bill Groups totaling %3 (LCY) have been settled.;ESP=Se han liquidado %1 documento/s en la remesa/s %2 por un importe de %3 (DL).';
      Text1100004@1004 : TextConst 'ENU=Receivable bill settlement %1/%2;ESP=Liquidaci¢n efecto a cobrar %1/%2';
      Text1100005@1005 : TextConst 'ENU=Receivable document settlement %1;ESP=Liquidaci¢n efecto a cobrar %1';
      Text1100009@1009 : TextConst 'ENU=Bill Group settlement %1;ESP=Liquidaci¢n remesa %1';
      Text1100010@1010 : TextConst 'ENU=Residual adjust generated by rounding Amount;ESP=Ajuste por redondeo';
      Text1100011@1011 : TextConst 'ENU=Bill Group settlement %1 Customer No. %2;ESP=Liquida remesa %1 cliente n§ %2';
      Text1100012@1012 : TextConst 'ENU=Receivable document settlement %1/%2;ESP=Liq. documento a cobrar %1/%2';
      SourceCodeSetup@1100000 : Record 242;
      GenJnlLine@1100001 : TEMPORARY Record 81;
      GenJnlLine2@1100002 : TEMPORARY Record 81;
      CustLedgEntry@1100003 : Record 21;
      BankAccPostingGr@1100004 : Record 277;
      BankAcc@1100005 : Record 270;
      CurrExchRate@1100006 : Record 330;
      Currency@1100007 : Record 4;
      GLSetup@1100008 : Record 98;
      GLReg@1100009 : Record 45;
      VATPostingSetup@1100010 : Record 325;
      DocPost@1100011 : Codeunit 7000006;
      CarteraManagement@1100012 : Codeunit 7000000;
      GenJnlPostLine@1100013 : Codeunit 12;
      CarteraDimMgt@1100035 : Codeunit 7000011;
      Window@1100014 : Dialog;
      PostingDate@1100015 : Date;
      DueOnly@1100016 : Boolean;
      Delay@1100017 : Decimal;
      SourceCode@1100018 : Code[10];
      Acct@1100019 : Code[20];
      DocCount@1100020 : Integer;
      TotalDocCount@1100021 : Integer;
      GroupAmount@1100022 : Decimal;
      GroupAmountLCY@1100023 : Decimal;
      GenJnlLineNextNo@1100024 : Integer;
      SumLCYAmt@1100025 : Decimal;
      TotalDisctdAmt@1100026 : Decimal;
      BillGrCount@1100027 : Integer;
      TotalBillGr@1100028 : Integer;
      TotalDoc@1100029 : Integer;
      ExistVATEntry@1100030 : Boolean;
      LastVATEntryNo@1100031 : Integer;
      FirstVATEntryNo@1100032 : Integer;
      IsRedrawn@1100033 : Boolean;
      BGPOPostBuffer@1100034 : TEMPORARY Record 7000012;
      NoRealVATBuffer@1100037 : TEMPORARY Record 7000012;
      ExistsNoRealVAT@1100036 : Boolean;

    PROCEDURE DiscFactLiabs@1(PostedDoc2@1100000 : Record 7000003);
    VAR
      Currency2@1100001 : Record 4;
      DisctedAmt@1100002 : Decimal;
      RoundingPrec@1100003 : Decimal;
    BEGIN
      DisctedAmt := 0;
      GenJnlLineNextNo := GenJnlLineNextNo + 10000;

      IF PostedDoc2."Currency Code" <> '' THEN BEGIN
        Currency2.GET(PostedDoc2."Currency Code");
        RoundingPrec := Currency2."Amount Rounding Precision";
      END ELSE
        RoundingPrec := GLSetup."Amount Rounding Precision";

      DisctedAmt := ROUND(DocPost.FindDisctdAmt(
        PostedDoc2."Remaining Amount",
        PostedDoc2."Account No.",
        PostedDoc2."Bank Account No."),RoundingPrec);

      TotalDisctdAmt := TotalDisctdAmt + DisctedAmt;

        WITH GenJnlLine DO BEGIN
          CLEAR(GenJnlLine);
          INIT;
          "Line No." := GenJnlLineNextNo;
          "Posting Date" := PostingDate;
          "Document Type" := "Document Type"::Payment;
          "Document No." := PostedBillGr."No.";
          BankAcc.TESTFIELD("Bank Acc. Posting Group");
          BankAccPostingGr.GET(BankAcc."Bank Acc. Posting Group");
          VALIDATE("Account Type","Account Type"::"G/L Account");
          BankAccPostingGr.TESTFIELD("Liabs. for Factoring Acc.");
          VALIDATE("Account No.",BankAccPostingGr."Liabs. for Factoring Acc.");
          Description := COPYSTR(
            STRSUBSTNO(Text1100011,
            PostedBillGr."No.",
            PostedDoc2."Account No."),1,MAXSTRLEN(Description));
          VALIDATE("Currency Code",PostedBillGr."Currency Code");
          VALIDATE(Amount,DisctedAmt);
          "Source Code" := SourceCode;
          "System-Created Entry" := TRUE;
          "Shortcut Dimension 1 Code" := PostedDoc2."Global Dimension 1 Code";
          "Shortcut Dimension 2 Code" := PostedDoc2."Global Dimension 2 Code";
          BGPOPostBuffer."Gain - Loss Amount (LCY)" := BGPOPostBuffer."Gain - Loss Amount (LCY)" + Amount;
          BGPOPostBuffer.MODIFY;
          INSERT;
          CarteraDimMgt.CustInsertJnlLinDimension(GenJnlLine,CustLedgEntry,TRUE);
          SumLCYAmt := SumLCYAmt + "Amount (LCY)";
        END;
    END;

    PROCEDURE FactBankAccounting@11();
    VAR
      CustLedgEntry2@1100000 : Record 21;
    BEGIN
      CASE TRUE OF
        PostedDoc."Dealing Type" = PostedDoc."Dealing Type"::Discount:
          BEGIN

            WITH GenJnlLine DO BEGIN
              CustLedgEntry2.GET(BGPOPostBuffer."Entry No.");
              GenJnlLineNextNo := GenJnlLineNextNo + 10000;
              CLEAR(GenJnlLine);
              INIT;
              "Line No." := GenJnlLineNextNo;
              "Posting Date" := PostingDate;
              "Document Type" := "Document Type"::Payment;
              "Document No." := PostedBillGr."No.";
              BankAcc.TESTFIELD("Bank Acc. Posting Group");
              BankAccPostingGr.GET(BankAcc."Bank Acc. Posting Group");
              VALIDATE("Account Type",GenJnlLine."Account Type"::"Bank Account");
              VALIDATE("Account No.",BankAcc."No.");
              Description := COPYSTR(STRSUBSTNO(Text1100009,PostedBillGr."No."),1,MAXSTRLEN(Description));
              VALIDATE("Currency Code",PostedBillGr."Currency Code");
              VALIDATE(Amount,GroupAmount - TotalDisctdAmt);
              "Source Code" := SourceCode;
              "System-Created Entry" := TRUE;
              "Shortcut Dimension 1 Code" := CustLedgEntry2."Global Dimension 1 Code";
              "Shortcut Dimension 2 Code" := CustLedgEntry2."Global Dimension 2 Code";
              INSERT;
              CarteraDimMgt.CustInsertJnlLinDimension(GenJnlLine,CustLedgEntry2,TRUE);
              SumLCYAmt := SumLCYAmt + "Amount (LCY)";
            END;
          END;
        ELSE
          BEGIN

            WITH GenJnlLine DO BEGIN
              CustLedgEntry2.GET(BGPOPostBuffer."Entry No.");
              GenJnlLineNextNo := GenJnlLineNextNo + 10000;
              CLEAR(GenJnlLine);
              INIT;
              "Line No." := GenJnlLineNextNo;
              "Posting Date" := PostingDate;
              "Document Type" := "Document Type"::Payment;
              "Document No." := PostedBillGr."No.";
              BankAcc.TESTFIELD("Bank Acc. Posting Group");
              BankAccPostingGr.GET(BankAcc."Bank Acc. Posting Group");
              VALIDATE("Account Type",GenJnlLine."Account Type"::"Bank Account");
              VALIDATE("Account No.",BankAcc."No.");
              Description := COPYSTR(STRSUBSTNO(Text1100009,PostedBillGr."No."),1,MAXSTRLEN(Description));
              VALIDATE("Currency Code",PostedBillGr."Currency Code");
              VALIDATE(Amount,GroupAmount);
              "Source Code" := SourceCode;
              "System-Created Entry" := TRUE;
              "Shortcut Dimension 1 Code" := CustLedgEntry2."Global Dimension 1 Code";
              "Shortcut Dimension 2 Code" := CustLedgEntry2."Global Dimension 2 Code";
              INSERT;
              CarteraDimMgt.CustInsertJnlLinDimension(GenJnlLine,CustLedgEntry2,TRUE);
              SumLCYAmt := SumLCYAmt + "Amount (LCY)";
            END;
          END;
      END;
    END;

    LOCAL PROCEDURE InsertGenJournalLine@4(AccType@1100000 : Integer;AccNo@1100001 : Code[20];Amount2@1100002 : Decimal);
    BEGIN
      GenJnlLineNextNo :=  GenJnlLineNextNo + 10000;

      WITH GenJnlLine DO BEGIN
        CLEAR(GenJnlLine);
        INIT;
        "Line No." := GenJnlLineNextNo;
        "Posting Date" := PostingDate;
        "Document Type" := "Document Type"::Payment;
        "Document No." := PostedBillGr."No.";
        "Account Type" := AccType;
        "Account No." := AccNo;
        Description := COPYSTR(
          STRSUBSTNO(Text1100012,PostedDoc."Document No.",PostedDoc."No."),
          1,MAXSTRLEN(Description));
        VALIDATE("Currency Code",PostedDoc."Currency Code");
        VALIDATE(Amount,-Amount2);
        "Applies-to Doc. Type" := CustLedgEntry."Document Type";
        "Applies-to Doc. No." := '';
        "Applies-to Bill No." := CustLedgEntry."Bill No.";
        "Source Code" := SourceCode;
        "System-Created Entry" := TRUE;
        "Shortcut Dimension 1 Code" := CustLedgEntry."Global Dimension 1 Code";
        "Shortcut Dimension 2 Code" := CustLedgEntry."Global Dimension 2 Code";
        INSERT;
        CarteraDimMgt.CustInsertJnlLinDimension(GenJnlLine,CustLedgEntry,TRUE);
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
