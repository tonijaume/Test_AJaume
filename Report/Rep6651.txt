OBJECT Report 6651 Delete Invd Sales Ret. Orders
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Delete Invd Sales Ret. Orders;
               ESP=Borrar ped. dev. ventas fact.];
    ProcessingOnly=Yes;
  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table36;
        DataItemTableView=SORTING(Document Type,No.)
                          WHERE(Document Type=CONST(Return Order));
        ReqFilterHeadingML=[ENU=Sales Return Order;
                            ESP=Devoluci¢n venta];
        OnPreDataItem=BEGIN
                        Window.OPEN(Text000);
                      END;

        OnAfterGetRecord=BEGIN
                           Window.UPDATE(1,"No.");

                           AllLinesDeleted := TRUE;
                           DocDim.RESET;
                           DocDim.SETRANGE("Document Type","Document Type");
                           DocDim.SETRANGE("Document No.","No.");
                           ItemChargeAssgntSales.RESET;
                           ItemChargeAssgntSales.SETRANGE("Document Type","Document Type");
                           ItemChargeAssgntSales.SETRANGE("Document No.","No.");
                           SalesOrderLine.RESET;
                           SalesOrderLine.SETRANGE("Document Type","Document Type");
                           SalesOrderLine.SETRANGE("Document No.","No.");
                           SalesOrderLine.SETFILTER("Quantity Invoiced",'<>0');
                           IF SalesOrderLine.FIND('-') THEN BEGIN
                             SalesOrderLine.SETRANGE("Quantity Invoiced");
                             SalesOrderLine.SETFILTER("Outstanding Quantity",'<>0');
                             IF NOT SalesOrderLine.FIND('-') THEN BEGIN
                               SalesOrderLine.SETRANGE("Outstanding Quantity");
                               SalesOrderLine.SETFILTER("Return Qty. Rcd. Not Invd.",'<>0');
                               IF NOT SalesOrderLine.FIND('-') THEN BEGIN
                                 SalesOrderLine.LOCKTABLE;
                                 IF NOT SalesOrderLine.FIND('-') THEN BEGIN
                                   SalesOrderLine.SETRANGE("Return Qty. Rcd. Not Invd.");
                                   DocDim.SETRANGE("Table ID",DATABASE::"Sales Line");
                                   IF SalesOrderLine.FIND('-') THEN
                                     REPEAT
                                       SalesOrderLine.CALCFIELDS("Qty. Assigned");
                                       IF ((SalesOrderLine."Qty. Assigned" = SalesOrderLine."Quantity Invoiced") OR
                                          (SalesOrderLine.Type <> SalesOrderLine.Type::"Charge (Item)")) AND
                                          (SalesOrderLine."Qty. Assigned" <> 0)
                                       THEN BEGIN
                                         DocDim.SETRANGE("Line No.",SalesOrderLine."Line No.");
                                         DocDim.DELETEALL;
                                         IF SalesOrderLine.Type = SalesOrderLine.Type::"Charge (Item)" THEN BEGIN
                                           ItemChargeAssgntSales.SETRANGE("Document Line No.",SalesOrderLine."Line No.");
                                           ItemChargeAssgntSales.DELETEALL;
                                         END;
                                         SalesOrderLine.DELETE;
                                       END ELSE
                                         AllLinesDeleted := FALSE;

                                     UNTIL SalesOrderLine.NEXT = 0;

                                   IF AllLinesDeleted THEN BEGIN
                                     SalesPost.DeleteHeader(
                                       "Sales Header",SalesShptHeader,SalesInvHeader,SalesCrMemoHeader,ReturnRcptHeader,
                                       PrepmtSalesInvHeader,PrepmtSalesCrMemoHeader);
                                     ReserveSalesLine.DeleteInvoiceSpecFromHeader("Sales Header");

                                     SalesCommentLine.SETRANGE("Document Type","Document Type");
                                     SalesCommentLine.SETRANGE("No.","No.");
                                     SalesCommentLine.DELETEALL;

                                     DocDim.SETRANGE("Table ID",DATABASE::"Sales Header");
                                     DocDim.SETRANGE("Line No.",0);
                                     DocDim.DELETEALL;

                                     WhseRequest.SETRANGE("Source Type",DATABASE::"Sales Line");
                                     WhseRequest.SETRANGE("Source Subtype","Document Type");
                                     WhseRequest.SETRANGE("Source No.","No.");
                                     WhseRequest.DELETEALL(TRUE);

                                     DELETE;
                                   END;
                                   COMMIT;
                                 END;
                               END;
                             END;
                           END;
                         END;

        ReqFilterFields=No.,Sell-to Customer No.,Bill-to Customer No.;
      }
      SECTIONS
      {
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Processing sales return orders #1##########;ESP=Procesando devoluciones ventas  #1##########';
      ReserveSalesLine@1013 : Codeunit 99000832;
      SalesOrderLine@1001 : Record 37;
      SalesShptHeader@1011 : Record 110;
      SalesInvHeader@1010 : Record 112;
      SalesCrMemoHeader@1009 : Record 114;
      ReturnRcptHeader@1005 : Record 6660;
      PrepmtSalesInvHeader@1015 : Record 112;
      PrepmtSalesCrMemoHeader@1014 : Record 114;
      SalesCommentLine@1002 : Record 44;
      DocDim@1003 : Record 357;
      ItemChargeAssgntSales@1007 : Record 5809;
      WhseRequest@1008 : Record 5765;
      SalesPost@1012 : Codeunit 80;
      Window@1004 : Dialog;
      AllLinesDeleted@1006 : Boolean;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
