OBJECT Report 789 Delete Phys. Inventory Ledger
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    Permissions=TableData 87=rimd,
                TableData 281=rimd,
                TableData 5815=rimd;
    CaptionML=[ENU=Delete Phys. Inventory Ledger;
               ESP=Eliminar movs. inv. f¡sico];
    ProcessingOnly=Yes;
  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table281;
        OnPreDataItem=BEGIN
                        IF NOT
                           CONFIRM(
                             Text000 +
                             Text001 +
                             Text002,FALSE)
                        THEN
                          CurrReport.BREAK;

                        IF EntrdDateComprReg."Ending Date" = 0D THEN
                          ERROR(STRSUBSTNO(Text003,EntrdDateComprReg.FIELDCAPTION("Ending Date")));

                        Window.OPEN(
                          Text004 +
                          Text005 +
                          Text006 +
                          Text007,
                          "Phys. Inventory Ledger Entry"."Item No.",
                          "Phys. Inventory Ledger Entry"."Posting Date",
                          NoOfDeleted);

                        IF PhysInvtLedgEntry2.FIND('+') THEN;
                        LastEntryNo := PhysInvtLedgEntry2."Entry No.";
                        SETRANGE("Entry No.",0,LastEntryNo);
                        SETRANGE("Posting Date",EntrdDateComprReg."Starting Date",EntrdDateComprReg."Ending Date");
                      END;

        OnAfterGetRecord=VAR
                           InvtPeriodEntry@1000 : Record 5815;
                           InvtPeriod@1001 : Record 5814;
                         BEGIN
                           IF NOT InvtPeriod.IsValidDate("Posting Date") THEN
                             InvtPeriod.ShowError("Posting Date");

                           PhysInvtLedgEntry2 := "Phys. Inventory Ledger Entry";
                           WITH PhysInvtLedgEntry2 DO BEGIN
                             SETCURRENTKEY("Item No.","Variant Code","Posting Date");
                             COPYFILTERS("Phys. Inventory Ledger Entry");
                             SETRANGE("Item No.","Item No.");
                             SETFILTER("Posting Date",DateComprMgt.GetDateFilter("Posting Date",EntrdDateComprReg,TRUE));
                             SETRANGE("Inventory Posting Group","Inventory Posting Group");
                             SETRANGE("Entry Type","Entry Type");

                             Window.UPDATE(1);
                             Window.UPDATE(2);

                             LedgEntryDim.SETRANGE("Table ID",DATABASE::"Phys. Inventory Ledger Entry");
                             LedgEntryDim.SETRANGE("Entry No.","Phys. Inventory Ledger Entry"."Entry No.");
                             LedgEntryDim.DELETEALL;

                             DELETE;

                             InvtPeriodEntry.RemoveItemRegNo("Entry No.",TRUE);

                             NoOfDeleted := NoOfDeleted + 1;
                             Window.UPDATE(3);
                           END;

                           IF NoOfDeleted >= LastNoOfDeleted + 10 THEN BEGIN
                             LastNoOfDeleted := NoOfDeleted;
                             COMMIT;
                           END;
                         END;

        ReqFilterFields=Item No.,Inventory Posting Group;
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=5060;
      Height=990;
      SaveValues=Yes;
      OnOpenForm=BEGIN
                   IF EntrdDateComprReg."Ending Date" = 0D THEN
                     EntrdDateComprReg."Ending Date" := TODAY;
                 END;

    }
    CONTROLS
    {
      { 1   ;TextBox      ;3410 ;0    ;1650 ;440  ;CaptionML=[ENU=Starting Date;
                                                              ESP=Fecha inicial];
                                                   SourceExpr=EntrdDateComprReg."Starting Date" }
      { 21  ;Label        ;0    ;0    ;3300 ;440  ;ParentControl=1 }
      { 2   ;TextBox      ;3410 ;550  ;1650 ;440  ;CaptionML=[ENU=Ending Date;
                                                              ESP=Fecha final];
                                                   SourceExpr=EntrdDateComprReg."Ending Date" }
      { 22  ;Label        ;0    ;550  ;3300 ;440  ;ParentControl=2 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF EntrdDateComprReg."Ending Date" = 0D THEN
                     EntrdDateComprReg."Ending Date" := TODAY;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones] }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Starting Date;
                             ESP=Fecha inicial];
                  SourceExpr=EntrdDateComprReg."Starting Date" }

      { 2   ;2   ;Field     ;
                  CaptionML=[ENU=Ending Date;
                             ESP=Fecha final];
                  SourceExpr=EntrdDateComprReg."Ending Date" }

    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU="This batch job deletes entries. ";ESP="Este proceso borra movimientos. "';
      Text001@1001 : TextConst 'ENU=Therefore, it is important that you make a backup of the database before you run the batch job.\\;ESP=Es muy importante realizar una copia de seguridad de la base de datos antes de ejecutar este proceso.\\';
      Text002@1002 : TextConst 'ENU=Do you want to delete the entries?;ESP=¨Confirma que desea eliminar los movimientos?';
      Text003@1003 : TextConst 'ENU=%1 must be specified.;ESP=Se debe indicar %1.';
      Text004@1004 : TextConst 'ENU=Deleting phys. inventory ledger entries...\\;ESP=Borrando movs. inventario f¡sico...\\';
      Text005@1005 : TextConst 'ENU=Item No.             #1##########\;ESP=N§ producto          #1##########\';
      Text006@1006 : TextConst 'ENU=Date                 #2######\\;ESP=Fecha                #2######\\';
      Text007@1007 : TextConst 'ENU=No. of entries del.  #3######;ESP=N§ movs. borrados    #3######';
      EntrdDateComprReg@1008 : Record 87;
      PhysInvtLedgEntry2@1009 : Record 281;
      LedgEntryDim@1015 : Record 355;
      DateComprMgt@1010 : Codeunit 356;
      Window@1011 : Dialog;
      LastEntryNo@1012 : Integer;
      NoOfDeleted@1013 : Integer;
      LastNoOfDeleted@1014 : Integer;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
