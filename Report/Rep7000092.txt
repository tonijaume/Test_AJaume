OBJECT Report 7000092 Bill group - Export N19
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVES6.00.01;
  }
  PROPERTIES
  {
    Permissions=TableData 7000005=m;
    CaptionML=[ENU=Bill group - Export N19;
               ESP=Remesa - Exportar N19];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   CompanyInfo.GET();
                   VATRegNo := COPYSTR(DELCHR(CompanyInfo."VAT Registration No.",'=',' .-/'),1,9);
                 END;

    OnPreReport=BEGIN
                  // IF ChargDate = 0D THEN
                  //     ChargDate:=;

                  IF WithExpenses THEN
                    Expenses := '1'
                  ELSE
                    Expenses := '0';

                  IF PrintedDocs THEN
                    PrintedDocs2 := '0'
                  ELSE
                    PrintedDocs2 := '1';

                  // CLEAR(FILE);
                  OutFile.TEXTMODE := TRUE;
                  OutFile.WRITEMODE := TRUE;
                  IF ISSERVICETIER THEN BEGIN
                    ExternalFile := RBMgt.EnvironFileName('','');
                    ToFile := Text10702;
                  END;
                  OutFile.CREATE(ExternalFile);
                END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table7000005;
        DataItemTableView=SORTING(No.)
                          WHERE(Factoring=CONST(" "));
        OnPreDataItem=BEGIN
                        CompanyInfo.GET;

                        "Bill Group".FIND('-');
                        BankAcc.GET("Bill Group"."Bank Account No.");

                        IF BankSuffix = '' THEN
                          ERROR(Text1100000);

                        IF BankSuffixBankAcc = '' THEN BEGIN
                          Suffix.SETRANGE (Suffix,BankSuffix);
                          IF Suffix.FIND ('-') THEN
                            BankSuffixBankAcc := Suffix."Bank Acc. Code";
                        END;

                        IF NOT DocMisc.CheckBankSuffix(BankSuffixBankAcc,"Bill Group"."Bank Account No.") THEN
                          IF NOT CONFIRM(Text1100001 +
                                         Text1100002,
                                         FALSE,
                                         "Bill Group".FIELDCAPTION("Bank Account No."),
                                         "Bill Group".TABLECAPTION) THEN
                             ERROR(Text1100003);


                        IsEuro := DocMisc.GetRegisterCode("Currency Code",RegisterCode,RegisterString);

                        RegisterCode2 := RegisterCode + 1;
                        DLDACurrency;

                        CLEAR(OutText);
                        OutText :=
                          RegisterString + '80' + FORMAT(CompanyInfo."VAT Registration No.",9) + PADSTR(BankSuffix,3,' ') +
                           FORMAT(WORKDATE,6,5) + PADSTR('',6,' ') +
                          PADSTR(CompanyInfo.Name,40,' ') + PADSTR('',20,' ') + CONVERTSTR(BankAcc."CCC Bank No.",' ','0') +
                          CONVERTSTR(BankAcc."CCC Bank Branch No.",' ','0') + PADSTR('',12,' ') + PADSTR('',40,' ') + PADSTR('',14,' ');
                        OutFile.WRITE(OutText);
                      END;

        OnAfterGetRecord=BEGIN
                           BillGrNo := COPYSTR("Bill Group"."No.",7,3);

                           CCCBankNo := BankAcc."CCC Bank No.";
                           CCCBankBranchNo := BankAcc."CCC Bank Branch No.";
                           CCCControlDigits := BankAcc."CCC Control Digits";
                           CCCAccNo := BankAcc."CCC Bank Account No.";

                           IF (CCCBankNo = '') OR (CCCBankBranchNo = '') OR
                              (CCCControlDigits = '') OR (CCCAccNo = '')
                           THEN
                             ERROR(Text1100005,BankAcc."No.");

                           RegisterCode2 := RegisterCode + 3;
                           DLDACurrency;

                           CLEAR(OutText);
                           IF ChargDate <> 0D THEN BEGIN
                               OutText :=
                                 RegisterString + '80' + FORMAT(CompanyInfo."VAT Registration No.",9) + PADSTR(BankSuffix,3,' ') +
                                 FORMAT(WORKDATE,6,5) +  FORMAT(ChargDate,6,5) + PADSTR(CompanyInfo.Name,40,' ') +
                                 CONVERTSTR(CCCBankNo,' ','0') + CONVERTSTR(CCCBankBranchNo,' ','0') +
                                 CONVERTSTR(CCCControlDigits,' ','0') + CONVERTSTR(CCCAccNo,' ','0') +
                                 PADSTR('',8,' ') + '01' + PADSTR('',10,' ') + PADSTR('',40,' ') + PADSTR('',14,' ') ;
                           END ELSE BEGIN
                               DateMaxEfe:=DateMax();
                               OutText :=
                                 RegisterString + '80' + FORMAT(CompanyInfo."VAT Registration No.",9) + PADSTR(BankSuffix,3,' ') +
                                 FORMAT(WORKDATE,6,5) + FORMAT(DateMaxEfe,6,5) + PADSTR(CompanyInfo.Name,40,' ') +
                                 CONVERTSTR(CCCBankNo,' ','0') + CONVERTSTR(CCCBankBranchNo,' ','0') +
                                 CONVERTSTR(CCCControlDigits,' ','0') + CONVERTSTR(CCCAccNo,' ','0') +
                                 PADSTR('',8,' ') + '01' + PADSTR('',10,' ') + PADSTR('',40,' ') + PADSTR('',14,' ') ;
                           END;
                           OutFile.WRITE(OutText);

                           TotalPrintedCust := TotalPrintedCust + 1;
                         END;

        OnPostDataItem=BEGIN
                         "No. Printed" := "No. Printed" + 1;
                         TotalPrinted := TotalPrinted + 2;

                         RegisterCode2 := RegisterCode + 9;
                         DLDACurrency;

                         IF IsEuro THEN
                           GeneralTotal := EuroAmount(TotalAmount)
                         ELSE
                           GeneralTotal := CONVERTSTR(FORMAT(TotalAmount,10,Text1100006),' ','0');

                         OutText :=
                           RegisterString + '80' + FORMAT(  CompanyInfo."VAT Registration No.",9) + PADSTR(BankSuffix,3,' ') + PADSTR('',12,' ') +
                           PADSTR('',40,' ') + '0001' + PADSTR('',16,' ') + GeneralTotal + PADSTR('',6,' ') +
                           CONVERTSTR(FORMAT(TotalDocs,10,Text1100006),' ','0') + CONVERTSTR(FORMAT(TotalPrinted,10,Text1100006),' ','0') +
                           PADSTR('',20,' ') + PADSTR('',18,' ');
                         OutFile.WRITE(OutText);
                         OutFile.CLOSE();
                         IF ISSERVICETIER THEN
                           DOWNLOAD(ExternalFile,'','C:',Text10701,ToFile);
                       END;

        ReqFilterFields=No.;
      }
      SECTIONS
      {
      }
       }
    { PROPERTIES
      {
        DataItemIndent=1;
        DataItemTable=Table7000002;
        DataItemTableView=SORTING(Type,Collection Agent,Bill Gr./Pmt. Order No.)
                          WHERE(Type=CONST(Receivable));
        OnAfterGetRecord=BEGIN
                           DocType := DocMisc.DocType("Cartera Doc."."Payment Method Code");

                           CustLedgEntry.GET("Cartera Doc."."Entry No.");

                           Customer.GET(CustLedgEntry."Customer No.");
                           IF NOT CustBankAcc.GET(CustLedgEntry."Customer No.","Cartera Doc."."Cust./Vendor Bank Acc. Code") THEN BEGIN
                             ERROR(Text1100007,
                                   CustBankAcc.TABLECAPTION,
                                   "Cartera Doc."."Cust./Vendor Bank Acc. Code",
                                   Customer.TABLECAPTION,
                                   CustLedgEntry."Customer No.");
                           END ELSE BEGIN
                             CustCCCBankCode := CustBankAcc."CCC Bank No.";
                             CustCCCBankBranchNo := CustBankAcc."CCC Bank Branch No.";
                             CustCCCControlDigits := CustBankAcc."CCC Control Digits";
                             CustCCCAccNo := CustBankAcc."CCC Bank Account No.";

                             IF CheckErrors THEN BEGIN
                               DocMisc.CheckControlDigit(CustCCCControlDigits,CustCCCBankCode,CustCCCAccNo,CustCCCBankBranchNo);
                               IF (CustBankAcc."CCC Control Digits" <> CustCCCControlDigits) AND
                                  (CustCCCControlDigits <> '**')
                               THEN BEGIN
                                 CustBankAcc."CCC Control Digits" := CustCCCControlDigits;
                                 CustBankAcc.MODIFY;
                               END;
                             END;
                           END;
                           IF (CustCCCBankCode = '') OR (CustCCCBankBranchNo = '') OR
                              (CustCCCControlDigits = '') OR (CustCCCAccNo = '')
                           THEN BEGIN
                             CustCCCBankCode := '0000';
                             CustCCCBankBranchNo := '0000';
                             CustCCCControlDigits := '00';
                             CustCCCAccNo := '0000000000';
                           END;

                           CustAddress[1] := COPYSTR(Customer.Name,1,30);
                           CustAddress[2] := COPYSTR(Customer.Address,1,30);
                           CustAddress[3] := COPYSTR(Customer.City,1,30);
                           CustAddress[4] := COPYSTR(Customer."Post Code",1,30);
                           CustNoNIF:=Customer."VAT Registration No.";
                           AdditionalInfo := Text1100008 + "Cartera Doc."."Document No." + '/' + "Cartera Doc."."No.";
                           AdditionalInfo2 := "Cartera Doc."."Document No." + '/' + "Cartera Doc."."No.";

                           IF Accepted = Accepted::Yes THEN
                             IsAccepted := '1'
                           ELSE
                             IsAccepted := '0';

                           TotalDocs := TotalDocs + 1;
                           TotalDocsCust := TotalDocsCust + 1;
                           TotalPrintedCust := TotalPrintedCust + 1;
                           TotalPosted := TotalPosted + 3;

                           CASE TRUE OF
                             LCY = LCY::Euro:
                               IF IsEuro THEN
                                 DocAmount := EuroAmount("Remaining Amt. (LCY)")
                               ELSE
                                 DocAmount := CONVERTSTR(FORMAT("Cartera Doc."."Remaining Amount",10,Text1100009),' ','0');
                             LCY = LCY::Other:
                               IF IsEuro THEN
                                 DocAmount := EuroAmount("Remaining Amount")
                               ELSE
                                 DocAmount := CONVERTSTR(FORMAT("Cartera Doc."."Remaining Amount",10,Text1100009),' ','0');
                           END;

                           RegisterCode2 := RegisterCode + 6;
                           DLDACurrency;

                           CLEAR(OutText);
                           OutText :=
                             RegisterString + '80' + FORMAT(CompanyInfo."VAT Registration No.",9) + PADSTR(BankSuffix,3,' ') +
                             PADSTR(FORMAT(CustNoNIF),12,' ') + PADSTR(FORMAT(Customer.Name),40,' ') +
                             CONVERTSTR(PADSTR(CustCCCBankCode,4,' '),' ','0') + CONVERTSTR(PADSTR(CustCCCBankBranchNo,4,' '),' ','0') +
                             PADSTR(CustCCCControlDigits,2,' ') + CONVERTSTR(PADSTR(CustCCCAccNo,10,' '),' ','0') +
                             DocAmount + Text1100010 + PADSTR(AdditionalInfo2,10,' ') + PADSTR(AdditionalInfo,40,' ') + PADSTR('',8,' ');

                           OutFile.WRITE(OutText);
                         END;

        OnPostDataItem=BEGIN
                         CLEAR(OutText);
                         TotalPrintedCust := TotalPrintedCust + 1;

                         CASE TRUE OF
                           LCY = LCY::Euro:
                             IF IsEuro THEN BEGIN
                               "Bill Group".CALCFIELDS("Bill Group"."Amount (LCY)");
                               TotalAmount := "Bill Group"."Amount (LCY)" + TotalAmount;
                               BillGrAmount := EuroAmount("Bill Group"."Amount (LCY)")
                             END ELSE BEGIN
                               "Bill Group".CALCFIELDS("Bill Group".Amount);
                               TotalAmount := "Bill Group".Amount + TotalAmount;
                               BillGrAmount := CONVERTSTR(FORMAT("Bill Group".Amount,10,Text1100006),' ','0')
                             END;
                           LCY = LCY::Other:
                             IF IsEuro THEN BEGIN
                               "Bill Group".CALCFIELDS(Amount);
                               TotalAmount := "Bill Group".Amount + TotalAmount;
                               BillGrAmount := EuroAmount("Bill Group".Amount)
                             END ELSE BEGIN
                               "Bill Group".CALCFIELDS("Bill Group".Amount);
                               TotalAmount := "Bill Group".Amount + TotalAmount;
                               BillGrAmount := CONVERTSTR(FORMAT("Bill Group".Amount,10,Text1100006),' ','0')
                             END;
                         END;

                         RegisterCode2 := RegisterCode + 8;
                         DLDACurrency;

                         OutText :=
                           RegisterString + '80' + FORMAT(CompanyInfo."VAT Registration No.",9) + PADSTR(BankSuffix,3,' ') + PADSTR('',12,' ') +
                           PADSTR('',40,' ') + PADSTR('',20,' ') + BillGrAmount + PADSTR('',6,' ') +
                           CONVERTSTR(FORMAT(TotalDocsCust,10,Text1100006),' ','0') + CONVERTSTR(FORMAT(TotalPrintedCust,10,Text1100006),' ','0') +
                           PADSTR('',20,' ') + PADSTR('',18,' ');
                         OutFile.WRITE(OutText);

                         TotalDocsCust := 0;
                          TotalPrinted := TotalPrinted + TotalPrintedCust;
                         TotalPrintedCust := 0;
                       END;

        TotalFields=Remaining Amt. (LCY);
        DataItemLink=Bill Gr./Pmt. Order No.=FIELD(No.);
      }
      SECTIONS
      {
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=8910;
      Height=2420;
      SaveValues=Yes;
      OnOpenForm=BEGIN
                   IF ExternalFile = '' THEN
                     ExternalFile := 'C:\' + Text10702;
                   RequestOptionsForm.FileNameControl.VISIBLE := NOT ISSERVICETIER;
                 END;

    }
    CONTROLS
    {
      { 1   ;TextBox      ;3410 ;0    ;5500 ;440  ;Name=FileNameControl;
                                                   CaptionML=[ENU=External file;
                                                              ESP=Archivo externo];
                                                   SourceExpr=ExternalFile;
                                                   OnAssistEdit=BEGIN
                                                                  ExternalFile := CommonDialogMgt.OpenFile(Text10700,ExternalFile,4,Text10701,1);
                                                                END;
                                                                 }
      { 2   ;Label        ;0    ;0    ;3300 ;440  ;ParentControl=1 }
      { 5   ;CheckBox     ;3520 ;1320 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Check Errors;
                                                              ESP=Test exportaci¢n];
                                                   SourceExpr=CheckErrors }
      { 6   ;Label        ;0    ;1320 ;3300 ;440  ;ParentControl=5 }
      { 7   ;Label        ;0    ;660  ;3300 ;440  ;Name=Charging Date;
                                                   ParentControl=9 }
      { 9   ;TextBox      ;3410 ;660  ;1650 ;440  ;CaptionML=[ENU=Charging Date;
                                                              ESP=Fecha cargo];
                                                   SourceExpr=ChargDate }
      { 3   ;TextBox      ;3410 ;1980 ;1650 ;440  ;CaptionML=[ENU=Bank Suffix;
                                                              ESP=Sufijo banco];
                                                   SourceExpr=BankSuffix;
                                                   TableRelation=Suffix.Suffix;
                                                   OnLookup=VAR
                                                              Suffix@1100002 : Record 7000024;
                                                              Suffixes@1100003 : Form 7000073;
                                                            BEGIN
                                                              Suffixes.SETTABLEVIEW(Suffix);
                                                              Suffixes.SETRECORD(Suffix);
                                                              Suffixes.LOOKUPMODE(TRUE);
                                                              Suffixes.EDITABLE(FALSE);
                                                              IF Suffixes.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                Suffixes.GETRECORD(Suffix);
                                                                BankSuffixBankAcc := Suffix."Bank Acc. Code";
                                                                BankSuffix := Suffix.Suffix;
                                                              END;
                                                            END;
                                                             }
      { 4   ;Label        ;0    ;1980 ;3300 ;440  ;ParentControl=3 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnInit=BEGIN
               FileNameControlVisible := TRUE;
             END;

      OnOpenPage=BEGIN
                   IF ExternalFile = '' THEN
                     ExternalFile := 'C:\' + Text10702;
                   FileNameControlVisible := NOT ISSERVICETIER;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones] }

      { 1   ;2   ;Field     ;
                  Name=FileNameControl;
                  CaptionML=[ENU=External file;
                             ESP=Archivo externo];
                  SourceExpr=ExternalFile;
                  Visible=FileNameControlVisible;
                  OnAssistEdit=BEGIN
                                 ExternalFile := CommonDialogMgt.OpenFile(Text10700,ExternalFile,4,Text10701,1);
                               END;
                                }

      { 9   ;2   ;Field     ;
                  CaptionML=[ENU=Charging Date;
                             ESP=Fecha cargo];
                  SourceExpr=ChargDate }

      { 5   ;2   ;Field     ;
                  CaptionML=[ENU=Check Errors;
                             ESP=Comprobar errores];
                  SourceExpr=CheckErrors }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Bank Suffix;
                             ESP=Sufijo banco];
                  SourceExpr=BankSuffix;
                  TableRelation=Suffix.Suffix;
                  OnLookup=VAR
                             Suffix@1100002 : Record 7000024;
                             Suffixes@1100003 : Form 7000073;
                           BEGIN
                             Suffixes.SETTABLEVIEW(Suffix);
                             Suffixes.SETRECORD(Suffix);
                             Suffixes.LOOKUPMODE(TRUE);
                             Suffixes.EDITABLE(FALSE);
                             IF Suffixes.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               Suffixes.GETRECORD(Suffix);
                               BankSuffixBankAcc := Suffix."Bank Acc. Code";
                               BankSuffix := Suffix.Suffix;
                             END;
                           END;
                            }

    }
  }
  CODE
  {
    VAR
      Text1100000@1000 : TextConst 'ENU=Bank Suffix cannot be blank.;ESP=Sufijo bancario no puede ser blanco.';
      Text1100001@1001 : TextConst 'ENU=The Bank Suffix selected belongs to a %1  different than the %2. \;ESP=El sufijo bancario seleccionado pertenece a %1 diferente de %2. \';
      Text1100002@1002 : TextConst 'ENU=Do you want to continue?.;ESP=¨Quiere continuar?';
      Text1100003@1003 : TextConst 'ENU=Process cancelled by request of user.;ESP=Proceso cancelado a peticion del usuario.';
      Text1100005@1005 : TextConst 'ENU=Some data for Bank Account %1 are missing.;ESP=Falta informaci¢n en el banco %1.';
      Text1100006@1006 : TextConst 'ENU=<integer>;ESP=<integer>';
      Text1100007@1007 : TextConst 'ENU=There is not %1 %2 for %3 %4.;ESP=No existe %1 %2 para %3 %4.';
      Text1100008@1008 : TextConst 'ENU=Bill;ESP=Efecto';
      Text1100009@1009 : TextConst 'ENU=<Integer>;ESP=<Integer>';
      Text1100010@1010 : TextConst 'ENU=DEVOL.;ESP=DEVOL.';
      CompanyInfo@1100000 : Record 79;
      Customer@1100001 : Record 18;
      CustLedgEntry@1100002 : Record 21;
      ShipToAddress@1100003 : Record 222;
      BankAcc@1100004 : Record 270;
      CustBankAcc@1100005 : Record 287;
      PaymentMethod@1100006 : Record 289;
      DocMisc@1100007 : Codeunit 7000007;
      OutFile@1100008 : File;
      ExternalFile@1100009 : Text[1024];
      Posted@1100010 : Boolean;
      WithExpenses@1100011 : Boolean;
      PrintedDocs@1100012 : Boolean;
      CheckErrors@1100013 : Boolean;
      VATRegNo@1100014 : Text[9];
      CCCBankNo@1100015 : Text[4];
      CustCCCBankCode@1100016 : Text[4];
      CCCBankBranchNo@1100017 : Text[4];
      CustCCCBankBranchNo@1100018 : Text[4];
      CCCControlDigits@1100019 : Text[2];
      CustCCCControlDigits@1100020 : Text[2];
      CCCAccNo@1100021 : Text[10];
      CustCCCAccNo@1100022 : Text[10];
      BillGrNo@1100023 : Code[4];
      DocNo@1100024 : Code[15];
      DocType@1100025 : Code[1];
      AdditionalInfo@1100026 : Text[30];
      AdditionalInfo2@1100027 : Text[30];
      CustAddress@1100028 : ARRAY [4] OF Text[50];
      TotalPosted@1100029 : Decimal;
      TotalDocs@1100030 : Decimal;
      TotalDocsCust@1100031 : Decimal;
      TotalPrintedCust@1100032 : Decimal;
      TotalPrinted@1100033 : Decimal;
      TotalAmount@1100034 : Decimal;
      Expenses@1100035 : Code[1];
      IsAccepted@1100036 : Code[1];
      PrintedDocs2@1100037 : Code[1];
      OutText@1100038 : Text[162];
      BillGrNo2@1100039 : Code[15];
      Weight@1100040 : Text[30];
      Cad1@1100041 : Text[30];
      Cad2@1100042 : Text[30];
      Par1@1100043 : Integer;
      Par2@1100044 : Integer;
      Tot1@1100045 : Integer;
      Tot2@1100046 : Integer;
      cont@1100047 : Integer;
      ChargDate@1100048 : Date;
      RegisterCode@1100049 : Integer;
      RegisterCode2@1100050 : Integer;
      RegisterString@1100051 : Text[2];
      DocAmount@1100052 : Text[10];
      BillGrAmount@1100053 : Text[10];
      GeneralTotal@1100054 : Text[10];
      CustNoNIF@1100055 : Text[30];
      Doc2@1100056 : Record 7000002;
      DateMaxEfe@1100057 : Date;
      IsEuro@1100058 : Boolean;
      LCY@1100059 : 'Euro,Other';
      BankSuffix@1100060 : Code[3];
      BankSuffixBankAcc@1100061 : Code[20];
      Suffix@1100062 : Record 7000024;
      RBMgt@1100063 : Codeunit 419;
      ToFile@1100064 : Text[1024];
      CommonDialogMgt@1100065 : Codeunit 412;
      Text10700@1100068 : TextConst 'ENU=Export to file;ESP=Exportar a archivo';
      Text10701@1100067 : TextConst 'ENU=ASC Files (*.asc)|*.asc|All Files (*.*)|*.*;ESP=Archivos ASC (*.asc)|*.asc|Todos los archivos (*.*)|*.*';
      Text10702@1100066 : TextConst 'ENU=EFECTOS.ASC;ESP=EFECTOS.ASC';
      FileNameControlVisible@19024830 : Boolean INDATASET;

    PROCEDURE DLDACurrency@2();
    BEGIN
      IF STRLEN(FORMAT(RegisterCode)) = 1 THEN
        RegisterString := '0' + FORMAT(RegisterCode2)
      ELSE
        RegisterString := FORMAT(RegisterCode2);
    END;

    PROCEDURE EuroAmount@3(Amount@1100000 : Decimal) : Text[10];
    VAR
      TextAmount@1100001 : Text[15];
    BEGIN
      TextAmount := CONVERTSTR(FORMAT(Amount),' ','0');
      IF STRPOS(TextAmount,',') = 0 THEN
        TextAmount := TextAmount + '00'
      ELSE BEGIN
        IF STRLEN(COPYSTR(TextAmount,STRPOS(TextAmount,','),STRLEN(TextAmount))) = 2 THEN
          TextAmount := TextAmount + '0';
        TextAmount := DELCHR(TextAmount,'=',',');
      END;
      IF STRPOS(TextAmount,'.') = 0 THEN
        TextAmount := TextAmount
      ELSE
        TextAmount := DELCHR(TextAmount,'=','.');

      WHILE STRLEN(TextAmount) < 10 DO
        TextAmount := '0' + TextAmount;

      EXIT(TextAmount);
    END;

    PROCEDURE DateMax@1() Datemax@1100000 : Date;
    BEGIN
      Doc2.SETRANGE(Doc2.Type,0);
      Doc2.SETRANGE(Doc2."Bill Gr./Pmt. Order No.","Bill Group"."No.");
      Doc2.FIND('-');
      Datemax:=Doc2."Due Date";
      REPEAT
      IF Datemax < Doc2."Due Date" THEN
           Datemax:=Doc2."Due Date";
      UNTIL Doc2.NEXT=0;
      Doc2.RESET;
      EXIT(Datemax);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
