OBJECT Report 750 Save as Standard Gen. Journal
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Save as Standard Gen. Journal;
               ESP=Guardar como diario general est ndar];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF Code = '' THEN
                    ERROR(Text000);

                  StdJournalCreated := FALSE;
                  SaveGenJnlAsStandardJnl;
                END;

  }
  DATAITEMS
  {
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=9240;
      Height=3300;
      OnOpenForm=BEGIN
                   RequestOptionsForm.Code.ACTIVATE;
                 END;

    }
    CONTROLS
    {
      { 1   ;Label        ;110  ;110  ;3300 ;440  ;ParentControl=2;
                                                   CaptionML=[ENU=Code;
                                                              ESP=C¢digo] }
      { 2   ;TextBox      ;3520 ;110  ;2750 ;440  ;Name=Code;
                                                   Lookup=Yes;
                                                   NotBlank=Yes;
                                                   SourceExpr=Code;
                                                   OnLookup=VAR
                                                              StdGenJnl@1001 : Record 750;
                                                              StdGenJnls@1000 : Form 750;
                                                            BEGIN
                                                              StdGenJnl.SETRANGE("Journal Template Name",GenJnlBatch."Journal Template Name");
                                                              StdGenJnls.SETTABLEVIEW(StdGenJnl);

                                                              StdGenJnls.LOOKUPMODE := TRUE;
                                                              StdGenJnls.EDITABLE := FALSE;
                                                              IF StdGenJnls.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                StdGenJnls.GETRECORD(StdGenJnl);
                                                                Code := StdGenJnl.Code;
                                                                Description := StdGenJnl.Description;
                                                              END;
                                                            END;
                                                             }
      { 3   ;Label        ;110  ;660  ;3300 ;440  ;ParentControl=4;
                                                   CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n] }
      { 4   ;TextBox      ;3520 ;660  ;5500 ;440  ;SourceExpr=Description }
      { 5   ;Label        ;110  ;1320 ;3300 ;440  ;CaptionML=[ENU=Save Amount;
                                                              ESP=Guardar importe] }
      { 8   ;CheckBox     ;3520 ;1320 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Save Amount;
                                                              ESP=Guardar importe];
                                                   SourceExpr=SaveAmount }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones] }

      { 2   ;2   ;Field     ;
                  Name=Code;
                  Lookup=Yes;
                  CaptionML=[ENU=Code;
                             ESP=C¢digo];
                  NotBlank=Yes;
                  SourceExpr=Code;
                  OnLookup=VAR
                             StdGenJnl@1001 : Record 750;
                             StdGenJnls@1000 : Form 750;
                           BEGIN
                             StdGenJnl.SETRANGE("Journal Template Name",GenJnlBatch."Journal Template Name");
                             StdGenJnls.SETTABLEVIEW(StdGenJnl);

                             StdGenJnls.LOOKUPMODE := TRUE;
                             StdGenJnls.EDITABLE := FALSE;
                             IF StdGenJnls.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               StdGenJnls.GETRECORD(StdGenJnl);
                               Code := StdGenJnl.Code;
                               Description := StdGenJnl.Description;
                             END;
                           END;
                            }

      { 4   ;2   ;Field     ;
                  CaptionML=[ENU=Description;
                             ESP=Descripci¢n];
                  SourceExpr=Description }

      { 8   ;2   ;Field     ;
                  CaptionML=[ENU=Save Amount;
                             ESP=Guardar importe];
                  SourceExpr=SaveAmount }

    }
  }
  CODE
  {
    VAR
      GenJnlLine@1008 : Record 81;
      GenJnlBatch@1002 : Record 232;
      StdGenJnl@1007 : Record 750;
      JnlLineDimFrom@1009 : Record 356;
      Code@1000 : Code[10];
      Description@1001 : Text[50];
      Text000@1004 : TextConst 'ENU=Please enter a code for Standard General Journal.;ESP=Introduzca un c¢digo para el Diario general est ndar.';
      Text001@1005 : TextConst 'ENU=Standard General Journal %1 already exists. Do you want to overwrite?;ESP=El Diario general est ndar %1 ya existe. ¨Desea sobrescribirlo?';
      SaveAmount@1006 : Boolean;
      StdJournalCreated@1003 : Boolean;
      NextLineNo@1010 : Integer;

    PROCEDURE Initialise@2(VAR SelectedGenJnlLines@1000 : Record 81;SelectedGenJnlBatch@1001 : Record 232);
    BEGIN
      GenJnlLine.COPY(SelectedGenJnlLines);
      GenJnlBatch := SelectedGenJnlBatch;
    END;

    LOCAL PROCEDURE SaveGenJnlAsStandardJnl@6();
    VAR
      StdGenJnlLine@1000 : Record 751;
    BEGIN
      StdGenJnl.INIT;
      StdGenJnl."Journal Template Name" := GenJnlBatch."Journal Template Name";
      StdGenJnl.Code := Code;
      StdGenJnl.Description := Description;

      IF StdGenJnlExists THEN
        IF NOT CONFIRM(Text001,FALSE,StdGenJnl.Code) THEN
          EXIT;

      JnlLineDimFrom.LOCKTABLE;
      StdGenJnlLine.LOCKTABLE;
      StdGenJnl.LOCKTABLE;

      IF StdGenJnlExists THEN BEGIN
        StdGenJnl.MODIFY(TRUE);
        StdGenJnlLine.SETRANGE("Journal Template Name",StdGenJnl."Journal Template Name");
        StdGenJnlLine.SETRANGE("Standard Journal Code",StdGenJnl.Code);
        StdGenJnlLine.DELETEALL(TRUE);
      END ELSE
        StdGenJnl.INSERT(TRUE);

      NextLineNo := 10000;
      IF GenJnlLine.FIND('-') THEN
        REPEAT
          StdGenJnlLine."Line No." := NextLineNo;
          NextLineNo := NextLineNo + 10000;
          StdGenJnlLine.INIT;
          StdGenJnlLine."Journal Template Name" := StdGenJnl."Journal Template Name";
          StdGenJnlLine."Standard Journal Code" := StdGenJnl.Code;
          StdGenJnlLine.TRANSFERFIELDS(GenJnlLine,FALSE);
          StdGenJnlLine."Shortcut Dimension 1 Code" := '';
          StdGenJnlLine."Shortcut Dimension 2 Code" := '';
          IF NOT SaveAmount THEN BEGIN
            StdGenJnlLine.Amount := 0;
            StdGenJnlLine."Debit Amount" := 0;
            StdGenJnlLine."Credit Amount" := 0;
            StdGenJnlLine."Amount (LCY)" := 0;
            StdGenJnlLine."VAT Amount" := 0;
            StdGenJnlLine."VAT Base Amount" := 0;
            StdGenJnlLine."VAT Difference" := 0;
            StdGenJnlLine."Bal. VAT Amount" := 0;
            StdGenJnlLine."Bal. VAT Base Amount" := 0;
            StdGenJnlLine."Bal. VAT Difference" := 0;
            StdGenJnlLine."Balance (LCY)" := 0;
          END;
          StdGenJnlLine.INSERT(TRUE);
          CopyGenJnlLineDims(GenJnlLine,StdGenJnlLine);
        UNTIL GenJnlLine.NEXT = 0;

      StdJournalCreated := TRUE;
    END;

    LOCAL PROCEDURE CopyGenJnlLineDims@3(GenJnlLine@1000 : Record 81;StdGenJnlLine@1001 : Record 751);
    VAR
      DimMgt@1004 : Codeunit 408;
      GLSetup@1002 : Record 98;
      JnlLineDimTo@1003 : Record 356;
    BEGIN
      JnlLineDimFrom.SETRANGE("Table ID",DATABASE::"Gen. Journal Line");
      JnlLineDimFrom.SETRANGE("Journal Template Name",GenJnlLine."Journal Template Name");
      JnlLineDimFrom.SETRANGE("Journal Batch Name",GenJnlLine."Journal Batch Name");
      JnlLineDimFrom.SETRANGE("Journal Line No.",GenJnlLine."Line No.");

      DimMgt.MoveJnlLineDimToJnlLineDim(JnlLineDimFrom,DATABASE::"Standard General Journal Line",
        StdGenJnlLine."Journal Template Name",StdGenJnlLine."Standard Journal Code",StdGenJnlLine."Line No.");
      GLSetup.GET;
      JnlLineDimTo.RESET;
      JnlLineDimTo.SETRANGE("Journal Template Name",StdGenJnlLine."Journal Template Name");
      JnlLineDimTo.SETRANGE("Journal Batch Name",StdGenJnlLine."Standard Journal Code");
      JnlLineDimTo.SETRANGE("Journal Line No.",StdGenJnlLine."Line No.");
      JnlLineDimTo.SETRANGE("Dimension Code",GLSetup."Global Dimension 1 Code");
      IF JnlLineDimTo.FINDFIRST THEN
        StdGenJnlLine."Shortcut Dimension 1 Code" := JnlLineDimTo."Dimension Value Code";

      JnlLineDimTo.SETRANGE("Dimension Code",GLSetup."Global Dimension 2 Code");
      IF JnlLineDimTo.FINDFIRST THEN
        StdGenJnlLine."Shortcut Dimension 2 Code" := JnlLineDimTo."Dimension Value Code";

      StdGenJnlLine.MODIFY;
    END;

    LOCAL PROCEDURE StdGenJnlExists@10() : Boolean;
    VAR
      StdGenJnl@1000 : Record 750;
    BEGIN
      StdGenJnl.SETRANGE("Journal Template Name",GenJnlBatch."Journal Template Name");
      StdGenJnl.SETRANGE(Code,Code);

      EXIT(StdGenJnl.FIND('-'));
    END;

    PROCEDURE GetStdGeneralJournal@1(VAR StdGenJnl1@1000 : Record 750) : Boolean;
    BEGIN
      IF StdJournalCreated THEN
        StdGenJnl1.COPY(StdGenJnl);

      EXIT(StdJournalCreated);
    END;

    PROCEDURE UTlocalCall@4(localFunctionName@1002 : Text[30];VAR param@1001 : ARRAY [20] OF Variant;VAR return@1000 : Variant);
    VAR
      GenJnlLine@1003 : Record 81;
      StdGenJnlLine@1004 : Record 751;
    BEGIN
      CASE localFunctionName OF
        'SaveGenJnlAsStandardJnl': SaveGenJnlAsStandardJnl;
        'CopyGenJnlLineDims':
          BEGIN
            GenJnlLine := param[1];
            StdGenJnlLine := param[2];
            CopyGenJnlLineDims(GenJnlLine,StdGenJnlLine);
          END;
        ELSE
          ERROR('Local function %1 is not included for test.',localFunctionName);
      END;
    END;

    PROCEDURE UTsetGlobalValue@38(globalVarName@1001 : Text[30];VAR value@1002 : Variant);
    BEGIN
      CASE globalVarName OF
        'Code': Code := value;
        'Description': Description := value;
        'SaveAmount': SaveAmount := value;
        ELSE
          ERROR('Global variable %1 is not included for test.',globalVarName);
      END;
    END;

    PROCEDURE UTgetGlobalValue@5(globalVarName@1001 : Text[30];VAR value@1002 : Variant);
    BEGIN
      CASE globalVarName OF
        'StdGenJnl': value := StdGenJnl;
        ELSE
          ERROR('Global variable %1 is not included for test.',globalVarName);
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
