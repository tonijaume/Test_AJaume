OBJECT Report 499 Delete Invoiced Purch. Orders
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Delete Invoiced Purch. Orders;
               ESP=Eliminar peds. compra factdos.];
    ProcessingOnly=Yes;
  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table38;
        DataItemTableView=SORTING(Document Type,No.)
                          WHERE(Document Type=CONST(Order));
        ReqFilterHeadingML=[ENU=Purchase Order;
                            ESP=Pedido compra];
        OnPreDataItem=BEGIN
                        Window.OPEN(Text000);
                      END;

        OnAfterGetRecord=VAR
                           ReservePurchLine@1000 : Codeunit 99000834;
                         BEGIN
                           Window.UPDATE(1,"No.");

                           AllLinesDeleted := TRUE;
                           DocDim.RESET;
                           DocDim.SETRANGE("Document Type","Document Type");
                           DocDim.SETRANGE("Document No.","No.");
                           ItemChargeAssgntPurch.RESET;
                           ItemChargeAssgntPurch.SETRANGE("Document Type","Document Type");
                           ItemChargeAssgntPurch.SETRANGE("Document No.","No.");
                           PurchLine.RESET;
                           PurchLine.SETRANGE("Document Type","Document Type");
                           PurchLine.SETRANGE("Document No.","No.");
                           PurchLine.SETFILTER("Quantity Invoiced",'<>0');
                           IF PurchLine.FIND('-') THEN BEGIN
                             PurchLine.SETRANGE("Quantity Invoiced");
                             PurchLine.SETFILTER("Outstanding Quantity",'<>0');
                             IF NOT PurchLine.FIND('-') THEN BEGIN
                               PurchLine.SETRANGE("Outstanding Quantity");
                               PurchLine.SETFILTER("Qty. Rcd. Not Invoiced",'<>0');
                               IF NOT PurchLine.FIND('-') THEN BEGIN
                                 PurchLine.LOCKTABLE;
                                 IF NOT PurchLine.FIND('-') THEN BEGIN
                                   PurchLine.SETRANGE("Qty. Rcd. Not Invoiced");
                                   DocDim.SETRANGE("Table ID",DATABASE::"Purchase Line");
                                   IF PurchLine.FIND('-') THEN
                                     REPEAT
                                       PurchLine.CALCFIELDS("Qty. Assigned");
                                       IF ((PurchLine."Qty. Assigned" = PurchLine."Quantity Invoiced") AND
                                         (PurchLine."Qty. Assigned" <> 0)) OR
                                         (PurchLine.Type <> PurchLine.Type::"Charge (Item)")
                                       THEN BEGIN
                                         DocDim.SETRANGE("Line No.",PurchLine."Line No.");
                                         DocDim.DELETEALL;
                                         IF PurchLine.Type = PurchLine.Type::"Charge (Item)" THEN BEGIN
                                           ItemChargeAssgntPurch.SETRANGE("Document Line No.",PurchLine."Line No.");
                                           ItemChargeAssgntPurch.DELETEALL;
                                         END;
                                         IF PurchLine.HASLINKS THEN
                                           PurchLine.DELETELINKS;

                                         PurchLine.DELETE;
                                       END ELSE
                                         AllLinesDeleted := FALSE;
                                     UNTIL PurchLine.NEXT = 0;

                                   IF AllLinesDeleted THEN BEGIN
                                     PurchPost.DeleteHeader(
                                       "Purchase Header",PurchRcptHeader,PurchInvHeader,PurchCrMemoHeader,
                                       ReturnShptHeader,PrepmtPurchInvHeader,PrepmtPurchCrMemoHeader);

                                     ReservePurchLine.DeleteInvoiceSpecFromHeader("Purchase Header");

                                     PurchCommentLine.SETRANGE("Document Type","Document Type");
                                     PurchCommentLine.SETRANGE("No.","No.");
                                     PurchCommentLine.DELETEALL;

                                     DocDim.SETRANGE("Table ID",DATABASE::"Purchase Header");
                                     DocDim.SETRANGE("Line No.",0);
                                     DocDim.DELETEALL;

                                     WhseRequest.SETRANGE("Source Type",DATABASE::"Purchase Line");
                                     WhseRequest.SETRANGE("Source Subtype","Document Type");
                                     WhseRequest.SETRANGE("Source No.","No.");
                                     WhseRequest.DELETEALL(TRUE);

                                     IF HASLINKS THEN
                                       DELETELINKS;

                                     DELETE;
                                   END;
                                   COMMIT;
                                 END;
                               END;
                             END;
                           END;
                         END;

        ReqFilterFields=No.,Buy-from Vendor No.,Pay-to Vendor No.;
      }
      SECTIONS
      {
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Processing purch. orders #1##########;ESP=Procesando pedidos compra #1##########';
      PurchLine@1001 : Record 39;
      PurchRcptHeader@1011 : Record 120;
      PurchInvHeader@1010 : Record 122;
      PurchCrMemoHeader@1009 : Record 124;
      ReturnShptHeader@1008 : Record 6650;
      PrepmtPurchInvHeader@1014 : Record 122;
      PrepmtPurchCrMemoHeader@1013 : Record 124;
      PurchCommentLine@1002 : Record 43;
      DocDim@1003 : Record 357;
      ItemChargeAssgntPurch@1005 : Record 5805;
      WhseRequest@1007 : Record 5765;
      PurchPost@1012 : Codeunit 90;
      Window@1004 : Dialog;
      AllLinesDeleted@1006 : Boolean;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
