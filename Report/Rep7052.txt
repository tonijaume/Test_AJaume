OBJECT Report 7052 Suggest Sales Price on Wksh.
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Suggest Sales Price on Wksh.;
               ESP=Sugerir precio venta en hoja];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  CASE ToSalesType OF
                    ToSalesType::Customer:
                      BEGIN
                        ToCust."No." := ToSalesCode;
                        IF ToCust."No." <> '' THEN
                          ToCust.FIND
                        ELSE BEGIN
                          IF NOT ToCust.FIND THEN
                            ToCust.INIT;
                          ToSalesCode := ToCust."No.";
                        END;
                      END;
                    ToSalesType::"Customer Price Group":
                      BEGIN
                        ToCustPriceGr.Code := ToSalesCode;
                        IF ToCustPriceGr.Code <> '' THEN
                          ToCustPriceGr.FIND
                        ELSE BEGIN
                          IF NOT ToCustPriceGr.FIND THEN
                            ToCustPriceGr.INIT;
                          ToSalesCode := ToCustPriceGr.Code;
                        END;
                      END;
                    ToSalesType::Campaign:
                      BEGIN
                        ToCampaign."No." := ToSalesCode;
                        IF ToCampaign."No." <> '' THEN
                          ToCampaign.FIND
                        ELSE BEGIN
                          IF NOT ToCampaign.FIND THEN
                            ToCampaign.INIT;
                          ToSalesCode := ToCampaign."No.";
                        END;
                        ToStartDate := ToCampaign."Starting Date";
                        ToEndDate := ToCampaign."Ending Date";
                      END;
                  END;

                  ReplaceUnitOfMeasure := ToUnitOfMeasure.Code <> '';
                  ReplaceCurrency := ToCurrency.Code <> '';
                  ReplaceStartingDate := ToStartDate <> 0D;
                  ReplaceEndingDate := ToEndDate <> 0D;

                  IF ReplaceUnitOfMeasure AND (ToUnitOfMeasure.Code <> '') THEN
                    ToUnitOfMeasure.FIND;

                  RoundingMethod.SETRANGE(Code,RoundingMethod.Code);
                END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table7002;
        DataItemTableView=SORTING(Item No.);
        OnPreDataItem=BEGIN
                        Window.OPEN(Text001);
                      END;

        OnAfterGetRecord=BEGIN
                           IF Item."No." <> "Item No." THEN BEGIN
                             Item.GET("Item No.");
                             Window.UPDATE(1,"Item No.");
                           END;

                           ReplaceSalesCode := NOT(("Sales Type" = ToSalesType) AND ("Sales Code" = ToSalesCode));

                           IF (ToSalesCode = '') AND (ToSalesType <> ToSalesType::"All Customers") THEN
                             ERROR(Text002,"Sales Type");

                           CLEAR(SalesPriceWksh);

                           SalesPriceWksh.VALIDATE("Sales Type",ToSalesType);
                           IF NOT ReplaceSalesCode THEN
                             SalesPriceWksh.VALIDATE("Sales Code","Sales Code")
                           ELSE
                             SalesPriceWksh.VALIDATE("Sales Code",ToSalesCode);

                           SalesPriceWksh.VALIDATE("Item No.","Item No.");
                           SalesPriceWksh."New Unit Price" := "Unit Price";
                           SalesPriceWksh."Minimum Quantity" := "Minimum Quantity";

                           IF NOT ReplaceUnitOfMeasure THEN
                             SalesPriceWksh."Unit of Measure Code" := "Unit of Measure Code"
                           ELSE BEGIN
                             SalesPriceWksh."Unit of Measure Code" := ToUnitOfMeasure.Code;
                             IF NOT (SalesPriceWksh."Unit of Measure Code" IN ['',Item."Base Unit of Measure"]) THEN
                               IF NOT ItemUnitOfMeasure.GET("Item No.",SalesPriceWksh."Unit of Measure Code") THEN
                                 CurrReport.SKIP;
                             SalesPriceWksh."New Unit Price" :=
                               SalesPriceWksh."New Unit Price" *
                               UOMMgt.GetQtyPerUnitOfMeasure(Item,SalesPriceWksh."Unit of Measure Code") /
                               UOMMgt.GetQtyPerUnitOfMeasure(Item,"Unit of Measure Code");
                           END;
                           SalesPriceWksh.VALIDATE("Unit of Measure Code");
                           SalesPriceWksh.VALIDATE("Variant Code","Variant Code");

                           IF NOT ReplaceCurrency THEN
                             SalesPriceWksh."Currency Code" := "Currency Code"
                           ELSE
                             SalesPriceWksh."Currency Code" := ToCurrency.Code;

                           IF ReplaceStartingDate THEN
                             SalesPriceWksh.VALIDATE("Starting Date",ToStartDate);
                           IF ReplaceEndingDate THEN
                             SalesPriceWksh.VALIDATE("Ending Date",ToEndDate);

                           IF "Currency Code" <> SalesPriceWksh."Currency Code" THEN BEGIN
                             IF "Currency Code" <> '' THEN BEGIN
                               FromCurrency.GET("Currency Code");
                               FromCurrency.TESTFIELD(Code);
                               SalesPriceWksh."New Unit Price" :=
                                 CurrExchRate.ExchangeAmtFCYToLCY(
                                   WORKDATE,"Currency Code",SalesPriceWksh."New Unit Price",
                                   CurrExchRate.ExchangeRate(
                                     WORKDATE,"Currency Code"));
                             END;
                             IF SalesPriceWksh."Currency Code" <> '' THEN
                               SalesPriceWksh."New Unit Price" :=
                                 CurrExchRate.ExchangeAmtLCYToFCY(
                                   WORKDATE,SalesPriceWksh."Currency Code",
                                   SalesPriceWksh."New Unit Price",CurrExchRate.ExchangeRate(
                                     WORKDATE,SalesPriceWksh."Currency Code"));
                           END;

                           IF SalesPriceWksh."Currency Code" = '' THEN
                             Currency2.InitRoundingPrecision
                           ELSE BEGIN
                             Currency2.GET(SalesPriceWksh."Currency Code");
                             Currency2.TESTFIELD("Unit-Amount Rounding Precision");
                           END;
                           SalesPriceWksh."New Unit Price" :=
                             ROUND(SalesPriceWksh."New Unit Price",Currency2."Unit-Amount Rounding Precision");

                           IF SalesPriceWksh."New Unit Price" > PriceLowerLimit THEN
                             SalesPriceWksh."New Unit Price" := SalesPriceWksh."New Unit Price" * UnitPriceFactor;
                           IF RoundingMethod.Code <> '' THEN BEGIN
                             RoundingMethod."Minimum Amount" := SalesPriceWksh."New Unit Price";
                             IF RoundingMethod.FIND('=<') THEN BEGIN
                               SalesPriceWksh."New Unit Price" :=
                                 SalesPriceWksh."New Unit Price" + RoundingMethod."Amount Added Before";
                               IF RoundingMethod.Precision > 0 THEN
                                 SalesPriceWksh."New Unit Price" :=
                                   ROUND(
                                     SalesPriceWksh."New Unit Price",
                                     RoundingMethod.Precision,COPYSTR('=><',RoundingMethod.Type + 1,1));
                               SalesPriceWksh."New Unit Price" := SalesPriceWksh."New Unit Price" +
                                 RoundingMethod."Amount Added After";
                             END;
                           END;

                           SalesPriceWksh."Price Includes VAT" := "Price Includes VAT";
                           SalesPriceWksh."VAT Bus. Posting Gr. (Price)" := "VAT Bus. Posting Gr. (Price)";
                           SalesPriceWksh."Allow Invoice Disc." := "Allow Invoice Disc.";
                           SalesPriceWksh."Allow Line Disc." := "Allow Line Disc.";
                           SalesPriceWksh.CalcCurrentPrice(PriceAlreadyExists);

                           IF PriceAlreadyExists OR CreateNewPrices THEN BEGIN
                             SalesPriceWksh2 := SalesPriceWksh;
                             IF SalesPriceWksh2.FIND('=') THEN
                               SalesPriceWksh.MODIFY(TRUE)
                             ELSE
                               SalesPriceWksh.INSERT(TRUE);
                           END;
                         END;

        ReqFilterFields=Sales Type,Sales Code,Item No.,Currency Code,Starting Date;
      }
      SECTIONS
      {
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=12540;
      Height=3740;
      SaveValues=Yes;
      OnOpenForm=BEGIN
                   IF UnitPriceFactor = 0 THEN BEGIN
                     UnitPriceFactor := 1;
                     ToCustPriceGr.Code := '';
                     ToUnitOfMeasure.Code := '';
                     ToCurrency.Code := '';
                     END;

                   RequestOptionsForm.SalesCodeCtrl.ENABLED := TRUE;
                   IF ToSalesType = ToSalesType::"All Customers" THEN
                     RequestOptionsForm.SalesCodeCtrl.ENABLED := FALSE;

                   RequestOptionsForm.SalesCodeCtrl.ENABLED := ToSalesType <> ToSalesType::"All Customers";
                   RequestOptionsForm.ToStartDateCtrl.ENABLED := ToSalesType <> ToSalesType::Campaign;
                   RequestOptionsForm.ToEndDateCtrl.ENABLED := ToSalesType <> ToSalesType::Campaign;
                 END;

    }
    CONTROLS
    {
      { 1   ;TextBox      ;3410 ;1100 ;2750 ;440  ;Name=SalesCodeCtrl;
                                                   CaptionML=[ENU=Sales Code;
                                                              ESP=C¢digo ventas];
                                                   SourceExpr=ToSalesCode;
                                                   OnValidate=VAR
                                                                Customer@1001 : Record 18;
                                                                CustomerPriceGroup@1003 : Record 6;
                                                                Campaign@1002 : Record 5071;
                                                              BEGIN
                                                                IF ToSalesType = ToSalesType::"All Customers" THEN EXIT;

                                                                CASE ToSalesType OF
                                                                  ToSalesType::Customer:  Customer.GET(ToSalesCode);
                                                                  ToSalesType::"Customer Price Group": CustomerPriceGroup.GET(ToSalesCode);
                                                                  ToSalesType::Campaign :
                                                                    BEGIN
                                                                      Campaign.GET(ToSalesCode);
                                                                      ToStartDate := Campaign."Starting Date";
                                                                      ToEndDate := Campaign."Ending Date";
                                                                    END;
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              CustList@1003 : Form 22;
                                                              CustPriceGrList@1002 : Form 7;
                                                              CampaignList@1004 : Form 5087;
                                                            BEGIN
                                                              CASE ToSalesType OF
                                                                ToSalesType::Customer:
                                                                  BEGIN
                                                                    CustList.LOOKUPMODE := TRUE;
                                                                    CustList.SETRECORD(ToCust);
                                                                    IF CustList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                      CustList.GETRECORD(ToCust);
                                                                      ToSalesCode := ToCust."No.";
                                                                    END;
                                                                  END;
                                                                ToSalesType::"Customer Price Group":
                                                                  BEGIN
                                                                    CustPriceGrList.LOOKUPMODE := TRUE;
                                                                    CustPriceGrList.SETRECORD(ToCustPriceGr);
                                                                    IF CustPriceGrList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                      CustPriceGrList.GETRECORD(ToCustPriceGr);
                                                                      ToSalesCode := ToCustPriceGr.Code;
                                                                    END;
                                                                  END;
                                                                ToSalesType::Campaign:
                                                                  BEGIN
                                                                    CampaignList.LOOKUPMODE := TRUE;
                                                                    CampaignList.SETRECORD(ToCampaign);
                                                                    IF CampaignList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                      CampaignList.GETRECORD(ToCampaign);
                                                                      ToSalesCode := ToCampaign."No.";
                                                                      ToStartDate := ToCampaign."Starting Date";
                                                                      ToEndDate := ToCampaign."Ending Date";
                                                                    END;
                                                                  END;
                                                              END;
                                                            END;
                                                             }
      { 21  ;Label        ;0    ;0    ;5500 ;440  ;LeaderDots=No;
                                                   CaptionML=[ENU=Copy to Sales Price Worksheet...;
                                                              ESP=Copiar a Hoja precio venta...] }
      { 31  ;Label        ;220  ;1100 ;3080 ;440  ;ParentControl=1 }
      { 2   ;TextBox      ;3410 ;2200 ;2750 ;440  ;ValidateTableRelation=No;
                                                   CaptionML=[ENU=Currency Code;
                                                              ESP=C¢d. divisa];
                                                   SourceExpr=ToCurrency.Code;
                                                   TableRelation=Currency;
                                                   OnValidate=BEGIN
                                                                IF ToCurrency.Code <> '' THEN
                                                                  ToCurrency.FIND;
                                                              END;
                                                               }
      { 22  ;Label        ;220  ;2200 ;3080 ;440  ;ParentControl=2 }
      { 3   ;TextBox      ;3410 ;2750 ;1650 ;440  ;Name=ToStartDateCtrl;
                                                   CaptionML=[ENU=Starting Date;
                                                              ESP=Fecha inicial];
                                                   SourceExpr=ToStartDate }
      { 23  ;Label        ;220  ;2750 ;3080 ;440  ;ParentControl=3 }
      { 4   ;TextBox      ;9790 ;0    ;2750 ;440  ;CaptionML=[ENU=Only Prices Above;
                                                              ESP=S¢lo precios superiores a];
                                                   DecimalPlaces=2:5;
                                                   SourceExpr=PriceLowerLimit }
      { 24  ;Label        ;6380 ;0    ;3300 ;440  ;ParentControl=4 }
      { 5   ;TextBox      ;9790 ;550  ;2750 ;440  ;CaptionML=[ENU=Adjustment Factor;
                                                              ESP=Factor ajuste];
                                                   DecimalPlaces=0:5;
                                                   SourceExpr=UnitPriceFactor;
                                                   MinValue=0 }
      { 25  ;Label        ;6380 ;550  ;3300 ;440  ;ParentControl=5 }
      { 6   ;TextBox      ;9790 ;1100 ;2750 ;440  ;CaptionML=[ENU=Rounding Method;
                                                              ESP=M‚todo redondeo];
                                                   SourceExpr=RoundingMethod.Code;
                                                   TableRelation="Rounding Method" }
      { 26  ;Label        ;6380 ;1100 ;3300 ;440  ;ParentControl=6 }
      { 7   ;CheckBox     ;9790 ;1650 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Create New Prices;
                                                              ESP=Crear tarifa nueva];
                                                   SourceExpr=CreateNewPrices }
      { 27  ;Label        ;6380 ;1650 ;3300 ;440  ;ParentControl=7 }
      { 14  ;TextBox      ;3410 ;1650 ;2750 ;440  ;ValidateTableRelation=No;
                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              ESP=C¢d. unidad medida];
                                                   SourceExpr=ToUnitOfMeasure.Code;
                                                   TableRelation="Unit of Measure";
                                                   OnValidate=BEGIN
                                                                IF ToUnitOfMeasure.Code <> '' THEN
                                                                  ToUnitOfMeasure.FIND;
                                                              END;
                                                               }
      { 15  ;Label        ;220  ;1650 ;3080 ;440  ;ParentControl=14 }
      { 8   ;TextBox      ;3410 ;550  ;2750 ;440  ;CaptionML=[ENU=Sales Type;
                                                              ESP=Tipo venta];
                                                   OptionCaptionML=[ENU=Customer,Customer Price Group,All Customers,Campaign;
                                                                    ESP=Cliente,Grupo precio cliente,Todos clientes,Campa¤a];
                                                   SourceExpr=ToSalesType;
                                                   OnValidate=BEGIN
                                                                RequestOptionsForm.SalesCodeCtrl.ENABLED := ToSalesType <> ToSalesType::"All Customers";
                                                                RequestOptionsForm.ToStartDateCtrl.ENABLED := ToSalesType <> ToSalesType::Campaign;
                                                                RequestOptionsForm.ToEndDateCtrl.ENABLED := ToSalesType <> ToSalesType::Campaign;

                                                                ToSalesCode := '';
                                                                ToStartDate := 0D;
                                                                ToEndDate := 0D;
                                                              END;
                                                               }
      { 9   ;Label        ;220  ;550  ;3080 ;440  ;ParentControl=8 }
      { 10  ;TextBox      ;3410 ;3300 ;1650 ;440  ;Name=ToEndDateCtrl;
                                                   CaptionML=[ENU=Ending Date;
                                                              ESP=Fecha final];
                                                   SourceExpr=ToEndDate }
      { 11  ;Label        ;220  ;3300 ;3080 ;440  ;ParentControl=10 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnInit=BEGIN
               ToEndDateCtrlEnable := TRUE;
               ToStartDateCtrlEnable := TRUE;
               SalesCodeCtrlEnable := TRUE;
             END;

      OnOpenPage=BEGIN
                   IF UnitPriceFactor = 0 THEN BEGIN
                     UnitPriceFactor := 1;
                     ToCustPriceGr.Code := '';
                     ToUnitOfMeasure.Code := '';
                     ToCurrency.Code := '';
                     END;

                   SalesCodeCtrlEnable := TRUE;
                   IF ToSalesType = ToSalesType::"All Customers" THEN
                     SalesCodeCtrlEnable := FALSE;

                   SalesCodeCtrlEnable := ToSalesType <> ToSalesType::"All Customers";
                   ToStartDateCtrlEnable := ToSalesType <> ToSalesType::Campaign;
                   ToEndDateCtrlEnable := ToSalesType <> ToSalesType::Campaign;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones] }

      { 21  ;2   ;Group     ;
                  CaptionML=[ENU=Copy to Sales Price Worksheet...;
                             ESP=Copiar a Hoja precio venta...] }

      { 8   ;3   ;Field     ;
                  CaptionML=[ENU=Sales Type;
                             ESP=Tipo venta];
                  OptionCaptionML=[ENU=Customer,Customer Price Group,All Customers,Campaign;
                                   ESP=Cliente,Grupo precio cliente,Todos clientes,Campa¤a];
                  SourceExpr=ToSalesType;
                  OnValidate=BEGIN
                               SalesCodeCtrlEnable := ToSalesType <> ToSalesType::"All Customers";
                               ToStartDateCtrlEnable := ToSalesType <> ToSalesType::Campaign;
                               ToEndDateCtrlEnable := ToSalesType <> ToSalesType::Campaign;

                               ToSalesCode := '';
                               ToStartDate := 0D;
                               ToEndDate := 0D;
                             END;
                              }

      { 1   ;3   ;Field     ;
                  Name=SalesCodeCtrl;
                  CaptionML=[ENU=Sales Code;
                             ESP=C¢digo ventas];
                  SourceExpr=ToSalesCode;
                  Enabled=SalesCodeCtrlEnable;
                  OnValidate=VAR
                               Customer@1001 : Record 18;
                               CustomerPriceGroup@1003 : Record 6;
                               Campaign@1002 : Record 5071;
                             BEGIN
                               IF ToSalesType = ToSalesType::"All Customers" THEN EXIT;

                               CASE ToSalesType OF
                                 ToSalesType::Customer:  Customer.GET(ToSalesCode);
                                 ToSalesType::"Customer Price Group": CustomerPriceGroup.GET(ToSalesCode);
                                 ToSalesType::Campaign :
                                   BEGIN
                                     Campaign.GET(ToSalesCode);
                                     ToStartDate := Campaign."Starting Date";
                                     ToEndDate := Campaign."Ending Date";
                                   END;
                               END;
                             END;

                  OnLookup=VAR
                             CustList@1003 : Form 22;
                             CustPriceGrList@1002 : Form 7;
                             CampaignList@1004 : Form 5087;
                           BEGIN
                             CASE ToSalesType OF
                               ToSalesType::Customer:
                                 BEGIN
                                   CustList.LOOKUPMODE := TRUE;
                                   CustList.SETRECORD(ToCust);
                                   IF CustList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                     CustList.GETRECORD(ToCust);
                                     ToSalesCode := ToCust."No.";
                                   END;
                                 END;
                               ToSalesType::"Customer Price Group":
                                 BEGIN
                                   CustPriceGrList.LOOKUPMODE := TRUE;
                                   CustPriceGrList.SETRECORD(ToCustPriceGr);
                                   IF CustPriceGrList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                     CustPriceGrList.GETRECORD(ToCustPriceGr);
                                     ToSalesCode := ToCustPriceGr.Code;
                                   END;
                                 END;
                               ToSalesType::Campaign:
                                 BEGIN
                                   CampaignList.LOOKUPMODE := TRUE;
                                   CampaignList.SETRECORD(ToCampaign);
                                   IF CampaignList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                     CampaignList.GETRECORD(ToCampaign);
                                     ToSalesCode := ToCampaign."No.";
                                     ToStartDate := ToCampaign."Starting Date";
                                     ToEndDate := ToCampaign."Ending Date";
                                   END;
                                 END;
                             END;
                           END;
                            }

      { 14  ;3   ;Field     ;
                  CaptionML=[ENU=Unit of Measure Code;
                             ESP=C¢d. unidad medida];
                  SourceExpr=ToUnitOfMeasure.Code;
                  TableRelation="Unit of Measure";
                  OnValidate=BEGIN
                               IF ToUnitOfMeasure.Code <> '' THEN
                                 ToUnitOfMeasure.FIND;
                             END;
                              }

      { 2   ;3   ;Field     ;
                  CaptionML=[ENU=Currency Code;
                             ESP=C¢d. divisa];
                  SourceExpr=ToCurrency.Code;
                  TableRelation=Currency;
                  OnValidate=BEGIN
                               IF ToCurrency.Code <> '' THEN
                                 ToCurrency.FIND;
                             END;
                              }

      { 3   ;3   ;Field     ;
                  Name=ToStartDateCtrl;
                  CaptionML=[ENU=Starting Date;
                             ESP=Fecha inicial];
                  SourceExpr=ToStartDate;
                  Enabled=ToStartDateCtrlEnable }

      { 10  ;3   ;Field     ;
                  Name=ToEndDateCtrl;
                  CaptionML=[ENU=Ending Date;
                             ESP=Fecha final];
                  SourceExpr=ToEndDate;
                  Enabled=ToEndDateCtrlEnable }

      { 4   ;2   ;Field     ;
                  CaptionML=[ENU=Only Prices Above;
                             ESP=S¢lo precios superiores a];
                  DecimalPlaces=2:5;
                  SourceExpr=PriceLowerLimit }

      { 5   ;2   ;Field     ;
                  CaptionML=[ENU=Adjustment Factor;
                             ESP=Factor ajuste];
                  DecimalPlaces=0:5;
                  SourceExpr=UnitPriceFactor;
                  MinValue=0 }

      { 6   ;2   ;Field     ;
                  CaptionML=[ENU=Rounding Method;
                             ESP=M‚todo redondeo];
                  SourceExpr=RoundingMethod.Code;
                  TableRelation="Rounding Method" }

      { 7   ;2   ;Field     ;
                  CaptionML=[ENU=Create New Prices;
                             ESP=Crear tarifa nueva];
                  SourceExpr=CreateNewPrices }

    }
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=Processing items  #1##########;ESP=Procesando prods. #1##########';
      SalesPrice2@1038 : Record 7002;
      SalesPriceWksh2@1037 : Record 7023;
      SalesPriceWksh@1005 : Record 7023;
      ToCust@1036 : Record 18;
      ToCustPriceGr@1035 : Record 6;
      ToCampaign@1020 : Record 5071;
      ToUnitOfMeasure@1006 : Record 204;
      ItemUnitOfMeasure@1007 : Record 5404;
      ToCurrency@1008 : Record 4;
      FromCurrency@1009 : Record 4;
      Currency2@1010 : Record 4;
      CurrExchRate@1011 : Record 330;
      RoundingMethod@1012 : Record 42;
      Item@1003 : Record 27;
      UOMMgt@1014 : Codeunit 5402;
      Window@1015 : Dialog;
      PriceAlreadyExists@1016 : Boolean;
      CreateNewPrices@1017 : Boolean;
      UnitPriceFactor@1018 : Decimal;
      PriceLowerLimit@1019 : Decimal;
      ToSalesType@1033 : 'Customer,Customer Price Group,All Customers,Campaign';
      ToSalesCode@1032 : Code[20];
      ToStartDate@1031 : Date;
      ToEndDate@1030 : Date;
      ReplaceSalesCode@1022 : Boolean;
      ReplaceUnitOfMeasure@1023 : Boolean;
      ReplaceCurrency@1024 : Boolean;
      ReplaceStartingDate@1025 : Boolean;
      ReplaceEndingDate@1002 : Boolean;
      Text002@1004 : TextConst 'ENU=Sales Code must be specified when copying from %1 to All Customers.;ESP=Se debe especificar C¢d. venta al copiar desde %1 a Todos clientes.';
      SalesCodeCtrlEnable@19004995 : Boolean INDATASET;
      ToStartDateCtrlEnable@19060085 : Boolean INDATASET;
      ToEndDateCtrlEnable@19023823 : Boolean INDATASET;

    PROCEDURE InitializeRequest@3(NewToSalesType@1005 : 'Customer,Customer Price Group,Campaign,All CUstomers';NewToSalesCode@1004 : Code[20];NewToStartDate@1002 : Date;NewToEndDate@1000 : Date;NewToCurrCode@1001 : Code[10];NewToUOMCode@1003 : Code[10];NewCreateNewPrices@1006 : Boolean);
    BEGIN
      ToSalesType := NewToSalesType;
      ToSalesCode := NewToSalesCode;
      ToStartDate := NewToStartDate;
      ToEndDate := NewToEndDate;
      ToCurrency.Code := NewToCurrCode;
      ToUnitOfMeasure.Code := NewToUOMCode;
      CreateNewPrices := NewCreateNewPrices;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
