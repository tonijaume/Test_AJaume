OBJECT Report 7009729 Transfer - Gestion/Contabilida
{
  OBJECT-PROPERTIES
  {
    Date=19/12/14;
    Time=[ 9:25:09];
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Transfer - Operations/Accountancy;
               ESP=Transfer - Operaciones/Contabilidad];
    ProcessingOnly=Yes;
  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table7009717;
        DataItemTableView=SORTING(TourOperador);
        DataItemVarName=diTouroperador;
        OnPreDataItem=BEGIN
                        rCompanyInfo.FINDFIRST;
                        rDelegacion.FINDFIRST;

                        cExcel.Nuevo;

                        wFila := 1;
                        cExcel.ValorCelda(wFila, 1, rCompanyInfo.Name, TRUE, FALSE, FALSE);
                        cExcel.ValorCelda(wFila, 6, rDelegacion.Codigo, TRUE, FALSE, FALSE);

                        wFila += 2;
                        cExcel.ValorCelda(wFila, 1, Text001, TRUE, FALSE, FALSE);
                        cExcel.ValorCelda2(wFila, 2, USERID);

                        wFila += 1;
                        cExcel.ValorCelda(wFila, 1, Text002, TRUE, FALSE, FALSE);
                        cExcel.ValorCelda2(wFila, 2, FORMAT(TODAY) + ' ' + FORMAT(TIME, 0, '<Hours24,2>:<Minutes,2>'));

                        wFila += 1;
                        cExcel.ValorCelda(wFila, 1, Text008, TRUE, FALSE, FALSE);
                        cExcel.ValorCelda2(wFila, 2, wFiltroFecha);

                        wFila += 1;
                        cExcel.ValorCelda(wFila, 1, Text009, TRUE, FALSE, FALSE);
                        cExcel.ValorCelda2(wFila, 2, wFiltroCuentas);


                        wFila += 2;
                        cExcel.ValorCelda(wFila, 1, UPPERCASE(FIELDCAPTION(TourOperador)), TRUE, FALSE, FALSE);
                        cExcel.ValorCelda(wFila, 2, UPPERCASE(FIELDCAPTION(Nombre)), TRUE, FALSE, FALSE);
                        cExcel.ValorCelda(wFila, 3, Text003, TRUE, FALSE, FALSE);
                        cExcel.ValorCelda(wFila, 4, Text004, TRUE, FALSE, FALSE);
                        cExcel.ValorCelda(wFila, 5, Text005, TRUE, FALSE, FALSE);
                        cExcel.ValorCelda(wFila, 6, Text007, TRUE, FALSE, FALSE);
                      END;

        OnAfterGetRecord=BEGIN
                           wImporteGestion   := 0;
                           wGestionPendiente := 0;
                           wImporteContab    := 0;

                           rIngreso.RESET;
                           rIngreso.SETCURRENTKEY(Fecha, "Transfer Privado", TourOperador, "Proveedor contable", Destino, Concepto, "Tipo transfer");
                           rIngreso.SETRANGE (TourOperador, TourOperador);
                           rIngreso.SETFILTER(Fecha       , wFiltroFecha);
                           rIngreso.SETRANGE (Concepto    , 'TRNI');
                           IF rIngreso.FINDFIRST THEN
                           BEGIN
                             rIngreso.CALCSUMS("Importe sin impuestos (DL)");
                             wImporteGestion := rIngreso."Importe sin impuestos (DL)";
                           END;

                           rIngreso.SETRANGE("N§ Factura", '');
                           IF rIngreso.FINDFIRST THEN
                           BEGIN
                             REPEAT
                               wGestionPendiente += rIngreso."Importe sin impuestos (DL)";
                             UNTIL rIngreso.NEXT = 0;
                           END;

                           rMovCon.RESET;
                           rMovCon.SETCURRENTKEY("G/L Account No.", "Global Dimension 1 Code", "Global Dimension 2 Code", "Posting Date");
                           rMovCon.SETFILTER("Posting Date"           , wFiltroFecha);
                           rMovCon.SETRANGE ("Global Dimension 2 Code", TourOperador);
                           rMovCon.SETFILTER("G/L Account No."        , wFiltroCuentas);
                           IF rMovCon.FINDFIRST THEN
                           BEGIN
                             rMovCon.CALCSUMS(Amount);
                             wImporteContab := rMovCon.Amount;
                           END;

                           IF (wImporteGestion <> 0) OR (wImporteContab <> 0) THEN
                           BEGIN
                             wFila += 1;
                             cExcel.ValorCelda2(wFila, 1, TourOperador);
                             cExcel.ValorCelda2(wFila, 2, Nombre);
                             cExcel.ValorCelda2(wFila, 3, FORMAT(wImporteGestion));
                             cExcel.ValorCelda2(wFila, 4, FORMAT(wGestionPendiente));
                             cExcel.ValorCelda2(wFila, 5, FORMAT(wImporteContab));

                             wTextFormula := '=+C' + FORMAT(wFila) + '+E' + FORMAT(wFila);
                             cExcel.ValorCelda2(wFila, 6, wTextFormula);
                           END;
                         END;

        OnPostDataItem=BEGIN
                         wFila += 1;
                         wTextFormula := '=SUM(C7:C' + FORMAT(wFila-1) + ')';
                         cExcel.ValorCelda(wFila, 3, wTextFormula, TRUE, FALSE, FALSE);

                         wTextFormula := '=SUM(D7:D' + FORMAT(wFila-1) + ')';
                         cExcel.ValorCelda(wFila, 4, wTextFormula, TRUE, FALSE, FALSE);

                         wTextFormula := '=SUM(E7:E' + FORMAT(wFila-1) + ')';
                         cExcel.ValorCelda(wFila, 5, wTextFormula, TRUE, FALSE, FALSE);

                         wTextFormula := '=SUM(F7:F' + FORMAT(wFila-1) + ')';
                         cExcel.ValorCelda(wFila, 6, wTextFormula, TRUE, FALSE, FALSE);

                         cExcel.JustificarHoja;
                         cExcel.Mostrar;
                       END;

      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=9020;
      Height=3410;
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1000000000;Label  ;220  ;220  ;3300 ;440  ;ParentControl=1000000001;
                                                   InPage=-1 }
      { 1000000001;TextBox;3630 ;220  ;5060 ;440  ;CaptionML=ESP=Filtro fecha;
                                                   SourceExpr=wFiltroFecha }
      { 1000000002;TextBox;3630 ;770  ;5060 ;440  ;ValidateTableRelation=No;
                                                   InPage=-1;
                                                   CaptionML=ESP=Filtro cuenta;
                                                   ClearOnLookup=No;
                                                   SourceExpr=wFiltroCuentas;
                                                   TableRelation="G/L Account" }
      { 1000000003;Label  ;220  ;770  ;3300 ;440  ;ParentControl=1000000002 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      wFiltroFecha@1000000000 : Text[250];
      wFiltroCuentas@1000000001 : Text[250];
      wTextFormula@1000000009 : Text[150];
      wImporteGestion@1000000005 : Decimal;
      wGestionPendiente@1000000006 : Decimal;
      wImporteContab@1000000007 : Decimal;
      wFila@1000000008 : Integer;
      rIngreso@1000000002 : Record 7009805;
      rMovCon@1000000003 : Record 17;
      rCompanyInfo@1000000010 : Record 79;
      rDelegacion@1000000011 : Record 7009718;
      cExcel@1000000004 : Codeunit 7009755;
      Text001@1000000012 : TextConst 'ENU=Generated by;ESP=Generado por';
      Text002@1000000013 : TextConst 'ENU=Generated day;ESP=Generado dia';
      Text003@1000000014 : TextConst 'ENU=OPERATIONS AMOUNT;ESP=IMPORTE OPERACIONES';
      Text004@1000000015 : TextConst 'ENU=OPERATIONS W/O INVOICE;ESP=OPERACIONES SIN FACTURAR';
      Text005@1000000016 : TextConst 'ENU=ACC. POSTED AMOUNT;ESP=CONT. IMPORTE REGISTRADO';
      Text006@1000000017 : TextConst 'ESP=CONT. SIN REGISTRAR';
      Text007@1000000018 : TextConst 'ENU=DIFFERENCE;ESP=DIFERENCIA';
      Text008@1000000019 : TextConst 'ENU=Date filter;ESP=Filtro fecha';
      Text009@1000000020 : TextConst 'ENU=G/L Account filter;ESP=Filtro cuentas';

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
