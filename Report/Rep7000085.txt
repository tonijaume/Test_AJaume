OBJECT Report 7000085 Partial Settl. - Payable
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVES6.00.01;
  }
  PROPERTIES
  {
    Permissions=TableData 21=imd,
                TableData 25=imd,
                TableData 45=m,
                TableData 7000003=imd,
                TableData 7000004=imd,
                TableData 7000021=imd,
                TableData 7000022=imd;
    CaptionML=[ENU=Partial Settl. - Payable;
               ESP=Liq. parcial - Pagos];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   PostingDate := WORKDATE;
                   RemainingAmt := 0;
                   AppliedAmt := 0;
                   CurrencyCode := '';
                 END;

    OnPreReport=BEGIN
                  GLSetup.GET;
                END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table7000003;
        DataItemTableView=SORTING(Bill Gr./Pmt. Order No.,Status,Category Code,Redrawn,Due Date)
                          WHERE(Status=CONST(Open));
        DataItemVarName=PostedDoc;
        OnPreDataItem=BEGIN
                        DocPost.CheckPostingDate(PostingDate);

                        SourceCodeSetup.GET;
                        SourceCode := SourceCodeSetup."Cartera Journal";
                        DocCount := 0;
                        SumLCYAmt := 0;
                        GenJnlLineNextNo := 0;
                        ExistVATEntry := FALSE;
                        Window.OPEN(
                          Text1100000);
                      END;

        OnAfterGetRecord=VAR
                           FromJnl@1100001 : Boolean;
                         BEGIN
                           IsRedrawn := CarteraManagement.CheckFromRedrawnDoc(PostedDoc."No.");
                           IF PostedPmtOrd."No." = '' THEN BEGIN
                             PostedPmtOrd.GET(PostedDoc."Bill Gr./Pmt. Order No.");
                             BankAcc.GET(PostedPmtOrd."Bank Account No.");
                             Delay := BankAcc."Delay for Notices";
                           END;

                           RemainingAmt2 := "Remaining Amount" - AppliedAmt;

                           DocCount := DocCount + 1;
                           Window.UPDATE(1,DocCount);

                           CASE "Document Type" OF
                             PostedDoc."Document Type"::Invoice,PostedDoc."Document Type"::"1":
                               BEGIN
                                 WITH GenJnlLine DO BEGIN
                                   GenJnlLineNextNo := GenJnlLineNextNo + 10000;
                                   CLEAR(GenJnlLine);
                                   INIT;
                                   "Line No." := GenJnlLineNextNo;
                                   "Posting Date" := PostingDate;
                                   "Reason Code" := PostedPmtOrd."Reason Code";
                                   GenJnlLine."Document Date" := "Document Date";
                                   VALIDATE("Account Type","Account Type"::Vendor);
                                   VendLedgEntry.GET(PostedDoc."Entry No.");
                                   VALIDATE("Account No.",VendLedgEntry."Vendor No.");
                                   "Document Type" := "Document Type"::Payment;
                                   Description := COPYSTR(STRSUBSTNO(Text1100001,PostedDoc."Document No."),1,MAXSTRLEN(Description));
                                   "Document No." := PostedPmtOrd."No.";
                                   VALIDATE("Currency Code",PostedDoc."Currency Code");
                                   VALIDATE(Amount,AppliedAmt);
                                   "Applies-to Doc. Type" := VendLedgEntry."Document Type";
                                   "Applies-to Doc. No." := VendLedgEntry."Document No.";
                                   "Applies-to Bill No." := VendLedgEntry."Bill No.";
                                   "Source Code" := SourceCode;
                                   "Global Dimension 1 Code" := VendLedgEntry."Global Dimension 1 Code";
                                   "System-Created Entry" := TRUE;
                                   "Shortcut Dimension 1 Code" := VendLedgEntry."Global Dimension 1 Code";
                                   "Shortcut Dimension 2 Code" := VendLedgEntry."Global Dimension 2 Code";
                                   INSERT;
                                   CarteraDimMgt.VendInsertJnlLinDimension(GenJnlLine,VendLedgEntry,TRUE);
                                   SumLCYAmt := SumLCYAmt + "Amount (LCY)";
                                 END;

                                 IF AppliedAmt = RemainingAmt THEN BEGIN
                                   VendLedgEntry."Document Status" := VendLedgEntry."Document Status"::Honored;
                                   VendLedgEntry.MODIFY;
                                   RemainingAmt2 := 0;
                                 END;
                               END;
                             PostedDoc."Document Type"::Bill:
                               BEGIN
                                 WITH GenJnlLine DO BEGIN
                                   GenJnlLineNextNo := GenJnlLineNextNo + 10000;
                                   CLEAR(GenJnlLine);
                                   INIT;
                                   "Line No." := GenJnlLineNextNo;
                                   "Posting Date" := PostingDate;
                                   "Document Type" := "Document Type"::Payment;
                                   "Document No." := PostedPmtOrd."No.";
                                   "Reason Code" := PostedPmtOrd."Reason Code";
                                   VALIDATE("Account Type","Account Type"::Vendor);
                                   VendLedgEntry.GET(PostedDoc."Entry No.");

                                   IF GLSetup."Unrealized VAT" THEN BEGIN
                                     FromJnl := FALSE;
                                     IF PostedDoc."From Journal" THEN
                                       FromJnl := TRUE;
                                     ExistsNoRealVAT := GenJnlPostLine.VendFindVATSetup(VATPostingSetup,VendLedgEntry,FromJnl);
                                   END;

                                   VALIDATE("Account No.",VendLedgEntry."Vendor No.");
                                   Description := COPYSTR(STRSUBSTNO(Text1100002,PostedDoc."Document No.",PostedDoc."No."),1,MAXSTRLEN(Description));
                                   VALIDATE("Currency Code",PostedDoc."Currency Code");
                                   VALIDATE(Amount,AppliedAmt);
                                   "Applies-to Doc. Type" := VendLedgEntry."Document Type";
                                   "Applies-to Doc. No." := VendLedgEntry."Document No.";
                                   "Applies-to Bill No." := VendLedgEntry."Bill No.";
                                   "Source Code" := SourceCode;
                                   "System-Created Entry" := TRUE;
                                   "Shortcut Dimension 1 Code" := VendLedgEntry."Global Dimension 1 Code";
                                   "Shortcut Dimension 2 Code" := VendLedgEntry."Global Dimension 2 Code";
                                   INSERT;
                                   CarteraDimMgt.VendInsertJnlLinDimension(GenJnlLine,VendLedgEntry,TRUE);
                                   SumLCYAmt := SumLCYAmt + "Amount (LCY)";
                                 END;
                                 IF GLSetup."Unrealized VAT" AND
                                    ExistsNoRealVAT AND
                                    (NOT IsRedrawn) THEN BEGIN
                                   CarteraManagement.VendUnrealizedVAT2(
                                     VendLedgEntry,
                                     -AppliedAmt,
                                     GenJnlLine,
                                     ExistVATEntry,
                                     FirstVATEntryNo,
                                     LastVATEntryNo,
                                     NoRealVATBuffer,
                                     FromJnl);

                                   IF NoRealVATBuffer.FIND('-') THEN BEGIN
                                     REPEAT
                                       BEGIN
                                         InsertGenJournalLine(
                                           GenJnlLine."Account Type"::"G/L Account",
                                           NoRealVATBuffer.Account,
                                           NoRealVATBuffer.Amount);
                                         InsertGenJournalLine(
                                           GenJnlLine."Account Type"::"G/L Account",
                                           NoRealVATBuffer."Balance Account",
                                           -NoRealVATBuffer.Amount);
                                       END;
                                     UNTIL NoRealVATBuffer.NEXT = 0;
                                   END;

                                 END;

                                 IF AppliedAmt = PostedDoc."Remaining Amount" THEN BEGIN
                                   VendLedgEntry."Document Status" := VendLedgEntry."Document Status"::Honored;
                                   VendLedgEntry.MODIFY;
                                 END;
                               END;
                           END;

                           Dep := "Global Dimension 1 Code";
                           Proj := "Global Dimension 2 Code";
                         END;

        OnPostDataItem=VAR
                         JnlLineDim@1100000 : Record 356;
                       BEGIN
                         IF DocCount = 0 THEN
                           ERROR(
                             Text1100003 +
                             Text1100004);

                         GenJnlLineNextNo := GenJnlLineNextNo + 10000;
                         WITH GenJnlLine DO BEGIN
                           CLEAR(GenJnlLine);
                           INIT;
                           "Line No." := GenJnlLineNextNo;
                           "Posting Date" := PostingDate;
                           "Document Type" := "Document Type"::Payment;
                           "Document No." := PostedPmtOrd."No.";
                           "Reason Code" := PostedPmtOrd."Reason Code";
                           VALIDATE("Account Type",GenJnlLine."Account Type"::"Bank Account");
                           VALIDATE("Account No.",BankAcc."No.");
                           IF PostedDoc."Document Type" = PostedDoc."Document Type"::Bill THEN
                             Description := COPYSTR(STRSUBSTNO(Text1100002,PostedDoc."Document No.",PostedDoc."No."),1,MAXSTRLEN(Description))
                           ELSE
                             Description := COPYSTR(STRSUBSTNO(Text1100001,PostedDoc."Document No."),1,MAXSTRLEN(Description));
                           VALIDATE("Currency Code",PostedPmtOrd."Currency Code");
                           VALIDATE(Amount,-AppliedAmt);
                           "Source Code" := SourceCode;
                           "System-Created Entry" := TRUE;
                           "Shortcut Dimension 1 Code" := Dep;
                           "Shortcut Dimension 2 Code" := Proj;
                           INSERT;
                           CarteraDimMgt.VendInsertJnlLinDimension(GenJnlLine,VendLedgEntry,TRUE);
                           SumLCYAmt := SumLCYAmt + "Amount (LCY)";
                         END;

                         IF PostedPmtOrd."Currency Code" <> '' THEN BEGIN
                           IF SumLCYAmt <> 0 THEN BEGIN
                             Currency.SETFILTER(Code,PostedPmtOrd."Currency Code");
                             Currency.FIND('-');
                             IF SumLCYAmt > 0 THEN BEGIN
                               Currency.TESTFIELD("Residual Gains Account");
                               Acct := Currency."Residual Gains Account";
                             END ELSE BEGIN
                               Currency.TESTFIELD("Residual Losses Account");
                               Acct := Currency."Residual Losses Account";
                             END;
                             GenJnlLineNextNo := GenJnlLineNextNo + 10000;
                             WITH GenJnlLine DO BEGIN
                               CLEAR(GenJnlLine);
                               INIT;
                               "Line No." := GenJnlLineNextNo;
                               "Posting Date" := PostingDate;
                               "Document Type" := "Document Type"::Payment;
                               "Document No." := PostedPmtOrd."No.";
                               "Reason Code" := PostedPmtOrd."Reason Code";
                               VALIDATE("Account Type","Account Type"::"G/L Account");
                               VALIDATE("Account No.",Acct);
                               Description := Text1100005;
                               VALIDATE("Currency Code",'');
                               VALIDATE(Amount,-SumLCYAmt);
                               "Source Code" := SourceCode;
                               "System-Created Entry" := TRUE;
                               INSERT;
                               CarteraDimMgt.VendInsertJnlLinDimension(GenJnlLine,VendLedgEntry,TRUE);
                             END;
                           END;
                         END;
                         IF GenJnlLine.FIND('-') THEN
                           REPEAT
                             GenJnlLine2 := GenJnlLine;
                             GenJnlPostLine.SetFromSettlement(TRUE);
                             JnlLineDim.SETRANGE("Table ID",DATABASE::"Gen. Journal Line");
                             JnlLineDim.SETRANGE("Journal Template Name",GenJnlLine2."Journal Template Name");
                             JnlLineDim.SETRANGE("Journal Batch Name",GenJnlLine2."Journal Batch Name");
                             JnlLineDim.SETRANGE("Journal Line No.",GenJnlLine2."Line No.");
                             GenJnlPostLine.RunWithCheck(GenJnlLine2,JnlLineDim);
                           UNTIL GenJnlLine.NEXT = 0;


                         VendLedgEntry.GET(PostedDoc."Entry No.");
                         PostedDoc.GET(PostedDoc.Type,PostedDoc."Entry No.");
                         PostedDoc."Honored/Rejtd. at Date" := PostingDate;
                         PostedDoc."Remaining Amount" := RemainingAmt2;
                         PostedDoc."Remaining Amt. (LCY)" := ROUND(CurrExchRate.ExchangeAmtFCYToLCY(
                           VendLedgEntry."Posting Date",
                           VendLedgEntry."Currency Code",
                           RemainingAmt2,
                           CurrExchRate.ExchangeRate(VendLedgEntry."Posting Date",VendLedgEntry."Currency Code")),
                           GLSetup."Amount Rounding Precision");
                         IF RemainingAmt2 = 0 THEN BEGIN
                           PostedDoc."Remaining Amt. (LCY)" := 0;
                           VendLedgEntry."Document Status" := VendLedgEntry."Document Status"::Honored;
                           PostedDoc.Status := PostedDoc.Status::Honored;
                           VendLedgEntry.Open := FALSE;
                         END ELSE BEGIN
                           VendLedgEntry."Document Status" := VendLedgEntry."Document Status"::Open;
                           PostedDoc.Status := PostedDoc.Status::Open;
                           VendLedgEntry.Open := TRUE;
                         END;

                         VendLedgEntry.MODIFY;
                         PostedDoc.MODIFY;

                         DocPost.ClosePmtOrdIfEmpty(PostedPmtOrd,PostingDate);

                         Window.CLOSE;

                         IF ExistVATEntry THEN BEGIN
                           GLReg.FIND('+');
                           GLReg."From VAT Entry No." := FirstVATEntryNo;
                           GLReg."To VAT Entry No." := LastVATEntryNo;
                           GLReg.MODIFY;
                         END;

                         COMMIT;

                         MESSAGE(
                           Text1100006,
                           DocCount,RemainingAmt,PostedPmtOrd."No.",AppliedAmt);
                       END;

      }
      SECTIONS
      {
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=5060;
      Height=2310;
      SaveValues=No;
    }
    CONTROLS
    {
      { 1   ;TextBox      ;3410 ;1870 ;1650 ;440  ;Editable=Yes;
                                                   CaptionML=[ENU=Settled Amount;
                                                              ESP=Importe liquidado];
                                                   NotBlank=Yes;
                                                   SourceExpr=AppliedAmt;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=CurrencyCode;
                                                   MinValue=0;
                                                   OnValidate=BEGIN
                                                                IF AppliedAmt > RemainingAmt THEN
                                                                  ERROR(Text1100007,RemainingAmt);
                                                              END;
                                                               }
      { 2   ;TextBox      ;3410 ;660  ;1650 ;440  ;Editable=No;
                                                   CaptionML=[ENU=Remaining Amount;
                                                              ESP=Importe pendiente];
                                                   SourceExpr=RemainingAmt;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=CurrencyCode }
      { 3   ;TextBox      ;3410 ;1210 ;1650 ;440  ;Editable=No;
                                                   CaptionML=[ENU=Currency Code;
                                                              ESP=C¢d. divisa];
                                                   SourceExpr=CurrencyCode;
                                                   TableRelation=Currency }
      { 4   ;TextBox      ;3410 ;0    ;1650 ;440  ;CaptionML=[ENU=Posting Date;
                                                              ESP=Fecha registro];
                                                   NotBlank=Yes;
                                                   SourceExpr=PostingDate;
                                                   OnValidate=VAR
                                                                VendLedgEntry2@1100000 : Record 25;
                                                              BEGIN
                                                                VendLedgEntry2.GET(VendLedgEntryNo);
                                                                IF VendLedgEntry2."Document Type" = VendLedgEntry2."Document Type"::Invoice THEN BEGIN
                                                                  VendLedgEntry2.CALCFIELDS("Remaining Amount");
                                                                  IF PostingDate > VendLedgEntry2."Pmt. Discount Date" THEN
                                                                    RemainingAmt := -VendLedgEntry2."Remaining Amount"
                                                                  ELSE
                                                                    RemainingAmt := -VendLedgEntry2."Remaining Amount" + VendLedgEntry2."Remaining Pmt. Disc. Possible";
                                                                  AppliedAmt := RemainingAmt;
                                                                END;
                                                              END;
                                                               }
      { 5   ;Label        ;0    ;0    ;3300 ;440  ;ParentControl=4 }
      { 6   ;Label        ;0    ;660  ;3300 ;440  ;ParentControl=2 }
      { 7   ;Label        ;0    ;1870 ;3300 ;440  ;ParentControl=1 }
      { 8   ;Label        ;0    ;1210 ;3300 ;440  ;ParentControl=3 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=No;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones] }

      { 4   ;2   ;Field     ;
                  CaptionML=[ENU=Posting Date;
                             ESP=Fecha registro];
                  NotBlank=Yes;
                  SourceExpr=PostingDate;
                  OnValidate=VAR
                               VendLedgEntry2@1100000 : Record 25;
                             BEGIN
                               VendLedgEntry2.GET(VendLedgEntryNo);
                               IF VendLedgEntry2."Document Type" = VendLedgEntry2."Document Type"::Invoice THEN BEGIN
                                 VendLedgEntry2.CALCFIELDS("Remaining Amount");
                                 IF PostingDate > VendLedgEntry2."Pmt. Discount Date" THEN
                                   RemainingAmt := -VendLedgEntry2."Remaining Amount"
                                 ELSE
                                   RemainingAmt := -VendLedgEntry2."Remaining Amount" + VendLedgEntry2."Remaining Pmt. Disc. Possible";
                                 AppliedAmt := RemainingAmt;
                               END;
                             END;
                              }

      { 2   ;2   ;Field     ;
                  CaptionML=[ENU=Remaining Amount;
                             ESP=Importe pendiente];
                  SourceExpr=RemainingAmt;
                  AutoFormatType=1;
                  AutoFormatExpr=CurrencyCode;
                  Editable=FALSE }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Currency Code;
                             ESP=C¢d. divisa];
                  SourceExpr=CurrencyCode;
                  TableRelation=Currency;
                  Editable=FALSE }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Settled Amount;
                             ESP=Importe liquidado];
                  NotBlank=Yes;
                  SourceExpr=AppliedAmt;
                  AutoFormatType=1;
                  AutoFormatExpr=CurrencyCode;
                  MinValue=0;
                  Editable=TRUE;
                  OnValidate=BEGIN
                               IF AppliedAmt > RemainingAmt THEN
                                 ERROR(Text1100007,RemainingAmt);
                             END;
                              }

    }
  }
  CODE
  {
    VAR
      Text1100000@1000 : TextConst 'ENU=Settling payable documents       #1######;ESP=Liquidando docs. a pagar         #1######';
      Text1100001@1001 : TextConst 'ENU=Partial Document settlement %1;ESP=Liq. parcial documento %1';
      Text1100002@1002 : TextConst 'ENU=Partial Bill settlement %1/%2;ESP=Liq. parcial efecto %1/%2';
      Text1100003@1003 : TextConst 'ENU=No payable documents have been found that can be settled. \;ESP=No hay documento a pagar que liquidar. \';
      Text1100004@1004 : TextConst 'ENU=Please check that the selection is not empty and at least one payable document is open.;ESP=Compruebe que ha seleccionado al menos un documento a pagar pendiente.';
      Text1100005@1005 : TextConst 'ENU=Residual adjust generated by rounding Amount;ESP=Ajuste por redondeo';
      Text1100006@1006 : TextConst 'ENU=%1 payable documents totaling %2 have been partially settled in Payment Order %3 by an amount of %4.;ESP=Se ha liquidado parcialmente %1 documento de un total de %2 de la orden pago %3 por un importe de %4.';
      Text1100007@1007 : TextConst 'ENU=The maximum permitted value is %1;ESP=El m ximo valor permitido es %1';
      Text1100008@1008 : TextConst 'ENU=Partial payable document settlement %1/%2;ESP=Liquidaci¢n parcial doc. a pagar %1/%2';
      Text1100009@1009 : TextConst 'ENU=Partial payable document settlement %1;ESP=Liquidaci¢n parcial doc. a pagar %1';
      SourceCodeSetup@1100000 : Record 242;
      PostedPmtOrd@1100001 : Record 7000021;
      GenJnlLine@1100002 : TEMPORARY Record 81;
      GenJnlLine2@1100003 : Record 81;
      VendLedgEntry@1100004 : Record 25;
      BankAcc@1100006 : Record 270;
      CurrExchRate@1100007 : Record 330;
      Currency@1100008 : Record 4;
      GLReg@1100009 : Record 45;
      GLSetup@1100010 : Record 98;
      VATPostingSetup@1100011 : Record 325;
      NoRealVATBuffer@1100035 : TEMPORARY Record 7000012;
      DocPost@1100012 : Codeunit 7000006;
      CarteraManagement@1100013 : Codeunit 7000000;
      GenJnlPostLine@1100014 : Codeunit 12;
      CarteraDimMgt@1100033 : Codeunit 7000011;
      Window@1100015 : Dialog;
      PostingDate@1100016 : Date;
      Delay@1100017 : Decimal;
      SourceCode@1100018 : Code[10];
      Acct@1100019 : Code[20];
      DocCount@1100020 : Integer;
      GenJnlLineNextNo@1100021 : Integer;
      SumLCYAmt@1100022 : Decimal;
      CurrencyCode@1100023 : Code[10];
      RemainingAmt@1100024 : Decimal;
      RemainingAmt2@1100025 : Decimal;
      AppliedAmt@1100026 : Decimal;
      ExistVATEntry@1100027 : Boolean;
      FirstVATEntryNo@1100028 : Integer;
      LastVATEntryNo@1100029 : Integer;
      IsRedrawn@1100030 : Boolean;
      Dep@1100031 : Code[20];
      Proj@1100032 : Code[20];
      ExistsNoRealVAT@1100034 : Boolean;
      VendLedgEntryNo@1100036 : Integer;

    PROCEDURE SetInitValue@2(Amount@1100000 : Decimal;CurrCode@1100001 : Code[10];EntryNo@1100002 : Integer);
    BEGIN
      CurrencyCode := CurrCode;
      RemainingAmt := Amount;
      AppliedAmt := RemainingAmt;
      VendLedgEntryNo := EntryNo;
    END;

    LOCAL PROCEDURE InsertGenJournalLine@4(AccType@1100000 : Integer;AccNo@1100001 : Code[20];Amount2@1100002 : Decimal);
    BEGIN
      GenJnlLineNextNo := GenJnlLineNextNo + 10000;

      WITH GenJnlLine DO BEGIN
        CLEAR(GenJnlLine);
        INIT;
        "Line No." := GenJnlLineNextNo;
        "Posting Date" := PostingDate;
        "Document No." := PostedPmtOrd."No.";
        "Reason Code" := PostedPmtOrd."Reason Code";
        "Account Type" := AccType;
        "Account No." := AccNo;
        IF PostedDoc."Document Type" = PostedDoc."Document Type"::Bill THEN
          Description := COPYSTR(STRSUBSTNO(Text1100008,PostedDoc."Document No.",PostedDoc."No."),1,MAXSTRLEN(Description))
        ELSE
          Description := COPYSTR(STRSUBSTNO(Text1100009,PostedDoc."Document No."),1,MAXSTRLEN(Description));
        VALIDATE("Currency Code",PostedDoc."Currency Code");
        VALIDATE(Amount,-Amount2);
        "Applies-to Doc. Type" := VendLedgEntry."Document Type";
        "Applies-to Doc. No." := '';
        "Applies-to Bill No." := VendLedgEntry."Bill No.";
        "Source Code" := SourceCode;
        "System-Created Entry" := TRUE;
        "Shortcut Dimension 1 Code" := VendLedgEntry."Global Dimension 1 Code";
        "Shortcut Dimension 2 Code" := VendLedgEntry."Global Dimension 2 Code";
        INSERT;
        CarteraDimMgt.VendInsertJnlLinDimension(GenJnlLine,VendLedgEntry,TRUE);
      END;
    END;

    PROCEDURE GetAmtLCY@7(Amount@1100000 : Decimal) : Decimal;
    VAR
      CurrencyExchRate@1100001 : Record 330;
      GLSetup@1100002 : Record 98;
    BEGIN
      IF PostedDoc."Currency Code" = '' THEN
        EXIT(Amount)
      ELSE BEGIN
        GLSetup.GET;
        EXIT(
          ROUND(
            CurrencyExchRate.ExchangeAmtFCYToLCY(
              PostingDate,PostedDoc."Currency Code",
              Amount,
              CurrExchRate.ExchangeRate(PostingDate,PostedDoc."Currency Code")),
            GLSetup."Amount Rounding Precision"));
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
