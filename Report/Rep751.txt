OBJECT Report 751 Save as Standard Item Journal
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Save as Standard Item Journal;
               ESP=Guardar como Diario productos est ndar];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF Code = '' THEN
                    ERROR(Text000);

                  StdJournalCreated := FALSE;
                  SaveItemJnlAsStandardJnl;
                END;

  }
  DATAITEMS
  {
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=9020;
      Height=3410;
      OnOpenForm=BEGIN
                   RequestOptionsForm.Code.ACTIVATE;
                 END;

    }
    CONTROLS
    {
      { 1   ;Label        ;0    ;110  ;3300 ;440  ;ParentControl=2;
                                                   CaptionML=[ENU=Code;
                                                              ESP=C¢digo] }
      { 2   ;TextBox      ;3410 ;110  ;2750 ;440  ;Name=Code;
                                                   NotBlank=Yes;
                                                   SourceExpr=Code;
                                                   OnLookup=VAR
                                                              StdItemJnl@1000 : Record 752;
                                                              StdItemJnls@1001 : Form 753;
                                                            BEGIN
                                                              StdItemJnl.SETRANGE("Journal Template Name",ItemJnlBatch."Journal Template Name");
                                                              StdItemJnls.SETTABLEVIEW(StdItemJnl);

                                                              StdItemJnls.LOOKUPMODE := TRUE;
                                                              StdItemJnls.EDITABLE := FALSE;
                                                              IF StdItemJnls.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                StdItemJnls.GETRECORD(StdItemJnl);
                                                                Code := StdItemJnl.Code;
                                                                Description := StdItemJnl.Description;
                                                              END;
                                                            END;
                                                             }
      { 3   ;Label        ;0    ;660  ;3300 ;440  ;ParentControl=8;
                                                   CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n] }
      { 4   ;Label        ;0    ;1320 ;3300 ;440  ;ParentControl=5 }
      { 5   ;CheckBox     ;3410 ;1320 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Save Unit Amount;
                                                              ESP=Guardar precio unitario];
                                                   SourceExpr=SaveUnitAmount }
      { 6   ;Label        ;0    ;1870 ;3300 ;440  ;ParentControl=7 }
      { 7   ;CheckBox     ;3410 ;1870 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Save Quantity;
                                                              ESP=Guardar cantidad];
                                                   SourceExpr=SaveQuantity }
      { 8   ;TextBox      ;3410 ;660  ;5500 ;440  ;SourceExpr=Description }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones] }

      { 2   ;2   ;Field     ;
                  Name=Code;
                  CaptionML=[ENU=Code;
                             ESP=C¢digo];
                  NotBlank=Yes;
                  SourceExpr=Code;
                  OnLookup=VAR
                             StdItemJnl@1000 : Record 752;
                             StdItemJnls@1001 : Form 753;
                           BEGIN
                             StdItemJnl.SETRANGE("Journal Template Name",ItemJnlBatch."Journal Template Name");
                             StdItemJnls.SETTABLEVIEW(StdItemJnl);

                             StdItemJnls.LOOKUPMODE := TRUE;
                             StdItemJnls.EDITABLE := FALSE;
                             IF StdItemJnls.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               StdItemJnls.GETRECORD(StdItemJnl);
                               Code := StdItemJnl.Code;
                               Description := StdItemJnl.Description;
                             END;
                           END;
                            }

      { 8   ;2   ;Field     ;
                  CaptionML=[ENU=Description;
                             ESP=Descripci¢n];
                  SourceExpr=Description }

      { 5   ;2   ;Field     ;
                  CaptionML=[ENU=Save Unit Amount;
                             ESP=Guardar precio unitario];
                  SourceExpr=SaveUnitAmount }

      { 7   ;2   ;Field     ;
                  CaptionML=[ENU=Save Quantity;
                             ESP=Guardar cantidad];
                  SourceExpr=SaveQuantity }

    }
  }
  CODE
  {
    VAR
      Text000@1001 : TextConst 'ENU=Please enter a code for Standard Item Journal.;ESP=Introduzca un c¢digo para el Diario productos est ndar.';
      Text001@1000 : TextConst 'ENU=Standard Item Journal %1 already exists. Do you want to overwrite?;ESP=El Diario productos est ndar %1 ya existe. ¨Desea sobrescribirlo?';
      ItemJnlLine@1007 : Record 83;
      ItemJnlBatch@1006 : Record 233;
      JnlLineDimFrom@1009 : Record 356;
      StdItemJnl@1010 : Record 752;
      Code@1005 : Code[10];
      Description@1004 : Text[50];
      SaveUnitAmount@1003 : Boolean;
      SaveQuantity@1008 : Boolean;
      StdJournalCreated@1002 : Boolean;

    PROCEDURE Initialise@2(VAR SelectedItemJnlLines@1000 : Record 83;SelectedItemJnlBatch@1001 : Record 233);
    BEGIN
      ItemJnlLine.COPY(SelectedItemJnlLines);
      ItemJnlBatch := SelectedItemJnlBatch;
    END;

    LOCAL PROCEDURE SaveItemJnlAsStandardJnl@6();
    VAR
      StdItemJnlLine@1000 : Record 753;
    BEGIN
      StdItemJnl.INIT;
      StdItemJnl."Journal Template Name" := ItemJnlBatch."Journal Template Name";
      StdItemJnl.Code := Code;
      StdItemJnl.Description := Description;

      IF StdItemJnlExists THEN
        IF NOT CONFIRM(Text001,FALSE,StdItemJnl.Code) THEN
          EXIT;

      JnlLineDimFrom.LOCKTABLE;
      StdItemJnlLine.LOCKTABLE;
      StdItemJnl.LOCKTABLE;

      IF StdItemJnlExists THEN BEGIN
        StdItemJnl.MODIFY(TRUE);
        StdItemJnlLine.SETRANGE("Journal Template Name",StdItemJnl."Journal Template Name");
        StdItemJnlLine.SETRANGE("Standard Journal Code",StdItemJnl.Code);
        StdItemJnlLine.DELETEALL(TRUE);
      END ELSE
        StdItemJnl.INSERT(TRUE);

      IF ItemJnlLine.FIND('-') THEN
        REPEAT
          StdItemJnlLine."Line No." := 0;
          StdItemJnlLine.INIT;
          StdItemJnlLine."Journal Template Name" := StdItemJnl."Journal Template Name";
          StdItemJnlLine."Standard Journal Code" := StdItemJnl.Code;
          StdItemJnlLine.TRANSFERFIELDS(ItemJnlLine,FALSE);
          IF NOT SaveUnitAmount THEN BEGIN
            StdItemJnlLine."Unit Amount" := 0;
            StdItemJnlLine.Amount := 0;
            StdItemJnlLine."Unit Cost" := 0;
            StdItemJnlLine."Indirect Cost %" := 0;
          END;
          IF NOT SaveQuantity THEN BEGIN
            StdItemJnlLine.Quantity := 0;
            StdItemJnlLine.Amount := 0;
          END;
          StdItemJnlLine.INSERT(TRUE);
          CopyItemJnlLineDims(ItemJnlLine,StdItemJnlLine);
        UNTIL ItemJnlLine.NEXT = 0;

      StdJournalCreated := TRUE;
    END;

    LOCAL PROCEDURE CopyItemJnlLineDims@5(ItemJnlLine@1000 : Record 83;StdItemJnlLine@1001 : Record 753);
    VAR
      DimMgt@1002 : Codeunit 408;
    BEGIN
      JnlLineDimFrom.SETRANGE("Table ID",DATABASE::"Item Journal Line");
      JnlLineDimFrom.SETRANGE("Journal Template Name",ItemJnlLine."Journal Template Name");
      JnlLineDimFrom.SETRANGE("Journal Batch Name",ItemJnlLine."Journal Batch Name");
      JnlLineDimFrom.SETRANGE("Journal Line No.",ItemJnlLine."Line No.");

      DimMgt.MoveJnlLineDimToJnlLineDim(JnlLineDimFrom,DATABASE::"Standard Item Journal Line",
        StdItemJnlLine."Journal Template Name",StdItemJnlLine."Standard Journal Code",StdItemJnlLine."Line No.");
    END;

    LOCAL PROCEDURE StdItemJnlExists@10() : Boolean;
    VAR
      StdItemJnl@1000 : Record 752;
    BEGIN
      StdItemJnl.SETRANGE("Journal Template Name",ItemJnlBatch."Journal Template Name");
      StdItemJnl.SETRANGE(Code,Code);

      EXIT(StdItemJnl.FIND('-'));
    END;

    PROCEDURE GetStdItemJournal@1(VAR StdItemJnl1@1000 : Record 752) : Boolean;
    BEGIN
      IF StdJournalCreated THEN
        StdItemJnl1.COPY(StdItemJnl);

      EXIT(StdJournalCreated);
    END;

    PROCEDURE UTlocalCall@4(localFunctionName@1002 : Text[30];VAR param@1001 : ARRAY [20] OF Variant;VAR return@1000 : Variant);
    VAR
      ItemJnlLine@1004 : Record 83;
      StdItemJnlLine@1003 : Record 753;
    BEGIN
      CASE localFunctionName OF
        'SaveItemJnlAsStandardJnl': SaveItemJnlAsStandardJnl;
        'CopyItemJnlLineDims':
          BEGIN
            ItemJnlLine := param[1];
            StdItemJnlLine := param[2];
            CopyItemJnlLineDims(ItemJnlLine,StdItemJnlLine);
          END;
        ELSE
          ERROR('Local function %1 is not included for test.',localFunctionName);
      END;
    END;

    PROCEDURE UTsetGlobalValue@38(globalVarName@1001 : Text[30];VAR value@1002 : Variant);
    BEGIN
      CASE globalVarName OF
        'Code': Code := value;
        'Description': Description := value;
        'SaveUnitAmount': SaveUnitAmount := value;
        'SaveQuantity': SaveQuantity := value;
        ELSE
          ERROR('Global variable %1 is not included for test.',globalVarName);
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
