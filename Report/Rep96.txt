OBJECT Report 96 Copy G/L Budget
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Copy G/L Budget;
               ESP=Copiar ppto. contable];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   IF AmountAdjustFactor = 0 THEN
                     AmountAdjustFactor := 1;

                   IF ToDateCompression = ToDateCompression::None THEN
                     ToDateCompression := ToDateCompression::Day;
                 END;

    OnPreReport=VAR
                  SelectedDim@1000 : Record 369;
                  GLSetup@1001 : Record 98;
                  GLBudgetName@1002 : Record 95;
                  Continue@1003 : Boolean;
                BEGIN
                  IF NOT NoMessage THEN
                    DimSelectionBuf.CompareDimText(3,REPORT::"Copy G/L Budget",'',ColumnDim,Text001);

                  IF (FromSource = FromSource::"G/L Budget Entry") AND (FromGLBudgetName = '') THEN
                    ERROR(STRSUBSTNO(Text002));

                  IF (FromSource = FromSource::"G/L Entry") AND (FromDate = '') THEN
                    ERROR(STRSUBSTNO(Text003));

                  IF ToGLBudgetName = '' THEN
                    ERROR(STRSUBSTNO(Text004));

                  Continue := TRUE;
                  GLBudgetName.SETRANGE(Name,ToGLBudgetName);
                  IF NOT GLBudgetName.FIND('-') THEN BEGIN
                    IF NOT NoMessage THEN
                      IF NOT CONFIRM(
                        Text005,FALSE,GLBudgetName.TABLECAPTION,ToGLBudgetName)
                      THEN
                        Continue := FALSE;
                    IF Continue THEN BEGIN
                      GLBudgetName.INIT;
                      GLBudgetName.Name := ToGLBudgetName;
                      GLBudgetName.INSERT;
                      COMMIT;
                    END;
                  END ELSE BEGIN
                    BudgetDim1Code := GLBudgetName."Budget Dimension 1 Code";
                    BudgetDim2Code := GLBudgetName."Budget Dimension 2 Code";
                    BudgetDim3Code := GLBudgetName."Budget Dimension 3 Code";
                    BudgetDim4Code := GLBudgetName."Budget Dimension 4 Code";
                  END;

                  IF (NOT NoMessage) AND Continue THEN
                    IF NOT CONFIRM(
                      Text006,FALSE)
                    THEN
                      Continue := FALSE;

                  IF Continue THEN BEGIN
                    SelectedDim.GetSelectedDim(USERID,3,REPORT::"Copy G/L Budget",'',TempSelectedDim);
                    IF TempSelectedDim.FIND('-') THEN
                      REPEAT
                        TempSelectedDim.Level := 0;
                        IF TempSelectedDim."Dimension Value Filter" <> '' THEN
                          IF FilterIncludesBlanks(TempSelectedDim."Dimension Value Filter") THEN
                            TempSelectedDim.Level := 1;
                        TempSelectedDim.MODIFY;
                      UNTIL TempSelectedDim.NEXT = 0;

                    IF DimVal.FIND('-') THEN BEGIN
                      REPEAT
                        TempDimVal.INIT;
                        TempDimVal := DimVal;
                        TempDimVal.INSERT;
                      UNTIL DimVal.NEXT = 0;
                    END;

                    ToGLBudgetEntry.LOCKTABLE;
                    IF ToGLBudgetEntry.FIND('+') THEN
                      GLBudgetEntryNo := ToGLBudgetEntry."Entry No." + 1
                    ELSE
                      GLBudgetEntryNo := 1;

                    GLSetup.GET;
                    GlobalDim1Code := GLSetup."Global Dimension 1 Code";
                    GlobalDim2Code := GLSetup."Global Dimension 2 Code";
                  END ELSE
                    CurrReport.QUIT;
                END;

    OnPostReport=VAR
                   FromGLBudgetEntry@1000 : Record 96;
                   FromGLBudgetDim@1001 : Record 361;
                   FromGLEntry@1002 : Record 17;
                   FromLedgEntryDim@1003 : Record 355;
                   NewDate@1004 : Date;
                   OldGLAccountNo@1005 : Code[20];
                   OldPostingDate@1006 : Date;
                   OldDescription@1007 : Text[50];
                   OldBUCode@1008 : Code[10];
                   Window@1009 : Dialog;
                   EndOfTable@1010 : Boolean;
                   WindowUpdateTime@1011 : Time;
                 BEGIN
                   WindowUpdateTime := TIME;
                   Window.OPEN(
                     Text007 +
                     Text008 +
                     Text009);

                   CASE FromSource OF
                     FromSource::"G/L Entry":
                       BEGIN
                         FromGLEntry.SETCURRENTKEY("G/L Account No.","Posting Date");
                         IF FromGLAccountNo <> '' THEN
                           FromGLEntry.SETFILTER("G/L Account No.",FromGLAccountNo);
                         FromGLEntry.SETFILTER("Posting Date",FromDate);
                         FromLedgEntryDim.SETRANGE("Table ID",DATABASE::"G/L Entry");
                         IF FromGLEntry.FIND('-') THEN BEGIN
                           NewDate := CalculatePeriodStart(FromGLEntry."Posting Date");
                           OldGLAccountNo := FromGLEntry."G/L Account No.";
                           OldPostingDate := NewDate;
                           OldBUCode := FromGLEntry."Business Unit Code";
                           REPEAT
                             IncludeFromEntry := TRUE;
                             IF (FromClosingEntryFilter = FromClosingEntryFilter::Include) OR
                                (NewDate = NORMALDATE(NewDate))
                             THEN BEGIN
                               FromLedgEntryDim.SETRANGE("Entry No.",FromGLEntry."Entry No.");
                               TempDimBufIn.RESET;
                               TempDimBufIn.DELETEALL;
                               IF TempSelectedDim.FIND('-') THEN
                                 REPEAT
                                   FromLedgEntryDim.INIT;
                                   FromLedgEntryDim.SETRANGE("Dimension Code",TempSelectedDim."Dimension Code");
                                   IF TempSelectedDim."Dimension Value Filter" <> '' THEN
                                     FromLedgEntryDim.SETFILTER("Dimension Value Code",TempSelectedDim."Dimension Value Filter");
                                   IF FromLedgEntryDim.FIND('-') THEN BEGIN
                                     TempDimBufIn.INIT;
                                     TempDimBufIn."Table ID" := DATABASE::"G/L Entry";
                                     TempDimBufIn."Entry No." := FromGLEntry."Entry No.";
                                     TempDimBufIn."Dimension Code" := TempSelectedDim."Dimension Code";
                                     IF TempSelectedDim."New Dimension Value Code" <> '' THEN
                                       TempDimBufIn."Dimension Value Code" := TempSelectedDim."New Dimension Value Code"
                                     ELSE
                                       TempDimBufIn."Dimension Value Code" := FromLedgEntryDim."Dimension Value Code";
                                     TempDimBufIn.INSERT;
                                   END ELSE
                                     IF TempSelectedDim."Dimension Value Filter" <> '' THEN
                                       IF TempSelectedDim.Level = 1 THEN BEGIN
                                         FromLedgEntryDim.SETRANGE("Dimension Value Code");
                                         IncludeFromEntry := NOT FromLedgEntryDim.FIND('-');
                                       END ELSE
                                         IncludeFromEntry := FALSE;
                                   FromLedgEntryDim.SETRANGE("Dimension Code");
                                   FromLedgEntryDim.SETRANGE("Dimension Value Code");
                                 UNTIL (TempSelectedDim.NEXT = 0) OR NOT IncludeFromEntry;
                               IF ToDateCompression = ToDateCompression::None THEN
                                 OldDescription := FromGLEntry.Description
                               ELSE
                                 OldDescription := '';
                               IF IncludeFromEntry THEN
                                 UpdateTempGLBudgetEntry(
                                   FromGLEntry."G/L Account No.",NewDate,
                                   FromGLEntry.Amount,OldDescription,FromGLEntry."Business Unit Code");
                               EndOfTable := (FromGLEntry.NEXT = 0);
                               IF EndOfTable THEN
                                 InsertGLBudgetEntry
                               ELSE BEGIN
                                 NewDate := CalculatePeriodStart(FromGLEntry."Posting Date");
                                 IF TIME - WindowUpdateTime >= 750 THEN BEGIN
                                   Window.UPDATE(1,FromGLEntry."G/L Account No.");
                                   Window.UPDATE(2,FromGLEntry."Posting Date");
                                   WindowUpdateTime := TIME;
                                 END;
                                 IF (FromGLEntry."G/L Account No." <> OldGLAccountNo) OR
                                    (NewDate <> OldPostingDate) OR
                                    (FromGLEntry."Business Unit Code" <> OldBUCode) OR
                                    (ToDateCompression = ToDateCompression::None)
                                 THEN BEGIN
                                   OldGLAccountNo := FromGLEntry."G/L Account No.";
                                   OldPostingDate := NewDate;
                                   OldBUCode := FromGLEntry."Business Unit Code";
                                   InsertGLBudgetEntry;
                                 END;
                               END;
                             END ELSE BEGIN
                               EndOfTable := (FromGLEntry.NEXT = 0);
                               NewDate := CalculatePeriodStart(FromGLEntry."Posting Date");
                             END;
                           UNTIL EndOfTable;
                         END;
                       END;
                     FromSource::"G/L Budget Entry":
                       BEGIN
                         FromGLBudgetEntry.SETCURRENTKEY("Budget Name","G/L Account No.",Description,Date);
                         FromGLBudgetEntry.SETRANGE("Budget Name",FromGLBudgetName);
                         IF FromGLAccountNo <> '' THEN
                           FromGLBudgetEntry.SETFILTER("G/L Account No.",FromGLAccountNo);
                         IF FromDate <> '' THEN
                           FromGLBudgetEntry.SETFILTER(Date,FromDate);
                         IF FromGLBudgetEntry.FIND('-') THEN BEGIN
                           NewDate := CalculatePeriodStart(FromGLBudgetEntry.Date);
                           OldGLAccountNo := FromGLBudgetEntry."G/L Account No.";
                           OldPostingDate := NewDate;
                           OldDescription := FromGLBudgetEntry.Description;
                           OldBUCode := FromGLBudgetEntry."Business Unit Code";
                           REPEAT
                             IncludeFromEntry := TRUE;
                             IF (FromClosingEntryFilter = FromClosingEntryFilter::Include) OR
                                (NewDate = NORMALDATE(NewDate))
                             THEN BEGIN
                               FromGLBudgetDim.SETRANGE("Entry No.",FromGLBudgetEntry."Entry No.");
                               TempDimBufIn.RESET;
                               TempDimBufIn.DELETEALL;
                               IF TempSelectedDim.FIND('-') THEN
                                 REPEAT
                                   FromGLBudgetDim.INIT;
                                   FromGLBudgetDim.SETRANGE("Dimension Code",TempSelectedDim."Dimension Code");
                                   IF TempSelectedDim."Dimension Value Filter" <> '' THEN
                                     FromGLBudgetDim.SETFILTER("Dimension Value Code",TempSelectedDim."Dimension Value Filter");
                                   IF FromGLBudgetDim.FIND('-') THEN BEGIN
                                     TempDimBufIn.INIT;
                                     TempDimBufIn."Table ID" := DATABASE::"G/L Budget Entry";
                                     TempDimBufIn."Entry No." := FromGLBudgetEntry."Entry No.";
                                     TempDimBufIn."Dimension Code" := TempSelectedDim."Dimension Code";
                                     IF TempSelectedDim."New Dimension Value Code" <> '' THEN
                                       TempDimBufIn."Dimension Value Code" := TempSelectedDim."New Dimension Value Code"
                                     ELSE
                                       TempDimBufIn."Dimension Value Code" := FromGLBudgetDim."Dimension Value Code";
                                     TempDimBufIn.INSERT;
                                   END ELSE
                                     IF TempSelectedDim."Dimension Value Filter" <> '' THEN
                                       IF TempSelectedDim.Level = 1 THEN BEGIN
                                         FromGLBudgetDim.SETRANGE("Dimension Value Code");
                                         IncludeFromEntry := NOT FromGLBudgetDim.FIND('-');
                                       END ELSE
                                         IncludeFromEntry := FALSE;
                                   FromGLBudgetDim.SETRANGE("Dimension Code");
                                   FromGLBudgetDim.SETRANGE("Dimension Value Code");
                                 UNTIL (TempSelectedDim.NEXT = 0) OR NOT IncludeFromEntry;
                               IF IncludeFromEntry THEN
                                 UpdateTempGLBudgetEntry(
                                   FromGLBudgetEntry."G/L Account No.",NewDate,
                                   FromGLBudgetEntry.Amount,FromGLBudgetEntry.Description,
                                   FromGLBudgetEntry."Business Unit Code");
                               EndOfTable := (FromGLBudgetEntry.NEXT = 0);
                               IF EndOfTable THEN
                                 InsertGLBudgetEntry
                               ELSE BEGIN
                                 NewDate := CalculatePeriodStart(FromGLBudgetEntry.Date);
                                 IF TIME - WindowUpdateTime >= 1000 THEN BEGIN
                                   Window.UPDATE(1,FromGLBudgetEntry."G/L Account No.");
                                   Window.UPDATE(2,FromGLBudgetEntry.Date);
                                   WindowUpdateTime := TIME;
                                 END;
                                 IF (FromGLBudgetEntry."G/L Account No." <> OldGLAccountNo) OR
                                    (NewDate <> OldPostingDate) OR
                                    (FromGLBudgetEntry.Description <> OldDescription) OR
                                    (FromGLBudgetEntry."Business Unit Code" <> OldBUCode)
                                 THEN BEGIN
                                   OldGLAccountNo := FromGLBudgetEntry."G/L Account No.";
                                   OldPostingDate := NewDate;
                                   OldDescription := FromGLBudgetEntry.Description;
                                   OldBUCode := FromGLBudgetEntry."Business Unit Code";
                                   InsertGLBudgetEntry;
                                 END;
                               END;
                             END ELSE BEGIN
                               EndOfTable := (FromGLBudgetEntry.NEXT = 0);
                               NewDate := CalculatePeriodStart(FromGLBudgetEntry.Date);
                             END;
                           UNTIL EndOfTable;
                         END;
                       END;
                   END;

                   Window.CLOSE;

                   IF NOT NoMessage THEN
                     MESSAGE(Text010);
                 END;

  }
  DATAITEMS
  {
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=8800;
      Height=8140;
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1   ;TextBox      ;3410 ;3300 ;5390 ;440  ;Editable=No;
                                                   PermanentAssist=Yes;
                                                   CaptionML=[ENU=Dimensions;
                                                              ESP=Dimensiones];
                                                   SourceExpr=ColumnDim;
                                                   OnAssistEdit=VAR
                                                                  DimSelectionBuf@1001 : Record 368;
                                                                BEGIN
                                                                  DimSelectionBuf.SetDimSelectionChange(3,REPORT::"Copy G/L Budget",ColumnDim);
                                                                END;
                                                                 }
      { 2   ;Label        ;220  ;3300 ;3080 ;440  ;ParentControl=1 }
      { 5   ;TextBox      ;3410 ;4950 ;2750 ;440  ;CaptionML=[ENU=G/L Account No.;
                                                              ESP=N§ cuenta];
                                                   SourceExpr=ToGLAccountNo;
                                                   TableRelation="G/L Account" }
      { 6   ;Label        ;220  ;4950 ;3080 ;440  ;ParentControl=5 }
      { 7   ;TextBox      ;3410 ;4400 ;2750 ;440  ;ValidateTableRelation=No;
                                                   CaptionML=[ENU=Budget Name;
                                                              ESP=Nombre ppto.];
                                                   SourceExpr=ToGLBudgetName;
                                                   TableRelation="G/L Budget Name" }
      { 8   ;Label        ;220  ;4400 ;3080 ;440  ;ParentControl=7 }
      { 11  ;Label        ;0    ;3850 ;3300 ;440  ;LeaderDots=No;
                                                   CaptionML=[ENU=Copy to;
                                                              ESP=Copiar a] }
      { 12  ;Label        ;0    ;0    ;3300 ;440  ;LeaderDots=No;
                                                   CaptionML=[ENU=Copy from;
                                                              ESP=Copiar desde] }
      { 15  ;TextBox      ;3410 ;550  ;2750 ;440  ;CaptionML=[ENU=Source;
                                                              ESP=Origen];
                                                   OptionCaptionML=[ENU=G/L Entry,G/L Budget Entry;
                                                                    ESP=Mov. contabilidad,Mov. presupuesto];
                                                   OptionString=G/L Entry,G/L Budget Entry;
                                                   SourceExpr=FromSource;
                                                   OnAfterValidate=BEGIN
                                                                     IF (FromSource = FromSource::"G/L Entry") THEN
                                                                       FromGLBudgetName := '';
                                                                   END;
                                                                    }
      { 16  ;Label        ;220  ;550  ;3080 ;440  ;ParentControl=15 }
      { 9   ;TextBox      ;3410 ;2200 ;2750 ;440  ;CaptionML=[ENU=Date;
                                                              ESP=Fecha];
                                                   SourceExpr=FromDate;
                                                   OnValidate=VAR
                                                                GLAcc@1001 : Record 15;
                                                                ApplicationManagement@1002 : Codeunit 1;
                                                              BEGIN
                                                                IF ApplicationManagement.MakeDateFilter(FromDate) = 0 THEN;
                                                                GLAcc.SETFILTER("Date Filter",FromDate);
                                                                FromDate := GLAcc.GETFILTER("Date Filter");
                                                              END;
                                                               }
      { 10  ;Label        ;220  ;2200 ;3080 ;440  ;ParentControl=9 }
      { 13  ;Label        ;0    ;5500 ;3300 ;440  ;LeaderDots=No;
                                                   CaptionML=[ENU=Apply;
                                                              ESP=Aplicar] }
      { 14  ;TextBox      ;3410 ;6600 ;2750 ;440  ;CaptionML=[ENU=Rounding Method;
                                                              ESP=M‚todo redondeo];
                                                   SourceExpr=RoundingMethod.Code;
                                                   TableRelation="Rounding Method" }
      { 17  ;Label        ;220  ;6600 ;3080 ;440  ;ParentControl=14 }
      { 18  ;TextBox      ;3410 ;6050 ;2750 ;440  ;CaptionML=[ENU=Adjustment Factor;
                                                              ESP=Factor ajuste];
                                                   DecimalPlaces=0:5;
                                                   NotBlank=Yes;
                                                   SourceExpr=AmountAdjustFactor;
                                                   MinValue=0 }
      { 19  ;Label        ;220  ;6050 ;3080 ;440  ;ParentControl=18 }
      { 20  ;TextBox      ;3410 ;7150 ;2750 ;440  ;CaptionML=[ENU=Date Change Formula;
                                                              ESP=F¢rmula de cambio fecha];
                                                   OptionCaptionML=[ENU=None,Day,Week,Month,Quarter,Year,Period;
                                                                    ESP=Ninguno,D¡a,Semana,Mes,Trimestre,A¤o,Periodo];
                                                   SourceExpr=DateAdjustExpression }
      { 21  ;Label        ;220  ;7150 ;3080 ;440  ;ParentControl=20 }
      { 22  ;TextBox      ;3410 ;1650 ;2750 ;440  ;ValidateTableRelation=No;
                                                   CaptionML=[ENU=G/L Account No.;
                                                              ESP=N§ cuenta];
                                                   ClearOnLookup=No;
                                                   SourceExpr=FromGLAccountNo;
                                                   TableRelation="G/L Account" }
      { 23  ;Label        ;220  ;1650 ;3080 ;440  ;ParentControl=22 }
      { 24  ;TextBox      ;3410 ;1100 ;2750 ;440  ;ValidateTableRelation=No;
                                                   CaptionML=[ENU=Budget Name;
                                                              ESP=Nombre ppto.];
                                                   SourceExpr=FromGLBudgetName;
                                                   TableRelation="G/L Budget Name";
                                                   OnAfterValidate=BEGIN
                                                                     IF (FromGLBudgetName <> '') AND (FromSource = FromSource::"G/L Entry") THEN
                                                                       FromSource := FromSource::"G/L Budget Entry";
                                                                   END;
                                                                    }
      { 25  ;Label        ;220  ;1100 ;3080 ;440  ;ParentControl=24 }
      { 3   ;TextBox      ;3410 ;7700 ;2750 ;440  ;CaptionML=[ENU=Date Compression;
                                                              ESP=Compresi¢n por fechas];
                                                   OptionCaptionML=[ENU=None,Day,Week,Month,Quarter,Year,Period;
                                                                    ESP=Ninguno,D¡a,Semana,Mes,Trimestre,A¤o,Periodo];
                                                   SourceExpr=ToDateCompression }
      { 4   ;Label        ;220  ;7700 ;3080 ;440  ;ParentControl=3 }
      { 26  ;TextBox      ;3410 ;2750 ;2750 ;440  ;CaptionML=[ENU=Closing Entries;
                                                              ESP=Movs. regularizaci¢n];
                                                   OptionCaptionML=[ENU=Include,Exclude;
                                                                    ESP=Incluir,Excluir];
                                                   SourceExpr=FromClosingEntryFilter }
      { 27  ;Label        ;220  ;2750 ;3080 ;440  ;ParentControl=26 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones] }

      { 12  ;2   ;Group     ;
                  CaptionML=[ENU=Copy from;
                             ESP=Copiar desde] }

      { 15  ;3   ;Field     ;
                  CaptionML=[ENU=Source;
                             ESP=Origen];
                  OptionCaptionML=[ENU=G/L Entry,G/L Budget Entry;
                                   ESP=Mov. contabilidad,Mov. presupuesto];
                  SourceExpr=FromSource;
                  OnValidate=BEGIN
                               FromSourceOnAfterValidate;
                             END;
                              }

      { 24  ;3   ;Field     ;
                  CaptionML=[ENU=Budget Name;
                             ESP=Nombre ppto.];
                  SourceExpr=FromGLBudgetName;
                  TableRelation="G/L Budget Name";
                  OnValidate=BEGIN
                               FromGLBudgetNameOnAfterValidat;
                             END;
                              }

      { 22  ;3   ;Field     ;
                  CaptionML=[ENU=G/L Account No.;
                             ESP=N§ cuenta];
                  SourceExpr=FromGLAccountNo;
                  TableRelation="G/L Account" }

      { 9   ;3   ;Field     ;
                  CaptionML=[ENU=Date;
                             ESP=Fecha];
                  SourceExpr=FromDate;
                  OnValidate=VAR
                               GLAcc@1001 : Record 15;
                               ApplicationManagement@1002 : Codeunit 1;
                             BEGIN
                               IF ApplicationManagement.MakeDateFilter(FromDate) = 0 THEN;
                               GLAcc.SETFILTER("Date Filter",FromDate);
                               FromDate := GLAcc.GETFILTER("Date Filter");
                             END;
                              }

      { 26  ;3   ;Field     ;
                  CaptionML=[ENU=Closing Entries;
                             ESP=Movs. regularizaci¢n];
                  OptionCaptionML=[ENU=Include,Exclude;
                                   ESP=Incluir,Excluir];
                  SourceExpr=FromClosingEntryFilter }

      { 1   ;3   ;Field     ;
                  CaptionML=[ENU=Dimensions;
                             ESP=Dimensiones];
                  SourceExpr=ColumnDim;
                  Editable=FALSE;
                  OnAssistEdit=VAR
                                 DimSelectionBuf@1001 : Record 368;
                               BEGIN
                                 DimSelectionBuf.SetDimSelectionChange(3,REPORT::"Copy G/L Budget",ColumnDim);
                               END;
                                }

      { 11  ;2   ;Group     ;
                  CaptionML=[ENU=Copy to;
                             ESP=Copiar a] }

      { 7   ;3   ;Field     ;
                  CaptionML=[ENU=Budget Name;
                             ESP=Nombre ppto.];
                  SourceExpr=ToGLBudgetName;
                  TableRelation="G/L Budget Name" }

      { 5   ;3   ;Field     ;
                  CaptionML=[ENU=G/L Account No.;
                             ESP=N§ cuenta];
                  SourceExpr=ToGLAccountNo;
                  TableRelation="G/L Account" }

      { 13  ;2   ;Group     ;
                  CaptionML=[ENU=Apply;
                             ESP=Liquidar] }

      { 18  ;3   ;Field     ;
                  CaptionML=[ENU=Adjustment Factor;
                             ESP=Factor ajuste];
                  DecimalPlaces=0:5;
                  NotBlank=Yes;
                  SourceExpr=AmountAdjustFactor;
                  MinValue=0 }

      { 14  ;3   ;Field     ;
                  CaptionML=[ENU=Rounding Method;
                             ESP=M‚todo redondeo];
                  SourceExpr=RoundingMethod.Code;
                  TableRelation="Rounding Method" }

      { 20  ;3   ;Field     ;
                  CaptionML=[ENU=Date Change Formula;
                             ESP=F¢rmula de cambio fecha];
                  OptionCaptionML=[ENU=None,Day,Week,Month,Quarter,Year,Period;
                                   ESP=Ninguno,D¡a,Semana,Mes,Trimestre,A¤o,Periodo];
                  SourceExpr=DateAdjustExpression }

      { 3   ;3   ;Field     ;
                  CaptionML=[ENU=Date Compression;
                             ESP=Compresi¢n por fechas];
                  OptionCaptionML=[ENU=None,Day,Week,Month,Quarter,Year,Period;
                                   ESP=Ninguno,D¡a,Semana,Mes,Trimestre,A¤o,Periodo];
                  SourceExpr=ToDateCompression }

    }
  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=Dimensions;ESP=Dimensiones';
      Text002@1001 : TextConst 'ENU=You must specify a budget name to copy from.;ESP=Debe especificar nombre ppto. para copiar de.';
      Text003@1002 : TextConst 'ENU=You must specify a date interval to copy from.;ESP=Debe especificar intervalo fecha para copiar de.';
      Text004@1003 : TextConst 'ENU=You must specify a budget name to copy to.;ESP=Debe especificar nombre ppto. para copiar en.';
      Text005@1004 : TextConst 'ENU=Do you want to create %1 %2.;ESP=Confirma que desea crear %1 %2.';
      Text006@1005 : TextConst 'ENU=Do you want to start the copy ?;ESP=¨Confirma que desea empezar la copia ?';
      Text007@1006 : TextConst 'ENU=Copying budget...\\;ESP=Copiando ppto....\\';
      Text008@1007 : TextConst 'ENU=G/L Account No. #1####################\;ESP=N§ cuenta       #1####################\';
      Text009@1008 : TextConst 'ENU=Posting Date    #2######;ESP=Fecha registro  #2######';
      Text010@1009 : TextConst 'ENU=Budget has been successfully copied.;ESP=Se ha copiado el ppto. correctamente.';
      ToGLBudgetEntry@1010 : Record 96;
      ToGLBudgetDim@1011 : Record 361;
      TempGLBudgetEntry@1012 : TEMPORARY Record 96;
      TempDimBufIn@1013 : TEMPORARY Record 360;
      TempDimBufOut@1014 : TEMPORARY Record 360;
      TempSelectedDim@1015 : TEMPORARY Record 369;
      DimVal@1016 : Record 349;
      TempDimVal@1017 : TEMPORARY Record 349;
      RoundingMethod@1018 : Record 42;
      DimSelectionBuf@1019 : Record 368;
      DimBufMgt@1020 : Codeunit 411;
      FromDate@1021 : Text[30];
      FromSource@1022 : 'G/L Entry,G/L Budget Entry';
      FromGLBudgetName@1023 : Code[10];
      FromGLAccountNo@1024 : Code[250];
      FromClosingEntryFilter@1025 : 'Include,Exclude';
      ToGLBudgetName@1026 : Code[10];
      ToGLAccountNo@1027 : Code[20];
      ToBUCode@1028 : Code[10];
      ToDateCompression@1029 : 'None,Day,Week,Month,Quarter,Year,Period';
      ColumnDim@1030 : Text[250];
      AmountAdjustFactor@1031 : Decimal;
      DateAdjustExpression@1032 : DateFormula;
      GLBudgetEntryNo@1033 : Integer;
      GlobalDim1Code@1034 : Code[20];
      GlobalDim2Code@1035 : Code[20];
      BudgetDim1Code@1036 : Code[20];
      BudgetDim2Code@1037 : Code[20];
      BudgetDim3Code@1038 : Code[20];
      BudgetDim4Code@1039 : Code[20];
      NoMessage@1040 : Boolean;
      PrevPostingDate@1041 : Date;
      PrevCalculatedPostingDate@1042 : Date;
      IncludeFromEntry@1043 : Boolean;

    LOCAL PROCEDURE UpdateTempGLBudgetEntry@6(GLAccountNo2@1000 : Code[20];PostingDate2@1001 : Date;Amount2@1002 : Decimal;Description2@1003 : Text[50];BUCode2@1004 : Code[10]);
    VAR
      DimEntryNo@1005 : Integer;
      Found@1006 : Boolean;
    BEGIN
      DimEntryNo := DimBufMgt.FindDimensions(TempDimBufIn);
      Found := TempDimBufIn.FIND('-');
      IF (Found AND (DimEntryNo = 0)) THEN BEGIN
        TempGLBudgetEntry."Entry No." := DimBufMgt.InsertDimensions(TempDimBufIn);
        TempGLBudgetEntry."G/L Account No." := GLAccountNo2;
        TempGLBudgetEntry.Date := PostingDate2;
        TempGLBudgetEntry.Amount := Amount2;
        TempGLBudgetEntry.Description := Description2;
        TempGLBudgetEntry."Business Unit Code" := BUCode2;
        TempGLBudgetEntry.INSERT;
      END ELSE BEGIN
        IF TempGLBudgetEntry.GET(DimEntryNo) THEN BEGIN
          TempGLBudgetEntry.Amount := TempGLBudgetEntry.Amount + Amount2;
          TempGLBudgetEntry.MODIFY;
        END ELSE BEGIN
          TempGLBudgetEntry."Entry No." := DimEntryNo;
          TempGLBudgetEntry."G/L Account No." := GLAccountNo2;
          TempGLBudgetEntry.Date := PostingDate2;
          TempGLBudgetEntry.Amount := Amount2;
          TempGLBudgetEntry.Description := Description2;
          TempGLBudgetEntry."Business Unit Code" := BUCode2;
          TempGLBudgetEntry.INSERT;
        END;
      END;
    END;

    LOCAL PROCEDURE InsertGLBudgetEntry@2();
    VAR
      Sign@1000 : Decimal;
    BEGIN
      IF TempGLBudgetEntry.FIND('-') THEN BEGIN
        REPEAT
          DimBufMgt.GetDimensions(TempGLBudgetEntry."Entry No.",TempDimBufOut);
          TempDimBufOut.SETRANGE("Entry No.",TempGLBudgetEntry."Entry No.");
          IF TempGLBudgetEntry.Amount <> 0 THEN BEGIN
            ToGLBudgetEntry := TempGLBudgetEntry;
            ToGLBudgetEntry."Entry No." := GLBudgetEntryNo;
            ToGLBudgetEntry."Budget Name" := ToGLBudgetName;
            IF ToGLAccountNo <> '' THEN
              ToGLBudgetEntry."G/L Account No." := ToGLAccountNo;
            IF ToBUCode <> '' THEN
              ToGLBudgetEntry."Business Unit Code" := ToBUCode;
            ToGLBudgetEntry."User ID" := USERID;
            ToGLBudgetEntry.Date := TempGLBudgetEntry.Date;
            ToGLBudgetEntry.Amount := ROUND(TempGLBudgetEntry.Amount * AmountAdjustFactor);
            IF RoundingMethod.Code <> '' THEN BEGIN
              IF ToGLBudgetEntry.Amount >= 0 THEN
                Sign := 1
              ELSE
                Sign := -1;
              RoundingMethod."Minimum Amount" := ABS(ToGLBudgetEntry.Amount);
              IF RoundingMethod.FIND('=<') THEN BEGIN
                ToGLBudgetEntry.Amount :=
                  ToGLBudgetEntry.Amount + Sign * RoundingMethod."Amount Added Before";
                IF RoundingMethod.Precision > 0 THEN BEGIN
                  ToGLBudgetEntry.Amount :=
                    Sign *
                    ROUND(
                      ABS(ToGLBudgetEntry.Amount),RoundingMethod.Precision,COPYSTR('=><',
                      RoundingMethod.Type+1,1));
                END;
                ToGLBudgetEntry.Amount :=
                  ToGLBudgetEntry.Amount + Sign * RoundingMethod."Amount Added After";
              END;
            END;
            IF TempDimBufOut.FIND('-') THEN BEGIN
              REPEAT
                ToGLBudgetDim."Entry No." := GLBudgetEntryNo;
                ToGLBudgetDim."Dimension Code" := TempDimBufOut."Dimension Code";
                ToGLBudgetDim."Dimension Value Code" := TempDimBufOut."Dimension Value Code";
                ToGLBudgetDim.INSERT;
                IF TempDimBufOut."Dimension Code" = GlobalDim1Code THEN
                  ToGLBudgetEntry."Global Dimension 1 Code" := TempDimBufOut."Dimension Value Code";
                IF TempDimBufOut."Dimension Code" = GlobalDim2Code THEN
                  ToGLBudgetEntry."Global Dimension 2 Code" := TempDimBufOut."Dimension Value Code";
                IF TempDimBufOut."Dimension Code" = BudgetDim1Code THEN
                  ToGLBudgetEntry."Budget Dimension 1 Code" := TempDimBufOut."Dimension Value Code";
                IF TempDimBufOut."Dimension Code" = BudgetDim2Code THEN
                  ToGLBudgetEntry."Budget Dimension 2 Code" := TempDimBufOut."Dimension Value Code";
                IF TempDimBufOut."Dimension Code" = BudgetDim3Code THEN
                  ToGLBudgetEntry."Budget Dimension 3 Code" := TempDimBufOut."Dimension Value Code";
                IF TempDimBufOut."Dimension Code" = BudgetDim4Code THEN
                  ToGLBudgetEntry."Budget Dimension 4 Code" := TempDimBufOut."Dimension Value Code";
              UNTIL TempDimBufOut.NEXT = 0;
            END;
            ToGLBudgetEntry.INSERT;
            GLBudgetEntryNo := GLBudgetEntryNo + 1;
          END;
        UNTIL TempGLBudgetEntry.NEXT = 0;
      END;
      DimBufMgt.DeleteAllDimensions();
      TempGLBudgetEntry.RESET;
      TempGLBudgetEntry.DELETEALL;
      TempDimBufOut.RESET;
      TempDimBufOut.DELETEALL;
    END;

    PROCEDURE Initialize@1(FromSource2@1000 : Option;FromGLBudgetName2@1001 : Code[10];FromGLAccountNo2@1002 : Code[250];FromDate2@1003 : Text[30];ToGlBudgetName2@1004 : Code[10];ToGLAccountNo2@1005 : Code[20];ToBUCode2@1006 : Code[10];AmountAdjustFactor2@1007 : Decimal;RoundingMethod2@1008 : Code[10];DateAdjustExpression2@1009 : DateFormula;NoMessage2@1010 : Boolean);
    BEGIN

      FromSource := FromSource2;
      FromGLBudgetName := FromGLBudgetName2;
      FromGLAccountNo := FromGLAccountNo2;
      FromDate := FromDate2;
      ToGLBudgetName := ToGlBudgetName2;
      ToGLAccountNo := ToGLAccountNo2;
      ToBUCode := ToBUCode2;
      AmountAdjustFactor := AmountAdjustFactor2;
      RoundingMethod.Code := RoundingMethod2;
      DateAdjustExpression := DateAdjustExpression2;
      NoMessage := NoMessage2;
    END;

    LOCAL PROCEDURE CalculatePeriodStart@4(PostingDate@1000 : Date) : Date;
    VAR
      AccountingPeriod@1001 : Record 50;
    BEGIN
      IF FORMAT(DateAdjustExpression) <> '' THEN
        IF PostingDate = CLOSINGDATE(PostingDate) THEN
          PostingDate := CLOSINGDATE((CALCDATE(DateAdjustExpression,NORMALDATE(PostingDate))))
        ELSE
          PostingDate := CALCDATE(DateAdjustExpression,PostingDate);
      IF PostingDate = CLOSINGDATE(PostingDate) THEN
        EXIT(PostingDate);

      CASE ToDateCompression OF
        ToDateCompression::Week :
          PostingDate := CALCDATE('<CW+1D-1W>',PostingDate);
        ToDateCompression::Month :
          PostingDate := CALCDATE('<CM+1D-1M>',PostingDate);
        ToDateCompression::Quarter :
          PostingDate := CALCDATE('<CQ+1D-1Q>',PostingDate);
        ToDateCompression::Year :
          PostingDate := CALCDATE('<CY+1D-1Y>',PostingDate);
        ToDateCompression::Period :
          BEGIN
            IF PostingDate <> PrevPostingDate THEN BEGIN
              PrevPostingDate := PostingDate;
              AccountingPeriod.SETRANGE("Starting Date",0D,PostingDate);
              IF AccountingPeriod.FIND('+') THEN BEGIN
                PrevCalculatedPostingDate := AccountingPeriod."Starting Date"
              END ELSE
                PrevCalculatedPostingDate := PostingDate;
            END;
            PostingDate := PrevCalculatedPostingDate;
          END;
      END;
      EXIT(PostingDate);
    END;

    LOCAL PROCEDURE FilterIncludesBlanks@17(TheFilter@1000 : Code[250]) : Boolean;
    VAR
      TempDimBuf2@1001 : TEMPORARY Record 360;
    BEGIN
      WITH TempDimBuf2 DO BEGIN
        DELETEALL; // Necessary because of C/SIDE error
        INIT;
        INSERT;
        SETFILTER("Dimension Code",TheFilter);
        EXIT(FIND('-'));
      END;
    END;

    LOCAL PROCEDURE FromSourceOnAfterValidate@19070078();
    BEGIN
      IF FromSource = FromSource::"G/L Entry" THEN
        FromGLBudgetName := '';
    END;

    LOCAL PROCEDURE FromGLBudgetNameOnAfterValidat@19006915();
    BEGIN
      IF (FromGLBudgetName <> '') AND (FromSource = FromSource::"G/L Entry") THEN
        FromSource := FromSource::"G/L Budget Entry";
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
