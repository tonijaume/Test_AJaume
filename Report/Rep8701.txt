OBJECT Report 8701 Mobile Option Translation
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Mobile Option Translation;
               ESP=Traducci¢n de opciones m¢viles];
    ProcessingOnly=Yes;
  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table8708;
        DataItemTableView=SORTING(Table No.,Field No.);
        OnAfterGetRecord=BEGIN
                           CurrReport.BREAK;
                         END;

        OnPostDataItem=BEGIN
                         CreateTranslation("Mobile Option Field Setup","Mobile Language");
                       END;

      }
      SECTIONS
      {
      }
       }
    { PROPERTIES
      {
        DataItemIndent=1;
        DataItemTable=Table8706;
        DataItemTableView=SORTING(Code);
        OnAfterGetRecord=BEGIN
                           CurrReport.BREAK;
                         END;

      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=9020;
      Height=3410;
    }
    CONTROLS
    {
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=Refreshing option field translations @1@@@@@@@@@@@@@@@@@@;ESP=Actualizando traducciones de campos de opciones @1@@@@@@@@@@@@@@@@@@';
      HideDialog@1004 : Boolean;

    PROCEDURE CreateLanguageTranslation@2(LanguageCode@1011 : Code[10]);
    VAR
      MobOptionFieldSetup@1000 : Record 8708;
    BEGIN
      IF MobOptionFieldSetup.FINDSET THEN
        REPEAT
          Translate(MobOptionFieldSetup."Table No.",MobOptionFieldSetup."Field No.",LanguageCode);
        UNTIL MobOptionFieldSetup.NEXT = 0;
    END;

    PROCEDURE CreateOptionTranslation@6(TableNo@1002 : Integer;FieldNo@1001 : Integer);
    VAR
      MobileLanguage@1000 : Record 8706;
    BEGIN
      IF MobileLanguage.FINDSET THEN
        REPEAT
          Translate(TableNo,FieldNo,MobileLanguage.Code);
        UNTIL MobileLanguage.NEXT = 0;
    END;

    PROCEDURE CreateTranslation@1(VAR MobOptFieldSetup@1010 : Record 8708;VAR MobileLanguage@1011 : Record 8706);
    VAR
      MobOptFieldCaption@1003 : Record 8709;
      Window@1002 : Dialog;
      RecNo@1001 : Integer;
      RecCount@1000 : Integer;
    BEGIN
      Window.OPEN(Text001);
      RecCount := MobOptFieldSetup.COUNTAPPROX;

      IF MobOptFieldSetup.FINDSET THEN BEGIN
        MobOptFieldSetup."Option String" := MobOptFieldSetup.GetOptionString;
        MobOptFieldSetup.MODIFY;

        RecNo := RecNo + 1;
        Window.UPDATE(1,ROUND(RecNo / RecCount * 10000,1));

        REPEAT
          IF MobileLanguage.FINDSET THEN BEGIN
            REPEAT
              MobOptFieldCaption.DeleteEntries(
                MobOptFieldSetup."Table No.",
                MobOptFieldSetup."Field No.",
                MobileLanguage.Code);
              Translate(
                MobOptFieldSetup."Table No.",
                MobOptFieldSetup."Field No.",
                MobileLanguage.Code);
            UNTIL MobileLanguage.NEXT = 0;
          END;
        UNTIL MobOptFieldSetup.NEXT = 0;
      END;

      Window.CLOSE;
    END;

    LOCAL PROCEDURE Translate@4(TableNo@1008 : Integer;FieldNo@1006 : Integer;LanguageCode@1004 : Code[10]);
    VAR
      Language@1000 : Record 8;
      MobOptionFieldCaption@1003 : Record 8709;
      RecRef@1002 : RecordRef;
      FldRef@1001 : FieldRef;
      CaptionNo@1007 : Integer;
      RemainingCaption@1005 : Text[250];
    BEGIN
      CurrReport.LANGUAGE := Language.GetLanguageID(LanguageCode);

      RecRef.OPEN(TableNo);
      FldRef := RecRef.FIELD(FieldNo);
      RemainingCaption := FldRef.OPTIONCAPTION;

      CaptionNo := 0;
      REPEAT
        MobOptionFieldCaption.INIT;
        MobOptionFieldCaption."Table No." := TableNo;
        MobOptionFieldCaption."Field No." := FieldNo;
        MobOptionFieldCaption."Language Code" := LanguageCode;
        MobOptionFieldCaption."Option No." := CaptionNo;

        IF STRPOS(RemainingCaption,',') <> 0 THEN BEGIN
          MobOptionFieldCaption."Option Caption" := COPYSTR(RemainingCaption,1,STRPOS(RemainingCaption,',') - 1);
          RemainingCaption := COPYSTR(RemainingCaption,STRPOS(RemainingCaption,',') + 1);
        END ELSE BEGIN
          MobOptionFieldCaption."Option Caption" := RemainingCaption;
          RemainingCaption := '';
        END;

        MobOptionFieldCaption.INSERT;
        CaptionNo := CaptionNo + 1;
      UNTIL RemainingCaption = '';
      CurrReport.LANGUAGE := GLOBALLANGUAGE;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
