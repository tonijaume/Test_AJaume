OBJECT Report 3010575 Copy CA Budget to G/L
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=CACEM6.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Copy CA Budget to G/L;
               ESP=Copiar ppto. costes en contable];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   IF NoOfCopies = 0 THEN
                     NoOfCopies := 1;
                   IF Factor = 0 THEN
                     Factor := 1;
                 END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table3010571;
        DataItemTableView=SORTING(Budget Name,Cost Type No.,Date);
        OnPreDataItem=BEGIN
                        IF Factor <= 0 THEN
                          ERROR(Text000);

                        IF NoOfCopies < 1 THEN
                          ERROR(Text001);

                        IF (NoOfCopies > 1) AND (DateChangeFormula = '') THEN
                          ERROR(Text002);

                        IF GETFILTER("Budget Name") = '' THEN
                          ERROR(Text003);

                        IF NOT CONFIRM(
                             Text004 +
                             Text005 +
                             Text006,
                             FALSE,GETFILTER("Budget Name"),TargetBudName.Name,Factor,
                             NoOfCopies,GETFILTER(Date),DateChangeFormula) THEN
                          ERROR(Text015);

                        LOCKTABLE;

                        IF TargetBudEntry.FIND('+') THEN
                          LastEntryNo := TargetBudEntry."Entry No.";

                        d.OPEN(Text007 + // Copy budget entries
                           Text008 + // No of entries #1
                          Text009 + // No of copies #2
                          Text010); // Copied #3

                        d.UPDATE(1,COUNT);
                        d.UPDATE(2,NoOfCopies);
                      END;

        OnAfterGetRecord=BEGIN
                           TargetBudEntry.INIT;
                           TargetBudEntry."Budget Name" := TargetBudName.Name;

                           // Get corresponding G/L account
                           IF NOT CostType.GET("Cost Type No.") THEN BEGIN
                             NoSkipped := NoSkipped + 1;
                             CurrReport.SKIP;
                           END;

                           GlAcc.SETFILTER("No.",'%1',CostType."G/L Account Range");
                           IF NOT GlAcc.FIND('-') THEN BEGIN
                             NoSkipped := NoSkipped + 1;
                             CurrReport.SKIP;
                           END;

                           TargetBudEntry."G/L Account No." := GlAcc."No.";
                           TargetBudEntry.Date := Date;
                           TargetBudEntry."Global Dimension 1 Code" := "Cost Center Code";
                           TargetBudEntry."Global Dimension 2 Code" := "Cost Object Code";
                           TargetBudEntry.Description :=
                             COPYSTR(Text011 + GETFILTER("Budget Name"),1,MAXSTRLEN(TargetBudEntry.Description));
                           TargetBudEntry."User ID" := USERID;

                           // Amt * req. window factor
                           TargetBudEntry.Amount := ROUND(Amount * Factor,0.01);

                           // Create entries according to "copies". Increment date.
                           FOR i := 1 TO NoOfCopies DO BEGIN
                             LastEntryNo := LastEntryNo + 1;
                             TargetBudEntry."Entry No." := LastEntryNo;

                             // Prepare date for next entry
                             IF DateChangeFormula <> '' THEN
                               TargetBudEntry.Date := CALCDATE(DateChangeFormula,TargetBudEntry.Date);

                             TargetBudEntry.INSERT;

                             NoInserted := NoInserted + 1;
                             IF (NoInserted MOD 100) = 0 THEN
                               d.UPDATE(3,NoInserted);
                           END;
                         END;

        OnPostDataItem=BEGIN
                         d.CLOSE;

                         IF NOT CONFIRM(
                              Text012 +
                              Text013 +
                              Text014,
                              TRUE,NoInserted,TargetBudName.Name,NoSkipped)
                         THEN
                           ERROR(Text015);
                       END;

        ReqFilterFields=Budget Name,Cost Type No.,Cost Center Code,Cost Object Code,Date;
      }
      SECTIONS
      {
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=6160;
      Height=2090;
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1   ;TextBox      ;3410 ;0    ;2750 ;440  ;Lookup=Yes;
                                                   CaptionML=[ENU=Target Budget Name;
                                                              ESP=Nombre ppto. destino];
                                                   SourceExpr=TargetBudName.Name;
                                                   TableRelation="G/L Budget Name" }
      { 31  ;Label        ;0    ;0    ;3300 ;440  ;ParentControl=1 }
      { 6   ;TextBox      ;3410 ;1650 ;2750 ;440  ;CaptionML=[ENU=Date Change Formula;
                                                              ESP=F¢rmula de cambio fecha];
                                                   DateFormula=Yes;
                                                   SourceExpr=DateChangeFormula }
      { 26  ;Label        ;0    ;1650 ;3300 ;440  ;ParentControl=6 }
      { 3   ;TextBox      ;3410 ;550  ;1650 ;440  ;CaptionML=[ENU=Amt multiplication factor;
                                                              ESP=Factor multiplic. importe];
                                                   SourceExpr=Factor }
      { 7   ;Label        ;0    ;550  ;3300 ;440  ;ParentControl=3 }
      { 8   ;TextBox      ;3410 ;1100 ;1650 ;440  ;CaptionML=[ENU=No. of Copies;
                                                              ESP=N§ copias];
                                                   SourceExpr=NoOfCopies }
      { 9   ;Label        ;0    ;1100 ;3300 ;440  ;ParentControl=8 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones] }

      { 1   ;2   ;Field     ;
                  Lookup=Yes;
                  CaptionML=[ENU=Target Budget Name;
                             ESP=Nombre ppto. destino];
                  SourceExpr=TargetBudName.Name;
                  TableRelation="G/L Budget Name" }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Amt multiplication factor;
                             ESP=Factor multiplic. importe];
                  SourceExpr=Factor }

      { 8   ;2   ;Field     ;
                  CaptionML=[ENU=No. of Copies;
                             ESP=N§ copias];
                  SourceExpr=NoOfCopies }

      { 6   ;2   ;Field     ;
                  CaptionML=[ENU=Date Change Formula;
                             ESP=F¢rmula de cambio fecha];
                  DateFormula=Yes;
                  SourceExpr=DateChangeFormula }

    }
  }
  CODE
  {
    VAR
      Text000@1150013 : TextConst 'ENU=The multiplication factor must no be 0 or less than 0.;ESP=El factor de multiplicaci¢n no debe ser 0 ni inferior a 0.';
      Text001@1150014 : TextConst 'ENU=No. of copies must be at least 1.;ESP=N§ copias debe ser al menos 1.';
      Text002@1150015 : TextConst 'ENU=If more than one copy should be created, a date change formula must be defined.;ESP=Si debe crearse m s de una copia, es necesario definir una f¢rmula de cambio de fecha.';
      Text003@1150016 : TextConst 'ENU=Define name of source budget.;ESP=Defina el nombre del presupuesto de origen.';
      Text004@1150017 : TextConst 'ENU=Cost budget "%1" will be copied to G/L budget "%2". The budget amounts will be multiplied by factor %3. \;ESP=El ppto. costes "%1" se copiar  en el ppto. contable "%2". Los importes de los pptos. se multiplicar n por el factor %3. \';
      Text005@1150018 : TextConst 'ENU=%4 copies will be created and the posting will be increased from the range of "%5" to  "%6".\\;ESP=Se crear n %4 copias y se incrementar  el registro desde el rango de "%5" hasta "%6".\\';
      Text006@1150019 : TextConst 'ENU=Do you want do copy the budget?;ESP=¨Desea copiar el presupuesto?';
      Text007@1150020 : TextConst 'ENU=Copying budget entries\;ESP=Copiando movs. ppto.\';
      Text008@1150021 : TextConst 'ENU=No of entries #1#####\;ESP=N§ movs.      #1#####\';
      Text009@1150022 : TextConst 'ENU=No of copies  #2#####\;ESP=N§ copias     #2#####\';
      Text010@1150023 : TextConst 'ENU=Copied        #3#####;ESP=Copiados      #3#####';
      Text011@1150024 : TextConst 'ENU="Copy of cost budget ";ESP="Copia ppto. costes "';
      Text012@1150025 : TextConst 'ENU=%1 entries generated in budget %2.\\;ESP=%1 movimientos generados en el presupuesto %2.\\';
      Text013@1150026 : TextConst 'ENU=%3 entries skipped. No corresponding G/L account defined.\\;ESP=%3 movimientos omitidos. No se defini¢ una cuenta correspondiente.\\';
      Text014@1150027 : TextConst 'ENU=Write definitely?;ESP=¨Escribir definitivamente?';
      Text015@1150028 : TextConst 'ENU=Job cancelled.;ESP=Tarea cancelada.';
      TargetBudEntry@1150000 : Record 96;
      TargetBudName@1150001 : Record 95;
      CostType@1150003 : Record 3010551;
      GlAcc@1150004 : Record 15;
      d@1150012 : Dialog;
      DateChangeFormula@1150005 : Code[10];
      LastEntryNo@1150006 : Integer;
      NoOfCopies@1150007 : Integer;
      Factor@1150008 : Decimal;
      i@1150009 : Integer;
      NoSkipped@1150010 : Integer;
      NoInserted@1150011 : Integer;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
