OBJECT Report 595 Adjust Exchange Rates
{
  OBJECT-PROPERTIES
  {
    Date=09/05/14;
    Time=23:24:11;
    Modified=Yes;
    Version List=AIC2009,NAVW16.00.01,NAVES6.00;
  }
  PROPERTIES
  {
    Permissions=TableData 21=rimd,
                TableData 25=rimd,
                TableData 86=rimd,
                TableData 254=rimd,
                TableData 379=rimd,
                TableData 380=rimd,
                TableData 7000002=rimd,
                TableData 7000003=rimd,
                TableData 7000004=rimd;
    CaptionML=[ENU=Adjust Exchange Rates;
               ESP=Ajustar tipos de cambio];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF EndDateReq = 0D THEN
                    EndDate := 31129999D
                  ELSE
                    EndDate := EndDateReq;
                  IF PostingDocNo = '' THEN
                    ERROR(Text000,GenJnlLine.FIELDCAPTION("Document No."));
                  IF NOT AdjCustVendBank AND AdjGLAcc THEN
                    IF NOT CONFIRM(Text001 + Text004,FALSE) THEN
                      ERROR(Text005);

                  SourceCodeSetup.GET;

                  IF ExchRateAdjReg.FIND('+') THEN
                    ExchRateAdjReg.INIT;

                  GLSetup.GET;

                  IF AdjGLAcc THEN BEGIN
                    GLSetup.TESTFIELD("Additional Reporting Currency");

                    Currency3.GET(GLSetup."Additional Reporting Currency");
                    Currency3.TESTFIELD("Realized G/L Gains Account");
                    "G/L Account".GET(Currency3."Realized G/L Gains Account");
                    "G/L Account".TESTFIELD(
                      "Exchange Rate Adjustment",
                      "G/L Account"."Exchange Rate Adjustment"::"No Adjustment");

                    Currency3.TESTFIELD("Realized G/L Losses Account");
                    "G/L Account".GET(Currency3."Realized G/L Losses Account");
                    "G/L Account".TESTFIELD(
                      "Exchange Rate Adjustment",
                      "G/L Account"."Exchange Rate Adjustment"::"No Adjustment");

                    WITH VATPostingSetup2 DO
                      IF FIND('-') THEN
                        REPEAT
                          IF "VAT Calculation Type" <> "VAT Calculation Type"::"Sales Tax" THEN BEGIN
                            CheckExchRateAdjustment(
                              "Purchase VAT Account",TABLECAPTION,FIELDCAPTION("Purchase VAT Account"));
                            CheckExchRateAdjustment(
                              "Reverse Chrg. VAT Acc.",TABLECAPTION,FIELDCAPTION("Reverse Chrg. VAT Acc."));
                            CheckExchRateAdjustment(
                              "Purch. VAT Unreal. Account",TABLECAPTION,FIELDCAPTION("Purch. VAT Unreal. Account"));
                            CheckExchRateAdjustment(
                              "Reverse Chrg. VAT Unreal. Acc.",TABLECAPTION,FIELDCAPTION("Reverse Chrg. VAT Unreal. Acc."));
                            CheckExchRateAdjustment(
                              "Sales VAT Account",TABLECAPTION,FIELDCAPTION("Sales VAT Account"));
                            CheckExchRateAdjustment(
                              "Sales VAT Unreal. Account",TABLECAPTION,FIELDCAPTION("Sales VAT Unreal. Account"));
                          END;
                        UNTIL NEXT = 0;

                    WITH TaxJurisdiction2 DO
                      IF FIND('-') THEN
                        REPEAT
                          CheckExchRateAdjustment(
                            "Tax Account (Purchases)",TABLECAPTION,FIELDCAPTION("Tax Account (Purchases)"));
                          CheckExchRateAdjustment(
                            "Reverse Charge (Purchases)",TABLECAPTION,FIELDCAPTION("Reverse Charge (Purchases)"));
                          CheckExchRateAdjustment(
                            "Unreal. Tax Acc. (Purchases)",TABLECAPTION,FIELDCAPTION("Unreal. Tax Acc. (Purchases)"));
                          CheckExchRateAdjustment(
                            "Unreal. Rev. Charge (Purch.)",TABLECAPTION,FIELDCAPTION("Unreal. Rev. Charge (Purch.)"));
                          CheckExchRateAdjustment(
                            "Tax Account (Sales)",TABLECAPTION,FIELDCAPTION("Tax Account (Sales)"));
                          CheckExchRateAdjustment(
                            "Unreal. Tax Acc. (Sales)",TABLECAPTION,FIELDCAPTION("Unreal. Tax Acc. (Sales)"));
                        UNTIL NEXT = 0;

                    AddCurrCurrencyFactor :=
                      CurrExchRate2.ExchangeRateAdjmt(PostingDate,GLSetup."Additional Reporting Currency");
                  END;
                END;

    OnPostReport=BEGIN
                   UpdateAnalysisView.UpdateAll(0,TRUE);
                 END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table4;
        DataItemTableView=SORTING(Code);
        OnPreDataItem=BEGIN
                        CheckPostingDate;
                        IF NOT AdjCustVendBank THEN
                          CurrReport.BREAK;

                        Window.OPEN(
                          Text006 +
                          Text007 +
                          Text008 +
                          Text009 +
                          Text010);

                        CustNoTotal := Customer.COUNT;
                        VendNoTotal := Vendor.COUNT;
                        Currency.COPYFILTER(Code,"Bank Account"."Currency Code");
                        FILTERGROUP(2);
                        "Bank Account".SETFILTER("Currency Code",'<>%1','');
                        FILTERGROUP(0);
                        BankAccNoTotal := "Bank Account".COUNT;
                        "Bank Account".RESET;
                      END;

        OnAfterGetRecord=BEGIN
                           "Last Date Adjusted" := PostingDate;
                           MODIFY;

                           "Currency Factor" :=
                             CurrExchRate.ExchangeRateAdjmt(PostingDate,Code);

                           Currency2 := Currency;
                           Currency2.INSERT;

                           AdjAmountBankGroup := 0; //+$002
                         END;

        OnPostDataItem=BEGIN
                         IF (Code = '') AND AdjCustVendBank THEN
                           ERROR(Text011);
                       END;

        ReqFilterFields=Code;
      }
      SECTIONS
      {
      }
       }
    { PROPERTIES
      {
        DataItemIndent=1;
        DataItemTable=Table270;
        DataItemTableView=SORTING(Bank Acc. Posting Group);
        OnPreDataItem=BEGIN
                        CurrReport.CREATETOTALS(AdjBase,AdjBaseLCY,AdjAmount);
                        SETRANGE("Date Filter",StartDate,EndDate);
                        TempEntryNoAmountBuf.DELETEALL;
                      END;

        OnAfterGetRecord=BEGIN
                           BankAccNo := BankAccNo + 1;
                           Window.UPDATE(1,ROUND(BankAccNo / BankAccNoTotal * 10000,1));

                           TempJnlLineDim.RESET;
                           TempJnlLineDim.DELETEALL;
                           TempDimBuf.RESET;
                           TempDimBuf.DELETEALL;

                           CALCFIELDS("Balance at Date","Balance at Date (LCY)");
                           AdjBase := "Balance at Date";
                           AdjBaseLCY := "Balance at Date (LCY)";
                           AdjAmount :=
                             ROUND(
                               CurrExchRate.ExchangeAmtFCYToLCYAdjmt(
                                 PostingDate,Currency.Code,"Balance at Date",Currency."Currency Factor")) -
                             "Balance at Date (LCY)";
                           AdjAmountBankGroup += AdjAmount; //+$002

                           IF AdjAmount <> 0 THEN BEGIN
                             GenJnlLine.VALIDATE("Posting Date",PostingDate);
                             GenJnlLine."Document No." := PostingDocNo;
                             GenJnlLine."Account Type" := GenJnlLine."Account Type"::"Bank Account";
                             GenJnlLine.VALIDATE("Account No.","No.");
                             GenJnlLine.Description := STRSUBSTNO(PostingDescription,Currency.Code,AdjBase);
                             GenJnlLine.VALIDATE(Amount,0);
                             GenJnlLine."Amount (LCY)" := AdjAmount;
                             GenJnlLine."Source Currency Code" := Currency.Code;
                             IF Currency.Code = GLSetup."Additional Reporting Currency" THEN
                               GenJnlLine."Source Currency Amount" := 0;
                             GenJnlLine."Source Code" := SourceCodeSetup."Exchange Rate Adjmt.";
                             GenJnlLine."System-Created Entry" := TRUE;
                             GetJnlLineDefDim(GenJnlLine,TempJnlLineDim);
                             CopyJnlLineDimToDimBuf(TempJnlLineDim,TempDimBuf);
                             PostGenJnlLine(GenJnlLine,TempJnlLineDim);
                             WITH TempEntryNoAmountBuf DO BEGIN
                               INIT;
                               "Business Unit Code" := '';
                               "Entry No." := GetDimCombID(TempDimBuf);
                               IF NOT FIND THEN
                                 INSERT;
                               Amount := Amount + AdjAmount;
                               Amount2 := Amount2 + AdjBase;
                               MODIFY;
                             END;
                             TotalAdjAmount := TotalAdjAmount + AdjAmount;
                             Window.UPDATE(4,TotalAdjAmount);
                           END;
                         END;

        OnPostDataItem=BEGIN
                         IF TempEntryNoAmountBuf.FIND('-') THEN
                           REPEAT
                             IF TempEntryNoAmountBuf.Amount <> 0 THEN BEGIN
                               TempJnlLineDim.RESET;
                               TempJnlLineDim.DELETEALL;
                               TempDimBuf.RESET;
                               TempDimBuf.DELETEALL;
                               DimBufMgt.GetDimensions(TempEntryNoAmountBuf."Entry No.",TempDimBuf);
                               DimMgt.CopyDimBufToJnlLineDim(
                                 TempDimBuf,TempJnlLineDim,GenJnlLine."Journal Template Name",
                                 GenJnlLine."Journal Batch Name",GenJnlLine."Line No.");
                               IF TempEntryNoAmountBuf.Amount > 0 THEN BEGIN
                                 Currency.TESTFIELD("Realized Gains Acc.");
                                 PostAdjmt(
                                   Currency."Realized Gains Acc.",-TempEntryNoAmountBuf.Amount,TempEntryNoAmountBuf.Amount2,
                                   "Currency Code",TempJnlLineDim,PostingDate,'');
                               END ELSE BEGIN
                                 Currency.TESTFIELD("Realized Losses Acc.");
                                 PostAdjmt(
                                   Currency."Realized Losses Acc.",-TempEntryNoAmountBuf.Amount,TempEntryNoAmountBuf.Amount2,
                                   "Currency Code",TempJnlLineDim,PostingDate,'');
                               END;
                             END;
                           UNTIL TempEntryNoAmountBuf.NEXT = 0;
                         TempEntryNoAmountBuf.DELETEALL;
                       END;

        DataItemLink=Currency Code=FIELD(Code);
      }
      SECTIONS
      {
      }
       }
    { PROPERTIES
      {
        DataItemIndent=2;
        DataItemTable=Table2000000026;
        DataItemTableView=SORTING(Number);
        MaxIteration=1;
        DataItemVarName=BankAccountGroupTotal;
        OnAfterGetRecord=VAR
                           BankAccount@1102601000 : Record 270;
                           GroupTotal@1102601001 : Boolean;
                         BEGIN
                           BankAccount.COPY("Bank Account");
                           IF BankAccount.NEXT = 1 THEN BEGIN
                             IF BankAccount."Bank Acc. Posting Group" <> "Bank Account"."Bank Acc. Posting Group" THEN
                               GroupTotal := TRUE;
                           END ELSE
                             GroupTotal := TRUE;

                           IF GroupTotal THEN BEGIN
                             //-$002IF AdjAmount <> 0 THEN BEGIN
                             IF AdjAmountBankGroup <> 0 THEN BEGIN //+$002
                               AdjExchRateBufferUpdate(
                                 "Bank Account"."Currency Code","Bank Account"."Bank Acc. Posting Group",
                                 AdjBase,AdjBaseLCY,AdjAmount,0,0,0,PostingDate,'','');
                               InsertExchRateAdjmtReg(3,"Bank Account"."Bank Acc. Posting Group","Bank Account"."Currency Code");
                               AdjExchRateBuffer.RESET;
                               AdjExchRateBuffer.DELETEALL;
                             END;
                             AdjAmountBankGroup := 0; //+$002
                           END;
                         END;

      }
      SECTIONS
      {
      }
       }
    { PROPERTIES
      {
        DataItemTable=Table18;
        DataItemTableView=SORTING(No.);
        OnPreDataItem=BEGIN
                        IF NOT AdjCustVendBank THEN
                          CurrReport.BREAK;

                        DtldCustLedgEntry.LOCKTABLE;
                        CustLedgerEntry.LOCKTABLE;

                        CustNo := 0;

                        IF DtldCustLedgEntry.FIND('+') THEN
                          NewEntryNo := DtldCustLedgEntry."Entry No." + 1
                        ELSE
                          NewEntryNo := 1;

                        CLEAR(DimMgt);
                        TempEntryNoAmountBuf.DELETEALL;
                      END;

        OnAfterGetRecord=BEGIN
                           CustNo := CustNo + 1;
                           Window.UPDATE(2,ROUND(CustNo / CustNoTotal * 10000,1));

                           TempCustLedgerEntry.DELETEALL;

                           Currency.COPYFILTER(Code,CustLedgerEntry."Currency Code");
                           CustLedgerEntry.FILTERGROUP(2);
                           CustLedgerEntry.SETFILTER("Currency Code",'<>%1','');
                           CustLedgerEntry.FILTERGROUP(0);

                           DtldCustLedgEntry.RESET;
                           DtldCustLedgEntry.SETCURRENTKEY("Customer No.","Posting Date","Entry Type");
                           DtldCustLedgEntry.SETRANGE("Customer No.",Customer."No.");
                           DtldCustLedgEntry.SETRANGE("Posting Date",CALCDATE('<+1D>',EndDate),31129999D);
                           IF DtldCustLedgEntry.FIND('-') THEN
                             REPEAT
                               CustLedgerEntry."Entry No." := DtldCustLedgEntry."Cust. Ledger Entry No.";
                               IF CustLedgerEntry.FIND('=') THEN
                                 IF (CustLedgerEntry."Posting Date" >= StartDate) AND
                                   (CustLedgerEntry."Posting Date" <= EndDate)
                                 THEN BEGIN
                                   TempCustLedgerEntry."Entry No." := CustLedgerEntry."Entry No.";
                                   IF TempCustLedgerEntry.INSERT THEN;
                                 END;
                             UNTIL DtldCustLedgEntry.NEXT = 0;

                           CustLedgerEntry.SETCURRENTKEY("Customer No.",Open);
                           CustLedgerEntry.SETRANGE("Customer No.","No.");
                           CustLedgerEntry.SETRANGE(Open,TRUE);
                           CustLedgerEntry.SETRANGE("Posting Date",0D,EndDate);
                           IF CustLedgerEntry.FIND('-') THEN
                             REPEAT
                               TempCustLedgerEntry."Entry No." := CustLedgerEntry."Entry No.";
                               IF TempCustLedgerEntry.INSERT THEN;
                             UNTIL CustLedgerEntry.NEXT = 0;
                           CustLedgerEntry.RESET;
                         END;

        OnPostDataItem=BEGIN
                         IF (CustNo <> 0) THEN
                           HandlePostAdjmt(1); // Customer
                       END;

      }
      SECTIONS
      {
      }
       }
    { PROPERTIES
      {
        DataItemIndent=1;
        DataItemTable=Table2000000026;
        DataItemTableView=SORTING(Number);
        DataItemVarName=CustomerLedgerEntryLoop;
        OnPreDataItem=VAR
                        DtldCustLedgEntry@1000 : Record 379;
                      BEGIN
                        IF NOT TempCustLedgerEntry.FIND('-') THEN
                          CurrReport.BREAK;
                        FirstEntry := TRUE;
                      END;

        OnAfterGetRecord=BEGIN
                           IF FirstEntry THEN BEGIN
                             TempCustLedgerEntry.FIND('-');
                             FirstEntry := FALSE
                           END ELSE
                             IF TempCustLedgerEntry.NEXT = 0 THEN
                               CurrReport.BREAK;
                           CustLedgerEntry.GET(TempCustLedgerEntry."Entry No.");
                           AdjustCustomerLedgerEntry(CustLedgerEntry,PostingDate);
                         END;

      }
      SECTIONS
      {
      }
       }
    { PROPERTIES
      {
        DataItemIndent=2;
        DataItemTable=Table379;
        DataItemTableView=SORTING(Cust. Ledger Entry No.,Posting Date);
        OnPreDataItem=BEGIN
                        SETCURRENTKEY("Cust. Ledger Entry No.");
                        SETRANGE("Cust. Ledger Entry No.",CustLedgerEntry."Entry No.");
                        SETFILTER("Posting Date",'%1..',CALCDATE('<+1D>',PostingDate));
                      END;

        OnAfterGetRecord=BEGIN
                           AdjustCustomerLedgerEntry(CustLedgerEntry,"Posting Date");
                         END;

      }
      SECTIONS
      {
      }
       }
    { PROPERTIES
      {
        DataItemTable=Table23;
        DataItemTableView=SORTING(No.);
        OnPreDataItem=BEGIN
                        IF NOT AdjCustVendBank THEN
                          CurrReport.BREAK;

                        DtldVendLedgEntry.LOCKTABLE;
                        VendorLedgerEntry.LOCKTABLE;

                        VendNo := 0;
                        IF DtldVendLedgEntry.FIND('+') THEN
                          NewEntryNo := DtldVendLedgEntry."Entry No." + 1
                        ELSE
                          NewEntryNo := 1;

                        CLEAR(DimMgt);
                        TempEntryNoAmountBuf.DELETEALL;
                      END;

        OnAfterGetRecord=BEGIN
                           VendNo := VendNo + 1;
                           Window.UPDATE(3,ROUND(VendNo / VendNoTotal * 10000,1));

                           TempVendorLedgerEntry.DELETEALL;

                           Currency.COPYFILTER(Code,VendorLedgerEntry."Currency Code");
                           VendorLedgerEntry.FILTERGROUP(2);
                           VendorLedgerEntry.SETFILTER("Currency Code",'<>%1','');
                           VendorLedgerEntry.FILTERGROUP(0);

                           DtldVendLedgEntry.RESET;
                           DtldVendLedgEntry.SETCURRENTKEY("Vendor No.","Posting Date","Entry Type");
                           DtldVendLedgEntry.SETRANGE("Vendor No.","No.");
                           DtldVendLedgEntry.SETRANGE("Posting Date",CALCDATE('<+1D>',EndDate),31129999D);
                           IF DtldVendLedgEntry.FIND('-') THEN
                             REPEAT
                               VendorLedgerEntry."Entry No." := DtldVendLedgEntry."Vendor Ledger Entry No.";
                               IF VendorLedgerEntry.FIND('=') THEN
                                 IF (VendorLedgerEntry."Posting Date" >= StartDate) AND
                                   (VendorLedgerEntry."Posting Date" <= EndDate)
                                 THEN BEGIN
                                   TempVendorLedgerEntry."Entry No." := VendorLedgerEntry."Entry No.";
                                   IF TempVendorLedgerEntry.INSERT THEN;
                                 END;
                             UNTIL DtldVendLedgEntry.NEXT = 0;

                           VendorLedgerEntry.SETCURRENTKEY("Vendor No.",Open);
                           VendorLedgerEntry.SETRANGE("Vendor No.","No.");
                           VendorLedgerEntry.SETRANGE(Open,TRUE);
                           VendorLedgerEntry.SETRANGE("Posting Date",0D,EndDate);
                           IF VendorLedgerEntry.FIND('-') THEN
                             REPEAT
                               TempVendorLedgerEntry."Entry No." := VendorLedgerEntry."Entry No.";
                               IF TempVendorLedgerEntry.INSERT THEN;
                             UNTIL VendorLedgerEntry.NEXT = 0;
                           VendorLedgerEntry.RESET;
                         END;

        OnPostDataItem=BEGIN
                         IF (VendNo <> 0) THEN
                           HandlePostAdjmt(2); // Vendor
                       END;

      }
      SECTIONS
      {
      }
       }
    { PROPERTIES
      {
        DataItemIndent=1;
        DataItemTable=Table2000000026;
        DataItemTableView=SORTING(Number);
        DataItemVarName=VendorLedgerEntryLoop;
        OnPreDataItem=BEGIN
                        IF NOT TempVendorLedgerEntry.FIND('-') THEN
                          CurrReport.BREAK;
                        FirstEntry := TRUE;
                      END;

        OnAfterGetRecord=BEGIN
                           IF FirstEntry THEN BEGIN
                             TempVendorLedgerEntry.FIND('-');
                             FirstEntry := FALSE
                           END ELSE
                             IF TempVendorLedgerEntry.NEXT = 0 THEN
                               CurrReport.BREAK;
                           VendorLedgerEntry.GET(TempVendorLedgerEntry."Entry No.");
                           AdjustVendorLedgerEntry(VendorLedgerEntry,PostingDate);
                         END;

      }
      SECTIONS
      {
      }
       }
    { PROPERTIES
      {
        DataItemIndent=2;
        DataItemTable=Table380;
        DataItemTableView=SORTING(Vendor Ledger Entry No.,Posting Date);
        OnPreDataItem=BEGIN
                        SETCURRENTKEY("Vendor Ledger Entry No.");
                        SETRANGE("Vendor Ledger Entry No.",VendorLedgerEntry."Entry No.");
                        SETFILTER("Posting Date",'%1..',CALCDATE('<+1D>',PostingDate));
                      END;

        OnAfterGetRecord=BEGIN
                           AdjustVendorLedgerEntry(VendorLedgerEntry,"Posting Date");
                         END;

      }
      SECTIONS
      {
      }
       }
    { PROPERTIES
      {
        DataItemTable=Table325;
        DataItemTableView=SORTING(VAT Bus. Posting Group,VAT Prod. Posting Group);
        OnPreDataItem=BEGIN
                        IF NOT AdjGLAcc OR
                           (GLSetup."VAT Exchange Rate Adjustment" = GLSetup."VAT Exchange Rate Adjustment"::"No Adjustment")
                        THEN
                          CurrReport.BREAK;

                        Window.OPEN(
                          Text012 +
                          Text013);

                        VATEntryNoTotal := VATEntry.COUNT;
                        IF NOT
                          VATEntry.SETCURRENTKEY(
                            Type,Closed,"VAT Bus. Posting Group","VAT Prod. Posting Group","Posting Date")
                        THEN
                          VATEntry.SETCURRENTKEY(
                            Type,Closed,"Tax Jurisdiction Code","Use Tax","Posting Date");
                        VATEntry.SETRANGE(Closed,FALSE);
                        VATEntry.SETRANGE("Posting Date",StartDate,EndDate);
                      END;

        OnAfterGetRecord=BEGIN
                           VATEntryNo := VATEntryNo + 1;
                           Window.UPDATE(1,ROUND(VATEntryNo / VATEntryNoTotal * 10000,1));

                           VATEntry.SETRANGE("VAT Bus. Posting Group","VAT Bus. Posting Group");
                           VATEntry.SETRANGE("VAT Prod. Posting Group","VAT Prod. Posting Group");

                           IF "VAT Calculation Type" <> "VAT Calculation Type"::"Sales Tax" THEN BEGIN
                             AdjustVATEntries(VATEntry.Type::Purchase,FALSE);
                             IF (VATEntry2.Amount <> 0) OR (VATEntry2."Additional-Currency Amount" <> 0) THEN BEGIN
                               TESTFIELD("Purchase VAT Account");
                               AdjustVATAccount(
                                 "Purchase VAT Account",
                                 VATEntry2.Amount,VATEntry2."Additional-Currency Amount",
                                 VATEntryTotalBase.Amount,VATEntryTotalBase."Additional-Currency Amount");
                               IF "VAT Calculation Type" = "VAT Calculation Type"::"Reverse Charge VAT" THEN BEGIN
                                 TESTFIELD("Reverse Chrg. VAT Acc.");
                                 AdjustVATAccount(
                                   "Reverse Chrg. VAT Acc.",
                                   -VATEntry2.Amount,-VATEntry2."Additional-Currency Amount",
                                   -VATEntryTotalBase.Amount,-VATEntryTotalBase."Additional-Currency Amount");
                               END;
                             END;
                             IF (VATEntry2."Remaining Unrealized Amount" <> 0) OR
                                (VATEntry2."Add.-Curr. Rem. Unreal. Amount" <> 0)
                             THEN BEGIN
                               TESTFIELD("Unrealized VAT Type");
                               TESTFIELD("Purch. VAT Unreal. Account");
                               AdjustVATAccount(
                                 "Purch. VAT Unreal. Account",
                                 VATEntry2."Remaining Unrealized Amount",
                                 VATEntry2."Add.-Curr. Rem. Unreal. Amount",
                                 VATEntryTotalBase."Remaining Unrealized Amount",
                                 VATEntryTotalBase."Add.-Curr. Rem. Unreal. Amount");
                               IF "VAT Calculation Type" = "VAT Calculation Type"::"Reverse Charge VAT" THEN BEGIN
                                 TESTFIELD("Reverse Chrg. VAT Unreal. Acc.");
                                 AdjustVATAccount(
                                   "Reverse Chrg. VAT Unreal. Acc.",
                                   -VATEntry2."Remaining Unrealized Amount",
                                   -VATEntry2."Add.-Curr. Rem. Unreal. Amount",
                                   -VATEntryTotalBase."Remaining Unrealized Amount",
                                   -VATEntryTotalBase."Add.-Curr. Rem. Unreal. Amount");

                               END;
                             END;

                             AdjustVATEntries(VATEntry.Type::Sale,FALSE);
                             IF (VATEntry2.Amount <> 0) OR (VATEntry2."Additional-Currency Amount" <> 0) THEN BEGIN
                               TESTFIELD("Sales VAT Account");
                               AdjustVATAccount(
                                 "Sales VAT Account",
                                 VATEntry2.Amount,VATEntry2."Additional-Currency Amount",
                                 VATEntryTotalBase.Amount,VATEntryTotalBase."Additional-Currency Amount");
                             END;
                             IF (VATEntry2."Remaining Unrealized Amount" <> 0) OR
                                (VATEntry2."Add.-Curr. Rem. Unreal. Amount" <> 0)
                             THEN BEGIN
                               TESTFIELD("Unrealized VAT Type");
                               TESTFIELD("Sales VAT Unreal. Account");
                               AdjustVATAccount(
                                 "Sales VAT Unreal. Account",
                                 VATEntry2."Remaining Unrealized Amount",
                                 VATEntry2."Add.-Curr. Rem. Unreal. Amount",
                                 VATEntryTotalBase."Remaining Unrealized Amount",
                                 VATEntryTotalBase."Add.-Curr. Rem. Unreal. Amount");
                             END;
                           END ELSE BEGIN
                             IF TaxJurisdiction.FIND('-') THEN
                               REPEAT
                                 VATEntry.SETRANGE("Tax Jurisdiction Code",TaxJurisdiction.Code);
                                 AdjustVATEntries(VATEntry.Type::Purchase,FALSE);
                                 AdjustPurchTax(FALSE);
                                 AdjustVATEntries(VATEntry.Type::Purchase,TRUE);
                                 AdjustPurchTax(TRUE);
                                 AdjustVATEntries(VATEntry.Type::Sale,FALSE);
                                 AdjustSalesTax;
                               UNTIL TaxJurisdiction.NEXT = 0;
                             VATEntry.SETRANGE("Tax Jurisdiction Code");
                           END;
                         END;

      }
      SECTIONS
      {
      }
       }
    { PROPERTIES
      {
        DataItemTable=Table15;
        DataItemTableView=SORTING(No.)
                          WHERE(Exchange Rate Adjustment=FILTER(Adjust Amount..Adjust Additional-Currency Amount));
        OnPreDataItem=BEGIN
                        IF NOT AdjGLAcc THEN
                          CurrReport.BREAK;

                        Window.OPEN(
                          Text014 +
                          Text015);

                        GLAccNoTotal := "G/L Account".COUNT;
                        SETRANGE("Date Filter",StartDate,EndDate);
                      END;

        OnAfterGetRecord=BEGIN
                           GLAccNo := GLAccNo + 1;
                           Window.UPDATE(1,ROUND(GLAccNo / GLAccNoTotal * 10000,1));
                           IF "Exchange Rate Adjustment" = "Exchange Rate Adjustment"::"No Adjustment" THEN
                             CurrReport.SKIP;

                           CALCFIELDS("Balance at Date","Add.-Currency Balance at Date");
                           CASE "Exchange Rate Adjustment" OF
                             "Exchange Rate Adjustment"::"Adjust Amount":
                               PostGLAccAdjmt(
                                 "No.","Exchange Rate Adjustment"::"Adjust Amount",
                                 ROUND(
                                   CurrExchRate2.ExchangeAmtFCYToLCYAdjmt(
                                     PostingDate,GLSetup."Additional Reporting Currency",
                                     "Add.-Currency Balance at Date",AddCurrCurrencyFactor) -
                                   "Balance at Date"),
                                 "Balance at Date",
                                 "Add.-Currency Balance at Date");
                             "Exchange Rate Adjustment"::"Adjust Additional-Currency Amount":
                               PostGLAccAdjmt(
                                 "No.","Exchange Rate Adjustment"::"Adjust Additional-Currency Amount",
                                 ROUND(
                                   CurrExchRate2.ExchangeAmtLCYToFCY(
                                     PostingDate,GLSetup."Additional Reporting Currency",
                                     "Balance at Date",AddCurrCurrencyFactor) -
                                   "Add.-Currency Balance at Date",
                                   Currency3."Amount Rounding Precision"),
                                 "Balance at Date",
                                 "Add.-Currency Balance at Date");
                           END;
                         END;

        OnPostDataItem=BEGIN
                         IF AdjGLAcc THEN BEGIN
                           GenJnlLine."Document No." := PostingDocNo;
                           GenJnlLine."Account Type" := GenJnlLine."Account Type"::"G/L Account";
                           GenJnlLine."Posting Date" := PostingDate;
                           GenJnlLine."Source Code" := SourceCodeSetup."Exchange Rate Adjmt.";

                           IF GLAmtTotal <> 0 THEN BEGIN
                             IF GLAmtTotal < 0 THEN
                               GenJnlLine."Account No." := Currency3."Realized G/L Losses Account"
                             ELSE
                               GenJnlLine."Account No." := Currency3."Realized G/L Gains Account";
                             GenJnlLine.Description :=
                               STRSUBSTNO(
                                 PostingDescription,
                                 GLSetup."Additional Reporting Currency",
                                 GLAddCurrBalAtDateTotal);
                             GenJnlLine."Additional-Currency Posting" := GenJnlLine."Additional-Currency Posting"::"Amount Only";
                             GenJnlLine."Currency Code" := '';
                             GenJnlLine.Amount := -GLAmtTotal;
                             GenJnlLine."Amount (LCY)" := -GLAmtTotal;
                             GetJnlLineDefDim(GenJnlLine,TempJnlLineDim);
                             PostGenJnlLine(GenJnlLine,TempJnlLineDim);
                           END;
                           IF GLAddCurrAmtTotal <> 0 THEN BEGIN
                             IF GLAddCurrAmtTotal > 0 THEN
                               GenJnlLine."Account No." := Currency3."Realized G/L Losses Account"
                             ELSE
                               GenJnlLine."Account No." := Currency3."Realized G/L Gains Account";
                             GenJnlLine.Description :=
                               STRSUBSTNO(
                                 PostingDescription,'',
                                 GLBalAtDateTotal);
                             GenJnlLine."Additional-Currency Posting" := GenJnlLine."Additional-Currency Posting"::"Additional-Currency Amount Only";
                             GenJnlLine."Currency Code" := GLSetup."Additional Reporting Currency";
                             GenJnlLine.Amount := -GLAddCurrAmtTotal;
                             GenJnlLine."Amount (LCY)" := 0;
                             GetJnlLineDefDim(GenJnlLine,TempJnlLineDim);
                             PostGenJnlLine(GenJnlLine,TempJnlLineDim);
                           END;

                           WITH ExchRateAdjReg DO BEGIN
                             "No." := "No." + 1;
                             "Creation Date" := PostingDate;
                             "Account Type" := "Account Type"::"G/L Account";
                             "Posting Group" := '';
                             "Currency Code" := GLSetup."Additional Reporting Currency";
                             "Currency Factor" := CurrExchRate2."Adjustment Exch. Rate Amount";
                             "Adjusted Base" := 0;
                             "Adjusted Base (LCY)" := GLBalAtDateBase;
                             "Adjusted Amt. (LCY)" := GLAmtTotal;
                             "Adjusted Base (Add.-Curr.)" := GLAddCurrBalAtDateBase;
                             "Adjusted Amt. (Add.-Curr.)" := GLAddCurrAmtTotal;
                             INSERT;
                           END;
                         END;
                       END;

      }
      SECTIONS
      {
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=9130;
      Height=5500;
      SaveValues=Yes;
      OnOpenForm=BEGIN
                   IF PostingDescription = '' THEN
                     PostingDescription := Text016;
                   IF NOT (AdjCustVendBank OR AdjGLAcc) THEN
                     AdjCustVendBank := TRUE;
                 END;

    }
    CONTROLS
    {
      { 1   ;TextBox      ;3410 ;550  ;1650 ;440  ;CaptionML=[ENU=Starting Date;
                                                              ESP=Fecha inicial];
                                                   SourceExpr=StartDate }
      { 21  ;Label        ;0    ;0    ;3300 ;440  ;LeaderDots=No;
                                                   CaptionML=[ENU=Adjustment Period;
                                                              ESP=Periodo de ajuste] }
      { 31  ;Label        ;220  ;550  ;3080 ;440  ;ParentControl=1 }
      { 2   ;TextBox      ;3410 ;1100 ;1650 ;440  ;CaptionML=[ENU=Ending Date;
                                                              ESP=Fecha final];
                                                   SourceExpr=EndDateReq;
                                                   OnValidate=BEGIN
                                                                PostingDate := EndDateReq;
                                                              END;
                                                               }
      { 22  ;Label        ;220  ;1100 ;3080 ;440  ;ParentControl=2 }
      { 3   ;TextBox      ;3410 ;1760 ;5500 ;440  ;CaptionML=[ENU=Posting Description;
                                                              ESP=Texto de registro];
                                                   SourceExpr=PostingDescription }
      { 23  ;Label        ;0    ;1760 ;3300 ;440  ;ParentControl=3 }
      { 4   ;TextBox      ;3410 ;2310 ;1650 ;440  ;CaptionML=[ENU=Posting Date;
                                                              ESP=Fecha registro];
                                                   SourceExpr=PostingDate;
                                                   OnValidate=BEGIN
                                                                CheckPostingDate;
                                                              END;
                                                               }
      { 24  ;Label        ;0    ;2310 ;3300 ;440  ;ParentControl=4 }
      { 5   ;TextBox      ;3410 ;2860 ;2750 ;440  ;CaptionML=[ENU=Document No.;
                                                              ESP=N§ documento];
                                                   SourceExpr=PostingDocNo }
      { 6   ;Label        ;0    ;2860 ;3300 ;440  ;ParentControl=5 }
      { 7   ;CheckBox     ;3410 ;3850 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Adjust Customer, Vendor and Bank Accounts;
                                                              ESP=Ajuste cliente, proveedor y banco];
                                                   SourceExpr=AdjCustVendBank }
      { 8   ;Label        ;0    ;3410 ;3300 ;880  ;ParentControl=7;
                                                   VertAlign=Bottom;
                                                   MultiLine=Yes }
      { 9   ;CheckBox     ;3410 ;4840 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Adjust G/L Accounts for Add.-Reporting Currency;
                                                              ESP=Ajuste Divisa.-Adic.];
                                                   SourceExpr=AdjGLAcc }
      { 10  ;Label        ;0    ;4400 ;3300 ;880  ;ParentControl=9;
                                                   MultiLine=Yes }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF PostingDescription = '' THEN
                     PostingDescription := Text016;
                   IF NOT (AdjCustVendBank OR AdjGLAcc) THEN
                     AdjCustVendBank := TRUE;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones] }

      { 21  ;2   ;Group     ;
                  CaptionML=[ENU=Adjustment Period;
                             ESP=Periodo de ajuste] }

      { 1   ;3   ;Field     ;
                  CaptionML=[ENU=Starting Date;
                             ESP=Fecha inicial];
                  SourceExpr=StartDate }

      { 2   ;3   ;Field     ;
                  CaptionML=[ENU=Ending Date;
                             ESP=Fecha final];
                  SourceExpr=EndDateReq;
                  OnValidate=BEGIN
                               PostingDate := EndDateReq;
                             END;
                              }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Posting Description;
                             ESP=Texto de registro];
                  SourceExpr=PostingDescription }

      { 4   ;2   ;Field     ;
                  CaptionML=[ENU=Posting Date;
                             ESP=Fecha registro];
                  SourceExpr=PostingDate;
                  OnValidate=BEGIN
                               CheckPostingDate;
                             END;
                              }

      { 5   ;2   ;Field     ;
                  CaptionML=[ENU=Document No.;
                             ESP=N§ documento];
                  SourceExpr=PostingDocNo }

      { 7   ;2   ;Field     ;
                  CaptionML=[ENU=Adjust Customer, Vendor and Bank Accounts;
                             ESP=Ajuste cliente, proveedor y banco];
                  SourceExpr=AdjCustVendBank;
                  MultiLine=Yes }

      { 9   ;2   ;Field     ;
                  CaptionML=[ENU=Adjust G/L Accounts for Add.-Reporting Currency;
                             ESP=Ajuste Divisa.-Adic.];
                  SourceExpr=AdjGLAcc;
                  MultiLine=Yes }

    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 must be entered.;ESP=Se debe indicar %1.';
      Text001@1001 : TextConst 'ENU="Do you want to adjust general ledger entries for currency fluctuations without adjusting customer, vendor and bank ledger entries? This may result in incorrect currency adjustments to payables, receivables and bank accounts.\\ ";ESP="¨Desea ajustar los movs. contabilidad para las fluctuaciones de divisa sin ajustar lo movs. de cliente, proveedor y bancos? Esto puede provocar ajustes incorrectos de divisa en pagos, cobros y cuentas bancos.\\ "';
      Text004@1004 : TextConst 'ENU=Do you wish to continue?;ESP=¨Confirma que desea continuar?';
      Text005@1005 : TextConst 'ENU=The adjustment of exchange rates has been canceled.;ESP=Se ha cancelado el proceso de ajuste de divisas.';
      Text006@1006 : TextConst 'ENU=Adjusting exchange rates...\\;ESP=Ajustando divisas...\\';
      Text007@1007 : TextConst 'ENU=Bank Account    @1@@@@@@@@@@@@@\\;ESP=Banco           @1@@@@@@@@@@@@@\\';
      Text008@1008 : TextConst 'ENU=Customer        @2@@@@@@@@@@@@@\;ESP=Cliente         @2@@@@@@@@@@@@@\';
      Text009@1009 : TextConst 'ENU=Vendor          @3@@@@@@@@@@@@@\;ESP=Proveedor       @3@@@@@@@@@@@@@\';
      Text010@1010 : TextConst 'ENU=Adjustment      #4#############;ESP=Ajuste          #4#############';
      Text011@1011 : TextConst 'ENU=No currencies have been found.;ESP=No se han encontrado divisas.';
      Text012@1012 : TextConst 'ENU=Adjusting VAT Entries...\\;ESP=Ajustando movs. IVA...\\';
      Text013@1013 : TextConst 'ENU=VAT Entry    @1@@@@@@@@@@@@@;ESP=Mov. IVA     @1@@@@@@@@@@@@@';
      Text014@1014 : TextConst 'ENU=Adjusting general ledger...\\;ESP=Ajustando contabilidad...\\';
      Text015@1015 : TextConst 'ENU=G/L Account    @1@@@@@@@@@@@@@;ESP=Cuenta         @1@@@@@@@@@@@@@';
      Text016@1016 : TextConst 'ENU=Exchange Rate Adjmt. of %1 %2;ESP=Aj. divisa %1 %2';
      Text017@1017 : TextConst 'ENU="%1 on %2 %3 must be %4. When this %2 is used in %5, the exchange rate adjustment is defined in the %6 field in the %7. %2 %3 is used in the %8 field in the %5. ";ESP="%1 en %2 %3 debe ser %4. Cuando este %2 se utiliza en %5, el ajuste tipo cambio se define en el campo %6 en el %7. %2 %3 se utiliza en el campo %8 en el %5. "';
      DtldCustLedgEntry@1019 : Record 379;
      TempDtldCustLedgEntry@1003 : TEMPORARY Record 379;
      DtldVendLedgEntry@1020 : Record 380;
      TempDtldVendLedgEntry@1018 : TEMPORARY Record 380;
      ExchRateAdjReg@1021 : Record 86;
      CustPostingGr@1022 : Record 92;
      VendPostingGr@1023 : Record 93;
      GenJnlLine@1024 : Record 81;
      SourceCodeSetup@1025 : Record 242;
      AdjExchRateBuffer@1026 : TEMPORARY Record 331;
      AdjExchRateBuffer2@1027 : TEMPORARY Record 331;
      Currency2@1028 : TEMPORARY Record 4;
      Currency3@1029 : Record 4;
      CurrExchRate@1030 : Record 330;
      CurrExchRate2@1031 : Record 330;
      GLSetup@1032 : Record 98;
      VATEntry@1033 : Record 254;
      VATEntry2@1034 : Record 254;
      VATEntryTotalBase@1035 : Record 254;
      TaxJurisdiction@1036 : Record 320;
      VATPostingSetup2@1037 : Record 325;
      TaxJurisdiction2@1038 : Record 320;
      Doc@1100002 : Record 7000002;
      PostedDoc@1100001 : Record 7000003;
      ClosedDoc@1100000 : Record 7000004;
      TempDimBuf@1049 : TEMPORARY Record 360;
      TempJnlLineDim@1050 : TEMPORARY Record 356;
      TempEntryNoAmountBuf@1079 : TEMPORARY Record 386;
      CustLedgerEntry@1083 : Record 21;
      TempCustLedgerEntry@1082 : TEMPORARY Record 21;
      VendorLedgerEntry@1081 : Record 25;
      TempVendorLedgerEntry@1085 : TEMPORARY Record 25;
      GenJnlPostLine@1039 : Codeunit 12;
      ApplicationManagement@1100100 : Codeunit 1;
      UpdateAnalysisView@1002 : Codeunit 410;
      DimMgt@1077 : Codeunit 408;
      DimBufMgt@1078 : Codeunit 411;
      Window@1040 : Dialog;
      TotalAdjAmount@1041 : Decimal;
      GainsAmount@1042 : Decimal;
      LossesAmount@1043 : Decimal;
      PostingDate@1044 : Date;
      PostingDescription@1045 : Text[50];
      AdjBase@1046 : Decimal;
      AdjBaseLCY@1047 : Decimal;
      AdjAmount@1048 : Decimal;
      CustNo@1051 : Decimal;
      CustNoTotal@1052 : Decimal;
      VendNo@1053 : Decimal;
      VendNoTotal@1054 : Decimal;
      BankAccNo@1055 : Decimal;
      BankAccNoTotal@1056 : Decimal;
      GLAccNo@1057 : Decimal;
      GLAccNoTotal@1058 : Decimal;
      GLAmtTotal@1059 : Decimal;
      GLAddCurrAmtTotal@1060 : Decimal;
      GLBalAtDateTotal@1061 : Decimal;
      GLAddCurrBalAtDateTotal@1062 : Decimal;
      GLBalAtDateBase@1063 : Decimal;
      GLAddCurrBalAtDateBase@1064 : Decimal;
      PostingDocNo@1065 : Code[20];
      StartDate@1066 : Date;
      EndDate@1067 : Date;
      EndDateReq@1068 : Date;
      Correction@1069 : Boolean;
      OK@1070 : Boolean;
      AdjCustVendBank@1071 : Boolean;
      AdjGLAcc@1072 : Boolean;
      AddCurrCurrencyFactor@1073 : Decimal;
      VATEntryNoTotal@1074 : Decimal;
      VATEntryNo@1075 : Decimal;
      NewEntryNo@1076 : Integer;
      Text018@1080 : TextConst 'ENU=This posting date cannot be entered because it does not occur within the adjustment period. Reenter the posting date.;ESP=Esta fecha de registro no es v lida porque no tiene lugar dentro del periodo de ajuste. Vuelva a introducir la fecha de registro.';
      FirstEntry@1084 : Boolean;
      AdjAmountBankGroup@1100217000 : Decimal;

    LOCAL PROCEDURE PostAdjmt@1(VAR GLAccNo@1000 : Code[20];PostingAmount@1001 : Decimal;AdjBase2@1002 : Decimal;CurrencyCode2@1003 : Code[10];VAR JnlLineDim@1004 : Record 356;PostingDate2@1005 : Date;ICCode@1006 : Code[20]);
    BEGIN
      WITH GenJnlLine DO BEGIN
        IF PostingAmount <> 0 THEN BEGIN
          INIT;
          VALIDATE("Posting Date",PostingDate2);
          "Document No." := PostingDocNo;
          "Account Type" := "Account Type"::"G/L Account";
          VALIDATE("Account No.",GLAccNo);
          Description := STRSUBSTNO(PostingDescription,CurrencyCode2,AdjBase2);
          VALIDATE(Amount,PostingAmount);
          "Source Currency Code" := CurrencyCode2;
          GenJnlLine."IC Partner Code" := ICCode;
          IF CurrencyCode2 = GLSetup."Additional Reporting Currency" THEN
            "Source Currency Amount" := 0;
          "Source Code" := SourceCodeSetup."Exchange Rate Adjmt.";
          "System-Created Entry" := TRUE;
          PostGenJnlLine(GenJnlLine,JnlLineDim);
        END;
      END;
    END;

    LOCAL PROCEDURE InsertExchRateAdjmtReg@2(AdjustAccType@1000 : Integer;PostingGrCode@1001 : Code[10];CurrencyCode@1002 : Code[10]);
    BEGIN
      IF Currency2.Code <> CurrencyCode THEN
        Currency2.GET(CurrencyCode);

      WITH ExchRateAdjReg DO BEGIN
        "No." := "No." + 1;
        "Creation Date" := PostingDate;
        "Account Type" := AdjustAccType;
        "Posting Group" := PostingGrCode;
        "Currency Code" := Currency2.Code;
        "Currency Factor" := Currency2."Currency Factor";
        "Adjusted Base" := AdjExchRateBuffer.AdjBase;
        "Adjusted Base (LCY)" := AdjExchRateBuffer.AdjBaseLCY;
        "Adjusted Amt. (LCY)" := AdjExchRateBuffer.AdjAmount;
        INSERT;
      END;
    END;

    PROCEDURE InitializeRequest@3(NewStartDate@1000 : Date;NewEndDate@1001 : Date;NewPostingDescription@1002 : Text[50];NewPostingDate@1003 : Date);
    BEGIN
      StartDate := NewStartDate;
      EndDate := NewEndDate;
      PostingDescription := NewPostingDescription;
      PostingDate := NewPostingDate;
    END;

    LOCAL PROCEDURE AdjExchRateBufferUpdate@15(CurrencyCode2@1000 : Code[10];PostingGroup2@1001 : Code[10];AdjBase2@1002 : Decimal;AdjBaseLCY2@1003 : Decimal;AdjAmount2@1004 : Decimal;GainsAmount2@1005 : Decimal;LossesAmount2@1006 : Decimal;DimEntryNo@1007 : Integer;Postingdate2@1008 : Date;ICCode@1009 : Code[20];AccNo@1100010 : Code[20]);
    BEGIN
      AdjExchRateBuffer.INIT;
      OK := AdjExchRateBuffer.GET(CurrencyCode2,PostingGroup2,AccNo,DimEntryNo,Postingdate2,ICCode);

      AdjExchRateBuffer.AdjBase := AdjExchRateBuffer.AdjBase + AdjBase2;
      AdjExchRateBuffer.AdjBaseLCY := AdjExchRateBuffer.AdjBaseLCY + AdjBaseLCY2;
      AdjExchRateBuffer.AdjAmount := AdjExchRateBuffer.AdjAmount + AdjAmount2;
      AdjExchRateBuffer.TotalGainsAmount := AdjExchRateBuffer.TotalGainsAmount + GainsAmount2;
      AdjExchRateBuffer.TotalLossesAmount := AdjExchRateBuffer.TotalLossesAmount + LossesAmount2;

      IF NOT OK THEN BEGIN
        AdjExchRateBuffer."Currency Code" := CurrencyCode2;
        AdjExchRateBuffer."Posting Group" := PostingGroup2;
        AdjExchRateBuffer."Dimension Entry No." := DimEntryNo;
        AdjExchRateBuffer."Posting Date" := Postingdate2;
        AdjExchRateBuffer."IC Partner Code" := ICCode;
        AdjExchRateBuffer."Account No." := AccNo;
        AdjExchRateBuffer.INSERT;
      END ELSE
        AdjExchRateBuffer.MODIFY;
    END;

    LOCAL PROCEDURE HandlePostAdjmt@7(AdjustAccType@1000 : Integer);
    VAR
      GLEntry@1001 : Record 17;
    BEGIN
      IF AdjExchRateBuffer.FIND('-') THEN BEGIN
        // Summarize per currency and dimension combination
        REPEAT
          AdjExchRateBuffer2.INIT;
          OK :=
            AdjExchRateBuffer2.GET(
              AdjExchRateBuffer."Currency Code",
              '','',
              AdjExchRateBuffer."Dimension Entry No.",
              AdjExchRateBuffer."Posting Date",
              AdjExchRateBuffer."IC Partner Code");
          AdjExchRateBuffer2.AdjBase := AdjExchRateBuffer2.AdjBase + AdjExchRateBuffer.AdjBase;
          AdjExchRateBuffer2.TotalGainsAmount := AdjExchRateBuffer2.TotalGainsAmount + AdjExchRateBuffer.TotalGainsAmount;
          AdjExchRateBuffer2.TotalLossesAmount := AdjExchRateBuffer2.TotalLossesAmount + AdjExchRateBuffer.TotalLossesAmount;
          IF NOT OK THEN BEGIN
            AdjExchRateBuffer2."Currency Code" := AdjExchRateBuffer."Currency Code";
            AdjExchRateBuffer2."Dimension Entry No." := AdjExchRateBuffer."Dimension Entry No.";
            AdjExchRateBuffer2."Posting Date" := AdjExchRateBuffer."Posting Date";
            AdjExchRateBuffer2."IC Partner Code" := AdjExchRateBuffer."IC Partner Code";
            // $001 AJS 10.12.2007, Error inconsistencia
            // AdjExchRateBuffer2."Account No." := AdjExchRateBuffer."Account No."; // es0002
            AdjExchRateBuffer2.INSERT;
          END ELSE
            AdjExchRateBuffer2.MODIFY;
        UNTIL AdjExchRateBuffer.NEXT = 0;

        // Post per posting group and per currency
        IF AdjExchRateBuffer2.FIND('-') THEN
          REPEAT
            WITH AdjExchRateBuffer DO BEGIN
              SETRANGE("Currency Code",AdjExchRateBuffer2."Currency Code");
              SETRANGE("Dimension Entry No.",AdjExchRateBuffer2."Dimension Entry No.");
              SETRANGE("Posting Date",AdjExchRateBuffer2."Posting Date");
              SETRANGE("IC Partner Code",AdjExchRateBuffer2."IC Partner Code");
              TempDimBuf.RESET;
              TempDimBuf.DELETEALL;
              TempJnlLineDim.RESET;
              TempJnlLineDim.DELETEALL;
              FIND('-');
              DimBufMgt.GetDimensions("Dimension Entry No.",TempDimBuf);
              DimMgt.CopyDimBufToJnlLineDim(TempDimBuf,TempJnlLineDim,'','',0);
              REPEAT
                IF AdjAmount <> 0 THEN
                  CASE AdjustAccType OF
                    1: // Customer
                      BEGIN
                        CustPostingGr.GET("Posting Group");
                        CustPostingGr.TESTFIELD("Receivables Account");
                        PostAdjmt(
                          CustPostingGr."Receivables Account",AdjAmount,AdjBase,"Currency Code",TempJnlLineDim,
                          AdjExchRateBuffer2."Posting Date","IC Partner Code");
                        InsertExchRateAdjmtReg(1,"Posting Group","Currency Code");
                      END;
                    2: // Vendor
                      BEGIN
                        VendPostingGr.GET("Posting Group");
                        VendPostingGr.TESTFIELD("Payables Account");
                        PostAdjmt(
                          VendPostingGr."Payables Account",AdjAmount,AdjBase,"Currency Code",TempJnlLineDim,
                          AdjExchRateBuffer2."Posting Date",AdjExchRateBuffer."IC Partner Code");
                        InsertExchRateAdjmtReg(2,"Posting Group","Currency Code");
                      END;
                  END;
              UNTIL AdjExchRateBuffer.NEXT = 0;
            END;

            WITH AdjExchRateBuffer2 DO BEGIN
              Currency2.GET("Currency Code");
              IF TotalGainsAmount <> 0 THEN BEGIN
                Currency2.TESTFIELD("Unrealized Gains Acc.");
                PostAdjmt(
                  Currency2."Unrealized Gains Acc.",-TotalGainsAmount,AdjBase,"Currency Code",TempJnlLineDim,
                  AdjExchRateBuffer2."Posting Date","IC Partner Code");
              END;
              IF TotalLossesAmount <> 0 THEN BEGIN
                Currency2.TESTFIELD("Unrealized Losses Acc.");
                PostAdjmt(
                  Currency2."Unrealized Losses Acc.",-TotalLossesAmount,AdjBase,"Currency Code",TempJnlLineDim,
                  AdjExchRateBuffer2."Posting Date","IC Partner Code");
              END;
            END;
          UNTIL AdjExchRateBuffer2.NEXT = 0;

        GLEntry.FIND('+');
        CASE AdjustAccType OF
          1: // Customer
            IF TempDtldCustLedgEntry.FIND('-') THEN
              REPEAT
                TempDtldCustLedgEntry."Transaction No." := GLEntry."Transaction No.";
                DtldCustLedgEntry := TempDtldCustLedgEntry;
                DtldCustLedgEntry.INSERT;
              UNTIL TempDtldCustLedgEntry.NEXT = 0;
          2: // Vendor
            IF TempDtldVendLedgEntry.FIND('-') THEN
              REPEAT
                TempDtldVendLedgEntry."Transaction No." := GLEntry."Transaction No.";
                DtldVendLedgEntry := TempDtldVendLedgEntry;
                DtldVendLedgEntry.INSERT;
              UNTIL TempDtldVendLedgEntry.NEXT = 0;
        END;

        AdjExchRateBuffer.RESET;
        AdjExchRateBuffer.DELETEALL;
        AdjExchRateBuffer2.RESET;
        AdjExchRateBuffer2.DELETEALL;
        TempDtldCustLedgEntry.RESET;
        TempDtldCustLedgEntry.DELETEALL;
        TempDtldVendLedgEntry.RESET;
        TempDtldVendLedgEntry.DELETEALL;
      END;
    END;

    LOCAL PROCEDURE AdjustVATEntries@12(VATType@1000 : Integer;UseTax@1001 : Boolean);
    BEGIN
      CLEAR(VATEntry2);
      WITH VATEntry DO BEGIN
        SETRANGE(Type,VATType);
        SETRANGE("Use Tax",UseTax);
        IF FIND('-') THEN
          REPEAT
            Accumulate(VATEntry2.Base,Base);
            Accumulate(VATEntry2.Amount,Amount);
            Accumulate(VATEntry2."Unrealized Amount","Unrealized Amount");
            Accumulate(VATEntry2."Unrealized Base","Unrealized Base");
            Accumulate(VATEntry2."Remaining Unrealized Amount","Remaining Unrealized Amount");
            Accumulate(VATEntry2."Remaining Unrealized Base","Remaining Unrealized Base");
            Accumulate(VATEntry2."Additional-Currency Amount","Additional-Currency Amount");
            Accumulate(VATEntry2."Additional-Currency Base","Additional-Currency Base");
            Accumulate(VATEntry2."Add.-Currency Unrealized Amt.","Add.-Currency Unrealized Amt.");
            Accumulate(VATEntry2."Add.-Currency Unrealized Base","Add.-Currency Unrealized Base");
            Accumulate(VATEntry2."Add.-Curr. Rem. Unreal. Amount","Add.-Curr. Rem. Unreal. Amount");
            Accumulate(VATEntry2."Add.-Curr. Rem. Unreal. Base","Add.-Curr. Rem. Unreal. Base");

            Accumulate(VATEntryTotalBase.Base,Base);
            Accumulate(VATEntryTotalBase.Amount,Amount);
            Accumulate(VATEntryTotalBase."Unrealized Amount","Unrealized Amount");
            Accumulate(VATEntryTotalBase."Unrealized Base","Unrealized Base");
            Accumulate(VATEntryTotalBase."Remaining Unrealized Amount","Remaining Unrealized Amount");
            Accumulate(VATEntryTotalBase."Remaining Unrealized Base","Remaining Unrealized Base");
            Accumulate(VATEntryTotalBase."Additional-Currency Amount","Additional-Currency Amount");
            Accumulate(VATEntryTotalBase."Additional-Currency Base","Additional-Currency Base");
            Accumulate(VATEntryTotalBase."Add.-Currency Unrealized Amt.","Add.-Currency Unrealized Amt.");
            Accumulate(VATEntryTotalBase."Add.-Currency Unrealized Base","Add.-Currency Unrealized Base");
            Accumulate(
              VATEntryTotalBase."Add.-Curr. Rem. Unreal. Amount","Add.-Curr. Rem. Unreal. Amount");
            Accumulate(VATEntryTotalBase."Add.-Curr. Rem. Unreal. Base","Add.-Curr. Rem. Unreal. Base");

            AdjustVATAmount(Base,"Additional-Currency Base");
            AdjustVATAmount(Amount,"Additional-Currency Amount");
            AdjustVATAmount("Unrealized Amount","Add.-Currency Unrealized Amt.");
            AdjustVATAmount("Unrealized Base","Add.-Currency Unrealized Base");
            AdjustVATAmount("Remaining Unrealized Amount","Add.-Curr. Rem. Unreal. Amount");
            AdjustVATAmount("Remaining Unrealized Base","Add.-Curr. Rem. Unreal. Base");
            MODIFY;

            Accumulate(VATEntry2.Base,-Base);
            Accumulate(VATEntry2.Amount,-Amount);
            Accumulate(VATEntry2."Unrealized Amount",-"Unrealized Amount");
            Accumulate(VATEntry2."Unrealized Base",-"Unrealized Base");
            Accumulate(VATEntry2."Remaining Unrealized Amount",-"Remaining Unrealized Amount");
            Accumulate(VATEntry2."Remaining Unrealized Base",-"Remaining Unrealized Base");
            Accumulate(VATEntry2."Additional-Currency Amount",-"Additional-Currency Amount");
            Accumulate(VATEntry2."Additional-Currency Base",-"Additional-Currency Base");
            Accumulate(VATEntry2."Add.-Currency Unrealized Amt.",-"Add.-Currency Unrealized Amt.");
            Accumulate(VATEntry2."Add.-Currency Unrealized Base",-"Add.-Currency Unrealized Base");
            Accumulate(VATEntry2."Add.-Curr. Rem. Unreal. Amount",-"Add.-Curr. Rem. Unreal. Amount");
            Accumulate(VATEntry2."Add.-Curr. Rem. Unreal. Base",-"Add.-Curr. Rem. Unreal. Base");
          UNTIL NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE AdjustVATAmount@4(VAR AmountLCY@1000 : Decimal;VAR AmountAddCurr@1001 : Decimal);
    BEGIN
      CASE GLSetup."VAT Exchange Rate Adjustment" OF
        GLSetup."VAT Exchange Rate Adjustment"::"Adjust Amount":
          BEGIN
            AmountLCY :=
              ROUND(
                CurrExchRate2.ExchangeAmtFCYToLCYAdjmt(
                  PostingDate,GLSetup."Additional Reporting Currency",
                  AmountAddCurr,AddCurrCurrencyFactor));
          END;
        GLSetup."VAT Exchange Rate Adjustment"::"Adjust Additional-Currency Amount":
          BEGIN
            AmountAddCurr :=
              ROUND(
                CurrExchRate2.ExchangeAmtLCYToFCY(
                  PostingDate,GLSetup."Additional Reporting Currency",
                  AmountLCY,AddCurrCurrencyFactor));
          END;
      END;
    END;

    LOCAL PROCEDURE AdjustVATAccount@13(AccNo@1000 : Code[20];AmountLCY@1001 : Decimal;AmountAddCurr@1002 : Decimal;BaseLCY@1003 : Decimal;BaseAddCurr@1004 : Decimal);
    BEGIN
      "G/L Account".GET(AccNo);
      "G/L Account".SETRANGE("Date Filter",StartDate,EndDate);
      CASE GLSetup."VAT Exchange Rate Adjustment" OF
        GLSetup."VAT Exchange Rate Adjustment"::"Adjust Amount":
          PostGLAccAdjmt(
            AccNo,GLSetup."VAT Exchange Rate Adjustment"::"Adjust Amount",
            -AmountLCY,BaseLCY,BaseAddCurr);
        GLSetup."VAT Exchange Rate Adjustment"::"Adjust Additional-Currency Amount":
          PostGLAccAdjmt(
            AccNo,GLSetup."VAT Exchange Rate Adjustment"::"Adjust Additional-Currency Amount",
            -AmountAddCurr,BaseLCY,BaseAddCurr);
      END;
    END;

    LOCAL PROCEDURE AdjustPurchTax@5(UseTax@1000 : Boolean);
    BEGIN
      IF (VATEntry2.Amount <> 0) OR (VATEntry2."Additional-Currency Amount" <> 0) THEN BEGIN
        TaxJurisdiction.TESTFIELD("Tax Account (Purchases)");
        AdjustVATAccount(
          TaxJurisdiction."Tax Account (Purchases)",
          VATEntry2.Amount,VATEntry2."Additional-Currency Amount",
          VATEntryTotalBase.Amount,VATEntryTotalBase."Additional-Currency Amount");
        IF UseTax THEN BEGIN
          TaxJurisdiction.TESTFIELD("Reverse Charge (Purchases)");
          AdjustVATAccount(
            TaxJurisdiction."Reverse Charge (Purchases)",
            -VATEntry2.Amount,-VATEntry2."Additional-Currency Amount",
            -VATEntryTotalBase.Amount,-VATEntryTotalBase."Additional-Currency Amount");
        END;
      END;
      IF (VATEntry2."Remaining Unrealized Amount" <> 0) OR
         (VATEntry2."Add.-Curr. Rem. Unreal. Amount" <> 0)
      THEN BEGIN
        TaxJurisdiction.TESTFIELD("Unrealized VAT Type");
        TaxJurisdiction.TESTFIELD("Unreal. Tax Acc. (Purchases)");
        AdjustVATAccount(
          TaxJurisdiction."Unreal. Tax Acc. (Purchases)",
          VATEntry2."Remaining Unrealized Amount",VATEntry2."Add.-Curr. Rem. Unreal. Amount",
          VATEntryTotalBase."Remaining Unrealized Amount",VATEntry2."Add.-Curr. Rem. Unreal. Amount");

        IF UseTax THEN BEGIN
          TaxJurisdiction.TESTFIELD("Unreal. Rev. Charge (Purch.)");
          AdjustVATAccount(
            TaxJurisdiction."Unreal. Rev. Charge (Purch.)",
            -VATEntry2."Remaining Unrealized Amount",
            -VATEntry2."Add.-Curr. Rem. Unreal. Amount",
            -VATEntryTotalBase."Remaining Unrealized Amount",
            -VATEntryTotalBase."Add.-Curr. Rem. Unreal. Amount");
        END;
      END;
    END;

    LOCAL PROCEDURE AdjustSalesTax@10();
    BEGIN
      TaxJurisdiction.TESTFIELD("Tax Account (Sales)");
      AdjustVATAccount(
        TaxJurisdiction."Tax Account (Sales)",
        VATEntry2.Amount,VATEntry2."Additional-Currency Amount",
        VATEntryTotalBase.Amount,VATEntryTotalBase."Additional-Currency Amount");
      IF (VATEntry2."Remaining Unrealized Amount" <> 0) OR
         (VATEntry2."Add.-Curr. Rem. Unreal. Amount" <> 0)
      THEN BEGIN
        TaxJurisdiction.TESTFIELD("Unrealized VAT Type");
        TaxJurisdiction.TESTFIELD("Unreal. Tax Acc. (Sales)");
        AdjustVATAccount(
          TaxJurisdiction."Unreal. Tax Acc. (Sales)",
          VATEntry2."Remaining Unrealized Amount",
          VATEntry2."Add.-Curr. Rem. Unreal. Amount",
          VATEntryTotalBase."Remaining Unrealized Amount",
          VATEntryTotalBase."Add.-Curr. Rem. Unreal. Amount");
      END;
    END;

    LOCAL PROCEDURE Accumulate@9(VAR TotalAmount@1000 : Decimal;AmountToAdd@1001 : Decimal);
    BEGIN
      TotalAmount := TotalAmount + AmountToAdd;
    END;

    LOCAL PROCEDURE PostGLAccAdjmt@23(GLAccNo@1000 : Code[20];ExchRateAdjmt@1001 : Integer;Amount@1002 : Decimal;BalanceAtDate@1003 : Decimal;AddCurrBalanceAtDate@1004 : Decimal);
    BEGIN
      GenJnlLine.INIT;
      CASE ExchRateAdjmt OF
        "G/L Account"."Exchange Rate Adjustment"::"Adjust Amount":
          BEGIN
            GenJnlLine."Additional-Currency Posting" := GenJnlLine."Additional-Currency Posting"::"Amount Only";
            GenJnlLine."Currency Code" := '';
            GenJnlLine.Amount := Amount;
            GenJnlLine."Amount (LCY)" := GenJnlLine.Amount;
            GLAmtTotal := GLAmtTotal + GenJnlLine.Amount;
            GLAddCurrBalAtDateTotal := GLAddCurrBalAtDateTotal + AddCurrBalanceAtDate;
            GLBalAtDateBase := GLBalAtDateBase + BalanceAtDate;
          END;
        "G/L Account"."Exchange Rate Adjustment"::"Adjust Additional-Currency Amount":
          BEGIN
            GenJnlLine."Additional-Currency Posting" := GenJnlLine."Additional-Currency Posting"::"Additional-Currency Amount Only";
            GenJnlLine."Currency Code" := GLSetup."Additional Reporting Currency";
            GenJnlLine.Amount := Amount;
            GenJnlLine."Amount (LCY)" := 0;
            GLAddCurrAmtTotal := GLAddCurrAmtTotal + GenJnlLine.Amount;
            GLBalAtDateTotal := GLBalAtDateTotal + BalanceAtDate;
            GLAddCurrBalAtDateBase := GLAddCurrBalAtDateBase + AddCurrBalanceAtDate;
          END;
      END;
      IF GenJnlLine.Amount <> 0 THEN BEGIN
        GenJnlLine."Document No." := PostingDocNo;
        GenJnlLine."Account Type" := GenJnlLine."Account Type"::"G/L Account";
        GenJnlLine."Account No." := GLAccNo;
        GenJnlLine."Posting Date" := PostingDate;
        CASE GenJnlLine."Additional-Currency Posting" OF
          GenJnlLine."Additional-Currency Posting"::"Amount Only":
            BEGIN
              GenJnlLine.Description :=
                STRSUBSTNO(
                  PostingDescription,
                  GLSetup."Additional Reporting Currency",
                  AddCurrBalanceAtDate);
            END;
          GenJnlLine."Additional-Currency Posting"::"Additional-Currency Amount Only":
            BEGIN
              GenJnlLine.Description :=
                STRSUBSTNO(
                  PostingDescription,
                  '',
                  BalanceAtDate);
            END;
        END;
        GenJnlLine."System-Created Entry" := TRUE;
        GenJnlLine."Source Code" := SourceCodeSetup."Exchange Rate Adjmt.";
        GetJnlLineDefDim(GenJnlLine,TempJnlLineDim);
        PostGenJnlLine(GenJnlLine,TempJnlLineDim);
      END;
    END;

    LOCAL PROCEDURE CheckExchRateAdjustment@6(AccNo@1000 : Code[10];SetupTableName@1001 : Text[100];SetupFieldName@1002 : Text[100]);
    VAR
      GLAcc@1003 : Record 15;
      GLSetup@1004 : Record 98;
    BEGIN
      IF AccNo = '' THEN
        EXIT;
      GLAcc.GET(AccNo);
      IF GLAcc."Exchange Rate Adjustment" <> GLAcc."Exchange Rate Adjustment"::"No Adjustment" THEN BEGIN
        GLAcc."Exchange Rate Adjustment" := GLAcc."Exchange Rate Adjustment"::"No Adjustment";
        ERROR(
          Text017,
          GLAcc.FIELDCAPTION("Exchange Rate Adjustment"),GLAcc.TABLECAPTION,
          GLAcc."No.",GLAcc."Exchange Rate Adjustment",
          SetupTableName,GLSetup.FIELDCAPTION("VAT Exchange Rate Adjustment"),
          GLSetup.TABLECAPTION,SetupFieldName);
      END;
    END;

    LOCAL PROCEDURE HandleCustDebitCredit@17(Amount@1000 : Decimal;"Amount (LCY)"@1001 : Decimal;Correction@1002 : Boolean;AdjAmount@1003 : Decimal);
    BEGIN
      IF ((Amount > 0) OR ("Amount (LCY)" > 0)) AND (NOT Correction) OR
         ((Amount < 0) OR ("Amount (LCY)" < 0)) AND Correction
      THEN BEGIN
        TempDtldCustLedgEntry."Debit Amount (LCY)" := AdjAmount;
        TempDtldCustLedgEntry."Credit Amount (LCY)" := 0;
      END ELSE BEGIN
        TempDtldCustLedgEntry."Debit Amount (LCY)" := 0;
        TempDtldCustLedgEntry."Credit Amount (LCY)" := -AdjAmount;
      END;
    END;

    LOCAL PROCEDURE HandleVendDebitCredit@14(Amount@1000 : Decimal;"Amount (LCY)"@1001 : Decimal;Correction@1002 : Boolean;AdjAmount@1003 : Decimal);
    BEGIN
      IF ((Amount > 0) OR ("Amount (LCY)" > 0)) AND (NOT Correction) OR
         ((Amount < 0) OR ("Amount (LCY)" < 0)) AND Correction
      THEN BEGIN
        TempDtldVendLedgEntry."Debit Amount (LCY)" := AdjAmount;
        TempDtldVendLedgEntry."Credit Amount (LCY)" := 0;
      END ELSE BEGIN
        TempDtldVendLedgEntry."Debit Amount (LCY)" := 0;
        TempDtldVendLedgEntry."Credit Amount (LCY)" := -AdjAmount;
      END;
    END;

    LOCAL PROCEDURE GetJnlLineDefDim@11(VAR GenJnlLine@1000 : Record 81;VAR TempJnlLineDim@1001 : Record 356);
    VAR
      TableID@1002 : ARRAY [10] OF Integer;
      No@1003 : ARRAY [10] OF Code[20];
    BEGIN
      WITH GenJnlLine DO BEGIN
        CASE "Account Type" OF
          "Account Type"::"G/L Account": TableID[1] := DATABASE::"G/L Account";
          "Account Type"::"Bank Account": TableID[1] := DATABASE::"Bank Account";
        END;
        No[1] := "Account No.";
        DimMgt.GetDefaultDim(TableID,No,"Source Code","Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
      END;
      DimMgt.GetJnlLineDefaultDim(TempJnlLineDim);
    END;

    LOCAL PROCEDURE CopyJnlLineDimToDimBuf@18(VAR JnlLineDim@1000 : Record 356;VAR DimBuf@1001 : Record 360);
    BEGIN
      IF JnlLineDim.FIND('-') THEN
        REPEAT
          DimBuf."Table ID" := JnlLineDim."Table ID";
          DimBuf."Entry No." := 0;
          DimBuf."Dimension Code" := JnlLineDim."Dimension Code";
          DimBuf."Dimension Value Code" := JnlLineDim."Dimension Value Code";
          DimBuf."Line No." := JnlLineDim."Journal Line No.";
          DimBuf.INSERT;
        UNTIL JnlLineDim.NEXT = 0;
    END;

    LOCAL PROCEDURE GetDimCombID@21(VAR DimBuf@1000 : Record 360) : Integer;
    VAR
      DimEntryNo@1001 : Integer;
    BEGIN
      DimEntryNo := DimBufMgt.FindDimensions(DimBuf);
      IF DimEntryNo = 0 THEN
        DimEntryNo := DimBufMgt.InsertDimensions(DimBuf);
      EXIT(DimEntryNo);
    END;

    LOCAL PROCEDURE PostGenJnlLine@8(VAR GenJnlLine@1000 : Record 81;VAR JnlLineDim@1001 : Record 356);
    BEGIN
      GenJnlLine."Shortcut Dimension 1 Code" := GetGlobalDimVal(GLSetup."Global Dimension 1 Code",JnlLineDim);
      GenJnlLine."Shortcut Dimension 2 Code" := GetGlobalDimVal(GLSetup."Global Dimension 2 Code",JnlLineDim);
      GenJnlPostLine.RunWithCheck(GenJnlLine,TempJnlLineDim);
    END;

    LOCAL PROCEDURE GetGlobalDimVal@16(GlobalDimCode@1000 : Code[20];VAR JnlLineDim@1001 : Record 356) : Code[20];
    VAR
      DimVal@1002 : Code[20];
    BEGIN
      IF GlobalDimCode = '' THEN
        DimVal := ''
      ELSE BEGIN
        JnlLineDim.SETRANGE("Dimension Code",GlobalDimCode);
        IF JnlLineDim.FIND('-') THEN
          DimVal := JnlLineDim."Dimension Value Code"
        ELSE
          DimVal := '';
        JnlLineDim.SETRANGE("Dimension Code");
      END;
      EXIT(DimVal);
    END;

    PROCEDURE CheckPostingDate@19();
    BEGIN
      IF PostingDate < StartDate THEN
        ERROR(Text018);
      IF PostingDate > EndDateReq THEN
        ERROR(Text018);
    END;

    PROCEDURE AdjustCustomerLedgerEntry@20(CusLedgerEntry@1001 : Record 21;PostingDate2@1000 : Date);
    VAR
      LedgerEntryDim@1005 : Record 355;
      DimEntryNo@1004 : Integer;
      OldAdjAmount@1003 : Decimal;
      Adjust@1002 : Boolean;
    BEGIN
      WITH CusLedgerEntry DO BEGIN
        SETRANGE("Date Filter",0D,PostingDate2);
        Currency2.GET("Currency Code");
        GainsAmount := 0;
        LossesAmount := 0;
        OldAdjAmount := 0;
        Adjust := FALSE;

        TempJnlLineDim.RESET;
        TempJnlLineDim.DELETEALL;
        TempDimBuf.RESET;
        TempDimBuf.DELETEALL;
        LedgerEntryDim.SETRANGE("Table ID",DATABASE::"Cust. Ledger Entry");
        LedgerEntryDim.SETRANGE("Entry No.","Entry No.");
        DimMgt.CopyLedgEntryDimToJnlLineDim(LedgerEntryDim,TempJnlLineDim);
        CopyJnlLineDimToDimBuf(TempJnlLineDim,TempDimBuf);
        DimEntryNo := GetDimCombID(TempDimBuf);

        CALCFIELDS(
          Amount,"Amount (LCY)","Remaining Amount","Remaining Amt. (LCY)","Original Amt. (LCY)",
          "Debit Amount","Credit Amount","Debit Amount (LCY)","Credit Amount (LCY)");

        // Calculate Old Unrealized GainLoss
        DtldCustLedgEntry.RESET;
        DtldCustLedgEntry.SETCURRENTKEY("Cust. Ledger Entry No.","Entry Type");
        DtldCustLedgEntry.SETRANGE("Cust. Ledger Entry No.","Entry No.");
        DtldCustLedgEntry.SETRANGE(
          "Entry Type",
          DtldCustLedgEntry."Entry Type"::"Unrealized Loss",
          DtldCustLedgEntry."Entry Type"::"Unrealized Gain");
        DtldCustLedgEntry.CALCSUMS("Amount (LCY)");

        TempDtldCustLedgEntry.RESET;
        TempDtldCustLedgEntry.SETCURRENTKEY("Cust. Ledger Entry No.","Entry Type");
        TempDtldCustLedgEntry.SETRANGE("Cust. Ledger Entry No.","Entry No.");
        TempDtldCustLedgEntry.SETRANGE(
          "Entry Type",
          TempDtldCustLedgEntry."Entry Type"::"Unrealized Loss",
          TempDtldCustLedgEntry."Entry Type"::"Unrealized Gain");
        TempDtldCustLedgEntry.CALCSUMS("Amount (LCY)","Debit Amount (LCY)","Credit Amount (LCY)");
        OldAdjAmount := DtldCustLedgEntry."Amount (LCY)" + TempDtldCustLedgEntry."Amount (LCY)";
        "Remaining Amt. (LCY)" := "Remaining Amt. (LCY)" + TempDtldCustLedgEntry."Amount (LCY)";
        "Debit Amount (LCY)" := "Debit Amount (LCY)" + TempDtldCustLedgEntry."Amount (LCY)";
        "Credit Amount (LCY)" := "Credit Amount (LCY)" + TempDtldCustLedgEntry."Amount (LCY)";
        TempDtldCustLedgEntry.RESET;

        // Modify Currency factor on Customer Ledger Entry
        IF "Adjusted Currency Factor" <> Currency2."Currency Factor" THEN BEGIN
          "Adjusted Currency Factor" := Currency2."Currency Factor";
          MODIFY;
        END;

        // Calculate New Unrealized GainLoss
        AdjAmount :=
          ROUND(
            CurrExchRate.ExchangeAmtFCYToLCYAdjmt(
              PostingDate2,Currency2.Code,"Remaining Amount",Currency2."Currency Factor")) -
          "Remaining Amt. (LCY)";

        IF AdjAmount <> 0 THEN BEGIN
          TempDtldCustLedgEntry.INIT;
          TempDtldCustLedgEntry."Entry No." := NewEntryNo;
          TempDtldCustLedgEntry."Cust. Ledger Entry No." := "Entry No.";
          TempDtldCustLedgEntry."Posting Date" := PostingDate2;
          TempDtldCustLedgEntry."Document No." := PostingDocNo;
          TempDtldCustLedgEntry.Amount := 0;
          TempDtldCustLedgEntry."Customer No." := "Customer No.";
          TempDtldCustLedgEntry."Currency Code" := "Currency Code";
          TempDtldCustLedgEntry."User ID" := USERID;
          TempDtldCustLedgEntry."Source Code" := SourceCodeSetup."Exchange Rate Adjmt.";
          TempDtldCustLedgEntry."Journal Batch Name" := "Journal Batch Name";
          TempDtldCustLedgEntry."Reason Code" := "Reason Code";
          TempDtldCustLedgEntry."Initial Entry Due Date" := "Due Date";
          TempDtldCustLedgEntry."Initial Entry Global Dim. 1" := "Global Dimension 1 Code";
          TempDtldCustLedgEntry."Initial Entry Global Dim. 2" := "Global Dimension 2 Code";
          TempDtldCustLedgEntry."Initial Document Type" := "Document Type";

          Correction :=
            ("Debit Amount" < 0) OR
            ("Credit Amount" < 0) OR
            ("Debit Amount (LCY)" < 0) OR
            ("Credit Amount (LCY)" < 0);

          IF OldAdjAmount > 0 THEN BEGIN
            CASE TRUE OF
              (AdjAmount > 0):
                BEGIN
                  TempDtldCustLedgEntry."Amount (LCY)" := AdjAmount;
                  TempDtldCustLedgEntry."Entry Type" := TempDtldCustLedgEntry."Entry Type"::"Unrealized Gain";
                  HandleCustDebitCredit(
                    Amount,"Amount (LCY)",Correction,TempDtldCustLedgEntry."Amount (LCY)");
                  TempDtldCustLedgEntry.INSERT;
                  NewEntryNo := NewEntryNo + 1;
                  GainsAmount := AdjAmount;
                  Adjust := TRUE;
                END;
              (AdjAmount < 0):
                BEGIN
                  IF -AdjAmount <= OldAdjAmount THEN BEGIN
                    TempDtldCustLedgEntry."Amount (LCY)" := AdjAmount;
                    TempDtldCustLedgEntry."Entry Type" := TempDtldCustLedgEntry."Entry Type"::"Unrealized Gain";
                    HandleCustDebitCredit(
                      Amount,"Amount (LCY)",Correction,TempDtldCustLedgEntry."Amount (LCY)");
                    TempDtldCustLedgEntry.INSERT;
                    NewEntryNo := NewEntryNo + 1;
                    GainsAmount := AdjAmount;
                    Adjust := TRUE;
                  END ELSE BEGIN
                    AdjAmount := AdjAmount + OldAdjAmount;
                    TempDtldCustLedgEntry."Amount (LCY)" := -OldAdjAmount;
                    TempDtldCustLedgEntry."Entry Type" := TempDtldCustLedgEntry."Entry Type"::"Unrealized Gain";
                    HandleCustDebitCredit(
                      Amount,"Amount (LCY)",Correction,TempDtldCustLedgEntry."Amount (LCY)");
                    TempDtldCustLedgEntry.INSERT;
                    NewEntryNo := NewEntryNo + 1;
                    AdjExchRateBufferUpdate(
                      "Currency Code",Customer."Customer Posting Group",
                      0,0,-OldAdjAmount,0,-OldAdjAmount,DimEntryNo,PostingDate2,Customer."IC Partner Code",
                      GetCustPostingGrAcc(CusLedgerEntry));
                    Adjust := FALSE;
                  END;
                END;
            END;
          END;
          IF OldAdjAmount < 0 THEN BEGIN
            CASE TRUE OF
              (AdjAmount < 0):
                BEGIN
                  TempDtldCustLedgEntry."Amount (LCY)" := AdjAmount;
                  TempDtldCustLedgEntry."Entry Type" := TempDtldCustLedgEntry."Entry Type"::"Unrealized Loss";
                  HandleCustDebitCredit(
                    Amount,"Amount (LCY)",Correction,TempDtldCustLedgEntry."Amount (LCY)");
                  TempDtldCustLedgEntry.INSERT;
                  NewEntryNo := NewEntryNo + 1;
                  LossesAmount := AdjAmount;
                  Adjust := TRUE;
                END;
              (AdjAmount > 0):
                BEGIN
                  IF AdjAmount <= -OldAdjAmount THEN BEGIN
                    TempDtldCustLedgEntry."Amount (LCY)" := AdjAmount;
                    TempDtldCustLedgEntry."Entry Type" := TempDtldCustLedgEntry."Entry Type"::"Unrealized Loss";
                    HandleCustDebitCredit(
                      Amount,"Amount (LCY)",Correction,TempDtldCustLedgEntry."Amount (LCY)");
                    TempDtldCustLedgEntry.INSERT;
                    NewEntryNo := NewEntryNo + 1;
                    LossesAmount := AdjAmount;
                    Adjust := TRUE;
                  END ELSE BEGIN
                    AdjAmount := OldAdjAmount + AdjAmount;
                    TempDtldCustLedgEntry."Amount (LCY)" := -OldAdjAmount;
                    TempDtldCustLedgEntry."Entry Type" := TempDtldCustLedgEntry."Entry Type"::"Unrealized Loss";
                    HandleCustDebitCredit(
                      Amount,"Amount (LCY)",Correction,TempDtldCustLedgEntry."Amount (LCY)");
                    TempDtldCustLedgEntry.INSERT;
                    NewEntryNo := NewEntryNo + 1;
                    AdjExchRateBufferUpdate(
                      "Currency Code",Customer."Customer Posting Group",
                      0,0,-OldAdjAmount,-OldAdjAmount,0,DimEntryNo,PostingDate2,Customer."IC Partner Code",
                      GetCustPostingGrAcc(CusLedgerEntry));
                    Adjust := FALSE;
                  END;
                END;
            END;
          END;

          CusLedgerEntry.CALCFIELDS(
          Amount,"Amount (LCY)","Remaining Amount","Remaining Amt. (LCY)","Original Amt. (LCY)",
          "Debit Amount","Credit Amount","Debit Amount (LCY)","Credit Amount (LCY)");
          IF (("Document Situation" <> 0) AND (ApplicationManagement.AccessToCartera)) THEN
            CASE "Document Situation" OF
              "Document Situation"::Cartera:
                IF Doc.GET(Doc.Type::Receivable,"Entry No.") THEN BEGIN
                  Doc."Remaining Amt. (LCY)" := "Remaining Amt. (LCY)";
                  Doc."Adjusted Amount" := AdjAmount;
                  Doc.Adjusted := TRUE;
                  Doc.MODIFY;
                END;
              "Document Situation"::"Posted BG/PO":
                IF PostedDoc.GET(PostedDoc.Type::Receivable,"Entry No.") THEN BEGIN
                  PostedDoc."Remaining Amt. (LCY)" := "Remaining Amt. (LCY)";
                  PostedDoc."Adjusted Amount" := AdjAmount;
                  PostedDoc.ReAdjusted := TRUE;
                  PostedDoc.MODIFY;
                END;
              "Document Situation"::"Closed BG/PO","Document Situation"::"Closed Documents":
                IF ClosedDoc.GET(ClosedDoc.Type::Receivable,"Entry No.") THEN BEGIN
                  ClosedDoc."Remaining Amt. (LCY)" := "Remaining Amt. (LCY)";
                  ClosedDoc.MODIFY;
                END;
            END;

          IF NOT Adjust THEN BEGIN
            TempDtldCustLedgEntry."Amount (LCY)" := AdjAmount;
            HandleCustDebitCredit(Amount,"Amount (LCY)",Correction,TempDtldCustLedgEntry."Amount (LCY)");
            TempDtldCustLedgEntry."Entry No." := NewEntryNo;
            IF AdjAmount < 0 THEN BEGIN
              TempDtldCustLedgEntry."Entry Type" := TempDtldCustLedgEntry."Entry Type"::"Unrealized Loss";
              GainsAmount := 0;
              LossesAmount := AdjAmount;
            END ELSE BEGIN
              IF AdjAmount > 0 THEN BEGIN
                TempDtldCustLedgEntry."Entry Type" := TempDtldCustLedgEntry."Entry Type"::"Unrealized Gain";
                GainsAmount := AdjAmount;
                LossesAmount := 0;
              END;
            END;
            TempDtldCustLedgEntry.INSERT;
            NewEntryNo := NewEntryNo + 1;
          END;

        TotalAdjAmount := TotalAdjAmount + AdjAmount;
        Window.UPDATE(4,TotalAdjAmount);
        AdjExchRateBufferUpdate(
          "Currency Code",Customer."Customer Posting Group",
          "Remaining Amount","Remaining Amt. (LCY)",TempDtldCustLedgEntry."Amount (LCY)",
          GainsAmount,LossesAmount,DimEntryNo,PostingDate2,Customer."IC Partner Code",
          GetCustPostingGrAcc(CusLedgerEntry));

        END;
      END;
    END;

    PROCEDURE AdjustVendorLedgerEntry@24(VendLedgerEntry@1001 : Record 25;PostingDate2@1000 : Date);
    VAR
      LedgerEntryDim@1006 : Record 355;
      DimEntryNo@1005 : Integer;
      OldAdjAmount@1004 : Decimal;
      Adjust@1003 : Boolean;
    BEGIN
      WITH VendLedgerEntry DO BEGIN
        SETRANGE("Date Filter",0D,PostingDate2);
        Currency2.GET("Currency Code");
        GainsAmount := 0;
        LossesAmount := 0;
        OldAdjAmount := 0;
        Adjust := FALSE;

        TempJnlLineDim.RESET;
        TempJnlLineDim.DELETEALL;
        TempDimBuf.RESET;
        TempDimBuf.DELETEALL;
        LedgerEntryDim.SETRANGE("Table ID",DATABASE::"Vendor Ledger Entry");
        LedgerEntryDim.SETRANGE("Entry No.","Entry No.");
        DimMgt.CopyLedgEntryDimToJnlLineDim(LedgerEntryDim,TempJnlLineDim);
        CopyJnlLineDimToDimBuf(TempJnlLineDim,TempDimBuf);
        DimEntryNo := GetDimCombID(TempDimBuf);

        CALCFIELDS(
          Amount,"Amount (LCY)","Remaining Amount","Remaining Amt. (LCY)","Original Amt. (LCY)",
          "Debit Amount","Credit Amount","Debit Amount (LCY)","Credit Amount (LCY)");

        // Calculate Old Unrealized GainLoss
        DtldVendLedgEntry.RESET;
        DtldVendLedgEntry.SETCURRENTKEY("Vendor Ledger Entry No.","Entry Type");
        DtldVendLedgEntry.SETRANGE("Vendor Ledger Entry No.","Entry No.");
        DtldVendLedgEntry.SETRANGE(
          "Entry Type",
          DtldVendLedgEntry."Entry Type"::"Unrealized Loss",
          DtldVendLedgEntry."Entry Type"::"Unrealized Gain");
        DtldVendLedgEntry.CALCSUMS("Amount (LCY)");
        OldAdjAmount := DtldVendLedgEntry."Amount (LCY)";

        TempDtldVendLedgEntry.RESET;
        TempDtldVendLedgEntry.SETCURRENTKEY("Vendor Ledger Entry No.","Entry Type");
        TempDtldVendLedgEntry.SETRANGE("Vendor Ledger Entry No.","Entry No.");
        TempDtldVendLedgEntry.SETRANGE(
          "Entry Type",
          TempDtldVendLedgEntry."Entry Type"::"Unrealized Loss",
          TempDtldVendLedgEntry."Entry Type"::"Unrealized Gain");
        TempDtldVendLedgEntry.CALCSUMS("Amount (LCY)","Debit Amount (LCY)","Credit Amount (LCY)");
        OldAdjAmount := DtldVendLedgEntry."Amount (LCY)" + TempDtldVendLedgEntry."Amount (LCY)";
        "Remaining Amt. (LCY)" := "Remaining Amt. (LCY)" + TempDtldVendLedgEntry."Amount (LCY)";
        "Debit Amount (LCY)" := "Debit Amount (LCY)" + TempDtldVendLedgEntry."Amount (LCY)";
        "Credit Amount (LCY)" := "Credit Amount (LCY)" + TempDtldVendLedgEntry."Amount (LCY)";
        TempDtldVendLedgEntry.RESET;

        // Modify Currency factor on Vendor Ledger Entry
        IF "Adjusted Currency Factor" <> Currency2."Currency Factor" THEN BEGIN
          "Adjusted Currency Factor" := Currency2."Currency Factor";
          MODIFY;
        END;

        // Calculate New Unrealized GainLoss
        AdjAmount :=
          ROUND(
            CurrExchRate.ExchangeAmtFCYToLCYAdjmt(
              PostingDate2,Currency2.Code,"Remaining Amount",Currency2."Currency Factor")) -
          "Remaining Amt. (LCY)";

        IF AdjAmount <> 0 THEN BEGIN
          TempDtldVendLedgEntry.INIT;
          TempDtldVendLedgEntry."Entry No." := NewEntryNo;
          TempDtldVendLedgEntry."Vendor Ledger Entry No." := "Entry No.";
          TempDtldVendLedgEntry."Posting Date" := PostingDate2;
          TempDtldVendLedgEntry."Document No." := PostingDocNo;
          TempDtldVendLedgEntry.Amount := 0;
          TempDtldVendLedgEntry."Vendor No." := "Vendor No.";
          TempDtldVendLedgEntry."Currency Code" := "Currency Code";
          TempDtldVendLedgEntry."User ID" := USERID;
          TempDtldVendLedgEntry."Source Code" := SourceCodeSetup."Exchange Rate Adjmt.";
          TempDtldVendLedgEntry."Journal Batch Name" := "Journal Batch Name";
          TempDtldVendLedgEntry."Reason Code" := "Reason Code";
          TempDtldVendLedgEntry."Initial Entry Due Date" := "Due Date";
          TempDtldVendLedgEntry."Initial Entry Global Dim. 1" := "Global Dimension 1 Code";
          TempDtldVendLedgEntry."Initial Entry Global Dim. 2" := "Global Dimension 2 Code";
          TempDtldVendLedgEntry."Initial Document Type" := "Document Type";

          Correction :=
            ("Debit Amount" < 0) OR
            ("Credit Amount" < 0) OR
            ("Debit Amount (LCY)" < 0) OR
            ("Credit Amount (LCY)" < 0);

          IF OldAdjAmount > 0 THEN BEGIN
            CASE TRUE OF
              (AdjAmount > 0):
                BEGIN
                  TempDtldVendLedgEntry."Amount (LCY)" := AdjAmount;
                  TempDtldVendLedgEntry."Entry Type" := TempDtldVendLedgEntry."Entry Type"::"Unrealized Gain";
                  HandleVendDebitCredit(Amount,"Amount (LCY)",Correction,TempDtldVendLedgEntry."Amount (LCY)");
                  TempDtldVendLedgEntry.INSERT;
                  NewEntryNo := NewEntryNo + 1;
                  GainsAmount := AdjAmount;
                  Adjust := TRUE;
                END;
              (AdjAmount < 0):
                BEGIN
                  IF -AdjAmount <= OldAdjAmount THEN BEGIN
                    TempDtldVendLedgEntry."Amount (LCY)" := AdjAmount;
                    TempDtldVendLedgEntry."Entry Type" := TempDtldVendLedgEntry."Entry Type"::"Unrealized Gain";
                    HandleVendDebitCredit(
                      Amount,"Amount (LCY)",Correction,TempDtldVendLedgEntry."Amount (LCY)");
                    TempDtldVendLedgEntry.INSERT;
                    NewEntryNo := NewEntryNo + 1;
                    GainsAmount := AdjAmount;
                    Adjust := TRUE;
                  END ELSE BEGIN
                    AdjAmount := AdjAmount + OldAdjAmount;
                    TempDtldVendLedgEntry."Amount (LCY)" := -OldAdjAmount;
                    TempDtldVendLedgEntry."Entry Type" := TempDtldVendLedgEntry."Entry Type"::"Unrealized Gain";
                    HandleVendDebitCredit(
                      Amount,"Amount (LCY)",Correction,TempDtldVendLedgEntry."Amount (LCY)");
                    TempDtldVendLedgEntry.INSERT;
                    NewEntryNo := NewEntryNo + 1;
                    AdjExchRateBufferUpdate(
                      "Currency Code",Vendor."Vendor Posting Group",
                      0,0,-OldAdjAmount,-OldAdjAmount,0,DimEntryNo,PostingDate2,Vendor."IC Partner Code",
                      GetVendPostingGrAcc(VendLedgerEntry));
                    Adjust := FALSE;
                  END;
                END;
            END;
          END;
          IF OldAdjAmount < 0 THEN BEGIN
            CASE TRUE OF
              (AdjAmount < 0):
                BEGIN
                  TempDtldVendLedgEntry."Amount (LCY)" := AdjAmount;
                  TempDtldVendLedgEntry."Entry Type" := TempDtldVendLedgEntry."Entry Type"::"Unrealized Loss";
                  HandleVendDebitCredit(Amount,"Amount (LCY)",Correction,TempDtldVendLedgEntry."Amount (LCY)");
                  TempDtldVendLedgEntry.INSERT;
                  NewEntryNo := NewEntryNo + 1;
                  LossesAmount := AdjAmount;
                  Adjust := TRUE;
                END;
              (AdjAmount > 0):
                BEGIN
                  IF AdjAmount <= -OldAdjAmount THEN BEGIN
                    TempDtldVendLedgEntry."Amount (LCY)" := AdjAmount;
                    TempDtldVendLedgEntry."Entry Type" := TempDtldVendLedgEntry."Entry Type"::"Unrealized Loss";
                    HandleVendDebitCredit(
                      Amount,"Amount (LCY)",Correction,TempDtldVendLedgEntry."Amount (LCY)");
                    TempDtldVendLedgEntry.INSERT;
                    NewEntryNo := NewEntryNo + 1;
                    LossesAmount := AdjAmount;
                    Adjust := TRUE;
                  END ELSE BEGIN
                    AdjAmount := OldAdjAmount + AdjAmount;
                    TempDtldVendLedgEntry."Amount (LCY)" := -OldAdjAmount;
                    TempDtldVendLedgEntry."Entry Type" := TempDtldVendLedgEntry."Entry Type"::"Unrealized Loss";
                    HandleVendDebitCredit(
                      Amount,"Amount (LCY)",Correction,TempDtldVendLedgEntry."Amount (LCY)");
                    TempDtldVendLedgEntry.INSERT;
                    NewEntryNo := NewEntryNo + 1;
                    AdjExchRateBufferUpdate(
                      "Currency Code",Vendor."Vendor Posting Group",
                      0,0,-OldAdjAmount,0,-OldAdjAmount,DimEntryNo,PostingDate2,Vendor."IC Partner Code",
                      GetVendPostingGrAcc(VendLedgerEntry));
                    Adjust := FALSE;
                  END;
                END;
            END;
          END;

          VendLedgerEntry.CALCFIELDS(
          Amount,"Amount (LCY)","Remaining Amount","Remaining Amt. (LCY)","Original Amt. (LCY)",
          "Debit Amount","Credit Amount","Debit Amount (LCY)","Credit Amount (LCY)");
          IF (("Document Situation" <> 0) AND (ApplicationManagement.AccessToCartera)) THEN
            CASE "Document Situation" OF
             "Document Situation"::Cartera:
               IF Doc.GET(Doc.Type::Payable,"Entry No.") THEN BEGIN
                 Doc."Remaining Amt. (LCY)" := "Remaining Amt. (LCY)";
                 Doc."Adjusted Amount" := AdjAmount;
                 Doc.Adjusted := TRUE;
                 Doc.MODIFY;
               END;
             "Document Situation"::"Posted BG/PO":
               IF PostedDoc.GET(PostedDoc.Type::Payable,"Entry No.") THEN BEGIN
                 PostedDoc."Remaining Amt. (LCY)" := "Remaining Amt. (LCY)";
                 PostedDoc."Adjusted Amount" := AdjAmount;
                 PostedDoc.ReAdjusted := TRUE;
                 PostedDoc.MODIFY;
               END;
             "Document Situation"::"Closed BG/PO","Document Situation"::"Closed Documents":
               IF ClosedDoc.GET(ClosedDoc.Type::Payable,"Entry No.") THEN BEGIN
                 ClosedDoc."Remaining Amt. (LCY)" := "Remaining Amt. (LCY)";
                 ClosedDoc.MODIFY;
               END;
          END;

          IF NOT Adjust THEN BEGIN
            TempDtldVendLedgEntry."Amount (LCY)" := AdjAmount;
            HandleVendDebitCredit(Amount,"Amount (LCY)",Correction,TempDtldVendLedgEntry."Amount (LCY)");
            TempDtldVendLedgEntry."Entry No." := NewEntryNo;
            IF AdjAmount < 0 THEN BEGIN
              TempDtldVendLedgEntry."Entry Type" := TempDtldVendLedgEntry."Entry Type"::"Unrealized Loss";
              GainsAmount := 0;
              LossesAmount := AdjAmount;
            END ELSE BEGIN
              IF AdjAmount > 0 THEN BEGIN
                TempDtldVendLedgEntry."Entry Type" := TempDtldVendLedgEntry."Entry Type"::"Unrealized Gain";
                GainsAmount := AdjAmount;
                LossesAmount := 0;
              END;
            END;
            TempDtldVendLedgEntry.INSERT;
            NewEntryNo := NewEntryNo + 1;
          END;

          TotalAdjAmount := TotalAdjAmount + AdjAmount;
          Window.UPDATE(4,TotalAdjAmount);
          AdjExchRateBufferUpdate(
            "Currency Code",Vendor."Vendor Posting Group",
            "Remaining Amount","Remaining Amt. (LCY)",
            TempDtldVendLedgEntry."Amount (LCY)",GainsAmount,LossesAmount,DimEntryNo,PostingDate2,Vendor."IC Partner Code",
            GetVendPostingGrAcc(VendLedgerEntry));
        END;

      END;
    END;

    PROCEDURE GetCustPostingGrAcc@1100001(CustLedgEntry@1000 : Record 21) : Code[20];
    VAR
      CustPostingGr@1001 : Record 92;
      PostedDoc@1002 : Record 7000003;
    BEGIN

      CLEAR(PostedDoc);
      WITH CustLedgEntry DO BEGIN
        TESTFIELD("Customer Posting Group");
        CustPostingGr.GET("Customer Posting Group");
        CASE TRUE OF
          ("Document Type" = "Document Type"::Invoice) AND
          ("Document Status" = "Document Status"::" ") AND
          ("Document Situation" = "Document Situation"::" "):
            BEGIN
              CustPostingGr.TESTFIELD("Receivables Account");
              EXIT(CustPostingGr."Receivables Account");
            END;
          ("Document Type" = "Document Type"::Invoice) AND
          ("Document Status" = "Document Status"::Rejected) AND
          ("Document Situation" IN ["Document Situation"::"Posted BG/PO","Document Situation"::"Closed BG/PO"]):
            BEGIN
              CustPostingGr.TESTFIELD("Rejected Factoring Acc.");
              EXIT(CustPostingGr."Rejected Factoring Acc.");
            END;
          ("Document Type" = "Document Type"::Invoice) AND
          ("Document Status" = "Document Status"::Open) AND
          ("Document Situation" = "Document Situation"::"Posted BG/PO"):
            BEGIN
              PostedDoc.GET(PostedDoc.Type::Receivable,CustLedgEntry."Entry No.");
              IF PostedDoc."Dealing Type" = PostedDoc."Dealing Type"::Discount THEN BEGIN
                CustPostingGr.TESTFIELD("Factoring for Discount Acc.");
                EXIT(CustPostingGr."Factoring for Discount Acc.");
              END ELSE BEGIN
                CustPostingGr.TESTFIELD("Factoring for Collection Acc.");
                EXIT(CustPostingGr."Factoring for Collection Acc.");
              END;
            END;
          ("Document Type" = "Document Type"::Bill) AND
          ("Document Status" = "Document Status"::Open) AND
          ("Document Situation" IN ["Document Situation"::Cartera,"Document Situation"::"BG/PO"]):
            BEGIN
              CustPostingGr.TESTFIELD("Bills Account");
              EXIT(CustPostingGr."Bills Account");
            END;
          ("Document Type" = "Document Type"::Bill) AND
          ("Document Status" = "Document Status"::Rejected) AND
          ("Document Situation" IN ["Document Situation"::"Posted BG/PO","Document Situation"::"Closed BG/PO",
                                    "Document Situation"::"Closed Documents"]):
            BEGIN
              CustPostingGr.TESTFIELD("Rejected Bills Acc.");
              EXIT(CustPostingGr."Rejected Bills Acc.");
            END;
          ("Document Type" = "Document Type"::Bill) AND
          ("Document Status" = "Document Status"::Open) AND
          ("Document Situation" = "Document Situation"::"Posted BG/PO"):
            BEGIN
              PostedDoc.GET(PostedDoc.Type::Receivable,CustLedgEntry."Entry No.");
              IF PostedDoc."Dealing Type" = PostedDoc."Dealing Type"::Discount THEN BEGIN
                CustPostingGr.TESTFIELD("Discted. Bills Acc.");
                EXIT(CustPostingGr."Discted. Bills Acc.");
              END ELSE BEGIN
                CustPostingGr.TESTFIELD("Bills on Collection Acc.");
                EXIT(CustPostingGr."Bills on Collection Acc.");
              END;
            END;
          ELSE
            BEGIN
              CustPostingGr.TESTFIELD("Receivables Account");
              EXIT(CustPostingGr."Receivables Account");
            END;
        END;
      END;
    END;

    PROCEDURE GetVendPostingGrAcc@1100000(VendLedgEntry@1000 : Record 25) : Code[20];
    VAR
      VendPostingGr@1001 : Record 93;
    BEGIN
      CLEAR(PostedDoc);
      WITH VendLedgEntry DO BEGIN
        TESTFIELD("Vendor Posting Group");
        VendPostingGr.GET("Vendor Posting Group");
        CASE TRUE OF
          ("Document Type" = "Document Type"::Invoice) AND
          ("Document Status" = "Document Status"::" ") AND
          ("Document Situation" = "Document Situation"::" "):
            BEGIN

              VendPostingGr.TESTFIELD("Payables Account");
              EXIT(VendPostingGr."Payables Account");
            END;
          ("Document Type" = "Document Type"::Invoice) AND
          ("Document Status" = "Document Status"::Open) AND
          ("Document Situation" = "Document Situation"::"Posted BG/PO"):
            BEGIN

              VendPostingGr.TESTFIELD("Invoices in  Pmt. Ord. Acc.");
              EXIT(VendPostingGr."Invoices in  Pmt. Ord. Acc.");
            END;
          ("Document Type" = "Document Type"::Bill) AND
          ("Document Status" = "Document Status"::Open) AND
          ("Document Situation" IN ["Document Situation"::Cartera,"Document Situation"::"BG/PO"]):
            BEGIN

              VendPostingGr.TESTFIELD("Bills Account");
              EXIT(VendPostingGr."Bills Account");
            END;
          ("Document Type" = "Document Type"::Bill) AND
          ("Document Status" = "Document Status"::Open) AND
          ("Document Situation" = "Document Situation"::"Posted BG/PO"):
            BEGIN

              VendPostingGr.TESTFIELD("Bills in Payment Order Acc.");
              EXIT(VendPostingGr."Bills in Payment Order Acc.");
            END;
          ELSE
            BEGIN

              VendPostingGr.TESTFIELD("Payables Account");
              EXIT(VendPostingGr."Payables Account");
            END;
         END;
      END;
    END;

    BEGIN
    {
      $001 AJS 10.12.2007, Error inconsistencia

         En la rutina HandlePostAdjmt hay que quitar que agrupe por cuenta.

      $002 AJS 09052014 Corrijo un error para evitar un error en la creacion del historico de ajustes en bancos
                        Nueva variable AdjAmountBankGroup para acumular los ajustes por grupo contable banco
    }
    END.
  }
  RDLDATA
  {
  }
}
