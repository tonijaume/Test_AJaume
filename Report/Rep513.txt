OBJECT Report 513 Move IC Trans. to Partner Comp
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Move IC Trans. to Partner Comp;
               ESP=Mover trans. IC a comp. asoc.];
    ProcessingOnly=Yes;
  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table414;
        DataItemTableView=SORTING(Transaction No.,IC Partner Code,Transaction Source,Document Type)
                          ORDER(Ascending);
        OnPreDataItem=BEGIN
                        CompanyInfo.GET;
                        CompanyInfo.TESTFIELD("IC Partner Code");
                        GLSetup.GET;
                        GLSetup.TESTFIELD("LCY Code");
                      END;

        OnAfterGetRecord=BEGIN
                           IF CurrentPartner.Code <> "IC Outbox Transaction"."IC Partner Code" THEN
                             CurrentPartner.GET("IC Outbox Transaction"."IC Partner Code");

                           CASE "IC Outbox Transaction"."Line Action" OF
                             "IC Outbox Transaction"."Line Action"::"Send to IC Partner":
                               ICInboxOutboxMgt.OutboxTransToInbox(
                                 "IC Outbox Transaction",TempICInboxTransaction,CompanyInfo."IC Partner Code");
                             "IC Outbox Transaction"."Line Action"::"Return to Inbox":
                                RecreateInboxTrans("IC Outbox Transaction");
                           END;
                         END;

        OnPostDataItem=BEGIN
                         TransferToPartner;
                       END;

      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=1;
        DataItemTable=Table415;
        DataItemTableView=SORTING(Transaction No.,IC Partner Code,Transaction Source,Line No.);
        OnAfterGetRecord=VAR
                           ICInboxJnlLine@1000 : Record 419;
                         BEGIN
                           IF "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" THEN
                             ICInboxOutboxMgt.OutboxJnlLineToInbox(TempICInboxTransaction,"IC Outbox Jnl. Line",TempICInboxJnlLine);
                         END;

        DataItemLink=Transaction No.=FIELD(Transaction No.),
                     IC Partner Code=FIELD(IC Partner Code),
                     Transaction Source=FIELD(Transaction Source);
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=2;
        DataItemTable=Table423;
        DataItemTableView=SORTING(Table ID,Transaction No.,IC Partner Code,Transaction Source,Line No.,Dimension Code)
                          ORDER(Ascending)
                          WHERE(Table ID=CONST(415));
        OnAfterGetRecord=BEGIN
                           IF "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" THEN
                             ICInboxOutboxMgt.OutboxJnlLineDimToInbox(
                               TempICInboxJnlLine,"IC Inbox/Outbox Jnl. Line Dim.",
                               TempInboxOutboxJnlLineDim,DATABASE::"IC Inbox Jnl. Line");
                         END;

        DataItemLink=IC Partner Code=FIELD(IC Partner Code),
                     Transaction No.=FIELD(Transaction No.),
                     Line No.=FIELD(Line No.);
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=1;
        DataItemTable=Table426;
        DataItemTableView=SORTING(IC Transaction No.,IC Partner Code,Transaction Source);
        OnAfterGetRecord=BEGIN
                           IF "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" THEN
                             ICInboxOutboxMgt.OutboxSalesHdrToInbox(TempICInboxTransaction,"IC Outbox Sales Header",TempInboxPurchHeader);
                         END;

        DataItemLink=IC Partner Code=FIELD(IC Partner Code),
                     IC Transaction No.=FIELD(Transaction No.),
                     Transaction Source=FIELD(Transaction Source);
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=2;
        DataItemTable=Table442;
        DataItemTableView=SORTING(Table ID,Transaction No.,IC Partner Code,Transaction Source,Line No.,Dimension Code)
                          ORDER(Ascending)
                          WHERE(Table ID=CONST(426),
                                Line No.=CONST(0));
        DataItemVarName=IC Document Dimension SH;
        OnAfterGetRecord=BEGIN
                           IF "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" THEN
                             ICInboxOutboxMgt.OutboxDocDimToInbox(
                               "IC Document Dimension SH",TempICDocDim,DATABASE::"IC Inbox Purchase Header",
                               TempInboxPurchHeader."IC Partner Code",TempInboxPurchHeader."Transaction Source");
                         END;

        DataItemLink=Transaction No.=FIELD(IC Transaction No.),
                     IC Partner Code=FIELD(IC Partner Code),
                     Transaction Source=FIELD(Transaction Source);
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=2;
        DataItemTable=Table427;
        DataItemTableView=SORTING(IC Transaction No.,IC Partner Code,Transaction Source);
        OnAfterGetRecord=BEGIN
                           IF "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" THEN
                             ICInboxOutboxMgt.OutboxSalesLineToInbox(TempICInboxTransaction,"IC Outbox Sales Line",TempInboxPurchLine);
                         END;

        DataItemLink=IC Transaction No.=FIELD(IC Transaction No.),
                     IC Partner Code=FIELD(IC Partner Code),
                     Transaction Source=FIELD(Transaction Source);
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=3;
        DataItemTable=Table442;
        DataItemTableView=SORTING(Table ID,Transaction No.,IC Partner Code,Transaction Source,Line No.,Dimension Code)
                          ORDER(Ascending)
                          WHERE(Table ID=CONST(427));
        DataItemVarName=IC Document Dimension SL;
        OnAfterGetRecord=BEGIN
                           IF "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" THEN
                             ICInboxOutboxMgt.OutboxDocDimToInbox(
                               "IC Document Dimension SL",TempICDocDim,DATABASE::"IC Inbox Purchase Line",
                               TempInboxPurchLine."IC Partner Code",TempInboxPurchLine."Transaction Source");
                         END;

        DataItemLink=Transaction No.=FIELD(IC Transaction No.),
                     IC Partner Code=FIELD(IC Partner Code),
                     Transaction Source=FIELD(Transaction Source),
                     Line No.=FIELD(Line No.);
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=1;
        DataItemTable=Table428;
        DataItemTableView=SORTING(IC Transaction No.,IC Partner Code,Transaction Source);
        OnAfterGetRecord=BEGIN
                           IF "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" THEN
                             ICInboxOutboxMgt.OutboxPurchHdrToInbox(TempICInboxTransaction,"IC Outbox Purchase Header",TempInboxSalesHeader);
                         END;

        DataItemLink=IC Partner Code=FIELD(IC Partner Code),
                     IC Transaction No.=FIELD(Transaction No.),
                     Transaction Source=FIELD(Transaction Source);
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=2;
        DataItemTable=Table442;
        DataItemTableView=SORTING(Table ID,Transaction No.,IC Partner Code,Transaction Source,Line No.,Dimension Code)
                          ORDER(Ascending)
                          WHERE(Table ID=CONST(428),
                                Line No.=CONST(0));
        DataItemVarName=IC Document Dimension PH;
        OnAfterGetRecord=BEGIN
                           IF "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" THEN
                             ICInboxOutboxMgt.OutboxDocDimToInbox(
                               "IC Document Dimension PH",TempICDocDim,DATABASE::"IC Inbox Sales Header",
                               TempInboxSalesHeader."IC Partner Code",TempInboxSalesHeader."Transaction Source");
                         END;

        DataItemLink=Transaction No.=FIELD(IC Transaction No.),
                     IC Partner Code=FIELD(IC Partner Code),
                     Transaction Source=FIELD(Transaction Source);
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=2;
        DataItemTable=Table429;
        DataItemTableView=SORTING(IC Transaction No.,IC Partner Code,Transaction Source,Line No.);
        OnAfterGetRecord=BEGIN
                           IF "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" THEN
                             ICInboxOutboxMgt.OutboxPurchLineToInbox(TempICInboxTransaction,"IC Outbox Purchase Line",TempInboxSalesLine);
                         END;

        DataItemLink=IC Transaction No.=FIELD(IC Transaction No.),
                     IC Partner Code=FIELD(IC Partner Code),
                     Transaction Source=FIELD(Transaction Source);
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=3;
        DataItemTable=Table442;
        DataItemTableView=SORTING(Table ID,Transaction No.,IC Partner Code,Transaction Source,Line No.,Dimension Code)
                          ORDER(Ascending)
                          WHERE(Table ID=CONST(429));
        DataItemVarName=IC Document Dimension PL;
        OnAfterGetRecord=BEGIN
                           IF "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" THEN
                             ICInboxOutboxMgt.OutboxDocDimToInbox(
                               "IC Document Dimension PL",TempICDocDim,DATABASE::"IC Inbox Sales Line",
                               TempInboxSalesLine."IC Partner Code",TempInboxSalesLine."Transaction Source");
                         END;

        DataItemLink=Transaction No.=FIELD(IC Transaction No.),
                     IC Partner Code=FIELD(IC Partner Code),
                     Transaction Source=FIELD(Transaction Source),
                     Line No.=FIELD(Line No.);
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=6270;
      Height=2750;
    }
    CONTROLS
    {
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      CurrentPartner@1000 : Record 413;
      CompanyInfo@1001 : Record 79;
      GLSetup@1010 : Record 98;
      TempICInboxTransaction@1003 : TEMPORARY Record 418;
      TempICInboxJnlLine@1006 : TEMPORARY Record 419;
      TempInboxPurchHeader@1013 : TEMPORARY Record 436;
      TempInboxPurchLine@1014 : TEMPORARY Record 437;
      TempInboxSalesHeader@1002 : TEMPORARY Record 434;
      TempInboxSalesLine@1004 : TEMPORARY Record 435;
      Text001@1007 : TextConst 'ENU=Your IC registration code %1 is not recognized by IC Partner %2;ESP=La IC asociada %2 no reconoce su c¢digo de registro IC %1';
      TempInboxOutboxJnlLineDim@1005 : TEMPORARY Record 423;
      TempICDocDim@1008 : TEMPORARY Record 442;
      ICInboxOutboxMgt@1009 : Codeunit 427;
      Text002@1011 : TextConst 'ENU=%1 %2 to IC Partner %3 already exists in the IC inbox of IC Partner %3. IC Partner %3 must complete the line action for transaction %2 in their IC inbox.;ESP=%1 %2 de socio IC %3 ya existe en la bandeja de entrada IC del socio IC %3. El socio IC %3 debe completar la acci¢n de l¡nea para la transacci¢n %2 en su bandeja de entrada IC.';

    LOCAL PROCEDURE TransferToPartner@9();
    VAR
      PartnerInboxTransaction@1004 : Record 418;
      PartnerInboxJnlLine@1007 : Record 419;
      PartnerInboxSalesHeader@1003 : Record 434;
      PartnerInboxSalesLine@1002 : Record 435;
      PartnerInboxPurchHeader@1001 : Record 436;
      PartnerInboxPurchLine@1000 : Record 437;
      PartnerInboxOutboxJnlLineDim@1010 : Record 423;
      PartnerICDocDim@1008 : Record 442;
      PartnerICPartner@1005 : Record 413;
    BEGIN
      PartnerICPartner.CHANGECOMPANY(CurrentPartner."Inbox Details");
      IF NOT PartnerICPartner.GET(CompanyInfo."IC Partner Code") THEN
        ERROR(Text001,CompanyInfo."IC Partner Code",CurrentPartner.Code);

      PartnerInboxTransaction.CHANGECOMPANY(CurrentPartner."Inbox Details");
      PartnerInboxTransaction.LOCKTABLE;
      IF TempICInboxTransaction.FIND('-') THEN
        REPEAT
          PartnerInboxTransaction := TempICInboxTransaction;
          IF NOT PartnerInboxTransaction.INSERT THEN
            ERROR(
              Text002,TempICInboxTransaction.FIELDCAPTION("Transaction No."),
              TempICInboxTransaction."Transaction No.",
              TempICInboxTransaction."IC Partner Code");
        UNTIL TempICInboxTransaction.NEXT = 0;

      PartnerInboxJnlLine.CHANGECOMPANY(CurrentPartner."Inbox Details");
      IF TempICInboxJnlLine.FIND('-') THEN
        REPEAT
          PartnerInboxJnlLine := TempICInboxJnlLine;
          IF PartnerInboxJnlLine."Currency Code" = '' THEN
            PartnerInboxJnlLine."Currency Code" := GLSetup."LCY Code";
          IF PartnerInboxJnlLine."Currency Code" = CurrentPartner."Currency Code" THEN
            PartnerInboxJnlLine."Currency Code" := '';
          PartnerInboxJnlLine.INSERT;
        UNTIL TempICInboxJnlLine.NEXT = 0;

      PartnerInboxPurchHeader.CHANGECOMPANY(CurrentPartner."Inbox Details");
      IF TempInboxPurchHeader.FIND('-') THEN
        REPEAT
          PartnerInboxPurchHeader := TempInboxPurchHeader;
          PartnerInboxPurchHeader."Buy-from Vendor No." := PartnerICPartner."Vendor No.";
          PartnerInboxPurchHeader."Pay-to Vendor No." := PartnerICPartner."Vendor No.";
          PartnerInboxPurchHeader.INSERT;
        UNTIL TempInboxPurchHeader.NEXT = 0;

      PartnerInboxPurchLine.CHANGECOMPANY(CurrentPartner."Inbox Details");
      IF TempInboxPurchLine.FIND('-') THEN
        REPEAT
          PartnerInboxPurchLine := TempInboxPurchLine;
          PartnerInboxPurchLine.INSERT;
        UNTIL TempInboxPurchLine.NEXT = 0;

      PartnerInboxSalesHeader.CHANGECOMPANY(CurrentPartner."Inbox Details");
      IF TempInboxSalesHeader.FIND('-') THEN
        REPEAT
          PartnerInboxSalesHeader := TempInboxSalesHeader;
          PartnerInboxSalesHeader."Sell-to Customer No." := PartnerICPartner."Customer No.";
          PartnerInboxSalesHeader."Bill-to Customer No." := PartnerICPartner."Customer No.";
          PartnerInboxSalesHeader.INSERT;
        UNTIL TempInboxSalesHeader.NEXT = 0;

      PartnerInboxSalesLine.CHANGECOMPANY(CurrentPartner."Inbox Details");
      IF TempInboxSalesLine.FIND('-') THEN
        REPEAT
          PartnerInboxSalesLine := TempInboxSalesLine;
          PartnerInboxSalesLine.INSERT;
        UNTIL TempInboxSalesLine.NEXT = 0;

      PartnerInboxOutboxJnlLineDim.CHANGECOMPANY(CurrentPartner."Inbox Details");
      IF TempInboxOutboxJnlLineDim.FIND('-') THEN
        REPEAT
          PartnerInboxOutboxJnlLineDim := TempInboxOutboxJnlLineDim;
          PartnerInboxOutboxJnlLineDim.INSERT;
        UNTIL TempInboxOutboxJnlLineDim.NEXT = 0;

      PartnerICDocDim.CHANGECOMPANY(CurrentPartner."Inbox Details");
      IF TempICDocDim.FIND('-') THEN
        REPEAT
          PartnerICDocDim := TempICDocDim;
          PartnerICDocDim.INSERT;
        UNTIL TempICDocDim.NEXT = 0;

      TempICInboxTransaction.DELETEALL;
      TempInboxPurchHeader.DELETEALL;
      TempInboxPurchLine.RESET;
      TempInboxPurchLine.DELETEALL;
      TempInboxSalesHeader.DELETEALL;
      TempInboxSalesLine.RESET;
      TempInboxSalesLine.DELETEALL;
      TempICInboxJnlLine.RESET;
      TempICInboxJnlLine.DELETEALL;
      TempInboxOutboxJnlLineDim.DELETEALL;
      TempICDocDim.DELETEALL;
    END;

    PROCEDURE RecreateInboxTrans@1(OutboxTrans@1000 : Record 414);
    VAR
      ICInboxTrans@1001 : Record 418;
      ICInboxJnlLine@1002 : Record 419;
      ICInboxSalesHdr@1003 : Record 434;
      ICInboxSalesLine@1004 : Record 435;
      ICInboxPurchHdr@1005 : Record 436;
      ICInboxPurchLine@1006 : Record 437;
      ICInboxOutboxJnlLineDim@1007 : Record 423;
      ICDocDim@1008 : Record 442;
      HandledICInboxTrans@1014 : Record 420;
      HandledICInboxJnlLine@1013 : Record 421;
      HandledICInboxSalesHdr@1012 : Record 438;
      HandledICInboxSalesLine@1011 : Record 439;
      HandledICInboxPurchHdr@1010 : Record 440;
      HandledICInboxPurchLine@1009 : Record 441;
      HandledICInboxOutboxJnlLineDim@1016 : Record 423;
      HandledICDocDim@1015 : Record 442;
      ICCommentLine@1017 : Record 424;
      HandledICCommentLine@1018 : Record 424;
    BEGIN
      HandledICInboxTrans.LOCKTABLE;
      ICInboxTrans.LOCKTABLE;

      HandledICInboxTrans.GET(
        OutboxTrans."Transaction No.",OutboxTrans."IC Partner Code",
        ICInboxTrans."Transaction Source"::"Created by Partner",OutboxTrans."Document Type");
      ICInboxTrans.TRANSFERFIELDS(HandledICInboxTrans,TRUE);
      ICInboxTrans."Line Action" := ICInboxTrans."Line Action"::"No Action";
      ICInboxTrans.INSERT;
      HandledICInboxTrans.DELETE;

      HandledICCommentLine.SETRANGE("Table Name",HandledICCommentLine."Table Name"::"Handled IC Inbox Transaction");
      HandledICCommentLine.SETRANGE("Transaction No.",HandledICInboxTrans."Transaction No.");
      HandledICCommentLine.SETRANGE("IC Partner Code",HandledICInboxTrans."IC Partner Code");
      HandledICCommentLine.SETRANGE("Transaction Source",HandledICInboxTrans."Transaction Source");
      IF HandledICCommentLine.FIND('-') THEN
        REPEAT
          ICCommentLine := HandledICCommentLine;
          ICCommentLine."Table Name" := ICCommentLine."Table Name"::"IC Inbox Transaction";
          ICCommentLine.INSERT;
          HandledICCommentLine.DELETE;
        UNTIL HandledICCommentLine.NEXT = 0;

      WITH HandledICInboxJnlLine DO BEGIN
        SETRANGE("Transaction No.",ICInboxTrans."Transaction No.");
        SETRANGE("IC Partner Code",ICInboxTrans."IC Partner Code");
        SETRANGE("Transaction Source",ICInboxTrans."Transaction Source");
        IF FIND('-') THEN
          REPEAT
            ICInboxJnlLine.TRANSFERFIELDS(HandledICInboxJnlLine,TRUE);
            ICInboxJnlLine.INSERT;
            HandledICInboxOutboxJnlLineDim.SETRANGE("Table ID",DATABASE::"Handled IC Inbox Jnl. Line");
            HandledICInboxOutboxJnlLineDim.SETRANGE("Transaction No.","Transaction No.");
            HandledICInboxOutboxJnlLineDim.SETRANGE("IC Partner Code","IC Partner Code");
            IF HandledICInboxOutboxJnlLineDim.FIND('-') THEN
              REPEAT
                ICInboxOutboxJnlLineDim := HandledICInboxOutboxJnlLineDim;
                ICInboxOutboxJnlLineDim."Table ID" := DATABASE::"IC Inbox Jnl. Line";
                ICInboxOutboxJnlLineDim.INSERT;
                HandledICInboxOutboxJnlLineDim.DELETE;
              UNTIL HandledICInboxOutboxJnlLineDim.NEXT = 0;
            DELETE;
          UNTIL NEXT = 0;
      END;

      WITH HandledICInboxSalesHdr DO BEGIN
        SETRANGE("IC Transaction No.",ICInboxTrans."Transaction No.");
        SETRANGE("IC Partner Code",ICInboxTrans."IC Partner Code");
        SETRANGE("Transaction Source",ICInboxTrans."Transaction Source");
        IF FIND('-') THEN
          REPEAT
            ICInboxSalesHdr.TRANSFERFIELDS(HandledICInboxSalesHdr,TRUE);
            ICInboxSalesHdr.INSERT;
            HandledICDocDim.SETRANGE("Table ID",DATABASE::"Handled IC Inbox Sales Header");
            HandledICDocDim.SETRANGE("Transaction No.","IC Transaction No.");
            HandledICDocDim.SETRANGE("IC Partner Code","IC Partner Code");
            IF HandledICDocDim.FIND('-') THEN
              REPEAT
                ICDocDim := HandledICDocDim;
                ICDocDim."Table ID" := DATABASE::"IC Inbox Sales Header";
                ICDocDim.INSERT;
                HandledICDocDim.DELETE;
              UNTIL HandledICDocDim.NEXT = 0;
            HandledICInboxSalesLine.SETRANGE("IC Transaction No.","IC Transaction No.");
            HandledICInboxSalesLine.SETRANGE("IC Partner Code","IC Partner Code");
            HandledICInboxSalesLine.SETRANGE("Transaction Source","Transaction Source");
            IF HandledICInboxSalesLine.FIND('-') THEN
              REPEAT
                ICInboxSalesLine.TRANSFERFIELDS(HandledICInboxSalesLine,TRUE);
                ICInboxSalesLine.INSERT;
                HandledICDocDim.SETRANGE("Table ID",DATABASE::"Handled IC Inbox Sales Line");
                HandledICDocDim.SETRANGE("Transaction No.","IC Transaction No.");
                HandledICDocDim.SETRANGE("IC Partner Code","IC Partner Code");
                IF HandledICDocDim.FIND('-') THEN
                  REPEAT
                    ICDocDim := HandledICDocDim;
                    ICDocDim."Table ID" := DATABASE::"IC Inbox Sales Line";
                    ICDocDim.INSERT;
                    HandledICDocDim.DELETE;
                  UNTIL HandledICDocDim.NEXT = 0;
                HandledICInboxSalesLine.DELETE;
              UNTIL HandledICInboxSalesLine.NEXT = 0;
            DELETE;
          UNTIL NEXT = 0;
      END;

      WITH HandledICInboxPurchHdr DO BEGIN
        SETRANGE("IC Transaction No.",ICInboxTrans."Transaction No.");
        SETRANGE("IC Partner Code",ICInboxTrans."IC Partner Code");
        SETRANGE("Transaction Source",ICInboxTrans."Transaction Source");
        IF FIND('-') THEN
          REPEAT
            ICInboxPurchHdr.TRANSFERFIELDS(HandledICInboxPurchHdr,TRUE);
            ICInboxPurchHdr.INSERT;
            HandledICDocDim.SETRANGE("Table ID",DATABASE::"Handled IC Inbox Purch. Header");
            HandledICDocDim.SETRANGE("Transaction No.","IC Transaction No.");
            HandledICDocDim.SETRANGE("IC Partner Code","IC Partner Code");
            IF HandledICDocDim.FIND('-') THEN
              REPEAT
                ICDocDim := HandledICDocDim;
                ICDocDim."Table ID" := DATABASE::"IC Inbox Purchase Header";
                ICDocDim.INSERT;
                HandledICDocDim.DELETE;
              UNTIL HandledICDocDim.NEXT = 0;
            HandledICInboxPurchLine.SETRANGE("IC Transaction No.","IC Transaction No.");
            HandledICInboxPurchLine.SETRANGE("IC Partner Code","IC Partner Code");
            HandledICInboxPurchLine.SETRANGE("Transaction Source","Transaction Source");
            IF HandledICInboxPurchLine.FIND('-') THEN
              REPEAT
                ICInboxPurchLine.TRANSFERFIELDS(HandledICInboxPurchLine,TRUE);
                ICInboxPurchLine.INSERT;
                HandledICDocDim.SETRANGE("Table ID",DATABASE::"Handled IC Inbox Purch. Line");
                HandledICDocDim.SETRANGE("Transaction No.","IC Transaction No.");
                HandledICDocDim.SETRANGE("IC Partner Code","IC Partner Code");
                IF HandledICDocDim.FIND('-') THEN
                  REPEAT
                    ICDocDim := HandledICDocDim;
                    ICDocDim."Table ID" := DATABASE::"IC Inbox Purchase Line";
                    ICDocDim.INSERT;
                    HandledICDocDim.DELETE;
                  UNTIL HandledICDocDim.NEXT = 0;
                HandledICInboxPurchLine.DELETE;
              UNTIL HandledICInboxPurchLine.NEXT = 0;
            DELETE;
          UNTIL NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
