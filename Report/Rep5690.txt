OBJECT Report 5690 Index Fixed Assets
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Index Fixed Assets;
               ESP=Ajustar valores activos];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF FAPostingDate = 0D THEN
                    ERROR(Text000,FAJnlLine.FIELDCAPTION("FA Posting Date"));
                  IF FAPostingDate <> NORMALDATE(FAPostingDate)  THEN
                    ERROR(Text001);

                  IF PostingDate = 0D THEN
                    PostingDate := FAPostingDate;
                  DeprBook.GET(DeprBookCode);
                  DeprBook.TESTFIELD("Allow Indexation",TRUE);
                  IF DeprBook."Use Same FA+G/L Posting Dates" AND (FAPostingDate <> PostingDate) THEN
                    ERROR(
                      Text002,
                      FAJnlLine.FIELDCAPTION("FA Posting Date"),
                      FAJnlLine.FIELDCAPTION("Posting Date"),
                      DeprBook.FIELDCAPTION("Use Same FA+G/L Posting Dates"),
                      FALSE,
                      DeprBook.TABLECAPTION,
                      DeprBook.FIELDCAPTION(Code),
                      DeprBook.Code);

                  DeprBook.IndexGLIntegration(GLIntegration);
                  FirstGenJnl := TRUE;
                  FirstFAJnl := TRUE;

                  FALedgEntry.SETCURRENTKEY(
                    "FA No.","Depreciation Book Code","FA Posting Category","FA Posting Type","FA Posting Date");
                  FALedgEntry.SETRANGE("Depreciation Book Code",DeprBookCode);
                  FALedgEntry.SETRANGE("FA Posting Category",FALedgEntry."FA Posting Category"::" ");
                  FALedgEntry.SETRANGE("FA Posting Date",0D,FAPostingDate);

                  MaintenanceLedgEntry.SETCURRENTKEY(
                    "FA No.","Depreciation Book Code","FA Posting Date");
                  MaintenanceLedgEntry.SETRANGE("Depreciation Book Code",DeprBookCode);
                  MaintenanceLedgEntry.SETRANGE("FA Posting Date",0D,FAPostingDate);

                  Window.OPEN(Text003);
                END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table5600;
        OnAfterGetRecord=BEGIN
                           IF NOT FADeprBook.GET("No.",DeprBookCode) THEN
                             CurrReport.SKIP;
                           Window.UPDATE(1,"No.");

                           IF Inactive OR
                              Blocked OR
                              (FADeprBook."Disposal Date" > 0D) OR
                              (FADeprBook."Acquisition Date" = 0D) OR
                              (FADeprBook."Acquisition Date" > FAPostingDate)
                           THEN
                             CurrReport.SKIP;

                           FALedgEntry.SETRANGE("FA No.","No.");
                           MaintenanceLedgEntry.SETRANGE("FA No.","No.");

                           i := 0;
                           WHILE i < 9 DO BEGIN
                             i := i + 1;
                             IF i = 7 THEN
                               i := 8;
                             IndexAmount := 0;
                             IF IndexChoices[i] THEN BEGIN
                               IF (i = 1) THEN
                                 FALedgEntry.SETRANGE("FA Posting Type",FALedgEntry."FA Posting Type"::"Acquisition Cost");
                               IF (i = 2) THEN
                                 FALedgEntry.SETRANGE("FA Posting Type",FALedgEntry."FA Posting Type"::Depreciation);
                               IF (i = 3) THEN
                                 FALedgEntry.SETRANGE("FA Posting Type",FALedgEntry."FA Posting Type"::"Write-Down");
                               IF (i = 4) THEN
                                 FALedgEntry.SETRANGE("FA Posting Type",FALedgEntry."FA Posting Type"::Appreciation);
                               IF (i = 5) THEN
                                 FALedgEntry.SETRANGE("FA Posting Type",FALedgEntry."FA Posting Type"::"Custom 1");
                               IF (i = 6) THEN
                                 FALedgEntry.SETRANGE("FA Posting Type",FALedgEntry."FA Posting Type"::"Custom 2");
                               IF (i = 9) THEN
                                 FALedgEntry.SETRANGE("FA Posting Type",FALedgEntry."FA Posting Type"::"Salvage Value");
                               IF i = 8 THEN BEGIN
                                 MaintenanceLedgEntry.CALCSUMS(Amount);
                                 IndexAmount := MaintenanceLedgEntry.Amount;
                               END ELSE BEGIN
                                 FALedgEntry.CALCSUMS(Amount);
                                 IndexAmount := FALedgEntry.Amount;
                               END;
                               IndexAmount :=
                                 DepreciationCalc.CalcRounding(DeprBookCode,IndexAmount * (IndexFigure / 100 - 1));
                               IF NOT GLIntegration[i] OR "Budgeted Asset" THEN
                                 InsertFAJnlLine("No.",IndexAmount,i - 1)
                               ELSE
                                 InsertGenJnlLine("No.",IndexAmount,i - 1);
                             END;
                           END;
                         END;

        ReqFilterFields=No.,FA Class Code,FA Subclass Code,Budgeted Asset;
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=8910;
      Height=9460;
      SaveValues=Yes;
      OnOpenForm=BEGIN
                   IF DeprBookCode = '' THEN BEGIN
                     FASetup.GET;
                     DeprBookCode := FASetup."Default Depr. Book";
                   END;
                 END;

    }
    CONTROLS
    {
      { 9   ;TextBox      ;3410 ;1320 ;1650 ;440  ;CaptionML=[ENU=FA Posting Date;
                                                              ESP=A/F Fecha registro];
                                                   SourceExpr=FAPostingDate }
      { 10  ;Label        ;0    ;1320 ;3300 ;440  ;ParentControl=9 }
      { 11  ;TextBox      ;3410 ;1870 ;1650 ;440  ;CaptionML=[ENU=Posting Date;
                                                              ESP=Fecha registro];
                                                   SourceExpr=PostingDate }
      { 12  ;Label        ;0    ;1870 ;3300 ;440  ;ParentControl=11 }
      { 13  ;TextBox      ;3410 ;2970 ;5500 ;440  ;CaptionML=[ENU=Posting Description;
                                                              ESP=Texto de registro];
                                                   SourceExpr=PostingDescription }
      { 14  ;Label        ;0    ;2970 ;3300 ;440  ;ParentControl=13 }
      { 15  ;TextBox      ;3410 ;0    ;2750 ;440  ;CaptionML=[ENU=Depreciation Book;
                                                              ESP=Libro amortizaci¢n];
                                                   SourceExpr=DeprBookCode;
                                                   TableRelation="Depreciation Book" }
      { 16  ;Label        ;0    ;0    ;3300 ;440  ;ParentControl=15 }
      { 3   ;CheckBox     ;3520 ;4840 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Acquisition Cost;
                                                              ESP=Coste];
                                                   SourceExpr=IndexChoices[1] }
      { 4   ;Label        ;0    ;4290 ;3300 ;440  ;LeaderDots=No;
                                                   CaptionML=[ENU=Index;
                                                              ESP=Indice] }
      { 1   ;CheckBox     ;3520 ;5390 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Depreciation;
                                                              ESP=Amortizaci¢n];
                                                   SourceExpr=IndexChoices[2] }
      { 5   ;TextBox      ;3410 ;660  ;2750 ;440  ;CaptionML=[ENU=Index Figure;
                                                              ESP=Valor ajuste];
                                                   SourceExpr=IndexFigure;
                                                   MinValue=0 }
      { 6   ;Label        ;0    ;660  ;3300 ;440  ;ParentControl=5 }
      { 2   ;CheckBox     ;3520 ;6050 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Write-Down;
                                                              ESP=Depreciaci¢n];
                                                   SourceExpr=IndexChoices[3] }
      { 7   ;CheckBox     ;3520 ;6600 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Appreciation;
                                                              ESP=Apreciaci¢n];
                                                   SourceExpr=IndexChoices[4] }
      { 8   ;CheckBox     ;3520 ;7260 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Custom 1;
                                                              ESP=Especial];
                                                   SourceExpr=IndexChoices[5] }
      { 17  ;CheckBox     ;3520 ;7810 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Custom 2;
                                                              ESP=Provisi¢n];
                                                   SourceExpr=IndexChoices[6] }
      { 18  ;CheckBox     ;3520 ;8470 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Salvage Value;
                                                              ESP=Valor residual];
                                                   SourceExpr=IndexChoices[9] }
      { 19  ;CheckBox     ;3520 ;9020 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Maintenance;
                                                              ESP=Mantenimiento];
                                                   SourceExpr=IndexChoices[8] }
      { 20  ;CheckBox     ;3410 ;3630 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Insert Bal. Account;
                                                              ESP=Insertar contrapartida];
                                                   SourceExpr=BalAccount }
      { 21  ;Label        ;0    ;3630 ;3300 ;440  ;ParentControl=20 }
      { 22  ;Label        ;0    ;2420 ;3300 ;440  ;ParentControl=23 }
      { 23  ;TextBox      ;3410 ;2420 ;2750 ;440  ;CaptionML=[ENU=Document No.;
                                                              ESP=N§ documento];
                                                   SourceExpr=DocumentNo }
      { 24  ;Label        ;220  ;4840 ;3190 ;440  ;ParentControl=3 }
      { 25  ;Label        ;220  ;5390 ;3190 ;440  ;ParentControl=1 }
      { 26  ;Label        ;220  ;6600 ;3190 ;440  ;ParentControl=7 }
      { 27  ;Label        ;220  ;6050 ;3190 ;440  ;ParentControl=2 }
      { 28  ;Label        ;220  ;9020 ;3190 ;440  ;ParentControl=19 }
      { 29  ;Label        ;220  ;8470 ;3190 ;440  ;ParentControl=18 }
      { 30  ;Label        ;220  ;7810 ;3190 ;440  ;ParentControl=17 }
      { 31  ;Label        ;220  ;7260 ;3190 ;440  ;ParentControl=8 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF DeprBookCode = '' THEN BEGIN
                     FASetup.GET;
                     DeprBookCode := FASetup."Default Depr. Book";
                   END;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones] }

      { 15  ;2   ;Field     ;
                  CaptionML=[ENU=Depreciation Book;
                             ESP=Libro amortizaci¢n];
                  SourceExpr=DeprBookCode;
                  TableRelation="Depreciation Book" }

      { 5   ;2   ;Field     ;
                  CaptionML=[ENU=Index Figure;
                             ESP=Valor ajuste];
                  SourceExpr=IndexFigure;
                  MinValue=0 }

      { 9   ;2   ;Field     ;
                  CaptionML=[ENU=FA Posting Date;
                             ESP=A/F Fecha registro];
                  SourceExpr=FAPostingDate }

      { 11  ;2   ;Field     ;
                  CaptionML=[ENU=Posting Date;
                             ESP=Fecha registro];
                  SourceExpr=PostingDate }

      { 23  ;2   ;Field     ;
                  CaptionML=[ENU=Document No.;
                             ESP=N§ documento];
                  SourceExpr=DocumentNo }

      { 13  ;2   ;Field     ;
                  CaptionML=[ENU=Posting Description;
                             ESP=Texto de registro];
                  SourceExpr=PostingDescription }

      { 20  ;2   ;Field     ;
                  CaptionML=[ENU=Insert Bal. Account;
                             ESP=Insertar contrapartida];
                  SourceExpr=BalAccount }

      { 4   ;2   ;Group     ;
                  CaptionML=[ENU=Index;
                             ESP=Öndice] }

      { 3   ;3   ;Field     ;
                  CaptionML=[ENU=Acquisition Cost;
                             ESP=Coste];
                  SourceExpr=IndexChoices[1] }

      { 1   ;3   ;Field     ;
                  CaptionML=[ENU=Depreciation;
                             ESP=Amortizaci¢n];
                  SourceExpr=IndexChoices[2] }

      { 2   ;3   ;Field     ;
                  CaptionML=[ENU=Write-Down;
                             ESP=Depreciaci¢n];
                  SourceExpr=IndexChoices[3] }

      { 7   ;3   ;Field     ;
                  CaptionML=[ENU=Appreciation;
                             ESP=Apreciaci¢n];
                  SourceExpr=IndexChoices[4] }

      { 8   ;3   ;Field     ;
                  CaptionML=[ENU=Custom 1;
                             ESP=Especial];
                  SourceExpr=IndexChoices[5] }

      { 17  ;3   ;Field     ;
                  CaptionML=[ENU=Custom 2;
                             ESP=Provisi¢n];
                  SourceExpr=IndexChoices[6] }

      { 18  ;3   ;Field     ;
                  CaptionML=[ENU=Salvage Value;
                             ESP=Valor residual];
                  SourceExpr=IndexChoices[9] }

      { 19  ;3   ;Field     ;
                  CaptionML=[ENU=Maintenance;
                             ESP=Mantenimiento];
                  SourceExpr=IndexChoices[8] }

    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You must specify %1.;ESP=Se debe indicar %1.';
      Text001@1001 : TextConst 'ENU=FA Posting Date must not be a closing date.;ESP=La fecha registro del activo no puede ser una fecha de cierre.';
      Text002@1002 : TextConst 'ENU="%1 and %2 must be identical. %3 must be %4 in %5 %6 = %7.";ESP="%1 y %2 deben ser iguales. %3 debe ser %4 en %5 %6 = %7."';
      Text003@1003 : TextConst 'ENU=Indexing fixed asset   #1##########;ESP=Ajustando activo       #1##########';
      GenJnlLine@1004 : Record 81;
      FASetup@1005 : Record 5603;
      FAJnlLine@1006 : Record 5621;
      DeprBook@1007 : Record 5611;
      FADeprBook@1008 : Record 5612;
      FALedgEntry@1009 : Record 5601;
      MaintenanceLedgEntry@1010 : Record 5625;
      FAJnlSetup@1011 : Record 5605;
      DepreciationCalc@1012 : Codeunit 5616;
      Window@1013 : Dialog;
      FAPostingDate@1014 : Date;
      IndexFigure@1015 : Decimal;
      IndexChoices@1016 : ARRAY [9] OF Boolean;
      IndexAmount@1017 : Decimal;
      GLIntegration@1018 : ARRAY [9] OF Boolean;
      FirstGenJnl@1019 : Boolean;
      FirstFAJnl@1020 : Boolean;
      PostingDate@1021 : Date;
      DocumentNo@1022 : Code[20];
      DocumentNo2@1023 : Code[20];
      DocumentNo3@1024 : Code[20];
      NoSeries2@1025 : Code[10];
      Noseries3@1026 : Code[10];
      PostingDescription@1027 : Text[50];
      DeprBookCode@1028 : Code[10];
      BalAccount@1029 : Boolean;
      FAJnlNextLineNo@1030 : Integer;
      GenJnlNextLineNo@1031 : Integer;
      i@1032 : Integer;

    LOCAL PROCEDURE InsertGenJnlLine@2(FANo@1000 : Code[20];IndexAmount@1001 : Decimal;PostingType@1002 : 'Acquisition Cost,Depreciation,Write-Down,Appreciation,Custom 1,Custom 2,Disposal,Maintenance,Salvage Value');
    VAR
      FAInsertGLAcc@1003 : Codeunit 5601;
    BEGIN
      IF IndexAmount = 0 THEN
        EXIT;
      IF FirstGenJnl THEN BEGIN
        GenJnlLine.LOCKTABLE;
        FAJnlSetup.GenJnlName(DeprBook,GenJnlLine,GenJnlNextLineNo);
        NoSeries2:= FAJnlSetup.GetGenNoSeries(GenJnlLine);
        IF DocumentNo = '' THEN
          DocumentNo2 := FAJnlSetup.GetGenJnlDocumentNo(GenJnlLine,FAPostingDate,TRUE)
        ELSE
          DocumentNo2 := DocumentNo;
        FirstGenJnl := FALSE;
      END;
      WITH GenJnlLine DO BEGIN
        INIT;
        "Line No." := 0;
        FAJnlSetup.SetGenJnlTrailCodes(GenJnlLine);
        "FA Posting Date" := FAPostingDate;
        "Posting Date" := PostingDate;
        IF "Posting Date" = "FA Posting Date" THEN
          "FA Posting Date" := 0D;
        "FA Posting Type" := PostingType + 1;
        "Account Type" := "Account Type"::"Fixed Asset";
        VALIDATE("Account No.",FANo);
        "Document No." := DocumentNo2;
        "Posting No. Series" := NoSeries2;
        Description := PostingDescription;
        VALIDATE("Depreciation Book Code",DeprBookCode);
        VALIDATE(Amount,IndexAmount);
        "Index Entry" := TRUE;
        GenJnlNextLineNo := GenJnlNextLineNo + 10000;
        "Line No." := GenJnlNextLineNo;
        INSERT(TRUE);
        IF BalAccount THEN BEGIN
          FAInsertGLAcc.GetBalAcc(GenJnlLine);
          IF FIND('+') THEN ;
          GenJnlNextLineNo := "Line No.";
        END;
      END;
    END;

    LOCAL PROCEDURE InsertFAJnlLine@3(FANo@1000 : Code[20];IndexAmount@1001 : Decimal;PostingType@1002 : 'Acquisition Cost,Depreciation,Write-Down,Appreciation,Custom 1,Custom 2,Disposal,Maintenance,Salvage Value');
    BEGIN
      IF IndexAmount = 0 THEN
        EXIT;
      IF FirstFAJnl THEN BEGIN
        FAJnlLine.LOCKTABLE;
        FAJnlSetup.FAJnlName(DeprBook,FAJnlLine,FAJnlNextLineNo);
        Noseries3:= FAJnlSetup.GetFANoSeries(FAJnlLine);
        IF DocumentNo = '' THEN
          DocumentNo3 := FAJnlSetup.GetFAJnlDocumentNo(FAJnlLine,FAPostingDate,TRUE)
        ELSE
          DocumentNo3 := DocumentNo;
        FirstFAJnl := FALSE;
      END;
      WITH FAJnlLine DO BEGIN
        INIT;
        "Line No." := 0;
        FAJnlSetup.SetFAJnlTrailCodes(FAJnlLine);
        "Posting Date" := PostingDate;
        "FA Posting Date" := FAPostingDate;
        IF "Posting Date" = "FA Posting Date" THEN
          "Posting Date" := 0D;
        "FA Posting Type" := PostingType;
        VALIDATE("FA No.",FANo);
        "Document No." := DocumentNo3;
        "Posting No. Series" := Noseries3;
        Description := PostingDescription;
        VALIDATE("Depreciation Book Code",DeprBookCode);
        VALIDATE(Amount,IndexAmount);
        "Index Entry" := TRUE;
        FAJnlNextLineNo := FAJnlNextLineNo + 10000;
        "Line No." := FAJnlNextLineNo;
        INSERT(TRUE);
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
