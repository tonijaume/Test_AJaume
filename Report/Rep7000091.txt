OBJECT Report 7000091 Bill group - Export N58
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVES6.00.01;
  }
  PROPERTIES
  {
    Permissions=TableData 7000005=m;
    CaptionML=[ENU=Bill group - Export N58;
               ESP=Remesa - Exportar N58];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   CompanyInfo.GET();
                   VATRegNo := COPYSTR(DELCHR(CompanyInfo."VAT Registration No.",'=',' .-/'),1,9);
                 END;

    OnPreReport=BEGIN
                  IF WithExpenses THEN
                    Expenses := '1'
                  ELSE
                    Expenses := '0';

                  IF PrintedDocs THEN
                    PrintedDocs2 := '0'
                  ELSE
                    PrintedDocs2 := '1';

                  // CLEAR(FILE);
                  OutFile.TEXTMODE := TRUE;
                  OutFile.WRITEMODE := TRUE;
                  IF ISSERVICETIER THEN BEGIN
                    ExternalFile := RBMgt.EnvironFileName('','');
                    ToFile := Text10702;
                  END;
                  OutFile.CREATE(ExternalFile);
                END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table7000005;
        DataItemTableView=SORTING(No.)
                          WHERE(Factoring=CONST(" "));
        OnPreDataItem=BEGIN
                        CompanyInfo.GET;

                        "Bill Group".FIND('-');

                        IF BankSuffix = '' THEN
                          ERROR(Text1100000);

                        IF BankSuffixBankAcc = '' THEN BEGIN
                          Suffix.SETRANGE (Suffix,BankSuffix);
                          IF Suffix.FIND ('-') THEN
                            BankSuffixBankAcc := Suffix."Bank Acc. Code";
                        END;

                        IF NOT DocMisc.CheckBankSuffix(BankSuffixBankAcc,"Bill Group"."Bank Account No.") THEN
                          IF NOT CONFIRM(Text1100001 +
                                         Text1100002,
                                         FALSE,
                                         "Bill Group".FIELDCAPTION("Bank Account No."),
                                         "Bill Group".TABLECAPTION) THEN
                             ERROR(Text1100003);

                        BankAcc.GET("Bank Account No.");
                        CCCBankNo := BankAcc."CCC Bank No.";
                        CCCBankBranchNo := BankAcc."CCC Bank Branch No.";
                        CCCControlDigits := BankAcc."CCC Control Digits";
                        CCCAccNo := BankAcc."CCC Bank Account No.";

                        IF (CCCBankNo = '') OR (CCCBankBranchNo = '') OR
                           (CCCControlDigits = '') OR (CCCAccNo = '')
                        THEN
                          ERROR(Text1100004,BankAcc."No.");

                        IsEuro := DocMisc.GetRegisterCode("Bill Group"."Currency Code",RegisterCode,RegisterString);

                        RegisterCode2 := RegisterCode + 1;
                        DLDACurrency;

                        CLEAR(OutText);
                        OutText :=
                          RegisterString + '70' + FORMAT(CompanyInfo."VAT Registration No.",9) + PADSTR(BankSuffix,3,' ') +
                          FORMAT(WORKDATE,6,5) + PADSTR('',6,' ') +
                          PADSTR(CompanyInfo.Name,40,' ') + PADSTR('',20,' ') + CONVERTSTR(BankAcc."CCC Bank No.",' ','0') +
                          CONVERTSTR(BankAcc."CCC Bank Branch No.",' ','0') + PADSTR('',12,' ') + PADSTR('',40,' ') + PADSTR('',14,' ');
                        OutFile.WRITE(OutText);
                      END;

        OnAfterGetRecord=BEGIN
                           BillGrNo := COPYSTR("Bill Group"."No.",7,3);


                           RegisterCode2 := RegisterCode + 3;
                           DLDACurrency;

                           CLEAR(OutText);
                           OutText :=
                             RegisterString + '70' + FORMAT(CompanyInfo."VAT Registration No.",9) + PADSTR(BankSuffix,3,' ') +
                             FORMAT(WORKDATE,6,5) +  PADSTR('',6,' ') +
                             PADSTR(CompanyInfo.Name,40,' ') + CONVERTSTR(CCCBankNo,' ','0') +
                             CONVERTSTR(CCCBankBranchNo,' ','0') + CONVERTSTR(CCCControlDigits,' ','0') +
                             CONVERTSTR(CCCAccNo,' ','0') + PADSTR('',8,' ') + '06' + PADSTR('',10,' ') +
                             PADSTR('',40,' ') + PADSTR('',2,' ') +'000000000' + PADSTR('',3,' ');
                           OutFile.WRITE(OutText);

                           TotalPrintedCust := TotalPrintedCust + 1;
                         END;

        OnPostDataItem=BEGIN
                         "No. Printed" := "No. Printed" + 1;
                         TotalPrinted := TotalPrinted + 2;

                         RegisterCode2 := RegisterCode + 9;
                         DLDACurrency;

                         IF IsEuro THEN
                           OutText :=
                             RegisterString + '70' + FORMAT(CompanyInfo."VAT Registration No.",9) + PADSTR(BankSuffix,3,' ') +
                             PADSTR('',12,' ') + PADSTR('',40,' ') +
                             '0001' + PADSTR('',16,' ') + EuroAmount(TotalAmount) + PADSTR('',6,' ') +
                             CONVERTSTR(FORMAT(TotalDoc,10,Text1100006),' ','0') + CONVERTSTR(FORMAT(TotalPrinted,10,Text1100006),' ','0') +
                             PADSTR('',20,' ') + PADSTR('',18,' ')
                         ELSE
                           OutText :=
                             RegisterString + '70' + FORMAT(CompanyInfo."VAT Registration No.",9) + PADSTR(BankSuffix,3,' ') +
                             PADSTR('',12,' ') + PADSTR('',40,' ') +
                             '0001' + PADSTR('',16,' ') + CONVERTSTR(FORMAT(TotalAmount,10,Text1100007),' ','0') + PADSTR('',6,' ') +
                             CONVERTSTR(FORMAT(TotalDoc,10,Text1100007),' ','0') + CONVERTSTR(FORMAT(TotalPrinted,10,Text1100007),' ','0') +
                             PADSTR('',20,' ') + PADSTR('',18,' ');

                         OutFile.WRITE(OutText);
                         OutFile.CLOSE();
                         IF ISSERVICETIER THEN
                           DOWNLOAD(ExternalFile,'','C:',Text10701,ToFile);
                       END;

        ReqFilterFields=No.;
      }
      SECTIONS
      {
      }
       }
    { PROPERTIES
      {
        DataItemIndent=1;
        DataItemTable=Table7000002;
        DataItemTableView=SORTING(Type,Collection Agent,Bill Gr./Pmt. Order No.)
                          WHERE(Type=CONST(Receivable));
        DataItemVarName=Doc;
        OnAfterGetRecord=BEGIN
                           DocType := DocMisc.DocType(Doc."Payment Method Code");

                           CustLedgEntry.GET(Doc."Entry No.");

                           Customer.GET(CustLedgEntry."Customer No.");
                           IF NOT CustBankAcc.GET(CustLedgEntry."Customer No.",Doc."Cust./Vendor Bank Acc. Code") THEN BEGIN
                             CustCCCBankCode := '0000';
                             CustCCCBankBranchNo := '0000';
                             CustCCCControlDigits := '00';
                             CustCCCAccNo := '0000000000';
                             NotDom := TRUE;
                           END ELSE BEGIN
                             CustCCCBankCode := CustBankAcc."CCC Bank No.";
                             CustCCCBankBranchNo := CustBankAcc."CCC Bank Branch No.";
                             CustCCCControlDigits := CustBankAcc."CCC Control Digits";
                             CustCCCAccNo := CustBankAcc."CCC Bank Account No.";
                             NotDom := FALSE;
                             IF Checkerrors THEN BEGIN
                               DocMisc.CheckControlDigit(CustCCCControlDigits,CustCCCBankCode,CustCCCAccNo,CustCCCBankBranchNo);
                               IF (CustBankAcc."CCC Control Digits" <> CustCCCControlDigits) AND (CustCCCControlDigits <> '**') THEN BEGIN
                                 CustBankAcc."CCC Control Digits" := CustCCCControlDigits;
                                 CustBankAcc.MODIFY;
                               END;
                             END;
                           END;

                           IF (CustCCCBankCode = '') OR (CustCCCBankBranchNo = '') OR
                              (CustCCCControlDigits = '') OR (CustCCCAccNo = '')
                           THEN BEGIN
                             CustCCCBankCode := '0000';
                             CustCCCBankBranchNo := '0000';
                             CustCCCControlDigits := '00';
                             CustCCCAccNo := '0000000000';
                             NotDom := TRUE;
                           END;

                           CustAddress[1] := Customer.Name;
                           CustAddress[2] := Customer.Address;
                           CustAddress[3] := Customer.City;
                           CustAddress[4] := Customer."Post Code";
                           IF Doc."Pmt. Address Code" <> '' THEN BEGIN
                             IF CustPmtAddress.GET(Doc."Account No.",Doc."Pmt. Address Code") THEN BEGIN
                               CustAddress[2] := CustPmtAddress.Address;
                               CustAddress[3] := CustPmtAddress.City;
                               CustAddress[4] := CustPmtAddress."Post Code";
                             END
                             ELSE
                               ERROR(Text1100011);
                           END;
                           CustNoNIF:=Customer."VAT Registration No.";
                           AdditionalInfo := Text1100008 + Doc."Document No." + '/' + Doc."No.";
                           AdditionalInfo2 := Doc."Document No." + '/' + Doc."No.";

                           IF Accepted = Accepted::Yes THEN
                             IsAccepted := '1'
                           ELSE
                             IsAccepted := '0';

                           TotalDoc := TotalDoc + 1;
                           TotalDocsCust := TotalDocsCust + 1;
                           TotalPrintedCust := TotalPrintedCust + 1;
                           TotalPosted := TotalPosted + 3;

                           CASE TRUE OF
                             LCY = LCY::Euro:
                               IF IsEuro THEN
                                 DocAmount := EuroAmount("Remaining Amt. (LCY)")
                               ELSE
                                 DocAmount := CONVERTSTR(FORMAT(Doc."Remaining Amount",10,Text1100006),' ','0');
                             LCY = LCY::Other:
                               IF IsEuro THEN
                                 DocAmount := EuroAmount("Remaining Amount")
                               ELSE
                                 DocAmount := CONVERTSTR(FORMAT(Doc."Remaining Amount",10,Text1100006),' ','0');
                           END;

                           RegisterCode2 := RegisterCode + 6;
                           DLDACurrency;

                           CLEAR(OutText);
                           OutText :=
                             RegisterString + '70' + FORMAT(CompanyInfo."VAT Registration No.",9) + PADSTR(BankSuffix,3,' ') +
                             PADSTR(FORMAT(CustNoNIF),12,' ') +
                             PADSTR(FORMAT(Customer.Name),40,' ') + CONVERTSTR(PADSTR(CustCCCBankCode,4,' '),' ','0') +
                             CONVERTSTR(PADSTR(CustCCCBankBranchNo,4,' '),' ','0') + PADSTR(CustCCCControlDigits,2,' ') +
                             CONVERTSTR(PADSTR(CustCCCAccNo,10,' '),' ','0') + DocAmount +
                             Text1100009 + PADSTR(AdditionalInfo2,10,' ') + PADSTR(AdditionalInfo,40,' ') +
                             FORMAT(Doc."Due Date",6,5) + PADSTR('',2,' ');

                           OutFile.WRITE(OutText);
                           PostalCode.GET (CompanyInfo."Post Code",CompanyInfo.City);

                           IF NotDom THEN BEGIN
                             CLEAR(OutText);
                             OutText :=
                               RegisterString + '76' + FORMAT(CompanyInfo."VAT Registration No.",9) + PADSTR(BankSuffix,3,' ') +
                               PADSTR(FORMAT(CustNoNIF),12,' ') + PADSTR(CustAddress[2],40,' ') +
                               PADSTR(CustAddress[3],35,' ') +  PADSTR(CustAddress[4],8,'0') +
                               PADSTR(CompanyInfo.City,35,' ') + COPYSTR(PostalCode."County Code",1,2) +
                               FORMAT("Bill Group"."Posting Date",6,5) + PADSTR('',8,' ');

                             OutFile.WRITE(OutText);
                             TotalPrintedCust := TotalPrintedCust + 1;
                           END;
                         END;

        OnPostDataItem=BEGIN
                         CLEAR(OutText);
                         TotalPrintedCust := TotalPrintedCust + 1;

                         CASE TRUE OF
                           LCY = LCY::Euro:
                             IF IsEuro THEN BEGIN
                               "Bill Group".CALCFIELDS("Bill Group"."Amount (LCY)");
                               TotalAmount := "Bill Group"."Amount (LCY)" + TotalAmount;
                               BillGrAmount := EuroAmount("Bill Group"."Amount (LCY)")
                             END ELSE BEGIN
                               "Bill Group".CALCFIELDS("Bill Group".Amount);
                               TotalAmount := "Bill Group".Amount + TotalAmount;
                               BillGrAmount := CONVERTSTR(FORMAT("Bill Group".Amount,10,Text1100007),' ','0')
                             END;
                           LCY = LCY::Other:
                             IF IsEuro THEN BEGIN
                               "Bill Group".CALCFIELDS(Amount);
                               TotalAmount := "Bill Group".Amount + TotalAmount;
                               BillGrAmount := EuroAmount("Bill Group".Amount)
                             END ELSE BEGIN
                               "Bill Group".CALCFIELDS("Bill Group".Amount);
                               TotalAmount := "Bill Group".Amount + TotalAmount;
                               BillGrAmount := CONVERTSTR(FORMAT("Bill Group".Amount,10,Text1100007),' ','0')
                             END;
                         END;

                         RegisterCode2 := RegisterCode + 8;
                         DLDACurrency;

                         OutText :=
                           RegisterString + '70' + FORMAT(CompanyInfo."VAT Registration No.",9) + PADSTR(BankSuffix,3,' ') +
                           PADSTR('',12,' ') + PADSTR('',40,' ') +
                           PADSTR('',20,' ') + BillGrAmount + PADSTR('',6,' ') + CONVERTSTR(FORMAT(TotalDocsCust,10,Text1100007),' ','0') +
                           CONVERTSTR(FORMAT(TotalPrintedCust,10,Text1100007),' ','0') + PADSTR('',20,' ') + PADSTR('',18,' ');
                         OutFile.WRITE(OutText);

                         TotalDocsCust := 0;
                          TotalPrinted := TotalPrinted + TotalPrintedCust;
                         TotalPrintedCust := 0;
                       END;

        TotalFields=Remaining Amt. (LCY);
        DataItemLink=Bill Gr./Pmt. Order No.=FIELD(No.);
      }
      SECTIONS
      {
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=8910;
      Height=1760;
      SaveValues=Yes;
      OnOpenForm=BEGIN
                   IF ExternalFile = '' THEN
                     ExternalFile := 'C:\' + Text10702;
                   RequestOptionsForm.FileNameControl.VISIBLE := NOT ISSERVICETIER;
                 END;

    }
    CONTROLS
    {
      { 1   ;TextBox      ;3410 ;0    ;5500 ;440  ;Name=FileNameControl;
                                                   CaptionML=[ENU=External file;
                                                              ESP=Archivo externo];
                                                   SourceExpr=ExternalFile;
                                                   OnAssistEdit=BEGIN
                                                                  ExternalFile := CommonDialogMgt.OpenFile(Text10700,ExternalFile,4,Text10701,1);
                                                                END;
                                                                 }
      { 2   ;Label        ;0    ;0    ;3300 ;440  ;ParentControl=1 }
      { 3   ;CheckBox     ;3410 ;660  ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Check Errors;
                                                              ESP=Test exportaci¢n];
                                                   SourceExpr=Checkerrors }
      { 4   ;Label        ;0    ;660  ;3300 ;440  ;ParentControl=3 }
      { 5   ;TextBox      ;3410 ;1320 ;1650 ;440  ;CaptionML=[ENU=Bank Suffix;
                                                              ESP=Sufijo banco];
                                                   SourceExpr=BankSuffix;
                                                   TableRelation=Suffix.Suffix;
                                                   OnLookup=VAR
                                                              Suffix@1100002 : Record 7000024;
                                                              Suffixes@1100003 : Form 7000073;
                                                            BEGIN
                                                              Suffixes.SETTABLEVIEW(Suffix);
                                                              Suffixes.SETRECORD(Suffix);
                                                              Suffixes.LOOKUPMODE(TRUE);
                                                              Suffixes.EDITABLE(FALSE);
                                                              IF Suffixes.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                Suffixes.GETRECORD(Suffix);
                                                                BankSuffixBankAcc := Suffix."Bank Acc. Code";
                                                                BankSuffix := Suffix.Suffix;
                                                              END;
                                                            END;
                                                             }
      { 6   ;Label        ;0    ;1320 ;3300 ;440  ;ParentControl=5 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnInit=BEGIN
               FileNameControlVisible := TRUE;
             END;

      OnOpenPage=BEGIN
                   IF ExternalFile = '' THEN
                     ExternalFile := 'C:\' + Text10702;
                   FileNameControlVisible := NOT ISSERVICETIER;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones] }

      { 1   ;2   ;Field     ;
                  Name=FileNameControl;
                  CaptionML=[ENU=External file;
                             ESP=Archivo externo];
                  SourceExpr=ExternalFile;
                  Visible=FileNameControlVisible;
                  OnAssistEdit=BEGIN
                                 ExternalFile := CommonDialogMgt.OpenFile(Text10700,ExternalFile,4,Text10701,1);
                               END;
                                }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Check Errors;
                             ESP=Comprobar errores];
                  SourceExpr=Checkerrors }

      { 5   ;2   ;Field     ;
                  CaptionML=[ENU=Bank Suffix;
                             ESP=Sufijo banco];
                  SourceExpr=BankSuffix;
                  TableRelation=Suffix.Suffix;
                  OnLookup=VAR
                             Suffix@1100002 : Record 7000024;
                             Suffixes@1100003 : Form 7000073;
                           BEGIN
                             Suffixes.SETTABLEVIEW(Suffix);
                             Suffixes.SETRECORD(Suffix);
                             Suffixes.LOOKUPMODE(TRUE);
                             Suffixes.EDITABLE(FALSE);
                             IF Suffixes.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               Suffixes.GETRECORD(Suffix);
                               BankSuffixBankAcc := Suffix."Bank Acc. Code";
                               BankSuffix := Suffix.Suffix;
                             END;
                           END;
                            }

    }
  }
  CODE
  {
    VAR
      Text1100000@1000 : TextConst 'ENU=Bank Suffix cannot be blank.;ESP=Sufijo bancario no puede ser blanco.';
      Text1100001@1001 : TextConst 'ENU=The Bank Suffix selected belongs to a %1  different than the %2. \;ESP=El sufijo bancario seleccionado pertenece a %1 diferente de %2. \';
      Text1100002@1002 : TextConst 'ENU=Do you want to continue?.;ESP=¨Quiere continuar?';
      Text1100003@1003 : TextConst 'ENU=Process cancelled by request of user.;ESP=Proceso cancelado a peticion del usuario.';
      Text1100004@1004 : TextConst 'ENU=Some data for Bank Account %1 are missing.;ESP=Falta informaci¢n en el banco %1.';
      Text1100006@1006 : TextConst 'ENU=<Integer>;ESP=<Integer>';
      Text1100007@1007 : TextConst 'ENU=<integer>;ESP=<integer>';
      Text1100008@1008 : TextConst 'ENU=Bill;ESP=Efecto';
      Text1100009@1009 : TextConst 'ENU=DEVOL.;ESP=DEVOL.';
      CompanyInfo@1100000 : Record 79;
      Customer@1100001 : Record 18;
      CustLedgEntry@1100002 : Record 21;
      ShipToAddress@1100003 : Record 222;
      BankAcc@1100004 : Record 270;
      CustBankAcc@1100005 : Record 287;
      PaymentMethod@1100006 : Record 289;
      GLSetup@1100007 : Record 98;
      PostalCode@1100008 : Record 225;
      DocMisc@1100009 : Codeunit 7000007;
      OutFile@1100010 : File;
      ExternalFile@1100011 : Text[1024];
      Posted@1100012 : Boolean;
      WithExpenses@1100013 : Boolean;
      PrintedDocs@1100014 : Boolean;
      Checkerrors@1100015 : Boolean;
      NotDom@1100016 : Boolean;
      VATRegNo@1100017 : Text[9];
      CCCBankNo@1100018 : Text[4];
      CustCCCBankCode@1100019 : Text[4];
      CCCBankBranchNo@1100020 : Text[4];
      CustCCCBankBranchNo@1100021 : Text[4];
      CCCControlDigits@1100022 : Text[2];
      CustCCCControlDigits@1100023 : Text[2];
      CCCAccNo@1100024 : Text[10];
      CustCCCAccNo@1100025 : Text[10];
      BillGrNo@1100026 : Code[4];
      DocNo@1100027 : Code[15];
      DocType@1100028 : Code[1];
      AdditionalInfo@1100029 : Text[30];
      AdditionalInfo2@1100030 : Text[30];
      CustAddress@1100031 : ARRAY [4] OF Text[50];
      TotalPosted@1100032 : Decimal;
      TotalDoc@1100033 : Decimal;
      TotalDocsCust@1100034 : Decimal;
      TotalPrintedCust@1100035 : Decimal;
      TotalPrinted@1100036 : Decimal;
      TotalAmount@1100037 : Decimal;
      Expenses@1100038 : Code[1];
      IsAccepted@1100039 : Code[1];
      PrintedDocs2@1100040 : Code[1];
      OutText@1100041 : Text[162];
      BillGrNo2@1100042 : Code[15];
      RegisterCode@1100043 : Integer;
      RegisterCode2@1100044 : Integer;
      RegisterString@1100045 : Text[2];
      DocAmount@1100046 : Text[10];
      BillGrAmount@1100047 : Text[10];
      CustNoNIF@1100048 : Text[20];
      IsEuro@1100049 : Boolean;
      LCY@1100050 : 'Euro,Other';
      BankSuffix@1100051 : Code[3];
      BankSuffixBankAcc@1100052 : Code[20];
      Suffix@1100053 : Record 7000024;
      CustPmtAddress@1100100 : Record 7000014;
      Text1100011@1100101 : TextConst 'ENU=The payment address does not exist;ESP=No existe la direcci¢n de pago';
      RBMgt@1100054 : Codeunit 419;
      ToFile@1100055 : Text[1024];
      CommonDialogMgt@1100056 : Codeunit 412;
      Text10700@1100059 : TextConst 'ENU=Export to file;ESP=Exportar a archivo';
      Text10701@1100058 : TextConst 'ENU=ASC Files (*.asc)|*.asc|All Files (*.*)|*.*;ESP=Archivos ASC (*.asc)|*.asc|Todos los archivos (*.*)|*.*';
      Text10702@1100057 : TextConst 'ENU=EFECTOS.ASC;ESP=EFECTOS.ASC';
      FileNameControlVisible@19024830 : Boolean INDATASET;

    PROCEDURE DLDACurrency@2();
    BEGIN
      IF STRLEN(FORMAT(RegisterCode)) = 1 THEN
        RegisterString := '0' + FORMAT(RegisterCode2)
      ELSE
        RegisterString := FORMAT(RegisterCode2);
    END;

    PROCEDURE EuroAmount@3(Amount@1100000 : Decimal) : Text[10];
    VAR
      TextAmount@1100001 : Text[15];
    BEGIN
      TextAmount := CONVERTSTR(FORMAT(Amount),' ','0');
      IF STRPOS(TextAmount,',') = 0 THEN
        TextAmount := TextAmount + '00'
      ELSE BEGIN
        IF STRLEN(COPYSTR(TextAmount,STRPOS(TextAmount,','),STRLEN(TextAmount))) = 2 THEN
          TextAmount := TextAmount + '0';
        TextAmount := DELCHR(TextAmount,'=',',');
      END;
      IF STRPOS(TextAmount,'.') = 0 THEN
        TextAmount := TextAmount
      ELSE
        TextAmount := DELCHR(TextAmount,'=','.');

      WHILE STRLEN(TextAmount) < 10 DO
        TextAmount := '0' + TextAmount;

      EXIT(TextAmount);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
