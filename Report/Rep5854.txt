OBJECT Report 5854 Roll Up Standard Cost
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Roll Up Standard Cost;
               ESP=Distribuir coste est ndar];
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  StdCostWkshName@1000 : Record 5840;
                BEGIN
                  RolledUp := FALSE;

                  IF ToStdCostWkshName = '' THEN
                    ERROR(STRSUBSTNO(Text003));
                  StdCostWkshName.GET(ToStdCostWkshName);
                END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table27;
        DataItemTableView=SORTING(No.);
        OnPreDataItem=BEGIN
                        StdCostWksh.LOCKTABLE;
                        CLEAR(CalcStdCost);
                        CalcStdCost.SetProperties(CalculationDate,TRUE,FALSE,FALSE,ToStdCostWkshName,TRUE);
                        CalcStdCost.CalcItems(Item,TempItem);

                        TempItem.SETRANGE("Replenishment System",TempItem."Replenishment System"::"Prod. Order");
                        IF TempItem.FIND('-') THEN
                          REPEAT
                            UpdateStdCostWksh;
                            RolledUp := TRUE;
                          UNTIL TempItem.NEXT = 0;
                      END;

        OnPostDataItem=BEGIN
                         IF NOT NoMessage THEN
                           IF RolledUp THEN
                             MESSAGE(Text000)
                           ELSE
                             MESSAGE(Text001);
                       END;

        ReqFilterFields=No.,Costing Method;
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=5060;
      Height=440;
      SaveValues=Yes;
      OnOpenForm=BEGIN
                   IF CalculationDate = 0D THEN
                     CalculationDate := WORKDATE;
                 END;

    }
    CONTROLS
    {
      { 15  ;TextBox      ;3410 ;0    ;1650 ;440  ;CaptionML=[ENU=Calculation Date;
                                                              ESP=Fecha c lculo];
                                                   SourceExpr=CalculationDate;
                                                   OnValidate=BEGIN
                                                                IF CalculationDate = 0D THEN
                                                                  ERROR(Text002);
                                                              END;
                                                               }
      { 16  ;Label        ;0    ;0    ;3300 ;440  ;ParentControl=15 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF CalculationDate = 0D THEN
                     CalculationDate := WORKDATE;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones] }

      { 15  ;2   ;Field     ;
                  CaptionML=[ENU=Calculation Date;
                             ESP=Fecha c lculo];
                  SourceExpr=CalculationDate;
                  OnValidate=BEGIN
                               IF CalculationDate = 0D THEN
                                 ERROR(Text002);
                             END;
                              }

    }
  }
  CODE
  {
    VAR
      TempItem@1003 : TEMPORARY Record 27;
      StdCostWksh@1004 : Record 5841;
      CalcStdCost@1000 : Codeunit 5812;
      CalculationDate@1001 : Date;
      ToStdCostWkshName@1002 : Code[10];
      RolledUp@1006 : Boolean;
      Text000@1007 : TextConst 'ENU=The standard costs have been rolled up successfully.;ESP=Los costes estandar han sido distribuidos satisfactoriamente.';
      Text001@1008 : TextConst 'ENU=There is nothing to roll up.;ESP=No hay nada que distribuir.';
      Text002@1009 : TextConst 'ENU=You must enter a calculation date.;ESP=Debe introducir una fecha de c lculo.';
      Text003@1010 : TextConst 'ENU=You must specify a worksheet name to roll up to.;ESP=Debe especif. un nombre hoja trab. para distrib.';
      NoMessage@1011 : Boolean;

    PROCEDURE UpdateStdCostWksh@3();
    VAR
      Found@1000 : Boolean;
    BEGIN
      WITH StdCostWksh DO BEGIN
        Found := GET(ToStdCostWkshName,Type::Item,TempItem."No.");
        VALIDATE("Standard Cost Worksheet Name",ToStdCostWkshName);
        VALIDATE(Type,StdCostWksh.Type::Item);
        VALIDATE("No.",TempItem."No.");
        "New Standard Cost" := TempItem."Standard Cost";

        "New Single-Lvl Material Cost" := TempItem."Single-Level Material Cost";
        "New Single-Lvl Cap. Cost" := TempItem."Single-Level Capacity Cost";
        "New Single-Lvl Subcontrd Cost" := TempItem."Single-Level Subcontrd. Cost";
        "New Single-Lvl Cap. Ovhd Cost" := TempItem."Single-Level Cap. Ovhd Cost";
        "New Single-Lvl Mfg. Ovhd Cost" := TempItem."Single-Level Mfg. Ovhd Cost";

        "New Rolled-up Material Cost" := TempItem."Rolled-up Material Cost";
        "New Rolled-up Cap. Cost" := TempItem."Rolled-up Capacity Cost";
        "New Rolled-up Subcontrd Cost" := TempItem."Rolled-up Subcontracted Cost";
        "New Rolled-up Cap. Ovhd Cost" := TempItem."Rolled-up Cap. Overhead Cost";
        "New Rolled-up Mfg. Ovhd Cost" := TempItem."Rolled-up Mfg. Ovhd Cost";

        IF Found THEN
          MODIFY(TRUE)
        ELSE
          INSERT(TRUE);
      END;
    END;

    PROCEDURE SetStdCostWksh@1(NewStdCostWkshName@1000 : Code[10]);
    BEGIN
      ToStdCostWkshName := NewStdCostWkshName;
    END;

    PROCEDURE Initialize@2(StdCostWkshName2@1000 : Code[10];NoMessage2@1001 : Boolean);
    BEGIN
      ToStdCostWkshName := StdCostWkshName2;
      NoMessage := NoMessage2;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
