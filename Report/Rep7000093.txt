OBJECT Report 7000093 Bill group - Export N32
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVES6.00.01;
  }
  PROPERTIES
  {
    Permissions=TableData 7000005=m;
    CaptionML=[ENU=Bill group - Export N32;
               ESP=Remesa - Exportar N32];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   CompanyInfo.GET();
                   VATRegNo := COPYSTR(DELCHR(CompanyInfo."VAT Registration No.",'=',' .-/'),1,9);
                 END;

    OnPreReport=BEGIN
                  IF WithExpenses THEN
                    Expenses := '1'
                  ELSE
                    Expenses := '0';

                  IF PrintedDocs THEN
                    PrintedDocs2 := '0'
                  ELSE
                    PrintedDocs2 := '1';

                  // CLEAR(FILE);
                  OutFile.TEXTMODE := TRUE;
                  OutFile.WRITEMODE := TRUE;
                  IF ISSERVICETIER THEN BEGIN
                    ExternalFile := RBMgt.EnvironFileName('','');
                    ToFile := Text10702;
                  END;
                  OutFile.CREATE(ExternalFile);

                  TotalAmount := 0;
                  DiskTotalPosted := 0;
                  DiskTotalDocs := 0;
                END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table7000005;
        DataItemTableView=SORTING(No.)
                          WHERE(Factoring=CONST(" "));
        OnPreDataItem=BEGIN
                        CompanyInfo.GET;

                        "Bill Group".FIND('-');

                        IF BankSuffix = '' THEN
                          ERROR(Text1100000);

                        IF BankSuffixBankAcc = '' THEN BEGIN
                          Suffix.SETRANGE (Suffix,BankSuffix);
                          IF Suffix.FIND ('-') THEN
                            BankSuffixBankAcc := Suffix."Bank Acc. Code";
                        END;

                        IsEuro := DocMisc.GetRegisterCode("Bill Group"."Currency Code",TotalBillGr,RegisterString);

                        IF NOT DocMisc.CheckBankSuffix(BankSuffixBankAcc,"Bill Group"."Bank Account No.") THEN
                          IF NOT CONFIRM(Text1100002 +
                                         Text1100003,
                                         FALSE,
                                         "Bill Group".FIELDCAPTION("Bank Account No."),
                                         "Bill Group".TABLECAPTION) THEN
                             ERROR(Text1100004);

                        TotalBillGr := "Bill Group".COUNT;

                        IF STRLEN("Bill Group"."No.") <= 4 THEN
                          DiskNo := PADSTR("Bill Group"."No.",4,'0')
                        ELSE
                          DiskNo := COPYSTR("Bill Group"."No.",STRLEN("Bill Group"."No.")-3,4);

                        BankAcc.GET("Bill Group"."Bank Account No.");
                        CCCBankNo := BankAcc."CCC Bank No.";
                        CCCBankBranchNo := BankAcc."CCC Bank Branch No.";
                        CCCControlDigits := BankAcc."CCC Control Digits";
                        CCCAccNo := BankAcc."CCC Bank Account No.";

                        IF (CCCBankNo = '') OR (CCCBankBranchNo = '') OR
                           (CCCControlDigits = '') OR (CCCAccNo = '')
                        THEN
                          ERROR(Text1100005,BankAcc."No.");

                        CLEAR(OutText);
                        OutText :=
                          '02' + RegisterString + '  ' + FORMAT(WORKDATE,6,5) + PADSTR(CONVERTSTR(DiskNo,' ','0'),4,'0') +
                          PADSTR('',35,' ') + PADSTR(CONVERTSTR(CCCBankNo,' ','0'),4,'0') +
                          PADSTR(CONVERTSTR(CCCBankBranchNo,' ','0'),4,'0');
                        OutFile.WRITE(OutText);
                      END;

        OnAfterGetRecord=BEGIN
                           IF STRLEN("No.") <= 4 THEN
                             BillGrNo := PADSTR("No.",4,'0')
                           ELSE
                             BillGrNo := COPYSTR("No.",STRLEN("Bill Group"."No.")-3,4);

                           CLEAR(OutText);
                           OutText :=
                             '11' + RegisterString + '  ' + FORMAT(WORKDATE,6,5) + PADSTR(CONVERTSTR(BillGrNo,' ','0'),4,'0') +
                             PADSTR('',12,' ') + PADSTR(VATRegNo,9,' ') + PADSTR(BankSuffix,6,' ') + PrintedDocs2 + PADSTR('',21,' ') +
                             CONVERTSTR(CCCBankNo,' ','0') +CONVERTSTR(CCCBankBranchNo,' ','0') +
                             CONVERTSTR(CCCControlDigits,' ','0') + CONVERTSTR(CCCAccNo,' ','0') +
                             CONVERTSTR(CCCBankNo,' ','0') +CONVERTSTR(CCCBankBranchNo,' ','0') +
                             CONVERTSTR(CCCControlDigits,' ','0') + CONVERTSTR(CCCAccNo,' ','0') +
                             CONVERTSTR(CCCBankNo,' ','0') + CONVERTSTR(CCCBankBranchNo,' ','0') +
                             CONVERTSTR(CCCControlDigits,' ','0') + CONVERTSTR(CCCAccNo,' ','0');
                           OutFile.WRITE(OutText);
                         END;

        OnPostDataItem=BEGIN
                         "No. Printed" := "No. Printed" + 1;
                         CLEAR(OutText);

                         IF IsEuro THEN
                           OutText :=
                             '98' + RegisterString + '  ' + PADSTR('',69,' ') + EuroAmount(TotalAmount) +
                             PADSTR('',41,' ') + CONVERTSTR(FORMAT(TotalBillGr,5,Text1100006),' ','0') +
                             CONVERTSTR(FORMAT(DiskTotalPosted + 2,7,Text1100006),' ','0') +
                             CONVERTSTR(FORMAT(DiskTotalDocs,6,Text1100006),' ','0')
                         ELSE
                           OutText :=
                             '98' + RegisterString + '  ' + PADSTR('',69,' ') + CONVERTSTR(FORMAT(TotalAmount,10,Text1100006),' ','0') +
                             PADSTR('',41,' ') + CONVERTSTR(FORMAT(TotalBillGr,5,Text1100006),' ','0') +
                             CONVERTSTR(FORMAT(DiskTotalPosted + 2,7,Text1100006),' ','0') +
                             CONVERTSTR(FORMAT(DiskTotalDocs,6,Text1100006),' ','0');

                         OutFile.WRITE(OutText);
                         OutFile.CLOSE();
                         IF ISSERVICETIER THEN
                           DOWNLOAD(ExternalFile,'','C:',Text10701,ToFile);
                       END;

        ReqFilterFields=No.;
        TotalFields=Amount (LCY);
      }
      SECTIONS
      {
      }
       }
    { PROPERTIES
      {
        DataItemIndent=1;
        DataItemTable=Table7000002;
        DataItemTableView=SORTING(Type,Collection Agent,Bill Gr./Pmt. Order No.)
                          WHERE(Collection Agent=CONST(Bank),
                                Type=CONST(Receivable));
        DataItemVarName=Doc;
        OnPreDataItem=BEGIN
                        TotalDocs := 0;
                        TotalPosted := 0;
                      END;

        OnAfterGetRecord=BEGIN
                           DocNo := COPYSTR(Doc."Document No.",1,10) + '/' + Doc."No.";

                           DocType := DocMisc.DocType(Doc."Payment Method Code");

                           CustLedgEntry.GET(Doc."Entry No.");

                           Customer.GET(CustLedgEntry."Customer No.");
                           IF NOT CustBankAcc.GET(CustLedgEntry."Customer No.",Doc."Cust./Vendor Bank Acc. Code") THEN
                             ERROR(Text1100007);

                           Customer2 := CustBankAcc."CCC Bank No.";
                           CustCCCBankBranchNo := CustBankAcc."CCC Bank Branch No.";
                           CustCCCControlDigits := CustBankAcc."CCC Control Digits";
                           CustCCCAccNo := CustBankAcc."CCC Bank Account No.";

                           IF (Customer2 = '') OR (CustCCCBankBranchNo = '') OR
                              (CustCCCControlDigits = '') OR (CustCCCAccNo = '')
                           THEN
                             ERROR(Text1100008,CustBankAcc."Customer No.");

                           CustAddress[1] := Customer.Name;
                           CustAddress[2] := Customer.Address;
                           CustAddress[3] := Customer.City;
                           CustAddress[4] := Customer."Post Code";
                           IF Doc."Pmt. Address Code" <> '' THEN BEGIN
                             IF CustPmtAddress.GET(Doc."Account No.",Doc."Pmt. Address Code") THEN BEGIN
                               CustAddress[2] := CustPmtAddress.Address;
                               CustAddress[3] := CustPmtAddress.City;
                               CustAddress[4] := CustPmtAddress."Post Code";
                             END
                             ELSE
                               ERROR(Text1100012);
                           END;

                           AdditionalInfo := Text1100009 + Doc."Document No." + '/' + Doc."No.";

                           IF Accepted = Accepted::Yes THEN
                             IsAccepted := '1'
                           ELSE
                             IsAccepted := '2';

                           TotalDocs := TotalDocs + 1;
                           TotalPosted := TotalPosted + 3;

                           CASE TRUE OF
                             LCY = LCY::Euro:
                               IF IsEuro THEN BEGIN
                                 DocAmount := DELSTR(EuroAmount("Remaining Amt. (LCY)"),1,1);
                                 TotalAmount := TotalAmount + Doc."Remaining Amt. (LCY)";
                               END ELSE BEGIN
                                 DocAmount := CONVERTSTR(FORMAT(Doc."Remaining Amount",9,Text1100010),' ','0');
                                 TotalAmount := TotalAmount + Doc."Remaining Amount";
                               END;
                             LCY = LCY::Other:
                               BEGIN
                                 IF IsEuro THEN
                                   DocAmount := DELSTR(EuroAmount("Remaining Amount"),1,1)
                                 ELSE
                                   DocAmount := CONVERTSTR(FORMAT(Doc."Remaining Amount",9,Text1100010),' ','0');
                                 TotalAmount := TotalAmount + Doc."Remaining Amount";
                               END;
                           END;

                           CLEAR(OutText);
                           OutText :=
                             '25' + RegisterString + '  ' + PADSTR(DocNo,15,' ') + FORMAT(WORKDATE,6,5) + PADSTR(BillGrNo,4,'0') +
                             PADSTR(COPYSTR(CompanyInfo."Post Code",1,2),2,'0') + PADSTR('',9,' ') + PADSTR(CompanyInfo.City,20,' ') +
                             PADSTR('',25,' ') + DocAmount +
                             PADSTR('',15,' ') +  FORMAT(Doc."Due Date",6,5);
                           OutFile.WRITE(OutText);

                           CLEAR(OutText);
                           OutText :=
                             '26' + RegisterString + '  ' + PADSTR(DocNo,15,' ') + PADSTR('',2,' ') + DocType + FORMAT("Bill Group"."Posting Date",6,5) +
                             IsAccepted + Expenses + Customer2 + CustCCCBankBranchNo + CustCCCControlDigits + CustCCCAccNo +
                             PADSTR(CompanyInfo.Name,34,' ') + PADSTR(CustAddress[1],34,' ') + PADSTR(AdditionalInfo,3,' ');
                           OutFile.WRITE(OutText);

                           CLEAR(OutText);
                           OutText :=
                            '27' + RegisterString + '  ' + PADSTR(DocNo,15,' ') + PADSTR('',2,' ') +  PADSTR(CustAddress[2],34,' ') +
                            PADSTR(CustAddress[4],5,' ') +  PADSTR(CustAddress[3],20,' ') +  PADSTR(CustAddress[4],2,' ');
                           OutFile.WRITE(OutText);
                         END;

        OnPostDataItem=BEGIN
                         CASE TRUE OF
                           LCY = LCY::Euro:
                             IF IsEuro THEN BEGIN
                               "Bill Group".CALCFIELDS("Bill Group"."Amount (LCY)");
                               BillGrAmount := EuroAmount("Bill Group"."Amount (LCY)")
                             END ELSE BEGIN
                               "Bill Group".CALCFIELDS("Bill Group".Amount);
                               BillGrAmount := CONVERTSTR(FORMAT("Bill Group".Amount,10,Text1100006),' ','0')
                             END;
                           LCY = LCY::Other:
                             IF IsEuro THEN BEGIN
                               "Bill Group".CALCFIELDS(Amount);
                               BillGrAmount := EuroAmount("Bill Group".Amount)
                             END ELSE BEGIN
                               "Bill Group".CALCFIELDS("Bill Group".Amount);
                               BillGrAmount := CONVERTSTR(FORMAT("Bill Group".Amount,10,Text1100006),' ','0')
                             END;
                         END;


                         TotalPosted := TotalPosted + 2;
                         CLEAR(OutText);
                         OutText :=
                           '71' + RegisterString + '  ' + FORMAT(WORKDATE,6,5) + PADSTR(BillGrNo,4,'0') + PADSTR('',59,' ') +
                           BillGrAmount + PADSTR('',46,' ') +
                           CONVERTSTR(FORMAT(TotalPosted,7,Text1100006),' ','0') +
                           CONVERTSTR(FORMAT(TotalDocs,6,Text1100006),' ','0') ;
                         OutFile.WRITE(OutText);

                         DiskTotalPosted := DiskTotalPosted + TotalPosted;
                         DiskTotalDocs := DiskTotalDocs + TotalDocs;
                       END;

        TotalFields=Remaining Amt. (LCY);
        DataItemLink=Bill Gr./Pmt. Order No.=FIELD(No.);
      }
      SECTIONS
      {
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=8910;
      Height=2420;
      SaveValues=Yes;
      OnOpenForm=BEGIN
                   IF ExternalFile = '' THEN
                     ExternalFile := 'C:\' + Text10702;
                   RequestOptionsForm.FileNameControl.VISIBLE := NOT ISSERVICETIER;
                 END;

    }
    CONTROLS
    {
      { 1   ;TextBox      ;3410 ;0    ;5500 ;440  ;Name=FileNameControl;
                                                   CaptionML=[ENU=External file;
                                                              ESP=Archivo externo];
                                                   SourceExpr=ExternalFile;
                                                   OnAssistEdit=BEGIN
                                                                  ExternalFile := CommonDialogMgt.OpenFile(Text10700,ExternalFile,4,Text10701,1);
                                                                END;
                                                                 }
      { 2   ;Label        ;0    ;0    ;3300 ;440  ;ParentControl=1 }
      { 7   ;CheckBox     ;3410 ;660  ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=With Expenses;
                                                              ESP=Con gastos];
                                                   SourceExpr=WithExpenses }
      { 8   ;Label        ;0    ;660  ;3300 ;440  ;ParentControl=7 }
      { 9   ;CheckBox     ;3410 ;1320 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Printed Bills Attached;
                                                              ESP=Efectos impr. adjuntos];
                                                   SourceExpr=PrintedDocs }
      { 10  ;Label        ;0    ;1320 ;3300 ;440  ;ParentControl=9 }
      { 3   ;TextBox      ;3410 ;1980 ;1650 ;440  ;CaptionML=[ENU=Bank Suffix;
                                                              ESP=Sufijo banco];
                                                   SourceExpr=BankSuffix;
                                                   TableRelation=Suffix.Suffix;
                                                   OnLookup=VAR
                                                              Suffix@1100002 : Record 7000024;
                                                              Suffixes@1100003 : Form 7000073;
                                                            BEGIN
                                                              Suffixes.SETTABLEVIEW(Suffix);
                                                              Suffixes.SETRECORD(Suffix);
                                                              Suffixes.LOOKUPMODE(TRUE);
                                                              Suffixes.EDITABLE(FALSE);
                                                              IF Suffixes.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                Suffixes.GETRECORD(Suffix);
                                                                BankSuffixBankAcc := Suffix."Bank Acc. Code";
                                                                BankSuffix := Suffix.Suffix;
                                                              END;
                                                            END;
                                                             }
      { 4   ;Label        ;0    ;1980 ;3300 ;440  ;ParentControl=3 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnInit=BEGIN
               FileNameControlVisible := TRUE;
             END;

      OnOpenPage=BEGIN
                   IF ExternalFile = '' THEN
                     ExternalFile := 'C:\' + Text10702;
                   FileNameControlVisible := NOT ISSERVICETIER;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones] }

      { 1   ;2   ;Field     ;
                  Name=FileNameControl;
                  CaptionML=[ENU=External file;
                             ESP=Archivo externo];
                  SourceExpr=ExternalFile;
                  Visible=FileNameControlVisible;
                  OnAssistEdit=BEGIN
                                 ExternalFile := CommonDialogMgt.OpenFile(Text10700,ExternalFile,4,Text10701,1);
                               END;
                                }

      { 7   ;2   ;Field     ;
                  CaptionML=[ENU=With Expenses;
                             ESP=Con gastos];
                  SourceExpr=WithExpenses }

      { 9   ;2   ;Field     ;
                  CaptionML=[ENU=Printed Bills Attached;
                             ESP=Efectos impr. adjuntos];
                  SourceExpr=PrintedDocs }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Bank Suffix;
                             ESP=Sufijo banco];
                  SourceExpr=BankSuffix;
                  TableRelation=Suffix.Suffix;
                  OnLookup=VAR
                             Suffix@1100002 : Record 7000024;
                             Suffixes@1100003 : Form 7000073;
                           BEGIN
                             Suffixes.SETTABLEVIEW(Suffix);
                             Suffixes.SETRECORD(Suffix);
                             Suffixes.LOOKUPMODE(TRUE);
                             Suffixes.EDITABLE(FALSE);
                             IF Suffixes.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               Suffixes.GETRECORD(Suffix);
                               BankSuffixBankAcc := Suffix."Bank Acc. Code";
                               BankSuffix := Suffix.Suffix;
                             END;
                           END;
                            }

    }
  }
  CODE
  {
    VAR
      Text1100000@1100047 : TextConst 'ENU=Bank Suffix cannot be blank.;ESP=Sufijo bancario no puede ser blanco.';
      Text1100002@1100049 : TextConst 'ENU=The Bank Suffix selected belongs to a %1  different than the %2. \;ESP=El sufijo bancario seleccionado pertenece a %1 diferente de %2. \';
      Text1100003@1100050 : TextConst 'ENU=Do you want to continue?.;ESP=¨Quiere continuar?';
      Text1100004@1100051 : TextConst 'ENU=Process cancelled by request of user.;ESP=Proceso cancelado a peticion del usuario.';
      Text1100005@1100052 : TextConst 'ENU=Some data for Bank Account %1 are missing.;ESP=Falta informaci¢n en el banco %1.';
      Text1100006@1100053 : TextConst 'ENU=<integer>;ESP=<integer>';
      Text1100007@1100054 : TextConst 'ENU=There is no bank account information for Customer %1.;ESP=No hay informaci¢n del banco del cliente %1.';
      Text1100008@1100055 : TextConst 'ENU=Some data from the Bank Account of Customer %1 are missing.;ESP=Falta alguna informaci¢n sobre el banco del cliente %1.';
      Text1100009@1100056 : TextConst 'ENU=Bill;ESP=Efecto';
      Text1100010@1100057 : TextConst 'ENU=<Integer>;ESP=<Integer>';
      CompanyInfo@1100000 : Record 79;
      Customer@1100001 : Record 18;
      CustLedgEntry@1100002 : Record 21;
      ShipToAddress@1100003 : Record 222;
      BankAcc@1100004 : Record 270;
      CustBankAcc@1100005 : Record 287;
      DocMisc@1100006 : Codeunit 7000007;
      OutFile@1100007 : File;
      ExternalFile@1100008 : Text[1024];
      Posted@1100009 : Boolean;
      WithExpenses@1100010 : Boolean;
      PrintedDocs@1100011 : Boolean;
      VATRegNo@1100012 : Text[9];
      CCCBankNo@1100013 : Text[4];
      Customer2@1100014 : Text[4];
      CCCBankBranchNo@1100015 : Text[4];
      CustCCCBankBranchNo@1100016 : Text[4];
      CCCControlDigits@1100017 : Text[2];
      CustCCCControlDigits@1100018 : Text[2];
      CCCAccNo@1100019 : Text[10];
      CustCCCAccNo@1100020 : Text[10];
      DiskNo@1100021 : Code[4];
      BillGrNo@1100022 : Code[4];
      DocNo@1100023 : Code[15];
      DocType@1100024 : Code[1];
      AdditionalInfo@1100025 : Text[30];
      CustAddress@1100026 : ARRAY [4] OF Text[50];
      TotalBillGr@1100027 : Integer;
      TotalPosted@1100028 : Integer;
      TotalDocs@1100029 : Integer;
      DiskTotalPosted@1100030 : Integer;
      DiskTotalDocs@1100031 : Integer;
      TotalAmount@1100032 : Decimal;
      DocAmount@1100033 : Text[10];
      BillGrAmount@1100034 : Text[10];
      Expenses@1100035 : Code[1];
      IsAccepted@1100036 : Code[1];
      PrintedDocs2@1100037 : Code[1];
      OutText@1100038 : Text[150];
      BillGrNo2@1100039 : Code[15];
      DocType2@1100040 : ' ,Draft,Receipt';
      RegisterString@1100041 : Text[2];
      IsEuro@1100042 : Boolean;
      LCY@1100043 : 'Euro,Other';
      BankSuffix@1100044 : Code[3];
      BankSuffixBankAcc@1100045 : Code[20];
      Suffix@1100046 : Record 7000024;
      CustPmtAddress@1100100 : Record 7000014;
      Text1100012@1100101 : TextConst 'ENU=The payment address does not exist;ESP=No existe la direcci¢n de pago';
      RBMgt@1100048 : Codeunit 419;
      ToFile@1100059 : Text[1024];
      Text10700@1100062 : TextConst 'ENU=Export to file;ESP=Exportar a archivo';
      Text10701@1100061 : TextConst 'ENU=ASC Files (*.asc)|*.asc|All Files (*.*)|*.*;ESP=Archivos ASC (*.asc)|*.asc|Todos los archivos (*.*)|*.*';
      Text10702@1100060 : TextConst 'ENU=EFECTOS.ASC;ESP=EFECTOS.ASC';
      CommonDialogMgt@1100058 : Codeunit 412;
      FileNameControlVisible@19024830 : Boolean INDATASET;

    PROCEDURE EuroAmount@3(Amount@1100000 : Decimal) : Text[10];
    VAR
      TextAmount@1100001 : Text[15];
    BEGIN
      TextAmount := CONVERTSTR(FORMAT(Amount),' ','0');
      IF STRPOS(TextAmount,',') = 0 THEN
        TextAmount := TextAmount + '00'
      ELSE BEGIN
        IF STRLEN(COPYSTR(TextAmount,STRPOS(TextAmount,','),STRLEN(TextAmount))) = 2 THEN
          TextAmount := TextAmount + '0';
        TextAmount := DELCHR(TextAmount,'=',',');
      END;

      IF STRPOS(TextAmount,'.') = 0 THEN
        TextAmount := TextAmount
      ELSE
        TextAmount := DELCHR(TextAmount,'=','.');

      WHILE STRLEN(TextAmount) < 10 DO
        TextAmount := '0' + TextAmount;

      EXIT(TextAmount);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
