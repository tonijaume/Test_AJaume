OBJECT Report 5692 Calculate Depreciation
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Calculate Depreciation;
               ESP=Calcular amortizaci¢n];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  DeprBook.GET(DeprBookCode);
                  IF DeprUntilDate = 0D THEN
                    ERROR(Text000,FAJnlLine.FIELDCAPTION("FA Posting Date"));
                  IF PostingDate = 0D THEN
                    PostingDate := DeprUntilDate;
                  IF UseForceNoOfDays AND (DaysInPeriod = 0) THEN
                    ERROR(Text001);

                  IF DeprBook."Use Same FA+G/L Posting Dates" AND (DeprUntilDate <> PostingDate) THEN
                    ERROR(
                      Text002,
                      FAJnlLine.FIELDCAPTION("FA Posting Date"),
                      FAJnlLine.FIELDCAPTION("Posting Date"),
                      DeprBook.FIELDCAPTION("Use Same FA+G/L Posting Dates"),
                      FALSE,
                      DeprBook.TABLECAPTION,
                      DeprBook.FIELDCAPTION(Code),
                      DeprBook.Code);

                  Window.OPEN(
                    Text003 +
                    Text004 +
                    Text005);
                END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table5600;
        OnAfterGetRecord=BEGIN
                           IF Inactive OR Blocked THEN
                             CurrReport.SKIP;

                           CalculateDepr.Calculate(
                             DeprAmount,Custom1Amount,NumberOfDays,Custom1NumberOfDays,
                             "No.",DeprBookCode,DeprUntilDate,EntryAmounts,0D,DaysInPeriod);
                           IF (DeprAmount <> 0) OR (Custom1Amount <> 0) THEN
                             Window.UPDATE(1,"No.")
                           ELSE
                             Window.UPDATE(2,"No.");

                           IF Custom1Amount <> 0 THEN
                             IF NOT DeprBook."G/L Integration - Custom 1" OR "Budgeted Asset" THEN BEGIN
                               FAJnlLineTmp."FA No." := "No.";
                               FAJnlLineTmp."FA Posting Type" := FAJnlLineTmp."FA Posting Type"::"Custom 1";
                               FAJnlLineTmp.Amount := Custom1Amount;
                               FAJnlLineTmp."No. of Depreciation Days" := Custom1NumberOfDays;
                               FAJnlLineTmp."FA Error Entry No." := Custom1ErrorNo;
                               FAJnlLineTmp."Line No." := FAJnlLineTmp."Line No." + 1;
                               FAJnlLineTmp.INSERT;
                             END ELSE BEGIN
                               GenJnlLineTmp."Account No." := "No.";
                               GenJnlLineTmp."FA Posting Type" := GenJnlLineTmp."FA Posting Type"::"Custom 1";
                               GenJnlLineTmp.Amount := Custom1Amount;
                               GenJnlLineTmp."No. of Depreciation Days" := Custom1NumberOfDays;
                               GenJnlLineTmp."FA Error Entry No." := Custom1ErrorNo;
                               GenJnlLineTmp."Line No." := GenJnlLineTmp."Line No." + 1;
                               GenJnlLineTmp.INSERT;
                             END;

                           IF DeprAmount <> 0 THEN
                             IF NOT DeprBook."G/L Integration - Depreciation" OR "Budgeted Asset" THEN BEGIN
                               FAJnlLineTmp."FA No." := "No.";
                               FAJnlLineTmp."FA Posting Type" := FAJnlLineTmp."FA Posting Type"::Depreciation;
                               FAJnlLineTmp.Amount := DeprAmount;
                               FAJnlLineTmp."No. of Depreciation Days" := NumberOfDays;
                               FAJnlLineTmp."FA Error Entry No." := ErrorNo;
                               FAJnlLineTmp."Line No." := FAJnlLineTmp."Line No." + 1;
                               FAJnlLineTmp.INSERT;
                             END ELSE BEGIN
                               GenJnlLineTmp."Account No." := "No.";
                               GenJnlLineTmp."FA Posting Type" := GenJnlLineTmp."FA Posting Type"::Depreciation;
                               GenJnlLineTmp.Amount := DeprAmount;
                               GenJnlLineTmp."No. of Depreciation Days" := NumberOfDays;
                               GenJnlLineTmp."FA Error Entry No." := ErrorNo;
                               GenJnlLineTmp."Line No." := GenJnlLineTmp."Line No." + 1;
                               GenJnlLineTmp.INSERT;
                             END;
                         END;

        OnPostDataItem=BEGIN
                         WITH FAJnlLine DO BEGIN
                           IF FAJnlLineTmp.FIND('-') THEN BEGIN
                             LOCKTABLE;
                             FAJnlSetup.FAJnlName(DeprBook,FAJnlLine,FAJnlNextLineNo);
                             NoSeries := FAJnlSetup.GetFANoSeries(FAJnlLine);
                             IF DocumentNo = '' THEN
                               DocumentNo2 := FAJnlSetup.GetFAJnlDocumentNo(FAJnlLine,DeprUntilDate,TRUE)
                             ELSE
                               DocumentNo2 := DocumentNo;
                           END;
                           IF FAJnlLineTmp.FIND('-') THEN
                             REPEAT
                               INIT;
                               "Line No." := 0;
                               FAJnlSetup.SetFAJnlTrailCodes(FAJnlLine);
                               LineNo := LineNo + 1;
                               Window.UPDATE(3,LineNo);
                               "Posting Date" := PostingDate;
                               "FA Posting Date" := DeprUntilDate;
                               IF "Posting Date" = "FA Posting Date" THEN
                                 "Posting Date" := 0D;
                               "FA Posting Type" := FAJnlLineTmp."FA Posting Type";
                               VALIDATE("FA No.",FAJnlLineTmp."FA No.");
                               "Document No." := DocumentNo2;
                               "Posting No. Series" := NoSeries;
                               Description := PostingDescription;
                               VALIDATE("Depreciation Book Code",DeprBookCode);
                               VALIDATE(Amount,FAJnlLineTmp.Amount);
                               "No. of Depreciation Days" := FAJnlLineTmp."No. of Depreciation Days";
                               "FA Error Entry No." := FAJnlLineTmp."FA Error Entry No.";
                               FAJnlNextLineNo := FAJnlNextLineNo + 10000;
                               "Line No." := FAJnlNextLineNo;
                               INSERT(TRUE);
                             UNTIL FAJnlLineTmp.NEXT = 0;
                         END;

                         WITH GenJnlLine DO BEGIN
                           IF GenJnlLineTmp.FIND('-') THEN BEGIN
                             LOCKTABLE;
                             FAJnlSetup.GenJnlName(DeprBook,GenJnlLine,GenJnlNextLineNo);
                             NoSeries := FAJnlSetup.GetGenNoSeries(GenJnlLine);
                             IF DocumentNo = '' THEN
                               DocumentNo2 := FAJnlSetup.GetGenJnlDocumentNo(GenJnlLine,DeprUntilDate,TRUE)
                             ELSE
                               DocumentNo2 := DocumentNo;

                           END;
                           IF GenJnlLineTmp.FIND('-') THEN
                             REPEAT
                               INIT;
                               "Line No." := 0;
                               FAJnlSetup.SetGenJnlTrailCodes(GenJnlLine);
                               LineNo := LineNo + 1;
                               Window.UPDATE(3,LineNo);
                               "Posting Date" := PostingDate;
                               "FA Posting Date" := DeprUntilDate;
                               IF "Posting Date" = "FA Posting Date" THEN
                                 "FA Posting Date" := 0D;
                               "FA Posting Type" := GenJnlLineTmp."FA Posting Type";
                               "Account Type" := "Account Type"::"Fixed Asset";
                               VALIDATE("Account No.",GenJnlLineTmp."Account No.");
                               Description := PostingDescription;
                               "Document No." := DocumentNo2;
                               "Posting No. Series" := NoSeries;
                               VALIDATE("Depreciation Book Code",DeprBookCode);
                               VALIDATE(Amount,GenJnlLineTmp.Amount);
                               "No. of Depreciation Days" := GenJnlLineTmp."No. of Depreciation Days";
                               "FA Error Entry No." := GenJnlLineTmp."FA Error Entry No.";
                               GenJnlNextLineNo := GenJnlNextLineNo + 1000;
                               "Line No." := GenJnlNextLineNo;
                               INSERT(TRUE);
                               IF BalAccount THEN
                                 FAInsertGLAcc.GetBalAcc2(GenJnlLine,GenJnlNextLineNo);
                             UNTIL GenJnlLineTmp.NEXT = 0;
                         END;
                       END;

        ReqFilterFields=No.,FA Class Code,FA Subclass Code,Budgeted Asset;
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=8910;
      Height=4510;
      SaveValues=Yes;
      OnOpenForm=BEGIN
                   IF DeprBookCode = '' THEN BEGIN
                     FASetup.GET;
                     DeprBookCode := FASetup."Default Depr. Book";
                   END;
                 END;

    }
    CONTROLS
    {
      { 9   ;TextBox      ;3410 ;550  ;1650 ;440  ;CaptionML=[ENU=FA Posting Date;
                                                              ESP=A/F Fecha registro];
                                                   SourceExpr=DeprUntilDate }
      { 10  ;Label        ;0    ;550  ;3300 ;440  ;ParentControl=9 }
      { 11  ;TextBox      ;3410 ;2310 ;1650 ;440  ;CaptionML=[ENU=Posting Date;
                                                              ESP=Fecha registro];
                                                   SourceExpr=PostingDate }
      { 12  ;Label        ;0    ;2310 ;3300 ;440  ;ParentControl=11 }
      { 13  ;TextBox      ;3410 ;3410 ;5500 ;440  ;CaptionML=[ENU=Posting Description;
                                                              ESP=Texto de registro];
                                                   SourceExpr=PostingDescription }
      { 14  ;Label        ;0    ;3410 ;3300 ;440  ;ParentControl=13 }
      { 15  ;TextBox      ;3410 ;0    ;2750 ;440  ;CaptionML=[ENU=Depreciation Book;
                                                              ESP=Libro amortizaci¢n];
                                                   SourceExpr=DeprBookCode;
                                                   TableRelation="Depreciation Book" }
      { 16  ;Label        ;0    ;0    ;3300 ;440  ;ParentControl=15 }
      { 3   ;CheckBox     ;3410 ;4070 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Insert Bal. Account;
                                                              ESP=Insertar contrapartida];
                                                   SourceExpr=BalAccount }
      { 4   ;Label        ;0    ;4070 ;3300 ;440  ;ParentControl=3 }
      { 5   ;TextBox      ;3410 ;1650 ;1650 ;440  ;CaptionML=[ENU=Force No. of Days;
                                                              ESP=Forzar n§ de d¡as];
                                                   BlankZero=Yes;
                                                   SourceExpr=DaysInPeriod;
                                                   MinValue=0;
                                                   OnValidate=BEGIN
                                                                IF NOT UseForceNoOfDays AND (DaysInPeriod <> 0) THEN
                                                                  ERROR(Text006);
                                                              END;
                                                               }
      { 6   ;Label        ;0    ;1650 ;3300 ;440  ;ParentControl=5 }
      { 7   ;Label        ;0    ;1100 ;3300 ;440  ;ParentControl=8 }
      { 8   ;CheckBox     ;3410 ;1100 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Use Force No. of Days;
                                                              ESP=Utilizar forzar n§ de d¡as];
                                                   SourceExpr=UseForceNoOfDays;
                                                   OnValidate=BEGIN
                                                                IF NOT UseForceNoOfDays THEN
                                                                  DaysInPeriod := 0;
                                                              END;
                                                               }
      { 1   ;TextBox      ;3410 ;2860 ;2750 ;440  ;CaptionML=[ENU=Document No.;
                                                              ESP=N§ documento];
                                                   SourceExpr=DocumentNo }
      { 2   ;Label        ;0    ;2860 ;3300 ;440  ;ParentControl=1 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF DeprBookCode = '' THEN BEGIN
                     FASetup.GET;
                     DeprBookCode := FASetup."Default Depr. Book";
                   END;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones] }

      { 15  ;2   ;Field     ;
                  CaptionML=[ENU=Depreciation Book;
                             ESP=Libro amortizaci¢n];
                  SourceExpr=DeprBookCode;
                  TableRelation="Depreciation Book" }

      { 9   ;2   ;Field     ;
                  CaptionML=[ENU=FA Posting Date;
                             ESP=A/F Fecha registro];
                  SourceExpr=DeprUntilDate }

      { 8   ;2   ;Field     ;
                  CaptionML=[ENU=Use Force No. of Days;
                             ESP=Utilizar forzar n§ de d¡as];
                  SourceExpr=UseForceNoOfDays;
                  OnValidate=BEGIN
                               IF NOT UseForceNoOfDays THEN
                                 DaysInPeriod := 0;
                             END;
                              }

      { 5   ;2   ;Field     ;
                  CaptionML=[ENU=Force No. of Days;
                             ESP=Forzar n§ de d¡as];
                  BlankZero=Yes;
                  SourceExpr=DaysInPeriod;
                  MinValue=0;
                  OnValidate=BEGIN
                               IF NOT UseForceNoOfDays AND (DaysInPeriod <> 0) THEN
                                 ERROR(Text006);
                             END;
                              }

      { 11  ;2   ;Field     ;
                  CaptionML=[ENU=Posting Date;
                             ESP=Fecha registro];
                  SourceExpr=PostingDate }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Document No.;
                             ESP=N§ documento];
                  SourceExpr=DocumentNo }

      { 13  ;2   ;Field     ;
                  CaptionML=[ENU=Posting Description;
                             ESP=Texto de registro];
                  SourceExpr=PostingDescription }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Insert Bal. Account;
                             ESP=Insertar contrapartida];
                  SourceExpr=BalAccount }

    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You must specify %1.;ESP=Se debe indicar %1.';
      Text001@1001 : TextConst 'ENU=Force No. of Days must be activated.;ESP=Se debe activar forzar n§ de d¡as.';
      Text002@1002 : TextConst 'ENU="%1 and %2 must be identical. %3 must be %4 in %5 %6 = %7.";ESP="%1 y %2 deben ser iguales. %3 debe ser %4 en %5 %6 = %7."';
      Text003@1003 : TextConst 'ENU=Depreciating fixed asset      #1##########\;ESP=Amortizando activo            #1##########\';
      Text004@1004 : TextConst 'ENU=Not depreciating fixed asset  #2##########\;ESP=No amortizando activo         #2##########\';
      Text005@1005 : TextConst 'ENU=Inserting journal lines       #3##########;ESP=Insertando l¡neas diario      #3##########';
      Text006@1006 : TextConst 'ENU=Use Force No. of Days must be activated.;ESP=Se debe activar utilizar forzar n§ de d¡as.';
      GenJnlLine@1007 : Record 81;
      GenJnlLineTmp@1008 : TEMPORARY Record 81;
      FASetup@1009 : Record 5603;
      FAJnlLine@1010 : Record 5621;
      FAJnlLineTmp@1011 : TEMPORARY Record 5621;
      DeprBook@1012 : Record 5611;
      FAJnlSetup@1013 : Record 5605;
      CalculateDepr@1014 : Codeunit 5610;
      FAInsertGLAcc@1015 : Codeunit 5601;
      Window@1016 : Dialog;
      DeprAmount@1017 : Decimal;
      Custom1Amount@1018 : Decimal;
      NumberOfDays@1019 : Integer;
      Custom1NumberOfDays@1020 : Integer;
      DeprUntilDate@1021 : Date;
      UseForceNoOfDays@1022 : Boolean;
      DaysInPeriod@1023 : Integer;
      PostingDate@1024 : Date;
      DocumentNo@1025 : Code[20];
      DocumentNo2@1026 : Code[20];
      NoSeries@1027 : Code[10];
      PostingDescription@1028 : Text[50];
      DeprBookCode@1029 : Code[10];
      BalAccount@1030 : Boolean;
      ErrorNo@1031 : Integer;
      Custom1ErrorNo@1032 : Integer;
      FAJnlNextLineNo@1033 : Integer;
      GenJnlNextLineNo@1034 : Integer;
      EntryAmounts@1035 : ARRAY [4] OF Decimal;
      LineNo@1036 : Integer;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
