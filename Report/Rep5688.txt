OBJECT Report 5688 Cancel FA Ledger Entries
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Cancel FA Ledger Entries;
               ESP=A/F Anular movs.];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF (EndingDate > 0D) AND (StartingDate > EndingDate) THEN
                    ERROR(Text000);
                  IF UseNewPostingDate THEN
                    IF NewPostingDate = 0D THEN
                      ERROR(Text002);
                  IF NOT UseNewPostingDate THEN
                    IF NewPostingDate > 0D THEN
                      ERROR(Text003);
                  IF NewPostingDate > 0D THEN
                    IF NORMALDATE(NewPostingDate) <> NewPostingDate THEN
                      ERROR(Text004);

                  IF EndingDate = 0D THEN
                    EndingDate2 := 31129999D
                  ELSE
                    EndingDate2 := EndingDate;
                  DeprBook.GET(DeprBookCode);
                  IF UseNewPostingDate THEN
                    DeprBook.TESTFIELD("Use Same FA+G/L Posting Dates",FALSE);
                  DeprBook.IndexGLIntegration(GLIntegration);
                  FirstGenJnl := TRUE;
                  FirstFAJnl := TRUE;
                  Window.OPEN(Text001);
                END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table5600;
        DataItemTableView=SORTING(No.);
        OnPreDataItem=BEGIN
                        DepreciationCalc.SetFAFilter(FALedgEntry,'',DeprBookCode,FALSE);
                        WITH FALedgEntry DO BEGIN
                          SETRANGE("FA Posting Category","FA Posting Category"::" ");
                          SETRANGE(
                            "FA Posting Type",
                            "FA Posting Type"::"Acquisition Cost","FA Posting Type"::"Salvage Value");
                          SETRANGE("FA Posting Date",StartingDate,EndingDate2);
                        END;
                      END;

        OnAfterGetRecord=BEGIN
                           Window.UPDATE(1,"No.");
                           IF Inactive OR Blocked THEN
                             CurrReport.SKIP;
                           IF NOT FADeprBook.GET("No.",DeprBookCode) THEN
                             CurrReport.SKIP;
                           FALedgEntry.SETRANGE("FA No.","No.");
                           IF FALedgEntry.FIND('+') THEN
                             REPEAT
                               SetJournalType(FALedgEntry);
                               IF NewPostingDate > 0D THEN
                                 FALedgEntry."Posting Date" := NewPostingDate;
                               CASE JournalType OF
                                 JournalType::SkipType:
                                   ;
                                 JournalType::GenJnlType:
                                   InsertGenJnlLine(FALedgEntry);
                                 JournalType::FAJnlType:
                                   InsertFAJnlLine(FALedgEntry);
                               END;
                             UNTIL FALedgEntry.NEXT(-1) = 0;
                         END;

        ReqFilterFields=No.,FA Class Code,FA Subclass Code;
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=10230;
      Height=6930;
      SaveValues=Yes;
      OnOpenForm=BEGIN
                   IF DeprBookCode = '' THEN BEGIN
                     FASetup.GET;
                     DeprBookCode := FASetup."Default Depr. Book";
                   END;
                 END;

    }
    CONTROLS
    {
      { 13  ;TextBox      ;3410 ;3410 ;5500 ;440  ;CaptionML=[ENU=Posting Description;
                                                              ESP=Texto de registro];
                                                   SourceExpr=PostingDescription }
      { 14  ;Label        ;0    ;3410 ;3300 ;440  ;ParentControl=13 }
      { 3   ;CheckBox     ;3410 ;4070 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Insert Bal. Account;
                                                              ESP=Insertar contrapartida];
                                                   SourceExpr=BalAccount }
      { 4   ;Label        ;0    ;4070 ;3300 ;440  ;ParentControl=3 }
      { 1   ;TextBox      ;3410 ;0    ;2750 ;440  ;CaptionML=[ENU=Cancel Book;
                                                              ESP=Anular libro];
                                                   SourceExpr=DeprBookCode;
                                                   TableRelation="Depreciation Book" }
      { 2   ;Label        ;0    ;0    ;3300 ;440  ;ParentControl=1 }
      { 7   ;Label        ;0    ;4730 ;3300 ;440  ;ParentControl=8;
                                                   CaptionML=[ENU=Cancel;
                                                              ESP=Cancelar] }
      { 8   ;CheckBox     ;3410 ;4730 ;3300 ;440  ;ShowCaption=Yes;
                                                   CaptionML=[ENU=Acquisition Cost;
                                                              ESP=Coste];
                                                   SourceExpr=CancelChoices[1] }
      { 9   ;CheckBox     ;3410 ;5280 ;3300 ;440  ;ShowCaption=Yes;
                                                   CaptionML=[ENU=Depreciation;
                                                              ESP=Amortizaci¢n];
                                                   SourceExpr=CancelChoices[2] }
      { 10  ;CheckBox     ;3410 ;5940 ;3300 ;440  ;ShowCaption=Yes;
                                                   CaptionML=[ENU=Write-Down;
                                                              ESP=Depreciaci¢n];
                                                   SourceExpr=CancelChoices[3] }
      { 11  ;CheckBox     ;3410 ;6490 ;3300 ;440  ;ShowCaption=Yes;
                                                   CaptionML=[ENU=Appreciation;
                                                              ESP=Apreciaci¢n];
                                                   SourceExpr=CancelChoices[4] }
      { 12  ;CheckBox     ;6930 ;4730 ;3300 ;440  ;ShowCaption=Yes;
                                                   CaptionML=[ENU=Custom 1;
                                                              ESP=Especial];
                                                   SourceExpr=CancelChoices[5] }
      { 17  ;CheckBox     ;6930 ;5280 ;3300 ;440  ;ShowCaption=Yes;
                                                   CaptionML=[ENU=Custom 2;
                                                              ESP=Provisi¢n];
                                                   SourceExpr=CancelChoices[6] }
      { 18  ;CheckBox     ;6930 ;5940 ;3300 ;440  ;ShowCaption=Yes;
                                                   CaptionML=[ENU=Salvage Value;
                                                              ESP=Valor residual];
                                                   SourceExpr=CancelChoices[9] }
      { 5   ;CheckBox     ;6930 ;6490 ;3300 ;440  ;ShowCaption=Yes;
                                                   CaptionML=[ENU=Disposal;
                                                              ESP=Venta/Baja];
                                                   SourceExpr=CancelChoices[7] }
      { 6   ;TextBox      ;3410 ;1210 ;1650 ;440  ;CaptionML=[ENU=Ending Date;
                                                              ESP=Fecha final];
                                                   SourceExpr=EndingDate }
      { 20  ;TextBox      ;3410 ;660  ;1650 ;440  ;CaptionML=[ENU=Starting Date;
                                                              ESP=Fecha inicial];
                                                   SourceExpr=StartingDate }
      { 21  ;Label        ;0    ;1210 ;3300 ;440  ;ParentControl=6 }
      { 22  ;Label        ;0    ;660  ;3300 ;440  ;ParentControl=20 }
      { 15  ;Label        ;0    ;2860 ;3300 ;440  ;ParentControl=16 }
      { 16  ;TextBox      ;3410 ;2860 ;2750 ;440  ;CaptionML=[ENU=Document No.;
                                                              ESP=N§ documento];
                                                   SourceExpr=DocumentNo }
      { 19  ;TextBox      ;3410 ;2310 ;1650 ;440  ;CaptionML=[ENU=New Posting Date;
                                                              ESP=Nueva fecha registro];
                                                   SourceExpr=NewPostingDate }
      { 23  ;CheckBox     ;3410 ;1760 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Use New Posting Date;
                                                              ESP=Usar nueva fecha registro];
                                                   SourceExpr=UseNewPostingDate }
      { 24  ;Label        ;0    ;2310 ;3300 ;440  ;ParentControl=19 }
      { 25  ;Label        ;0    ;1760 ;3300 ;440  ;ParentControl=23 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF DeprBookCode = '' THEN BEGIN
                     FASetup.GET;
                     DeprBookCode := FASetup."Default Depr. Book";
                   END;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones] }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Cancel Book;
                             ESP=Anular libro];
                  SourceExpr=DeprBookCode;
                  TableRelation="Depreciation Book" }

      { 20  ;2   ;Field     ;
                  CaptionML=[ENU=Starting Date;
                             ESP=Fecha inicial];
                  SourceExpr=StartingDate }

      { 6   ;2   ;Field     ;
                  CaptionML=[ENU=Ending Date;
                             ESP=Fecha final];
                  SourceExpr=EndingDate }

      { 23  ;2   ;Field     ;
                  CaptionML=[ENU=Use New Posting Date;
                             ESP=Usar nueva fecha registro];
                  SourceExpr=UseNewPostingDate }

      { 19  ;2   ;Field     ;
                  CaptionML=[ENU=New Posting Date;
                             ESP=Nueva fecha registro];
                  SourceExpr=NewPostingDate }

      { 16  ;2   ;Field     ;
                  CaptionML=[ENU=Document No.;
                             ESP=N§ documento];
                  SourceExpr=DocumentNo }

      { 13  ;2   ;Field     ;
                  CaptionML=[ENU=Posting Description;
                             ESP=Texto de registro];
                  SourceExpr=PostingDescription }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Insert Bal. Account;
                             ESP=Insertar contrapartida];
                  SourceExpr=BalAccount }

      { 1904404401;2;Group  ;
                  CaptionML=[ENU=Cancel;
                             ESP=Cancelar] }

      { 8   ;3   ;Field     ;
                  CaptionML=[ENU=Acquisition Cost;
                             ESP=Coste];
                  SourceExpr=CancelChoices[1] }

      { 9   ;3   ;Field     ;
                  CaptionML=[ENU=Depreciation;
                             ESP=Amortizaci¢n];
                  SourceExpr=CancelChoices[2] }

      { 10  ;3   ;Field     ;
                  CaptionML=[ENU=Write-Down;
                             ESP=Depreciaci¢n];
                  SourceExpr=CancelChoices[3] }

      { 11  ;3   ;Field     ;
                  CaptionML=[ENU=Appreciation;
                             ESP=Apreciaci¢n];
                  SourceExpr=CancelChoices[4] }

      { 12  ;3   ;Field     ;
                  CaptionML=[ENU=Custom 1;
                             ESP=Especial];
                  SourceExpr=CancelChoices[5] }

      { 17  ;3   ;Field     ;
                  CaptionML=[ENU=Custom 2;
                             ESP=Provisi¢n];
                  SourceExpr=CancelChoices[6] }

      { 18  ;3   ;Field     ;
                  CaptionML=[ENU=Salvage Value;
                             ESP=Valor residual];
                  SourceExpr=CancelChoices[9] }

      { 5   ;3   ;Field     ;
                  CaptionML=[ENU=Disposal;
                             ESP=Venta/Baja];
                  SourceExpr=CancelChoices[7] }

    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=The Starting Date is later than the Ending Date.;ESP=La fecha inicial es posterior a la fecha final.';
      Text001@1001 : TextConst 'ENU=Canceling fixed asset    #1##########;ESP=Cancelando activo        #1##########';
      GenJnlLine@1002 : Record 81;
      FASetup@1003 : Record 5603;
      FAJnlLine@1004 : Record 5621;
      FADeprBook@1005 : Record 5612;
      DeprBook@1006 : Record 5611;
      FALedgEntry@1007 : Record 5601;
      FAJnlSetup@1008 : Record 5605;
      DepreciationCalc@1009 : Codeunit 5616;
      CancelFALedgEntries@1010 : Codeunit 5624;
      Window@1011 : Dialog;
      CancelChoices@1012 : ARRAY [9] OF Boolean;
      GLIntegration@1013 : ARRAY [9] OF Boolean;
      DocumentNo@1014 : Code[20];
      DocumentNo2@1015 : Code[20];
      DocumentNo3@1016 : Code[20];
      NoSeries2@1017 : Code[10];
      NoSeries3@1018 : Code[10];
      PostingDescription@1019 : Text[50];
      JournalType@1020 : 'SkipType,GenJnlType,FAJnlType';
      DeprBookCode@1021 : Code[10];
      BalAccount@1022 : Boolean;
      StartingDate@1023 : Date;
      EndingDate@1024 : Date;
      EndingDate2@1025 : Date;
      FirstGenJnl@1026 : Boolean;
      FirstFAJnl@1027 : Boolean;
      FAJnlNextLineNo@1028 : Integer;
      GenJnlNextLineNo@1029 : Integer;
      Text002@1032 : TextConst 'ENU=You must specify New Posting Date.;ESP=Debe especificar nueva fecha registro.';
      Text003@1031 : TextConst 'ENU=You must not specify New Posting Date.;ESP=No debe especificar nueva fecha registro.';
      Text004@1030 : TextConst 'ENU=You must not specify a closing date.;ESP=No debe especificar una fecha de cierre.';
      UseNewPostingDate@1034 : Boolean;
      NewPostingDate@1033 : Date;

    LOCAL PROCEDURE InsertGenJnlLine@2(VAR FALedgEntry@1000 : Record 5601);
    VAR
      FAInsertGLAcc@1001 : Codeunit 5601;
    BEGIN
      IF FirstGenJnl THEN BEGIN
        GenJnlLine.LOCKTABLE;
        FAJnlSetup.GenJnlName(DeprBook,GenJnlLine,GenJnlNextLineNo);
        NoSeries2:= FAJnlSetup.GetGenNoSeries(GenJnlLine);
        IF DocumentNo = '' THEN
          DocumentNo2 := FAJnlSetup.GetGenJnlDocumentNo(GenJnlLine,FALedgEntry."FA Posting Date",TRUE)
        ELSE
          DocumentNo2 := DocumentNo;
      END;
      FirstGenJnl := FALSE;

      WITH GenJnlLine DO BEGIN
        FALedgEntry.MoveToGenJnl(GenJnlLine);
        "Shortcut Dimension 1 Code" := FALedgEntry."Global Dimension 1 Code";
        "Shortcut Dimension 2 Code" := FALedgEntry."Global Dimension 2 Code";
        VALIDATE("Depreciation Book Code",DeprBookCode);
        VALIDATE(Amount,-Amount);
        "FA Error Entry No." := FALedgEntry."Entry No.";
        VALIDATE(Correction,DeprBook."Mark Errors as Corrections");
        "Document No." := DocumentNo2;
        "Posting No. Series" := NoSeries2;
        "Document Type" := "Document Type"::" ";
        "External Document No." := '';
        IF PostingDescription <> '' THEN
          Description := PostingDescription;
        GenJnlNextLineNo := GenJnlNextLineNo + 10000;
        "Line No." := GenJnlNextLineNo;
        INSERT(TRUE);
        CancelFALedgEntries.CopyFALedgDimToJnlLineDim(
          DATABASE::"Gen. Journal Line","Journal Template Name",
          "Journal Batch Name","Line No.",FALedgEntry."Entry No.");
        IF BalAccount THEN BEGIN
          FAInsertGLAcc.GetBalAcc(GenJnlLine);
          IF FIND('+') THEN ;
          GenJnlNextLineNo := "Line No.";
        END;
      END;
    END;

    LOCAL PROCEDURE InsertFAJnlLine@3(VAR FALedgEntry@1000 : Record 5601);
    BEGIN
      IF FirstFAJnl THEN BEGIN
        FAJnlLine.LOCKTABLE;
        FAJnlSetup.FAJnlName(DeprBook,FAJnlLine,FAJnlNextLineNo);
        NoSeries3:= FAJnlSetup.GetFANoSeries(FAJnlLine);
        IF DocumentNo = '' THEN
          DocumentNo3 := FAJnlSetup.GetFAJnlDocumentNo(FAJnlLine,FALedgEntry."FA Posting Date",TRUE)
        ELSE
          DocumentNo3 := DocumentNo;
      END;
      FirstFAJnl := FALSE;

      WITH FAJnlLine DO BEGIN
        FALedgEntry.MoveToFAJnl(FAJnlLine);
        "Shortcut Dimension 1 Code" := FALedgEntry."Global Dimension 1 Code";
        "Shortcut Dimension 2 Code" := FALedgEntry."Global Dimension 2 Code";
        VALIDATE("Depreciation Book Code",DeprBookCode);
        VALIDATE(Amount,-Amount);
        "FA Error Entry No." := FALedgEntry."Entry No.";
        VALIDATE(Correction,DeprBook."Mark Errors as Corrections");
        "Document No." := DocumentNo3;
        "Posting No. Series" := NoSeries3;
        "Document Type" := "Document Type"::" ";
        "External Document No." := '';
        IF PostingDescription <> '' THEN
          Description := PostingDescription;
        FAJnlNextLineNo := FAJnlNextLineNo + 10000;
        "Line No." := FAJnlNextLineNo;
        INSERT(TRUE);
        CancelFALedgEntries.CopyFALedgDimToJnlLineDim(
          DATABASE::"FA Journal Line","Journal Template Name",
          "Journal Batch Name","Line No.",FALedgEntry."Entry No.");
      END;
    END;

    LOCAL PROCEDURE SetJournalType@10(VAR FALedgEntry@1000 : Record 5601);
    VAR
      Index@1001 : Integer;
    BEGIN
      Index := FALedgEntry.ConvertPostingType + 1;
      IF CancelChoices[Index] THEN BEGIN
        IF GLIntegration[Index] AND NOT "Fixed Asset"."Budgeted Asset" THEN
          JournalType := JournalType::GenJnlType
        ELSE
          JournalType := JournalType::FAJnlType
      END ELSE
        JournalType := JournalType::SkipType;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
