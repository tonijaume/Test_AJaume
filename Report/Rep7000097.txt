OBJECT Report 7000097 Reject Docs.
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVES6.00.01;
  }
  PROPERTIES
  {
    Permissions=TableData 21=imd,
                TableData 25=imd,
                TableData 7000002=imd,
                TableData 7000003=imd,
                TableData 7000004=imd,
                TableData 7000006=imd,
                TableData 7000007=imd;
    CaptionML=[ENU=Reject Docs.;
               ESP=Impagar documentos];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  GLSetup.GET;

                  IF UseJournal = UseJournal::AuxJournal THEN BEGIN
                    IF NOT GenJnlBatch.GET(TemplName,BatchName) THEN
                      ERROR(Text1100000);
                    ReasonCode :=  GenJnlBatch."Reason Code";
                    GenJnlTemplate.GET(TemplName);
                    SourceCode := GenJnlTemplate."Source Code";
                  END;
                END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table21;
        DataItemTableView=SORTING(Entry No.)
                          WHERE(Open=CONST(Yes));
        DataItemVarName=CustLedgEntry;
        OnPreDataItem=BEGIN
                        DocPost.CheckPostingDate(PostingDate);

                        FIND('-');
                        ArePostedDocs := "Document Situation" = "Document Situation"::"Posted BG/PO";

                        IF UseJournal = UseJournal::AuxJournal THEN BEGIN
                          GenJnlBatch.GET(TemplName,BatchName);
                          ReasonCode := GenJnlBatch."Reason Code";
                          GenJnlTemplate.GET(TemplName);
                          SourceCode := GenJnlTemplate."Source Code";
                        END ELSE BEGIN
                          ReasonCode := '';
                          SourceCodeSetup.GET;
                          SourceCode := SourceCodeSetup."Cartera Journal"
                        END;

                        CASE "Document Type" OF
                          "Document Type"::Bill:
                            Window.OPEN(
                              Text1100001);
                          "Document Type"::Invoice:
                            Window.OPEN(
                              Text1100002);
                          ELSE
                            Window.OPEN(
                              Text1100003);
                        END;

                        DocCount :=0;
                        GenJnlLineNextNo := 0;
                        ExistVATEntry := FALSE;
                      END;

        OnAfterGetRecord=VAR
                           FromJnl@1100001 : Boolean;
                         BEGIN
                           IsRedrawn := CarteraManagement.CheckFromRedrawnDoc("Bill No.");
                           DocCount := DocCount + 1;
                           Window.UPDATE(1,DocCount);

                           IF GLSetup."Unrealized VAT" AND
                              (("Document Type" = "Document Type"::Bill) OR ("Document Type" = "Document Type"::Invoice)) THEN BEGIN
                             FromJnl := FALSE;
                               IF PostedDoc."From Journal" THEN
                                 FromJnl := TRUE;
                             ExistsNoRealVAT := GenJnlPostLine.CustFindVATSetup(VATPostingSetup,CustLedgEntry,FromJnl);
                           END;

                           IF ArePostedDocs THEN BEGIN
                             PostedDoc.GET(PostedDoc.Type::Receivable,"Entry No.");
                               PostedBillGr.GET(PostedDoc."Bill Gr./Pmt. Order No.");
                               IF PostedBillGr."Dealing Type" = PostedBillGr."Dealing Type"::Discount THEN
                                 Discount := TRUE
                               ELSE
                                 Discount := FALSE;
                               IF PostedBillGr.Factoring = PostedBillGr.Factoring::" " THEN
                                 Factoring := FALSE
                               ELSE
                                 Factoring := TRUE;
                               BankAcc.GET(PostedDoc."Bank Account No.");
                               BankAcc.TESTFIELD("Bank Acc. Posting Group");
                               BankAccPostingGr.GET(BankAcc."Bank Acc. Posting Group");
                             IF IncludeExpenses THEN BEGIN
                               BankAcc.TESTFIELD("Operation Fees Code");
                               FeeRange.InitRejExpenses(
                                 BankAcc."Operation Fees Code",
                                 BankAcc."Currency Code");
                             END;
                           END;

                           CustLedgEntry.CALCFIELDS("Remaining Amount","Remaining Amt. (LCY)");

                           IF CustLedgEntry."Document Type" = CustLedgEntry."Document Type"::Bill THEN BEGIN
                             CurrDescription := STRSUBSTNO(Text1100004,"Document No.","Bill No.");
                             CurrDocNo := "Document No.";
                             CurrDocNo2 := "Bill No.";
                             PrepareBillRejPosting("Remaining Amount");
                           END ELSE BEGIN
                             CurrDescription := STRSUBSTNO(Text1100005,"Document No.");
                             CurrDocNo := "Document No.";
                             CurrDocNo2 := "Bill No.";
                             PrepareInvoiceRejPosting("Remaining Amount");
                           END;

                           IF ArePostedDocs THEN BEGIN
                             PostedDoc.GET(PostedDoc.Type::Receivable,"Entry No.");
                             PostedDoc.Status := PostedDoc.Status::Rejected;
                             PostedDoc."Honored/Rejtd. at Date" := PostingDate;
                             PostedDoc.MODIFY;
                             "Document Status" := "Document Status"::Rejected;
                             MODIFY;
                             IF IncludeExpenses THEN
                               FeeRange.CalcRejExpensesAmt(
                                 BankAcc."Operation Fees Code",
                                 BankAcc."Currency Code",
                                 "Remaining Amount",
                                 CustLedgEntry."Entry No.");
                           END ELSE BEGIN
                             Doc.GET(Doc.Type::Receivable,"Entry No.");
                             ClosedDoc.TRANSFERFIELDS(Doc);
                             ClosedDoc.Status := ClosedDoc.Status::Rejected;
                             ClosedDoc."Honored/Rejtd. at Date" := PostingDate;
                             ClosedDoc."Bill Gr./Pmt. Order No." := '';
                             ClosedDoc."Remaining Amount" := Doc."Remaining Amount";
                             ClosedDoc."Remaining Amt. (LCY)" := Doc."Remaining Amt. (LCY)";
                             ClosedDoc."Amount for Collection" := 0;
                             ClosedDoc."Amt. for Collection (LCY)" := 0;
                             ClosedDoc.INSERT(TRUE);
                             CarteraDimMgt.MoveDocToClosedDoc(Doc);
                             Doc.DELETE;
                             "Document Status" := "Document Status"::Rejected;
                             "Document Situation" := "Document Situation"::"Closed Documents";
                             MODIFY;
                           END;

                           DocPost.InsertDtldCustLedgEntry(
                             CustLedgEntry,
                             CustLedgEntry."Remaining Amount",
                             CustLedgEntry."Remaining Amt. (LCY)",
                             EntryType::Rejection,
                             PostingDate);

                           IF GLSetup."Unrealized VAT" AND
                              ExistsNoRealVAT AND
                              (NOT IsRedrawn) THEN BEGIN
                             CarteraManagement.CustUnrealizedVAT2(
                               CustLedgEntry,
                               CustLedgEntry."Remaining Amt. (LCY)",
                               GenJnlLine,
                               ExistVATEntry,
                               FirstVATEntryNo,
                               LastVATEntryNo,
                               NoRealVATBuffer,
                               FromJnl);

                               TempCurrCode := CustLedgEntry."Currency Code";
                               CustLedgEntry."Currency Code" := '';

                             IF NoRealVATBuffer.FIND('-') THEN BEGIN
                               REPEAT
                                 BEGIN
                                   FindInClosedBills := TRUE;
                                   InsertGenJournalLine(
                                     GenJnlLine."Account Type"::"G/L Account",
                                     NoRealVATBuffer.Account,
                                     -NoRealVATBuffer.Amount,
                                     CustLedgEntry."Global Dimension 1 Code",
                                     CustLedgEntry."Global Dimension 2 Code");
                                   InsertGenJournalLine(
                                     GenJnlLine."Account Type"::"G/L Account",
                                     NoRealVATBuffer."Balance Account",
                                     NoRealVATBuffer.Amount,
                                     CustLedgEntry."Global Dimension 1 Code",
                                     CustLedgEntry."Global Dimension 2 Code");
                                   FindInClosedBills := FALSE;
                                 END;
                               UNTIL NoRealVATBuffer.NEXT = 0;
                             END;

                             CustLedgEntry."Currency Code" := TempCurrCode;

                           END;

                           IF IncludeExpenses AND ArePostedDocs THEN BEGIN
                             CurrDescription := Text1100006;
                             CurrDocNo := PostedBillGr."No.";
                             CurrDocNo2 := '';
                             IF (FeeRange.GetTotalRejExpensesAmt <> 0) OR (UseJournal = UseJournal::AuxJournal) THEN BEGIN
                               BankAccPostingGr.TESTFIELD("Rejection Expenses Acc.");
                               NoRegs := FeeRange.NoRegRejExpenses;
                               FOR i := 0 TO NoRegs - 1 DO BEGIN
                                 FeeRange.GetRejExpensesAmt(MemIntReg,i);
                                 CustLedgEntry.GET(MemIntReg."Entry No.");
                                 InsertGenJournalLine(
                                   GenJnlLine."Account Type"::"G/L Account",
                                   BankAccPostingGr."Rejection Expenses Acc.",
                                   MemIntReg.Amount,
                                   "Global Dimension 1 Code",
                                   "Global Dimension 2 Code");
                                 InsertGenJournalLine(
                                   GenJnlLine."Account Type"::"Bank Account",
                                   BankAcc."No.",
                                   -MemIntReg.Amount,
                                   "Global Dimension 1 Code",
                                   "Global Dimension 2 Code");
                               END;
                             END;
                           END;

                           IF (FeeRange.GetTotalRejExpensesAmt <> 0) AND ArePostedDocs THEN BEGIN
                             PostedBillGr."Rejection Expenses Amt." := PostedBillGr."Rejection Expenses Amt." + FeeRange.GetTotalRejExpensesAmt;
                             PostedBillGr.MODIFY;
                           END;
                         END;

        OnPostDataItem=VAR
                         PostedDoc2@1170000000 : Record 7000003;
                       BEGIN
                         IF GenJnlLine.FIND('-') THEN
                           REPEAT
                             IF ArePostedDocs THEN BEGIN
                               PostedDoc2.SETRANGE("Document No.",GenJnlLine."Document No.");
                               IF PostedDoc2.FIND('-') THEN BEGIN
                                 REPEAT
                                   PostedBillGr.GET(PostedDoc2."Bill Gr./Pmt. Order No.");
                                   DocPost.CloseBillGroupIfEmpty(PostedBillGr,PostingDate);
                                 UNTIL PostedDoc2.NEXT = 0;
                               END;
                             END;
                           UNTIL GenJnlLine.NEXT = 0;

                         PostGenJournal;
                         IF ExistVATEntry THEN BEGIN
                           GLReg.FIND('+');
                           GLReg."From VAT Entry No." := FirstVATEntryNo;
                           GLReg."To VAT Entry No." := LastVATEntryNo;
                           GLReg.MODIFY;
                         END;


                         COMMIT;

                         MESSAGE(Text1100007,DocCount);
                       END;

        TotalFields=Remaining Amount;
      }
      SECTIONS
      {
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=7150;
      Height=4400;
      SaveValues=Yes;
      SourceTable=Table18;
      OnOpenForm=BEGIN
                   PostingDate := WORKDATE;
                 END;

    }
    CONTROLS
    {
      { 2   ;TextBox      ;3630 ;2970 ;2750 ;440  ;ParentControl=1100000;
                                                   InFrame=Yes;
                                                   CaptionML=[ENU=Template Name;
                                                              ESP=Nombre libro];
                                                   SourceExpr=TemplName;
                                                   TableRelation="Gen. Journal Template".Name WHERE (Type=CONST(Cartera),
                                                                                                     Recurring=CONST(No));
                                                   OnValidate=BEGIN
                                                                IF TemplName = '' THEN
                                                                  BatchName := ''
                                                                ELSE
                                                                  IF UseJournal = UseJournal::Direct THEN
                                                                    ERROR(Text1100008);
                                                              END;
                                                               }
      { 3   ;Label        ;220  ;2970 ;3180 ;440  ;ParentControl=2 }
      { 4   ;TextBox      ;3630 ;3520 ;2750 ;440  ;ParentControl=1100000;
                                                   InFrame=Yes;
                                                   CaptionML=[ENU=Batch Name;
                                                              ESP=Nombre secci¢n];
                                                   SourceExpr=BatchName;
                                                   TableRelation="Gen. Journal Batch".Name;
                                                   OnValidate=BEGIN
                                                                IF BatchName = '' THEN
                                                                  EXIT;
                                                              END;

                                                   OnLookup=BEGIN
                                                              IF TemplName = '' THEN
                                                                EXIT;

                                                              IF GenJnlBatch.GET(TemplName,Text) THEN;
                                                              GenJnlBatch.SETRANGE("Journal Template Name",TemplName);
                                                              IF FORM.RUNMODAL(FORM::"General Journal Batches",GenJnlBatch) = ACTION::LookupOK THEN
                                                                BatchName := GenJnlBatch.Name;
                                                            END;

                                                   OnAfterValidate=BEGIN
                                                                     IF TemplName = '' THEN
                                                                       BatchName := '';
                                                                   END;
                                                                    }
      { 11  ;Label        ;220  ;3520 ;3180 ;440  ;ParentControl=4 }
      { 9   ;OptionButton ;3410 ;1870 ;3740 ;440  ;CaptionML=[ENU=Auxiliary Journal;
                                                              ESP=Diario auxiliar];
                                                   SourceExpr=UseJournal;
                                                   OptionValue=AuxJournal }
      { 10  ;Label        ;0    ;1320 ;3300 ;440  ;ParentControl=15;
                                                   CaptionML=[ENU=Post;
                                                              ESP=Registrar] }
      { 15  ;OptionButton ;3410 ;1320 ;3740 ;440  ;CaptionML=[ENU=Direct;
                                                              ESP=Directamente];
                                                   SourceExpr=UseJournal;
                                                   OptionValue=Direct;
                                                   OnAfterValidate=BEGIN
                                                                     IF UseJournal = UseJournal::Direct THEN BEGIN
                                                                       BatchName := '';
                                                                       TemplName := '';
                                                                     END;
                                                                   END;
                                                                    }
      { 18  ;TextBox      ;3410 ;0    ;1650 ;440  ;CaptionML=[ENU=Posting Date;
                                                              ESP=Fecha registro];
                                                   SourceExpr=PostingDate }
      { 19  ;Label        ;0    ;0    ;3300 ;440  ;ParentControl=18 }
      { 5   ;CheckBox     ;3410 ;660  ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Include Expenses;
                                                              ESP=Incluye gastos];
                                                   SourceExpr=IncludeExpenses }
      { 6   ;Label        ;0    ;660  ;3300 ;440  ;ParentControl=5 }
      { 1100000;Frame     ;0    ;2530 ;7150 ;1650 ;CaptionML=[ENU=Auxiliary Journal;
                                                              ESP=Diario auxiliar] }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      SourceTable=Table18;
      OnOpenPage=BEGIN
                   PostingDate := WORKDATE;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones] }

      { 18  ;2   ;Field     ;
                  CaptionML=[ENU=Posting Date;
                             ESP=Fecha registro];
                  SourceExpr=PostingDate }

      { 5   ;2   ;Field     ;
                  CaptionML=[ENU=Include Expenses;
                             ESP=Incluye gastos];
                  SourceExpr=IncludeExpenses }

      { 15  ;2   ;Field     ;
                  CaptionML=[ENU=Post;
                             ESP=Registro];
                  OptionCaptionML=[ENU=Direct,Auxiliary Journal;
                                   ESP=Directo,Diario auxiliar];
                  SourceExpr=UseJournal;
                  OnValidate=BEGIN
                               IF UseJournal = UseJournal::Direct THEN
                                 DirectUseJournalOnValidate;
                             END;
                              }

      { 1100000;2;Group     ;
                  CaptionML=[ENU=Auxiliary Journal;
                             ESP=Diario auxiliar] }

      { 2   ;3   ;Field     ;
                  CaptionML=[ENU=Template Name;
                             ESP=Nombre libro];
                  SourceExpr=TemplName;
                  TableRelation="Gen. Journal Template".Name WHERE (Type=CONST(Cartera),
                                                                    Recurring=CONST(No));
                  OnValidate=BEGIN
                               IF TemplName = '' THEN
                                 BatchName := ''
                               ELSE
                                 IF UseJournal = UseJournal::Direct THEN
                                   ERROR(Text1100008);
                             END;
                              }

      { 4   ;3   ;Field     ;
                  CaptionML=[ENU=Batch Name;
                             ESP=Nombre secci¢n];
                  SourceExpr=BatchName;
                  TableRelation="Gen. Journal Batch".Name;
                  OnValidate=BEGIN
                               BatchNameOnValidate;
                                 BatchNameOnAfterValidate;
                             END;

                  OnLookup=BEGIN
                             IF TemplName = '' THEN
                               EXIT;

                             IF GenJnlBatch.GET(TemplName,Text) THEN;
                             GenJnlBatch.SETRANGE("Journal Template Name",TemplName);
                             IF FORM.RUNMODAL(FORM::"General Journal Batches",GenJnlBatch) = ACTION::LookupOK THEN
                               BatchName := GenJnlBatch.Name;
                           END;
                            }

    }
  }
  CODE
  {
    VAR
      Text1100000@1000 : TextConst 'ENU=Please fill in both the Template Name and the Batch Name of the Auxiliary Journal with correct values.;ESP=Rellene el nombre del libro y la secci¢n del diario con los valores apropiados.';
      Text1100001@1001 : TextConst 'ENU=Rejecting receivable bills              #1######;ESP=Impagando efectos a cobrar              #1######';
      Text1100002@1002 : TextConst 'ENU=Rejecting receivable invoices           #1######;ESP=Impagando facturas a cobrar             #1######';
      Text1100003@1003 : TextConst 'ENU=Rejecting receivable documents          #1######;ESP=Impagando documentos a cobrar           #1######';
      Text1100004@1004 : TextConst 'ENU=Rejected Bill %1/%2;ESP=Efecto impagado %1/%2';
      Text1100005@1005 : TextConst 'ENU=Rejected Document %1;ESP=Documento impagado %1';
      Text1100006@1006 : TextConst 'ENU=Document Rejection Expenses;ESP=Gastos impago documentos';
      Text1100007@1007 : TextConst 'ENU=%1 documents have been rejected.;ESP=Se ha/n impagado %1 documento/s.';
      Text1100008@1008 : TextConst 'ENU=This field must be blank for Direct Posting.;ESP=Este campo debe estar en blanco para registro directo.';
      GenJnlTemplate@1100000 : Record 80;
      GenJnlBatch@1100001 : Record 232;
      PostedBillGr@1100002 : Record 7000006;
      GenJnlLine@1100003 : TEMPORARY Record 81;
      BankAcc@1100004 : Record 270;
      SourceCodeSetup@1100005 : Record 242;
      CustPostingGr@1100006 : Record 92;
      BankAccPostingGr@1100007 : Record 277;
      Doc@1100008 : Record 7000002;
      PostedDoc@1100009 : Record 7000003;
      ClosedDoc@1100010 : Record 7000004;
      FeeRange@1100011 : Record 7000019;
      GLSetup@1100012 : Record 98;
      GLReg@1100013 : Record 45;
      Currency@1100014 : Record 4;
      TempJnlLineDim@1100052 : Record 7000026;
      JnlLineDim2@1100053 : Record 356;
      GenJnlPostLine@1100015 : Codeunit 12;
      DocPost@1100016 : Codeunit 7000006;
      CarteraManagement@1100017 : Codeunit 7000000;
      GenJnlManagement@1100018 : Codeunit 230;
      CarteraDimMgt@1100050 : Codeunit 7000011;
      DimMgt@1100051 : Codeunit 408;
      CarteraJnlForm@1100019 : Form 7000036;
      Window@1100020 : Dialog;
      IncludeExpenses@1100021 : Boolean;
      PostingDate@1100022 : Date;
      PostingDate2@1100023 : Date;
      TransactionNo@1100024 : Integer;
      CurrDescription@1100025 : Text[250];
      CurrDocNo@1100026 : Code[20];
      CurrDocNo2@1100027 : Code[20];
      UseJournal@1100028 : 'Direct,AuxJournal';
      BatchName@1100029 : Code[10];
      TemplName@1100030 : Code[10];
      Discount@1100031 : Boolean;
      SourceCode@1100032 : Code[10];
      GenJnlLineNextNo@1100033 : Integer;
      DocCount@1100034 : Integer;
      BalanceAccNo@1100035 : Code[20];
      ArePostedDocs@1100036 : Boolean;
      ReasonCode@1100037 : Code[10];
      Factoring@1100038 : Boolean;
      VATPostingSetup@1100039 : Record 325;
      NoRealVATBuffer@1100055 : TEMPORARY Record 7000012;
      FirstVATEntryNo@1100040 : Integer;
      LastVATEntryNo@1100041 : Integer;
      ExistVATEntry@1100042 : Boolean;
      IsRedrawn@1100043 : Boolean;
      GainLossAmt@1100044 : Decimal;
      TempCurrCode@1100045 : Code[10];
      NoRegs@1100046 : Integer;
      i@1100047 : Integer;
      MemIntReg@1100048 : Record 7000012;
      EntryType@1100049 : ' ,Initial Entry,Application,Unrealized Loss,Unrealized Gain,Realized Loss,Realized Gain,Payment Discount,Payment Discount (VAT Excl.),Payment Discount (VAT Adjustment),Appln. Rounding,Correction of Remaining Amount,,,,,,,,,Settlement,Rejection,Redrawal,Expenses';
      ExistsNoRealVAT@1100054 : Boolean;
      FindInClosedBills@1100056 : Boolean;
      BGPOBuffer@1170000000 : TEMPORARY Record 7000012;

    LOCAL PROCEDURE PrepareBillRejPosting@8(RemainingAmt@1100000 : Decimal);
    VAR
      TempCurrencyCode@1100001 : Code[10];
      Type@1100002 : 'Receivable,Payable';
    BEGIN
      CustPostingGr.GET(CustLedgEntry."Customer Posting Group");
      CustPostingGr.TESTFIELD("Rejected Bills Acc.");
      InsertGenJournalLine(
        GenJnlLine."Account Type"::"G/L Account",
        CustPostingGr."Rejected Bills Acc.",
        RemainingAmt,
        CustLedgEntry."Global Dimension 1 Code",
        CustLedgEntry."Global Dimension 2 Code");
      IF ArePostedDocs THEN
        IF Discount THEN BEGIN
          CustPostingGr.TESTFIELD("Discted. Bills Acc.");
          BalanceAccNo := CustPostingGr."Discted. Bills Acc.";
        END ELSE BEGIN
          CustPostingGr.TESTFIELD("Bills on Collection Acc.");
          BalanceAccNo := CustPostingGr."Bills on Collection Acc.";
        END
      ELSE BEGIN
        CustPostingGr.TESTFIELD("Bills Account");
        BalanceAccNo := CustPostingGr."Bills Account";
      END;


      PostingDate2 := 0D;
      GainLossAmt := 0;
      IF CustLedgEntry."Currency Code" <> '' THEN BEGIN
        Currency.GET(CustLedgEntry."Currency Code");
        IF ArePostedDocs THEN
          PostingDate2 := CarteraManagement.GetLastDate(CustLedgEntry."Currency Code",PostedBillGr."Posting Date",Type::Receivable)
        ELSE
          PostingDate2 := CarteraManagement.GetLastDate(CustLedgEntry."Currency Code",CustLedgEntry."Posting Date",Type::Receivable);

        GainLossAmt := CarteraManagement.GetGainLoss(
          PostingDate2,
          PostingDate,
          RemainingAmt,
          CustLedgEntry."Currency Code");
      END;

      InsertGenJournalLine(
        GenJnlLine."Account Type"::"G/L Account",
        BalanceAccNo,
        -RemainingAmt,
        CustLedgEntry."Global Dimension 1 Code",
        CustLedgEntry."Global Dimension 2 Code");

      IF GainLossAmt <> 0 THEN BEGIN
        TempCurrencyCode := CustLedgEntry."Currency Code";
        CustLedgEntry."Currency Code" := '';
        IF GainLossAmt > 0 THEN BEGIN
          Currency.TESTFIELD("Realized Gains Acc.");
          InsertGenJournalLine(
            GenJnlLine."Account Type"::"G/L Account",
            Currency."Realized Gains Acc.",
            -GainLossAmt,
            CustLedgEntry."Global Dimension 1 Code",
            CustLedgEntry."Global Dimension 2 Code");
        END ELSE BEGIN
          Currency.TESTFIELD("Realized Losses Acc.");
          InsertGenJournalLine(
            GenJnlLine."Account Type"::"G/L Account",
            Currency."Realized Losses Acc.",
            -GainLossAmt,
            CustLedgEntry."Global Dimension 1 Code",
            CustLedgEntry."Global Dimension 2 Code");
        END;
        InsertGenJournalLine(
          GenJnlLine."Account Type"::"G/L Account",
          BalanceAccNo,
          GainLossAmt,
          CustLedgEntry."Global Dimension 1 Code",
          CustLedgEntry."Global Dimension 2 Code");

        CustLedgEntry."Currency Code" := TempCurrencyCode;
      END;


      IF Discount THEN BEGIN
        BankAccPostingGr.TESTFIELD("Liabs. for Disc. Bills Acc.");
        InsertGenJournalLine(
          GenJnlLine."Account Type"::"G/L Account",
          BankAccPostingGr."Liabs. for Disc. Bills Acc.",
          RemainingAmt,
          CustLedgEntry."Global Dimension 1 Code",
          CustLedgEntry."Global Dimension 2 Code");

        InsertGenJournalLine(
          GenJnlLine."Account Type"::"Bank Account",
          BankAcc."No.",
          -RemainingAmt,
          CustLedgEntry."Global Dimension 1 Code",
          CustLedgEntry."Global Dimension 2 Code");
      END;
    END;

    LOCAL PROCEDURE PrepareInvoiceRejPosting@3(RemainingAmt@1100000 : Decimal);
    VAR
      PostingAmt@1100001 : Decimal;
      TempCurrencyCode@1100002 : Code[10];
      Type@1100003 : 'Receivable,Payable';
    BEGIN
      CustPostingGr.GET(CustLedgEntry."Customer Posting Group");
      CustPostingGr.TESTFIELD("Rejected Factoring Acc.");

      PostingDate2 := 0D;
      GainLossAmt := 0;
      IF CustLedgEntry."Currency Code" <> '' THEN BEGIN
        Currency.GET(CustLedgEntry."Currency Code");
        IF ArePostedDocs THEN
          PostingDate2 := CarteraManagement.GetLastDate(CustLedgEntry."Currency Code",PostedBillGr."Posting Date",Type::Receivable)
        ELSE
          PostingDate2 := CarteraManagement.GetLastDate(CustLedgEntry."Currency Code",CustLedgEntry."Posting Date",Type::Receivable);
        GainLossAmt := CarteraManagement.GetGainLoss(
          PostingDate2,
          PostingDate,
          RemainingAmt,
          CustLedgEntry."Currency Code");
      END;



      IF ArePostedDocs THEN BEGIN
        InsertGenJournalLine(
          GenJnlLine."Account Type"::"G/L Account",
          CustPostingGr."Rejected Factoring Acc.",
          RemainingAmt,
          CustLedgEntry."Global Dimension 1 Code",
          CustLedgEntry."Global Dimension 2 Code");
        IF ArePostedDocs THEN
          IF Discount THEN BEGIN
            CustPostingGr.TESTFIELD("Factoring for Discount Acc.");
            BalanceAccNo := CustPostingGr."Factoring for Discount Acc.";
          END ELSE BEGIN
            CustPostingGr.TESTFIELD("Factoring for Collection Acc.");
            BalanceAccNo := CustPostingGr."Factoring for Collection Acc.";
          END;
        InsertGenJournalLine(
          GenJnlLine."Account Type"::"G/L Account",
          BalanceAccNo,
          -RemainingAmt,
          CustLedgEntry."Global Dimension 1 Code",
          CustLedgEntry."Global Dimension 2 Code");
      END;

      IF GainLossAmt <> 0 THEN BEGIN
        TempCurrencyCode := CustLedgEntry."Currency Code";
        CustLedgEntry."Currency Code" := '';
        IF GainLossAmt > 0 THEN BEGIN
          Currency.TESTFIELD("Realized Gains Acc.");
          InsertGenJournalLine(
            GenJnlLine."Account Type"::"G/L Account",
            Currency."Realized Gains Acc.",
            -GainLossAmt,
            CustLedgEntry."Global Dimension 1 Code",
            CustLedgEntry."Global Dimension 2 Code");
        END ELSE BEGIN
          Currency.TESTFIELD("Realized Losses Acc.");
          InsertGenJournalLine(
            GenJnlLine."Account Type"::"G/L Account",
            Currency."Realized Losses Acc.",
            -GainLossAmt,
            CustLedgEntry."Global Dimension 1 Code",
            CustLedgEntry."Global Dimension 2 Code");
        END;
        InsertGenJournalLine(
          GenJnlLine."Account Type"::"G/L Account",
          BalanceAccNo,
          GainLossAmt,
          CustLedgEntry."Global Dimension 1 Code",
          CustLedgEntry."Global Dimension 2 Code");

        CustLedgEntry."Currency Code" := TempCurrencyCode;
      END;

      IF Discount THEN BEGIN
        BankAccPostingGr.TESTFIELD(BankAccPostingGr."Liabs. for Factoring Acc.");
        InsertGenJournalLine(
          GenJnlLine."Account Type"::"G/L Account",
          BankAccPostingGr."Liabs. for Factoring Acc.",
          DocPost.FindDisctdAmt(RemainingAmt,CustLedgEntry."Customer No.",BankAcc."No."),
          CustLedgEntry."Global Dimension 1 Code",
          CustLedgEntry."Global Dimension 2 Code");

        InsertGenJournalLine(
          GenJnlLine."Account Type"::"Bank Account",
          BankAcc."No.",
          -DocPost.FindDisctdAmt(RemainingAmt,CustLedgEntry."Customer No.",BankAcc."No."),
          CustLedgEntry."Global Dimension 1 Code",
          CustLedgEntry."Global Dimension 2 Code");
      END;
    END;

    LOCAL PROCEDURE InsertGenJournalLine@4(AccType@1100000 : Integer;AccNo@1100001 : Code[20];Amount2@1100002 : Decimal;Dep@1100003 : Code[20];Proj@1100004 : Code[20]);
    VAR
      CarteraDocDim@1100005 : Record 7000025;
      PostedDocDim@1100006 : Record 359;
    BEGIN
      GenJnlLineNextNo :=  GenJnlLineNextNo + 10000;

      WITH GenJnlLine DO BEGIN
        CLEAR(GenJnlLine);
        INIT;
        "Line No." := GenJnlLineNextNo;
        "Posting Date" := PostingDate;
        IF UseJournal = UseJournal::AuxJournal THEN BEGIN
          "Journal Template Name" := TemplName;
          "Journal Batch Name" := BatchName;
        END;
        "Document No." := CurrDocNo;
        "Bill No." := CurrDocNo2;
        VALIDATE("Account Type",AccType);
        VALIDATE("Account No.",AccNo);
        Description := COPYSTR(CurrDescription,1,MAXSTRLEN(Description));
        VALIDATE("Currency Code",CustLedgEntry."Currency Code");
        VALIDATE(Amount,Amount2);
        IF AccType = "Account Type"::"G/L Account" THEN BEGIN
          "Source No." := CustLedgEntry."Customer No.";
          "Source Type" := "Source Type"::Customer;
        END;
        "Source Code" := SourceCode;
        "Reason Code" := ReasonCode;
        "Shortcut Dimension 1 Code" := Dep;
        "Shortcut Dimension 2 Code" := Proj;
        IF UseJournal = UseJournal::AuxJournal THEN
          "System-Created Entry" := FALSE
        ELSE
          "System-Created Entry" := TRUE;
        INSERT;

        IF CustLedgEntry."Document Type" = CustLedgEntry."Document Type"::Bill THEN BEGIN
          IF ArePostedDocs THEN
            CarteraDocDim.SETRANGE("Table ID",DATABASE::"Posted Cartera Doc.")
          ELSE
            IF FindInClosedBills THEN
              CarteraDocDim.SETRANGE("Table ID",DATABASE::"Closed Cartera Doc.")
            ELSE
              CarteraDocDim.SETRANGE("Table ID",DATABASE::"Cartera Doc.");
          CarteraDocDim.SETRANGE("Document No.",CustLedgEntry."Document No.");
          CarteraDocDim.SETRANGE("Bill No.",CustLedgEntry."Bill No.");
          CarteraDocDim.SETRANGE(Type,0); // 0 = Receivable
          IF CarteraDocDim.FIND('-') THEN BEGIN
            REPEAT
              BEGIN
                TempJnlLineDim.INIT;
                TempJnlLineDim."Table ID" := DATABASE::"Gen. Journal Line";
                TempJnlLineDim."Journal Template Name" := "Journal Template Name";
                TempJnlLineDim."Journal Batch Name" := "Journal Batch Name";
                TempJnlLineDim."Journal Line No." := "Line No.";
                TempJnlLineDim."Dimension Code" := CarteraDocDim."Dimension Code";
                TempJnlLineDim."Dimension Value Code" := CarteraDocDim."Dimension Value Code";
                TempJnlLineDim.INSERT;
              END;
            UNTIL CarteraDocDim.NEXT = 0;
          CarteraDocDim.RESET;
          END;
        END ELSE BEGIN
          PostedDocDim.SETRANGE("Table ID",DATABASE::"Sales Invoice Header");
          PostedDocDim.SETRANGE("Document No.",CustLedgEntry."Document No.");
          IF PostedDocDim.FIND('-') THEN BEGIN
            REPEAT
              BEGIN
                TempJnlLineDim.INIT;
                TempJnlLineDim."Table ID" := DATABASE::"Gen. Journal Line";
                TempJnlLineDim."Journal Template Name" := "Journal Template Name";
                TempJnlLineDim."Journal Batch Name" := "Journal Batch Name";
                TempJnlLineDim."Journal Line No." := "Line No.";
                TempJnlLineDim."Dimension Code" := PostedDocDim."Dimension Code";
                TempJnlLineDim."Dimension Value Code" := PostedDocDim."Dimension Value Code";
                TempJnlLineDim.INSERT;
              END;
            UNTIL PostedDocDim.NEXT = 0;
          PostedDocDim.RESET;
          END;
        END;
      END;
    END;

    LOCAL PROCEDURE PostGenJournal@1();
    VAR
      GenJnlPostLine@1100000 : Codeunit 12;
      GenJnlLine2@1100001 : Record 81;
      LastLineNo@1100002 : Integer;
    BEGIN
      Window.CLOSE;

      IF NOT GenJnlLine.FIND('-') THEN
        EXIT;

      TempJnlLineDim.RESET;

      IF UseJournal = UseJournal::AuxJournal THEN BEGIN
        GenJnlLine2.LOCKTABLE;
        GenJnlLine2.SETRANGE("Journal Template Name",TemplName);
        GenJnlLine2.SETRANGE("Journal Batch Name",BatchName);
        IF GenJnlLine2.FIND('+') THEN BEGIN
          LastLineNo := GenJnlLine2."Line No.";
          TransactionNo := GenJnlLine2."Transaction No." + 1;
        END;
        REPEAT
          GenJnlLine2 := GenJnlLine;
          GenJnlLine2."Line No." := GenJnlLine2."Line No." + LastLineNo;
          GenJnlLine2."Transaction No." := TransactionNo;
          GenJnlLine2.INSERT;
          CarteraDimMgt.CopyJnlLinDim(GenJnlLine,GenJnlLine2,TempJnlLineDim,JnlLineDim2);
        UNTIL GenJnlLine.NEXT = 0;
        COMMIT;
        GenJnlLine2.RESET;
        GenJnlTemplate.GET(TemplName);
        GenJnlLine2.FILTERGROUP := 2;
        GenJnlLine2.SETRANGE("Journal Template Name",TemplName);
        GenJnlLine2.FILTERGROUP := 0;
        GenJnlManagement.SetName(BatchName,GenJnlLine2);
        CarteraJnlForm.SETTABLEVIEW(GenJnlLine2);
        CarteraJnlForm.SETRECORD(GenJnlLine2);
        CarteraJnlForm.AllowClosing(TRUE);
        CarteraJnlForm.RUNMODAL;
      END
      ELSE
        REPEAT
          GenJnlLine2 := GenJnlLine;
          CarteraDimMgt.CopyJnlLinDim(GenJnlLine,GenJnlLine2,TempJnlLineDim,JnlLineDim2);
          JnlLineDim2.RESET;
          JnlLineDim2.SETRANGE("Table ID",DATABASE::"Gen. Journal Line");
          JnlLineDim2.SETRANGE("Journal Template Name",GenJnlLine2."Journal Template Name");
          JnlLineDim2.SETRANGE("Journal Batch Name",GenJnlLine2."Journal Batch Name");
          JnlLineDim2.SETRANGE("Journal Line No.",GenJnlLine2."Line No.");
          GenJnlPostLine.RunWithCheck(GenJnlLine2,JnlLineDim2);
        UNTIL GenJnlLine.NEXT = 0;
    END;

    LOCAL PROCEDURE BatchNameOnAfterValidate@19021358();
    BEGIN
      IF TemplName = '' THEN
        BatchName := '';
    END;

    LOCAL PROCEDURE BatchNameOnValidate@19072059();
    BEGIN
      IF BatchName = '' THEN
        EXIT;
    END;

    LOCAL PROCEDURE DirectUseJournalOnAfterValidat@19031890();
    BEGIN
      IF UseJournal = UseJournal::Direct THEN BEGIN
        BatchName := '';
        TemplName := '';
      END;
    END;

    LOCAL PROCEDURE DirectUseJournalOnValidate@19003802();
    BEGIN
      DirectUseJournalOnAfterValidat;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
