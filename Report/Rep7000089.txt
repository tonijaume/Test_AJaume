OBJECT Report 7000089 Bill group - Export factoring
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVES6.00.01;
  }
  PROPERTIES
  {
    Permissions=TableData 7000005=m;
    CaptionML=[ENU=Bill group - Export factoring;
               ESP=Remesa - Exportar factoring];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   CompanyInfo.GET();
                   CompanyInfo.TESTFIELD("VAT Registration No.");
                   NameCompany := CompanyInfo.Name;
                   NameCompany := PADSTR(NameCompany,30,' ');
                 END;

    OnPreReport=BEGIN
                  // CLEAR(FILE);
                  OutFile.TEXTMODE := TRUE;
                  OutFile.WRITEMODE := TRUE;
                  IF ISSERVICETIER THEN BEGIN
                    ExternalFile := RBMgt.EnvironFileName('','');
                    ToFile := Text10702;
                  END;
                  OutFile.CREATE(ExternalFile);
                END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table7000005;
        OnPreDataItem=BEGIN
                        IF BankSuffix = '' THEN
                          ERROR(Text1100000);

                        IF NOT DocMisc.CheckBankSuffix(BankSuffixBankAcc,"Bill Group"."Bank Account No.") THEN
                          IF NOT CONFIRM(Text1100001 +
                                         Text1100002,
                                         FALSE,
                                         "Bill Group".FIELDCAPTION("Bank Account No."),
                                         "Bill Group".TABLECAPTION) THEN
                             ERROR(Text1100003);

                        OutText := '03' + PADSTR(BankSuffix,5,'0') + NameCompany + FORMAT(DeliveryDate,0,Text1100004) + PADSTR('',189,' ');
                        OutFile.WRITE(OutText);
                      END;

        OnAfterGetRecord=BEGIN
                           IF Factoring = Factoring::" " THEN
                             ERROR(Text1100005,"No.");

                           TESTFIELD("Bank Account No.");
                           BankAcc.GET("Bill Group"."Bank Account No.");

                           CCCBankNo := BankAcc."CCC Bank No.";
                           CCCBankBranchNo := BankAcc."CCC Bank Branch No.";
                           CCCAccNo := BankAcc."CCC Bank Account No.";
                           CCCControlDigits := BankAcc."CCC Control Digits";

                           IF (CCCBankNo = '') OR (CCCBankBranchNo = '') OR
                              (CCCControlDigits = '') OR (CCCAccNo = '')
                           THEN
                             ERROR(Text1100006,BankAcc."No.");

                           CCCBankNo := PADSTR('',MAXSTRLEN(CCCBankNo) - STRLEN(CCCBankNo),'0') + CCCBankNo;
                           CCCBankBranchNo := PADSTR('',MAXSTRLEN(CCCBankBranchNo) - STRLEN(CCCBankBranchNo),'0') + CCCBankBranchNo;
                           CCCAccNo := PADSTR('',MAXSTRLEN(CCCAccNo) - STRLEN(CCCAccNo),'0') + CCCAccNo;
                           IF CCCControlDigits = '' THEN
                             CCCControlDigits := PADSTR('',MAXSTRLEN(CCCControlDigits),' ')
                           ELSE
                             CCCControlDigits := PADSTR('',MAXSTRLEN(CCCControlDigits) - STRLEN(CCCControlDigits),'0') + CCCControlDigits;

                           NoBillGroup := NoBillGroup + 1;

                           OutText := '06' + (PADSTR('',5 -  STRLEN(FORMAT(NoBillGroup)),'0') + FORMAT(NoBillGroup))
                              + FORMAT("Posting Date",0,Text1100007) + PADSTR('',219,' ');
                           OutFile.WRITE(OutText);
                         END;

        OnPostDataItem=BEGIN
                         TotalRmgAmount := FORMAT(TotalAmount,15,Text1100008);

                         IF TotalRmgAmount <> DELCHR(TotalRmgAmount,'=','.') THEN
                           TotalRmgAmount := DELCHR(TotalRmgAmount,'=','.')
                         ELSE
                           TotalRmgAmount := FORMAT(TotalAmount * 100,15,Text1100008);

                         TotalRmgAmount := CONVERTSTR(TotalRmgAmount,' ','0');

                         OutText := '66' + (PADSTR('',4 - STRLEN(FORMAT(TotalDocCust)),'0') + FORMAT(TotalDocCust))
                           + PADSTR('',4,'0') + TotalRmgAmount + PADSTR('',15,'0') + PADSTR('',192,' ');
                         OutFile.WRITE(OutText);


                         OutText := '99' + (PADSTR('',4 - STRLEN(FORMAT(NoBillGroup)),'0') + FORMAT(NoBillGroup)) +
                           PADSTR('',226,' ');
                         OutFile.WRITE(OutText);

                         OutFile.CLOSE;
                         IF ISSERVICETIER THEN
                           DOWNLOAD(ExternalFile,'','C:',Text10701,ToFile);
                       END;

        ReqFilterFields=No.;
      }
      SECTIONS
      {
      }
       }
    { PROPERTIES
      {
        DataItemIndent=1;
        DataItemTable=Table7000002;
        DataItemTableView=SORTING(Type,Bill Gr./Pmt. Order No.,Collection Agent,Due Date,Global Dimension 1 Code,Global Dimension 2 Code,Category Code,Posting Date,Document No.,Accepted,Currency Code,Document Type);
        OnAfterGetRecord=BEGIN
                           TESTFIELD("Account No.");
                           Customer.GET("Account No.");
                           IF "Document Type" = "Document Type"::Invoice THEN BEGIN
                             TotalDocCust := TotalDocCust + 1;
                             IF PaymentMethod.GET("Payment Method Code") THEN
                               DocType := ' '
                             ELSE BEGIN
                               CASE PaymentMethod."Bill Type" OF
                                 1: DocType := Text1100009;

                                 2: DocType := Text1100010;

                                 3: DocType := Text1100011;

                                 4: DocType := Text1100012;

                                 5: DocType := Text1100013;
                                 ELSE
                                      DocType := ' ';
                                 END;
                             END;
                             RmgAmount := FORMAT("Remaining Amount",15,Text1100008);

                             IF RmgAmount <> DELCHR(RmgAmount,'=','.') THEN
                               RmgAmount := DELCHR(RmgAmount,'=','.')
                             ELSE
                               RmgAmount := FORMAT("Remaining Amount" * 100,15,Text1100008);

                             RmgAmount := CONVERTSTR(RmgAmount,' ','0');

                             TotalAmount := TotalAmount + "Remaining Amount";
                             CustPostCode := DELSTR(Customer."Post Code",1,STRPOS(Customer."Post Code",'-'));

                             GetCurrencyCode("Cartera Doc.",CurrCode);

                             OutText := '09' + PADSTR('',5,'0') + PADSTR(Customer.Name,30,' ') + PADSTR(Customer."VAT Registration No.",14,' ')
                               + PADSTR(Customer.Address,30,' ') + PADSTR(Customer.City,30,' ') +
                                (PADSTR('',5 - STRLEN(CustPostCode),'0') + CustPostCode) + Text1100014 + PADSTR("Document No.",10,' ') + PADSTR('',10,' ') +
                               FORMAT("Posting Date",0,Text1100007)
                               + FORMAT("Due Date",0,Text1100007) + RmgAmount + PADSTR('',2,'0') + PADSTR('',2,'0') + PADSTR('',2,'0')
                               + CCCBankNo + CCCBankBranchNo + CCCAccNo + DocType + PADSTR("Account No.",15,' ') + CCCControlDigits
                               + Text1100015 + CurrCode + PADSTR('',21,' ');
                             OutFile.WRITE(OutText);
                           END;
                         END;

        DataItemLink=Bill Gr./Pmt. Order No.=FIELD(No.);
      }
      SECTIONS
      {
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=8910;
      Height=1760;
      SaveValues=Yes;
      OnOpenForm=BEGIN
                   IF ExternalFile = '' THEN
                     ExternalFile := 'C:\' + Text10702;
                   IF DeliveryDate = 0D THEN
                     DeliveryDate := TODAY;
                   "Bill Group".SETFILTER("Bill Group".Factoring,'<>%1',"Bill Group".Factoring::" ");
                   RequestOptionsForm.FileNameControl.VISIBLE := NOT ISSERVICETIER;
                 END;

    }
    CONTROLS
    {
      { 1   ;TextBox      ;3410 ;0    ;5500 ;440  ;Name=FileNameControl;
                                                   CaptionML=[ENU=External file;
                                                              ESP=Archivo externo];
                                                   SourceExpr=ExternalFile;
                                                   OnAssistEdit=BEGIN
                                                                  ExternalFile := CommonDialogMgt.OpenFile(Text10700,ExternalFile,4,Text10701,1);
                                                                END;
                                                                 }
      { 2   ;Label        ;0    ;0    ;3300 ;440  ;ParentControl=1 }
      { 4   ;TextBox      ;3410 ;1320 ;1650 ;440  ;CaptionML=[ENU=Bank Suffix;
                                                              ESP=Sufijo banco];
                                                   SourceExpr=BankSuffix;
                                                   TableRelation=Suffix.Suffix;
                                                   OnLookup=VAR
                                                              Suffix@1100002 : Record 7000024;
                                                              Suffixes@1100003 : Form 7000073;
                                                            BEGIN
                                                              Suffixes.SETTABLEVIEW(Suffix);
                                                              Suffixes.SETRECORD(Suffix);
                                                              Suffixes.LOOKUPMODE(TRUE);
                                                              Suffixes.EDITABLE(FALSE);
                                                              IF Suffixes.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                Suffixes.GETRECORD(Suffix);
                                                                BankSuffixBankAcc := Suffix."Bank Acc. Code";
                                                                BankSuffix := Suffix.Suffix;
                                                              END;
                                                            END;
                                                             }
      { 5   ;Label        ;0    ;1320 ;3300 ;440  ;ParentControl=4 }
      { 1100002;TextBox   ;3410 ;660  ;1650 ;440  ;CaptionML=[ENU=Delivery date;
                                                              ESP=Fecha de emisi¢n];
                                                   SourceExpr=DeliveryDate }
      { 1100003;Label     ;0    ;660  ;3300 ;440  ;ParentControl=1100002 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnInit=BEGIN
               FileNameControlVisible := TRUE;
             END;

      OnOpenPage=BEGIN
                   IF ExternalFile = '' THEN
                     ExternalFile := 'C:\' + Text10702;
                   IF DeliveryDate = 0D THEN
                     DeliveryDate := TODAY;
                   "Bill Group".SETFILTER("Bill Group".Factoring,'<>%1',"Bill Group".Factoring::" ");
                   FileNameControlVisible := NOT ISSERVICETIER;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones] }

      { 1   ;2   ;Field     ;
                  Name=FileNameControl;
                  CaptionML=[ENU=External file;
                             ESP=Archivo externo];
                  SourceExpr=ExternalFile;
                  Visible=FileNameControlVisible;
                  OnAssistEdit=BEGIN
                                 ExternalFile := CommonDialogMgt.OpenFile(Text10700,ExternalFile,4,Text10701,1);
                               END;
                                }

      { 1100002;2;Field     ;
                  CaptionML=[ENU=Delivery date;
                             ESP=Fecha de emisi¢n];
                  SourceExpr=DeliveryDate }

      { 4   ;2   ;Field     ;
                  CaptionML=[ENU=Bank Suffix;
                             ESP=Sufijo banco];
                  SourceExpr=BankSuffix;
                  TableRelation=Suffix.Suffix;
                  OnLookup=VAR
                             Suffix@1100002 : Record 7000024;
                             Suffixes@1100003 : Form 7000073;
                           BEGIN
                             Suffixes.SETTABLEVIEW(Suffix);
                             Suffixes.SETRECORD(Suffix);
                             Suffixes.LOOKUPMODE(TRUE);
                             Suffixes.EDITABLE(FALSE);
                             IF Suffixes.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               Suffixes.GETRECORD(Suffix);
                               BankSuffixBankAcc := Suffix."Bank Acc. Code";
                               BankSuffix := Suffix.Suffix;
                             END;
                           END;
                            }

    }
  }
  CODE
  {
    VAR
      Text1100000@1000 : TextConst 'ENU=Bank Suffix cannot be blank.;ESP=Sufijo bancario no puede ser blanco.';
      Text1100001@1001 : TextConst 'ENU=The Bank Suffix selected belongs to a %1  different than the %2. \;ESP=El sufijo bancario seleccionado pertenece a %1 diferente de %2. \';
      Text1100002@1002 : TextConst 'ENU=Do you want to continue?.;ESP=¨Quiere continuar?';
      Text1100003@1003 : TextConst 'ENU=Process cancelled by request of user.;ESP=Proceso cancelado a peticion del usuario.';
      Text1100004@1004 : TextConst 'ENU=<year><Month,2><Day,2>;ESP=<year><Month,2><Day,2>';
      Text1100005@1005 : TextConst 'ENU=Bill Group %1 cannot be sent to factoring;ESP=No se puede enviar la remesa %1 a factoring';
      Text1100006@1006 : TextConst 'ENU=Some data for Bank Account %1 are missing.;ESP=Falta informaci¢n en el banco %1.';
      Text1100007@1007 : TextConst 'ENU=<Year><Month,2><Day,2>;ESP=<Year><Month,2><Day,2>';
      Text1100008@1008 : TextConst 'ENU=<Integer><Decimals>;ESP=<Integer><Decimals>';
      Text1100009@1009 : TextConst 'ENU=B;ESP=B';
      Text1100010@1010 : TextConst 'ENU=R;ESP=R';
      Text1100011@1011 : TextConst 'ENU=I;ESP=I';
      Text1100012@1012 : TextConst 'ENU=C;ESP=C';
      Text1100013@1013 : TextConst 'ENU=T;ESP=T';
      Text1100014@1014 : TextConst 'ENU=F;ESP=F';
      Text1100015@1015 : TextConst 'ENU=ES;ESP=ES';
      CompanyInfo@1100000 : Record 79;
      Customer@1100001 : Record 18;
      BankAcc@1100002 : Record 270;
      CustBankAcc@1100003 : Record 287;
      PaymentMethod@1100004 : Record 289;
      GLSetup@1100005 : Record 98;
      DocMisc@1100006 : Codeunit 7000007;
      OutFile@1100007 : File;
      ExternalFile@1100008 : Text[1024];
      CheckErrors@1100009 : Boolean;
      CCCBankNo@1100010 : Text[4];
      VATRegCust@1100011 : Text[12];
      CCCBankBranchNo@1100012 : Text[4];
      CustCCCBankBranchNo@1100013 : Text[4];
      CCCControlDigits@1100014 : Text[2];
      CustCCCControlDigits@1100015 : Text[2];
      CCCAccNo@1100016 : Text[10];
      CustCCCAccNo@1100017 : Text[10];
      BillGrNo@1100018 : Code[4];
      TotalDoc@1100019 : Decimal;
      TotalDocCust@1100020 : Integer;
      TotalAmount@1100021 : Decimal;
      OutText@1100022 : Text[250];
      DeliveryDate@1100023 : Date;
      Rel@1100024 : Text[1];
      DocType@1100025 : Text[2];
      RmgAmount@1100026 : Text[15];
      CustBankAccCode@1100027 : Code[20];
      NameCompany@1100028 : Text[50];
      NoBillGroup@1100029 : Integer;
      TotalRmgAmount@1100030 : Text[15];
      CustPostCode@1100031 : Text[5];
      CurrCode@1100032 : Text[3];
      BankSuffixBankAcc@1100033 : Code[10];
      BankSuffix@1100034 : Code[3];
      RBMgt@1100035 : Codeunit 419;
      ToFile@1100036 : Text[1024];
      CommonDialogMgt@1100037 : Codeunit 412;
      Text10700@1100038 : TextConst 'ENU=Export to file;ESP=Exportar a archivo';
      Text10701@1100039 : TextConst 'ENU=ASC Files (*.asc)|*.asc|All Files (*.*)|*.*;ESP=Archivos ASC (*.asc)|*.asc|Todos los archivos (*.*)|*.*';
      Text10702@1100040 : TextConst 'ENU=FACTORING.ASC;ESP=FACTORING.ASC';
      FileNameControlVisible@19024830 : Boolean INDATASET;

    PROCEDURE GetCurrencyCode@2(Doc@1100000 : Record 7000002;VAR Code@1100001 : Text[3]);
    BEGIN
      GLSetup.GET;
      IF Doc."Currency Code" = '' THEN
        Code := COPYSTR(GLSetup."LCY Code",1,3)
      ELSE BEGIN
        IF STRLEN(Doc."Currency Code") > 3 THEN
          Code := COPYSTR(Doc."Currency Code",1,3)
        ELSE
          Code := PADSTR(Doc."Currency Code",3,' ');
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
