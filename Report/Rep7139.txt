OBJECT Report 7139 Date Comp. Item Budget Entries
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Date Compr. Item Budget Entries;
               ESP=Fecha compres. movs. ppto. pdto.];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  DimSelectionBuf.CompareDimText(
                    3,REPORT::"Date Comp. Item Budget Entries",'',RetainDimText,Text009);
                END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table7134;
        DataItemTableView=SORTING(Analysis Area,Budget Name,Item No.,Date);
        OnPreDataItem=VAR
                        GLSetup@1000 : Record 98;
                      BEGIN
                        IF NOT
                           CONFIRM(
                             Text000 +
                             Text001,FALSE)
                        THEN
                          CurrReport.BREAK;

                        IF EntrdDateComprReg."Ending Date" = 0D THEN
                          ERROR(
                            Text002,
                            EntrdDateComprReg.FIELDCAPTION("Ending Date"));

                        DateComprReg.INIT;
                        DateComprReg."Starting Date" := EntrdDateComprReg."Starting Date";
                        DateComprReg."Ending Date" := EntrdDateComprReg."Ending Date";
                        DateComprReg."Period Length" := EntrdDateComprReg."Period Length";

                        IF AnalysisView.FIND('-') THEN BEGIN
                          AnalysisView.CheckDimensionsAreRetained(3,REPORT::"Date Comp. Item Budget Entries",TRUE);
                          AnalysisView.CheckViewsAreUpdated;
                          COMMIT;
                        END;

                        SelectedDim.GetSelectedDim(
                          USERID,3,REPORT::"Date Comp. Item Budget Entries",'',TempSelectedDim);
                        GLSetup.GET;
                        Retain[2] :=
                          TempSelectedDim.GET(
                            USERID,3,REPORT::"Date Comp. Item Budget Entries",'',GLSetup."Global Dimension 1 Code");
                        Retain[3] :=
                          TempSelectedDim.GET(
                            USERID,3,REPORT::"Date Comp. Item Budget Entries",'',GLSetup."Global Dimension 2 Code");

                        ItemBudgetEntry2.LOCKTABLE;
                        IF ItemBudgetEntry2.FIND('+') THEN;
                        LastEntryNo := ItemBudgetEntry2."Entry No.";
                        LowestEntryNo := 2147483647;

                        Window.OPEN(
                          Text003 +
                          Text004 +
                          Text005 +
                          Text006 +
                          Text007 +
                          Text008);

                        SETRANGE("Entry No.",0,LastEntryNo);
                        SETRANGE(Date,EntrdDateComprReg."Starting Date",EntrdDateComprReg."Ending Date");
                        SETRANGE("Analysis Area",AnalysisAreaSelection);
                      END;

        OnAfterGetRecord=BEGIN
                           ItemBudgetEntry2 := "Item Budget Entry";
                           WITH ItemBudgetEntry2 DO BEGIN
                             SETCURRENTKEY("Analysis Area","Budget Name","Item No.",Date);
                             COPYFILTERS("Item Budget Entry");
                             SETFILTER(Date,DateComprMgt.GetDateFilter(Date,EntrdDateComprReg,FALSE));
                             SETRANGE("Analysis Area","Analysis Area");
                             SETRANGE("Budget Name","Budget Name");
                             SETRANGE("Item No.","Item No.");

                             LastEntryNo := LastEntryNo + 1;

                             IF RetainNo(FIELDNO("Global Dimension 1 Code")) THEN
                               SETRANGE("Global Dimension 1 Code","Global Dimension 1 Code");
                             IF RetainNo(FIELDNO("Global Dimension 2 Code")) THEN
                               SETRANGE("Global Dimension 2 Code","Global Dimension 2 Code");
                             IF Quantity >= 0 THEN
                               SETFILTER(Quantity,'>=0')
                             ELSE
                               SETFILTER(Quantity,'<0');
                             IF "Cost Amount" >= 0 THEN
                               SETFILTER("Cost Amount",'>=0')
                             ELSE
                               SETFILTER("Cost Amount",'<0');
                             IF "Sales Amount" >= 0 THEN
                               SETFILTER("Sales Amount",'>=0')
                             ELSE
                               SETFILTER("Sales Amount",'<0');

                             InitNewEntry(NewItemBudgetEntry);

                             DimBufMgt.CollectItemBudgetDimEntryNo(
                               TempSelectedDim,"Entry No.",
                               0,FALSE,DimEntryNo);
                             ComprDimEntryNo := DimEntryNo;
                             SummarizeEntry(NewItemBudgetEntry,ItemBudgetEntry2);
                             WHILE NEXT <> 0 DO BEGIN
                               DimBufMgt.CollectItemBudgetDimEntryNo(
                                 TempSelectedDim,"Entry No.",
                                 ComprDimEntryNo,TRUE,DimEntryNo);
                               IF DimEntryNo = ComprDimEntryNo THEN
                                 SummarizeEntry(NewItemBudgetEntry,ItemBudgetEntry2);
                             END;

                             InsertNewEntry(NewItemBudgetEntry,ComprDimEntryNo);

                             ComprCollectedEntries;
                           END;

                           IF DateComprReg."No. Records Deleted" >= NoOfDeleted + 10 THEN BEGIN
                             NoOfDeleted := DateComprReg."No. Records Deleted";
                             COMMIT;
                             ItemBudgetEntry2.LOCKTABLE;
                             ItemBudgetEntry2.RESET;
                             IF ItemBudgetEntry2.FIND('+') THEN;
                             LastEntryNo := ItemBudgetEntry2."Entry No.";
                           END;
                         END;

        OnPostDataItem=VAR
                         UpdateAnalysisView@1000 : Codeunit 410;
                       BEGIN
                         IF AnalysisView.FIND('-') THEN
                           IF LowestEntryNo < 2147483647 THEN
                             UpdateAnalysisView.SetLastBudgetEntryNo(LowestEntryNo - 1);
                       END;

        ReqFilterFields=Budget Name,Item No.;
      }
      SECTIONS
      {
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=8910;
      Height=3410;
      SaveValues=Yes;
      OnOpenForm=BEGIN
                   IF EntrdDateComprReg."Ending Date" = 0D THEN
                     EntrdDateComprReg."Ending Date" := TODAY;

                   WITH "Item Budget Entry" DO BEGIN
                     InsertField(FIELDNO("Global Dimension 1 Code"),FIELDCAPTION("Global Dimension 1 Code"));
                     InsertField(FIELDNO("Global Dimension 2 Code"),FIELDCAPTION("Global Dimension 2 Code"));
                   END;

                   RetainDimText := DimSelectionBuf.GetDimSelectionText(3,REPORT::"Date Comp. Item Budget Entries",'');
                 END;

    }
    CONTROLS
    {
      { 1   ;TextBox      ;3410 ;660  ;1650 ;440  ;CaptionML=[ENU=Starting Date;
                                                              ESP=Fecha inicial];
                                                   ClosingDates=Yes;
                                                   SourceExpr=EntrdDateComprReg."Starting Date" }
      { 21  ;Label        ;0    ;660  ;3300 ;440  ;ParentControl=1 }
      { 2   ;TextBox      ;3410 ;1210 ;1650 ;440  ;CaptionML=[ENU=Ending Date;
                                                              ESP=Fecha final];
                                                   ClosingDates=Yes;
                                                   SourceExpr=EntrdDateComprReg."Ending Date" }
      { 22  ;Label        ;0    ;1210 ;3300 ;440  ;ParentControl=2 }
      { 3   ;TextBox      ;3410 ;1760 ;2750 ;440  ;CaptionML=[ENU=Period Length;
                                                              ESP=Longitud periodo];
                                                   OptionCaptionML=[ENU=Day,Week,Month,Quarter,Year,Period;
                                                                    ESP=D¡a,Semana,Mes,Trimestre,A¤o,Periodo];
                                                   OptionString=Day,Week,Month,Quarter,Year,Period;
                                                   SourceExpr=EntrdDateComprReg."Period Length" }
      { 23  ;Label        ;0    ;1760 ;3300 ;440  ;ParentControl=3 }
      { 4   ;TextBox      ;3410 ;2310 ;5500 ;440  ;CaptionML=[ENU=Posting Description;
                                                              ESP=Texto de registro];
                                                   SourceExpr=EntrdItemBudgetEntry.Description }
      { 24  ;Label        ;0    ;2310 ;3300 ;440  ;ParentControl=4 }
      { 6   ;TextBox      ;3410 ;2970 ;5500 ;440  ;Editable=No;
                                                   PermanentAssist=Yes;
                                                   CaptionML=[ENU=Retain Dimensions;
                                                              ESP=Retener dimensiones];
                                                   SourceExpr=RetainDimText;
                                                   OnAssistEdit=BEGIN
                                                                  DimSelectionBuf.SetDimSelectionMultiple(3,REPORT::"Date Comp. Item Budget Entries",RetainDimText);
                                                                END;
                                                                 }
      { 7   ;Label        ;0    ;2970 ;3300 ;440  ;ParentControl=6 }
      { 5   ;TextBox      ;3410 ;0    ;2750 ;440  ;CaptionML=[ENU=Analysis Area;
                                                              ESP=µrea an lisis];
                                                   OptionCaptionML=[ENU=Sales,Purchase;
                                                                    ESP=Ventas,Compra];
                                                   OptionString=Sales,Purchase;
                                                   SourceExpr=AnalysisAreaSelection }
      { 8   ;Label        ;0    ;0    ;3300 ;440  ;ParentControl=5 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF EntrdDateComprReg."Ending Date" = 0D THEN
                     EntrdDateComprReg."Ending Date" := TODAY;

                   WITH "Item Budget Entry" DO BEGIN
                     InsertField(FIELDNO("Global Dimension 1 Code"),FIELDCAPTION("Global Dimension 1 Code"));
                     InsertField(FIELDNO("Global Dimension 2 Code"),FIELDCAPTION("Global Dimension 2 Code"));
                   END;

                   RetainDimText := DimSelectionBuf.GetDimSelectionText(3,REPORT::"Date Comp. Item Budget Entries",'');
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESP=Opciones] }

      { 5   ;2   ;Field     ;
                  CaptionML=[ENU=Analysis Area;
                             ESP=µrea an lisis];
                  OptionCaptionML=[ENU=Sales,Purchase;
                                   ESP=Ventas,Compra];
                  SourceExpr=AnalysisAreaSelection }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Starting Date;
                             ESP=Fecha inicial];
                  ClosingDates=Yes;
                  SourceExpr=EntrdDateComprReg."Starting Date" }

      { 2   ;2   ;Field     ;
                  CaptionML=[ENU=Ending Date;
                             ESP=Fecha final];
                  ClosingDates=Yes;
                  SourceExpr=EntrdDateComprReg."Ending Date" }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Period Length;
                             ESP=Longitud periodo];
                  OptionCaptionML=[ENU=Day,Week,Month,Quarter,Year,Period;
                                   ESP=D¡a,Semana,Mes,Trimestre,A¤o,Periodo];
                  SourceExpr=EntrdDateComprReg."Period Length" }

      { 4   ;2   ;Field     ;
                  CaptionML=[ENU=Posting Description;
                             ESP=Texto de registro];
                  SourceExpr=EntrdItemBudgetEntry.Description }

      { 6   ;2   ;Field     ;
                  CaptionML=[ENU=Retain Dimensions;
                             ESP=Retener dimensiones];
                  SourceExpr=RetainDimText;
                  Editable=FALSE;
                  OnAssistEdit=BEGIN
                                 DimSelectionBuf.SetDimSelectionMultiple(3,REPORT::"Date Comp. Item Budget Entries",RetainDimText);
                               END;
                                }

    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=This batch job deletes entries.\\;ESP=Este proceso borra movimientos.\\';
      Text001@1001 : TextConst 'ENU=Do you want to date compress the entries?;ESP=¨Confirma que desea comprimir los movimientos?';
      Text002@1002 : TextConst 'ENU=%1 must be specified.;ESP=Se debe indicar %1.';
      Text003@1003 : TextConst 'ENU=Date compressing Item budget entries...\\;ESP=Fecha compres. movs. ppto. pdto...\\';
      Text004@1004 : TextConst 'ENU=Budget Name          #1##########\;ESP=Nombre presupuesto   #1##########\';
      Text005@1005 : TextConst 'ENU=Item No.             #2##########\;ESP=N§ de producto       #2##########\';
      Text006@1006 : TextConst 'ENU=Date                 #3######\\;ESP=Fecha                #3######\\';
      Text007@1007 : TextConst 'ENU=No. of new entries   #4######\;ESP=Nuevo n§ movs.       #4######\';
      Text008@1008 : TextConst 'ENU=No. of entries del.  #5######;ESP=N§ movs. borrados    #5######';
      Text009@1009 : TextConst 'ENU=Retain Dimensions;ESP=Retener dimensiones';
      DateComprReg@1010 : Record 87;
      EntrdDateComprReg@1011 : Record 87;
      EntrdItemBudgetEntry@1012 : Record 7134;
      NewItemBudgetEntry@1013 : Record 7134;
      ItemBudgetEntry2@1014 : Record 7134;
      SelectedDim@1015 : Record 369;
      TempSelectedDim@1016 : TEMPORARY Record 369;
      DimSelectionBuf@1017 : Record 368;
      AnalysisView@1018 : Record 7152;
      DateComprMgt@1019 : Codeunit 356;
      DimBufMgt@1020 : Codeunit 411;
      DimMgt@1021 : Codeunit 408;
      Window@1022 : Dialog;
      NoOfFields@1023 : Integer;
      Retain@1024 : ARRAY [10] OF Boolean;
      FieldNumber@1025 : ARRAY [10] OF Integer;
      FieldNameArray@1026 : ARRAY [10] OF Text[100];
      LastEntryNo@1027 : Integer;
      LowestEntryNo@1028 : Integer;
      NoOfDeleted@1029 : Integer;
      i@1030 : Integer;
      ComprDimEntryNo@1031 : Integer;
      DimEntryNo@1032 : Integer;
      RetainDimText@1033 : Text[250];
      AnalysisAreaSelection@1034 : 'Sales,Purchase';

    LOCAL PROCEDURE InsertField@2(Number@1000 : Integer;Name@1001 : Text[100]);
    BEGIN
      NoOfFields := NoOfFields + 1;
      FieldNumber[NoOfFields] := Number;
      FieldNameArray[NoOfFields] := Name;
    END;

    LOCAL PROCEDURE RetainNo@3(Number@1000 : Integer) : Boolean;
    BEGIN
      EXIT(Retain[Index(Number)]);
    END;

    LOCAL PROCEDURE Index@6(Number@1000 : Integer) : Integer;
    BEGIN
      FOR i := 1 TO NoOfFields DO
        IF Number = FieldNumber[i] THEN
          EXIT(i);
    END;

    PROCEDURE SummarizeEntry@5(VAR NewItemBudgetEntry@1000 : Record 7134;ItemBudgetEntry@1001 : Record 7134);
    VAR
      ItemBudgetDim@1002 : Record 7135;
    BEGIN
      WITH ItemBudgetEntry DO BEGIN
        NewItemBudgetEntry.Quantity := NewItemBudgetEntry.Quantity + Quantity;
        NewItemBudgetEntry."Cost Amount" := NewItemBudgetEntry."Cost Amount" + "Cost Amount";
        NewItemBudgetEntry."Sales Amount" := NewItemBudgetEntry."Sales Amount" + "Sales Amount";
        DELETE;
        IF "Entry No." < LowestEntryNo THEN
          LowestEntryNo := "Entry No.";
        ItemBudgetDim.RESET;
        ItemBudgetDim.SETRANGE("Entry No.","Entry No.");
        ItemBudgetDim.DELETEALL;
        DateComprReg."No. Records Deleted" := DateComprReg."No. Records Deleted" + 1;
        Window.UPDATE(5,DateComprReg."No. Records Deleted");
      END;
    END;

    PROCEDURE ComprCollectedEntries@12();
    VAR
      ItemBudgetEntry@1000 : Record 7134;
      OldDimEntryNo@1001 : Integer;
      Found@1002 : Boolean;
      ItemBudgetEntryNo@1003 : Integer;
    BEGIN
      OldDimEntryNo := 0;
      IF DimBufMgt.FindFirstDimEntryNo(DimEntryNo,ItemBudgetEntryNo) THEN BEGIN
        InitNewEntry(NewItemBudgetEntry);
        REPEAT
          ItemBudgetEntry.GET(ItemBudgetEntryNo);
          SummarizeEntry(ItemBudgetEntry,ItemBudgetEntry);
          OldDimEntryNo := DimEntryNo;
          Found := DimBufMgt.NextDimEntryNo(DimEntryNo,ItemBudgetEntryNo);
          IF (OldDimEntryNo <> DimEntryNo) OR NOT Found THEN BEGIN
            InsertNewEntry(NewItemBudgetEntry,OldDimEntryNo);
            IF Found THEN
              InitNewEntry(NewItemBudgetEntry);
          END;
          OldDimEntryNo := DimEntryNo;
        UNTIL NOT Found;
      END;
      DimBufMgt.DeleteAllDimEntryNo;
    END;

    PROCEDURE InitNewEntry@7(VAR NewItemBudgetEntry@1000 : Record 7134);
    BEGIN
      LastEntryNo := LastEntryNo + 1;

      WITH ItemBudgetEntry2 DO BEGIN
        NewItemBudgetEntry.INIT;
        NewItemBudgetEntry."Entry No." := LastEntryNo;
        NewItemBudgetEntry."Analysis Area" := AnalysisAreaSelection;
        NewItemBudgetEntry."Budget Name" := "Budget Name";
        NewItemBudgetEntry."Item No." := "Item No.";
        NewItemBudgetEntry.Date := GETRANGEMIN(Date);
        NewItemBudgetEntry.Description := EntrdItemBudgetEntry.Description;
        NewItemBudgetEntry."User ID" := USERID;

        IF RetainNo(FIELDNO("Global Dimension 1 Code")) THEN
          NewItemBudgetEntry."Global Dimension 1 Code" := "Global Dimension 1 Code";
        IF RetainNo(FIELDNO("Global Dimension 2 Code")) THEN
          NewItemBudgetEntry."Global Dimension 2 Code" := "Global Dimension 2 Code";

        Window.UPDATE(1,NewItemBudgetEntry."Budget Name");
        Window.UPDATE(2,NewItemBudgetEntry."Item No.");
        Window.UPDATE(3,NewItemBudgetEntry.Date);
        DateComprReg."No. of New Records" := DateComprReg."No. of New Records" + 1;
        Window.UPDATE(4,DateComprReg."No. of New Records");
      END;
    END;

    PROCEDURE InsertNewEntry@23(VAR NewItemBudgetEntry@1000 : Record 7134;DimEntryNo@1001 : Integer);
    VAR
      TempDimBuf@1002 : TEMPORARY Record 360;
    BEGIN
      NewItemBudgetEntry.INSERT;
      TempDimBuf.DELETEALL;
      DimBufMgt.GetDimensions(DimEntryNo,TempDimBuf);
      DimMgt.MoveDimBufToItemBudgetDim(TempDimBuf,NewItemBudgetEntry."Entry No.");
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}
