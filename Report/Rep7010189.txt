OBJECT Report 7010189 CNT-MEX-Calculo comision GV
{
  OBJECT-PROPERTIES
  {
    Date=28/07/14;
    Time=16:05:11;
    Modified=Yes;
    Version List=AIC2009,CNT-OC-14047;
  }
  PROPERTIES
  {
    CaptionML=[ENU=CNT-Sale Guide commission summary;
               ESP=CNT-Resumen comision Guia Venta];
    OnInitReport=BEGIN
                   wDiaInicio := CALCDATE('<-1M>', TODAY);
                   wDiaInicio := DMY2DATE(1, DATE2DMY(wDiaInicio, 2), DATE2DMY(wDiaInicio, 3));

                   wDiaFin := CALCDATE('<+1M-1D>', wDiaInicio);
                 END;

    OnPreReport=BEGIN
                  rCompanyInfo.FINDFIRST;
                END;

    Orientation=Landscape;
  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table7010241;
        DataItemTableView=SORTING(Codigo)
                          WHERE(Bloqueado=CONST(No),
                                Trabaja a credito=CONST(No));
        NewPagePerRecord=Yes;
        DataItemVarName=diGuia;
        PrintOnlyIfDetail=Yes;
        OnPreDataItem=BEGIN
                        IF (wDiaInicio = 0D) OR
                           (wDiaFin = 0D) THEN
                          ERROR(Text001);

                        IF wDiaInicio > wDiaFin THEN
                          ERROR(Text002);

                        //+$002 <
                        wTVentasGuia     := 0;
                        wTComisionGuia   := 0;
                        wTComisionGuiaDL := 0;
                        //+$002 >
                      END;

        OnAfterGetRecord=BEGIN
                           wID := 0;

                           CLEAR(rBufferTMP);
                           rBufferTMP.DELETEALL;

                           CLEAR(rEquipoTMP);
                           rEquipoTMP.DELETEALL;

                           IF "Trabaja en un Pool" THEN
                             _CalculaComisionPool
                           ELSE
                           BEGIN
                             _CalculaComisionPrincipal;
                             _CalculaComisionNormal(FALSE, wDiaInicio, wDiaFin, 0D, '');
                           END;
                         END;

        ReqFilterFields=Codigo;
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Header;
            PrintOnEveryPage=Yes;
            SectionWidth=22200;
            SectionHeight=1269;
          }
          CONTROLS
          {
            { 1100253001;Label  ;0    ;0    ;7500 ;423  ;FontSize=8;
                                                         FontBold=Yes;
                                                         CaptionML=[ENU=Sales guide commission summary;
                                                                    ESP=Resumen comisiones Guia venta] }
            { 1100253002;TextBox;19050;423  ;3150 ;423  ;HorzAlign=Right;
                                                         SourceExpr=FORMAT(TODAY,0,4) }
            { 1100253003;TextBox;0    ;846  ;7500 ;423  ;FontBold=Yes;
                                                         SourceExpr=rCompanyInfo.Name }
            { 1100253004;TextBox;21750;846  ;450  ;423  ;CaptionML=ESP=P g.;
                                                         SourceExpr=CurrReport.PAGENO }
            { 1100253005;Label  ;21000;846  ;750  ;423  ;ParentControl=1100253004 }
            { 1100253006;TextBox;18150;0    ;4050 ;423  ;HorzAlign=Right;
                                                         SourceExpr=USERID }
          }
           }
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=22200;
            SectionHeight=846;
          }
          CONTROLS
          {
            { 1100253008;TextBox;3150 ;0    ;6600 ;423  ;HorzAlign=Left;
                                                         SourceExpr=Nombre }
            { 1100253009;Label  ;0    ;0    ;3000 ;423  ;ParentControl=1100253008;
                                                         LeaderDots=Yes }
            { 1100217000;Label  ;0    ;423  ;3000 ;423  ;ParentControl=1100217001;
                                                         LeaderDots=Yes }
            { 1100217001;TextBox;3150 ;423  ;6600 ;423  ;HorzAlign=Left;
                                                         CaptionML=[ENU=Period;
                                                                    ESP=Periodo];
                                                         SourceExpr=FORMAT(wDiaInicio) + ' - ' + FORMAT(wDiaFin) }
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=1;
        DataItemTable=Table2000000026;
        DataItemTableView=SORTING(Number);
        DataItemVarName=diResumen;
        OnPreDataItem=BEGIN
                        wVentasGuia     := 0;
                        wComisionGuia   := 0;
                        wComisionGuiaDL := 0;

                        CLEAR(rBufferTMP);
                        rBufferTMP.SETCURRENTKEY("Fecha venta", Excursion, "Porcentaje comision");
                        SETRANGE(Number, 1, rBufferTMP.COUNT);
                        IF NOT rBufferTMP.FINDSET THEN
                          CurrReport.SKIP;
                      END;

        OnAfterGetRecord=BEGIN
                           IF Number <> 1 THEN
                             rBufferTMP.NEXT;

                           wVentasGuia     += rBufferTMP.Importe;
                           wComisionGuia   += rBufferTMP."Importe comision";
                           wComisionGuiaDL += rBufferTMP."Importe comision DL";
                         END;

        OnPostDataItem=BEGIN
                         //+$002 <
                         wTVentasGuia     += wVentasGuia;
                         wTComisionGuia   += wComisionGuia;
                         wTComisionGuiaDL += wComisionGuiaDL;
                         //+$002 >
                       END;

      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Header;
            PrintOnEveryPage=Yes;
            SectionWidth=22200;
            SectionHeight=846;
          }
          CONTROLS
          {
            { 1100253007;Label  ;0    ;0    ;5850 ;846  ;ParentControl=1100253000;
                                                         VertAlign=Bottom;
                                                         FontBold=Yes;
                                                         MultiLine=Yes }
            { 1100253015;Label  ;16350;0    ;900  ;846  ;ParentControl=1100253010;
                                                         HorzAlign=Right;
                                                         VertAlign=Bottom;
                                                         FontBold=Yes;
                                                         MultiLine=Yes }
            { 1100253016;Label  ;17400;0    ;1500 ;846  ;ParentControl=1100253011;
                                                         HorzAlign=Right;
                                                         VertAlign=Bottom;
                                                         FontBold=Yes;
                                                         MultiLine=Yes }
            { 1100253017;Label  ;19050;0    ;1350 ;846  ;ParentControl=1100253013;
                                                         HorzAlign=Right;
                                                         VertAlign=Bottom;
                                                         FontBold=Yes;
                                                         MultiLine=Yes }
            { 1100253019;Label  ;20550;0    ;1650 ;846  ;ParentControl=1100253014;
                                                         HorzAlign=Right;
                                                         VertAlign=Bottom;
                                                         FontBold=Yes;
                                                         MultiLine=Yes }
            { 1100217005;Label  ;6000 ;0    ;1200 ;846  ;ParentControl=1100217002;
                                                         VertAlign=Bottom;
                                                         FontBold=Yes;
                                                         MultiLine=Yes }
            { 1100217006;Label  ;7350 ;0    ;1650 ;846  ;ParentControl=1100217003;
                                                         VertAlign=Bottom;
                                                         FontBold=Yes;
                                                         MultiLine=Yes }
            { 1100217007;Label  ;11250;0    ;600  ;846  ;ParentControl=1100217004;
                                                         VertAlign=Bottom;
                                                         FontBold=Yes;
                                                         MultiLine=Yes }
            { 1100217008;Label  ;12000;0    ;4200 ;846  ;ParentControl=1100217009;
                                                         VertAlign=Bottom;
                                                         FontBold=Yes;
                                                         MultiLine=Yes }
            { 1100217010;Label  ;9150 ;0    ;1950 ;846  ;ParentControl=1100217011;
                                                         VertAlign=Bottom;
                                                         FontBold=Yes;
                                                         MultiLine=Yes }
          }
           }
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=22200;
            SectionHeight=423;
          }
          CONTROLS
          {
            { 1100253000;TextBox;0    ;0    ;5850 ;423  ;CaptionML=ESP=Excursion;
                                                         SourceExpr=rBufferTMP.Excursion + ' - ' + cNom.nom_producto(rBufferTMP.Excursion) }
            { 1100253010;TextBox;16350;0    ;900  ;423  ;CaptionML=ESP=% Comision;
                                                         SourceExpr=rBufferTMP."Porcentaje comision" }
            { 1100253011;TextBox;17400;0    ;1500 ;423  ;CaptionML=ESP=Ventas;
                                                         SourceExpr=rBufferTMP.Importe }
            { 1100253013;TextBox;19050;0    ;1350 ;423  ;CaptionML=ESP=Comision;
                                                         SourceExpr=rBufferTMP."Importe comision" }
            { 1100253014;TextBox;20550;0    ;1650 ;423  ;CaptionML=ESP=Comision (DL);
                                                         SourceExpr=rBufferTMP."Importe comision DL" }
            { 1100217002;TextBox;6000 ;0    ;1200 ;423  ;HorzAlign=Left;
                                                         CaptionML=ESP=Fecha venta;
                                                         SourceExpr=rBufferTMP."Fecha venta" }
            { 1100217003;TextBox;7350 ;0    ;1650 ;423  ;CaptionML=ESP=Hotel;
                                                         SourceExpr=rBufferTMP.Hotel }
            { 1100217004;TextBox;11250;0    ;600  ;423  ;CaptionML=ESP=Pool;
                                                         SourceExpr=rBufferTMP.Pool }
            { 1100217009;TextBox;12000;0    ;4200 ;423  ;CaptionML=ESP=Equipo;
                                                         SourceExpr=rBufferTMP.Equipo }
            { 1100217011;TextBox;9150 ;0    ;1950 ;423  ;CaptionML=ESP=Touroperador;
                                                         SourceExpr=rBufferTMP.Touroperador }
          }
           }
        { PROPERTIES
          {
            SectionType=Footer;
            SectionWidth=22200;
            SectionHeight=846;
          }
          CONTROLS
          {
            { 1100217013;Shape  ;17400;0    ;1500 ;423  ;ShapeStyle=HorzLine }
            { 1100217015;Shape  ;19050;0    ;1350 ;423  ;ShapeStyle=HorzLine }
            { 1100217016;TextBox;19050;423  ;1350 ;423  ;CaptionML=ESP=Comision;
                                                         SourceExpr=wComisionGuia }
            { 1100217017;TextBox;17400;423  ;1500 ;423  ;CaptionML=ESP=Ventas;
                                                         SourceExpr=wVentasGuia }
            { 1100217018;TextBox;20550;423  ;1650 ;423  ;CaptionML=ESP=Comision (DL);
                                                         SourceExpr=wComisionGuiaDL }
            { 1100217019;Shape  ;20550;0    ;1650 ;423  ;ShapeStyle=HorzLine }
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=2;
        DataItemTable=Table7010162;
        DataItemTableView=SORTING(Producto,TourOperador,Guia Venta,Oficina,Fecha Excursion,Fecha Venta,Anulada,Reembolsada);
        DataItemVarName=diDetalle;
        OnPreDataItem=BEGIN
                        diDetalle.SETRANGE ("Fecha Excursion", wDiaInicio, wDiaFin);
                        diDetalle.SETRANGE ("Fecha Venta"    , rBufferTMP."Fecha venta");
                        diDetalle.SETFILTER("Guia Venta"     , rBufferTMP.Equipo);
                        diDetalle.SETRANGE (Anulada          , FALSE);
                        diDetalle.SETRANGE (Reembolsada      , FALSE);
                        diDetalle.SETRANGE (TourOperador     , rBufferTMP.Touroperador);
                        diDetalle.SETRANGE (Producto         , rBufferTMP.Excursion);
                        diDetalle.SETRANGE ("Hotel Cliente"  , rBufferTMP.Hotel);
                        diDetalle.SETRANGE("No comisionable" , FALSE);       //+$003
                      END;

      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=22200;
            SectionHeight=423;
            OnPreSection=BEGIN
                           CurrReport.SHOWOUTPUT(wMostrarDetalle);
                         END;

          }
          CONTROLS
          {
            { 1100217012;TextBox;1500 ;0    ;2250 ;423  ;SourceExpr="N§ Ticket" }
            { 1100217014;TextBox;4050 ;0    ;1800 ;423  ;SourceExpr="Guia Venta" }
            { 1100217020;TextBox;6150 ;0    ;1800 ;423  ;SourceExpr="Ingreso comisionable neto" }
            { 1100217022;TextBox;8250 ;0    ;1500 ;423  ;SourceExpr="Comision Guia Venta" }
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemTable=Table2000000026;
        DataItemTableView=SORTING(Number);
        MaxIteration=1;
        DataItemVarName=diTotal;
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=22200;
            SectionHeight=846;
          }
          CONTROLS
          {
            { 1100217021;Shape  ;17400;0    ;4800 ;423  ;ShapeStyle=HorzLine }
            { 1100217023;TextBox;17400;423  ;1500 ;423  ;CaptionML=ESP=Ventas;
                                                         SourceExpr=wTVentasGuia }
            { 1100217024;TextBox;19050;423  ;1350 ;423  ;CaptionML=ESP=Comision;
                                                         SourceExpr=wTComisionGuia }
            { 1100217025;TextBox;20550;423  ;1650 ;423  ;CaptionML=ESP=Comision (DL);
                                                         SourceExpr=wTComisionGuiaDL }
            { 1100217026;Label  ;14550;423  ;2700 ;423  ;VertAlign=Bottom;
                                                         FontBold=Yes;
                                                         MultiLine=Yes;
                                                         LeaderDots=Yes;
                                                         CaptionML=[ENU=Grand total;
                                                                    ESP=Total general] }
          }
           }
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=9020;
      Height=3410;
      CaptionML=[ENU=CNT-Sale Guide commission summary;
                 ESP=CNT-Resumen comision Guia Venta];
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1100253000;TextBox;3630 ;220  ;1700 ;440  ;InPage=-1;
                                                   CaptionML=[ENU=From date;
                                                              ESP=Fecha inicio];
                                                   SourceExpr=wDiaInicio }
      { 1100253001;Label  ;220  ;220  ;3300 ;440  ;ParentControl=1100253000 }
      { 1100253002;Label  ;220  ;770  ;3300 ;440  ;ParentControl=1100253003;
                                                   InPage=-1 }
      { 1100253003;TextBox;3630 ;770  ;1700 ;440  ;CaptionML=[ENU=To date;
                                                              ESP=Fecha final];
                                                   SourceExpr=wDiaFin }
      { 1100217000;CheckBox;3630;1320 ;440  ;440  ;InPage=-1;
                                                   ShowCaption=No;
                                                   CaptionML=[ENU=View detail;
                                                              ESP=Mostrar detalle];
                                                   SourceExpr=wMostrarDetalle }
      { 1100217001;Label  ;220  ;1320 ;3300 ;440  ;ParentControl=1100217000 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      rCompanyInfo@1100253004 : Record 79;
      Text001@1100253002 : TextConst 'ENU=You must specify a date range.;ESP=Se debe especificar un periodo de fechas.';
      Text002@1100253003 : TextConst 'ENU=The period end date must be after start date.;ESP=La fecha de final del periodo debe ser posterior a la fecha de inicio.';
      rBufferTMP@1100253005 : TEMPORARY Record 88002;
      rEquipoTMP@1100217002 : TEMPORARY Record 7010249;
      rTipoCam@1100217000 : Record 330;
      cNom@1100217001 : Codeunit 7009702;
      wDiaInicio@1100253001 : Date;
      wDiaFin@1100253000 : Date;
      wID@1100253006 : Integer;
      wVentasGuia@1100253007 : Decimal;
      wComisionGuia@1100253008 : Decimal;
      wComisionGuiaDL@1100253009 : Decimal;
      wMostrarDetalle@1100217003 : Boolean;
      wTVentasGuia@1100217004 : Decimal;
      wTComisionGuia@1100217005 : Decimal;
      wTComisionGuiaDL@1100217006 : Decimal;

    LOCAL PROCEDURE _CalculaComisionPool@1100253000();
    VAR
      lrEquipo@1100253002 : Record 7010249;
      lrTMPFechas@1100217000 : TEMPORARY Record 7010162;
    BEGIN
      // _CalculaComisionPool
      IF NOT diGuia."Trabaja en un Pool" THEN
        EXIT;

      //. Mexico calcula las comisiones sobre la fecha de excursion, pero para la gesti¢n de los POOL
      //. Hay que seguir trabajando con la fecha de venta
      //. Vamos a recorrer las reservas con el filtro de fecha excursion y crearemos un temporal de fechas de venta
      _TemporalFechasVenta(lrTMPFechas);

      //. Para cada dia del periodo comprobamos en que pool esta trabajando el guia
      lrTMPFechas.RESET;
      IF lrTMPFechas.FINDSET THEN
      BEGIN
        REPEAT
          lrEquipo.RESET;
          lrEquipo.SETRANGE ("Guia venta" , diGuia.Codigo);
          lrEquipo.SETFILTER("Fecha desde", '<=%1', lrTMPFechas."Fecha Venta");
          lrEquipo.SETFILTER("Fecha hasta", '>=%1', lrTMPFechas."Fecha Venta");
          lrEquipo.SETRANGE (Hotel        , lrTMPFechas."Hotel Cliente");
          IF lrEquipo.FINDSET THEN
          BEGIN
            REPEAT
              IF NOT rEquipoTMP.GET(lrEquipo.ID) THEN
              BEGIN
                _CalculaComisionEquipo(lrEquipo.Hotel, lrTMPFechas."Fecha Venta");

                CLEAR(rEquipoTMP);
                rEquipoTMP.ID := lrEquipo.ID;
                rEquipoTMP.INSERT;
              END;
            UNTIL lrEquipo.NEXT = 0;
          END
          //. Si un dia no esta dentro de ningun equipo le calculamos solo su comision
          ELSE
            _CalculaComisionNormal(TRUE, wDiaInicio, wDiaFin, lrTMPFechas."Fecha Venta", lrTMPFechas."Hotel Cliente");
        UNTIL lrTMPFechas.NEXT = 0;
      END;

      //. Eliminamos las lineas que tienen el total de comision a cero

      rBufferTMP.RESET;
      rBufferTMP.SETRANGE("Importe comision", 0);
      IF rBufferTMP.FINDSET(TRUE) THEN
        rBufferTMP.DELETEALL;
    END;

    LOCAL PROCEDURE _CalculaComisionEquipo@1100253004(pwHotel@1100253000 : Code[20];pwDia@1100253004 : Date);
    VAR
      lrEquipo@1100253002 : Record 7010249;
      lrRsv@1100253001 : Record 7010162;
      lrBufferTMP@1100253010 : TEMPORARY Record 88002;
      lrBufferTMP2@1100217002 : TEMPORARY Record 88002;
      lrTempExc@1100217000 : TEMPORARY Record 88002;
      lcGuia@1100217004 : Codeunit 7010144;
      lwGuias@1100253005 : Integer;
      lwPrCm@1100217001 : Decimal;
      lwID@1100217003 : Integer;
    BEGIN
      // _CalculaComisionEquipo

      //. Preparamos el apuntador de reservas
      lrRsv.RESET;
      lrRsv.SETCURRENTKEY("Guia Venta", "Fecha Excursion");

      //. Buscamos todos los guias que estan en el mismo Pool

      lrEquipo.RESET;
      lrEquipo.SETRANGE (Hotel        , pwHotel);
      lrEquipo.SETFILTER("Fecha desde", '<=%1', pwDia);
      lrEquipo.SETFILTER("Fecha hasta", '>=%1', pwDia);
      IF lrEquipo.FINDSET THEN
      BEGIN
        lwGuias      := 0;

        lrBufferTMP.RESET;
        lrBufferTMP.DELETEALL;
        REPEAT
          //. Contamos cuantos guias tiene reservas dentro del Pool
          lwGuias += 1;

          //. Para cada dia acumulamos ventas y comisiones
          lrRsv.SETRANGE ("Guia Venta"     , lrEquipo."Guia venta");
          lrRsv.SETRANGE ("Hotel Cliente"  , lrEquipo.Hotel);
          lrRsv.SETRANGE ("Fecha Venta"    , lrEquipo."Fecha desde", lrEquipo."Fecha hasta");
          lrRsv.SETRANGE ("Fecha Excursion", wDiaInicio, wDiaFin);
          lrRsv.SETRANGE (Anulada          , FALSE);
          //lrRsv.SETRANGE (Reembolsada      , FALSE);     //$004
          lrRsv.SETFILTER("N§ Ticket"      , '<>%1', '');
          lrRsv.SETRANGE("No comisionable" , FALSE);       //+$003
          //.lrRsv.SETRANGE(Confirmada     , TRUE);
          IF lrRsv.FINDSET THEN
            _AcumularComisionReserva(lrRsv, lrBufferTMP, TRUE);
        UNTIL lrEquipo.NEXT = 0;

        // Calculamos el importe comisi¢n al tipo mas bajo

        // Creamos un temporal de excursiones/touroperadores
        CLEAR(lrTempExc);
        CLEAR(lrBufferTMP);
        IF lrBufferTMP.FINDLAST THEN
          lwID := lrBufferTMP.ID;
        lrBufferTMP2.COPY(lrBufferTMP, TRUE);

        IF lrBufferTMP.FINDSET THEN
        BEGIN
          REPEAT
            lrTempExc.SETRANGE(Excursion   , lrBufferTMP.Excursion);
            lrTempExc.SETRANGE(Touroperador, lrBufferTMP.Touroperador);
            IF NOT lrTempExc.FINDFIRST THEN BEGIN
              lrTempExc.ID           := lrBufferTMP.ID;
              lrTempExc.Excursion    := lrBufferTMP.Excursion;
              lrTempExc.Touroperador := lrBufferTMP.Touroperador;
              lrTempExc.INSERT;

              //. Buscamos si existe registro de temporal para esta excursion con el guia que estamos calculando
              lrBufferTMP2.RESET;
              lrBufferTMP2.SETRANGE(Excursion   , lrBufferTMP.Excursion);
              lrBufferTMP2.SETRANGE("Guia Venta", diGuia.Codigo);
              lrBufferTMP2.SETRANGE(Touroperador, lrBufferTMP.Touroperador);
              IF NOT lrBufferTMP2.FINDFIRST THEN
              BEGIN
                lwID += 100;

                CLEAR(lrBufferTMP2);
                lrBufferTMP2.ID                    := lwID;
                lrBufferTMP2.Excursion             := lrBufferTMP.Excursion;
                lrBufferTMP2."Guia Venta"          := diGuia.Codigo;
                lrBufferTMP2.Touroperador          := lrBufferTMP.Touroperador;
                lrBufferTMP2."Fecha venta"         := lrBufferTMP."Fecha venta";
                lrBufferTMP2.Hotel                 := lrBufferTMP.Hotel;
                lrBufferTMP2.Pool                  := lrBufferTMP.Pool;
                lrBufferTMP2."Porcentaje comision" := lcGuia.BuscaPrecioSinReserva(diGuia.Codigo, lrBufferTMP.Touroperador,
                                                                                   lrBufferTMP."Fecha venta",
                                                                                   lrBufferTMP.Dia, lrBufferTMP.Hotel,
                                                                                   lrBufferTMP.Excursion);
                lrBufferTMP2.INSERT(TRUE);
              END;
            END;
          UNTIL lrBufferTMP.NEXT = 0;
        END;

        // Por cada excursi¢n buscamos el tipo mas bajo de comision
        CLEAR(lrTempExc);
        IF lrTempExc.FINDSET THEN BEGIN
          REPEAT
            CLEAR(lrBufferTMP);
            lrBufferTMP.SETCURRENTKEY(Excursion, "Porcentaje comision");
            lrBufferTMP.SETRANGE(Excursion  , lrTempExc.Excursion);
            lrBufferTMP.SETRANGE(Touroperador, lrTempExc.Touroperador);
            CLEAR(lwPrCm);
            IF lrBufferTMP.FINDFIRST THEN // Buscamos la comision minima
              lwPrCm := lrBufferTMP."Porcentaje comision";
            lrBufferTMP.SETCURRENTKEY(ID); // Cambiamos la clave ya que modificamos el campo afectado
            IF lrBufferTMP.FINDSET THEN BEGIN
              REPEAT
                lrBufferTMP."Importe comision"    := lrBufferTMP.Importe * lwPrCm / 100;
                lrBufferTMP."Importe comision DL" := rTipoCam.ExchangeAmtFCYToFCY(lrBufferTMP."Fecha Cambio",
                                                                                  lrBufferTMP.Divisa, '',
                                                                                  lrBufferTMP."Importe comision");
                // Calculamos el diferencial de comision
                lrBufferTMP."Porcentaje comision 2" := lrBufferTMP."Porcentaje comision" - lwPrCm;
                lrBufferTMP."Porcentaje comision"   := lwPrCm;
                lrBufferTMP.MODIFY;
              UNTIL lrBufferTMP.NEXT = 0
            END;
          UNTIL lrTempExc.NEXT = 0;
        END;

        //. Pasar al acumulado total, indicando el numero de guias que componian el grupo
        CLEAR(lrBufferTMP);
        _AcumularTotalesGuia(lrBufferTMP, lwGuias);

        // Ahora buscamos la diferencia que le toca con respecto a su comision
        CLEAR(lrBufferTMP);
        lrBufferTMP.SETRANGE("Guia Venta", diGuia.Codigo);
        IF lrBufferTMP.FINDSET THEN BEGIN
          REPEAT
            lrBufferTMP."Porcentaje comision" := lrBufferTMP."Porcentaje comision 2";
            lrBufferTMP."Importe comision"    := lrBufferTMP.Importe * lrBufferTMP."Porcentaje comision" / 100;
            lrBufferTMP."Importe comision DL" := rTipoCam.ExchangeAmtFCYToFCY(lrBufferTMP."Fecha Cambio",
                                                                       lrBufferTMP.Divisa, '',
                                                                       lrBufferTMP."Importe comision");
            // Ponemos los pax a 0 ya que ya se han sumado en su vuelta anterior
            rBufferTMP.Adultos                := 0;
            rBufferTMP.Ni¤os                  := 0;
            lrBufferTMP.MODIFY;
          UNTIL lrBufferTMP.NEXT=0
          //
        END;
        // Ojo esta filtrado por guia venta
        _AcumularTotalesGuia(lrBufferTMP, 0);
      END;
    END;

    LOCAL PROCEDURE _CalculaComisionPrincipal@1100253001();
    VAR
      lrEquipo@1100253002 : Record 7010249;
      lrRsv@1100253001 : Record 7010162;
      lrBufferTMP@1100253000 : TEMPORARY Record 88002;
      lwDia@1100253003 : Date;
    BEGIN
      IF NOT diGuia."Guia principal" THEN
        EXIT;

      //. Preparamos el apuntador de reservas
      lrRsv.RESET;
      lrRsv.SETCURRENTKEY("Guia Venta", "Fecha Excursion");

      //. Acumular las ventas normales

      _CalculaComisionNormal(TRUE, wDiaInicio, wDiaFin, 0D, '');

      //. Buscar las reservas de sus guias asistentes para acumular sobre la comision del guia principal

      lrBufferTMP.RESET;
      lrBufferTMP.DELETEALL;

      FOR lwDia := wDiaInicio TO wDiaFin DO
      BEGIN
        lrEquipo.RESET;
        lrEquipo.SETRANGE ("Guia principal", diGuia.Codigo);
        lrEquipo.SETFILTER("Fecha desde"   , '<=%1', lwDia);
        lrEquipo.SETFILTER("Fecha hasta"   , '>=%1', lwDia);
        IF lrEquipo.FINDSET THEN
        BEGIN
          REPEAT
            lrRsv.SETRANGE ("Guia Venta"   , lrEquipo."Guia venta");
            lrRsv.SETRANGE ("Hotel Cliente", lrEquipo.Hotel);
            lrRsv.SETRANGE ("Fecha Venta"  , lwDia);
            //.lrRsv.SETRANGE(Confirmada     , TRUE);
            lrRsv.SETRANGE (Anulada        , FALSE);
            lrRsv.SETRANGE (Reembolsada    , FALSE);
            lrRsv.SETFILTER("N§ Ticket"    , '<>%1', '');
            lrRsv.SETRANGE("No comisionable" , FALSE);       //+$003
            IF lrRsv.FINDSET THEN
              _AcumularComisionAsistente(lrRsv, lrBufferTMP);
          UNTIL lrEquipo.NEXT = 0;
        END;
      END;

      //. Pasar al acumulado total
      CLEAR(lrBufferTMP);
      _AcumularTotalesGuia(lrBufferTMP, 0);
    END;

    LOCAL PROCEDURE _CalculaComisionNormal@1100253002(pwCalculoDesdePrincipal@1100253002 : Boolean;pwDiaInicio@1100217000 : Date;pwDiaFinal@1100217001 : Date;pwDiaVenta@1100217002 : Date;pwHotel@1100217003 : Code[20]);
    VAR
      lrRsv@1100253000 : Record 7010162;
      lrBufferTMP@1100253001 : TEMPORARY Record 88002;
    BEGIN
      IF (diGuia."Guia principal" AND NOT pwCalculoDesdePrincipal) THEN
        EXIT;

      lrBufferTMP.RESET;
      lrBufferTMP.DELETEALL;

      //. Acumular las ventas normales de este guia

      lrRsv.RESET;
      lrRsv.SETCURRENTKEY("Guia Venta", "Fecha Excursion");
      lrRsv.SETRANGE ("Guia Venta"     , diGuia.Codigo);
      lrRsv.SETRANGE ("Fecha Excursion", wDiaInicio, wDiaFin);

      IF pwDiaVenta <> 0D THEN
        lrRsv.SETRANGE("Fecha Venta", pwDiaVenta);
      IF pwHotel <> '' THEN
        lrRsv.SETRANGE("Hotel Cliente", pwHotel);

      lrRsv.SETRANGE (Anulada          , FALSE);
      lrRsv.SETRANGE (Reembolsada      , FALSE);
      lrRsv.SETFILTER("N§ Ticket"      , '<>%1', '');
      lrRsv.SETRANGE("No comisionable" , FALSE);       //+$003
      //.lrRsv.SETRANGE(Confirmada     , TRUE);
      IF lrRsv.FINDSET THEN
        _AcumularComisionReserva(lrRsv, lrBufferTMP, FALSE);

      //. Pasar al acumulado total
      CLEAR(lrBufferTMP);
      _AcumularTotalesGuia(lrBufferTMP, 0);
    END;

    LOCAL PROCEDURE _AcumularComisionReserva@1100253007(VAR prRsv@1100253000 : Record 7010162;VAR prBufferTMP@1100253001 : TEMPORARY Record 88002;pwDesgl@1100217000 : Boolean);
    VAR
      lrDetEco@1100253002 : Record 7010245;
      lwID@1100253004 : Integer;
      lwNew@1100217001 : Boolean;
    BEGIN
      // _AcumularComisionReserva
      // pwDesgl Lo que le indicamos realmente es que haga una por reserva
      // En Pool debemos hacer asi ya que necesitamos volver a calcular la comisi¢n por cada reserva
      // ya que si no tendr¡amos diferencias de redondeo

      lwID := 0;
      lwNew := TRUE;
      IF prBufferTMP.FINDLAST THEN
        lwID := prBufferTMP.ID;
      REPEAT
        IF NOT pwDesgl THEN BEGIN
          prBufferTMP.SETRANGE(Excursion             , prRsv.Producto);
          prBufferTMP.SETRANGE("Porcentaje comision" , prRsv."Precio Comision Guia Venta");
          prBufferTMP.SETRANGE(Divisa                , prRsv.Divisa);
          prBufferTMP.SETRANGE("Fecha Cambio"        , prRsv."Fecha cambio");
          prBufferTMP.SETRANGE("Guia Venta"          , prRsv."Guia Venta");
          prBufferTMP.SETRANGE(Hotel                 , prRsv."Hotel Cliente");
          prBufferTMP.SETRANGE("Fecha venta"         , prRsv."Fecha Venta");
          prBufferTMP.SETRANGE(Touroperador          , prRsv.TourOperador);
          lwNew :=  NOT prBufferTMP.FINDFIRST;
        END;
        IF lwNew THEN BEGIN
          lwID += 100;

          prBufferTMP.INIT;
          prBufferTMP.ID                    := lwID;
          prBufferTMP.Excursion             := prRsv.Producto;
          prBufferTMP."Porcentaje comision" := prRsv."Precio Comision Guia Venta";
          prBufferTMP.Divisa                := prRsv.Divisa;
          prBufferTMP."Fecha Cambio"        := prRsv."Fecha cambio";
          prBufferTMP."Guia Venta"          := prRsv."Guia Venta";
          prBufferTMP.Hotel                 := prRsv."Hotel Cliente";
          prBufferTMP."Fecha venta"         := prRsv."Fecha Venta";
          prBufferTMP.Touroperador          := prRsv.TourOperador;
          prBufferTMP.Pool                  := pwDesgl;
          prBufferTMP.INSERT;
        END;

        lrDetEco.GET(prRsv.Delegacion, prRsv."N§ Reserva");

        prBufferTMP.Importe               += lrDetEco."Ingreso comisionable neto" - prRsv."Importe Reembolsado Neto"; //$004

        prBufferTMP."Importe comision"    += prRsv."Comision Guia Venta";
        prBufferTMP."Importe comision DL" += rTipoCam.ExchangeAmtFCYToFCY(prRsv."Fecha cambio",
                                                                           prRsv.Divisa, '',
                                                                           prRsv."Comision Guia Venta");
        prBufferTMP.Adultos               += (prRsv.Adultos - prRsv."Venta - Invitados Adultos" - prRsv."Adultos Reembolsados"); //$004
        prBufferTMP.Ni¤os                 += (prRsv.Ni¤os - prRsv."Venta - Invitados ni¤os" - prRsv."Ni¤os Reembolsados");  //$004

        prBufferTMP.MODIFY;
      UNTIL prRsv.NEXT = 0;
    END;

    LOCAL PROCEDURE _AcumularComisionAsistente@1100253005(VAR prRsv@1100253001 : Record 7010162;VAR prBufferTMP@1100253000 : TEMPORARY Record 88002);
    VAR
      lrDetEco@1100253004 : Record 7010245;
      lrTipoCam@1100253003 : Record 330;
      lwID@1100253002 : Integer;
    BEGIN
      // _AcumularComisionAsistente

      lwID := 0;
      IF prBufferTMP.FINDLAST THEN
        lwID := prBufferTMP.ID;

      REPEAT
        prBufferTMP.Excursion             := prRsv.Producto;
        prBufferTMP."Porcentaje comision" := prRsv."Precio Comision Guia Venta";
        prBufferTMP.Divisa                := prRsv.Divisa;
        prBufferTMP."Fecha Cambio"        := prRsv."Fecha cambio";
        prBufferTMP.Hotel                 := prRsv."Hotel Cliente";
        prBufferTMP."Fecha venta"         := prRsv."Fecha Venta";
        prBufferTMP.Touroperador          := prRsv.TourOperador;
        IF NOT prBufferTMP.FIND THEN
        BEGIN
          lwID += 100;

          prBufferTMP.INIT;
          prBufferTMP.ID                    := lwID;
          prBufferTMP.Excursion             := prRsv.Producto;
          prBufferTMP."Porcentaje comision" := prRsv."Precio Comision Guia Venta";
          prBufferTMP.Divisa                := prRsv.Divisa;
          prBufferTMP."Fecha Cambio"        := prRsv."Fecha cambio";
          prBufferTMP.Hotel                 := prRsv."Hotel Cliente";
          prBufferTMP."Fecha venta"         := prRsv."Fecha Venta";
          prBufferTMP.Touroperador          := prRsv.TourOperador;
          prBufferTMP.INSERT;
        END;

        lrDetEco.GET(prRsv.Delegacion, prRsv."N§ Reserva");

        prBufferTMP."Importe comision"    += lrDetEco."Comision GV asistente";
        prBufferTMP."Importe comision DL" += lrTipoCam.ExchangeAmtFCYToFCY(prRsv."Fecha cambio",
                                                                           prRsv.Divisa,
                                                                           '',
                                                                           lrDetEco."Comision GV asistente");
        prBufferTMP.MODIFY;
      UNTIL prRsv.NEXT = 0;
    END;

    LOCAL PROCEDURE _AcumularTotalesGuia@1100253008(VAR prBufferTMP@1100253000 : TEMPORARY Record 88002;pwNumGuias@1100253001 : Integer);
    VAR
      lrEquipo@1100217000 : Record 7010249;
    BEGIN
      // _AcumularTotalesGuia

      //. Pasar al acumulado total

      CLEAR(rBufferTMP);
      prBufferTMP.SETFILTER("Porcentaje comision", '<>%1',0);
      IF prBufferTMP.FINDSET THEN
      BEGIN
        REPEAT
          rBufferTMP.SETRANGE(Excursion             , prBufferTMP.Excursion);
          rBufferTMP.SETRANGE("Porcentaje comision" , prBufferTMP."Porcentaje comision");
          rBufferTMP.SETRANGE(Divisa                , prBufferTMP.Divisa);
          rBufferTMP.SETRANGE(Hotel                 , prBufferTMP.Hotel);
          rBufferTMP.SETRANGE("Fecha venta"         , prBufferTMP."Fecha venta");
          rBufferTMP.SETRANGE(Touroperador          , prBufferTMP.Touroperador);
          IF NOT rBufferTMP.FINDFIRST THEN
          BEGIN
            wID += 100;

            rBufferTMP.INIT;
            rBufferTMP.Excursion             := prBufferTMP.Excursion;
            rBufferTMP."Porcentaje comision" := prBufferTMP."Porcentaje comision";
            rBufferTMP.Divisa                := prBufferTMP.Divisa;
            rBufferTMP.Hotel                 := prBufferTMP.Hotel;
            rBufferTMP.ID                    := wID;
            rBufferTMP.Pool                  := prBufferTMP.Pool;
            rBufferTMP."Fecha venta"         := prBufferTMP."Fecha venta";
            rBufferTMP.Touroperador          := prBufferTMP.Touroperador;

            //. Rellenar los guias que componen el equipo
            lrEquipo.RESET;
            lrEquipo.SETFILTER("Fecha desde", '<=%1', prBufferTMP."Fecha venta");
            lrEquipo.SETFILTER("Fecha hasta", '>=%1', prBufferTMP."Fecha venta");
            lrEquipo.SETRANGE (Hotel        , prBufferTMP.Hotel);
            IF lrEquipo.FINDSET THEN
            BEGIN
              REPEAT
                IF STRPOS(rBufferTMP.Equipo, lrEquipo."Guia venta") = 0 THEN
                BEGIN
                  IF rBufferTMP.Equipo = '' THEN
                    rBufferTMP.Equipo := lrEquipo."Guia venta"
                  ELSE
                    rBufferTMP.Equipo := rBufferTMP.Equipo + '|' + lrEquipo."Guia venta";
                END;
              UNTIL lrEquipo.NEXT = 0;
            END;

            rBufferTMP.INSERT;
          END;

          IF pwNumGuias = 0 THEN
          BEGIN
            rBufferTMP.Importe               += prBufferTMP.Importe;
            rBufferTMP."Importe comision"    += prBufferTMP."Importe comision";
            rBufferTMP."Importe comision DL" += prBufferTMP."Importe comision DL";
          END
          //. Prorrateo de las ventas/comisiones para los guias que trabajan en un Pool
          ELSE
          BEGIN
            rBufferTMP.Importe               += ROUND(prBufferTMP.Importe / pwNumGuias);
            rBufferTMP."Importe comision"    += ROUND(prBufferTMP."Importe comision" / pwNumGuias);
            rBufferTMP."Importe comision DL" += ROUND(prBufferTMP."Importe comision DL" / pwNumGuias);
          END;

          rBufferTMP.Adultos               += prBufferTMP.Adultos;
          rBufferTMP.Ni¤os                 += prBufferTMP.Ni¤os;
          rBufferTMP.MODIFY;
        UNTIL prBufferTMP.NEXT = 0;
      END;

      CLEAR(prBufferTMP);
    END;

    PROCEDURE _TemporalFechasVenta@1100217001(VAR prTMPFechas@1100217000 : TEMPORARY Record 7010162);
    VAR
      lrRsv@1100217001 : Record 7010162;
      lrEquipo@1100217002 : Record 7010249;
      lwEntero@1100217003 : Integer;
    BEGIN
      // _TemporalFechasVenta

      //. Buscar las fechas de venta que hay dentro del periodo de fechas de servicio que se estan calculando

      prTMPFechas.RESET;
      prTMPFechas.DELETEALL;

      lwEntero := 0;

      lrRsv.RESET;
      lrRsv.SETCURRENTKEY("Guia Venta", "Fecha Excursion");
      lrRsv.SETRANGE ("Guia Venta"     , diGuia.Codigo);
      lrRsv.SETRANGE ("Fecha Excursion", wDiaInicio, wDiaFin);
      lrRsv.SETRANGE (Anulada          , FALSE);
      //lrRsv.SETRANGE (Reembolsada      , FALSE);  // $004
      lrRsv.SETFILTER("N§ Ticket"      , '<>%1', '');
      lrRsv.SETRANGE("No comisionable" , FALSE);       //+$003
      IF lrRsv.FINDSET THEN
      BEGIN
        REPEAT
          prTMPFechas.SETRANGE("Fecha Venta"  , lrRsv."Fecha Venta");
          IF NOT prTMPFechas.FINDFIRST THEN
          BEGIN
            //. Buscamos todos los hoteles en los que esta ese dia
            lrEquipo.RESET;
            lrEquipo.SETRANGE ("Guia venta" , diGuia.Codigo);
            lrEquipo.SETFILTER("Fecha desde", '<=%1', lrRsv."Fecha Venta");
            lrEquipo.SETFILTER("Fecha hasta", '>=%1', lrRsv."Fecha Venta");
            IF lrEquipo.FINDSET THEN
            BEGIN
              REPEAT
                prTMPFechas.SETRANGE("Hotel Cliente", lrEquipo.Hotel);
                IF NOT prTMPFechas.FINDFIRST THEN
                BEGIN
                  lwEntero += 1;

                  prTMPFechas.INIT;
                  prTMPFechas."N§ Reserva"    := lrRsv."N§ Reserva" + (-1000000 * lwEntero);
                  prTMPFechas."Fecha Venta"   := lrRsv."Fecha Venta";
                  prTMPFechas."Hotel Cliente" := lrEquipo.Hotel;
                  prTMPFechas.INSERT;
                END;
              UNTIL lrEquipo.NEXT = 0;
            END
            //. Si ese dia no esta en ningun equipo registramos solo los hoteles
            //. en los que el ha vendido
            ELSE
            BEGIN
              prTMPFechas.SETRANGE("Hotel Cliente", lrRsv."Hotel Cliente");
              IF NOT prTMPFechas.FINDFIRST THEN
              BEGIN
                prTMPFechas.INIT;
                prTMPFechas."N§ Reserva"    := lrRsv."N§ Reserva";
                prTMPFechas."Fecha Venta"   := lrRsv."Fecha Venta";
                prTMPFechas."Hotel Cliente" := lrRsv."Hotel Cliente";
                prTMPFechas.INSERT;
              END;
            END;
          END;
        UNTIL lrRsv.NEXT = 0;
      END;
    END;

    BEGIN
    {
      $001 AJS 09042013 Nuevo nivel de detalle por reserva

      $002 AJS 19122013 Agrego un gran total final

      $003 MNC 01072014 No incluir No Comisionables

      $004 ARM 28072014 Incluir los reembolsos
    }
    END.
  }
  RDLDATA
  {
  }
}
