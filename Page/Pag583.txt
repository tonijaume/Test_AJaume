OBJECT Page 583 XBRL Taxonomy Lines
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=XBRL Taxonomy Lines;
               ESP=L¡n. taxonom¡a XBRL];
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table395;
    SourceTableView=SORTING(XBRL Taxonomy Name,Presentation Order);
    PageType=List;
    SourceTableTemporary=Yes;
    OnOpenPage=VAR
                 XBRLTaxonomy@1000 : Record 394;
                 XBRLTaxonomyLabel@1001 : Record 401;
               BEGIN
                 IF GETFILTER("XBRL Taxonomy Name") <> '' THEN
                   CurrentTaxonomy := GETRANGEMIN("XBRL Taxonomy Name");
                 IF NOT XBRLTaxonomy.GET(CurrentTaxonomy) THEN
                   IF NOT XBRLTaxonomy.FIND('-') THEN
                     XBRLTaxonomy.INIT;
                 CurrentTaxonomy := XBRLTaxonomy.Name;

                 XBRLTaxonomyLabel.SETRANGE("XBRL Taxonomy Name",CurrentTaxonomy);
                 IF CurrentLang <> '' THEN
                   XBRLTaxonomyLabel.SETRANGE("XML Language Identifier",CurrentLang);
                 IF XBRLTaxonomyLabel.FIND('-') THEN
                   CurrentLang := XBRLTaxonomyLabel."XML Language Identifier"
                 ELSE
                   IF CurrentLang <> '' THEN BEGIN
                     XBRLTaxonomyLabel.SETRANGE("XML Language Identifier");
                     IF XBRLTaxonomyLabel.FIND('-') THEN
                       CurrentLang := XBRLTaxonomyLabel."XML Language Identifier"
                   END;

                 SETRANGE("Label Language Filter",CurrentLang);

                 IF ISSERVICETIER THEN
                   ExpandAll
                 ELSE
                   InitTempTable;
               END;

    OnAfterGetRecord=VAR
                       XBRLLine@1102601000 : Record 395;
                     BEGIN
                       LabelIndent := 0;
                       IF IsExpanded(Rec) THEN
                         ActualExpansionStatus := 1
                       ELSE
                         IF HasChildren(Rec) THEN
                           ActualExpansionStatus := 0
                         ELSE
                           ActualExpansionStatus := 2;

                       XBRLLine.GET("XBRL Taxonomy Name","Line No.");
                       IF ("Source Type" <> XBRLLine."Source Type") OR
                          ("Constant Amount" <> XBRLLine."Constant Amount") OR
                          (Description <> XBRLLine.Description)
                       THEN BEGIN
                         XBRLLine.CALCFIELDS(Label,Information,Rollup,"G/L Map Lines",Notes,Reference);
                         Rec := XBRLLine;
                         MODIFY;
                       END;

                       IF Label = '' THEN
                         Label := Name;
                       LabelOnFormat;
                     END;

    OnModifyRecord=VAR
                     XBRLLine@1102601000 : Record 395;
                     RecRef@1000 : RecordRef;
                     xRecRef@1001 : RecordRef;
                     ChangeLogMgt@1002 : Codeunit 423;
                   BEGIN
                     RecRef.GETTABLE(Rec);
                     xRecRef.GETTABLE(xRec);
                     XBRLLine.GET("XBRL Taxonomy Name","Line No.");
                     XBRLLine := Rec;
                     XBRLLine.MODIFY;
                     Rec := XBRLLine;
                     MODIFY;
                     ChangeLogMgt.LogModification(RecRef,xRecRef);
                     EXIT(FALSE);
                   END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 21      ;1   ;ActionGroup;
                      CaptionML=[ENU=&XBRL Line;
                                 ESP=L¡nea &XBRL] }
      { 22      ;2   ;Action    ;
                      ShortCutKey=May£s+F7;
                      CaptionML=[ENU=Card;
                                 ESP=Ficha];
                      RunObject=Page 584;
                      RunFormLink=XBRL Taxonomy Name=FIELD(XBRL Taxonomy Name),
                                  Line No.=FIELD(Line No.);
                      Image=EditLines }
      { 23      ;2   ;Separator ;
                      CaptionML=[ENU="";
                                 ESP=""] }
      { 24      ;2   ;Action    ;
                      CaptionML=[ENU=Information;
                                 ESP=Informaci¢n];
                      RunObject=Page 585;
                      RunFormLink=XBRL Taxonomy Name=FIELD(XBRL Taxonomy Name),
                                  XBRL Taxonomy Line No.=FIELD(Line No.),
                                  Comment Type=CONST(Information),
                                  Label Language Filter=FIELD(Label Language Filter) }
      { 30      ;2   ;Action    ;
                      CaptionML=[ENU=Re&ference;
                                 ESP=Re&ferencia];
                      RunObject=Page 585;
                      RunFormLink=XBRL Taxonomy Name=FIELD(XBRL Taxonomy Name),
                                  XBRL Taxonomy Line No.=FIELD(Line No.),
                                  Comment Type=CONST(Reference),
                                  Label Language Filter=FIELD(Label Language Filter) }
      { 25      ;2   ;Action    ;
                      CaptionML=[ENU=Rollups;
                                 ESP=Distribuciones];
                      RunObject=Page 587;
                      RunFormLink=XBRL Taxonomy Name=FIELD(XBRL Taxonomy Name),
                                  XBRL Taxonomy Line No.=FIELD(Line No.),
                                  Label Language Filter=FIELD(Label Language Filter) }
      { 26      ;2   ;Action    ;
                      CaptionML=[ENU=Notes;
                                 ESP=Notas];
                      RunObject=Page 585;
                      RunFormLink=XBRL Taxonomy Name=FIELD(XBRL Taxonomy Name),
                                  XBRL Taxonomy Line No.=FIELD(Line No.),
                                  Comment Type=CONST(Notes),
                                  Label Language Filter=FIELD(Label Language Filter) }
      { 27      ;2   ;Action    ;
                      CaptionML=[ENU=G/L Map Lines;
                                 ESP=L¡n. contabilidad asignadas];
                      RunObject=Page 586;
                      RunFormLink=XBRL Taxonomy Name=FIELD(XBRL Taxonomy Name),
                                  XBRL Taxonomy Line No.=FIELD(Line No.),
                                  Label Language Filter=FIELD(Label Language Filter) }
      { 34      ;2   ;Action    ;
                      CaptionML=[ENU=C&onstants;
                                 ESP=C&onstantes];
                      RunObject=Page 598;
                      RunFormLink=XBRL Taxonomy Name=FIELD(XBRL Taxonomy Name),
                                  XBRL Taxonomy Line No.=FIELD(Line No.),
                                  Label Language Filter=FIELD(Label Language Filter) }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 43      ;1   ;ActionGroup;
                      CaptionML=[ENU=F&unctions;
                                 ESP=Acci&ones] }
      { 45      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[ENU=Copy XBRL Setup;
                                 ESP=Copiar conf. XBRL];
                      OnAction=VAR
                                 XBRLCopySetup@1000 : Report 507;
                               BEGIN
                                 XBRLCopySetup.SetCopyTo(CurrentTaxonomy);
                                 XBRLCopySetup.RUN;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 38  ;1   ;Group     ;
                CaptionML=[ENU=General;
                           ESP=General] }

    { 39  ;2   ;Field     ;
                CaptionML=[ENU=Taxonomy Name;
                           ESP=Nombre taxonom¡a];
                SourceExpr=CurrentTaxonomy;
                TableRelation="XBRL Taxonomy";
                OnValidate=BEGIN
                             CurrentTaxonomyOnAfterValidate;
                           END;

                OnLookup=VAR
                           XBRLTaxonomy@1000 : Record 394;
                         BEGIN
                           XBRLTaxonomy.Name := CurrentTaxonomy;
                           IF FORM.RUNMODAL(0,XBRLTaxonomy) <> ACTION::LookupOK THEN
                             EXIT(FALSE);

                           Text := XBRLTaxonomy.Name;
                           EXIT(TRUE);
                         END;
                          }

    { 41  ;2   ;Field     ;
                CaptionML=[ENU=Label Language;
                           ESP=Etiqueta idioma];
                SourceExpr=CurrentLang;
                OnValidate=VAR
                             XBRLTaxonomyLabel@1000 : Record 401;
                           BEGIN
                             XBRLTaxonomyLabel.SETRANGE("XBRL Taxonomy Name",CurrentTaxonomy);
                             XBRLTaxonomyLabel.SETRANGE("XML Language Identifier",CurrentLang);
                             IF CurrentLang <> '' THEN
                               IF NOT XBRLTaxonomyLabel.FIND('-') THEN
                                 ERROR(Text001,CurrentLang);
                             SETRANGE("Label Language Filter",CurrentLang);
                           END;

                OnLookup=VAR
                           XBRLTaxonomyLabel@1001 : Record 401;
                           XBRLTaxonomyLabels@1000 : Form 590;
                         BEGIN
                           XBRLTaxonomyLabel.SETRANGE("XBRL Taxonomy Name",CurrentTaxonomy);
                           IF NOT XBRLTaxonomyLabel.FIND('-') THEN
                             ERROR(Text002,"XBRL Taxonomy Name");
                           XBRLTaxonomyLabel.SETRANGE(
                             "XBRL Taxonomy Line No.",XBRLTaxonomyLabel."XBRL Taxonomy Line No.");
                           XBRLTaxonomyLabels.SETTABLEVIEW(XBRLTaxonomyLabel);
                           XBRLTaxonomyLabels.LOOKUPMODE := TRUE;
                           IF XBRLTaxonomyLabels.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             XBRLTaxonomyLabels.GETRECORD(XBRLTaxonomyLabel);
                             Text := XBRLTaxonomyLabel."XML Language Identifier";
                             EXIT(TRUE);
                           END ELSE
                             EXIT(FALSE);
                         END;
                          }

    { 1   ;1   ;Group     ;
                IndentationColumnName=LabelIndent;
                IndentationControls=Label;
                ShowAsTree=Yes;
                GroupType=Repeater }

    { 28  ;2   ;Field     ;
                SourceExpr="Line No.";
                Visible=FALSE }

    { 2   ;2   ;Field     ;
                DrillDown=No;
                SourceExpr=Label }

    { 4   ;2   ;Field     ;
                SourceExpr="Source Type" }

    { 6   ;2   ;Field     ;
                SourceExpr="Constant Amount" }

    { 10  ;2   ;Field     ;
                SourceExpr=Information }

    { 32  ;2   ;Field     ;
                SourceExpr=Reference }

    { 12  ;2   ;Field     ;
                SourceExpr=Notes }

    { 8   ;2   ;Field     ;
                SourceExpr="G/L Map Lines" }

    { 31  ;2   ;Field     ;
                SourceExpr=Rollup;
                Visible=FALSE }

    { 14  ;2   ;Field     ;
                SourceExpr=Description }

    { 16  ;2   ;Field     ;
                SourceExpr=Name;
                Visible=FALSE }

  }
  CODE
  {
    VAR
      CurrentTaxonomy@1004 : Code[20];
      CurrentLang@1001 : Text[10];
      ActualExpansionStatus@1000 : Integer;
      Text001@1002 : TextConst 'ENU=Labels are not defined for language %1.;ESP=No hay etiquetas definidas para el idioma %1.';
      Text002@1006 : TextConst 'ENU=There are no labels defined for %1.;ESP=No existen etiquetas definidas para %1.';
      LabelEmphasize@19077489 : Boolean INDATASET;
      LabelIndent@19035629 : Integer INDATASET;

    PROCEDURE SetCurrentSchema@2(NewCurrentTaxonomy@1000 : Code[20]);
    BEGIN
      CurrentTaxonomy := NewCurrentTaxonomy;
    END;

    LOCAL PROCEDURE InitTempTable@4();
    VAR
      XBRLLine@1000 : Record 395;
    BEGIN
      ResetFilter;
      DELETEALL;
      XBRLLine.SETRANGE("XBRL Taxonomy Name",CurrentTaxonomy);
      XBRLLine.SETRANGE(Level,0);
      IF XBRLLine.FIND('-') THEN
        REPEAT
          Rec := XBRLLine;
          INSERT;
        UNTIL XBRLLine.NEXT = 0;
    END;

    LOCAL PROCEDURE ExpandAll@12();
    VAR
      XBRLLine@1000 : Record 395;
    BEGIN
      ResetFilter;
      DELETEALL;
      XBRLLine.SETRANGE("XBRL Taxonomy Name",CurrentTaxonomy);
      IF XBRLLine.FIND('-') THEN
        REPEAT
          Rec := XBRLLine;
          INSERT;
        UNTIL XBRLLine.NEXT = 0;
    END;

    LOCAL PROCEDURE HasChildren@19(ActualXBRLLine@1000 : Record 395) : Boolean;
    VAR
      XBRLLine2@1001 : Record 395;
    BEGIN
      XBRLLine2 := ActualXBRLLine;
      XBRLLine2.SETCURRENTKEY("XBRL Taxonomy Name","Presentation Order");
      XBRLLine2.SETRANGE("XBRL Taxonomy Name",ActualXBRLLine."XBRL Taxonomy Name");
      IF XBRLLine2.NEXT = 0 THEN
        EXIT(FALSE)
      ELSE
        EXIT(XBRLLine2.Level > ActualXBRLLine.Level);
    END;

    LOCAL PROCEDURE IsExpanded@20(ActualXBRLLine@1000 : Record 395) : Boolean;
    VAR
      xXBRLLine@1001 : Record 395;
      Found@1002 : Boolean;
    BEGIN
      xXBRLLine.COPY(Rec);
      ResetFilter;
      Rec := ActualXBRLLine;
      Found := (NEXT <> 0);
      IF Found THEN
        Found := (Level > ActualXBRLLine.Level);
      COPY(xXBRLLine);
      EXIT(Found);
    END;

    LOCAL PROCEDURE ToggleExpandCollapse@1();
    VAR
      XBRLLine@1000 : Record 395;
      xXBRLLine@1001 : Record 395;
    BEGIN
      xXBRLLine.COPY(Rec);
      ResetFilter;
      IF ActualExpansionStatus = 0 THEN BEGIN // Has children, but not expanded
        XBRLLine.SETCURRENTKEY("XBRL Taxonomy Name","Presentation Order");
        XBRLLine.SETRANGE("XBRL Taxonomy Name",CurrentTaxonomy);
        XBRLLine.SETFILTER("Presentation Order",'>=%1',"Presentation Order");
        XBRLLine.SETFILTER("Line No.",'<>%1',"Line No.");
        XBRLLine.SETRANGE(Level,Level,Level + 1);
        XBRLLine := Rec;
        IF XBRLLine.NEXT <> 0 THEN
          REPEAT
            IF XBRLLine.Level > xXBRLLine.Level THEN BEGIN
              Rec := XBRLLine;
              IF INSERT THEN;
            END;
          UNTIL (XBRLLine.NEXT = 0) OR (XBRLLine.Level = xXBRLLine.Level);
      END ELSE
        IF ActualExpansionStatus = 1 THEN BEGIN // Has children and is already expanded
          WHILE (NEXT <> 0) AND (Level > xXBRLLine.Level) DO
            DELETE;
        END;
      COPY(xXBRLLine);
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE ResetFilter@3();
    BEGIN
      RESET;
      SETCURRENTKEY("XBRL Taxonomy Name","Presentation Order");
      FILTERGROUP(2);
      SETRANGE("XBRL Taxonomy Name",CurrentTaxonomy);
      FILTERGROUP(0);
    END;

    LOCAL PROCEDURE CurrentTaxonomyOnAfterValidate@19000234();
    BEGIN
      FILTERGROUP(2);
      SETRANGE("XBRL Taxonomy Name",CurrentTaxonomy);
      FILTERGROUP(0);
      SETRANGE("XBRL Taxonomy Name");
      InitTempTable;
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE ActualExpansionStatusOnPush@19057112();
    BEGIN
      ToggleExpandCollapse;
    END;

    LOCAL PROCEDURE LabelOnFormat@19054229();
    BEGIN
      LabelIndent := Level;

      IF (Level = 0) OR (ActualExpansionStatus < 2) THEN
        LabelEmphasize := TRUE;
    END;

    BEGIN
    END.
  }
}
