OBJECT Page 5222 Human Resource Comment Sheet
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Comment Sheet;
               ESP=Hoja comentarios];
    MultipleNewLines=Yes;
    LinksAllowed=No;
    SourceTable=Table5208;
    DataCaptionExpr=Caption(Rec);
    DelayedInsert=Yes;
    PageType=List;
    AutoSplitKey=Yes;
    OnNewRecord=BEGIN
                  SetUpNewLine;
                END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                SourceExpr=Date }

    { 4   ;2   ;Field     ;
                SourceExpr=Comment }

    { 6   ;2   ;Field     ;
                SourceExpr=Code;
                Visible=FALSE }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=untitled;ESP=SinTitulo';
      Employee@1001 : Record 5200;
      EmployeeAbsence@1002 : Record 5207;
      EmployeeQualification@1003 : Record 5203;
      EmployeeRelative@1004 : Record 5205;
      MiscArticleInfo@1005 : Record 5214;
      ConfidentialInfo@1006 : Record 5216;

    PROCEDURE Caption@1(HRCommentLine@1000 : Record 5208) : Text[110];
    BEGIN
      CASE HRCommentLine."Table Name" OF
        HRCommentLine."Table Name"::"Employee Absence":
          IF EmployeeAbsence.GET(HRCommentLine."Table Line No.") THEN BEGIN
            Employee.GET(EmployeeAbsence."Employee No.");
            EXIT(
              Employee."No." + ' ' + Employee.FullName + ' ' +
              EmployeeAbsence."Cause of Absence Code" + ' ' +
              FORMAT(EmployeeAbsence."From Date"));
          END;

        HRCommentLine."Table Name"::Employee:
          IF Employee.GET(HRCommentLine."No.") THEN
            EXIT(HRCommentLine."No." + ' ' + Employee.FullName);

        HRCommentLine."Table Name"::"Alternative Address":
          IF Employee.GET(HRCommentLine."No.") THEN
            EXIT(
              HRCommentLine."No." + ' ' + Employee.FullName + ' ' +
              HRCommentLine."Alternative Address Code");

        HRCommentLine."Table Name"::"Employee Qualification":
          IF EmployeeQualification.GET(HRCommentLine."No.",HRCommentLine."Table Line No.") AND
             Employee.GET(HRCommentLine."No.")
          THEN
            EXIT(
              HRCommentLine."No." + ' ' + Employee.FullName + ' ' +
              EmployeeQualification."Qualification Code");

        HRCommentLine."Table Name"::"Employee Relative":
          IF EmployeeRelative.GET(HRCommentLine."No.",HRCommentLine."Table Line No.") AND
             Employee.GET(HRCommentLine."No.")
          THEN
            EXIT(
              HRCommentLine."No." + ' ' + Employee.FullName + ' ' +
              EmployeeRelative."Relative Code");

        HRCommentLine."Table Name"::"Misc. Article Information":
          IF MiscArticleInfo.GET(
             HRCommentLine."No.",HRCommentLine."Alternative Address Code",HRCommentLine."Table Line No.") AND
             Employee.GET(HRCommentLine."No.")
          THEN
            EXIT(
              HRCommentLine."No." + ' ' + Employee.FullName + ' ' +
              MiscArticleInfo."Misc. Article Code");

        HRCommentLine."Table Name"::"Confidential Information":
          IF ConfidentialInfo.GET(HRCommentLine."No.",HRCommentLine."Table Line No.") AND
             Employee.GET(HRCommentLine."No.")
          THEN
            EXIT(
              HRCommentLine."No." + ' ' + Employee.FullName + ' ' +
              ConfidentialInfo."Confidential Code");
      END;
      EXIT(Text000);
    END;

    BEGIN
    END.
  }
}
