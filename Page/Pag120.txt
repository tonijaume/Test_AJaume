OBJECT Page 120 G/L Budget Entries
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW13.70,NAVES6.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=G/L Budget Entries;
               ESP=Movs. pptos. contabilidad];
    SourceTable=Table96;
    DelayedInsert=Yes;
    DataCaptionFields=G/L Account No.,Budget Name;
    PageType=List;
    OnInit=BEGIN
             "Budget Dimension 4 CodeEnable" := TRUE;
             "Budget Dimension 3 CodeEnable" := TRUE;
             "Budget Dimension 2 CodeEnable" := TRUE;
             "Budget Dimension 1 CodeEnable" := TRUE;
             "Global Dimension 2 CodeEnable" := TRUE;
             "Global Dimension 1 CodeEnable" := TRUE;
             "Budget Dimension 4 CodeVisible" := TRUE;
             "Budget Dimension 3 CodeVisible" := TRUE;
             "Budget Dimension 2 CodeVisible" := TRUE;
             "Budget Dimension 1 CodeVisible" := TRUE;
             "Global Dimension 2 CodeVisible" := TRUE;
             "Global Dimension 1 CodeVisible" := TRUE;
             LowestModifiedEntryNo := 2147483647;
           END;

    OnOpenPage=VAR
                 GLBudgetName@1000 : Record 95;
               BEGIN
                 IF GETFILTER("Budget Name") = '' THEN
                   GLBudgetName.INIT
                 ELSE BEGIN
                   COPYFILTER("Budget Name",GLBudgetName.Name);
                   GLBudgetName.FIND('-');
                 END;
                 CurrPage.EDITABLE := NOT GLBudgetName.Blocked;
                 GLSetup.GET;
                 "Global Dimension 1 CodeEnable" := GLSetup."Global Dimension 1 Code" <> '';
                 "Global Dimension 2 CodeEnable" := GLSetup."Global Dimension 2 Code" <> '';
                 "Budget Dimension 1 CodeEnable" := GLBudgetName."Budget Dimension 1 Code" <> '';
                 "Budget Dimension 2 CodeEnable" := GLBudgetName."Budget Dimension 2 Code" <> '';
                 "Budget Dimension 3 CodeEnable" := GLBudgetName."Budget Dimension 3 Code" <> '';
                 "Budget Dimension 4 CodeEnable" := GLBudgetName."Budget Dimension 4 Code" <> '';
                 "Global Dimension 1 CodeVisible" := GLSetup."Global Dimension 1 Code" <> '';
                 "Global Dimension 2 CodeVisible" := GLSetup."Global Dimension 2 Code" <> '';
                 "Budget Dimension 1 CodeVisible" := GLBudgetName."Budget Dimension 1 Code" <> '';
                 "Budget Dimension 2 CodeVisible" := GLBudgetName."Budget Dimension 2 Code" <> '';
                 "Budget Dimension 3 CodeVisible" := GLBudgetName."Budget Dimension 3 Code" <> '';
                 "Budget Dimension 4 CodeVisible" := GLBudgetName."Budget Dimension 4 Code" <> '';
               END;

    OnClosePage=VAR
                  UpdateAnalysisView@1000 : Codeunit 410;
                BEGIN
                  IF LowestModifiedEntryNo < 2147483647 THEN
                    UpdateAnalysisView.SetLastBudgetEntryNo(LowestModifiedEntryNo - 1);
                END;

    OnAfterGetRecord=BEGIN
                       OnAfterGetCurrRecord;
                     END;

    OnNewRecord=BEGIN
                  TempGLBudgetDim.DELETEALL;
                  "Budget Name" := GETRANGEMIN("Budget Name");
                  IF GLBudgetName.Name <> "Budget Name" THEN
                    GLBudgetName.GET("Budget Name");
                  IF GETFILTER("G/L Account No.") <> '' THEN
                    "G/L Account No." := GetFirstGLAcc(GETFILTER("G/L Account No."));
                  IF GETFILTER("Old G/L Account No.") <> '' THEN
                    "Old G/L Account No." := GetFirstHistoricGLAcc(GETFILTER("Old G/L Account No."));
                  Date := GetFirstDate(GETFILTER(Date));
                  "User ID" := USERID;

                  IF GETFILTER("Global Dimension 1 Code") <> '' THEN
                    "Global Dimension 1 Code" :=
                      GetFirstDimValue(GLSetup."Global Dimension 1 Code",GETFILTER("Global Dimension 1 Code"));

                  IF GETFILTER("Global Dimension 2 Code") <> '' THEN
                    "Global Dimension 2 Code" :=
                      GetFirstDimValue(GLSetup."Global Dimension 2 Code",GETFILTER("Global Dimension 2 Code"));

                  IF GETFILTER("Budget Dimension 1 Code") <> '' THEN
                    "Budget Dimension 1 Code" :=
                      GetFirstDimValue(GLBudgetName."Budget Dimension 1 Code",GETFILTER("Budget Dimension 1 Code"));

                  IF GETFILTER("Budget Dimension 2 Code") <> '' THEN
                    "Budget Dimension 2 Code" :=
                      GetFirstDimValue(GLBudgetName."Budget Dimension 2 Code",GETFILTER("Budget Dimension 2 Code"));

                  IF GETFILTER("Budget Dimension 3 Code") <> '' THEN
                    "Budget Dimension 3 Code" :=
                      GetFirstDimValue(GLBudgetName."Budget Dimension 3 Code",GETFILTER("Budget Dimension 3 Code"));

                  IF GETFILTER("Budget Dimension 4 Code") <> '' THEN
                    "Budget Dimension 4 Code" :=
                      GetFirstDimValue(GLBudgetName."Budget Dimension 4 Code",GETFILTER("Budget Dimension 4 Code"));

                  IF GETFILTER("Business Unit Code") <> '' THEN
                    "Business Unit Code" := GetFirstBusUnit(GETFILTER("Business Unit Code"));
                  OnAfterGetCurrRecord;
                END;

    OnModifyRecord=BEGIN
                     IF "Entry No." < LowestModifiedEntryNo THEN
                       LowestModifiedEntryNo := "Entry No.";
                     EXIT(TRUE);
                   END;

    OnDeleteRecord=BEGIN
                     IF "Entry No." < LowestModifiedEntryNo THEN
                       LowestModifiedEntryNo := "Entry No.";
                     EXIT(TRUE);
                   END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 23      ;1   ;ActionGroup;
                      CaptionML=[ENU=Ent&ry;
                                 ESP=&Movimiento] }
      { 24      ;2   ;Action    ;
                      ShortCutKey=May£s+Ctrl+D;
                      CaptionML=[ENU=Dimensions;
                                 ESP=Dimensiones];
                      RunObject=Page 561;
                      RunFormLink=Entry No.=FIELD(Entry No.);
                      Image=Dimensions }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                SourceExpr="Budget Name";
                Editable=FALSE }

    { 4   ;2   ;Field     ;
                SourceExpr=Date }

    { 6   ;2   ;Field     ;
                SourceExpr="G/L Account No." }

    { 1100000;2;Field     ;
                CaptionML=[ENU=Old G/L Account No.;
                           ESP=N§ de cuenta antiguo];
                SourceExpr="Old G/L Account No.";
                Visible=FALSE }

    { 21  ;2   ;Field     ;
                SourceExpr=Description }

    { 8   ;2   ;Field     ;
                SourceExpr="Global Dimension 1 Code";
                Visible="Global Dimension 1 CodeVisible";
                Enabled="Global Dimension 1 CodeEnable" }

    { 10  ;2   ;Field     ;
                SourceExpr="Global Dimension 2 Code";
                Visible="Global Dimension 2 CodeVisible";
                Enabled="Global Dimension 2 CodeEnable" }

    { 25  ;2   ;Field     ;
                SourceExpr="Budget Dimension 1 Code";
                Visible="Budget Dimension 1 CodeVisible";
                Enabled="Budget Dimension 1 CodeEnable" }

    { 27  ;2   ;Field     ;
                SourceExpr="Budget Dimension 2 Code";
                Visible="Budget Dimension 2 CodeVisible";
                Enabled="Budget Dimension 2 CodeEnable" }

    { 29  ;2   ;Field     ;
                SourceExpr="Budget Dimension 3 Code";
                Visible="Budget Dimension 3 CodeVisible";
                Enabled="Budget Dimension 3 CodeEnable" }

    { 31  ;2   ;Field     ;
                SourceExpr="Budget Dimension 4 Code";
                Visible="Budget Dimension 4 CodeVisible";
                Enabled="Budget Dimension 4 CodeEnable" }

    { 19  ;2   ;Field     ;
                SourceExpr="Business Unit Code";
                Visible=FALSE }

    { 12  ;2   ;Field     ;
                SourceExpr=Amount }

    { 14  ;2   ;Field     ;
                SourceExpr="Entry No.";
                Editable=FALSE }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      GLSetup@1000 : Record 98;
      GLBudgetName@1001 : Record 95;
      TempGLBudgetDim@1002 : TEMPORARY Record 361;
      CurrentEntryNo@1003 : Integer;
      LowestModifiedEntryNo@1004 : Integer;
      "Global Dimension 1 CodeVisible"@19028123 : Boolean INDATASET;
      "Global Dimension 2 CodeVisible"@19054752 : Boolean INDATASET;
      "Budget Dimension 1 CodeVisible"@19036733 : Boolean INDATASET;
      "Budget Dimension 2 CodeVisible"@19019212 : Boolean INDATASET;
      "Budget Dimension 3 CodeVisible"@19014779 : Boolean INDATASET;
      "Budget Dimension 4 CodeVisible"@19001818 : Boolean INDATASET;
      "Global Dimension 1 CodeEnable"@19057401 : Boolean INDATASET;
      "Global Dimension 2 CodeEnable"@19039220 : Boolean INDATASET;
      "Budget Dimension 1 CodeEnable"@19069571 : Boolean INDATASET;
      "Budget Dimension 2 CodeEnable"@19071456 : Boolean INDATASET;
      "Budget Dimension 3 CodeEnable"@19074873 : Boolean INDATASET;
      "Budget Dimension 4 CodeEnable"@19060014 : Boolean INDATASET;

    LOCAL PROCEDURE GetFirstGLAcc@3(GLAccFilter@1000 : Text[250]) : Code[20];
    VAR
      GLAcc@1001 : Record 15;
    BEGIN
      WITH GLAcc DO BEGIN
        SETFILTER("No.",GLAccFilter);
        IF FIND('-') THEN
          EXIT("No.")
        ELSE
          EXIT('');
      END;
    END;

    LOCAL PROCEDURE GetFirstDate@1(DateFilter@1001 : Text[250]) : Date;
    VAR
      Period@1002 : Record 2000000007;
    BEGIN
      IF DateFilter = '' THEN
        EXIT(0D);
      WITH Period DO BEGIN
        Period.SETRANGE("Period Type","Period Type"::Date);
        SETFILTER("Period Start",DateFilter);
        IF FIND('-') THEN
          EXIT("Period Start")
        ELSE
          EXIT(0D);
      END;
    END;

    LOCAL PROCEDURE GetFirstDimValue@2(DimCode@1000 : Code[20];DimValFilter@1001 : Text[250]) : Code[20];
    VAR
      DimVal@1002 : Record 349;
    BEGIN
      IF (DimCode = '') OR (DimValFilter = '') THEN
        EXIT('');
      WITH DimVal DO BEGIN
        SETRANGE("Dimension Code",DimCode);
        SETFILTER(Code,DimValFilter);
        IF FIND('-') THEN
          EXIT(Code)
        ELSE
          EXIT('');
      END;
    END;

    LOCAL PROCEDURE GetFirstBusUnit@5(BusUnitFilter@1000 : Text[250]) : Code[10];
    VAR
      BusUnit@1001 : Record 220;
    BEGIN
      WITH BusUnit DO BEGIN
        SETFILTER(Code,BusUnitFilter);
        IF FIND('-') THEN
          EXIT(Code)
        ELSE
          EXIT('');
      END;
    END;

    LOCAL PROCEDURE CheckIfDimChanged@11();
    VAR
      GLBudgetDim@1000 : Record 361;
      EOF_GLBudgetDim@1001 : Boolean;
      EOF_TempGLBudgetDim@1002 : Boolean;
      Match@1003 : Boolean;
    BEGIN
      IF "Entry No." >= LowestModifiedEntryNo THEN
        EXIT;

      IF "Entry No." <> CurrentEntryNo THEN BEGIN
        TempGLBudgetDim.DELETEALL;
        GLBudgetDim.SETRANGE("Entry No.","Entry No.");
        IF GLBudgetDim.FIND('-') THEN
          REPEAT
            TempGLBudgetDim := GLBudgetDim;
            TempGLBudgetDim.INSERT;
          UNTIL GLBudgetDim.NEXT = 0;
        CurrentEntryNo := "Entry No.";
      END ELSE BEGIN
        GLBudgetDim.SETRANGE("Entry No.","Entry No.");
        EOF_GLBudgetDim := NOT GLBudgetDim.FIND('-');
        EOF_TempGLBudgetDim := NOT TempGLBudgetDim.FIND('-');
        Match :=
          (EOF_GLBudgetDim = EOF_TempGLBudgetDim) AND
          (TempGLBudgetDim."Dimension Code" = GLBudgetDim."Dimension Code") AND
          (TempGLBudgetDim."Dimension Value Code" = GLBudgetDim."Dimension Value Code");
        WHILE Match AND (NOT EOF_GLBudgetDim) AND (NOT EOF_TempGLBudgetDim) DO BEGIN
          EOF_GLBudgetDim := GLBudgetDim.NEXT = 0;
          EOF_TempGLBudgetDim := TempGLBudgetDim.NEXT = 0;
          Match :=
            (EOF_GLBudgetDim = EOF_TempGLBudgetDim) AND
            (TempGLBudgetDim."Dimension Code" = GLBudgetDim."Dimension Code") AND
            (TempGLBudgetDim."Dimension Value Code" = GLBudgetDim."Dimension Value Code");
        END;
        IF NOT Match THEN
          LowestModifiedEntryNo := "Entry No.";
      END;
    END;

    LOCAL PROCEDURE GetFirstHistoricGLAcc@1100000(HistoricGLAccFilter@1100000 : Text[250]) : Code[20];
    VAR
      HistoricGLAcc@1100001 : Record 10721;
    BEGIN
      WITH HistoricGLAcc DO BEGIN
        SETFILTER("No.",HistoricGLAccFilter);
        IF FIND('-') THEN
          EXIT("No.")
        ELSE
          EXIT('');
      END;
    END;

    LOCAL PROCEDURE OnAfterGetCurrRecord@19077479();
    BEGIN
      xRec := Rec;
      IF "Entry No." <> 0 THEN
        CheckIfDimChanged;
      IF "Budget Name" <> xRec."Budget Name" THEN;
    END;

    BEGIN
    END.
  }
}
