OBJECT Page 6809 EP WP Table Actions
{
  OBJECT-PROPERTIES
  {
    Date=13/02/07;
    Time=12:00:00;
    Version List=NAVW15.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=EP WP Table Actions;
               ESP=Acciones tabla elto. Web EP];
    SourceTable=Table6809;
    DelayedInsert=Yes;
    SourceTableView=SORTING(Position)
                    ORDER(Ascending);
    DataCaptionFields=WP Request Code;
    PageType=List;
    AutoSplitKey=Yes;
    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 1000000012;1 ;ActionGroup;
                      CaptionML=[ENU=&Action;
                                 ESP=&Acci¢n] }
      { 1000000013;2 ;ActionGroup;
                      CaptionML=[ENU=&Create Head;
                                 ESP=&Crear cabecera] }
      { 1000000026;3 ;Action    ;
                      CaptionML=[ENU=Criteria;
                                 ESP=Criterio];
                      RunObject=Page 6824;
                      RunFormLink=Group Code=FIELD(Group Code),
                                  WP Request Code=FIELD(WP Request Code),
                                  Table No.=FIELD(Table No.),
                                  Header or Line=FIELD(Header or Line),
                                  Action Type=FIELD(Type),
                                  Position=FIELD(Position) }
      { 1000000027;2 ;Separator  }
      { 1000000028;2 ;ActionGroup;
                      CaptionML=[ENU=C&onnection between WP Requests;
                                 ESP=C&onexi¢n entre solicitudes elemento Web] }
      { 1000000025;3 ;Action    ;
                      CaptionML=[ENU=Fields;
                                 ESP=Campos];
                      OnAction=VAR
                                 EPGrpReqTypeActionFilterFields@1000000000 : Record 6813;
                                 frmEPGrpReqTypeActFilterFields@1000000001 : Form 6813;
                               BEGIN
                                 IF (Type IN [Type::"Connect to another WP Request",Type::"Show Report"]) THEN BEGIN
                                   EPGrpReqTypeActionFilterFields.RESET;
                                   EPGrpReqTypeActionFilterFields.SETRANGE("Group Code","Group Code");
                                   EPGrpReqTypeActionFilterFields.SETRANGE("WP Request Code","WP Request Code");
                                   EPGrpReqTypeActionFilterFields.SETRANGE("Table No.","Table No.");
                                   EPGrpReqTypeActionFilterFields.SETRANGE("Header or Line","Header or Line");
                                   EPGrpReqTypeActionFilterFields.SETRANGE("Action Type",Type);
                                   EPGrpReqTypeActionFilterFields.SETRANGE(Position,Position);
                                   EPGrpReqTypeActionFilterFields.SETRANGE("Apply Filter to",EPGrpReqTypeActionFilterFields."Apply Filter to"::Field);

                                   frmEPGrpReqTypeActFilterFields.SetActionCode(Code);

                                   IF Type = Type::"Show Report" THEN BEGIN
                                     TESTFIELD("Target Table No.");
                                     TESTFIELD("Report ID");
                                     frmEPGrpReqTypeActFilterFields.SetPrintActionValues(TRUE,"Target Table No.")
                                   END ELSE
                                     frmEPGrpReqTypeActFilterFields.SetPrintActionValues(FALSE,"Target Table No.");

                                   frmEPGrpReqTypeActFilterFields.SETTABLEVIEW(EPGrpReqTypeActionFilterFields);
                                   frmEPGrpReqTypeActFilterFields.RUNMODAL;
                                 END ELSE
                                   ERROR(Text003);
                               END;
                                }
      { 1000000014;3 ;Action    ;
                      CaptionML=[ENU=Keys;
                                 ESP=Claves];
                      OnAction=VAR
                                 EPGrpReqTypeActionFilterKeys@1000000001 : Record 6813;
                                 frmEPGrpReqTypeActFilterKeys@1000000000 : Form 6814;
                               BEGIN
                                 IF (Type IN [Type::"Connect to another WP Request",Type::"Show Report"]) THEN BEGIN
                                   EPGrpReqTypeActionFilterKeys.RESET;
                                   EPGrpReqTypeActionFilterKeys.FILTERGROUP(4);
                                   EPGrpReqTypeActionFilterKeys.SETRANGE("Group Code","Group Code");
                                   EPGrpReqTypeActionFilterKeys.SETRANGE("WP Request Code","WP Request Code");
                                   EPGrpReqTypeActionFilterKeys.SETRANGE("Table No.","Table No.");
                                   EPGrpReqTypeActionFilterKeys.SETRANGE("Header or Line","Header or Line");
                                   EPGrpReqTypeActionFilterKeys.SETRANGE("Action Type",Type);
                                   EPGrpReqTypeActionFilterKeys.SETRANGE(Position,Position);
                                   EPGrpReqTypeActionFilterKeys.SETRANGE("Apply Filter to",EPGrpReqTypeActionFilterKeys."Apply Filter to"::Key);
                                   EPGrpReqTypeActionFilterKeys.FILTERGROUP(0);

                                   frmEPGrpReqTypeActFilterKeys.SetActionCode(Code);

                                   IF Type = Type::"Show Report" THEN BEGIN
                                     TESTFIELD("Target Table No.");
                                     TESTFIELD("Report ID");
                                     frmEPGrpReqTypeActFilterKeys.SetPrintActionValues(TRUE,"Target Table No.")
                                   END ELSE
                                     frmEPGrpReqTypeActFilterKeys.SetPrintActionValues(FALSE,"Target Table No.");

                                   frmEPGrpReqTypeActFilterKeys.SETTABLEVIEW(EPGrpReqTypeActionFilterKeys);
                                   frmEPGrpReqTypeActFilterKeys.RUNMODAL;
                                 END ELSE
                                   ERROR(Text003);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1000000000;1;Group  ;
                GroupType=Repeater }

    { 1000000021;2;Field  ;
                SourceExpr=Type }

    { 1000000008;2;Field  ;
                SourceExpr="Target WP Request" }

    { 1000000001;2;Field  ;
                Lookup=Yes;
                SourceExpr=Code;
                OnValidate=BEGIN
                             IF Type <> Type::"Connect to another WP Request" THEN
                               EXIT;
                           END;

                OnLookup=VAR
                           EPGrpReqType@1000000000 : Record 6805;
                           tmpReqTypeType@1000000004 : ' ,List,Detail,Head+Line,System';
                         BEGIN
                           IF Type <> Type::"Connect to another WP Request" THEN
                             EXIT;

                           EPGrpReqType.RESET;
                           EPGrpReqType.SETRANGE("Group Code","Group Code");
                           EPGrpReqType.SETRANGE(Code,"WP Request Code");
                           IF NOT EPGrpReqType.FIND('-') THEN BEGIN
                             ERROR(Text001);
                           END ELSE BEGIN
                             tmpReqTypeType := EPGrpReqType.Type;
                           END;

                           EPGrpReqType.RESET;
                           EPGrpReqType.SETRANGE("Group Code","Group Code");
                           EPGrpReqType.SETFILTER(Code,'<>%1',"WP Request Code");
                           EPGrpReqType.FILTERGROUP(4);
                           IF "Target WP Request" = "Target WP Request"::Current THEN BEGIN
                             CASE tmpReqTypeType OF
                               tmpReqTypeType::"Head+Line":
                                 EPGrpReqType.SETRANGE(Type,EPGrpReqType.Type::"Header + Line");
                               tmpReqTypeType::List:
                                 EPGrpReqType.SETRANGE(Type,EPGrpReqType.Type::List);
                               tmpReqTypeType::Detail:
                                 EPGrpReqType.SETRANGE(Type,EPGrpReqType.Type::Card);
                               ELSE
                                 ERROR(Text002,EPGrpReqType.FIELDCAPTION(Type));
                             END;
                           END;
                           EPGrpReqType.FILTERGROUP(0);

                           IF ACTION::LookupOK = FORM.RUNMODAL(FORM::"EP Web Part Request List",EPGrpReqType) THEN BEGIN
                             VALIDATE(Code,EPGrpReqType.Code);
                             Description := EPGrpReqType.Description;
                           END;
                         END;
                          }

    { 1000000029;2;Field  ;
                SourceExpr="Target Table No.";
                LookupFormID=Objects }

    { 1000000034;2;Field  ;
                SourceExpr="Report ID";
                LookupFormID=Objects }

    { 1000000003;2;Field  ;
                SourceExpr=Description }

    { 1   ;2   ;Field     ;
                SourceExpr="Picture Code" }

    { 1000000017;2;Field  ;
                SourceExpr="Field Validation Required" }

    { 1000000023;2;Field  ;
                SourceExpr="Show in Mode" }

    { 1000000010;2;Field  ;
                SourceExpr="HTML Layout" }

    { 1000000015;2;Field  ;
                SourceExpr=Position }

    { 1000000019;2;Field  ;
                SourceExpr="Restrict to Editable" }

    { 1000000032;2;Field  ;
                AssistEdit=Yes;
                SourceExpr=Caption;
                OnAssistEdit=VAR
                               EPCaption@1000 : Record 6850;
                             BEGIN
                               EPCaption.RESET;
                               EPCaption.FILTERGROUP(2);
                               EPCaption.SETRANGE("Table No.",6809);
                               EPCaption.SETRANGE("Language ID",GLOBALLANGUAGE);
                               EPCaption.FILTERGROUP(0);
                               IF EPCaption.FIND('-') THEN;

                               IF ACTION::LookupOK = FORM.RUNMODAL(FORM::"EP WP Action Captions",EPCaption) THEN BEGIN
                                 "Caption ID" := EPCaption."Caption ID";
                                 Caption := EPCaption.Caption;
                               END;
                               CurrPage.UPDATE;
                             END;
                              }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=The Web Part Request cannot be found.;ESP=No se encuentra la Solicitud elemento Web';
      Text002@1001 : TextConst 'ENU=The Web Part Request does not have a valid %1.;ESP=La Solicitud elemento Web no tiene un %1 v lido.';
      Text003@1002 : TextConst 'ENU=A connection cannot be defined for this type of Action.;ESP=No se puede definir una conexi¢n para este tipo de acci¢n.';

    BEGIN
    END.
  }
}
