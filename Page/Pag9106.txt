OBJECT Page 9106 Customer Ledger Entry FactBox
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Customer Ledger Entry Details;
               ESP=Detalles movimiento cliente];
    SourceTable=Table21;
    PageType=CardPart;
    OnOpenPage=BEGIN
                 DocumentHeading := GetDocumentHeading(Rec);
                 CalcNoOfRecords;
               END;

    OnFindRecord=BEGIN
                   NoOfReminderFinEntries := 0;
                   NoOfAppliedEntries := 0;
                   DocumentHeading := '';

                   EXIT(FIND(Which));
                 END;

    OnAfterGetRecord=BEGIN
                       DocumentHeading := GetDocumentHeading(Rec);
                       CalcNoOfRecords;
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 13  ;1   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=Document;
                           ESP=Documento];
                SourceExpr=DocumentHeading;
                OnDrillDown=VAR
                              SalesInvoiceHdr@1000 : Record 112;
                              SalesCrMemoHdr@1001 : Record 114;
                              GLEntry@1006 : Record 17;
                              PostedSalesInvoiceCard@1002 : Form 132;
                              PostedSalesCrMemoCard@1003 : Form 134;
                              GeneralLedgEntriesList@1007 : Form 20;
                              CheckGLEntry@1005 : Boolean;
                            BEGIN
                              CheckGLEntry := TRUE;

                              IF "Document Type" = "Document Type"::Invoice THEN BEGIN
                                SalesInvoiceHdr.SETRANGE("No.","Document No.");
                                IF SalesInvoiceHdr.FINDFIRST THEN BEGIN
                                  PostedSalesInvoiceCard.SETTABLEVIEW(SalesInvoiceHdr);
                                  PostedSalesInvoiceCard.RUN;
                                  CheckGLEntry := FALSE;
                                END
                              END;

                              IF "Document Type" = "Document Type"::"Credit Memo" THEN BEGIN
                                SalesCrMemoHdr.SETRANGE("No.","Document No.");
                                IF SalesCrMemoHdr.FINDFIRST THEN BEGIN
                                  PostedSalesCrMemoCard.SETTABLEVIEW(SalesCrMemoHdr);
                                  PostedSalesCrMemoCard.RUN;
                                  CheckGLEntry := FALSE;
                                END
                              END;

                              IF CheckGLEntry THEN BEGIN
                                GLEntry.SETCURRENTKEY("Document No.","Posting Date");
                                GLEntry.SETRANGE("Document No.","Document No.");
                                GLEntry.SETRANGE("Posting Date","Posting Date");
                                GeneralLedgEntriesList.SETTABLEVIEW(GLEntry);
                                GeneralLedgEntriesList.RUN
                              END;
                            END;
                             }

    { 1   ;1   ;Field     ;
                SourceExpr="Due Date" }

    { 3   ;1   ;Field     ;
                SourceExpr="Pmt. Discount Date" }

    { 5   ;1   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=Reminder/Fin. Charge Entries;
                           ESP=Movs. recordatorio/inter‚s];
                SourceExpr=NoOfReminderFinEntries;
                Editable=TRUE;
                OnDrillDown=VAR
                              ReminderFinEntry@1001 : Record 300;
                              ReminderFinEntriesList@1000 : Form 444;
                            BEGIN
                              ReminderFinEntry.SETRANGE("Customer Entry No.","Entry No.");
                              ReminderFinEntriesList.SETTABLEVIEW(ReminderFinEntry);
                              ReminderFinEntriesList.RUN;
                            END;
                             }

    { 8   ;1   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=Applied Entries;
                           ESP=Movs. conciliados];
                SourceExpr=NoOfAppliedEntries;
                Editable=TRUE;
                OnDrillDown=VAR
                              CustLedEntry@1001 : Record 21;
                              AppliedCustomerEntriesList@1000 : Form 61;
                            BEGIN
                              AppliedCustomerEntriesList.SetTempCustLedgEntry("Entry No.");
                              AppliedCustomerEntriesList.RUN;
                            END;
                             }

  }
  CODE
  {
    VAR
      NoOfReminderFinEntries@1002 : Integer;
      NoOfAppliedEntries@1000 : Integer;
      DocumentHeading@1001 : Text[250];
      Text000@1003 : TextConst 'ENU=Document;ESP=Documento';

    PROCEDURE CalcNoOfRecords@3();
    VAR
      ReminderFinChargeEntry@1005 : Record 300;
    BEGIN
      ReminderFinChargeEntry.RESET;
      ReminderFinChargeEntry.SETRANGE("Customer Entry No.","Entry No.");
      NoOfReminderFinEntries := ReminderFinChargeEntry.COUNT;

      NoOfAppliedEntries := 0;
      IF Rec."Entry No." <> 0 THEN
        NoOfAppliedEntries := GetNoOfAppliedEntries(Rec);
    END;

    PROCEDURE GetNoOfAppliedEntries@1(CustLedgerEntry@1000 : Record 21) : Integer;
    BEGIN
      GetAppliedEntries(CustLedgerEntry);
      EXIT(CustLedgerEntry.COUNT);
    END;

    PROCEDURE GetAppliedEntries@2(VAR CustLedgerEntry@1000 : Record 21);
    VAR
      DtldCustLedgEntry1@1005 : Record 379;
      DtldCustLedgEntry2@1004 : Record 379;
      CreateCustLedgEntry@1002 : Record 21;
    BEGIN
      CreateCustLedgEntry := CustLedgerEntry;

      DtldCustLedgEntry1.SETCURRENTKEY("Cust. Ledger Entry No.");
      DtldCustLedgEntry1.SETRANGE("Cust. Ledger Entry No.",CreateCustLedgEntry."Entry No.");
      DtldCustLedgEntry1.SETRANGE(Unapplied,FALSE);
      IF DtldCustLedgEntry1.FINDSET THEN BEGIN
        REPEAT
          IF DtldCustLedgEntry1."Cust. Ledger Entry No." =
            DtldCustLedgEntry1."Applied Cust. Ledger Entry No."
          THEN BEGIN
            DtldCustLedgEntry2.INIT;
            DtldCustLedgEntry2.SETCURRENTKEY("Applied Cust. Ledger Entry No.","Entry Type");
            DtldCustLedgEntry2.SETRANGE(
              "Applied Cust. Ledger Entry No.",DtldCustLedgEntry1."Applied Cust. Ledger Entry No.");
            DtldCustLedgEntry2.SETRANGE("Entry Type",DtldCustLedgEntry2."Entry Type"::Application);
            DtldCustLedgEntry2.SETRANGE(Unapplied,FALSE);
            IF DtldCustLedgEntry2.FINDFIRST THEN BEGIN
              REPEAT
                IF DtldCustLedgEntry2."Cust. Ledger Entry No." <>
                  DtldCustLedgEntry2."Applied Cust. Ledger Entry No."
                THEN BEGIN
                  CustLedgerEntry.SETCURRENTKEY("Entry No.");
                  CustLedgerEntry.SETRANGE("Entry No.",DtldCustLedgEntry2."Cust. Ledger Entry No.");
                  IF CustLedgerEntry.FINDFIRST THEN BEGIN
                    CustLedgerEntry.MARK(TRUE);
                  END;
                END;
              UNTIL DtldCustLedgEntry2.NEXT = 0;
            END;
          END ELSE BEGIN
            CustLedgerEntry.SETCURRENTKEY("Entry No.");
            CustLedgerEntry.SETRANGE("Entry No.",DtldCustLedgEntry1."Applied Cust. Ledger Entry No.");
            IF CustLedgerEntry.FINDFIRST THEN
              CustLedgerEntry.MARK(TRUE);
          END;
        UNTIL DtldCustLedgEntry1.NEXT = 0;
      END;

      CustLedgerEntry.SETCURRENTKEY("Entry No.");
      CustLedgerEntry.SETRANGE("Entry No.");

      IF CreateCustLedgEntry."Closed by Entry No." <> 0 THEN BEGIN
        CustLedgerEntry."Entry No." := CreateCustLedgEntry."Closed by Entry No.";
        CustLedgerEntry.MARK(TRUE);
      END;

      CustLedgerEntry.SETCURRENTKEY("Closed by Entry No.");
      CustLedgerEntry.SETRANGE("Closed by Entry No.",CreateCustLedgEntry."Entry No.");
      IF CustLedgerEntry.FINDSET THEN
        REPEAT
          CustLedgerEntry.MARK(TRUE);
        UNTIL CustLedgerEntry.NEXT = 0;

      CustLedgerEntry.SETCURRENTKEY("Entry No.");
      CustLedgerEntry.SETRANGE("Closed by Entry No.");

      CustLedgerEntry.MARKEDONLY(TRUE);
    END;

    PROCEDURE GetDocumentHeading@6(CustLedgerEntry@1000 : Record 21) : Text[50];
    VAR
      Heading@1001 : Text[50];
    BEGIN
      IF CustLedgerEntry."Document Type" = 0 THEN
        Heading := Text000
      ELSE
        Heading := FORMAT(CustLedgerEntry."Document Type");
      Heading := Heading + ' ' + CustLedgerEntry."Document No.";
      EXIT(Heading);
    END;

    BEGIN
    END.
  }
}
