OBJECT Page 693 Program Selection
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Program Selection;
               ESP=Selecci¢n programa];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table2000000065;
    DataCaptionExpr=STRSUBSTNO(Text001,AllObjWithCaption."Object Caption",Name);
    PageType=List;
    OnAfterGetRecord=BEGIN
                       GetDefaultStylesheet;
                       OnAfterGetCurrRecord;
                     END;

    OnNewRecord=BEGIN
                  OnAfterGetCurrRecord;
                END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                CaptionML=[ENU=Name;
                           ESP=Nombre];
                SourceExpr=Name;
                Editable=FALSE }

    { 4   ;2   ;Field     ;
                CaptionML=[ENU=Executable;
                           ESP=Ejecutable];
                SourceExpr=Executable;
                Visible=FALSE;
                Editable=FALSE }

    { 9   ;2   ;Field     ;
                CaptionML=[ENU=Style Sheet;
                           ESP=Hoja estilos];
                SourceExpr=StylesheetName;
                OnValidate=BEGIN
                             ValidateStylesheet;
                               StylesheetNameOnAfterValidate;
                           END;

                OnLookup=BEGIN
                           LookupStylesheet;
                         END;
                          }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      AllObjWithCaption@1004 : Record 2000000058;
      StylesheetName@1000 : Text[250];
      ObjectType@1001 : Integer;
      ObjectID@1002 : Integer;
      StylesheetID@1003 : GUID;
      Text001@1005 : TextConst 'ENU=Send %1 to %2;ESP=Enviar %1 a %2';

    PROCEDURE SetObjectID@9(NewObjectType@1000 : Integer;NewObjectID@1001 : Integer);
    BEGIN
      ObjectType := NewObjectType;
      ObjectID := NewObjectID;
      IF NOT AllObjWithCaption.GET(AllObjWithCaption."Object Type"::Form,NewObjectID) THEN
        AllObjWithCaption.INIT;
    END;

    PROCEDURE GetSelectedStyleSheetID@1() : GUID;
    BEGIN
      GetDefaultStylesheet;
      EXIT(StylesheetID);
    END;

    LOCAL PROCEDURE LookupStylesheet@2();
    VAR
      Stylesheet@1000 : Record 2000000066;
      Stylesheets@1001 : Form 694;
    BEGIN
      Stylesheet.SETRANGE("Program ID","Program ID");
      Stylesheet.SETRANGE("Object Type",ObjectType);
      Stylesheet.SETFILTER("Object ID",'%1|%2',0,ObjectID);
      IF StylesheetName <> '' THEN BEGIN
        Stylesheet.SETRANGE(Name,StylesheetName);
        IF Stylesheet.FIND('-') THEN
          Stylesheets.SETRECORD(Stylesheet);
        Stylesheet.SETRANGE(Name);
      END;
      Stylesheets.SetParams(ObjectType,ObjectID,Name);
      Stylesheets.LOOKUPMODE := TRUE;
      Stylesheets.SETTABLEVIEW(Stylesheet);
      IF Stylesheets.RUNMODAL = ACTION::LookupOK THEN BEGIN
        Stylesheets.GETRECORD(Stylesheet);
        SetDefaultStylesheet(Stylesheet);
      END;
    END;

    LOCAL PROCEDURE ValidateStylesheet@3();
    VAR
      Stylesheet@1000 : Record 2000000066;
    BEGIN
      Stylesheet.SETRANGE("Program ID","Program ID");
      Stylesheet.SETRANGE("Object Type",ObjectType);
      Stylesheet.SETFILTER("Object ID",'%1|%2',0,ObjectID);
      Stylesheet.SETRANGE(Name,StylesheetName);
      IF NOT Stylesheet.FIND('-') THEN BEGIN
        Stylesheet.SETFILTER(Name,'*@' + StylesheetName + '*');
        Stylesheet.FIND('-')
      END;
      SetDefaultStylesheet(Stylesheet);
    END;

    LOCAL PROCEDURE GetDefaultStylesheet@5();
    VAR
      UserDefaultStylesheet@1000 : Record 2000000067;
      Stylesheet@1001 : Record 2000000066;
      Found@1002 : Boolean;
    BEGIN
      IF UserDefaultStylesheet.GET(UPPERCASE(USERID),ObjectType,ObjectID,"Program ID") THEN
        Found := Stylesheet.GET(UserDefaultStylesheet."Style Sheet ID");

      IF NOT Found THEN BEGIN
        Stylesheet.SETRANGE("Object ID",ObjectID);
        Stylesheet.SETRANGE("Object Type",ObjectType);
        Stylesheet.SETRANGE("Program ID","Program ID");
        Found := Stylesheet.FIND('-');
        IF NOT Found THEN BEGIN
          Stylesheet.SETRANGE("Object ID",0);
          Found := Stylesheet.FIND('-');
        END;
      END;
      IF Found THEN BEGIN
        StylesheetID := Stylesheet."Style Sheet ID";
        StylesheetName := Stylesheet.Name;
      END ELSE BEGIN
        CLEAR(StylesheetID);
        StylesheetName := '';
      END;
    END;

    LOCAL PROCEDURE SetDefaultStylesheet@10(VAR Stylesheet@1000 : Record 2000000066);
    VAR
      UserDefaultStylesheet@1001 : Record 2000000067;
    BEGIN
      StylesheetID := Stylesheet."Style Sheet ID";
      StylesheetName := Stylesheet.Name;

      UserDefaultStylesheet.SETRANGE("User ID",UPPERCASE(USERID));
      UserDefaultStylesheet.SETRANGE("Object Type",Stylesheet."Object Type");
      UserDefaultStylesheet.SETRANGE("Object ID",ObjectID);
      UserDefaultStylesheet.SETRANGE("Program ID",Stylesheet."Program ID");
      UserDefaultStylesheet.DELETEALL;

      UserDefaultStylesheet."User ID" := UPPERCASE(USERID);
      UserDefaultStylesheet."Object Type" := Stylesheet."Object Type";
      UserDefaultStylesheet."Object ID" := ObjectID;
      UserDefaultStylesheet."Program ID" := Stylesheet."Program ID";
      UserDefaultStylesheet."Style Sheet ID" := Stylesheet."Style Sheet ID";
      UserDefaultStylesheet.INSERT;
    END;

    LOCAL PROCEDURE StylesheetNameOnAfterValidate@19058654();
    BEGIN
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE OnAfterGetCurrRecord@19077479();
    BEGIN
      xRec := Rec;
      GetDefaultStylesheet;
    END;

    BEGIN
    END.
  }
}
