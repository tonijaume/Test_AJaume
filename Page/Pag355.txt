OBJECT Page 355 Receivables-Payables Lines
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Lines;
               ESP=L¡neas];
    LinksAllowed=No;
    SourceTable=Table2000000007;
    PageType=ListPart;
    OnOpenPage=BEGIN
                 RESET;
               END;

    OnFindRecord=BEGIN
                   EXIT(PeriodFormMgt.FindDate(Which,Rec,EntrdPeriodLength));
                 END;

    OnNextRecord=BEGIN
                   EXIT(PeriodFormMgt.NextDate(Steps,Rec,EntrdPeriodLength));
                 END;

    OnAfterGetRecord=BEGIN
                       SetDateFilter;
                       GLSetup.CALCFIELDS("Cust. Balances Due","Vendor Balances Due");
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                Editable=FALSE;
                GroupType=Repeater }

    { 18  ;2   ;Field     ;
                CaptionML=[ENU=Period Start;
                           ESP=Inicio periodo];
                SourceExpr="Period Start" }

    { 20  ;2   ;Field     ;
                CaptionML=[ENU=Period Name;
                           ESP=Nombre periodo];
                SourceExpr="Period Name" }

    { 22  ;2   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=Cust. Balances Due;
                           ESP=Saldo pdte. clientes];
                SourceExpr=GLSetup."Cust. Balances Due";
                AutoFormatType=1;
                OnDrillDown=BEGIN
                              ShowCustEntriesDue;
                            END;
                             }

    { 24  ;2   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=Vendor Balances Due;
                           ESP=Saldo pdte. proveedores];
                SourceExpr=GLSetup."Vendor Balances Due";
                AutoFormatType=1;
                OnDrillDown=BEGIN
                              ShowVendEntriesDue;
                            END;
                             }

    { 12  ;2   ;Field     ;
                CaptionML=[ENU=Receivables-Payables;
                           ESP=Cobros y pagos];
                SourceExpr=GLSetup."Cust. Balances Due" - GLSetup."Vendor Balances Due";
                AutoFormatType=1 }

  }
  CODE
  {
    VAR
      GLSetup@1000 : Record 98;
      CustLedgEntry@1001 : Record 21;
      VendLedgEntry@1002 : Record 25;
      PeriodFormMgt@1003 : Codeunit 359;
      EntrdPeriodLength@1004 : 'Day,Week,Month,Quarter,Year,Period';
      AmountType@1005 : 'Net Change,Balance at Date';

    PROCEDURE Set@1(VAR NewGLSetup@1000 : Record 98;NewEntrdPeriodLength@1001 : Integer;NewAmountType@1002 : 'Net Change,Balance at Date');
    BEGIN
      GLSetup.COPY(NewGLSetup);
      EntrdPeriodLength := NewEntrdPeriodLength;
      AmountType := NewAmountType;
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE ShowCustEntriesDue@2();
    BEGIN
      SetDateFilter;
      CustLedgEntry.RESET;
      CustLedgEntry.SETCURRENTKEY(Open,"Due Date");
      CustLedgEntry.SETRANGE(Open,TRUE);
      CustLedgEntry.SETFILTER("Due Date",GLSetup.GETFILTER("Date Filter"));
      CustLedgEntry.SETFILTER("Global Dimension 1 Code",GLSetup.GETFILTER("Global Dimension 1 Filter"));
      CustLedgEntry.SETFILTER("Global Dimension 2 Code",GLSetup.GETFILTER("Global Dimension 2 Filter"));
      FORM.RUN(0,CustLedgEntry)
    END;

    LOCAL PROCEDURE ShowVendEntriesDue@3();
    BEGIN
      SetDateFilter;
      VendLedgEntry.RESET;
      VendLedgEntry.SETCURRENTKEY(Open,"Due Date");
      VendLedgEntry.SETRANGE(Open,TRUE);
      VendLedgEntry.SETFILTER("Due Date",GLSetup.GETFILTER("Date Filter"));
      VendLedgEntry.SETFILTER("Global Dimension 1 Code",GLSetup.GETFILTER("Global Dimension 1 Filter"));
      VendLedgEntry.SETFILTER("Global Dimension 2 Code",GLSetup.GETFILTER("Global Dimension 2 Filter"));
      FORM.RUN(0,VendLedgEntry);
    END;

    LOCAL PROCEDURE SetDateFilter@4();
    BEGIN
      IF AmountType = AmountType::"Net Change" THEN
        GLSetup.SETRANGE("Date Filter","Period Start","Period End")
      ELSE
        GLSetup.SETRANGE("Date Filter",0D,"Period End");
    END;

    BEGIN
    END.
  }
}
