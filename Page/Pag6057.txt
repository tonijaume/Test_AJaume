OBJECT Page 6057 Contract Line Selection
{
  OBJECT-PROPERTIES
  {
    Date=15/09/04;
    Time=12:00:00;
    Version List=NAVW14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Contract Line Selection;
               ESP=Selecci¢n l¡nea contrato];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table5940;
    DataCaptionFields=Customer No.;
    PageType=Worksheet;
    OnInit=BEGIN
             CurrPage.LOOKUPMODE := TRUE;
           END;

    OnOpenPage=BEGIN
                 OKButton := FALSE;
                 SETCURRENTKEY("Customer No.","Ship-to Code");
                 FILTERGROUP(2);
                 SETRANGE("Customer No.",CustomerNo);
                 SETFILTER(Status,'<>%1',Status::Defective);
                 FILTERGROUP(0);
                 SETRANGE("Ship-to Code",ShipToCode);
                 SetSelectionFilter;
               END;

    OnQueryClosePage=BEGIN
                       IF CloseAction = ACTION::LookupOK THEN
                         LookupOKOnPush;
                       IF OKButton THEN BEGIN
                         ServContract.GET(ContractType,ContractNo);
                         Rec.MARKEDONLY(TRUE);
                         ServContractLine.HideDialogBox(TRUE);
                         IF Rec.FIND('-') THEN BEGIN
                           REPEAT
                             CheckServContractLine;
                           UNTIL Rec.NEXT = 0;
                           Rec.MARKEDONLY(FALSE);
                         END ELSE BEGIN
                           Rec.MARKEDONLY(FALSE);
                           CheckServContractLine;
                         END;
                         CreateServContractLines;
                         COMMIT;
                       END;
                     END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 32      ;1   ;ActionGroup;
                      CaptionML=[ENU=&Serv. Item;
                                 ESP=&Prod. serv.] }
      { 33      ;2   ;Action    ;
                      ShortCutKey=May£s+F7;
                      CaptionML=[ENU=&Card;
                                 ESP=&Ficha];
                      RunObject=Page 5980;
                      RunFormOnRec=Yes;
                      RunFormLink=No.=FIELD(No.);
                      Image=EditLines }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 2   ;1   ;Field     ;
                CaptionML=[ENU=Selection Filter;
                           ESP=Filtro selecci¢n];
                OptionCaptionML=[ENU=All Service Items,Service Items without Contract;
                                 ESP=Todos prod. servicio,Prod. servicio sin contrato];
                SourceExpr=SelectionFilter;
                OnValidate=BEGIN
                             SelectionFilterOnAfterValidate;
                           END;
                            }

    { 1   ;1   ;Group     ;
                Editable=FALSE;
                GroupType=Repeater }

    { 6   ;2   ;Field     ;
                SourceExpr="No." }

    { 18  ;2   ;Field     ;
                CaptionML=[ENU=Item No.;
                           ESP=N§ producto];
                SourceExpr="Item No." }

    { 4   ;2   ;Field     ;
                SourceExpr="Serial No." }

    { 22  ;2   ;Field     ;
                SourceExpr=Description }

    { 28  ;2   ;Field     ;
                SourceExpr="Customer No." }

    { 10  ;2   ;Field     ;
                SourceExpr="Ship-to Code" }

    { 14  ;2   ;Field     ;
                SourceExpr="Vendor No.";
                Visible=FALSE }

    { 8   ;2   ;Field     ;
                SourceExpr="Vendor Item No.";
                Visible=FALSE }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      ServContract@1000 : Record 5965;
      ServContractLine@1008 : Record 5964;
      ServItem@1001 : Record 5940;
      OKButton@1002 : Boolean;
      AllServItems@1003 : Boolean;
      CustomerNo@1004 : Code[20];
      ShipToCode@1005 : Code[10];
      ContractNo@1006 : Code[20];
      ContractType@1007 : Integer;
      LineNo@1009 : Integer;
      Text000@1010 : TextConst 'ENU=%1 %2 already exists in this service contract.;ESP=Ya existe %1 %2 en este contrato servicio.';
      Text001@1012 : TextConst 'ENU=%1 %2 already belongs to one or more service contracts/quotes.\\Do you want to include this service item into the document?;ESP=%1 %2 ya pertenece a una o m s ofertas/contratos servicio.\\¨Desea incluir este prod. servicio en el documento?';
      Text002@1011 : TextConst 'ENU=%1 %2 has a different ship-to code for this customer.\\Do you want to include this service item into the document?;ESP=%1 %2 tiene un C¢d. direcci¢n env¡o diferente para este cliente.\\¨Desea incluir este prod. servicio en el documento?';
      SelectionFilter@1013 : 'All Service Items,Service Items without Contract';

    PROCEDURE SetSelection@1(CustNo@1000 : Code[20];ShipNo@1001 : Code[10];CtrType@1004 : Integer;CtrNo@1002 : Code[20];AllServItem@1003 : Boolean);
    BEGIN
      AllServItems := AllServItem;
      CustomerNo := CustNo;
      ShipToCode := ShipNo;
      ContractType := CtrType;
      ContractNo := CtrNo;
    END;

    PROCEDURE FindlineNo@2() : Integer;
    BEGIN
      ServContractLine.RESET;
      ServContractLine.SETRANGE("Contract Type",ContractType);
      ServContractLine.SETRANGE("Contract No.",ContractNo);
      IF ServContractLine.FIND('+') THEN
        EXIT(ServContractLine."Line No.")
      ELSE
        EXIT(0);
    END;

    PROCEDURE CheckServContractLine@3();
    BEGIN
      ServItem.GET("No.");

      ServContractLine.RESET;
      ServContractLine.SETCURRENTKEY("Service Item No.");
      ServContractLine.SETRANGE("Contract No.",ServContract."Contract No.");
      ServContractLine.SETRANGE("Contract Type",ServContract."Contract Type");
      ServContractLine.SETRANGE("Service Item No.",ServItem."No.");
      IF ServContractLine.FIND('-') THEN BEGIN
        MESSAGE(Text000,ServItem.TABLECAPTION,ServItem."No.");
        EXIT;
      END;

      ServContractLine.RESET;
      ServContractLine.SETCURRENTKEY("Service Item No.","Contract Status");
      ServContractLine.SETRANGE("Service Item No.",ServItem."No.");
      ServContractLine.SETFILTER("Contract Status",'<>%1',ServContractLine."Contract Status"::Cancelled);
      ServContractLine.SETRANGE("Contract Type",ServContractLine."Contract Type"::Contract);
      ServContractLine.SETFILTER("Contract No.",'<>%1',ServContract."Contract No.");
      IF ServContractLine.FIND('-') THEN BEGIN
        IF NOT CONFIRM(Text001,TRUE,ServItem.TABLECAPTION,ServItem."No.") THEN
          EXIT;
      END ELSE BEGIN
        ServContractLine.RESET;
        ServContractLine.SETCURRENTKEY("Service Item No.");
        ServContractLine.SETRANGE("Service Item No.",ServItem."No.");
        ServContractLine.SETRANGE("Contract Type",ServContractLine."Contract Type"::Quote);
        ServContractLine.SETFILTER("Contract No.",'<>%1',ServContract."Contract No.");
        IF ServContractLine.FIND('-') THEN
          IF NOT CONFIRM(Text001,TRUE,ServItem.TABLECAPTION,ServItem."No.") THEN
            EXIT;
      END;

      IF (ServItem."Ship-to Code" <> ServContract."Ship-to Code") THEN
        IF NOT CONFIRM(Text002,FALSE,ServItem.TABLECAPTION,ServItem."No.") THEN
          EXIT;

      ServItem.MARK(TRUE);
    END;

    PROCEDURE CreateServContractLines@4();
    BEGIN
      ServContractLine.LOCKTABLE;
      LineNo := FindlineNo + 10000;
      ServItem.MARKEDONLY(TRUE);
      IF ServItem.FIND('-') THEN
        REPEAT
          ServContractLine.INIT;
          ServContractLine.HideDialogBox(TRUE);
          ServContractLine."Contract Type" := ServContract."Contract Type";
          ServContractLine."Contract No." := ServContract."Contract No.";
          ServContractLine."Line No." := LineNo;
          ServContractLine.SetupNewLine;
          ServContractLine.VALIDATE("Service Item No.",ServItem."No.");
          ServContractLine.VALIDATE("Line Value",ServItem."Default Contract Value");
          ServContractLine.VALIDATE("Line Discount %",ServItem."Default Contract Discount %");
          ServContractLine.INSERT(TRUE);
          LineNo := LineNo + 10000;
        UNTIL ServItem.NEXT = 0
    END;

    PROCEDURE SetSelectionFilter@5();
    BEGIN
      CASE SelectionFilter OF
        SelectionFilter::"All Service Items":
          SETRANGE("No. of Active Contracts");
        SelectionFilter::"Service Items without Contract":
          SETRANGE("No. of Active Contracts",0);
      END;
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE SelectionFilterOnAfterValidate@19033692();
    BEGIN
      CurrPage.UPDATE;
      SetSelectionFilter;
    END;

    LOCAL PROCEDURE LookupOKOnPush@19031339();
    BEGIN
      OKButton := TRUE;
    END;

    BEGIN
    END.
  }
}
