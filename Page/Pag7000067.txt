OBJECT Page 7000067 Posted Bills
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVES6.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Posted Bills;
               ESP=Efectos registrados];
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table7000003;
    DataCaptionExpr=Caption;
    DataCaptionFields=Type;
    PageType=Worksheet;
    OnInit=BEGIN
             StatusFilterEditable := TRUE;
             ClasFilterEditable := TRUE;
           END;

    OnOpenPage=BEGIN
                 UpdateStatistics;
               END;

    OnAfterGetRecord=BEGIN
                       OnAfterGetCurrRecord;
                     END;

    OnNewRecord=BEGIN
                  OnAfterGetCurrRecord;
                END;

    OnModifyRecord=BEGIN
                     CODEUNIT.RUN(CODEUNIT::"Posted Cartera Doc.- Edit",Rec);
                     UpdateStatistics;
                     EXIT(FALSE);
                   END;

    OnDeleteRecord=BEGIN
                     UpdateStatistics;
                   END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 19      ;1   ;ActionGroup;
                      CaptionML=[ENU=&Bill;
                                 ESP=E&fecto] }
      { 1100000 ;2   ;Action    ;
                      ShortCutKey=May£s+Ctrl+D;
                      CaptionML=[ENU=Dime&nsions;
                                 ESP=Dime&nsiones];
                      Image=Dimensions;
                      OnAction=VAR
                                 CarteraDocDim@1100003 : Record 7000025;
                                 PostedDocDim@1100002 : Record 359;
                                 CarteraDimensions@1100001 : Form 7000080;
                                 PostedDocDimensions@1100000 : Form 547;
                               BEGIN
                                 IF "Document Type" = "Document Type"::Bill THEN BEGIN
                                   CarteraDocDim.SETCURRENTKEY("Table ID","Document Type",Type,"Document No.","Bill No.","Line No.","Dimension Code");
                                   CarteraDocDim.SETRANGE("Table ID",DATABASE::"Posted Cartera Doc.");
                                   CarteraDocDim.SETRANGE("Document Type",CarteraDocDim."Document Type"::"Posted Bill");
                                   CarteraDocDim.SETRANGE("Document No.",Rec."Document No.");
                                   CarteraDocDim.SETRANGE("Bill No.",Rec."No.");
                                   CarteraDocDim.SETRANGE(Type,Rec.Type);
                                   CarteraDimensions.SETTABLEVIEW(CarteraDocDim);
                                   CarteraDimensions.RUN;
                                   CarteraDocDim.RESET;
                                 END ELSE BEGIN
                                   PostedDocDim.SETCURRENTKEY("Table ID","Document No.","Line No.","Dimension Code");
                                   IF Rec.Type = Rec.Type::Receivable THEN
                                     PostedDocDim.SETRANGE("Table ID",DATABASE::"Sales Invoice Header")
                                   ELSE
                                     PostedDocDim.SETRANGE("Table ID",DATABASE::"Purch. Inv. Header");
                                   PostedDocDim.SETRANGE("Document No.",Rec."Document No.");
                                   PostedDocDimensions.SETTABLEVIEW(PostedDocDim);
                                   PostedDocDimensions.RUN;
                                   PostedDocDim.RESET;
                                 END;
                               END;
                                }
      { 1100001 ;2   ;Separator  }
      { 41      ;2   ;Action    ;
                      CaptionML=[ENU=B&G/PO;
                                 ESP=&Remesa/orden pago];
                      OnAction=VAR
                                 SalesPostedGroup@1100001 : Record 7000006;
                                 PurchasePostedGroup@1100002 : Record 7000021;
                               BEGIN
                                 IF Type = Type::Receivable THEN BEGIN
                                   SalesPostedGroup.SETRANGE("No.",Rec."Bill Gr./Pmt. Order No.");
                                   FORM.RUN(FORM::"Posted Bill Groups",SalesPostedGroup)
                                 END ELSE BEGIN
                                   PurchasePostedGroup.SETRANGE("No.",Rec."Bill Gr./Pmt. Order No.");
                                   FORM.RUN(FORM::"Posted Payment Orders",PurchasePostedGroup);
                                 END;
                               END;
                                }
      { 43      ;2   ;Separator  }
      { 45      ;2   ;Action    ;
                      CaptionML=[ENU=Analysis;
                                 ESP=An lisis];
                      OnAction=BEGIN
                                 FORM.RUNMODAL(FORM::"Posted Bills Analysis",Rec);
                               END;
                                }
      { 36      ;2   ;Action    ;
                      CaptionML=[ENU=Maturity;
                                 ESP=Vencimientos];
                      OnAction=BEGIN
                                 IF Type = Type::Receivable THEN
                                   FORM.RUNMODAL(FORM::"Posted Receiv. Bills Maturity")
                                 ELSE
                                   FORM.RUNMODAL(FORM::"Posted Payable Bills Maturity");
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 46      ;1   ;ActionGroup;
                      CaptionML=[ENU=F&unctions;
                                 ESP=Acci&ones] }
      { 62      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[ENU=Categorize;
                                 ESP=Clasificar];
                      OnAction=BEGIN
                                 CurrPage.SETSELECTIONFILTER(PostedDoc);
                                 CarteraManagement.CategorizePostedDocs(PostedDoc);
                               END;
                                }
      { 63      ;2   ;Action    ;
                      CaptionML=[ENU=Decategorize;
                                 ESP=Desclasificar];
                      OnAction=BEGIN
                                 CurrPage.SETSELECTIONFILTER(PostedDoc);
                                 CarteraManagement.DecategorizePostedDocs(PostedDoc);
                               END;
                                }
      { 37      ;2   ;Separator  }
      { 3       ;2   ;Action    ;
                      CaptionML=[ENU=Settle;
                                 ESP=Liquidar];
                      OnAction=BEGIN
                                 CurrPage.SETSELECTIONFILTER(PostedDoc);
                                 IF NOT PostedDoc.FIND('=><') THEN
                                   EXIT;

                                 PostedDoc.SETRANGE(Status,PostedDoc.Status::Open);
                                 IF NOT PostedDoc.FIND('-') THEN
                                   ERROR(
                                     Text1100000 +
                                     Text1100001);

                                 IF PostedDoc.Type = PostedDoc.Type::Receivable THEN
                                   REPORT.RUNMODAL(REPORT::"Settle Docs. in Post. Bill Gr.",TRUE,FALSE,PostedDoc)
                                 ELSE
                                   REPORT.RUNMODAL(REPORT::"Settle Docs. in Posted PO",TRUE,FALSE,PostedDoc);
                               END;
                                }
      { 50      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[ENU=Reject;
                                 ESP=Rechazar];
                      Image=Reject;
                      OnAction=BEGIN
                                 Reject;
                               END;
                                }
      { 2       ;2   ;Action    ;
                      CaptionML=[ENU=Redraw;
                                 ESP=Recircular];
                      OnAction=BEGIN
                                 Redraw;
                               END;
                                }
      { 39      ;2   ;Separator  }
      { 40      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[ENU=Print;
                                 ESP=Imprimir];
                      Image=Print;
                      OnAction=BEGIN
                                 PrintBillRec;
                               END;
                                }
      { 47      ;1   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[ENU=&Navigate;
                                 ESP=&Navegar];
                      Promoted=Yes;
                      Image=Navigate;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 Option := STRMENU(Text1100002);
                                 CASE Option OF
                                   0:EXIT;
                                   1:CarteraManagement.NavigatePostedDoc(Rec);
                                   2:BEGIN
                                       Navigate.SetDoc("Posting Date","Bill Gr./Pmt. Order No.");
                                       Navigate.RUN;
                                     END;
                                 END;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 38  ;1   ;Group     ;
                CaptionML=[ENU=Options;
                           ESP=Opciones] }

    { 14  ;2   ;Field     ;
                Name=ClasFilter;
                CaptionML=[ENU=Category Filter;
                           ESP=Filtro clasificaci¢n];
                SourceExpr=CategoryFilter;
                TableRelation="Category Code";
                Editable=ClasFilterEditable;
                OnValidate=BEGIN
                             CategoryFilterOnAfterValidate;
                           END;
                            }

    { 31  ;2   ;Field     ;
                Name=StatusFilter;
                CaptionML=[ENU=Status Filter;
                           ESP=Filtro estado];
                SourceExpr=StatusFilter;
                Editable=StatusFilterEditable;
                OnValidate=BEGIN
                             StatusFilterOnAfterValidate;
                           END;
                            }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 8   ;2   ;Field     ;
                SourceExpr="Document No.";
                Editable=FALSE }

    { 18  ;2   ;Field     ;
                SourceExpr="No.";
                Editable=FALSE }

    { 24  ;2   ;Field     ;
                SourceExpr="Due Date" }

    { 20  ;2   ;Field     ;
                SourceExpr=Status }

    { 64  ;2   ;Field     ;
                SourceExpr="Document Type" }

    { 10  ;2   ;Field     ;
                SourceExpr=Description;
                Editable=FALSE }

    { 27  ;2   ;Field     ;
                SourceExpr="Amt. for Collection (LCY)";
                Editable=FALSE }

    { 22  ;2   ;Field     ;
                SourceExpr="Remaining Amt. (LCY)";
                Editable=FALSE }

    { 12  ;2   ;Field     ;
                SourceExpr="Payment Method Code";
                Visible=FALSE;
                Editable=FALSE }

    { 28  ;2   ;Field     ;
                SourceExpr=Accepted }

    { 34  ;2   ;Field     ;
                SourceExpr="Collection Agent";
                Editable=FALSE }

    { 44  ;2   ;Field     ;
                SourceExpr="Bank Account No.";
                Editable=FALSE }

    { 56  ;2   ;Field     ;
                SourceExpr="Bill Gr./Pmt. Order No.";
                Editable=FALSE }

    { 58  ;2   ;Field     ;
                SourceExpr="Honored/Rejtd. at Date";
                Editable=FALSE }

    { 60  ;2   ;Field     ;
                SourceExpr=Redrawn;
                Editable=FALSE }

    { 32  ;2   ;Field     ;
                SourceExpr=Place;
                Editable=FALSE }

    { 4   ;2   ;Field     ;
                SourceExpr="Category Code" }

    { 6   ;2   ;Field     ;
                SourceExpr="Account No.";
                Visible=FALSE;
                Editable=FALSE }

    { 51  ;2   ;Field     ;
                SourceExpr="Entry No.";
                Editable=FALSE }

    { 49  ;1   ;Group      }

    { 52  ;2   ;Field     ;
                Name=[TotalCurrAmt ];
                CaptionML=[ENU=Total Remaining Amt. (LCY);
                           ESP=Imp. pendiente total (DL)];
                SourceExpr=TotalCurrAmtLCY;
                AutoFormatType=1;
                Editable=FALSE }

  }
  CODE
  {
    VAR
      Text1100000@1000 : TextConst 'ENU=No bills have been found that can be settled. \;ESP=No se han encontrado efectos para liquidar. \';
      Text1100001@1001 : TextConst 'ENU=Please check that at least one open bill was selected.;ESP=Compruebe que ha seleccionado al menos un efecto pendiente.';
      Text1100002@1002 : TextConst 'ENU=Related to Bill,Related to Bill Group;ESP=Asociado a efecto,Asociado a remesa';
      Text1100003@1003 : TextConst 'ENU=Open|Honored|Rejected;ESP=Pendiente|Pagado|Impagado';
      Text1100004@1004 : TextConst 'ENU=Only Receivable Bills can be printed.;ESP=S¢lo se pueden imprimir efectos a cobrar.';
      Text1100005@1005 : TextConst 'ENU=Only Receivable Bills can be rejected.;ESP=S¢lo se pueden impagar efectos a cobrar.';
      Text1100006@1006 : TextConst 'ENU=No bills have been found that can be rejected. \;ESP=No hay efectos que impagar. \';
      Text1100007@1007 : TextConst 'ENU=No bills have been found that can be redrawn. \;ESP=No hay efectos que recircular. \';
      Text1100008@1008 : TextConst 'ENU=Please check that at least one rejected or honored bill was selected.;ESP=Compruebe que ha seleccionado al menos un efecto pagado o impagado.';
      Text1100009@1009 : TextConst 'ENU=Only bills can be redrawn.;ESP=S¢lo se pueden recircular efectos.';
      PostedDoc@1100000 : Record 7000003;
      CustLedgEntry@1100001 : Record 21;
      VendLedgEntry@1100002 : Record 25;
      Navigate@1100003 : Form 344;
      CarteraManagement@1100005 : Codeunit 7000000;
      CategoryFilter@1100006 : Code[250];
      ActiveFilter@1100007 : Text[250];
      TotalCurrAmtLCY@1100008 : Decimal;
      StatusFilter@1100010 : 'Open,Honored,Rejected,All';
      Option@1100011 : '0,1,2';
      Call@1100012 : Boolean;
      ClasFilterEditable@19046841 : Boolean INDATASET;
      StatusFilterEditable@19023660 : Boolean INDATASET;

    PROCEDURE UpdateStatistics@4();
    BEGIN
      PostedDoc.RESET;
      PostedDoc.SETCURRENTKEY("Bank Account No.","Bill Gr./Pmt. Order No.",Status,
        "Category Code",Redrawn,"Due Date","Document Type");
      PostedDoc.COPYFILTERS(Rec);
      PostedDoc.SETRANGE("Document Type",PostedDoc."Document Type"::Bill);
      PostedDoc.SETFILTER("Category Code",CategoryFilter);
      PostedDoc.CALCSUMS(PostedDoc."Remaining Amt. (LCY)");
      TotalCurrAmtLCY := PostedDoc."Remaining Amt. (LCY)";

      ActiveFilter := GETFILTER(Status);
      IF ActiveFilter = '' THEN
        StatusFilter := StatusFilter::All
      ELSE BEGIN
        IF ActiveFilter = Text1100003 THEN
          StatusFilter := StatusFilter::All
        ELSE
          StatusFilter := Status;
      END;


      IF Call = TRUE THEN
        CategoryFilter := GETFILTER("Category Code");
    END;

    PROCEDURE GetSelect@1(VAR NewPostedDoc@1100000 : Record 7000003);
    BEGIN
      CurrPage.SETSELECTIONFILTER(NewPostedDoc);
    END;

    PROCEDURE PrintBillRec@2();
    BEGIN
      CurrPage.SETSELECTIONFILTER(PostedDoc);
      IF NOT PostedDoc.FIND('-') THEN
        EXIT;

      IF PostedDoc.Type <> PostedDoc.Type::Receivable THEN
        ERROR(Text1100004);

      CustLedgEntry.RESET;
      REPEAT
        CustLedgEntry.GET(PostedDoc."Entry No.");
        CustLedgEntry.MARK(TRUE);
      UNTIL PostedDoc.NEXT = 0;

      CustLedgEntry.MARKEDONLY(TRUE);
      CustLedgEntry.PrintBill(TRUE);
    END;

    PROCEDURE Reject@3();
    BEGIN
      CurrPage.SETSELECTIONFILTER(PostedDoc);
      IF NOT PostedDoc.FIND('=><') THEN
        EXIT;

      IF PostedDoc.Type <> PostedDoc.Type::Receivable THEN
        ERROR(Text1100005);

      PostedDoc.SETRANGE(Status,PostedDoc.Status::Open);
      IF NOT PostedDoc.FIND('-') THEN
        ERROR(
          Text1100006 +
          Text1100001);
      CustLedgEntry.RESET;
      REPEAT
        CustLedgEntry.GET(PostedDoc."Entry No.");
        CustLedgEntry.MARK(TRUE);
      UNTIL PostedDoc.NEXT = 0;

      CustLedgEntry.MARKEDONLY(TRUE);
      REPORT.RUNMODAL(REPORT::"Reject Docs.",TRUE,FALSE,CustLedgEntry);
    END;

    PROCEDURE Redraw@5();
    BEGIN
      CurrPage.SETSELECTIONFILTER(PostedDoc);
      IF NOT PostedDoc.FIND('=><') THEN
        EXIT;

      PostedDoc.SETFILTER(Status,'<>%1',PostedDoc.Status::Open);
      IF NOT PostedDoc.FIND('-') THEN
        ERROR(
          Text1100007 +
          Text1100008);

      PostedDoc.SETFILTER("Document Type",'<>%1',PostedDoc."Document Type"::Bill);
      IF PostedDoc.FIND('-') THEN
        ERROR(Text1100009);
      PostedDoc.SETRANGE("Document Type");

      IF Type = Type::Receivable THEN BEGIN
        CustLedgEntry.RESET;
        REPEAT
          CustLedgEntry.GET(PostedDoc."Entry No.");
          CustLedgEntry.MARK(TRUE);
        UNTIL PostedDoc.NEXT = 0;

        CustLedgEntry.MARKEDONLY(TRUE);
        REPORT.RUNMODAL(REPORT::"Redraw Receivable Bills",TRUE,FALSE,CustLedgEntry);
      END ELSE BEGIN
        VendLedgEntry.RESET;
        REPEAT
          VendLedgEntry.GET(PostedDoc."Entry No.");
          VendLedgEntry.MARK(TRUE);
        UNTIL PostedDoc.NEXT = 0;

        VendLedgEntry.MARKEDONLY(TRUE);
        REPORT.RUNMODAL(REPORT::"Redraw Payable Bills",TRUE,FALSE,VendLedgEntry);
      END;
    END;

    PROCEDURE Called@6();
    BEGIN
      Call := TRUE;
      ClasFilterEditable := FALSE;
      StatusFilterEditable := FALSE;
    END;

    LOCAL PROCEDURE CategoryFilterOnAfterValidate@19040013();
    BEGIN
      UpdateStatistics;
    END;

    LOCAL PROCEDURE StatusFilterOnAfterValidate@19021475();
    BEGIN
      IF StatusFilter = StatusFilter::All THEN
        SETRANGE(Status)
      ELSE
        SETRANGE(Status,StatusFilter);
      UpdateStatistics;
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE OnAfterGetCurrRecord@19077479();
    BEGIN
      xRec := Rec;
      UpdateStatistics;
    END;

    LOCAL PROCEDURE OnBeforePutRecord@19001631();
    BEGIN
      UpdateStatistics;
    END;

    BEGIN
    END.
  }
}
