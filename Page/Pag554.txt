OBJECT Page 554 Analysis by Dimensions
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01,NAVES6.00.01;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Analysis by Dimensions;
               ESP=An lisis por dimensiones];
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    LinksAllowed=No;
    SourceTable=Table367;
    DataCaptionExpr=AnalysisViewCode;
    PageType=Card;
    OnInit=BEGIN
             Dim4FilterEnable := TRUE;
             Dim3FilterEnable := TRUE;
             Dim2FilterEnable := TRUE;
             Dim1FilterEnable := TRUE;
           END;

    OnOpenPage=VAR
                 GLAcc@1002 : Record 15;
                 Field@1000 : Record 2000000041;
               BEGIN
                 IF (NewAnalysisViewCode <> '') AND (NewAnalysisViewCode <> AnalysisViewCode) THEN
                   AnalysisViewCode := NewAnalysisViewCode;

                 ValidateAnalysisViewCode;

                 GLSetup.GET;
                 IF GLSetup."Additional Reporting Currency" = '' THEN
                   ShowInAddCurr := FALSE
                 ELSE
                   Currency.GET(GLSetup."Additional Reporting Currency");

                 IF (LineDimCode = '') AND (ColumnDimCode = '') THEN BEGIN
                   LineDimCode := GLAcc.TABLECAPTION;
                   ColumnDimCode := Text000;
                 END;
                 LineDimOption := DimCodeToOption(LineDimCode);
                 ColumnDimOption := DimCodeToOption(ColumnDimCode);
                 Field.GET(DATABASE::"G/L Account",42);
                 BusUnitFilterCaption := Field."Field Caption";

                 CalculateClosingDateFilter;
                 FindPeriod('');

                 CreateCaptionSet(Rec,Step::First,32,PrimaryKeyFirstColInSet,ColumnCaptions,ColumnsSet);
               END;

    OnFindRecord=BEGIN
                   EXIT(FindRec(LineDimOption,Rec,Which));
                 END;

    OnNextRecord=BEGIN
                   EXIT(NextRec(LineDimOption,Rec,Steps));
                 END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 24      ;1   ;ActionGroup;
                      CaptionML=[ENU=&Actions;
                                 ESP=&Acciones] }
      { 25      ;2   ;Action    ;
                      CaptionML=[ENU=Reverse Lines and Columns;
                                 ESP=Invertir l¡neas y columnas];
                      OnAction=VAR
                                 TempDimCode@1000 : Text[30];
                               BEGIN
                                 TempDimCode := ColumnDimCode;
                                 ColumnDimCode := LineDimCode;
                                 LineDimCode := TempDimCode;
                                 ValidateLineDimCode;
                                 ValidateColumnDimCode;
                                 CreateCaptionSet(Rec,Step::First,32,PrimaryKeyFirstColInSet,ColumnCaptions,ColumnsSet);
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 19      ;1   ;Action    ;
                      CaptionML=[ENU=&Show Matrix;
                                 ESP=&Mostrar matriz];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ShowMatrix;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 MatrixForm@1198 : Form 9249;
                               BEGIN
                                 CLEAR(MatrixForm);

                                 MatrixForm.Load(
                                   LineDimOption,ColumnDimOption,LineDimCode,ColumnDimCode,PeriodType,DateFilter,GLAccFilter,
                                   BusUnitFilter,BudgetFilter,Dim1Filter,Dim2Filter,Dim3Filter,Dim4Filter);
                                 MatrixForm.Load2(
                                   QtyType,AnalysisViewCode,ShowOppositeSign,ShowColumnName,
                                   ShowActualBudg,AmountField,ClosingEntryFilter,RoundingFactor,ShowInAddCurr,
                                   ColumnCaptions,PrimaryKeyFirstColInSet,HistoricGLAccFilter);
                                 MatrixForm.RUNMODAL;
                               END;
                                }
      { 12      ;1   ;Action    ;
                      CaptionML=[ENU=Previous Set;
                                 ESP=Conjunto anterior];
                      ToolTipML=ENU=Previous Set;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=PreviousSet;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 CreateCaptionSet(Rec,Step::Previous,32,PrimaryKeyFirstColInSet,ColumnCaptions,ColumnsSet);
                               END;
                                }
      { 11      ;1   ;Action    ;
                      CaptionML=[ENU=Next Set;
                                 ESP=Conjunto siguiente];
                      ToolTipML=ENU=Next Set;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=NextSet;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 CreateCaptionSet(Rec,Step::Next,32,PrimaryKeyFirstColInSet,ColumnCaptions,ColumnsSet);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                CaptionML=[ENU=General;
                           ESP=General] }

    { 5   ;2   ;Field     ;
                CaptionML=[ENU=Analysis View Code;
                           ESP=C¢d. vista an lisis];
                SourceExpr=AnalysisViewCode;
                TableRelation="Analysis View";
                OnValidate=BEGIN
                             ValidateAnalysisViewCode;
                             ValidateColumnDimCode;
                             ValidateLineDimCode;
                           END;

                OnLookup=VAR
                           AnalysisViewList@1002 : Form 556;
                         BEGIN
                           AnalysisViewList.LOOKUPMODE := TRUE;
                           AnalysisViewList.SETRECORD(AnalysisView);
                           IF AnalysisViewList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             AnalysisViewList.GETRECORD(AnalysisView);
                             AnalysisViewCode := AnalysisView.Code;
                             Text := AnalysisView.Code;
                             ValidateAnalysisViewCode;
                             ValidateColumnDimCode;
                             ValidateLineDimCode;
                             CurrPage.UPDATE;
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 6   ;2   ;Field     ;
                CaptionML=[ENU=Show as Lines;
                           ESP=Muestra como l¡neas];
                SourceExpr=LineDimCode;
                OnValidate=BEGIN
                             IF (UPPERCASE(LineDimCode) = UPPERCASE(ColumnDimCode)) AND (LineDimCode <> '') THEN BEGIN
                               ColumnDimCode := '';
                               ValidateColumnDimCode;
                             END;
                             ValidateLineDimCode;
                             IF LineDimOption = LineDimOption::Period THEN
                               SETCURRENTKEY("Period Start")
                             ELSE
                               SETCURRENTKEY(Code);
                               LineDimCodeOnAfterValidate;
                           END;

                OnLookup=VAR
                           NewCode@1002 : Text[30];
                         BEGIN
                           NewCode := GetDimSelection(LineDimCode);
                           IF NewCode = LineDimCode THEN
                             EXIT(FALSE)
                           ELSE BEGIN
                             Text := NewCode;
                             LineDimCode := NewCode;
                             ValidateLineDimCode;
                             CurrPage.UPDATE;
                             EXIT(TRUE);
                           END
                         END;
                          }

    { 9   ;2   ;Field     ;
                CaptionML=[ENU=Show as Columns;
                           ESP=Muestra como columnas];
                SourceExpr=ColumnDimCode;
                OnValidate=BEGIN
                             IF (UPPERCASE(LineDimCode) = UPPERCASE(ColumnDimCode)) AND (LineDimCode <> '') THEN BEGIN
                               LineDimCode := '';
                               ValidateLineDimCode;
                             END;
                             ValidateColumnDimCode;

                             CreateCaptionSet(Rec,Step::First,32,PrimaryKeyFirstColInSet,ColumnCaptions,ColumnsSet);
                               ColumnDimCodeOnAfterValidate;
                           END;

                OnLookup=VAR
                           NewCode@1002 : Text[30];
                         BEGIN
                           NewCode := GetDimSelection(ColumnDimCode);
                           IF NewCode = ColumnDimCode THEN
                             EXIT(FALSE)
                           ELSE BEGIN
                             Text := NewCode;
                             ColumnDimCode := NewCode;
                             ValidateColumnDimCode;
                             CurrPage.UPDATE;
                             EXIT(TRUE);
                           END
                         END;
                          }

    { 1907524401;1;Group  ;
                CaptionML=[ENU=Filters;
                           ESP=Filtros] }

    { 36  ;2   ;Field     ;
                CaptionML=[ENU=Date Filter;
                           ESP=Filtro fecha];
                SourceExpr=DateFilter;
                OnValidate=VAR
                             GLAcc@1001 : Record 15;
                             ApplicationManagement@1002 : Codeunit 1;
                           BEGIN
                             IF ApplicationManagement.MakeDateFilter(DateFilter) = 0 THEN;
                             GLAcc.SETFILTER("Date Filter",DateFilter);
                             DateFilter := GLAcc.GETFILTER("Date Filter");
                             InternalDateFilter := DateFilter;
                               DateFilterOnAfterValidate;
                           END;
                            }

    { 38  ;2   ;Field     ;
                CaptionML=[ENU=G/L Account Filter;
                           ESP=Filtro cuenta];
                SourceExpr=GLAccFilter;
                OnValidate=BEGIN
                             GLAccFilterOnAfterValidate;
                           END;

                OnLookup=VAR
                           GLAccList@1002 : Form 18;
                         BEGIN
                           GLAccList.LOOKUPMODE(TRUE);
                           IF NOT (GLAccList.RUNMODAL = ACTION::LookupOK) THEN
                             EXIT(FALSE)
                           ELSE
                             Text := GLAccList.GetSelectionFilter;
                           EXIT(TRUE);
                         END;
                          }

    { 1100000;2;Field     ;
                CaptionML=[ENU=Historic G/L Account Filter;
                           ESP=Filtro cuenta de hist¢rico];
                SourceExpr=HistoricGLAccFilter;
                OnValidate=BEGIN
                             HistoricGLAccFilterOnAfterVali;
                           END;

                OnLookup=VAR
                           HistoricGLAccList@1100000 : Form 10731;
                         BEGIN
                           HistoricGLAccList.LOOKUPMODE(TRUE);
                           IF NOT (HistoricGLAccList.RUNMODAL = ACTION::LookupOK) THEN
                             EXIT(FALSE)
                           ELSE
                             Text := HistoricGLAccList.GetSelectionFilter;
                           EXIT(TRUE);
                         END;
                          }

    { 22  ;2   ;Field     ;
                SourceExpr=BusUnitFilter;
                CaptionClass='3,' + BusUnitFilterCaption;
                OnLookup=VAR
                           BusUnitList@1002 : Form 240;
                         BEGIN
                           BusUnitList.LOOKUPMODE(TRUE);
                           IF NOT (BusUnitList.RUNMODAL = ACTION::LookupOK) THEN
                             EXIT(FALSE);
                           Text := BusUnitList.GetSelectionFilter;
                           EXIT(TRUE);
                         END;
                          }

    { 53  ;2   ;Field     ;
                CaptionML=[ENU=Budget Filter;
                           ESP=Filtro presupuesto];
                SourceExpr=BudgetFilter;
                TableRelation="G/L Budget Name".Name }

    { 34  ;2   ;Field     ;
                Name=Dim1Filter;
                CaptionML=[ENU=Dimension 1 Filter;
                           ESP=Filtro dimensi¢n 1];
                SourceExpr=Dim1Filter;
                CaptionClass=GetCaptionClass(1);
                Enabled=Dim1FilterEnable;
                OnValidate=BEGIN
                             Dim1FilterOnAfterValidate;
                           END;

                OnLookup=BEGIN
                           EXIT(LookUpDimFilter(AnalysisView."Dimension 1 Code",Text));
                         END;
                          }

    { 30  ;2   ;Field     ;
                Name=Dim2Filter;
                CaptionML=[ENU=Dimension 2 Filter;
                           ESP=Filtro dimensi¢n 2];
                SourceExpr=Dim2Filter;
                CaptionClass=GetCaptionClass(2);
                Enabled=Dim2FilterEnable;
                OnValidate=BEGIN
                             Dim2FilterOnAfterValidate;
                           END;

                OnLookup=BEGIN
                           EXIT(LookUpDimFilter(AnalysisView."Dimension 2 Code",Text));
                         END;
                          }

    { 46  ;2   ;Field     ;
                Name=Dim3Filter;
                CaptionML=[ENU=Dimension 3 Filter;
                           ESP=Filtro dimensi¢n 3];
                SourceExpr=Dim3Filter;
                CaptionClass=GetCaptionClass(3);
                Enabled=Dim3FilterEnable;
                OnValidate=BEGIN
                             Dim3FilterOnAfterValidate;
                           END;

                OnLookup=BEGIN
                           EXIT(LookUpDimFilter(AnalysisView."Dimension 3 Code",Text));
                         END;
                          }

    { 35  ;2   ;Field     ;
                Name=Dim4Filter;
                CaptionML=[ENU=Dimension 4 Filter;
                           ESP=Filtro dimensi¢n 4];
                SourceExpr=Dim4Filter;
                CaptionClass=GetCaptionClass(4);
                Enabled=Dim4FilterEnable;
                OnValidate=BEGIN
                             Dim4FilterOnAfterValidate;
                           END;

                OnLookup=BEGIN
                           EXIT(LookUpDimFilter(AnalysisView."Dimension 4 Code",Text));
                         END;
                          }

    { 1903121901;1;Group  ;
                CaptionML=[ENU=Options;
                           ESP=Opciones] }

    { 29  ;2   ;Field     ;
                CaptionML=[ENU=Show;
                           ESP=Muestra];
                OptionCaptionML=[ENU=Actual Amounts,Budgeted Amounts,Variance,Variance%,Index%;
                                 ESP=Importes reales,Importes pptados.,Desviaci¢n,% Desviaci¢n,% Öndice];
                SourceExpr=ShowActualBudg }

    { 47  ;2   ;Field     ;
                CaptionML=[ENU=Show Amount Field;
                           ESP=Muestra Importe];
                OptionCaptionML=[ENU=Amount,Debit Amount,Credit Amount;
                                 ESP=Importe,Importe deudor,Importe acreedor];
                SourceExpr=AmountField }

    { 52  ;2   ;Field     ;
                CaptionML=[ENU=Closing Entries;
                           ESP=Movs. regularizaci¢n];
                OptionCaptionML=[ENU=Include,Exclude;
                                 ESP=Incluir,Excluir];
                SourceExpr=ClosingEntryFilter;
                OnValidate=BEGIN
                             CalculateClosingDateFilter;
                             FindPeriod('=');
                           END;
                            }

    { 18  ;2   ;Field     ;
                CaptionML=[ENU=Rounding Factor;
                           ESP=Factor redondeo];
                OptionCaptionML=[ENU=None,1,1000,1000000;
                                 ESP=No,1,1000,1000000];
                SourceExpr=RoundingFactor }

    { 49  ;2   ;Field     ;
                CaptionML=[ENU=Show Amounts in Add. Reporting Currency;
                           ESP=Muestra importes en div.-adic.];
                SourceExpr=ShowInAddCurr;
                MultiLine=Yes }

    { 4   ;2   ;Field     ;
                CaptionML=[ENU=Show Column Name;
                           ESP=Muestra nombre columna];
                SourceExpr=ShowColumnName;
                OnValidate=BEGIN
                             ShowColumnNameOnAfterValidate;
                           END;
                            }

    { 54  ;2   ;Field     ;
                CaptionML=[ENU=Show Opposite Sign;
                           ESP=Muestra signo opuesto];
                SourceExpr=ShowOppositeSign;
                MultiLine=Yes }

    { 1906098301;1;Group  ;
                CaptionML=[ENU=Matrix Options;
                           ESP=Opciones matriz] }

    { 13  ;2   ;Field     ;
                CaptionML=[ENU=View by;
                           ESP=Ver por];
                OptionCaptionML=[ENU=Day,Week,Month,Quarter,Year,Accounting Period;
                                 ESP=D¡a,Semana,Mes,Trimestre,A¤o,Periodo contable];
                SourceExpr=PeriodType;
                OnValidate=BEGIN
                             FindPeriod('');
                               PeriodTypeOnAfterValidate;
                           END;
                            }

    { 14  ;2   ;Field     ;
                CaptionML=[ENU=Column Set;
                           ESP=Conjunto de columnas];
                SourceExpr=ColumnsSet;
                Editable=FALSE }

    { 20  ;2   ;Field     ;
                CaptionML=[ENU=View as;
                           ESP=Ver como];
                OptionCaptionML=[ENU=Net Change,Balance at Date;
                                 ESP=Saldo periodo,Saldo a la fecha];
                SourceExpr=QtyType }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Period;ESP=Periodo';
      Text002@1002 : TextConst 'ENU=You have not yet defined an analysis view.;ESP=No tiene a£n definida una vista an lisis.';
      Text003@1003 : TextConst 'ENU=%1 is not a valid line definition.;ESP=%1 no es una def. de l¡nea v lida.';
      Text004@1004 : TextConst 'ENU=%1 is not a valid column definition.;ESP=%1 no es una def. de columna v lida.';
      Text005@1005 : TextConst 'ENU=1,6,,Dimension 1 Filter;ESP=1,6,,Filtro dimensi¢n 1';
      Text006@1006 : TextConst 'ENU=1,6,,Dimension 2 Filter;ESP=1,6,,Filtro dimensi¢n 2';
      Text007@1007 : TextConst 'ENU=1,6,,Dimension 3 Filter;ESP=1,6,,Filtro dimensi¢n 3';
      Text008@1008 : TextConst 'ENU=1,6,,Dimension 4 Filter;ESP=1,6,,Filtro dimensi¢n 4';
      GLAccount@1047 : Record 15;
      GLSetup@1009 : Record 98;
      AnalysisView@1011 : Record 363;
      AnalysisViewEntry@1012 : Record 365;
      Currency@1044 : Record 4;
      AnalysisViewCode@1016 : Code[10];
      NewAnalysisViewCode@1059 : Code[10];
      LineDimOption@1017 : 'G/L Account,Period,Business Unit,Dimension 1,Dimension 2,Dimension 3,Dimension 4,,,,,Historic G/L Account';
      ColumnDimOption@1018 : 'G/L Account,Period,Business Unit,Dimension 1,Dimension 2,Dimension 3,Dimension 4,,,,,Historic G/L Account';
      LineDimCode@1019 : Text[30];
      ColumnDimCode@1020 : Text[30];
      PeriodType@1021 : 'Day,Week,Month,Quarter,Year,Accounting Period';
      AmountType@1022 : 'Net Change,Balance at Date';
      RoundingFactor@1023 : 'None,1,1000,1000000';
      AmountField@1024 : 'Amount,Debit Amount,Credit Amount';
      ShowActualBudg@1025 : 'Actual Amounts,Budgeted Amounts,Variance,Variance%,Index%';
      ShowInAddCurr@1026 : Boolean;
      ShowOppositeSign@1027 : Boolean;
      ClosingEntryFilter@1028 : 'Include,Exclude';
      ShowColumnName@1029 : Boolean;
      DateFilter@1030 : Text[250];
      InternalDateFilter@1031 : Text[250];
      ExcludeClosingDateFilter@1032 : Text[1024];
      GLAccFilter@1033 : Code[250];
      BudgetFilter@1034 : Text[250];
      Dim1Filter@1035 : Code[250];
      Dim2Filter@1036 : Code[250];
      Dim3Filter@1037 : Code[250];
      Dim4Filter@1038 : Code[250];
      MatrixHeader@1039 : Text[50];
      MatrixAmount@1040 : Decimal;
      PeriodInitialized@1041 : Boolean;
      BusUnitFilter@1043 : Code[250];
      ColumnCaptions@1046 : ARRAY [32] OF Text[80];
      Direction@1048 : 'Backward,Forward';
      NoOfColumns@1051 : Integer;
      FirstColumn@1050 : Text[1024];
      LastColumn@1049 : Text[1024];
      FirstColumnDate@1057 : Date;
      LastColumnDate@1053 : Date;
      ColumnFilters@1052 : ARRAY [32] OF Code[100];
      PrimaryKeyFirstColInSet@1054 : Text[1024];
      ColumnsSet@1055 : Text[1024];
      Step@1056 : 'First,Previous,Same,Next';
      QtyType@1058 : 'Net Change,Balance at Date';
      BusUnitFilterCaption@1001 : Text[80];
      HistoricGLAcc@1100000 : Record 10721;
      HistoricGLAccFilter@1100001 : Code[250];
      BusUnitFilter2@1101100000 : Code[250];
      Dim1FilterEnable@19054192 : Boolean INDATASET;
      Dim2FilterEnable@19062512 : Boolean INDATASET;
      Dim3FilterEnable@19011605 : Boolean INDATASET;
      Dim4FilterEnable@19064250 : Boolean INDATASET;

    LOCAL PROCEDURE DimCodeToOption@21(DimCode@1000 : Text[30]) : Integer;
    VAR
      GLAcc@1002 : Record 15;
      BusUnit@1001 : Record 220;
    BEGIN
      CASE DimCode OF
        '' :
          EXIT(-1);
        GLAcc.TABLECAPTION :
          EXIT(0);
        Text000 :
          EXIT(1);
        BusUnit.TABLECAPTION :
          EXIT(2);
        AnalysisView."Dimension 1 Code" :
          EXIT(3);
        AnalysisView."Dimension 2 Code" :
          EXIT(4);
        AnalysisView."Dimension 3 Code" :
          EXIT(5);
        AnalysisView."Dimension 4 Code" :
          EXIT(6);
        HistoricGLAcc.TABLECAPTION :
          EXIT(11);
        ELSE
          EXIT(-1);
      END;
    END;

    LOCAL PROCEDURE FindRec@59(DimOption@1000 : 'G/L Account,Period,Business Unit,Dimension 1,Dimension 2,Dimension 3,Dimension 4,,,,,Historic G/L Account';VAR DimCodeBuf@1001 : Record 367;Which@1002 : Text[250]) : Boolean;
    VAR
      GLAcc@1003 : Record 15;
      BusUnit@1004 : Record 220;
      Period@1005 : Record 2000000007;
      DimVal@1006 : Record 349;
      PeriodFormMgt@1007 : Codeunit 359;
      Found@1008 : Boolean;
      HistoricGLAcc@1100000 : Record 10721;
    BEGIN
      CASE DimOption OF
        DimOption::"G/L Account" :
          BEGIN
            GLAcc."No." := DimCodeBuf.Code;
            IF GLAccFilter <> '' THEN
              GLAcc.SETFILTER("No.",GLAccFilter);
            Found := GLAcc.FIND(Which);
            IF Found THEN
              CopyGLAccToBuf(GLAcc,DimCodeBuf);
          END;
        DimOption::Period :
          BEGIN
            IF NOT PeriodInitialized THEN
              DateFilter := '';
            PeriodInitialized := TRUE;
            Period."Period Start" := DimCodeBuf."Period Start";
            IF DateFilter <> '' THEN
              Period.SETFILTER("Period Start",DateFilter)
            ELSE
              IF NOT PeriodInitialized AND (InternalDateFilter <> '') THEN
                Period.SETFILTER("Period Start",InternalDateFilter);
            Found := PeriodFormMgt.FindDate(Which,Period,PeriodType);
            IF Found THEN
              CopyPeriodToBuf(Period,DimCodeBuf);
          END;
        DimOption::"Business Unit" :
          BEGIN
            BusUnit.Code := DimCodeBuf.Code;
            IF BusUnitFilter <> '' THEN
              BusUnit.SETFILTER(Code,BusUnitFilter);
            Found := BusUnit.FIND(Which);
            IF Found THEN
              CopyBusUnitToBuf(BusUnit,DimCodeBuf);
          END;
        DimOption::"Dimension 1" :
          BEGIN
            IF Dim1Filter <> '' THEN
              DimVal.SETFILTER(Code,Dim1Filter);
            DimVal."Dimension Code" := AnalysisView."Dimension 1 Code";
            DimVal.SETRANGE("Dimension Code",DimVal."Dimension Code");
            DimVal.Code := DimCodeBuf.Code;
            Found := DimVal.FIND(Which);
            IF Found THEN
              CopyDimValueToBuf(DimVal,DimCodeBuf);
          END;
        DimOption::"Dimension 2" :
          BEGIN
            IF Dim2Filter <> '' THEN
              DimVal.SETFILTER(Code,Dim2Filter);
            DimVal."Dimension Code" := AnalysisView."Dimension 2 Code";
            DimVal.SETRANGE("Dimension Code",DimVal."Dimension Code");
            DimVal.Code := DimCodeBuf.Code;
            Found := DimVal.FIND(Which);
            IF Found THEN
              CopyDimValueToBuf(DimVal,DimCodeBuf);
          END;
        DimOption::"Dimension 3" :
          BEGIN
            IF Dim3Filter <> '' THEN
              DimVal.SETFILTER(Code,Dim3Filter);
            DimVal."Dimension Code" := AnalysisView."Dimension 3 Code";
            DimVal.SETRANGE("Dimension Code",DimVal."Dimension Code");
            DimVal.Code := DimCodeBuf.Code;
            Found := DimVal.FIND(Which);
            IF Found THEN
              CopyDimValueToBuf(DimVal,DimCodeBuf);
          END;
        DimOption::"Dimension 4" :
          BEGIN
            IF Dim4Filter <> '' THEN
              DimVal.SETFILTER(Code,Dim4Filter);
            DimVal."Dimension Code" := AnalysisView."Dimension 4 Code";
            DimVal.SETRANGE("Dimension Code",DimVal."Dimension Code");
            DimVal.Code := DimCodeBuf.Code;
            Found := DimVal.FIND(Which);
            IF Found THEN
              CopyDimValueToBuf(DimVal,DimCodeBuf);
          END;
        DimOption::"Historic G/L Account" :
          BEGIN
            HistoricGLAcc."No." := DimCodeBuf.Code;
            IF HistoricGLAccFilter <> '' THEN
              HistoricGLAcc.SETFILTER("No.",HistoricGLAccFilter);
            Found := HistoricGLAcc.FIND(Which);
            IF Found THEN
              CopyHistoricGLAccToBuf(HistoricGLAcc,DimCodeBuf);
          END;
      END;
      EXIT(Found);
    END;

    LOCAL PROCEDURE NextRec@60(DimOption@1000 : 'G/L Account,Period,Business Unit,Dimension 1,Dimension 2,Dimension 3,Dimension 4,,,,,Historic G/L Account';VAR DimCodeBuf@1001 : Record 367;Steps@1002 : Integer) : Integer;
    VAR
      GLAcc@1003 : Record 15;
      BusUnit@1004 : Record 220;
      Period@1005 : Record 2000000007;
      DimVal@1006 : Record 349;
      PeriodFormMgt@1007 : Codeunit 359;
      ResultSteps@1008 : Integer;
    BEGIN
      CASE DimOption OF
        DimOption::"G/L Account" :
          BEGIN
            GLAcc."No." := DimCodeBuf.Code;
            IF GLAccFilter <> '' THEN
              GLAcc.SETFILTER("No.",GLAccFilter);
            ResultSteps := GLAcc.NEXT(Steps);
            IF ResultSteps <> 0 THEN
              CopyGLAccToBuf(GLAcc,DimCodeBuf);
          END;
        DimOption::Period :
          BEGIN
            IF DateFilter <> '' THEN
              Period.SETFILTER("Period Start",DateFilter);
            Period."Period Start" := DimCodeBuf."Period Start";
            ResultSteps := PeriodFormMgt.NextDate(Steps,Period,PeriodType);
            IF ResultSteps <> 0 THEN
              CopyPeriodToBuf(Period,DimCodeBuf);
          END;
        DimOption::"Business Unit" :
          BEGIN
            BusUnit.Code := DimCodeBuf.Code;
            IF BusUnitFilter <> '' THEN
              BusUnit.SETFILTER(Code,BusUnitFilter);
            ResultSteps := BusUnit.NEXT(Steps);
            IF ResultSteps <> 0 THEN
              CopyBusUnitToBuf(BusUnit,DimCodeBuf);
          END;
        DimOption::"Dimension 1" :
          BEGIN
            IF Dim1Filter <> '' THEN
              DimVal.SETFILTER(Code,Dim1Filter);
            DimVal."Dimension Code" := AnalysisView."Dimension 1 Code";
            DimVal.SETRANGE("Dimension Code",DimVal."Dimension Code");
            DimVal.Code := DimCodeBuf.Code;
            ResultSteps := DimVal.NEXT(Steps);
            IF ResultSteps <> 0 THEN
              CopyDimValueToBuf(DimVal,DimCodeBuf);
          END;
        DimOption::"Dimension 2" :
          BEGIN
            IF Dim2Filter <> '' THEN
              DimVal.SETFILTER(Code,Dim2Filter);
            DimVal."Dimension Code" := AnalysisView."Dimension 2 Code";
            DimVal.SETRANGE("Dimension Code",DimVal."Dimension Code");
            DimVal.Code := DimCodeBuf.Code;
            ResultSteps := DimVal.NEXT(Steps);
            IF ResultSteps <> 0 THEN
              CopyDimValueToBuf(DimVal,DimCodeBuf);
          END;
        DimOption::"Dimension 3" :
          BEGIN
            IF Dim3Filter <> '' THEN
              DimVal.SETFILTER(Code,Dim3Filter);
            DimVal."Dimension Code" := AnalysisView."Dimension 3 Code";
            DimVal.SETRANGE("Dimension Code",DimVal."Dimension Code");
            DimVal.Code := DimCodeBuf.Code;
            ResultSteps := DimVal.NEXT(Steps);
            IF ResultSteps <> 0 THEN
              CopyDimValueToBuf(DimVal,DimCodeBuf);
          END;
        DimOption::"Dimension 4" :
          BEGIN
            IF Dim4Filter <> '' THEN
              DimVal.SETFILTER(Code,Dim4Filter);
            DimVal."Dimension Code" := AnalysisView."Dimension 4 Code";
            DimVal.SETRANGE("Dimension Code",DimVal."Dimension Code");
            DimVal.Code := DimCodeBuf.Code;
            ResultSteps := DimVal.NEXT(Steps);
            IF ResultSteps <> 0 THEN
              CopyDimValueToBuf(DimVal,DimCodeBuf);
          END;
        DimOption::"Historic G/L Account" :
          BEGIN
            HistoricGLAcc."No." := DimCodeBuf.Code;
            IF HistoricGLAccFilter <> '' THEN
              HistoricGLAcc.SETFILTER("No.",HistoricGLAccFilter);
            ResultSteps := HistoricGLAcc.NEXT(Steps);
            IF ResultSteps <> 0 THEN
              CopyHistoricGLAccToBuf(HistoricGLAcc,DimCodeBuf);
          END;
      END;
      EXIT(ResultSteps);
    END;

    LOCAL PROCEDURE CopyGLAccToBuf@1(VAR TheGLAcc@1000 : Record 15;VAR TheDimCodeBuf@1001 : Record 367);
    BEGIN
      WITH TheDimCodeBuf DO BEGIN
        INIT;
        Code := TheGLAcc."No.";
        Name := TheGLAcc.Name;
        Totaling := TheGLAcc.Totaling;
        Indentation := TheGLAcc.Indentation;
        "Show in Bold" := TheGLAcc."Account Type" <> TheGLAcc."Account Type"::Posting;
      END;
    END;

    LOCAL PROCEDURE CopyPeriodToBuf@8(VAR ThePeriod@1000 : Record 2000000007;VAR TheDimCodeBuf@1001 : Record 367);
    VAR
      Period2@1002 : Record 2000000007;
    BEGIN
      WITH TheDimCodeBuf DO BEGIN
        INIT;
        Code := FORMAT(ThePeriod."Period Start");
        "Period Start" := ThePeriod."Period Start";
        IF ClosingEntryFilter = ClosingEntryFilter::Include THEN
          "Period End" := CLOSINGDATE(ThePeriod."Period End")
        ELSE
          "Period End" := ThePeriod."Period End";
        IF DateFilter <> '' THEN BEGIN
          Period2.SETFILTER("Period End",DateFilter);
          IF Period2.GETRANGEMAX("Period End") < "Period End" THEN
            "Period End" := Period2.GETRANGEMAX("Period End");
        END;
        Name := ThePeriod."Period Name";
      END;
    END;

    LOCAL PROCEDURE CopyBusUnitToBuf@5(VAR TheBusUnit@1000 : Record 220;VAR TheDimCodeBuf@1001 : Record 367);
    BEGIN
      WITH TheDimCodeBuf DO BEGIN
        INIT;
        Code := TheBusUnit.Code;
        Name := TheBusUnit.Name;
      END;
    END;

    LOCAL PROCEDURE CopyDimValueToBuf@9(VAR TheDimVal@1000 : Record 349;VAR TheDimCodeBuf@1001 : Record 367);
    BEGIN
      WITH TheDimCodeBuf DO BEGIN
        INIT;
        Code := TheDimVal.Code;
        Name := TheDimVal.Name;
        Totaling := TheDimVal.Totaling;
        Indentation := TheDimVal.Indentation;
        "Show in Bold" :=
          TheDimVal."Dimension Value Type" <> TheDimVal."Dimension Value Type"::Standard;
      END;
    END;

    LOCAL PROCEDURE FindPeriod@2(SearchText@1000 : Code[10]);
    VAR
      Calendar@1001 : Record 2000000007;
      PeriodFormMgt@1002 : Codeunit 359;
    BEGIN
      IF DateFilter <> '' THEN BEGIN
        Calendar.SETFILTER("Period Start",DateFilter);
        IF NOT PeriodFormMgt.FindDate('+',Calendar,PeriodType) THEN
          PeriodFormMgt.FindDate('+',Calendar,PeriodType::Day);
        Calendar.SETRANGE("Period Start");
      END;
      IF PeriodFormMgt.FindDate(SearchText,Calendar,PeriodType) THEN
        IF ClosingEntryFilter = ClosingEntryFilter::Include THEN
          Calendar."Period End" := CLOSINGDATE(Calendar."Period End");
      IF AmountType = AmountType::"Net Change" THEN BEGIN
        AnalysisViewEntry.SETRANGE("Posting Date",Calendar."Period Start",Calendar."Period End");
        IF AnalysisViewEntry.GETRANGEMIN("Posting Date") = AnalysisViewEntry.GETRANGEMAX("Posting Date") THEN
          AnalysisViewEntry.SETRANGE("Posting Date",AnalysisViewEntry.GETRANGEMIN("Posting Date"));
      END ELSE
        AnalysisViewEntry.SETRANGE("Posting Date",0D,Calendar."Period End");

      InternalDateFilter := AnalysisViewEntry.GETFILTER("Posting Date");
      IF (LineDimOption <> LineDimOption::Period) AND (ColumnDimOption <> ColumnDimOption::Period) THEN
        DateFilter := InternalDateFilter;
    END;

    LOCAL PROCEDURE CalculateClosingDateFilter@45();
    VAR
      AccountingPeriod@1000 : Record 50;
      FirstRec@1001 : Boolean;
    BEGIN
      IF ClosingEntryFilter = ClosingEntryFilter::Include THEN
        ExcludeClosingDateFilter := ''
      ELSE BEGIN
        AccountingPeriod.SETCURRENTKEY("New Fiscal Year");
        AccountingPeriod.SETRANGE("New Fiscal Year",TRUE);
        FirstRec := TRUE;
        IF AccountingPeriod.FIND('-') THEN
          REPEAT
            IF FirstRec THEN
              ExcludeClosingDateFilter :=
                STRSUBSTNO('<>%1',CLOSINGDATE(AccountingPeriod."Starting Date" - 1))
            ELSE
              ExcludeClosingDateFilter :=
                ExcludeClosingDateFilter + STRSUBSTNO('&<>%1',CLOSINGDATE(AccountingPeriod."Starting Date" - 1));
            FirstRec := FALSE;
          UNTIL AccountingPeriod.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE GetDimSelection@3(OldDimSelCode@1000 : Text[30]) : Text[30];
    VAR
      GLAcc@1002 : Record 15;
      BusUnit@1003 : Record 220;
      DimSelection@1001 : Form 568;
    BEGIN
      DimSelection.InsertDimSelBuf(FALSE,GLAcc.TABLECAPTION,GLAcc.TABLECAPTION);
      DimSelection.InsertDimSelBuf(FALSE,HistoricGLAcc.TABLECAPTION,HistoricGLAcc.TABLECAPTION);
      DimSelection.InsertDimSelBuf(FALSE,Text000,Text000);
      DimSelection.InsertDimSelBuf(FALSE,BusUnit.TABLECAPTION,BusUnit.TABLECAPTION);

      IF AnalysisView."Dimension 1 Code" <> '' THEN
        DimSelection.InsertDimSelBuf(FALSE,AnalysisView."Dimension 1 Code",'');
      IF AnalysisView."Dimension 2 Code" <> '' THEN
        DimSelection.InsertDimSelBuf(FALSE,AnalysisView."Dimension 2 Code",'');
      IF AnalysisView."Dimension 3 Code" <> '' THEN
        DimSelection.InsertDimSelBuf(FALSE,AnalysisView."Dimension 3 Code",'');
      IF AnalysisView."Dimension 4 Code" <> '' THEN
        DimSelection.InsertDimSelBuf(FALSE,AnalysisView."Dimension 4 Code",'');

      DimSelection.LOOKUPMODE := TRUE;
      IF DimSelection.RUNMODAL = ACTION::LookupOK THEN
        EXIT(DimSelection.GetDimSelCode)
      ELSE
        EXIT(OldDimSelCode);
    END;

    LOCAL PROCEDURE LookUpCode@4(DimOption@1000 : 'G/L Account,Period,Business Unit,Dimension 1,Dimension 2,Dimension 3,Dimension 4,,,,,Historic G/L Account';DimCode@1001 : Text[30];Code@1002 : Text[30]);
    VAR
      GLAcc@1003 : Record 15;
      BusUnit@1004 : Record 220;
      DimVal@1005 : Record 349;
      DimValList@1006 : Form 560;
      HistoricGLAcc@1100000 : Record 10721;
    BEGIN
      CASE DimOption OF
        DimOption::"G/L Account" :
          BEGIN
            GLAcc.GET(Code);
            FORM.RUNMODAL(FORM::"G/L Account List",GLAcc);
          END;
        DimOption::Period : ;
        DimOption::"Business Unit" :
          BEGIN
            BusUnit.GET(Code);
            FORM.RUNMODAL(FORM::"Business Unit List",BusUnit);
          END;
        DimOption::"Dimension 1",DimOption::"Dimension 2",
        DimOption::"Dimension 3",DimOption::"Dimension 4" :
          BEGIN
            DimVal.SETRANGE("Dimension Code",DimCode);
            DimVal.GET(DimCode,Code);
            DimValList.SETTABLEVIEW(DimVal);
            DimValList.SETRECORD(DimVal);
            DimValList.RUNMODAL;
          END;
        DimOption::"Historic G/L Account" :
          BEGIN
            HistoricGLAcc.GET(Code);
            FORM.RUNMODAL(FORM::"Historic G/L Account List",HistoricGLAcc);
          END;
      END;
    END;

    LOCAL PROCEDURE LookUpDimFilter@57(Dim@1000 : Code[20];VAR Text@1001 : Text[250]) : Boolean;
    VAR
      DimVal@1002 : Record 349;
      DimValList@1003 : Form 560;
    BEGIN
      IF Dim = '' THEN
        EXIT(FALSE);
      DimValList.LOOKUPMODE(TRUE);
      DimVal.SETRANGE("Dimension Code",Dim);
      DimValList.SETTABLEVIEW(DimVal);
      IF DimValList.RUNMODAL = ACTION::LookupOK THEN BEGIN
        DimValList.GETRECORD(DimVal);
        Text := DimValList.GetSelectionFilter;
        EXIT(TRUE);
      END ELSE
        EXIT(FALSE)
    END;

    LOCAL PROCEDURE ValidateAnalysisViewCode@11();
    VAR
      AnalysisViewFilter@1000 : Record 364;
    BEGIN
      AnalysisView.Code := AnalysisViewCode;
      IF NOT AnalysisView.FIND('=<>') THEN
        ERROR(Text002);
      AnalysisViewCode := AnalysisView.Code;
      GLAccFilter := AnalysisView."G/L Account Filter";
      Dim1Filter := '';
      Dim2Filter := '';
      Dim3Filter := '';
      Dim4Filter := '';
      SETPERMISSIONFILTER;
      Dim1Filter := GETFILTER("Dimension 1 Value Filter");
      Dim2Filter := GETFILTER("Dimension 2 Value Filter");
      Dim3Filter := GETFILTER("Dimension 3 Value Filter");
      Dim4Filter := GETFILTER("Dimension 4 Value Filter");
      HistoricGLAccFilter := AnalysisView."Historic G/L Account Filter";

      Dim1FilterEnable :=
        (AnalysisView."Dimension 1 Code" <> '') AND
        (GETFILTER("Dimension 1 Value Filter") = '');
      Dim2FilterEnable :=
        (AnalysisView."Dimension 2 Code" <> '') AND
        (GETFILTER("Dimension 2 Value Filter") = '');
      Dim3FilterEnable :=
        (AnalysisView."Dimension 3 Code" <> '') AND
        (GETFILTER("Dimension 3 Value Filter") = '');
      Dim4FilterEnable :=
        (AnalysisView."Dimension 4 Code" <> '') AND
        (GETFILTER("Dimension 4 Value Filter") = '');

      IF Dim1FilterEnable THEN
        IF AnalysisViewFilter.GET(AnalysisView.Code,AnalysisView."Dimension 1 Code") THEN
          Dim1Filter := AnalysisViewFilter."Dimension Value Filter";

      IF Dim2FilterEnable THEN
        IF AnalysisViewFilter.GET(AnalysisView.Code,AnalysisView."Dimension 2 Code") THEN
          Dim2Filter := AnalysisViewFilter."Dimension Value Filter";

      IF Dim3FilterEnable THEN
        IF AnalysisViewFilter.GET(AnalysisView.Code,AnalysisView."Dimension 3 Code") THEN
          Dim3Filter := AnalysisViewFilter."Dimension Value Filter";

      IF Dim4FilterEnable THEN
        IF AnalysisViewFilter.GET(AnalysisView.Code,AnalysisView."Dimension 4 Code") THEN
          Dim4Filter := AnalysisViewFilter."Dimension Value Filter";
    END;

    LOCAL PROCEDURE ValidateLineDimCode@19();
    VAR
      GLAcc@1001 : Record 15;
      BusUnit@1000 : Record 220;
    BEGIN
      IF (UPPERCASE(LineDimCode) <> UPPERCASE(GLAcc.TABLECAPTION)) AND
         (UPPERCASE(LineDimCode) <> UPPERCASE(BusUnit.TABLECAPTION)) AND
         (UPPERCASE(LineDimCode) <> UPPERCASE(Text000)) AND
         (UPPERCASE(LineDimCode) <> AnalysisView."Dimension 1 Code") AND
         (UPPERCASE(LineDimCode) <> AnalysisView."Dimension 2 Code") AND
         (UPPERCASE(LineDimCode) <> AnalysisView."Dimension 3 Code") AND
         (UPPERCASE(LineDimCode) <> AnalysisView."Dimension 4 Code") AND
         (UPPERCASE(LineDimCode) <> UPPERCASE(HistoricGLAcc.TABLECAPTION)) AND
         (LineDimCode <> '') THEN BEGIN
        MESSAGE(Text003,LineDimCode);
        LineDimCode := '';
      END;
      LineDimOption := DimCodeToOption(LineDimCode);
      InternalDateFilter := AnalysisViewEntry.GETFILTER("Posting Date");
      IF (LineDimOption <> LineDimOption::Period) AND (ColumnDimOption <> ColumnDimOption::Period) THEN BEGIN
        DateFilter := InternalDateFilter;
        IF STRPOS(DateFilter,'&') > 1 THEN
          DateFilter := COPYSTR(DateFilter,1,STRPOS(DateFilter,'&') - 1);
      END ELSE
        PeriodInitialized := FALSE;
    END;

    LOCAL PROCEDURE ValidateColumnDimCode@20();
    VAR
      GLAcc@1001 : Record 15;
      BusUnit@1000 : Record 220;
    BEGIN
      IF (UPPERCASE(ColumnDimCode) <> UPPERCASE(GLAcc.TABLECAPTION)) AND
         (UPPERCASE(ColumnDimCode) <> UPPERCASE(BusUnit.TABLECAPTION)) AND
         (UPPERCASE(ColumnDimCode) <> UPPERCASE(Text000)) AND
         (UPPERCASE(ColumnDimCode) <> AnalysisView."Dimension 1 Code") AND
         (UPPERCASE(ColumnDimCode) <> AnalysisView."Dimension 2 Code") AND
         (UPPERCASE(ColumnDimCode) <> AnalysisView."Dimension 3 Code") AND
         (UPPERCASE(ColumnDimCode) <> AnalysisView."Dimension 4 Code") AND
         (UPPERCASE(ColumnDimCode) <> UPPERCASE(HistoricGLAcc.TABLECAPTION)) AND
         (ColumnDimCode <> '') THEN BEGIN
        MESSAGE(Text004,ColumnDimCode);
        ColumnDimCode := '';
      END;
      ColumnDimOption := DimCodeToOption(ColumnDimCode);
      InternalDateFilter := AnalysisViewEntry.GETFILTER("Posting Date");
      IF (LineDimOption <> LineDimOption::Period) AND (ColumnDimOption <> ColumnDimOption::Period) THEN BEGIN
        DateFilter := InternalDateFilter;
        IF STRPOS(DateFilter,'&') > 1 THEN
          DateFilter := COPYSTR(DateFilter,1,STRPOS(DateFilter,'&') - 1);
      END ELSE
        PeriodInitialized := FALSE;
    END;

    LOCAL PROCEDURE GetCaptionClass@12(AnalysisViewDimType@1000 : Integer) : Text[250];
    BEGIN
      IF AnalysisView.Code <> AnalysisViewCode THEN
        AnalysisView.GET(AnalysisViewCode);
      CASE AnalysisViewDimType OF
        1:
          BEGIN
            IF AnalysisView."Dimension 1 Code" <> '' THEN
              EXIT('1,6,' + AnalysisView."Dimension 1 Code")
            ELSE
              EXIT(Text005);
          END;
        2:
          BEGIN
            IF AnalysisView."Dimension 2 Code" <> '' THEN
              EXIT('1,6,' + AnalysisView."Dimension 2 Code")
            ELSE
              EXIT(Text006);
          END;
        3:
          BEGIN
            IF AnalysisView."Dimension 3 Code" <> '' THEN
              EXIT('1,6,' + AnalysisView."Dimension 3 Code")
            ELSE
              EXIT(Text007);
          END;
        4:
          BEGIN
            IF AnalysisView."Dimension 4 Code" <> '' THEN
              EXIT('1,6,' + AnalysisView."Dimension 4 Code")
            ELSE
              EXIT(Text008);
          END;
      END;
    END;

    PROCEDURE CreateCaptionSet@29(RecRef@1000 : Record 367;Step@1008 : 'First,Previous,Same,Next';MaximumNoOfCaptions@1001 : Integer;VAR PrimaryKeyFirstCaptionInCurrSe@1006 : Text[1024];VAR CaptionSet@1005 : ARRAY [32] OF Text[1024];VAR CaptionRange@1004 : Text[1024]) : Integer;
    VAR
      CurrentCaptionOrdinal@1003 : Integer;
    BEGIN
      CLEAR(CaptionSet);
      CaptionRange := '';

      CurrentCaptionOrdinal := 0;

      CASE Step OF
        Step::First:
          BEGIN
            IF (ColumnDimOption = ColumnDimOption::Period) AND (DateFilter = '') THEN
              FindRec(ColumnDimOption,RecRef,'=><')
          ELSE
            IF NOT FindRec(ColumnDimOption,RecRef,'-') THEN
              EXIT;
          END;
        Step::Previous:
          BEGIN
            RecRef.SETPOSITION(PrimaryKeyFirstCaptionInCurrSe);
            IF ColumnDimOption = ColumnDimOption::Period THEN
              EVALUATE(RecRef."Period Start",RecRef.Code);
            IF NOT FindRec(ColumnDimOption,RecRef,'=') THEN
              EXIT;
            NextRec(ColumnDimOption,RecRef,-MaximumNoOfCaptions);
          END;
        Step::Same:
          BEGIN
            RecRef.SETPOSITION(PrimaryKeyFirstCaptionInCurrSe);
            IF ColumnDimOption = ColumnDimOption::Period THEN
              EVALUATE(RecRef."Period Start",RecRef.Code);
            IF NOT FindRec(ColumnDimOption,RecRef,'=') THEN
              EXIT;
          END;
        Step::Next:
          BEGIN
            RecRef.SETPOSITION(PrimaryKeyFirstCaptionInCurrSe);
            IF ColumnDimOption = ColumnDimOption::Period THEN
              EVALUATE(RecRef."Period Start",RecRef.Code);
            IF NOT FindRec(ColumnDimOption,RecRef,'=') THEN
              EXIT;
            IF NOT (NextRec(ColumnDimOption,RecRef,MaximumNoOfCaptions) = MaximumNoOfCaptions) THEN BEGIN
              RecRef.SETPOSITION(PrimaryKeyFirstCaptionInCurrSe);
              FindRec(ColumnDimOption,RecRef,'=');
            END;
          END;
      END;

      PrimaryKeyFirstCaptionInCurrSe := RecRef.GETPOSITION;

      REPEAT
        CurrentCaptionOrdinal := CurrentCaptionOrdinal + 1;
        IF ShowColumnName THEN
          CaptionSet[CurrentCaptionOrdinal] := RecRef.Name
        ELSE
          CaptionSet[CurrentCaptionOrdinal] := RecRef.Code;
      UNTIL (CurrentCaptionOrdinal = MaximumNoOfCaptions) OR (NextRec(ColumnDimOption,RecRef,1) <> 1);

      IF CurrentCaptionOrdinal = 1 THEN
        CaptionRange := CaptionSet[1]
      ELSE
        CaptionRange := CaptionSet[1] + '..' + CaptionSet[CurrentCaptionOrdinal];
    END;

    PROCEDURE SetAnalysisViewCode@14(NextAnalysisViewCode@1000 : Code[10]);
    BEGIN
      NewAnalysisViewCode := NextAnalysisViewCode;
    END;

    LOCAL PROCEDURE CopyHistoricGLAccToBuf@1100000(VAR TheHistoricGLAcc@1100000 : Record 10721;VAR TheDimCodeBuf@1100001 : Record 367);
    BEGIN
      WITH TheDimCodeBuf DO BEGIN
        INIT;
        Code := TheHistoricGLAcc."No.";
        Name := TheHistoricGLAcc.Name;
        Totaling := TheHistoricGLAcc.Totaling;
        Indentation := TheHistoricGLAcc.Indentation;
        "Show in Bold" := TheHistoricGLAcc."Account Type" <> TheHistoricGLAcc."Account Type"::Posting;
      END;
    END;

    LOCAL PROCEDURE LineDimCodeOnAfterValidate@19031201();
    BEGIN
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE ColumnDimCodeOnAfterValidate@19023109();
    BEGIN
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE DateFilterOnAfterValidate@19006009();
    BEGIN
      IF ColumnDimOption = ColumnDimOption::Period THEN
        CreateCaptionSet(Rec,Step::First,32,PrimaryKeyFirstColInSet,ColumnCaptions,ColumnsSet);
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE Dim2FilterOnAfterValidate@19005475();
    BEGIN
      IF ColumnDimOption = ColumnDimOption::"Dimension 2" THEN
        CreateCaptionSet(Rec,Step::First,32,PrimaryKeyFirstColInSet,ColumnCaptions,ColumnsSet);
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE Dim1FilterOnAfterValidate@19005240();
    BEGIN
      IF ColumnDimOption = ColumnDimOption::"Dimension 1" THEN
        CreateCaptionSet(Rec,Step::First,32,PrimaryKeyFirstColInSet,ColumnCaptions,ColumnsSet);
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE Dim4FilterOnAfterValidate@19067462();
    BEGIN
      IF ColumnDimOption = ColumnDimOption::"Dimension 4" THEN
        CreateCaptionSet(Rec,Step::First,32,PrimaryKeyFirstColInSet,ColumnCaptions,ColumnsSet);
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE Dim3FilterOnAfterValidate@19064950();
    BEGIN
      IF ColumnDimOption = ColumnDimOption::"Dimension 3" THEN
        CreateCaptionSet(Rec,Step::First,32,PrimaryKeyFirstColInSet,ColumnCaptions,ColumnsSet);
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE GLAccFilterOnAfterValidate@19040142();
    BEGIN
      IF ColumnDimOption = ColumnDimOption::"G/L Account" THEN
        CreateCaptionSet(Rec,Step::First,32,PrimaryKeyFirstColInSet,ColumnCaptions,ColumnsSet);
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE HistoricGLAccFilterOnAfterVali@19019845();
    BEGIN
      IF ColumnDimOption = ColumnDimOption::"Historic G/L Account" THEN
        CreateCaptionSet(Rec,Step::First,32,PrimaryKeyFirstColInSet,ColumnCaptions,ColumnsSet);
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE ShowColumnNameOnAfterValidate@19074585();
    BEGIN
      CreateCaptionSet(Rec,Step::First,32,PrimaryKeyFirstColInSet,ColumnCaptions,ColumnsSet);
    END;

    LOCAL PROCEDURE PeriodTypeOnAfterValidate@19015506();
    BEGIN
      IF ColumnDimOption = ColumnDimOption::Period THEN
        CreateCaptionSet(Rec,Step::First,32,PrimaryKeyFirstColInSet,ColumnCaptions,ColumnsSet);
    END;

    BEGIN
    END.
  }
}
