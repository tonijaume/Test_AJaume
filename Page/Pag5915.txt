OBJECT Page 5915 Service Tasks
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Service Tasks;
               ESP=Tareas servicio];
    SaveValues=Yes;
    InsertAllowed=No;
    SourceTable=Table5901;
    SourceTableView=SORTING(Response Date,Response Time,Priority);
    PageType=Worksheet;
    OnOpenPage=BEGIN
                 IF UserMgt.GetServiceFilter() <> '' THEN BEGIN
                   FILTERGROUP(2);
                   SETRANGE("Responsibility Center",UserMgt.GetServiceFilter());
                   FILTERGROUP(0);
                 END;

                 SetAllFilters;

                 IF ISEMPTY THEN BEGIN
                   ServOrderFilter := '';
                   SetServOrderFilter;
                 END;
               END;

    OnAfterGetRecord=BEGIN
                       ServHeader.GET("Document Type","Document No.");

                       IF NOT Cust.GET(ServHeader."Customer No.") THEN
                         CLEAR(Cust);

                       CALCFIELDS("No. of Active/Finished Allocs");
                     END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 51      ;1   ;ActionGroup;
                      CaptionML=[ENU=&Service Tasks;
                                 ESP=Tareas del &servicio] }
      { 53      ;2   ;Action    ;
                      ShortCutKey=May£s+F7;
                      CaptionML=[ENU=&Show Document;
                                 ESP=&Mostrar documento];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=View;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 IF ServHeader.GET("Document Type","Document No.") THEN BEGIN
                                   CASE "Document Type" OF
                                     "Document Type"::Quote:
                                       FORM.RUNMODAL(FORM::"Service Quote",ServHeader);
                                     "Document Type"::Order:
                                       FORM.RUNMODAL(FORM::"Service Order",ServHeader);
                                   END;

                                   IF (ServOrderFilter <> '') THEN BEGIN
                                     ServItemLine.RESET;
                                     ServItemLine.COPYFILTERS(Rec);
                                     IF ServItemLine.GETRANGEMIN("Document No.") = ServItemLine.GETRANGEMAX("Document No.") THEN
                                       IF ServItemLine.ISEMPTY THEN BEGIN
                                         ServOrderFilter := '';
                                         SetServOrderFilter;
                                       END
                                   END;
                                 END;
                               END;
                                }
      { 54      ;2   ;Action    ;
                      CaptionML=[ENU=&Item Worksheet;
                                 ESP=&Hoja trabajo producto];
                      RunObject=Page 5903;
                      RunFormLink=Document Type=FIELD(Document Type),
                                  Document No.=FIELD(Document No.),
                                  Line No.=FIELD(Line No.),
                                  Resource Filter=FIELD(Resource Filter);
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ItemWorksheet;
                      PromotedCategory=Process }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 36      ;1   ;ActionGroup;
                      CaptionML=[ENU=&Print;
                                 ESP=&Imprimir] }
      { 37      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[ENU=Service &Tasks;
                                 ESP=&Tareas servicio];
                      OnAction=BEGIN
                                 CLEAR(ServItemLine);
                                 FILTERGROUP(2);
                                 ServItemLine.SETVIEW(Rec.GETVIEW);
                                 FILTERGROUP(0);
                                 ServItemLine.SETRANGE("No. of Allocations");
                                 ServItemLine.SETRANGE("No. of Active/Finished Allocs");

                                 REPORT.RUN(REPORT::"Service Tasks",TRUE,TRUE,ServItemLine);
                               END;
                                }
      { 48      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[ENU=Service Item &Worksheet;
                                 ESP=&Hoja trabajo prod. serv.];
                      OnAction=BEGIN
                                 CLEAR(ServItemLine);
                                 ServItemLine.SETRANGE("Document Type","Document Type");
                                 ServItemLine.SETRANGE("Document No.","Document No.");
                                 ServItemLine.SETRANGE("Line No.","Line No.");
                                 REPORT.RUN(REPORT::"Service Item Worksheet",TRUE,TRUE,ServItemLine);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 29  ;1   ;Group     ;
                CaptionML=[ENU=General;
                           ESP=General] }

    { 42  ;2   ;Field     ;
                CaptionML=[ENU=Resource Filter;
                           ESP=Filtro recurso];
                SourceExpr=ResourceFilter;
                OnValidate=BEGIN
                             FILTERGROUP(2);
                             TempTextFilter := GETFILTER("Resource Filter");
                             FILTERGROUP(0);
                             SetResourceFilter;
                             IF NOT TestFilter THEN BEGIN
                               ResourceFilter := TempTextFilter;
                               SetResourceFilter;
                               ERROR(Text000,TABLECAPTION);
                             END;
                               ResourceFilterOnAfterValidate;
                           END;

                OnLookup=BEGIN
                           Res.RESET;
                           IF FORM.RUNMODAL(0,Res) = ACTION::LookupOK THEN BEGIN
                             Text := Res."No.";
                             SetResourceFilter;
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 63  ;2   ;Field     ;
                CaptionML=[ENU=Resource Group Filter;
                           ESP=Filtro grupo recurso];
                SourceExpr=ResourceGroupFilter;
                OnValidate=BEGIN
                             FILTERGROUP(2);
                             TempTextFilter := GETFILTER("Resource Group Filter");
                             FILTERGROUP(0);
                             SetResourceGroupFilter;
                             IF NOT TestFilter THEN BEGIN
                               ResourceGroupFilter := TempTextFilter;
                               SetResourceGroupFilter;
                               ERROR(Text000,TABLECAPTION);
                             END;
                               ResourceGroupFilterOnAfterVali;
                           END;

                OnLookup=BEGIN
                           ResourceGroup.RESET;
                           IF FORM.RUNMODAL(0,ResourceGroup) = ACTION::LookupOK THEN BEGIN
                             Text := ResourceGroup."No.";
                             SetResourceGroupFilter;
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 32  ;2   ;Field     ;
                CaptionML=[ENU=Response Date Filter;
                           ESP=Filtro fecha respuesta];
                SourceExpr=RespDateFilter;
                OnValidate=BEGIN
                             FILTERGROUP(2);
                             TempTextFilter := GETFILTER("Response Date");
                             FILTERGROUP(0);
                             SetRespDateFilter;
                             IF NOT TestFilter THEN BEGIN
                               RespDateFilter := TempTextFilter;
                               SetRespDateFilter;
                               ERROR(Text000,TABLECAPTION);
                             END;
                               RespDateFilterOnAfterValidate;
                           END;
                            }

    { 39  ;2   ;Field     ;
                CaptionML=[ENU=Allocation Status Filter;
                           ESP=Filtro estado asignaci¢n];
                OptionCaptionML=[ENU=" ,Nonactive,Active,Finished,Canceled,Reallocation Needed";
                                 ESP=" ,Inactivo,Activo,Terminado,Cancelado,Reasignaci¢n necesaria"];
                SourceExpr=AllocationStatus;
                OnValidate=BEGIN
                             TempAllocationStatus := "Allocation Status Filter";
                             SetAllocationFilter;
                             IF NOT TestFilter THEN BEGIN
                               AllocationStatus := TempAllocationStatus;
                               SetAllocationFilter;
                               ERROR(Text000,TABLECAPTION);
                             END;
                               AllocationStatusOnAfterValidat;
                           END;
                            }

    { 56  ;2   ;Field     ;
                CaptionML=[ENU=Document Filter;
                           ESP=Filtro documento];
                OptionCaptionML=[ENU=Order,Quote,All;
                                 ESP=Pedido,Oferta,Todos];
                SourceExpr=DocFilter;
                OnValidate=BEGIN
                             ServOrderFilter := '';
                             SetServOrderFilter;
                             SetDocFilter;
                               DocFilterOnAfterValidate;
                           END;
                            }

    { 60  ;2   ;Field     ;
                CaptionML=[ENU=No. Filter;
                           ESP=Filtro N§];
                SourceExpr=ServOrderFilter;
                OnValidate=BEGIN
                             FILTERGROUP(2);
                             TempTextFilter := GETFILTER("Document No.");
                             FILTERGROUP(0);
                             SetServOrderFilter;
                             IF NOT TestFilter THEN BEGIN
                               ServOrderFilter := TempTextFilter;
                               SetServOrderFilter;
                               ERROR(Text000,TABLECAPTION);
                             END;
                               ServOrderFilterOnAfterValidate;
                           END;

                OnLookup=BEGIN
                           ServHeader.RESET;
                           SetDocFilterHeader(ServHeader);
                           IF FORM.RUNMODAL(0,ServHeader) = ACTION::LookupOK THEN BEGIN
                             Text := ServHeader."No.";
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 40  ;2   ;Field     ;
                CaptionML=[ENU=Repair Status Code Filter;
                           ESP=Filtro c¢d. estado reparaci¢n];
                SourceExpr=RepairStatusFilter;
                OnValidate=BEGIN
                             FILTERGROUP(2);
                             TempTextFilter := GETFILTER("Repair Status Code");
                             FILTERGROUP(0);
                             SetRepStatFilter;
                             IF NOT TestFilter THEN BEGIN
                               RepairStatusFilter := TempTextFilter;
                               SetRepStatFilter;
                               ERROR(Text000,TABLECAPTION);
                             END;
                               RepairStatusFilterOnAfterValid;
                           END;

                OnLookup=BEGIN
                           RepairStatus.RESET;
                           IF FORM.RUNMODAL(0,RepairStatus) = ACTION::LookupOK THEN BEGIN
                             Text := RepairStatus.Code;
                             SetRepStatFilter;
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 1   ;1   ;Group     ;
                Editable=FALSE;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                SourceExpr="Response Date" }

    { 4   ;2   ;Field     ;
                SourceExpr="Response Time" }

    { 6   ;2   ;Field     ;
                SourceExpr=Priority }

    { 57  ;2   ;Field     ;
                SourceExpr="Document Type" }

    { 26  ;2   ;Field     ;
                SourceExpr="Document No." }

    { 8   ;2   ;Field     ;
                SourceExpr="Repair Status Code" }

    { 12  ;2   ;Field     ;
                SourceExpr="Service Item No." }

    { 49  ;2   ;Field     ;
                SourceExpr="Customer No." }

    { 61  ;2   ;Field     ;
                SourceExpr="Ship-to Code";
                Visible=FALSE }

    { 45  ;2   ;Field     ;
                CaptionML=[ENU=Customer Name;
                           ESP=Nombre cliente];
                SourceExpr=Cust.Name;
                Visible=FALSE }

    { 18  ;2   ;Field     ;
                SourceExpr="Service Shelf No." }

    { 14  ;2   ;Field     ;
                SourceExpr="Item No." }

    { 10  ;2   ;Field     ;
                SourceExpr="Service Item Group Code" }

    { 38  ;2   ;Field     ;
                SourceExpr="Variant Code";
                Visible=FALSE }

    { 16  ;2   ;Field     ;
                SourceExpr="Serial No.";
                OnAssistEdit=BEGIN
                               CLEAR(ItemLedgerEntry);
                               ItemLedgerEntry.SETRANGE(ItemLedgerEntry."Item No.", "Item No.");
                               ItemLedgerEntry.SETRANGE(ItemLedgerEntry."Variant Code", "Variant Code");
                               ItemLedgerEntry.SETRANGE(ItemLedgerEntry."Serial No.", "Serial No.");
                               FORM.RUN(FORM::"Item Ledger Entries",ItemLedgerEntry);
                             END;
                              }

    { 34  ;2   ;Field     ;
                SourceExpr=Description;
                Visible=FALSE }

    { 20  ;2   ;Field     ;
                SourceExpr=Warranty }

    { 22  ;2   ;Field     ;
                SourceExpr="Contract No." }

    { 24  ;2   ;Field     ;
                SourceExpr="No. of Allocations" }

    { 44  ;1   ;Group      }

    { 31  ;2   ;Field     ;
                CaptionML=[ENU=Description;
                           ESP=Descripci¢n];
                SourceExpr=Description;
                Editable=FALSE }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      ItemLedgerEntry@1013 : Record 32;
      RepairStatus@1010 : Record 5927;
      Cust@1002 : Record 18;
      ServHeader@1000 : Record 5900;
      ServItemLine@1001 : Record 5901;
      Res@1011 : Record 156;
      ResourceGroup@1015 : Record 152;
      UserMgt@1003 : Codeunit 5700;
      RepairStatusFilter@1006 : Text[250];
      RespDateFilter@1007 : Text[250];
      ServOrderFilter@1008 : Text[250];
      ResourceFilter@1009 : Text[250];
      AllocationFilter@1004 : ' ,Allocated,Not Allocated';
      AllocationStatus@1005 : ' ,Nonactive,Active,Finished,Canceled,Reallocation Needed';
      DocFilter@1012 : 'Order,Quote,All';
      ResourceGroupFilter@1014 : Text[250];
      TempTextFilter@1016 : Text[250];
      Text000@1017 : TextConst 'ENU=There is no %1 within the filter.;ESP=No hay %1 dentro del filtro.';
      TempAllocationStatus@1018 : ' ,Nonactive,Active,Finished,Canceled,Reallocation Needed';

    PROCEDURE SetAllFilters@5();
    BEGIN
      SetRepStatFilter;
      SetRespDateFilter;
      SetDocFilter;
      SetServOrderFilter;
      SetResourceFilter;
      SetResourceGroupFilter;
      SetAllocationFilter;
    END;

    PROCEDURE SetRepStatFilter@4();
    BEGIN
      FILTERGROUP(2);
      SETFILTER("Repair Status Code",RepairStatusFilter);
      RepairStatusFilter := GETFILTER("Repair Status Code");
      FILTERGROUP(0);
    END;

    PROCEDURE SetRespDateFilter@6();
    VAR
      ApplicationManagement@1000 : Codeunit 1;
    BEGIN
      FILTERGROUP(2);
      IF ApplicationManagement.MakeDateFilter(RespDateFilter) = 0 THEN;
      SETFILTER("Response Date",RespDateFilter);
      RespDateFilter := GETFILTER("Response Date");
      FILTERGROUP(0);
    END;

    PROCEDURE SetDocFilter@8();
    BEGIN
      FILTERGROUP(2);
      CASE DocFilter OF
        DocFilter::Order:
          SETRANGE("Document Type","Document Type"::Order);
        DocFilter::Quote:
          SETRANGE("Document Type","Document Type"::Quote);
        DocFilter::All:
          SETRANGE("Document Type");
      END;
      FILTERGROUP(0);
    END;

    PROCEDURE SetDocFilterHeader@2(VAR ServHeader@1000 : Record 5900);
    BEGIN
      WITH ServHeader DO BEGIN
        FILTERGROUP(2);
        CASE DocFilter OF
          DocFilter::Order:
            SETRANGE("Document Type","Document Type"::Order);
          DocFilter::Quote:
            SETRANGE("Document Type","Document Type"::Quote);
          DocFilter::All:
            SETRANGE("Document Type");
        END;
        FILTERGROUP(0);
      END;
    END;

    PROCEDURE SetServOrderFilter@7();
    BEGIN
      FILTERGROUP(2);
      SETFILTER("Document No.",ServOrderFilter);
      ServOrderFilter := GETFILTER("Document No.");
      FILTERGROUP(0);
    END;

    PROCEDURE SetResourceFilter@1();
    BEGIN
      FILTERGROUP(2);
      IF ResourceFilter <> '' THEN BEGIN
        SETFILTER("No. of Active/Finished Allocs",'>0');
        SETFILTER("Resource Filter",ResourceFilter);
        ResourceFilter := GETFILTER("Resource Filter");
      END ELSE BEGIN
        IF ResourceGroupFilter = '' THEN
          SETRANGE("No. of Active/Finished Allocs");
        SETRANGE("Resource Filter");
      END;
      FILTERGROUP(0);
    END;

    PROCEDURE SetResourceGroupFilter@9();
    BEGIN
      FILTERGROUP(2);
      IF ResourceGroupFilter <> '' THEN BEGIN
        SETFILTER("No. of Active/Finished Allocs",'>0');
        SETFILTER("Resource Group Filter",ResourceGroupFilter);
        ResourceGroupFilter := GETFILTER("Resource Group Filter");
      END ELSE BEGIN
        IF ResourceFilter = '' THEN
          SETRANGE("No. of Active/Finished Allocs");
        SETRANGE("Resource Group Filter");
      END;
      FILTERGROUP(0);
    END;

    PROCEDURE SetAllocationFilter@3();
    BEGIN
      FILTERGROUP(2);
      CASE AllocationStatus OF
        AllocationStatus::" ":
          BEGIN
            SETRANGE("Allocation Status Filter");
            SETRANGE("No. of Allocations");
          END;
        AllocationStatus::Nonactive:
          BEGIN
            SETRANGE("Allocation Status Filter","Allocation Status Filter"::Nonactive);
            SETFILTER("No. of Allocations",'>0');
          END;
        AllocationStatus::Active:
          BEGIN
            SETRANGE("Allocation Status Filter","Allocation Status Filter"::Active);
            SETFILTER("No. of Allocations",'>0');
          END;
        AllocationStatus::Finished:
          BEGIN
            SETRANGE("Allocation Status Filter","Allocation Status Filter"::Finished);
            SETFILTER("No. of Allocations",'>0');
          END;
        AllocationStatus::Canceled:
          BEGIN
            SETRANGE("Allocation Status Filter","Allocation Status Filter"::Canceled);
            SETFILTER("No. of Allocations",'>0');
          END;
        AllocationStatus::"Reallocation Needed":
         BEGIN
           SETRANGE("Allocation Status Filter","Allocation Status Filter"::"Reallocation Needed");
           SETFILTER("No. of Allocations",'>0');
         END;
      END;
      FILTERGROUP(0);
    END;

    PROCEDURE TestFilter@10() TestResult : Boolean;
    BEGIN
      IF ServOrderFilter <> '' THEN BEGIN
        FILTERGROUP(2);
        IF GETRANGEMIN("Document No.") = GETRANGEMAX("Document No.") THEN
          IF ISEMPTY THEN BEGIN
            FILTERGROUP(0);
            EXIT(FALSE);
          END;
        FILTERGROUP(0);
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE RepairStatusFilterOnAfterValid@19075265();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE ResourceFilterOnAfterValidate@19005532();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE AllocationStatusOnAfterValidat@19046183();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE DocFilterOnAfterValidate@19004301();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE ServOrderFilterOnAfterValidate@19063213();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE RespDateFilterOnAfterValidate@19063229();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE ResourceGroupFilterOnAfterVali@19056724();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    BEGIN
    END.
  }
}
