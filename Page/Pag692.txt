OBJECT Page 692 Import Style Sheet
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Import Style Sheet;
               ESP=Importar hoja estilos];
    DataCaptionExpr=AllObjWithCaption."Object Caption";
    PageType=Card;
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                CaptionML=[ENU=General;
                           ESP=General] }

    { 5   ;2   ;Field     ;
                CaptionML=[ENU=Style Sheet;
                           ESP=Hoja estilos];
                SourceExpr=FileName;
                OnValidate=VAR
                             RBAutoMgt@1001 : Codeunit 419;
                             BLOBRef@1002 : Record 99008535;
                           BEGIN
                             IF NOT EXISTS(FileName) THEN
                               ERROR(Text002,FileName);
                             RBAutoMgt.BLOBImport(BLOBRef,FileName,FALSE);
                             StyleSheet."Style Sheet" := BLOBRef.Blob;
                           END;

                OnAssistEdit=BEGIN
                               LookupStyleSheet;
                             END;
                              }

    { 2   ;2   ;Field     ;
                CaptionML=[ENU=Name;
                           ESP=Nombre];
                SourceExpr=Description;
                OnValidate=BEGIN
                             StyleSheet.Name := Description;
                           END;
                            }

    { 8   ;2   ;Field     ;
                CaptionML=[ENU=Send-to Program;
                           ESP=Enviar a programa];
                SourceExpr=ApplicationName;
                OnValidate=BEGIN
                             FindApplication;
                           END;

                OnLookup=BEGIN
                           LookupApplication;
                         END;
                          }

  }
  CODE
  {
    VAR
      SendToProgram@1002 : Record 2000000065;
      StyleSheet@1006 : Record 2000000066;
      AllObjWithCaption@1010 : Record 2000000058;
      FileName@1000 : Text[250];
      Description@1003 : Text[250];
      ApplicationName@1001 : Text[250];
      ObjectType@1004 : Integer;
      ObjectID@1005 : Integer;
      Text001@1007 : TextConst 'ENU=No %1 found.;ESP=No se encontr¢ ning£n %1.';
      Text002@1008 : TextConst 'ENU=The file %1 could not be found.;ESP=No se encontr¢ el archivo %1.';
      Text003@1009 : TextConst 'ENU=Stylesheets (*.xslt)|*.xsl*;ESP=Hojas de estilos (*.xslt)|*.xsl*';

    PROCEDURE SetObjectID@2(NewObjectType@1000 : Integer;NewObjectID@1001 : Integer;NewApplicationID@1002 : GUID);
    BEGIN
      IF NOT SendToProgram.GET(NewApplicationID) THEN
        SendToProgram.FIND('-');
      ApplicationName := SendToProgram.Name;
      ObjectType := NewObjectType;
      ObjectID := NewObjectID;
      IF NOT AllObjWithCaption.GET(AllObjWithCaption."Object Type"::Form,ObjectID) THEN
        AllObjWithCaption.INIT;
      StyleSheet.INIT;
      StyleSheet."Style Sheet ID" := CREATEGUID;
      StyleSheet."Object ID" := ObjectID;
      StyleSheet."Object Type" := ObjectType;
      StyleSheet."Program ID" := SendToProgram."Program ID";
      StyleSheet.Date := TODAY;
    END;

    PROCEDURE GetStyleSheet@1(VAR ReturnStyleSheet@1000 : Record 2000000066);
    BEGIN
      ReturnStyleSheet := StyleSheet;
    END;

    LOCAL PROCEDURE LookupApplication@5();
    VAR
      SendToPrograms@1000 : Form 691;
    BEGIN
      SendToPrograms.LOOKUPMODE := TRUE;
      SendToPrograms.SETRECORD(SendToProgram);
      IF SendToPrograms.RUNMODAL = ACTION::LookupOK THEN BEGIN
        SendToPrograms.GETRECORD(SendToProgram);
        StyleSheet."Program ID" := SendToProgram."Program ID";
        ApplicationName := SendToProgram.Name;
      END;
    END;

    LOCAL PROCEDURE FindApplication@6();
    BEGIN
      SendToProgram.RESET;
      IF ApplicationName = '' THEN BEGIN
        SendToProgram.FIND('-');
        ApplicationName := SendToProgram.Name;
        EXIT;
      END;
      SendToProgram.SETRANGE(Name,ApplicationName);
      IF SendToProgram.FIND('-') THEN BEGIN
        ApplicationName := SendToProgram.Name;
        EXIT;
      END;
      SendToProgram.SETFILTER(Name,'*@' + ApplicationName + '*');
      IF SendToProgram.FIND('-') THEN BEGIN
        ApplicationName := SendToProgram.Name;
        EXIT;
      END;
      ERROR(Text001,SendToProgram.TABLECAPTION);
    END;

    LOCAL PROCEDURE LookupStyleSheet@8();
    VAR
      CommonDlg@1002 : Codeunit 412;
      RBAutoMgt@1000 : Codeunit 419;
      BLOBRef@1001 : Record 99008535;
      NVInStream@1102601000 : InStream;
      NVOutStream@1102601001 : OutStream;
    BEGIN
      IF NOT ISSERVICETIER THEN
        FileName := CommonDlg.OpenFile(CurrPage.CAPTION,FileName,4,Text003,0)
      ELSE
        UPLOADINTOSTREAM('','',Text003,FileName,NVInStream);

      IF (Description = '') AND (FileName <> '') THEN BEGIN
        Description := FileName;
        WHILE (STRPOS(Description,'\') > 0) AND (STRLEN(Description) > STRPOS(Description,'\')) DO
          Description := COPYSTR(Description,STRPOS(Description,'\') + 1);
        StyleSheet.Name := Description;
      END;

      IF NOT ISSERVICETIER THEN BEGIN
        IF EXISTS(FileName) THEN
          RBAutoMgt.BLOBImport(BLOBRef,FileName,FALSE);
      END ELSE BEGIN
        BLOBRef.Blob.CREATEOUTSTREAM(NVOutStream);
        COPYSTREAM(NVOutStream,NVInStream);
      END;

      StyleSheet."Style Sheet" := BLOBRef.Blob;
    END;

    BEGIN
    END.
  }
}
