OBJECT Page 232 Apply Customer Entries
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01,NAVES6.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Apply Customer Entries;
               ESP=Movs. pendientes cliente];
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table21;
    DataCaptionFields=Customer No.;
    PageType=Worksheet;
    OnInit=BEGIN
             "Applies-to IDVisible" := TRUE;
           END;

    OnOpenPage=BEGIN
                 IF CalcType = CalcType::Direct THEN BEGIN
                   Cust.GET("Customer No.");
                   ApplnCurrencyCode := Cust."Currency Code";
                   FindApplyingEntry;
                 END;

                 IF ApplnType = ApplnType::"Applies-to Doc. No." THEN
                   "Applies-to IDVisible" := FALSE
                 ELSE
                   "Applies-to IDVisible" := TRUE;

                 GLSetup.GET;

                 IF ApplnType = ApplnType::"Applies-to Doc. No." THEN
                   CalcApplnAmount;
                 PostingDone := FALSE;
               END;

    OnAfterGetRecord=BEGIN
                       OnAfterGetCurrRecord;
                     END;

    OnNewRecord=BEGIN
                  OnAfterGetCurrRecord;
                END;

    OnModifyRecord=BEGIN
                     CODEUNIT.RUN(CODEUNIT::"Cust. Entry-Edit",Rec);
                     EXIT(FALSE);
                   END;

    OnQueryClosePage=BEGIN
                       IF CloseAction = ACTION::LookupOK THEN
                         LookupOKOnPush;
                       IF ApplnType = ApplnType::"Applies-to Doc. No." THEN BEGIN
                         IF OK AND (ApplyingCustLedgEntry."Posting Date" < "Posting Date") THEN BEGIN
                           OK := FALSE;
                           ERROR(
                             Text006,ApplyingCustLedgEntry."Document Type",ApplyingCustLedgEntry."Document No.",
                             "Document Type","Document No.");
                         END;
                         IF OK THEN BEGIN
                           IF "Amount to Apply" = 0 THEN
                             "Amount to Apply" := "Remaining Amount";
                           CODEUNIT.RUN(CODEUNIT::"Cust. Entry-Edit",Rec);
                         END;
                       END;
                       IF (CalcType = CalcType::Direct) AND NOT OK AND NOT PostingDone THEN BEGIN
                         Rec := ApplyingCustLedgEntry;
                         "Applying Entry" := FALSE;
                         "Applies-to ID" := '';
                         "Amount to Apply" := 0;
                         CODEUNIT.RUN(CODEUNIT::"Cust. Entry-Edit",Rec);
                       END;
                     END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 37      ;1   ;ActionGroup;
                      CaptionML=[ENU=Ent&ry;
                                 ESP=&Movimiento] }
      { 38      ;2   ;Action    ;
                      CaptionML=[ENU=Reminder/Fin. Charge Entries;
                                 ESP=Movs. recordatorio/inter‚s];
                      RunObject=Page 444;
                      RunFormView=SORTING(Customer Entry No.);
                      RunFormLink=Customer Entry No.=FIELD(Entry No.) }
      { 95      ;2   ;Action    ;
                      CaptionML=[ENU=Applied E&ntries;
                                 ESP=&Movs. conciliados];
                      RunObject=Page 61;
                      RunFormOnRec=Yes }
      { 53      ;2   ;Action    ;
                      ShortCutKey=May£s+Ctrl+D;
                      CaptionML=[ENU=Dimensions;
                                 ESP=Dimensiones];
                      RunObject=Page 544;
                      RunFormLink=Table ID=CONST(21),
                                  Entry No.=FIELD(Entry No.);
                      Image=Dimensions }
      { 62      ;2   ;Action    ;
                      ShortCutKey=Ctrl+F7;
                      CaptionML=[ENU=Detailed &Ledger Entries;
                                 ESP=M&ovimientos detallados];
                      RunObject=Page 573;
                      RunFormView=SORTING(Cust. Ledger Entry No.,Posting Date);
                      RunFormLink=Cust. Ledger Entry No.=FIELD(Entry No.) }
      { 30      ;1   ;ActionGroup;
                      CaptionML=[ENU=&Application;
                                 ESP=Li&quidaci¢n] }
      { 32      ;2   ;Action    ;
                      ShortCutKey=F7;
                      CaptionML=[ENU=Set Applies-to ID;
                                 ESP=Marcar id. de liquidaci¢n];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=SelectLineToApply;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 IF (CalcType = CalcType::GenJnlLine) AND (ApplnType = ApplnType::"Applies-to Doc. No.") THEN
                                   ERROR(Text004);

                                 SetCustApplId;
                               END;
                                }
      { 34      ;2   ;Action    ;
                      ShortCutKey=F9;
                      Ellipsis=Yes;
                      CaptionML=[ENU=Post Application;
                                 ESP=Registrar liquidaci¢n marcada];
                      Promoted=Yes;
                      Image=PostApplication;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 CustEntryApplyPostedEntries@1001 : Codeunit 226;
                               BEGIN
                                 IF CalcType = CalcType::Direct THEN BEGIN
                                   IF ApplyingCustLedgEntry."Entry No." <> 0 THEN BEGIN
                                     Rec := ApplyingCustLedgEntry;
                                     // Post Application
                                     CustEntryApplyPostedEntries.RUN(Rec);
                                     PostingDone := TRUE;
                                     CurrPage.CLOSE;
                                   END ELSE
                                     ERROR(Text002);
                                 END ELSE
                                   ERROR(Text003);
                               END;
                                }
      { 87      ;2   ;Separator ;
                      CaptionML=[ENU=-;
                                 ESP=-] }
      { 88      ;2   ;Action    ;
                      CaptionML=[ENU=Show Only Selected Entries to Be Applied;
                                 ESP=Mostrar s¢lo entradas seleccionadas para aplicar];
                      OnAction=BEGIN
                                 ShowAppliedEntries := NOT ShowAppliedEntries;
                                 IF ShowAppliedEntries THEN BEGIN
                                   IF CalcType = CalcType::GenJnlLine THEN
                                     SETRANGE("Applies-to ID",GenJnlLine."Applies-to ID")
                                   ELSE BEGIN
                                     CustEntryApplID := USERID;
                                     IF CustEntryApplID = '' THEN
                                       CustEntryApplID := '***';
                                     SETRANGE("Applies-to ID",CustEntryApplID);
                                   END;
                                 END ELSE
                                   SETRANGE("Applies-to ID");
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 36      ;1   ;Action    ;
                      CaptionML=[ENU=&Navigate;
                                 ESP=&Navegar];
                      Promoted=Yes;
                      Image=Navigate;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 Navigate.SetDoc("Posting Date","Document No.");
                                 Navigate.RUN;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 70  ;1   ;Group     ;
                CaptionML=[ENU=General;
                           ESP=General] }

    { 73  ;2   ;Field     ;
                CaptionML=[ENU=Posting Date;
                           ESP=Fecha registro];
                SourceExpr=ApplyingCustLedgEntry."Posting Date";
                Editable=FALSE }

    { 75  ;2   ;Field     ;
                CaptionML=[ENU=Document Type;
                           ESP=Tipo documento];
                OptionCaptionML=[ENU=" ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund,,,,,,,,,,,,,,,Bill";
                                 ESP=" ,Pago,Factura,Abono,Docs. inter‚s,Recordatorio,Reembolso,,,,,,,,,,,,,,,Efecto"];
                SourceExpr=ApplyingCustLedgEntry."Document Type";
                Editable=FALSE }

    { 77  ;2   ;Field     ;
                CaptionML=[ENU=Document No.;
                           ESP=N§ documento];
                SourceExpr=ApplyingCustLedgEntry."Document No.";
                Editable=FALSE }

    { 71  ;2   ;Field     ;
                CaptionML=[ENU=Customer No.;
                           ESP=N§ cliente];
                SourceExpr=ApplyingCustLedgEntry."Customer No.";
                Editable=FALSE }

    { 85  ;2   ;Field     ;
                CaptionML=[ENU=Description;
                           ESP=Descripci¢n];
                SourceExpr=ApplyingCustLedgEntry.Description;
                Editable=FALSE }

    { 79  ;2   ;Field     ;
                CaptionML=[ENU=Currency Code;
                           ESP=C¢d. divisa];
                SourceExpr=ApplyingCustLedgEntry."Currency Code";
                Editable=FALSE }

    { 81  ;2   ;Field     ;
                CaptionML=[ENU=Amount;
                           ESP=Importe];
                SourceExpr=ApplyingCustLedgEntry.Amount;
                Editable=FALSE }

    { 83  ;2   ;Field     ;
                CaptionML=[ENU=Remaining Amount;
                           ESP=Importe pendiente];
                SourceExpr=ApplyingCustLedgEntry."Remaining Amount";
                Editable=FALSE }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 22  ;2   ;Field     ;
                SourceExpr="Applies-to ID";
                Visible="Applies-to IDVisible" }

    { 2   ;2   ;Field     ;
                SourceExpr="Posting Date";
                Editable=FALSE }

    { 4   ;2   ;Field     ;
                SourceExpr="Document Type";
                Editable=FALSE }

    { 6   ;2   ;Field     ;
                SourceExpr="Document No.";
                Editable=FALSE }

    { 1100002;2;Field     ;
                SourceExpr="Bill No.";
                Editable=FALSE }

    { 1100006;2;Field     ;
                SourceExpr="Document Status";
                Editable=FALSE }

    { 1100004;2;Field     ;
                SourceExpr="Document Situation";
                Editable=FALSE }

    { 8   ;2   ;Field     ;
                SourceExpr="Customer No.";
                Editable=FALSE }

    { 10  ;2   ;Field     ;
                SourceExpr=Description;
                Editable=FALSE }

    { 39  ;2   ;Field     ;
                SourceExpr="Currency Code" }

    { 60  ;2   ;Field     ;
                SourceExpr="Original Amount";
                Visible=FALSE;
                Editable=FALSE }

    { 12  ;2   ;Field     ;
                SourceExpr=Amount;
                Visible=FALSE;
                Editable=FALSE }

    { 14  ;2   ;Field     ;
                SourceExpr="Remaining Amount";
                Editable=FALSE }

    { 33  ;2   ;Field     ;
                CaptionML=[ENU=Appln. Remaining Amount;
                           ESP=Importe pendiente liquidaci¢n];
                SourceExpr=CalcApplnRemainingAmount("Remaining Amount");
                AutoFormatType=1;
                AutoFormatExpr=ApplnCurrencyCode }

    { 89  ;2   ;Field     ;
                SourceExpr="Amount to Apply";
                OnValidate=BEGIN
                             CODEUNIT.RUN(CODEUNIT::"Cust. Entry-Edit",Rec);

                             IF (xRec."Amount to Apply" = 0) OR (Rec."Amount to Apply" = 0) AND
                                (ApplnType = ApplnType::"Applies-to ID")
                             THEN
                               SetCustApplId;
                               AmounttoApplyOnAfterValidate;
                           END;
                            }

    { 93  ;2   ;Field     ;
                CaptionML=[ENU=Appln. Amount to Apply;
                           ESP=Importe pendiente para liq.];
                SourceExpr=CalcApplnAmounttoApply("Amount to Apply");
                AutoFormatExpr=ApplnCurrencyCode }

    { 16  ;2   ;Field     ;
                SourceExpr="Due Date" }

    { 18  ;2   ;Field     ;
                SourceExpr="Pmt. Discount Date";
                OnValidate=BEGIN
                             PmtDiscountDateOnAfterValidate;
                           END;
                            }

    { 65  ;2   ;Field     ;
                SourceExpr="Pmt. Disc. Tolerance Date" }

    { 20  ;2   ;Field     ;
                SourceExpr="Original Pmt. Disc. Possible";
                Visible=FALSE }

    { 63  ;2   ;Field     ;
                SourceExpr="Remaining Pmt. Disc. Possible";
                OnValidate=BEGIN
                             RemainingPmtDiscPossibleOnAfte;
                           END;
                            }

    { 1100000;2;Field     ;
                SourceExpr="Pmt. Disc. Given (LCY)" }

    { 51  ;2   ;Field     ;
                CaptionML=[ENU=Appln. Pmt. Disc. Possible;
                           ESP=Dto. P.P. posible liqdn.];
                SourceExpr=CalcApplnRemainingAmount("Remaining Pmt. Disc. Possible");
                AutoFormatType=1;
                AutoFormatExpr=ApplnCurrencyCode }

    { 67  ;2   ;Field     ;
                SourceExpr="Max. Payment Tolerance" }

    { 24  ;2   ;Field     ;
                SourceExpr=Open;
                Editable=FALSE }

    { 26  ;2   ;Field     ;
                SourceExpr=Positive;
                Editable=FALSE }

    { 54  ;2   ;Field     ;
                SourceExpr="Global Dimension 1 Code" }

    { 56  ;2   ;Field     ;
                SourceExpr="Global Dimension 2 Code" }

    { 41  ;1   ;Group      }

    { 1903222401;2;Group  ;
                GroupType=FixedLayout }

    { 1901742001;3;Group  ;
                CaptionML=[ENU=Appln. Currency;
                           ESP=Divisa de liquidaci¢n] }

    { 48  ;4   ;Field     ;
                SourceExpr=ApplnCurrencyCode;
                TableRelation=Currency;
                Editable=FALSE }

    { 1903098801;3;Group  ;
                CaptionML=[ENU=Amount to Apply;
                           ESP=Importe pendiente de liquidar] }

    { 44  ;4   ;Field     ;
                CaptionML=[ENU=Amount to Apply;
                           ESP=Importe pendiente de liquidar];
                SourceExpr=AppliedAmount;
                AutoFormatType=1;
                AutoFormatExpr=ApplnCurrencyCode;
                Editable=FALSE }

    { 1902760701;3;Group  ;
                CaptionML=[ENU=Pmt. Disc. Amount;
                           ESP=Importe desc. P.P.] }

    { 91  ;4   ;Field     ;
                CaptionML=[ENU=Pmt. Disc. Amount;
                           ESP=Importe desc. P.P.];
                SourceExpr=-PmtDiscAmount;
                AutoFormatType=1;
                AutoFormatExpr=ApplnCurrencyCode;
                Editable=FALSE }

    { 1901741901;3;Group  ;
                CaptionML=[ENU=Rounding;
                           ESP=Redondeo] }

    { 58  ;4   ;Field     ;
                CaptionML=[ENU=Rounding;
                           ESP=Redondeo];
                SourceExpr=ApplnRounding;
                AutoFormatType=1;
                AutoFormatExpr=ApplnCurrencyCode;
                Editable=FALSE }

    { 1900546301;3;Group  ;
                CaptionML=[ENU=Applied Amount;
                           ESP=Importe liquidado] }

    { 97  ;4   ;Field     ;
                CaptionML=[ENU=Applied Amount;
                           ESP=Importe liquidado];
                SourceExpr=AppliedAmount + (-PmtDiscAmount) + ApplnRounding;
                AutoFormatType=1;
                AutoFormatExpr=ApplnCurrencyCode;
                Editable=FALSE }

    { 1901991601;3;Group  ;
                CaptionML=[ENU=Available Amount;
                           ESP=Cantidad disponible] }

    { 46  ;4   ;Field     ;
                CaptionML=[ENU=Available Amount;
                           ESP=Cantidad disponible];
                SourceExpr=ApplyingAmount;
                AutoFormatType=1;
                AutoFormatExpr=ApplnCurrencyCode;
                Editable=FALSE }

    { 1900206001;3;Group  ;
                CaptionML=[ENU=Balance;
                           ESP=Saldo] }

    { 42  ;4   ;Field     ;
                CaptionML=[ENU=Balance;
                           ESP=Saldo];
                SourceExpr=AppliedAmount + (-PmtDiscAmount) + ApplyingAmount + ApplnRounding;
                AutoFormatType=1;
                AutoFormatExpr=ApplnCurrencyCode;
                Editable=FALSE }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1903096107;1;Part   ;
                SubFormLink=Entry No.=FIELD(Entry No.);
                PagePartID=Page9106;
                Visible=TRUE;
                PartType=Page }

  }
  CODE
  {
    VAR
      ApplyingCustLedgEntry@1035 : TEMPORARY Record 21;
      AppliedCustLedgEntry@1001 : Record 21;
      Currency@1002 : Record 4;
      CurrExchRate@1003 : Record 330;
      GenJnlLine@1004 : Record 81;
      GenJnlLine2@1005 : Record 81;
      SalesHeader@1006 : Record 36;
      ServHeader@1045 : Record 5900;
      Cust@1007 : Record 18;
      CustLedgEntry@1008 : Record 21;
      GLSetup@1009 : Record 98;
      TotalSalesLine@1010 : Record 37;
      TotalSalesLineLCY@1011 : Record 37;
      TotalServLine@1046 : Record 5902;
      TotalServLineLCY@1047 : Record 5902;
      Navigate@1012 : Form 344;
      GenJnlPostLine@1042 : Codeunit 12;
      CustEntrySetApplID@1013 : Codeunit 101;
      GenJnlApply@1014 : Codeunit 225;
      SalesPost@1015 : Codeunit 80;
      GenJnlLineApply@1016 : Boolean;
      AppliedAmount@1017 : Decimal;
      ApplyingAmount@1018 : Decimal;
      PmtDiscAmount@1041 : Decimal;
      ApplnDate@1019 : Date;
      ApplnCurrencyCode@1020 : Code[10];
      ApplnRoundingPrecision@1021 : Decimal;
      ApplnRounding@1022 : Decimal;
      ApplnType@1023 : ' ,Applies-to Doc. No.,Applies-to ID';
      AmountRoundingPrecision@1024 : Decimal;
      VATAmount@1025 : Decimal;
      VATAmountText@1026 : Text[30];
      ProfitLCY@1027 : Decimal;
      ProfitPct@1028 : Decimal;
      CalcType@1029 : 'Direct,GenJnlLine,SalesHeader,ServHeader';
      CustEntryApplID@1031 : Code[20];
      ValidExchRate@1032 : Boolean;
      DifferentCurrenciesInAppln@1034 : Boolean;
      Text002@1037 : TextConst 'ENU=You must select an applying entry before you can post the application.;ESP=Seleccione un movimiento de liquidaci¢n antes de registrar la liquidaci¢n.';
      ShowAppliedEntries@1038 : Boolean;
      Text003@1039 : TextConst 'ENU=You must post the application from the window where you entered the applying entry.;ESP=Registre la liquidaci¢n desde la ventana en la que registro el movimiento.';
      Text004@1030 : TextConst 'ENU=You are not allowed to set Applies-to ID while selecting Applies-to Doc. No.;ESP=No tiene permiso para establecer el Id. de liquidaci¢n al seleccionar Liq. por n§ de documento.';
      OK@1043 : Boolean;
      Text006@1044 : TextConst 'ENU=You are not allowed to apply and post an entry to an entry with an earlier posting date.\\Instead, post %1 %2 and then apply it to %3 %4.;ESP=No tiene permiso para aplicar ni registrar un movimiento con una fecha de registro anterior.\\En su lugar, registre %1 %2 y, a continuaci¢n, apl¡quelo a %3 %4.';
      PostingDone@1000 : Boolean;
      "Applies-to IDVisible"@19043506 : Boolean INDATASET;

    PROCEDURE SetGenJnlLine@1(NewGenJnlLine@1000 : Record 81;ApplnTypeSelect@1001 : Integer);
    BEGIN
      GenJnlLine := NewGenJnlLine;
      GenJnlLineApply := TRUE;

      IF GenJnlLine."Account Type" = GenJnlLine."Account Type"::Customer THEN
        ApplyingAmount := GenJnlLine.Amount;
      IF GenJnlLine."Bal. Account Type" = GenJnlLine."Bal. Account Type"::Customer THEN
        ApplyingAmount := -GenJnlLine.Amount;
      ApplnDate := GenJnlLine."Posting Date";
      ApplnCurrencyCode := GenJnlLine."Currency Code";
      CalcType := CalcType::GenJnlLine;

      CASE ApplnTypeSelect OF
        GenJnlLine.FIELDNO("Applies-to Doc. No."):
          ApplnType := ApplnType::"Applies-to Doc. No.";
        GenJnlLine.FIELDNO("Applies-to ID"):
          ApplnType := ApplnType::"Applies-to ID";
      END;

      SetApplyingCustLedgEntry;
    END;

    PROCEDURE SetSales@2(NewSalesHeader@1000 : Record 36;VAR NewCustLedgEntry@1001 : Record 21;ApplnTypeSelect@1002 : Integer);
    VAR
      TotalAdjCostLCY@1003 : Decimal;
    BEGIN
      SalesHeader := NewSalesHeader;
      Rec.COPYFILTERS(NewCustLedgEntry);

      SalesPost.SumSalesLines(
        SalesHeader,0,TotalSalesLine,TotalSalesLineLCY,
        VATAmount,VATAmountText,ProfitLCY,ProfitPct,TotalAdjCostLCY);

      CASE SalesHeader."Document Type" OF
        SalesHeader."Document Type"::"Return Order",
        SalesHeader."Document Type"::"Credit Memo":
          ApplyingAmount := -TotalSalesLine."Amount Including VAT"
        ELSE
          ApplyingAmount := TotalSalesLine."Amount Including VAT";
      END;

      ApplnDate := SalesHeader."Posting Date";
      ApplnCurrencyCode := SalesHeader."Currency Code";
      CalcType := CalcType::SalesHeader;

      CASE ApplnTypeSelect OF
        SalesHeader.FIELDNO("Applies-to Doc. No."):
          ApplnType := ApplnType::"Applies-to Doc. No.";
        SalesHeader.FIELDNO("Applies-to ID"):
          ApplnType := ApplnType::"Applies-to ID";
      END;

      SetApplyingCustLedgEntry;
    END;

    PROCEDURE SetService@8(NewServHeader@1000 : Record 5900;VAR NewCustLedgEntry@1001 : Record 21;ApplnTypeSelect@1002 : Integer);
    VAR
      ServAmountsMgt@1003 : Codeunit 5986;
      TotalAdjCostLCY@1004 : Decimal;
    BEGIN
      ServHeader := NewServHeader;
      Rec.COPYFILTERS(NewCustLedgEntry);

      ServAmountsMgt.SumServiceLines(
        ServHeader,0,TotalServLine,TotalServLineLCY,
        VATAmount,VATAmountText,ProfitLCY,ProfitPct,TotalAdjCostLCY);

      CASE ServHeader."Document Type" OF
        ServHeader."Document Type"::"Credit Memo":
          ApplyingAmount := -TotalServLine."Amount Including VAT"
        ELSE
          ApplyingAmount := TotalServLine."Amount Including VAT";
      END;

      ApplnDate := ServHeader."Posting Date";
      ApplnCurrencyCode := ServHeader."Currency Code";
      CalcType := CalcType::ServHeader;

      CASE ApplnTypeSelect OF
        ServHeader.FIELDNO("Applies-to Doc. No."):
          ApplnType := ApplnType::"Applies-to Doc. No.";
        ServHeader.FIELDNO("Applies-to ID"):
          ApplnType := ApplnType::"Applies-to ID";
      END;

      SetApplyingCustLedgEntry;
    END;

    PROCEDURE SetCustLedgEntry@13(NewCustLedgEntry@1000 : Record 21);
    BEGIN
      Rec := NewCustLedgEntry;
    END;

    PROCEDURE SetApplyingCustLedgEntry@9();
    VAR
      "CustEntry-Edit"@1000 : Codeunit 103;
    BEGIN
      CASE CalcType OF
        CalcType::SalesHeader:
          BEGIN
            ApplyingCustLedgEntry."Entry No." := 1;
            ApplyingCustLedgEntry."Posting Date" := SalesHeader."Posting Date";
            IF SalesHeader."Document Type" = SalesHeader."Document Type"::"Return Order" THEN
              ApplyingCustLedgEntry."Document Type" := SalesHeader."Document Type"::"Credit Memo"
            ELSE
              ApplyingCustLedgEntry."Document Type" := SalesHeader."Document Type";
            ApplyingCustLedgEntry."Document No." := SalesHeader."No.";
            ApplyingCustLedgEntry."Customer No." := SalesHeader."Bill-to Customer No.";
            ApplyingCustLedgEntry.Description := SalesHeader."Posting Description";
            ApplyingCustLedgEntry."Currency Code" := SalesHeader."Currency Code";
            IF ApplyingCustLedgEntry."Document Type" = ApplyingCustLedgEntry."Document Type"::"Credit Memo" THEN  BEGIN
              ApplyingCustLedgEntry.Amount := -TotalSalesLine."Amount Including VAT";
              ApplyingCustLedgEntry."Remaining Amount" := -TotalSalesLine."Amount Including VAT";
            END ELSE BEGIN
              ApplyingCustLedgEntry.Amount := TotalSalesLine."Amount Including VAT";
              ApplyingCustLedgEntry."Remaining Amount" := TotalSalesLine."Amount Including VAT";
            END;
            CalcApplnAmount;
          END;
        CalcType::ServHeader:
          BEGIN
            ApplyingCustLedgEntry."Entry No." := 1;
            ApplyingCustLedgEntry."Posting Date" := ServHeader."Posting Date";
            ApplyingCustLedgEntry."Document Type" := ServHeader."Document Type";
            ApplyingCustLedgEntry."Document No." := ServHeader."No.";
            ApplyingCustLedgEntry."Customer No." := ServHeader."Bill-to Customer No.";
            ApplyingCustLedgEntry.Description := ServHeader."Posting Description";
            ApplyingCustLedgEntry."Currency Code" := ServHeader."Currency Code";
            IF ApplyingCustLedgEntry."Document Type" = ApplyingCustLedgEntry."Document Type"::"Credit Memo" THEN  BEGIN
              ApplyingCustLedgEntry.Amount := -TotalServLine."Amount Including VAT";
              ApplyingCustLedgEntry."Remaining Amount" := -TotalServLine."Amount Including VAT";
            END ELSE BEGIN
              ApplyingCustLedgEntry.Amount := TotalServLine."Amount Including VAT";
              ApplyingCustLedgEntry."Remaining Amount" := TotalServLine."Amount Including VAT";
            END;
            CalcApplnAmount;
          END;
        CalcType::Direct:
          BEGIN
            IF "Applying Entry" THEN BEGIN
              IF ApplyingCustLedgEntry."Entry No." <> 0 THEN
                CustLedgEntry := ApplyingCustLedgEntry;
              "CustEntry-Edit".RUN(Rec);
              IF "Applies-to ID" = '' THEN
                SetCustApplId;
              CALCFIELDS(Amount);
              ApplyingCustLedgEntry := Rec;
              IF CustLedgEntry."Entry No." <> 0 THEN BEGIN
                Rec := CustLedgEntry;
                "Applying Entry" := FALSE;
                SetCustApplId;
              END;
              SETCURRENTKEY("Entry No.");
              SETFILTER("Entry No.",'<> %1',ApplyingCustLedgEntry."Entry No.");
              ApplyingAmount := ApplyingCustLedgEntry."Remaining Amount";
              ApplnDate := ApplyingCustLedgEntry."Posting Date";
              ApplnCurrencyCode := ApplyingCustLedgEntry."Currency Code";
            END;
            CalcApplnAmount;
          END;
        CalcType::GenJnlLine:
          BEGIN
            ApplyingCustLedgEntry."Entry No." := 1;
            ApplyingCustLedgEntry."Posting Date" := GenJnlLine."Posting Date";
            ApplyingCustLedgEntry."Document Type" := GenJnlLine."Document Type";
            ApplyingCustLedgEntry."Document No." := GenJnlLine."Document No.";
            ApplyingCustLedgEntry."Customer No." := GenJnlLine."Account No.";
            ApplyingCustLedgEntry.Description := GenJnlLine.Description;
            ApplyingCustLedgEntry."Currency Code" := GenJnlLine."Currency Code";
            ApplyingCustLedgEntry.Amount := GenJnlLine.Amount;
            ApplyingCustLedgEntry."Remaining Amount" := GenJnlLine.Amount;
            CalcApplnAmount;
          END;
      END;
    END;

    PROCEDURE SetCustApplId@11();
    BEGIN
      IF (CalcType = CalcType::GenJnlLine) AND (ApplyingCustLedgEntry."Posting Date" < "Posting Date") THEN
        ERROR(
          Text006,ApplyingCustLedgEntry."Document Type",ApplyingCustLedgEntry."Document No.",
          "Document Type","Document No.");

      IF ApplyingCustLedgEntry."Entry No." <> 0 THEN
        GenJnlApply.CheckAgainstApplnCurrency(
          ApplnCurrencyCode,"Currency Code",GenJnlLine."Account Type"::Customer,TRUE);

      CustLedgEntry.COPY(Rec);
      CurrPage.SETSELECTIONFILTER(CustLedgEntry);

      IF GenJnlLineApply THEN
        CustEntrySetApplID.SetApplId(
          CustLedgEntry,ApplyingCustLedgEntry,AppliedAmount,PmtDiscAmount,GenJnlLine."Applies-to ID")
      ELSE
        IF CalcType = CalcType::SalesHeader THEN
          CustEntrySetApplID.SetApplId(
            CustLedgEntry,ApplyingCustLedgEntry,AppliedAmount,PmtDiscAmount,SalesHeader."Applies-to ID")
        ELSE
          CustEntrySetApplID.SetApplId(
            CustLedgEntry,ApplyingCustLedgEntry,AppliedAmount,PmtDiscAmount,ServHeader."Applies-to ID");

      CalcApplnAmount;
    END;

    PROCEDURE CalcApplnAmount@4();
    VAR
      ExchAccGLJnlLine@1000 : Codeunit 366;
      I@1001 : Integer;
    BEGIN
      AppliedAmount := 0;
      PmtDiscAmount := 0;
      DifferentCurrenciesInAppln := FALSE;

      CASE CalcType OF
        CalcType::Direct:
          BEGIN
            FindAmountRounding;
            CustEntryApplID := USERID;
            IF CustEntryApplID = '' THEN
              CustEntryApplID := '***';

            CustLedgEntry := ApplyingCustLedgEntry;

            AppliedCustLedgEntry.SETCURRENTKEY("Customer No.",Open,Positive);
            AppliedCustLedgEntry.SETRANGE("Customer No.","Customer No.");
            AppliedCustLedgEntry.SETRANGE(Open,TRUE);
            AppliedCustLedgEntry.SETRANGE("Applies-to ID",CustEntryApplID);

            IF ApplyingCustLedgEntry."Entry No." <> 0 THEN BEGIN
              CustLedgEntry.CALCFIELDS("Remaining Amount");
              AppliedCustLedgEntry.SETFILTER("Entry No.",'<>%1',ApplyingCustLedgEntry."Entry No.");
            END;

            HandlChosenEntries(0,
              CustLedgEntry."Remaining Amount",
              CustLedgEntry."Currency Code",
              CustLedgEntry."Posting Date");
          END;

        CalcType::GenJnlLine:
          BEGIN
            FindAmountRounding;
            IF GenJnlLine."Bal. Account Type" = GenJnlLine."Bal. Account Type"::Customer THEN
              ExchAccGLJnlLine.RUN(GenJnlLine);

            CASE ApplnType OF
              ApplnType::"Applies-to Doc. No.":
                BEGIN
                  AppliedCustLedgEntry := Rec;
                  WITH AppliedCustLedgEntry DO BEGIN
                    CALCFIELDS("Remaining Amount");
                    IF "Currency Code" <> ApplnCurrencyCode THEN BEGIN
                      "Remaining Amount" :=
                        CurrExchRate.ExchangeAmtFCYToFCY(
                          ApplnDate,"Currency Code",ApplnCurrencyCode,"Remaining Amount");
                      "Remaining Pmt. Disc. Possible" :=
                        CurrExchRate.ExchangeAmtFCYToFCY(
                          ApplnDate,"Currency Code",ApplnCurrencyCode,"Remaining Pmt. Disc. Possible");
                      "Amount to Apply" :=
                        CurrExchRate.ExchangeAmtFCYToFCY(
                          ApplnDate,"Currency Code",ApplnCurrencyCode,"Amount to Apply");
                    END;

                    IF "Amount to Apply" <> 0 THEN
                      AppliedAmount := ROUND("Amount to Apply",AmountRoundingPrecision)
                    ELSE
                      AppliedAmount := ROUND("Remaining Amount",AmountRoundingPrecision);

                    IF GenJnlPostLine.CheckCalcPmtDiscGenJnlCust(
                         GenJnlLine,AppliedCustLedgEntry,0,FALSE) AND
                       ((ABS(GenJnlLine.Amount) + ApplnRoundingPrecision >=
                         ABS(AppliedAmount - "Remaining Pmt. Disc. Possible")) OR
                        (GenJnlLine.Amount = 0))
                    THEN
                      PmtDiscAmount := "Remaining Pmt. Disc. Possible";

                    IF NOT DifferentCurrenciesInAppln THEN
                      DifferentCurrenciesInAppln := ApplnCurrencyCode <> "Currency Code";

                  END;
                  CheckRounding;
                END;

              ApplnType::"Applies-to ID":
                BEGIN
                  GenJnlLine2 := GenJnlLine;
                  AppliedCustLedgEntry.SETCURRENTKEY("Customer No.",Open,Positive);
                  AppliedCustLedgEntry.SETRANGE("Customer No.",GenJnlLine."Account No.");
                  AppliedCustLedgEntry.SETRANGE(Open,TRUE);
                  AppliedCustLedgEntry.SETRANGE("Applies-to ID",GenJnlLine."Applies-to ID");

                  HandlChosenEntries(1,
                    GenJnlLine2.Amount,
                    GenJnlLine2."Currency Code",
                    GenJnlLine2."Posting Date");
                END;
              END;
          END;

        CalcType::SalesHeader,CalcType::ServHeader:
          BEGIN
            FindAmountRounding;

            CASE ApplnType OF
              ApplnType::"Applies-to Doc. No.":
                BEGIN
                  AppliedCustLedgEntry := Rec;
                  WITH AppliedCustLedgEntry DO BEGIN
                    CALCFIELDS("Remaining Amount");

                    IF "Currency Code" <> ApplnCurrencyCode THEN
                      "Remaining Amount" :=
                        CurrExchRate.ExchangeAmtFCYToFCY(
                          ApplnDate,"Currency Code",ApplnCurrencyCode,"Remaining Amount");

                    AppliedAmount := ROUND("Remaining Amount",AmountRoundingPrecision);

                    IF NOT DifferentCurrenciesInAppln THEN
                      DifferentCurrenciesInAppln := ApplnCurrencyCode <> "Currency Code";

                  END;
                  CheckRounding;
                END;

              ApplnType::"Applies-to ID":
                BEGIN
                  AppliedCustLedgEntry.SETCURRENTKEY("Customer No.",Open,Positive);
                  IF CalcType = CalcType::SalesHeader THEN
                    AppliedCustLedgEntry.SETRANGE("Customer No.",SalesHeader."Bill-to Customer No.")
                  ELSE
                    AppliedCustLedgEntry.SETRANGE("Customer No.",ServHeader."Bill-to Customer No.");
                  AppliedCustLedgEntry.SETRANGE(Open,TRUE);
                  IF CalcType = CalcType::SalesHeader THEN
                    AppliedCustLedgEntry.SETRANGE("Applies-to ID",SalesHeader."Applies-to ID")
                  ELSE
                    AppliedCustLedgEntry.SETRANGE("Applies-to ID",ServHeader."Applies-to ID");

                  HandlChosenEntries(2,
                    GenJnlLine.Amount,
                    GenJnlLine."Currency Code",
                    GenJnlLine."Posting Date");

                END;
            END;
          END;
      END;
    END;

    PROCEDURE CalcApplnRemainingAmount@6(Amount@1000 : Decimal) : Decimal;
    VAR
      ApplnRemainingAmount@1001 : Decimal;
    BEGIN
      ValidExchRate := TRUE;
      IF ApplnCurrencyCode = "Currency Code" THEN
        EXIT(Amount);

      IF ApplnDate = 0D THEN
        ApplnDate := "Posting Date";
      ApplnRemainingAmount :=
        CurrExchRate.ApplnExchangeAmtFCYToFCY(
          ApplnDate,"Currency Code",ApplnCurrencyCode,Amount,ValidExchRate);
      EXIT(ApplnRemainingAmount);
    END;

    PROCEDURE CalcApplnAmounttoApply@10(AmounttoApply@1000 : Decimal) : Decimal;
    VAR
      ApplnAmounttoApply@1001 : Decimal;
    BEGIN
      ValidExchRate := TRUE;

      IF ApplnCurrencyCode = "Currency Code" THEN
        EXIT(AmounttoApply);

      IF ApplnDate = 0D THEN
        ApplnDate := "Posting Date";
      ApplnAmounttoApply :=
        CurrExchRate.ApplnExchangeAmtFCYToFCY(
          ApplnDate,"Currency Code",ApplnCurrencyCode,AmounttoApply,ValidExchRate);
      EXIT(ApplnAmounttoApply);
    END;

    PROCEDURE FindAmountRounding@7();
    BEGIN
      IF ApplnCurrencyCode = '' THEN BEGIN
        Currency.INIT;
        Currency.Code := '';
        Currency.InitRoundingPrecision;
      END ELSE
        IF ApplnCurrencyCode <> Currency.Code THEN
          Currency.GET(ApplnCurrencyCode);

      AmountRoundingPrecision := Currency."Amount Rounding Precision";
    END;

    PROCEDURE CheckRounding@3();
    BEGIN
      ApplnRounding := 0;

      CASE CalcType OF
        CalcType::SalesHeader,CalcType::ServHeader:
          EXIT;
        CalcType::GenJnlLine:
          IF ("Document Type" <> "Document Type"::Payment) AND
             ("Document Type" <> "Document Type"::Refund)
          THEN
            EXIT;
      END;

      IF ApplnCurrencyCode = '' THEN
        ApplnRoundingPrecision := GLSetup."Appln. Rounding Precision"
      ELSE BEGIN
        IF ApplnCurrencyCode <> "Currency Code" THEN
          Currency.GET(ApplnCurrencyCode);
        ApplnRoundingPrecision := Currency."Appln. Rounding Precision";
      END;

      IF (ABS((AppliedAmount - PmtDiscAmount) + ApplyingAmount) <= ApplnRoundingPrecision) AND DifferentCurrenciesInAppln THEN
        ApplnRounding := -((AppliedAmount - PmtDiscAmount) + ApplyingAmount);
    END;

    PROCEDURE GetCustLedgEntry@5(VAR CustLedgEntry@1000 : Record 21);
    BEGIN
      CustLedgEntry := Rec;
    END;

    PROCEDURE FindApplyingEntry@12();
    BEGIN
      IF CalcType = CalcType::Direct THEN BEGIN
        CustEntryApplID := USERID;
        IF CustEntryApplID = '' THEN
          CustEntryApplID := '***';

        CustLedgEntry.SETCURRENTKEY("Customer No.","Applies-to ID",Open);
        CustLedgEntry.SETRANGE("Customer No.","Customer No.");
        CustLedgEntry.SETRANGE("Applies-to ID",CustEntryApplID);
        CustLedgEntry.SETRANGE(Open,TRUE);
        CustLedgEntry.SETRANGE("Applying Entry",TRUE);
        IF CustLedgEntry.FIND('-') THEN BEGIN
          CustLedgEntry.CALCFIELDS(Amount,"Remaining Amount");
          ApplyingCustLedgEntry := CustLedgEntry;
          SETCURRENTKEY("Entry No.");
          SETFILTER("Entry No.",'<> %1',CustLedgEntry."Entry No.");
          ApplyingAmount := CustLedgEntry."Remaining Amount";
          ApplnDate := CustLedgEntry."Posting Date";
          ApplnCurrencyCode := CustLedgEntry."Currency Code";
        END;
        CalcApplnAmount;
      END;
    END;

    PROCEDURE HandlChosenEntries@14(Type@1000 : 'Direct,GenJnlLine,SalesHeader';CurrentAmount@1001 : Decimal;CurrencyCode@1002 : Code[10];"Posting Date"@1003 : Date);
    VAR
      AppliedCustLedgEntryTemp@1004 : TEMPORARY Record 21;
      PossiblePmtDisc@1007 : Decimal;
      OldPmtDisc@1008 : Decimal;
      CorrectionAmount@1009 : Decimal;
      CalculateCurrency@1006 : Boolean;
      CanUseDisc@1005 : Boolean;
      FromZeroGenJnl@1010 : Boolean;
    BEGIN
      IF AppliedCustLedgEntry.FINDSET(FALSE,FALSE) THEN BEGIN
        REPEAT
          AppliedCustLedgEntryTemp := AppliedCustLedgEntry;
          AppliedCustLedgEntryTemp.INSERT;
        UNTIL AppliedCustLedgEntry.NEXT = 0;
      END ELSE
        EXIT;

      FromZeroGenJnl := (CurrentAmount = 0) AND (Type = Type::GenJnlLine);

      REPEAT
        IF NOT FromZeroGenJnl THEN
          AppliedCustLedgEntryTemp.SETRANGE(Positive,CurrentAmount < 0);
        IF AppliedCustLedgEntryTemp.FINDFIRST THEN BEGIN
          AppliedCustLedgEntryTemp.CALCFIELDS("Remaining Amount");

          IF Type = Type::Direct THEN
            CalculateCurrency := ApplyingCustLedgEntry."Entry No." <> 0
          ELSE
            CalculateCurrency := TRUE;


          IF (CurrencyCode <> AppliedCustLedgEntryTemp."Currency Code") AND CalculateCurrency THEN BEGIN
            AppliedCustLedgEntryTemp."Remaining Amount" :=
              GenJnlPostLine.ExchAmount(
                AppliedCustLedgEntryTemp."Remaining Amount",
                AppliedCustLedgEntryTemp."Currency Code",
                CurrencyCode,"Posting Date");
            AppliedCustLedgEntryTemp."Remaining Pmt. Disc. Possible" :=
              GenJnlPostLine.ExchAmount(
                AppliedCustLedgEntryTemp."Remaining Pmt. Disc. Possible",
                AppliedCustLedgEntryTemp."Currency Code",
                CurrencyCode,"Posting Date");
            AppliedCustLedgEntryTemp."Amount to Apply" :=
              GenJnlPostLine.ExchAmount(
                AppliedCustLedgEntryTemp."Amount to Apply",
                AppliedCustLedgEntryTemp."Currency Code",
                CurrencyCode,"Posting Date");
          END;

          CASE Type OF
            Type::Direct:
              CanUseDisc := GenJnlPostLine.CheckCalcPmtDiscCust(CustLedgEntry,AppliedCustLedgEntryTemp,0,FALSE,FALSE);
            Type::GenJnlLine:
              CanUseDisc := GenJnlPostLine.CheckCalcPmtDiscGenJnlCust(GenJnlLine2,AppliedCustLedgEntryTemp,0,FALSE)
            ELSE
              CanUseDisc := FALSE;
          END;

          IF CanUseDisc AND
            (ABS(AppliedCustLedgEntryTemp."Amount to Apply") >= ABS(AppliedCustLedgEntryTemp."Remaining Amount" -
            AppliedCustLedgEntryTemp."Remaining Pmt. Disc. Possible")) THEN BEGIN

            IF (ABS(CurrentAmount - PossiblePmtDisc) > ABS(AppliedCustLedgEntryTemp."Remaining Amount" -
              AppliedCustLedgEntryTemp."Remaining Pmt. Disc. Possible"))
            THEN BEGIN
              PmtDiscAmount := PmtDiscAmount + AppliedCustLedgEntryTemp."Remaining Pmt. Disc. Possible";
              CurrentAmount := CurrentAmount + AppliedCustLedgEntryTemp."Remaining Amount" -
                AppliedCustLedgEntryTemp."Remaining Pmt. Disc. Possible";
            END ELSE IF (ABS(CurrentAmount - PossiblePmtDisc) = ABS(AppliedCustLedgEntryTemp."Remaining Amount" -
              AppliedCustLedgEntryTemp."Remaining Pmt. Disc. Possible"))
            THEN BEGIN
              PmtDiscAmount := PmtDiscAmount + AppliedCustLedgEntryTemp."Remaining Pmt. Disc. Possible" + PossiblePmtDisc;
              CurrentAmount := CurrentAmount + AppliedCustLedgEntryTemp."Remaining Amount" -
                AppliedCustLedgEntryTemp."Remaining Pmt. Disc. Possible" - PossiblePmtDisc;
              PossiblePmtDisc := 0;
              AppliedAmount := AppliedAmount + CorrectionAmount;
            END ELSE IF (FromZeroGenJnl) THEN BEGIN
              PmtDiscAmount := PmtDiscAmount + AppliedCustLedgEntryTemp."Remaining Pmt. Disc. Possible";
              CurrentAmount := CurrentAmount +
                AppliedCustLedgEntryTemp."Remaining Amount" - AppliedCustLedgEntryTemp."Remaining Pmt. Disc. Possible";
            END ELSE BEGIN
              IF (CurrentAmount + AppliedCustLedgEntryTemp."Remaining Amount" >= 0) <> (CurrentAmount >= 0) THEN BEGIN
                PmtDiscAmount := PmtDiscAmount + PossiblePmtDisc;
                CurrentAmount := CurrentAmount - PossiblePmtDisc;
                AppliedAmount := AppliedAmount + CorrectionAmount;
              END;
              CurrentAmount := CurrentAmount + AppliedCustLedgEntryTemp."Remaining Amount";
              PossiblePmtDisc := AppliedCustLedgEntryTemp."Remaining Pmt. Disc. Possible";
            END;
          END ELSE BEGIN
            IF (((CurrentAmount - PossiblePmtDisc + AppliedCustLedgEntryTemp."Amount to Apply") * CurrentAmount) <= 0) THEN BEGIN
              PmtDiscAmount := PmtDiscAmount + PossiblePmtDisc;
              CurrentAmount := CurrentAmount - PossiblePmtDisc;
              PossiblePmtDisc := 0;
              AppliedAmount := AppliedAmount + CorrectionAmount;
            END;
            CurrentAmount := CurrentAmount + AppliedCustLedgEntryTemp."Amount to Apply";
          END;
        END ELSE BEGIN
          AppliedCustLedgEntryTemp.SETRANGE(Positive);
          AppliedCustLedgEntryTemp.FINDFIRST;
        END;

        IF (OldPmtDisc <> PmtDiscAmount) THEN
          AppliedAmount := AppliedAmount + AppliedCustLedgEntryTemp."Remaining Amount"
        ELSE
          AppliedAmount := AppliedAmount + AppliedCustLedgEntryTemp."Amount to Apply";
        OldPmtDisc := PmtDiscAmount;

        IF PossiblePmtDisc <> 0 THEN
          CorrectionAmount := AppliedCustLedgEntryTemp."Remaining Amount" - AppliedCustLedgEntryTemp."Amount to Apply"
        ELSE
          CorrectionAmount := 0;

        IF NOT DifferentCurrenciesInAppln THEN
          DifferentCurrenciesInAppln:= ApplnCurrencyCode <> AppliedCustLedgEntryTemp."Currency Code";

        AppliedCustLedgEntryTemp.DELETE;
        AppliedCustLedgEntryTemp.SETRANGE(Positive);

      UNTIL NOT AppliedCustLedgEntryTemp.FINDFIRST;
      CheckRounding;
    END;

    LOCAL PROCEDURE AmounttoApplyOnAfterValidate@19038179();
    BEGIN
      IF ApplnType <> ApplnType::"Applies-to Doc. No." THEN BEGIN
        CalcApplnAmount;
        CurrPage.UPDATE(FALSE);
      END;
    END;

    LOCAL PROCEDURE PmtDiscountDateOnAfterValidate@19051222();
    BEGIN
      CurrPage.UPDATE(TRUE);
      CalcApplnAmount;
    END;

    LOCAL PROCEDURE RemainingPmtDiscPossibleOnAfte@19061040();
    BEGIN
      CurrPage.UPDATE(TRUE);
      CalcApplnAmount;
    END;

    LOCAL PROCEDURE OnAfterGetCurrRecord@19077479();
    BEGIN
      xRec := Rec;
      IF ApplnType = ApplnType::"Applies-to Doc. No." THEN
        CalcApplnAmount;
    END;

    LOCAL PROCEDURE LookupOKOnPush@19031339();
    BEGIN
      OK := TRUE;
    END;

    BEGIN
    END.
  }
}
