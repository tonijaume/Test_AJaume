OBJECT Page 401 Purch. Credit Memo Statistics
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00,NAVES6.00.01;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[ENU=Purch. Credit Memo Statistics;
               ESP=Estad. abono compras];
    LinksAllowed=No;
    SourceTable=Table124;
    PageType=ListPlus;
    OnAfterGetRecord=VAR
                       VendLedgEntry@1000 : Record 25;
                     BEGIN
                       CLEARALL;

                       IF "Currency Code" = '' THEN
                         Currency.InitRoundingPrecision
                       ELSE
                         Currency.GET("Currency Code");

                       PurchCrMemoLine.SETRANGE("Document No.","No.");

                       IF PurchCrMemoLine.FIND('-') THEN
                         REPEAT
                           VendAmount := VendAmount + PurchCrMemoLine.Amount;
                           AmountInclVAT := AmountInclVAT + PurchCrMemoLine."Amount Including VAT";
                           IF "Prices Including VAT" THEN BEGIN
                             InvDiscAmount := InvDiscAmount + PurchCrMemoLine."Inv. Discount Amount" /
                             (1 + (PurchCrMemoLine."VAT %" + PurchCrMemoLine."EC %") / 100);
                             PmtDiscAmount := PmtDiscAmount + PurchCrMemoLine."Pmt. Disc. Rcd. Amount" /
                               (1+ (PurchCrMemoLine."VAT %" + PurchCrMemoLine."EC %") / 100)
                           END ELSE BEGIN
                             InvDiscAmount := InvDiscAmount + PurchCrMemoLine."Inv. Discount Amount";
                             PmtDiscAmount := PmtDiscAmount + PurchCrMemoLine."Pmt. Disc. Rcd. Amount";
                           END;
                           LineQty := LineQty + PurchCrMemoLine.Quantity;
                           TotalNetWeight := TotalNetWeight + (PurchCrMemoLine.Quantity * PurchCrMemoLine."Net Weight");
                           TotalGrossWeight := TotalGrossWeight + (PurchCrMemoLine.Quantity * PurchCrMemoLine."Gross Weight");
                           TotalVolume := TotalVolume + (PurchCrMemoLine.Quantity * PurchCrMemoLine."Unit Volume");
                           IF PurchCrMemoLine."Units per Parcel" > 0 THEN
                             TotalParcels := TotalParcels + ROUND(PurchCrMemoLine.Quantity / PurchCrMemoLine."Units per Parcel",1,'>');
                           IF PurchCrMemoLine."VAT %" <> VATPercentage THEN
                             IF VATPercentage = 0 THEN
                               VATPercentage := PurchCrMemoLine."VAT %" + PurchCrMemoLine."EC %"
                             ELSE
                               VATPercentage := -1;
                         UNTIL PurchCrMemoLine.NEXT = 0;
                       VATAmount := AmountInclVAT - VendAmount;
                       InvDiscAmount := ROUND(InvDiscAmount,Currency."Amount Rounding Precision");

                       IF VATPercentage <= 0 THEN
                         VATAmountText := Text000
                       ELSE
                         VATAmountText := STRSUBSTNO(Text001,VATPercentage);

                       IF "Currency Code" = '' THEN
                         AmountLCY := VendAmount
                       ELSE
                         AmountLCY :=
                           CurrExchRate.ExchangeAmtFCYToLCY(
                             WORKDATE,"Currency Code",VendAmount,"Currency Factor");

                       VendLedgEntry.SETCURRENTKEY("Document No.");
                       VendLedgEntry.SETRANGE("Document No.","No.");
                       VendLedgEntry.SETRANGE("Document Type",VendLedgEntry."Document Type"::"Credit Memo");
                       VendLedgEntry.SETRANGE("Vendor No.","Pay-to Vendor No.");
                       IF VendLedgEntry.FINDFIRST THEN
                         AmountLCY := VendLedgEntry."Purchase (LCY)";

                       IF NOT Vend.GET("Pay-to Vendor No.") THEN
                         CLEAR(Vend);
                       Vend.CALCFIELDS("Balance (LCY)");

                       PurchCrMemoLine.CalcVATAmountLines(Rec,TempVATAmountLine);
                       CurrPage.SubForm.FORM.SetTempVATAmountLine(TempVATAmountLine);
                       CurrPage.SubForm.FORM.InitGlobals("Currency Code",FALSE,FALSE,FALSE,FALSE,"VAT Base Discount %");
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 29  ;1   ;Group     ;
                CaptionML=[ENU=General;
                           ESP=General] }

    { 2   ;2   ;Field     ;
                CaptionML=[ENU=Amount;
                           ESP=Importe];
                SourceExpr=VendAmount + InvDiscAmount + PmtDiscAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code" }

    { 6   ;2   ;Field     ;
                CaptionML=[ENU=Inv. Discount Amount;
                           ESP=Importe dto. factura];
                SourceExpr=InvDiscAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code" }

    { 1100000;2;Field     ;
                CaptionML=[ENU=Pmt. Disc. Rcd. Amount;
                           ESP=Dto. P.P.];
                SourceExpr=PmtDiscAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                Editable=TRUE }

    { 12  ;2   ;Field     ;
                CaptionML=[ENU=Total;
                           ESP=Total];
                SourceExpr=VendAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code" }

    { 16  ;2   ;Field     ;
                CaptionML=[ENU=VAT Amount;
                           ESP=Importe IVA];
                SourceExpr=VATAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=FORMAT (VATAmountText) }

    { 20  ;2   ;Field     ;
                CaptionML=[ENU=Total Incl. VAT;
                           ESP=Total IVA incl.];
                SourceExpr=AmountInclVAT;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code" }

    { 26  ;2   ;Field     ;
                CaptionML=[ENU=Purchase (LCY);
                           ESP=Compra (DL)];
                SourceExpr=AmountLCY;
                AutoFormatType=1 }

    { 3   ;2   ;Field     ;
                CaptionML=[ENU=Quantity;
                           ESP=Cantidad];
                DecimalPlaces=0:6;
                SourceExpr=LineQty }

    { 13  ;2   ;Field     ;
                CaptionML=[ENU=Parcels;
                           ESP=Lotes];
                DecimalPlaces=0:6;
                SourceExpr=TotalParcels }

    { 7   ;2   ;Field     ;
                CaptionML=[ENU=Net Weight;
                           ESP=Peso neto];
                DecimalPlaces=0:6;
                SourceExpr=TotalNetWeight }

    { 10  ;2   ;Field     ;
                CaptionML=[ENU=Gross Weight;
                           ESP=Peso bruto];
                DecimalPlaces=0:6;
                SourceExpr=TotalGrossWeight }

    { 17  ;2   ;Field     ;
                CaptionML=[ENU=Volume;
                           ESP=Volumen];
                DecimalPlaces=0:6;
                SourceExpr=TotalVolume }

    { 18  ;1   ;Part      ;
                Name=SubForm;
                PagePartID=Page576;
                Editable=FALSE }

    { 1901061301;1;Group  ;
                CaptionML=[ENU=Vendor;
                           ESP=Proveedor] }

    { 21  ;2   ;Field     ;
                CaptionML=[ENU=Balance (LCY);
                           ESP=Saldo (DL)];
                SourceExpr=Vend."Balance (LCY)";
                AutoFormatType=1 }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=VAT Amount;ESP=Importe IVA';
      Text001@1001 : TextConst 'ENU=%1% VAT;ESP=%1% IVA';
      CurrExchRate@1002 : Record 330;
      PurchCrMemoLine@1003 : Record 125;
      Vend@1004 : Record 23;
      TempVATAmountLine@1005 : TEMPORARY Record 290;
      Currency@1018 : Record 4;
      VendAmount@1006 : Decimal;
      AmountInclVAT@1007 : Decimal;
      InvDiscAmount@1008 : Decimal;
      AmountLCY@1009 : Decimal;
      LineQty@1010 : Decimal;
      TotalNetWeight@1011 : Decimal;
      TotalGrossWeight@1012 : Decimal;
      TotalVolume@1013 : Decimal;
      TotalParcels@1014 : Decimal;
      VATAmount@1015 : Decimal;
      VATPercentage@1016 : Decimal;
      VATAmountText@1017 : Text[30];
      PmtDiscAmount@1100000 : Decimal;

    BEGIN
    END.
  }
}
