OBJECT Page 7133 Item Budget Entries
{
  OBJECT-PROPERTIES
  {
    Date=15/09/04;
    Time=12:00:00;
    Version List=NAVW14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Item Budget Entries;
               ESP=Movimientos de presupuesto de productos];
    SourceTable=Table7134;
    DataCaptionExpr=GetCaption;
    DelayedInsert=Yes;
    PageType=List;
    OnInit=BEGIN
             "Budget Dimension 3 CodeEnable" := TRUE;
             "Budget Dimension 2 CodeEnable" := TRUE;
             "Budget Dimension 1 CodeEnable" := TRUE;
             "Global Dimension 2 CodeEnable" := TRUE;
             "Global Dimension 1 CodeEnable" := TRUE;
             "Budget Dimension 3 CodeVisible" := TRUE;
             "Budget Dimension 2 CodeVisible" := TRUE;
             "Budget Dimension 1 CodeVisible" := TRUE;
             "Global Dimension 2 CodeVisible" := TRUE;
             "Global Dimension 1 CodeVisible" := TRUE;
             LowestModifiedEntryNo := 2147483647;
           END;

    OnOpenPage=BEGIN
                 IF GETFILTER("Budget Name") = '' THEN
                   ItemBudgetName.INIT
                 ELSE BEGIN
                   COPYFILTER("Analysis Area",ItemBudgetName."Analysis Area");
                   COPYFILTER("Budget Name",ItemBudgetName.Name);
                   ItemBudgetName.FIND('-');
                 END;
                 CurrPage.EDITABLE := NOT ItemBudgetName.Blocked;
                 GLSetup.GET;
                 "Global Dimension 1 CodeEnable" := GLSetup."Global Dimension 1 Code" <> '';
                 "Global Dimension 2 CodeEnable" := GLSetup."Global Dimension 2 Code" <> '';
                 "Budget Dimension 1 CodeEnable" := ItemBudgetName."Budget Dimension 1 Code" <> '';
                 "Budget Dimension 2 CodeEnable" := ItemBudgetName."Budget Dimension 2 Code" <> '';
                 "Budget Dimension 3 CodeEnable" := ItemBudgetName."Budget Dimension 3 Code" <> '';
                 "Global Dimension 1 CodeVisible" := GLSetup."Global Dimension 1 Code" <> '';
                 "Global Dimension 2 CodeVisible" := GLSetup."Global Dimension 2 Code" <> '';
                 "Budget Dimension 1 CodeVisible" := ItemBudgetName."Budget Dimension 1 Code" <> '';
                 "Budget Dimension 2 CodeVisible" := ItemBudgetName."Budget Dimension 2 Code" <> '';
                 "Budget Dimension 3 CodeVisible" := ItemBudgetName."Budget Dimension 3 Code" <> '';
               END;

    OnClosePage=VAR
                  UpdateItemAnalysisView@1000 : Codeunit 7150;
                BEGIN
                  IF LowestModifiedEntryNo < 2147483647 THEN
                    UpdateItemAnalysisView.SetLastBudgetEntryNo(LowestModifiedEntryNo - 1);
                END;

    OnAfterGetRecord=BEGIN
                       OnAfterGetCurrRecord;
                     END;

    OnNewRecord=BEGIN
                  TempItemBudgetDim.DELETEALL;
                  "Budget Name" := GETRANGEMIN("Budget Name");
                  "Analysis Area" := GETRANGEMIN("Analysis Area");
                  IF (ItemBudgetName.Name <> "Budget Name") OR (ItemBudgetName."Analysis Area" <> "Analysis Area") THEN
                    ItemBudgetName.GET("Analysis Area","Budget Name");
                  IF GETFILTER("Item No.") <> '' THEN
                    "Item No." := GetFirstItem(GETFILTER("Item No."));
                  Date := GetFirstDate(GETFILTER(Date));
                  "User ID" := USERID;

                  IF GETFILTER("Global Dimension 1 Code") <> '' THEN
                    "Global Dimension 1 Code" :=
                      GetFirstDimValue(GLSetup."Global Dimension 1 Code",GETFILTER("Global Dimension 1 Code"));

                  IF GETFILTER("Global Dimension 2 Code") <> '' THEN
                    "Global Dimension 2 Code" :=
                      GetFirstDimValue(GLSetup."Global Dimension 2 Code",GETFILTER("Global Dimension 2 Code"));

                  IF GETFILTER("Budget Dimension 1 Code") <> '' THEN
                    "Budget Dimension 1 Code" :=
                      GetFirstDimValue(ItemBudgetName."Budget Dimension 1 Code",GETFILTER("Budget Dimension 1 Code"));

                  IF GETFILTER("Budget Dimension 2 Code") <> '' THEN
                    "Budget Dimension 2 Code" :=
                      GetFirstDimValue(ItemBudgetName."Budget Dimension 2 Code",GETFILTER("Budget Dimension 2 Code"));

                  IF GETFILTER("Budget Dimension 3 Code") <> '' THEN
                    "Budget Dimension 3 Code" :=
                      GetFirstDimValue(ItemBudgetName."Budget Dimension 3 Code",GETFILTER("Budget Dimension 3 Code"));

                  IF GETFILTER("Location Code") <> '' THEN
                    "Location Code" := GetFirstLocationCode(GETFILTER("Location Code"));
                  OnAfterGetCurrRecord;
                END;

    OnModifyRecord=BEGIN
                     IF "Entry No." < LowestModifiedEntryNo THEN
                       LowestModifiedEntryNo := "Entry No.";
                     EXIT(TRUE);
                   END;

    OnDeleteRecord=BEGIN
                     IF "Entry No." < LowestModifiedEntryNo THEN
                       LowestModifiedEntryNo := "Entry No.";
                     EXIT(TRUE);
                   END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                SourceExpr="Budget Name";
                Editable=FALSE }

    { 4   ;2   ;Field     ;
                SourceExpr=Date }

    { 6   ;2   ;Field     ;
                SourceExpr="Item No." }

    { 8   ;2   ;Field     ;
                SourceExpr=Description }

    { 10  ;2   ;Field     ;
                SourceExpr="Source Type" }

    { 12  ;2   ;Field     ;
                SourceExpr="Source No." }

    { 16  ;2   ;Field     ;
                SourceExpr="Global Dimension 1 Code";
                Visible="Global Dimension 1 CodeVisible";
                Enabled="Global Dimension 1 CodeEnable" }

    { 18  ;2   ;Field     ;
                SourceExpr="Global Dimension 2 Code";
                Visible="Global Dimension 2 CodeVisible";
                Enabled="Global Dimension 2 CodeEnable" }

    { 20  ;2   ;Field     ;
                SourceExpr="Budget Dimension 1 Code";
                Visible="Budget Dimension 1 CodeVisible";
                Enabled="Budget Dimension 1 CodeEnable" }

    { 22  ;2   ;Field     ;
                SourceExpr="Budget Dimension 2 Code";
                Visible="Budget Dimension 2 CodeVisible";
                Enabled="Budget Dimension 2 CodeEnable" }

    { 24  ;2   ;Field     ;
                SourceExpr="Budget Dimension 3 Code";
                Visible="Budget Dimension 3 CodeVisible";
                Enabled="Budget Dimension 3 CodeEnable" }

    { 14  ;2   ;Field     ;
                SourceExpr="Location Code";
                Visible=FALSE }

    { 26  ;2   ;Field     ;
                SourceExpr=Quantity }

    { 28  ;2   ;Field     ;
                SourceExpr="Cost Amount" }

    { 30  ;2   ;Field     ;
                SourceExpr="Sales Amount" }

    { 32  ;2   ;Field     ;
                SourceExpr="Entry No.";
                Editable=FALSE }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      GLSetup@1004 : Record 98;
      ItemBudgetName@1003 : Record 7132;
      TempItemBudgetDim@1002 : TEMPORARY Record 7135;
      CurrentEntryNo@1001 : Integer;
      LowestModifiedEntryNo@1000 : Integer;
      "Global Dimension 1 CodeVisible"@19028123 : Boolean INDATASET;
      "Global Dimension 2 CodeVisible"@19054752 : Boolean INDATASET;
      "Budget Dimension 1 CodeVisible"@19036733 : Boolean INDATASET;
      "Budget Dimension 2 CodeVisible"@19019212 : Boolean INDATASET;
      "Budget Dimension 3 CodeVisible"@19014779 : Boolean INDATASET;
      "Global Dimension 1 CodeEnable"@19057401 : Boolean INDATASET;
      "Global Dimension 2 CodeEnable"@19039220 : Boolean INDATASET;
      "Budget Dimension 1 CodeEnable"@19069571 : Boolean INDATASET;
      "Budget Dimension 2 CodeEnable"@19071456 : Boolean INDATASET;
      "Budget Dimension 3 CodeEnable"@19074873 : Boolean INDATASET;

    LOCAL PROCEDURE GetFirstItem@3(ItemFilter@1000 : Text[250]) : Code[20];
    VAR
      Item@1001 : Record 27;
    BEGIN
      WITH Item DO BEGIN
        SETFILTER("No.",ItemFilter);
        IF FIND('-') THEN
          EXIT("No.")
        ELSE
          EXIT('');
      END;
    END;

    LOCAL PROCEDURE GetFirstDate@1(DateFilter@1001 : Text[250]) : Date;
    VAR
      Period@1002 : Record 2000000007;
    BEGIN
      IF DateFilter = '' THEN
        EXIT(0D);
      WITH Period DO BEGIN
        Period.SETRANGE("Period Type","Period Type"::Date);
        SETFILTER("Period Start",DateFilter);
        IF FIND('-') THEN
          EXIT("Period Start")
        ELSE
          EXIT(0D);
      END;
    END;

    LOCAL PROCEDURE GetFirstDimValue@2(DimCode@1000 : Code[20];DimValFilter@1001 : Text[250]) : Code[20];
    VAR
      DimVal@1002 : Record 349;
    BEGIN
      IF (DimCode = '') OR (DimValFilter = '') THEN
        EXIT('');
      WITH DimVal DO BEGIN
        SETRANGE("Dimension Code",DimCode);
        SETFILTER(Code,DimValFilter);
        IF FIND('-') THEN
          EXIT(Code)
        ELSE
          EXIT('');
      END;
    END;

    LOCAL PROCEDURE GetFirstLocationCode@5(LocationCodetFilter@1000 : Text[250]) : Code[10];
    VAR
      Location@1001 : Record 14;
    BEGIN
      WITH Location DO BEGIN
        SETFILTER(Code,LocationCodetFilter);
        IF FIND('-') THEN
          EXIT(Code)
        ELSE
          EXIT('');
      END;
    END;

    LOCAL PROCEDURE CheckIfDimChanged@11();
    VAR
      ItemBudgetDim@1000 : Record 7135;
      EOF_ItemBudgetDim@1001 : Boolean;
      EOF_TempItemBudgetDim@1002 : Boolean;
      Match@1003 : Boolean;
    BEGIN
      IF "Entry No." >= LowestModifiedEntryNo THEN
        EXIT;

      IF "Entry No." <> CurrentEntryNo THEN BEGIN
        TempItemBudgetDim.DELETEALL;
        ItemBudgetDim.SETRANGE("Entry No.","Entry No.");
        IF ItemBudgetDim.FIND('-') THEN
          REPEAT
            TempItemBudgetDim := ItemBudgetDim;
            TempItemBudgetDim.INSERT;
          UNTIL ItemBudgetDim.NEXT = 0;
        CurrentEntryNo := "Entry No.";
      END ELSE BEGIN
        ItemBudgetDim.SETRANGE("Entry No.","Entry No.");
        EOF_ItemBudgetDim := NOT ItemBudgetDim.FIND('-');
        EOF_TempItemBudgetDim := NOT TempItemBudgetDim.FIND('-');
        Match :=
          (EOF_ItemBudgetDim = EOF_TempItemBudgetDim) AND
          (TempItemBudgetDim."Dimension Code" = ItemBudgetDim."Dimension Code") AND
          (TempItemBudgetDim."Dimension Value Code" = ItemBudgetDim."Dimension Value Code");
        WHILE Match AND (NOT EOF_ItemBudgetDim) AND (NOT EOF_TempItemBudgetDim) DO BEGIN
          EOF_ItemBudgetDim := ItemBudgetDim.NEXT = 0;
          EOF_TempItemBudgetDim := TempItemBudgetDim.NEXT = 0;
          Match :=
            (EOF_ItemBudgetDim = EOF_TempItemBudgetDim) AND
            (TempItemBudgetDim."Dimension Code" = ItemBudgetDim."Dimension Code") AND
            (TempItemBudgetDim."Dimension Value Code" = ItemBudgetDim."Dimension Value Code");
        END;
        IF NOT Match THEN
          LowestModifiedEntryNo := "Entry No.";
      END;
    END;

    LOCAL PROCEDURE OnAfterGetCurrRecord@19077479();
    BEGIN
      xRec := Rec;
      IF "Entry No." <> 0 THEN
        CheckIfDimChanged;
      IF "Budget Name" <> xRec."Budget Name" THEN;
    END;

    BEGIN
    END.
  }
}
