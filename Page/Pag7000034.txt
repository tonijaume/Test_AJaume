OBJECT Page 7000034 Posted Bill Gr. Maturity Lines
{
  OBJECT-PROPERTIES
  {
    Date=24/10/01;
    Time=12:00:00;
    Version List=NAVES4.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Lines;
               ESP=L¡neas];
    SourceTable=Table2000000007;
    PageType=ListPart;
    OnOpenPage=BEGIN
                 RESET;
               END;

    OnFindRecord=BEGIN
                   EXIT(PeriodFormManagement.FindDate(Which,Rec,PeriodLength));
                 END;

    OnNextRecord=BEGIN
                   EXIT(PeriodFormManagement.NextDate(Steps,Rec,PeriodLength));
                 END;

    OnAfterGetRecord=BEGIN
                       SetDateFilter;
                       IF PostedBillGr."No." <> '' THEN BEGIN
                         PostedBillGr.CALCFIELDS("Amount Grouped","Amount Grouped (LCY)");
                         DocAmount := PostedBillGr."Amount Grouped";
                         DocAmountLCY := PostedBillGr."Amount Grouped (LCY)";
                       END ELSE BEGIN
                         DocAmount := 0;
                         DocAmountLCY := 0;
                       END;
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                Editable=FALSE;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                SourceExpr="Period Start" }

    { 6   ;2   ;Field     ;
                SourceExpr="Period Name" }

    { 4   ;2   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=Amount;
                           ESP=Importe];
                SourceExpr=DocAmount;
                AutoFormatType=1;
                AutoFormatExpr=PostedBillGr."Currency Code";
                OnDrillDown=BEGIN
                              ShowDocEntries;
                            END;
                             }

    { 8   ;2   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=Amount (LCY);
                           ESP=Importe (DL)];
                SourceExpr=DocAmountLCY;
                AutoFormatType=1;
                OnDrillDown=BEGIN
                              ShowDocEntries;
                            END;
                             }

  }
  CODE
  {
    VAR
      PostedBillGr@1100000 : Record 7000006;
      PostedDoc@1100001 : Record 7000003;
      PeriodFormManagement@1100002 : Codeunit 359;
      PeriodLength@1100003 : 'Day,Week,Month,Quarter,Year,Period';
      AmountType@1100004 : 'Net Change,Balance at Date';
      DocAmount@1100005 : Decimal;
      DocAmountLCY@1100006 : Decimal;

    PROCEDURE Set@1(VAR NewPostedBillGr@1100000 : Record 7000006;NewPeriodLength@1100001 : Integer;NewAmountType@1100002 : 'Net Change,Balance at Date');
    BEGIN
      PostedBillGr.COPY(NewPostedBillGr);
      PeriodLength := NewPeriodLength;
      AmountType := NewAmountType;
      CurrPage.UPDATE(FALSE);
    END;

    PROCEDURE SetDateFilter@5();
    BEGIN
      IF AmountType = AmountType::"Net Change" THEN
        PostedBillGr.SETRANGE("Due Date Filter","Period Start","Period End")
      ELSE
        PostedBillGr.SETRANGE("Due Date Filter",0D,"Period End");
    END;

    LOCAL PROCEDURE ShowDocEntries@8();
    BEGIN
      SetDateFilter;
      PostedDoc.SETRANGE("Bill Gr./Pmt. Order No.",PostedBillGr."No.");
      PostedDoc.SETRANGE("Collection Agent",PostedDoc."Collection Agent"::Bank);
      PostedDoc.SETFILTER("Due Date",PostedBillGr.GETFILTER("Due Date Filter"));
      PostedDoc.SETFILTER("Global Dimension 1 Code",PostedBillGr.GETFILTER("Global Dimension 1 Filter"));
      PostedDoc.SETFILTER("Global Dimension 2 Code",PostedBillGr.GETFILTER("Global Dimension 2 Filter"));
      PostedDoc.SETFILTER("Category Code",PostedBillGr.GETFILTER("Category Filter"));
      FORM.RUNMODAL(0,PostedDoc);
    END;

    BEGIN
    END.
  }
}
