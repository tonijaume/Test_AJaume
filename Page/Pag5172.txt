OBJECT Page 5172 Answer Points
{
  OBJECT-PROPERTIES
  {
    Date=21/05/03;
    Time=12:00:00;
    Version List=NAVW13.70;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Answer Points;
               ESP=Puntos respuesta];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table5088;
    DataCaptionExpr=CaptionStr;
    PageType=Worksheet;
    OnOpenPage=VAR
                 TempProfileQuestnLine@1000 : Record 5088;
               BEGIN
                 TargetQuestnCode := "Profile Questionnaire Code";
                 TargetQuestLineNo := "Line No.";
                 TargetQuestnLineNoEnd := "Line No.";
                 CurrQuestnCode := "Profile Questionnaire Code";

                 IF TempProfileQuestnLine.GET(TargetQuestnCode,"Line No.") THEN
                   WHILE (TempProfileQuestnLine.NEXT <> 0) AND
                         (TempProfileQuestnLine.Type = TempProfileQuestnLine.Type::Answer) DO
                     TargetQuestnLineNoEnd := TempProfileQuestnLine."Line No.";

                 SetRatingFilter;
                 CaptionStr := FORMAT("Profile Questionnaire Code") + '  ' + Description;
               END;

    OnAfterGetRecord=VAR
                       Rating@1000 : Record 5111;
                     BEGIN
                       DescriptionIndent := 0;
                       IF Rating.GET(TargetQuestnCode,TargetQuestLineNo,"Profile Questionnaire Code","Line No.") THEN
                         Points := Rating.Points
                       ELSE
                         Points := 0;
                       OnAfterGetCurrRecord;
                       TypeOnFormat;
                       DescriptionOnFormat;
                     END;

    OnNewRecord=BEGIN
                  OnAfterGetCurrRecord;
                END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 4       ;1   ;ActionGroup;
                      CaptionML=[ENU=&Points;
                                 ESP=&Puntos] }
      { 7       ;2   ;Action    ;
                      ShortCutKey=May£s+Ctrl+L;
                      CaptionML=[ENU=List;
                                 ESP=Lista];
                      OnAction=VAR
                                 Rating@1000 : Record 5111;
                               BEGIN
                                 Rating.SETRANGE("Profile Questionnaire Code",TargetQuestnCode);
                                 Rating.SETRANGE(Rating."Profile Questionnaire Line No.",TargetQuestLineNo);
                                 IF FORM.RUNMODAL(FORM::"Answer Points List",Rating) = ACTION::LookupOK THEN BEGIN
                                   CurrQuestnCode := Rating."Rating Profile Quest. Code";
                                   SETRANGE("Profile Questionnaire Code",CurrQuestnCode);
                                   GET(CurrQuestnCode,Rating."Rating Profile Quest. Line No.");
                                 END;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 5   ;1   ;Field     ;
                CaptionML=[ENU=Profile Questionnaire Code;
                           ESP=C¢d. cuestionario perfil];
                SourceExpr=CurrQuestnCode;
                OnLookup=VAR
                           ProfileQuestnHeader@1002 : Record 5087;
                         BEGIN
                           IF ProfileQuestnHeader.GET("Profile Questionnaire Code") THEN
                             IF FORM.RUNMODAL(
                               FORM::"Profile Questionnaire List",ProfileQuestnHeader) = ACTION::LookupOK
                             THEN BEGIN
                               SETRANGE("Profile Questionnaire Code",ProfileQuestnHeader.Code);
                               CurrQuestnCode := ProfileQuestnHeader.Code;
                               SetRatingFilter;
                               CurrPage.UPDATE;
                             END;
                         END;
                          }

    { 13  ;1   ;Group     ;
                IndentationColumnName=DescriptionIndent;
                IndentationControls=Description;
                GroupType=Repeater }

    { 15  ;2   ;Field     ;
                SourceExpr=Type;
                Editable=FALSE }

    { 17  ;2   ;Field     ;
                SourceExpr=Description;
                Editable=FALSE }

    { 8   ;2   ;Field     ;
                SourceExpr="No. of Contacts" }

    { 1   ;2   ;Field     ;
                CaptionML=[ENU=Points;
                           ESP=Puntos];
                DecimalPlaces=0:0;
                BlankZero=Yes;
                SourceExpr=Points;
                OnValidate=VAR
                             Rating@1000 : Record 5111;
                           BEGIN
                             TESTFIELD(Type,Type::Answer);

                             IF Rating.GET(TargetQuestnCode,TargetQuestLineNo,"Profile Questionnaire Code","Line No.") THEN
                               IF Points = 0 THEN
                                 Rating.DELETE
                               ELSE BEGIN
                                 Rating.Points := Points;
                                 Rating.MODIFY;
                               END
                             ELSE BEGIN
                               Rating."Profile Questionnaire Code" := TargetQuestnCode;
                               Rating."Profile Questionnaire Line No." := TargetQuestLineNo;
                               Rating."Rating Profile Quest. Code" := "Profile Questionnaire Code";
                               Rating."Rating Profile Quest. Line No." := "Line No.";
                               Rating.Points := Points;
                               Rating.INSERT(TRUE);
                             END;
                           END;
                            }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      CurrQuestnCode@1003 : Code[10];
      TargetQuestnCode@1000 : Code[10];
      TargetQuestLineNo@1002 : Integer;
      TargetQuestnLineNoEnd@1005 : Integer;
      Points@1001 : Decimal;
      CaptionStr@1004 : Text[260];
      TypeEmphasize@19012868 : Boolean INDATASET;
      DescriptionEmphasize@19010547 : Boolean INDATASET;
      DescriptionIndent@19057867 : Integer INDATASET;

    PROCEDURE SetRatingFilter@1();
    VAR
      TempProfileQuestnLine@1000 : Record 5088;
      FilterLineStart@1001 : Integer;
      FilterLineEnd@1002 : Integer;
    BEGIN
      FILTERGROUP(2);
      IF CurrQuestnCode = TargetQuestnCode THEN
        SETFILTER("Line No.",'<%1|>%2',TargetQuestLineNo,TargetQuestnLineNoEnd)
      ELSE
        SETRANGE("Line No.");
      FILTERGROUP(0);
    END;

    LOCAL PROCEDURE OnAfterGetCurrRecord@19077479();
    VAR
      Rating@1000 : Record 5111;
    BEGIN
      xRec := Rec;
      IF Rating.GET(TargetQuestnCode,TargetQuestLineNo,"Profile Questionnaire Code","Line No.") THEN
        Points := Rating.Points
      ELSE
        Points := 0;
    END;

    LOCAL PROCEDURE TypeOnFormat@19025827();
    BEGIN
      IF Type = Type::Question THEN
        TypeEmphasize := TRUE
    END;

    LOCAL PROCEDURE DescriptionOnFormat@19023855();
    BEGIN
      IF Type = Type::Question THEN
        DescriptionEmphasize := TRUE
      ELSE
        DescriptionIndent := 1;
    END;

    BEGIN
    END.
  }
}
