OBJECT Page 402 Sales Order Statistics
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00,NAVES6.00.01;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Sales Order Statistics;
               ESP=Estad. pedido ventas];
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table36;
    PageType=Card;
    OnOpenPage=BEGIN
                 SalesSetup.GET;
                 AllowInvDisc := NOT (SalesSetup."Calc. Inv. Discount" AND CustInvDiscRecExists("Invoice Disc. Code"));
                 AllowVATDifference :=
                   SalesSetup."Allow VAT Difference" AND
                   NOT ("Document Type" IN ["Document Type"::Quote,"Document Type"::"Blanket Order"]);
                 VATLinesFormIsEditable := AllowVATDifference OR AllowInvDisc;
                 CurrPage.EDITABLE := VATLinesFormIsEditable;
               END;

    OnAfterGetRecord=VAR
                       SalesLine@1000 : Record 37;
                       TempSalesLine@1001 : TEMPORARY Record 37;
                       SalesPostPrepmt@1002 : Codeunit 442;
                     BEGIN
                       CurrPage.CAPTION(STRSUBSTNO(Text000,"Document Type"));

                       IF PrevNo = "No." THEN
                         EXIT;
                       PrevNo := "No.";
                       FILTERGROUP(2);
                       SETRANGE("No.",PrevNo);
                       FILTERGROUP(0);

                       CLEAR(SalesLine);
                       CLEAR(TotalSalesLine);
                       CLEAR(TotalSalesLineLCY);

                       FOR i := 1 TO 3 DO BEGIN
                         TempSalesLine.DELETEALL;
                         CLEAR(TempSalesLine);
                         CLEAR(SalesPost);
                         SalesPost.GetSalesLines(Rec,TempSalesLine,i - 1);
                         CLEAR(SalesPost);
                         CASE i OF
                           1: SalesLine.CalcVATAmountLines(0,Rec,TempSalesLine,TempVATAmountLine1);
                           2: SalesLine.CalcVATAmountLines(0,Rec,TempSalesLine,TempVATAmountLine2);
                           3: SalesLine.CalcVATAmountLines(0,Rec,TempSalesLine,TempVATAmountLine3);
                         END;

                         SalesPost.SumSalesLinesTemp(
                           Rec,TempSalesLine,i - 1,TotalSalesLine[i],TotalSalesLineLCY[i],
                           VATAmount[i],VATAmountText[i],ProfitLCY[i],ProfitPct[i],TotalAdjCostLCY[i]);

                         IF i = 3 THEN
                           TotalAdjCostLCY[i] := TotalSalesLineLCY[i]."Unit Cost (LCY)";

                         AdjProfitLCY[i] := TotalSalesLineLCY[i].Amount - TotalAdjCostLCY[i];
                         IF TotalSalesLineLCY[i].Amount <> 0 THEN
                           AdjProfitPct[i] := ROUND(AdjProfitLCY[i] / TotalSalesLineLCY[i].Amount * 100,0.1);

                         IF "Prices Including VAT" THEN BEGIN
                           TotalAmount2[i] := TotalSalesLine[i].Amount;
                           TotalAmount1[i] := TotalAmount2[i] + VATAmount[i];
                           TotalSalesLine[i]."Line Amount" := TotalAmount1[i] + TotalSalesLine[i]."Inv. Discount Amount"
                             + TotalSalesLine[i]."Pmt. Disc. Given Amount";
                         END ELSE BEGIN
                           TotalAmount1[i] := TotalSalesLine[i].Amount;
                           TotalAmount2[i] := TotalSalesLine[i]."Amount Including VAT";
                         END;
                       END;
                       TempSalesLine.DELETEALL;
                       CLEAR(TempSalesLine);
                       SalesPostPrepmt.GetSalesLines(Rec,0,TempSalesLine);
                       SalesPostPrepmt.SumPrepmt(
                         Rec,TempSalesLine,TempVATAmountLine4,PrepmtTotalAmount,PrepmtVATAmount,PrepmtVATAmountText);
                       PrepmtInvPct :=
                         Pct(TotalSalesLine[1]."Prepmt. Amt. Inv.",PrepmtTotalAmount);
                       PrepmtDeductedPct :=
                         Pct(TotalSalesLine[1]."Prepmt Amt Deducted",TotalSalesLine[1]."Prepmt. Amt. Inv.");
                       IF "Prices Including VAT" THEN BEGIN
                         PrepmtTotalAmount2 := PrepmtTotalAmount;
                         PrepmtTotalAmount := PrepmtTotalAmount + PrepmtVATAmount;
                       END ELSE
                         PrepmtTotalAmount2 := PrepmtTotalAmount + PrepmtVATAmount;

                       IF Cust.GET("Bill-to Customer No.") THEN
                         Cust.CALCFIELDS("Balance (LCY)")
                       ELSE
                         CLEAR(Cust);

                       CASE TRUE OF
                         Cust."Credit Limit (LCY)" = 0:
                           CreditLimitLCYExpendedPct := 0;
                         Cust."Balance (LCY)" / Cust."Credit Limit (LCY)" < 0:
                           CreditLimitLCYExpendedPct := 0;
                         Cust."Balance (LCY)" / Cust."Credit Limit (LCY)" > 1:
                           CreditLimitLCYExpendedPct := 10000;
                         ELSE
                           CreditLimitLCYExpendedPct := ROUND(Cust."Balance (LCY)" / Cust."Credit Limit (LCY)" * 10000,1);
                       END;

                       TempVATAmountLine1.MODIFYALL(Modified,FALSE);
                       TempVATAmountLine2.MODIFYALL(Modified,FALSE);
                       TempVATAmountLine3.MODIFYALL(Modified,FALSE);
                       TempVATAmountLine4.MODIFYALL(Modified,FALSE);

                       PrevTab := -1;
                     END;

    OnQueryClosePage=BEGIN
                       GetVATSpecification(PrevTab);
                       IF TempVATAmountLine1.GetAnyLineModified OR TempVATAmountLine2.GetAnyLineModified THEN
                         UpdateVATOnSalesLines;
                       EXIT(TRUE);
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                CaptionML=[ENU=General;
                           ESP=General] }

    { 39  ;2   ;Field     ;
                SourceExpr=TotalSalesLine[1]."Line Amount";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text002,FALSE);
                Editable=FALSE }

    { 30  ;2   ;Field     ;
                CaptionML=[ENU=Inv. Discount Amount;
                           ESP=Importe dto. factura];
                SourceExpr=TotalSalesLine[1]."Inv. Discount Amount";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                OnValidate=BEGIN
                             ActiveTab := ActiveTab::General;
                             UpdateInvDiscAmount(1);
                           END;
                            }

    { 1100002;2;Field     ;
                CaptionML=[ENU=Pmt. Disc. Given Amount;
                           ESP=Dto. P.P.];
                SourceExpr=TotalSalesLine[1]."Pmt. Disc. Given Amount";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                Editable=FALSE;
                OnValidate=BEGIN
                             ActiveTab := ActiveTab::General;
                             UpdateInvDiscAmount(1);
                           END;
                            }

    { 20  ;2   ;Field     ;
                SourceExpr=TotalAmount1[1];
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text001,FALSE);
                OnValidate=BEGIN
                             ActiveTab := ActiveTab::General;
                             UpdateTotalAmount(1);
                           END;
                            }

    { 16  ;2   ;Field     ;
                CaptionML=[ENU=VAT Amount;
                           ESP=Importe IVA];
                SourceExpr=VATAmount[1];
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=FORMAT (VATAmountText[1]);
                Editable=FALSE }

    { 10  ;2   ;Field     ;
                SourceExpr=TotalAmount2[1];
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text001,TRUE);
                Editable=FALSE;
                OnValidate=BEGIN
                             TotalAmount21OnAfterValidate;
                           END;
                            }

    { 7   ;2   ;Field     ;
                CaptionML=[ENU=Sales (LCY);
                           ESP=Ventas (DL)];
                SourceExpr=TotalSalesLineLCY[1].Amount;
                AutoFormatType=1;
                Editable=FALSE }

    { 32  ;2   ;Field     ;
                CaptionML=[ENU=Original Profit (LCY);
                           ESP=Bf§ original (DL)];
                SourceExpr=ProfitLCY[1];
                AutoFormatType=1;
                Editable=FALSE }

    { 46  ;2   ;Field     ;
                CaptionML=[ENU=Adjusted Profit (LCY);
                           ESP=Beneficio actualizado (DL)];
                SourceExpr=AdjProfitLCY[1];
                AutoFormatType=1;
                Editable=FALSE }

    { 24  ;2   ;Field     ;
                CaptionML=[ENU=Original Profit %;
                           ESP=% Bf§ original];
                DecimalPlaces=1:1;
                SourceExpr=ProfitPct[1];
                Editable=FALSE }

    { 52  ;2   ;Field     ;
                CaptionML=[ENU=Adjusted Profit %;
                           ESP=% beneficio actualizado];
                DecimalPlaces=1:1;
                SourceExpr=AdjProfitPct[1];
                Editable=FALSE }

    { 22  ;2   ;Field     ;
                CaptionML=[ENU=Quantity;
                           ESP=Cantidad];
                DecimalPlaces=0:6;
                SourceExpr=TotalSalesLine[1].Quantity;
                Editable=FALSE }

    { 8   ;2   ;Field     ;
                CaptionML=[ENU=Parcels;
                           ESP=Lotes];
                DecimalPlaces=0:6;
                SourceExpr=TotalSalesLine[1]."Units per Parcel";
                Editable=FALSE }

    { 18  ;2   ;Field     ;
                CaptionML=[ENU=Net Weight;
                           ESP=Peso neto];
                DecimalPlaces=0:6;
                SourceExpr=TotalSalesLine[1]."Net Weight";
                Editable=FALSE }

    { 11  ;2   ;Field     ;
                CaptionML=[ENU=Gross Weight;
                           ESP=Peso bruto];
                DecimalPlaces=0:6;
                SourceExpr=TotalSalesLine[1]."Gross Weight";
                Editable=FALSE }

    { 2   ;2   ;Field     ;
                CaptionML=[ENU=Volume;
                           ESP=Volumen];
                DecimalPlaces=0:6;
                SourceExpr=TotalSalesLine[1]."Unit Volume";
                Editable=FALSE }

    { 37  ;2   ;Field     ;
                CaptionML=[ENU=Original Cost (LCY);
                           ESP=Coste original (DL)];
                SourceExpr=TotalSalesLineLCY[1]."Unit Cost (LCY)";
                AutoFormatType=1;
                Editable=FALSE }

    { 35  ;2   ;Field     ;
                CaptionML=[ENU=Adjusted Cost (LCY);
                           ESP=Coste actualizado (DL)];
                SourceExpr=TotalAdjCostLCY[1];
                AutoFormatType=1;
                Editable=FALSE }

    { 120 ;2   ;Field     ;
                CaptionML=[ENU=Cost Adjmt. Amount (LCY);
                           ESP=Importe coste ajustado (DL)];
                SourceExpr=TotalAdjCostLCY[1] - TotalSalesLineLCY[1]."Unit Cost (LCY)";
                AutoFormatType=1;
                Editable=FALSE;
                OnLookup=BEGIN
                           LookupAdjmtValueEntries(0);
                         END;
                          }

    { 119 ;2   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=No. of VAT Lines;
                           ESP=N§ de l¡neas de IVA];
                SourceExpr=TempVATAmountLine1.COUNT;
                OnDrillDown=BEGIN
                              VATLinesDrillDown(TempVATAmountLine1,FALSE);
                              UpdateHeaderInfo(1, TempVATAmountLine1);
                            END;
                             }

    { 1905885101;1;Group  ;
                CaptionML=[ENU=Invoicing;
                           ESP=Facturaci¢n] }

    { 71  ;2   ;Field     ;
                SourceExpr=TotalSalesLine[2]."Line Amount";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text002,FALSE);
                Editable=FALSE }

    { 51  ;2   ;Field     ;
                CaptionML=[ENU=Inv. Discount Amount;
                           ESP=Importe dto. factura];
                SourceExpr=TotalSalesLine[2]."Inv. Discount Amount";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                OnValidate=BEGIN
                             ActiveTab := ActiveTab::Invoicing;
                             UpdateInvDiscAmount(2);
                           END;
                            }

    { 1100003;2;Field     ;
                CaptionML=[ENU=Pmt. Disc. Given Amount;
                           ESP=Dto. P.P.];
                SourceExpr=TotalSalesLine[2]."Pmt. Disc. Given Amount";
                AutoFormatType=1;
                Editable=FALSE;
                OnValidate=BEGIN
                             ActiveTab := ActiveTab::Invoicing;
                             UpdateInvDiscAmount(2);
                           END;
                            }

    { 50  ;2   ;Field     ;
                SourceExpr=TotalAmount1[2];
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text001,FALSE);
                OnValidate=BEGIN
                             ActiveTab := ActiveTab::Invoicing;
                             UpdateTotalAmount(2);
                           END;
                            }

    { 49  ;2   ;Field     ;
                SourceExpr=VATAmount[2];
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=FORMAT (VATAmountText[2]);
                Editable=FALSE }

    { 48  ;2   ;Field     ;
                SourceExpr=TotalAmount2[2];
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text001,TRUE);
                Editable=FALSE }

    { 69  ;2   ;Field     ;
                CaptionML=[ENU=Sales (LCY);
                           ESP=Ventas (DL)];
                SourceExpr=TotalSalesLineLCY[2].Amount;
                AutoFormatType=1;
                Editable=FALSE }

    { 67  ;2   ;Field     ;
                CaptionML=[ENU=Original Profit (LCY);
                           ESP=Bf§ original (DL)];
                SourceExpr=ProfitLCY[2];
                AutoFormatType=1;
                Editable=FALSE }

    { 75  ;2   ;Field     ;
                CaptionML=[ENU=Adjusted Profit (LCY);
                           ESP=Beneficio actualizado (DL)];
                SourceExpr=AdjProfitLCY[2];
                AutoFormatType=1;
                Editable=FALSE }

    { 66  ;2   ;Field     ;
                CaptionML=[ENU=Original Profit %;
                           ESP=% Bf§ original];
                DecimalPlaces=1:1;
                SourceExpr=ProfitPct[2];
                Editable=FALSE }

    { 79  ;2   ;Field     ;
                CaptionML=[ENU=Adjusted Profit %;
                           ESP=% beneficio actualizado];
                DecimalPlaces=1:1;
                SourceExpr=AdjProfitPct[2];
                Editable=FALSE }

    { 65  ;2   ;Field     ;
                CaptionML=[ENU=Quantity;
                           ESP=Cantidad];
                DecimalPlaces=0:6;
                SourceExpr=TotalSalesLine[2].Quantity;
                Editable=FALSE }

    { 62  ;2   ;Field     ;
                CaptionML=[ENU=Parcels;
                           ESP=Lotes];
                DecimalPlaces=0:6;
                SourceExpr=TotalSalesLine[2]."Units per Parcel";
                Editable=FALSE }

    { 64  ;2   ;Field     ;
                CaptionML=[ENU=Net Weight;
                           ESP=Peso neto];
                DecimalPlaces=0:6;
                SourceExpr=TotalSalesLine[2]."Net Weight";
                Editable=FALSE }

    { 63  ;2   ;Field     ;
                CaptionML=[ENU=Gross Weight;
                           ESP=Peso bruto];
                DecimalPlaces=0:6;
                SourceExpr=TotalSalesLine[2]."Gross Weight";
                Editable=FALSE }

    { 61  ;2   ;Field     ;
                CaptionML=[ENU=Volume;
                           ESP=Volumen];
                DecimalPlaces=0:6;
                SourceExpr=TotalSalesLine[2]."Unit Volume";
                Editable=FALSE }

    { 68  ;2   ;Field     ;
                CaptionML=[ENU=Original Cost (LCY);
                           ESP=Coste original (DL)];
                SourceExpr=TotalSalesLineLCY[2]."Unit Cost (LCY)";
                AutoFormatType=1;
                Editable=FALSE }

    { 17  ;2   ;Field     ;
                CaptionML=[ENU=Adjusted Cost (LCY);
                           ESP=Coste actualizado (DL)];
                SourceExpr=TotalAdjCostLCY[2];
                AutoFormatType=1;
                Editable=FALSE }

    { 122 ;2   ;Field     ;
                CaptionML=[ENU=Cost Adjmt. Amount (LCY);
                           ESP=Importe coste ajustado (DL)];
                SourceExpr=TotalAdjCostLCY[2] - TotalSalesLineLCY[2]."Unit Cost (LCY)";
                AutoFormatType=1;
                Editable=FALSE;
                OnLookup=BEGIN
                           LookupAdjmtValueEntries(1);
                         END;
                          }

    { 125 ;2   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=No. of VAT Lines;
                           ESP=N§ de l¡neas de IVA];
                SourceExpr=TempVATAmountLine2.COUNT;
                OnDrillDown=VAR
                              DrillDownForm@1000 : Form 576;
                            BEGIN
                              VATLinesDrillDown(TempVATAmountLine2,TRUE);
                              UpdateHeaderInfo(2, TempVATAmountLine2);
                            END;
                             }

    { 1906801201;1;Group  ;
                CaptionML=[ENU=Shipping;
                           ESP=Env¡o] }

    { 105 ;2   ;Field     ;
                SourceExpr=TotalSalesLine[3]."Line Amount";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text002,FALSE);
                Editable=FALSE }

    { 83  ;2   ;Field     ;
                CaptionML=[ENU=Inv. Discount Amount;
                           ESP=Importe dto. factura];
                SourceExpr=TotalSalesLine[3]."Inv. Discount Amount";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                Editable=FALSE }

    { 1100005;2;Field     ;
                CaptionML=[ENU=Pmt. Disc. Given Amount;
                           ESP=Dto. P.P.];
                SourceExpr=TotalSalesLine[3]."Pmt. Disc. Given Amount";
                AutoFormatType=1;
                Editable=FALSE }

    { 82  ;2   ;Field     ;
                SourceExpr=TotalAmount1[3];
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text001,FALSE);
                Editable=FALSE }

    { 81  ;2   ;Field     ;
                SourceExpr=VATAmount[3];
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=FORMAT (VATAmountText[3]);
                Editable=FALSE }

    { 80  ;2   ;Field     ;
                SourceExpr=TotalAmount2[3];
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text001,TRUE);
                Editable=FALSE }

    { 103 ;2   ;Field     ;
                CaptionML=[ENU=Sales (LCY);
                           ESP=Ventas (DL)];
                SourceExpr=TotalSalesLineLCY[3].Amount;
                AutoFormatType=1;
                Editable=FALSE }

    { 102 ;2   ;Field     ;
                CaptionML=[ENU=Cost (LCY);
                           ESP=Coste (DL)];
                SourceExpr=TotalSalesLineLCY[3]."Unit Cost (LCY)";
                AutoFormatType=1;
                Editable=FALSE }

    { 101 ;2   ;Field     ;
                CaptionML=[ENU=Profit (LCY);
                           ESP=Bf§ bruto (DL)];
                SourceExpr=ProfitLCY[3];
                AutoFormatType=1;
                Editable=FALSE }

    { 100 ;2   ;Field     ;
                CaptionML=[ENU=Profit %;
                           ESP=% Bf§ bruto];
                DecimalPlaces=1:1;
                SourceExpr=ProfitPct[3];
                Editable=FALSE }

    { 99  ;2   ;Field     ;
                CaptionML=[ENU=Quantity;
                           ESP=Cantidad];
                DecimalPlaces=0:6;
                SourceExpr=TotalSalesLine[3].Quantity;
                Editable=FALSE }

    { 94  ;2   ;Field     ;
                CaptionML=[ENU=Parcels;
                           ESP=Lotes];
                DecimalPlaces=0:6;
                SourceExpr=TotalSalesLine[3]."Units per Parcel";
                Editable=FALSE }

    { 98  ;2   ;Field     ;
                CaptionML=[ENU=Net Weight;
                           ESP=Peso neto];
                DecimalPlaces=0:6;
                SourceExpr=TotalSalesLine[3]."Net Weight";
                Editable=FALSE }

    { 97  ;2   ;Field     ;
                CaptionML=[ENU=Gross Weight;
                           ESP=Peso bruto];
                DecimalPlaces=0:6;
                SourceExpr=TotalSalesLine[3]."Gross Weight";
                Editable=FALSE }

    { 93  ;2   ;Field     ;
                CaptionML=[ENU=Volume;
                           ESP=Volumen];
                DecimalPlaces=0:6;
                SourceExpr=TotalSalesLine[3]."Unit Volume";
                Editable=FALSE }

    { 127 ;2   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=No. of VAT Lines;
                           ESP=N§ de l¡neas de IVA];
                SourceExpr=TempVATAmountLine3.COUNT;
                OnDrillDown=VAR
                              DrillDownForm@1000 : Form 576;
                            BEGIN
                              VATLinesDrillDown(TempVATAmountLine3,FALSE);
                            END;
                             }

    { 1900201301;1;Group  ;
                CaptionML=[ENU=Prepayment;
                           ESP=Prepago] }

    { 95  ;2   ;Field     ;
                SourceExpr=PrepmtTotalAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text006,FALSE);
                OnValidate=BEGIN
                             ActiveTab := ActiveTab::Prepayment;
                             UpdatePrepmtAmount;
                           END;
                            }

    { 115 ;2   ;Field     ;
                CaptionML=[ENU=Prepayment Amount Invoiced;
                           ESP=Importe prepago facturado];
                SourceExpr=PrepmtVATAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=FORMAT (PrepmtVATAmountText);
                Editable=FALSE }

    { 117 ;2   ;Field     ;
                SourceExpr=PrepmtTotalAmount2;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text006,TRUE);
                Editable=FALSE }

    { 109 ;2   ;Field     ;
                SourceExpr=TotalSalesLine[1]."Prepmt. Amt. Inv.";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text007,FALSE);
                Editable=FALSE }

    { 110 ;2   ;Field     ;
                ExtendedDatatype=Ratio;
                CaptionML=[ENU=Invoiced % of Prepayment Amt.;
                           ESP=% facturado de importe prepago];
                ToolTipML=[ENU=Invoiced % of Prepayment Amt.;
                           ESP=% facturado de importe prepago];
                SourceExpr=PrepmtInvPct }

    { 107 ;2   ;Field     ;
                SourceExpr=TotalSalesLine[1]."Prepmt Amt Deducted";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text008,FALSE);
                Editable=FALSE }

    { 108 ;2   ;Field     ;
                ExtendedDatatype=Ratio;
                CaptionML=[ENU=Deducted % of Prepayment Amt. to Deduct;
                           ESP=% descontado del importe prepago que se debe descontar];
                ToolTipML=[ENU=Deducted % of Prepayment Amt. to Deduct;
                           ESP=% descontado del importe prepago que se debe descontar];
                SourceExpr=PrepmtDeductedPct }

    { 96  ;2   ;Field     ;
                SourceExpr=TotalSalesLine[1]."Prepmt Amt to Deduct";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text009,FALSE);
                Editable=FALSE }

    { 130 ;2   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=No. of VAT Lines;
                           ESP=N§ de l¡neas de IVA];
                SourceExpr=TempVATAmountLine4.COUNT;
                OnDrillDown=VAR
                              DrillDownForm@1000 : Form 576;
                            BEGIN
                              VATLinesDrillDown(TempVATAmountLine4,TRUE);
                            END;
                             }

    { 1903289601;1;Group  ;
                CaptionML=[ENU=Customer;
                           ESP=Cliente] }

    { 26  ;2   ;Field     ;
                CaptionML=[ENU=Balance (LCY);
                           ESP=Saldo (DL)];
                SourceExpr=Cust."Balance (LCY)";
                AutoFormatType=1;
                Editable=FALSE }

    { 28  ;2   ;Field     ;
                CaptionML=[ENU=Credit Limit (LCY);
                           ESP=Cr‚dito m ximo (DL)];
                SourceExpr=Cust."Credit Limit (LCY)";
                AutoFormatType=1;
                Editable=FALSE }

    { 36  ;2   ;Field     ;
                ExtendedDatatype=Ratio;
                CaptionML=[ENU=Expended % of Credit Limit (LCY);
                           ESP=% L¡mite cr‚dito usado (DL)];
                ToolTipML=[ENU=Expended % of Credit Limit (LCY);
                           ESP=% L¡mite cr‚dito usado (DL)];
                SourceExpr=CreditLimitLCYExpendedPct }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Sales %1 Statistics;ESP=Estad¡sticas %1 ventas';
      Text001@1001 : TextConst 'ENU=Total;ESP=Total';
      Text002@1002 : TextConst 'ENU=Amount;ESP=Importe';
      Text003@1003 : TextConst 'ENU=%1 must not be 0.;ESP=%1 no debe ser 0.';
      Text004@1004 : TextConst 'ENU=%1 must not be greater than %2.;ESP=%1 no debe ser m s grande de %2';
      Text005@1005 : TextConst 'ENU=You cannot change the invoice discount because there is a %1 record for %2 %3.;ESP=No puede cambiar el dto. factura porque hay un %1 registro para %2 %3.';
      TotalSalesLine@1006 : ARRAY [3] OF Record 37;
      TotalSalesLineLCY@1007 : ARRAY [3] OF Record 37;
      Cust@1008 : Record 18;
      TempVATAmountLine1@1009 : TEMPORARY Record 290;
      TempVATAmountLine2@1010 : TEMPORARY Record 290;
      TempVATAmountLine3@1011 : TEMPORARY Record 290;
      TempVATAmountLine4@1034 : TEMPORARY Record 290;
      SalesSetup@1012 : Record 311;
      SalesPost@1013 : Codeunit 80;
      TotalAmount1@1014 : ARRAY [3] OF Decimal;
      TotalAmount2@1015 : ARRAY [3] OF Decimal;
      VATAmount@1016 : ARRAY [3] OF Decimal;
      PrepmtTotalAmount@1037 : Decimal;
      PrepmtVATAmount@1038 : Decimal;
      PrepmtTotalAmount2@1040 : Decimal;
      VATAmountText@1017 : ARRAY [3] OF Text[30];
      PrepmtVATAmountText@1039 : Text[30];
      ProfitLCY@1018 : ARRAY [3] OF Decimal;
      ProfitPct@1019 : ARRAY [3] OF Decimal;
      AdjProfitLCY@1031 : ARRAY [3] OF Decimal;
      AdjProfitPct@1030 : ARRAY [3] OF Decimal;
      TotalAdjCostLCY@1029 : ARRAY [3] OF Decimal;
      CreditLimitLCYExpendedPct@1020 : Decimal;
      PrepmtInvPct@1036 : Decimal;
      PrepmtDeductedPct@1035 : Decimal;
      i@1021 : Integer;
      PrevNo@1022 : Code[20];
      ActiveTab@1023 : 'General,Invoicing,Shipping,Prepayment';
      PrevTab@1024 : 'General,Invoicing,Shipping,Prepayment';
      VATLinesFormIsEditable@1026 : Boolean;
      AllowInvDisc@1027 : Boolean;
      AllowVATDifference@1028 : Boolean;
      ValidAdjCost@1032 : ARRAY [3] OF Boolean;
      Text006@1033 : TextConst 'ENU=Prepmt. Amount;ESP=Importe prepago';
      Text007@1041 : TextConst 'ENU=Prepmt. Amt. Invoiced;ESP=Importe prepago facturado';
      Text008@1042 : TextConst 'ENU=Prepmt. Amt. Deducted;ESP=Importe prepago descontado';
      Text009@1043 : TextConst 'ENU=Prepmt. Amt. to Deduct;ESP=Importe prepago para descontar';
      VATLinesForm@1044 : Form 9401;

    LOCAL PROCEDURE UpdateHeaderInfo@5(IndexNo@1000 : Integer;VAR VATAmountLine@1001 : Record 290);
    VAR
      CurrExchRate@1002 : Record 330;
      UseDate@1003 : Date;
    BEGIN
      TotalSalesLine[IndexNo]."Inv. Discount Amount" := VATAmountLine.GetTotalInvDiscAmount;
      TotalAmount1[IndexNo] :=
        TotalSalesLine[IndexNo]."Line Amount" - TotalSalesLine[IndexNo]."Inv. Discount Amount" -
        TotalSalesLine[IndexNo]."Pmt. Disc. Given Amount";
      VATAmount[IndexNo] := VATAmountLine.GetTotalVATAmount;
      IF "Prices Including VAT" THEN BEGIN
        TotalAmount1[IndexNo] := VATAmountLine.GetTotalAmountInclVAT;
        TotalAmount2[IndexNo] := TotalAmount1[IndexNo] - VATAmount[IndexNo];
        TotalSalesLine[IndexNo]."Line Amount" :=
          TotalAmount1[IndexNo] + TotalSalesLine[IndexNo]."Inv. Discount Amount"
          + TotalSalesLine[IndexNo]."Pmt. Disc. Given Amount";
      END ELSE
        TotalAmount2[IndexNo] := TotalAmount1[IndexNo] + VATAmount[IndexNo];

      IF "Prices Including VAT" THEN
        TotalSalesLineLCY[IndexNo].Amount := TotalAmount2[IndexNo]
      ELSE
        TotalSalesLineLCY[IndexNo].Amount := TotalAmount1[IndexNo];
      IF "Currency Code" <> '' THEN
        IF ("Document Type" IN ["Document Type"::"Blanket Order","Document Type"::Quote]) AND
           ("Posting Date" = 0D)
        THEN
          UseDate := WORKDATE
        ELSE
          UseDate := "Posting Date";

      TotalSalesLineLCY[IndexNo].Amount :=
        CurrExchRate.ExchangeAmtFCYToLCY(
          UseDate,"Currency Code",TotalSalesLineLCY[IndexNo].Amount,"Currency Factor");

      ProfitLCY[IndexNo] := TotalSalesLineLCY[IndexNo].Amount - TotalSalesLineLCY[IndexNo]."Unit Cost (LCY)";
      IF TotalSalesLineLCY[IndexNo].Amount = 0 THEN
        ProfitPct[IndexNo] := 0
      ELSE
        ProfitPct[IndexNo] := ROUND(100 * ProfitLCY[IndexNo] / TotalSalesLineLCY[IndexNo].Amount,0.01);

      AdjProfitLCY[IndexNo] := TotalSalesLineLCY[IndexNo].Amount - TotalAdjCostLCY[IndexNo];
      IF TotalSalesLineLCY[IndexNo].Amount = 0 THEN
        AdjProfitPct[IndexNo] := 0
      ELSE
        AdjProfitPct[IndexNo] := ROUND(100 * AdjProfitLCY[IndexNo] / TotalSalesLineLCY[IndexNo].Amount,0.01);
    END;

    LOCAL PROCEDURE GetVATSpecification@21(QtyType@1000 : 'General,Invoicing,Shipping');
    BEGIN
      CASE QtyType OF
        QtyType::General:
          BEGIN
            VATLinesForm.GetTempVATAmountLine(TempVATAmountLine1);
            UpdateHeaderInfo(1,TempVATAmountLine1);
          END;
        QtyType::Invoicing:
          BEGIN
            VATLinesForm.GetTempVATAmountLine(TempVATAmountLine2);
            UpdateHeaderInfo(2,TempVATAmountLine2);
          END;
        QtyType::Shipping:
          VATLinesForm.GetTempVATAmountLine(TempVATAmountLine3);
      END;
    END;

    LOCAL PROCEDURE UpdateTotalAmount@16(IndexNo@1000 : Integer);
    VAR
      SaveTotalAmount@1001 : Decimal;
    BEGIN
      CheckAllowInvDisc;
      IF "Prices Including VAT" THEN BEGIN
        SaveTotalAmount := TotalAmount1[IndexNo];
        UpdateInvDiscAmount(IndexNo);
        TotalAmount1[IndexNo] := SaveTotalAmount;
      END;

      WITH TotalSalesLine[IndexNo] DO
        "Inv. Discount Amount" := "Line Amount" - TotalAmount1[IndexNo];
      UpdateInvDiscAmount(IndexNo);
    END;

    LOCAL PROCEDURE UpdateInvDiscAmount@3(ModifiedIndexNo@1000 : Integer);
    VAR
      PartialInvoicing@1001 : Boolean;
      MaxIndexNo@1002 : Integer;
      IndexNo@1003 : ARRAY [2] OF Integer;
      i@1004 : Integer;
      InvDiscBaseAmount@1005 : Decimal;
    BEGIN
      CheckAllowInvDisc;
      IF NOT (ModifiedIndexNo IN [1,2]) THEN
        EXIT;

      IF ModifiedIndexNo = 1 THEN
        InvDiscBaseAmount := TempVATAmountLine1.GetTotalInvDiscBaseAmount(FALSE,"Currency Code")
      ELSE
        InvDiscBaseAmount := TempVATAmountLine2.GetTotalInvDiscBaseAmount(FALSE,"Currency Code");

      IF InvDiscBaseAmount = 0 THEN
        ERROR(Text003,TempVATAmountLine2.FIELDCAPTION("Inv. Disc. Base Amount"));

      IF TotalSalesLine[ModifiedIndexNo]."Inv. Discount Amount" / InvDiscBaseAmount > 1 THEN
        ERROR(
          Text004,
          TotalSalesLine[ModifiedIndexNo].FIELDCAPTION("Inv. Discount Amount"),
          TempVATAmountLine2.FIELDCAPTION("Inv. Disc. Base Amount"));

      PartialInvoicing := (TotalSalesLine[1]."Line Amount" <> TotalSalesLine[2]."Line Amount");

      IndexNo[1] := ModifiedIndexNo;
      IndexNo[2] := 3 - ModifiedIndexNo;
      IF (ModifiedIndexNo = 2) AND PartialInvoicing THEN
        MaxIndexNo := 1
      ELSE
        MaxIndexNo := 2;

      IF NOT PartialInvoicing THEN
        IF ModifiedIndexNo = 1 THEN
          TotalSalesLine[2]."Inv. Discount Amount" := TotalSalesLine[1]."Inv. Discount Amount"
        ELSE
          TotalSalesLine[1]."Inv. Discount Amount" := TotalSalesLine[2]."Inv. Discount Amount";

      FOR i := 1 TO MaxIndexNo DO BEGIN
        WITH TotalSalesLine[IndexNo[i]] DO BEGIN
          IF (i = 1) OR NOT PartialInvoicing THEN
            IF IndexNo[i] = 1 THEN BEGIN
              TempVATAmountLine1.SetInvoiceDiscountAmount(
                "Inv. Discount Amount","Currency Code","Prices Including VAT","VAT Base Discount %");
            END ELSE BEGIN
              TempVATAmountLine2.SetInvoiceDiscountAmount(
                "Inv. Discount Amount","Currency Code","Prices Including VAT","VAT Base Discount %");
            END;

          IF (i = 2) AND PartialInvoicing THEN
            IF IndexNo[i] = 1 THEN BEGIN
              InvDiscBaseAmount := TempVATAmountLine2.GetTotalInvDiscBaseAmount(FALSE,"Currency Code");
              IF InvDiscBaseAmount = 0 THEN
                TempVATAmountLine1.SetInvoiceDiscountPercent(
                  0,"Currency Code","Prices Including VAT",FALSE,"VAT Base Discount %")
              ELSE
                TempVATAmountLine1.SetInvoiceDiscountPercent(
                  100 * TempVATAmountLine2.GetTotalInvDiscAmount / InvDiscBaseAmount,
                  "Currency Code","Prices Including VAT",FALSE,"VAT Base Discount %");
            END ELSE BEGIN
              InvDiscBaseAmount := TempVATAmountLine1.GetTotalInvDiscBaseAmount(FALSE,"Currency Code");
              IF InvDiscBaseAmount = 0 THEN
                TempVATAmountLine2.SetInvoiceDiscountPercent(
                  0,"Currency Code","Prices Including VAT",FALSE,"VAT Base Discount %")
              ELSE
                TempVATAmountLine2.SetInvoiceDiscountPercent(
                  100 * TempVATAmountLine1.GetTotalInvDiscAmount / InvDiscBaseAmount,
                  "Currency Code","Prices Including VAT",FALSE,"VAT Base Discount %");
            END;
        END;
      END;

      UpdateHeaderInfo(1,TempVATAmountLine1);
      UpdateHeaderInfo(2,TempVATAmountLine2);

      IF ModifiedIndexNo = 1 THEN
        VATLinesForm.SetTempVATAmountLine(TempVATAmountLine1)
      ELSE
        VATLinesForm.SetTempVATAmountLine(TempVATAmountLine2);

      "Invoice Discount Calculation" := "Invoice Discount Calculation"::Amount;
      "Invoice Discount Value" := TotalSalesLine[1]."Inv. Discount Amount";
      MODIFY;

      UpdateVATOnSalesLines;
    END;

    LOCAL PROCEDURE UpdatePrepmtAmount@9();
    VAR
      TempSalesLine@1001 : TEMPORARY Record 37;
      SalesPostPrepmt@1000 : Codeunit 442;
    BEGIN
      SalesPostPrepmt.UpdatePrepmtAmountOnSaleslines(Rec,PrepmtTotalAmount);
      SalesPostPrepmt.GetSalesLines(Rec,0,TempSalesLine);
      SalesPostPrepmt.SumPrepmt(
        Rec,TempSalesLine,TempVATAmountLine4,PrepmtTotalAmount,PrepmtVATAmount,PrepmtVATAmountText);
      PrepmtInvPct :=
        Pct(TotalSalesLine[1]."Prepmt. Amt. Inv.",PrepmtTotalAmount);
      PrepmtDeductedPct :=
        Pct(TotalSalesLine[1]."Prepmt Amt Deducted",TotalSalesLine[1]."Prepmt. Amt. Inv.");
      IF "Prices Including VAT" THEN BEGIN
        PrepmtTotalAmount2 := PrepmtTotalAmount;
        PrepmtTotalAmount := PrepmtTotalAmount + PrepmtVATAmount;
      END ELSE
        PrepmtTotalAmount2 := PrepmtTotalAmount + PrepmtVATAmount;
      MODIFY;
    END;

    LOCAL PROCEDURE GetCaptionClass@2(FieldCaption@1000 : Text[100];ReverseCaption@1001 : Boolean) : Text[80];
    BEGIN
      IF "Prices Including VAT" XOR ReverseCaption THEN
        EXIT('2,1,' + FieldCaption);
      EXIT('2,0,' + FieldCaption);
    END;

    LOCAL PROCEDURE UpdateVATOnSalesLines@1();
    VAR
      SalesLine@1000 : Record 37;
    BEGIN
      GetVATSpecification(ActiveTab);
      IF TempVATAmountLine1.GetAnyLineModified THEN
        SalesLine.UpdateVATOnLines(0,Rec,SalesLine,TempVATAmountLine1);
      IF TempVATAmountLine2.GetAnyLineModified THEN
        SalesLine.UpdateVATOnLines(1,Rec,SalesLine,TempVATAmountLine2);
      PrevNo := '';
    END;

    LOCAL PROCEDURE CustInvDiscRecExists@4(InvDiscCode@1000 : Code[20]) : Boolean;
    VAR
      CustInvDisc@1001 : Record 19;
    BEGIN
      CustInvDisc.SETRANGE(Code,InvDiscCode);
      EXIT(CustInvDisc.FIND('-'));
    END;

    LOCAL PROCEDURE CheckAllowInvDisc@8();
    VAR
      CustInvDisc@1000 : Record 19;
    BEGIN
      IF NOT AllowInvDisc THEN
        ERROR(
          Text005,
          CustInvDisc.TABLECAPTION,FIELDCAPTION("Invoice Disc. Code"),"Invoice Disc. Code");
    END;

    LOCAL PROCEDURE Pct@6(Numerator@1001 : Decimal;Denominator@1000 : Decimal) : Decimal;
    BEGIN
      IF Denominator = 0 THEN
        EXIT(0);
      EXIT(ROUND(Numerator / Denominator * 10000,1));
    END;

    PROCEDURE VATLinesDrillDown@7(VAR VATLinesToDrillDown@1000 : Record 290;ThisTabAllowsVATEditing@1001 : Boolean);
    BEGIN
      CLEAR(VATLinesForm);
      VATLinesForm.SetTempVATAmountLine(VATLinesToDrillDown);
      VATLinesForm.InitGlobals(
        "Currency Code",AllowVATDifference,AllowVATDifference AND ThisTabAllowsVATEditing,
        "Prices Including VAT",AllowInvDisc,"VAT Base Discount %");
      VATLinesForm.RUNMODAL;
      VATLinesForm.GetTempVATAmountLine(VATLinesToDrillDown);
    END;

    LOCAL PROCEDURE TotalAmount21OnAfterValidate@19074760();
    BEGIN
      WITH TotalSalesLine[1] DO BEGIN
        IF "Prices Including VAT" THEN
          "Inv. Discount Amount" := "Line Amount" - "Amount Including VAT"
        ELSE
          "Inv. Discount Amount" := "Line Amount" - Amount;
      END;
        UpdateInvDiscAmount(1);
    END;

    BEGIN
    END.
  }
}
