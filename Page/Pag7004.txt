OBJECT Page 7004 Sales Line Discounts
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Sales Line Discounts;
               ESP=Descuentos l¡nea ventas];
    SaveValues=Yes;
    SourceTable=Table7004;
    DataCaptionExpr=GetCaption;
    DelayedInsert=Yes;
    PageType=Worksheet;
    OnInit=BEGIN
             CodeFilterCtrlEnable := TRUE;
             SalesCodeFilterCtrlEnable := TRUE;
             "Sales CodeEditable" := TRUE;
           END;

    OnOpenPage=BEGIN
                 GetRecFilters;
                 SetRecFilters;
               END;

    OnAfterGetRecord=BEGIN
                       OnAfterGetCurrRecord;
                     END;

    OnNewRecord=BEGIN
                  OnAfterGetCurrRecord;
                END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 20  ;1   ;Group     ;
                CaptionML=[ENU=General;
                           ESP=General] }

    { 28  ;2   ;Field     ;
                CaptionML=[ENU=Sales Type Filter;
                           ESP=Filtro tipo ventas];
                OptionCaptionML=[ENU=Customer,Customer Discount Group,All Customers,Campaign,None;
                                 ESP=Cliente,Grupo descuento cliente,Todos los clientes,Campa¤a,Ninguno];
                SourceExpr=SalesTypeFilter;
                OnValidate=BEGIN
                             SalesTypeFilterOnAfterValidate;
                           END;
                            }

    { 26  ;2   ;Field     ;
                Name=SalesCodeFilterCtrl;
                CaptionML=[ENU=Sales Code Filter;
                           ESP=Filtro c¢d. ventas];
                SourceExpr=SalesCodeFilter;
                Enabled=SalesCodeFilterCtrlEnable;
                OnValidate=BEGIN
                             SalesCodeFilterOnAfterValidate;
                           END;

                OnLookup=VAR
                           CustList@1005 : Form 22;
                           CustDiscGrList@1004 : Form 512;
                           CampaignList@1003 : Form 5087;
                           ItemList@1002 : Form 31;
                         BEGIN
                           IF SalesTypeFilter = SalesTypeFilter::"All Customers" THEN EXIT;

                           CASE SalesTypeFilter OF
                             SalesTypeFilter::Customer:
                               BEGIN
                                 CustList.LOOKUPMODE := TRUE;
                                 IF CustList.RUNMODAL = ACTION::LookupOK THEN
                                   Text := CustList.GetSelectionFilter
                                 ELSE
                                   EXIT(FALSE);
                               END;
                             SalesTypeFilter::"Customer Discount Group":
                               BEGIN
                                 CustDiscGrList.LOOKUPMODE := TRUE;
                                 IF CustDiscGrList.RUNMODAL = ACTION::LookupOK THEN
                                   Text := CustDiscGrList.GetSelectionFilter
                                 ELSE
                                   EXIT(FALSE);
                               END;
                             SalesTypeFilter::Campaign:
                               BEGIN
                                 CampaignList.LOOKUPMODE := TRUE;
                                 IF CampaignList.RUNMODAL = ACTION::LookupOK THEN
                                   Text := CampaignList.GetSelectionFilter
                                 ELSE
                                   EXIT(FALSE);
                               END;
                           END;

                           EXIT(TRUE);
                         END;
                          }

    { 34  ;2   ;Field     ;
                CaptionML=[ENU=Starting Date Filter;
                           ESP=Filtro fecha inicio];
                SourceExpr=StartingDateFilter;
                OnValidate=VAR
                             ApplicationMgt@1000 : Codeunit 1;
                           BEGIN
                             IF ApplicationMgt.MakeDateFilter(StartingDateFilter) = 0 THEN;
                               StartingDateFilterOnAfterValid;
                           END;
                            }

    { 10  ;2   ;Field     ;
                CaptionML=[ENU=Type Filter;
                           ESP=Filtro tipo];
                OptionCaptionML=[ENU=Item,Item Discount Group,None;
                                 ESP=Producto,Grupo dto. producto,Ninguno];
                SourceExpr=ItemTypeFilter;
                OnValidate=BEGIN
                             ItemTypeFilterOnAfterValidate;
                           END;
                            }

    { 40  ;2   ;Field     ;
                Name=CodeFilterCtrl;
                CaptionML=[ENU=Code Filter;
                           ESP=Filtro C¢digo];
                SourceExpr=CodeFilter;
                Enabled=CodeFilterCtrlEnable;
                OnValidate=BEGIN
                             CodeFilterOnAfterValidate;
                           END;

                OnLookup=VAR
                           ItemList@1002 : Form 31;
                           ItemDiscGrList@1003 : Form 513;
                         BEGIN
                           CASE Type OF
                             Type::Item:
                               BEGIN
                                 ItemList.LOOKUPMODE := TRUE;
                                 IF ItemList.RUNMODAL = ACTION::LookupOK THEN
                                   Text := ItemList.GetSelectionFilter
                                 ELSE
                                   EXIT(FALSE);
                               END;
                             Type::"Item Disc. Group":
                               BEGIN
                                 ItemDiscGrList.LOOKUPMODE := TRUE;
                                 IF ItemDiscGrList.RUNMODAL = ACTION::LookupOK THEN
                                   Text := ItemDiscGrList.GetSelectionFilter
                                 ELSE
                                   EXIT(FALSE);
                               END;
                           END;

                           EXIT(TRUE);
                         END;
                          }

    { 16  ;2   ;Field     ;
                Name=SalesCodeFilterCtrl;
                CaptionML=[ENU=Currency Code Filter;
                           ESP=Filtro c¢d. divisa];
                SourceExpr=CurrencyCodeFilter;
                OnValidate=BEGIN
                             CurrencyCodeFilterOnAfterValid;
                           END;

                OnLookup=VAR
                           CurrencyList@1000 : Form 5;
                         BEGIN
                           CurrencyList.LOOKUPMODE := TRUE;
                           IF CurrencyList.RUNMODAL = ACTION::LookupOK THEN
                             Text := CurrencyList.GetSelectionFilter
                           ELSE
                             EXIT(FALSE);

                           EXIT(TRUE);
                         END;
                          }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 32  ;2   ;Field     ;
                SourceExpr="Sales Type" }

    { 18  ;2   ;Field     ;
                SourceExpr="Sales Code";
                Editable="Sales CodeEditable" }

    { 42  ;2   ;Field     ;
                SourceExpr=Type }

    { 4   ;2   ;Field     ;
                SourceExpr=Code }

    { 30  ;2   ;Field     ;
                SourceExpr="Variant Code";
                Visible=FALSE }

    { 14  ;2   ;Field     ;
                SourceExpr="Currency Code";
                Visible=FALSE }

    { 8   ;2   ;Field     ;
                SourceExpr="Unit of Measure Code" }

    { 12  ;2   ;Field     ;
                SourceExpr="Minimum Quantity" }

    { 6   ;2   ;Field     ;
                SourceExpr="Line Discount %" }

    { 36  ;2   ;Field     ;
                SourceExpr="Starting Date" }

    { 2   ;2   ;Field     ;
                SourceExpr="Ending Date" }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      Cust@1001 : Record 18;
      CustDiscGr@1005 : Record 340;
      Campaign@1008 : Record 5071;
      Item@1004 : Record 27;
      ItemDiscGr@1013 : Record 341;
      SalesTypeFilter@1000 : 'Customer,Customer Discount Group,All Customers,Campaign,None';
      SalesCodeFilter@1003 : Text[250];
      ItemTypeFilter@1012 : 'Item,Item Discount Group,None';
      CodeFilter@1011 : Text[250];
      StartingDateFilter@1006 : Text[30];
      Text000@1007 : TextConst 'ENU=All Customers;ESP=Todos clientes';
      CurrencyCodeFilter@1002 : Text[250];
      "Sales CodeEditable"@19031640 : Boolean INDATASET;
      SalesCodeFilterCtrlEnable@19013164 : Boolean INDATASET;
      CodeFilterCtrlEnable@19026169 : Boolean INDATASET;

    PROCEDURE GetRecFilters@2();
    BEGIN
      IF GETFILTERS <> '' THEN BEGIN
        IF GETFILTER("Sales Type") <> '' THEN
          SalesTypeFilter := "Sales Type"
        ELSE
          SalesTypeFilter := SalesTypeFilter::None;

        IF GETFILTER(Type) <> '' THEN
          ItemTypeFilter := Type
        ELSE
          ItemTypeFilter := ItemTypeFilter::None;

        SalesCodeFilter := GETFILTER("Sales Code");
        CodeFilter := GETFILTER(Code);
        CurrencyCodeFilter := GETFILTER("Currency Code");
        EVALUATE(StartingDateFilter,GETFILTER("Starting Date"));
      END;
    END;

    PROCEDURE SetRecFilters@1();
    BEGIN
      SalesCodeFilterCtrlEnable := TRUE;
      CodeFilterCtrlEnable := TRUE;

      IF SalesTypeFilter <> SalesTypeFilter::None THEN
        SETRANGE("Sales Type",SalesTypeFilter)
      ELSE
        SETRANGE("Sales Type");

      IF SalesTypeFilter IN [SalesTypeFilter::"All Customers",SalesTypeFilter::None] THEN BEGIN
        SalesCodeFilterCtrlEnable := FALSE;
        SalesCodeFilter := '';
      END;

      IF SalesCodeFilter <> '' THEN
        SETFILTER("Sales Code",SalesCodeFilter)
      ELSE
        SETRANGE("Sales Code");

      IF ItemTypeFilter <> ItemTypeFilter::None THEN
        SETRANGE(Type,ItemTypeFilter)
      ELSE
        SETRANGE(Type);

      IF ItemTypeFilter = ItemTypeFilter::None THEN BEGIN
        CodeFilterCtrlEnable := FALSE;
        CodeFilter := '';
      END;

      IF CodeFilter <> '' THEN BEGIN
        SETFILTER(Code,CodeFilter);
      END ELSE
        SETRANGE(Code);

      IF CurrencyCodeFilter <> '' THEN BEGIN
        SETFILTER("Currency Code",CurrencyCodeFilter);
      END ELSE
        SETRANGE("Currency Code");

      IF StartingDateFilter <> '' THEN
        SETFILTER("Starting Date",StartingDateFilter)
      ELSE
        SETRANGE("Starting Date");

      CurrPage.UPDATE(FALSE);
    END;

    PROCEDURE GetCaption@3() : Text[250];
    VAR
      ObjTransl@1000 : Record 377;
      SourceTableName@1002 : Text[100];
      SalesSrcTableName@1003 : Text[100];
      Description@1001 : Text[250];
    BEGIN
      GetRecFilters;
      "Sales CodeEditable" := "Sales Type" <> "Sales Type"::"All Customers";

      SourceTableName := '';
      CASE ItemTypeFilter OF
        ItemTypeFilter::Item:
          BEGIN
            SourceTableName := ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,27);
            Item."No." := CodeFilter;
          END;
        ItemTypeFilter::"Item Discount Group":
          BEGIN
            SourceTableName := ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,341);
            ItemDiscGr.Code := CodeFilter;
          END;
      END;

      SalesSrcTableName := '';
      CASE SalesTypeFilter OF
        SalesTypeFilter::Customer:
          BEGIN
            SalesSrcTableName := ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,18);
            Cust."No." := SalesCodeFilter;
            IF Cust.FIND THEN
              Description := Cust.Name;
          END;
        SalesTypeFilter::"Customer Discount Group":
          BEGIN
            SalesSrcTableName := ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,340);
            CustDiscGr.Code := SalesCodeFilter;
            IF CustDiscGr.FIND THEN
              Description := CustDiscGr.Description;
          END;
        SalesTypeFilter::Campaign:
          BEGIN
            SalesSrcTableName := ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,5071);
            Campaign."No." := SalesCodeFilter;
            IF Campaign.FIND THEN
              Description := Campaign.Description;
          END;

        SalesTypeFilter::"All Customers":
          BEGIN
            SalesSrcTableName := Text000;
            Description := '';
          END;
      END;

      IF SalesSrcTableName = Text000 THEN
        EXIT(STRSUBSTNO('%1 %2 %3 %4 %5',SalesSrcTableName,SalesCodeFilter,Description,SourceTableName,CodeFilter));
      EXIT(STRSUBSTNO('%1 %2 %3 %4 %5',SalesSrcTableName,SalesCodeFilter,Description,SourceTableName,CodeFilter));
    END;

    LOCAL PROCEDURE SalesCodeFilterOnAfterValidate@19067727();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    LOCAL PROCEDURE SalesTypeFilterOnAfterValidate@19024044();
    BEGIN
      CurrPage.SAVERECORD;
      SalesCodeFilter := '';
      SetRecFilters;
    END;

    LOCAL PROCEDURE StartingDateFilterOnAfterValid@19055286();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    LOCAL PROCEDURE ItemTypeFilterOnAfterValidate@19002579();
    BEGIN
      CurrPage.SAVERECORD;
      CodeFilter := '';
      SetRecFilters;
    END;

    LOCAL PROCEDURE CodeFilterOnAfterValidate@19025728();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    LOCAL PROCEDURE CurrencyCodeFilterOnAfterValid@19015290();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    LOCAL PROCEDURE OnAfterGetCurrRecord@19077479();
    BEGIN
      xRec := Rec;
      "Sales CodeEditable" := "Sales Type" <> "Sales Type"::"All Customers";
    END;

    BEGIN
    END.
  }
}
