OBJECT Page 350 G/L Acc. Balance/Budget Lines
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Lines;
               ESP=L¡neas];
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table2000000007;
    PageType=ListPart;
    OnOpenPage=BEGIN
                 RESET;
               END;

    OnFindRecord=BEGIN
                   EXIT(PeriodFormMgt.FindDate(Which,Rec,GLPeriodLength));
                 END;

    OnNextRecord=BEGIN
                   EXIT(PeriodFormMgt.NextDate(Steps,Rec,GLPeriodLength));
                 END;

    OnAfterGetRecord=BEGIN
                       SetDateFilter;
                       CalcFormFields;
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 14  ;2   ;Field     ;
                CaptionML=[ENU=Period Start;
                           ESP=Inicio periodo];
                SourceExpr="Period Start";
                Editable=FALSE }

    { 16  ;2   ;Field     ;
                CaptionML=[ENU=Period Name;
                           ESP=Nombre periodo];
                SourceExpr="Period Name";
                Editable=FALSE }

    { 4   ;2   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=Debit Amount;
                           ESP=Importe debe];
                BlankNumbers=BlankNegAndZero;
                SourceExpr=GLAcc."Debit Amount";
                AutoFormatType=1;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              BalanceDrillDown;
                            END;
                             }

    { 2   ;2   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=Credit Amount;
                           ESP=Importe haber];
                BlankNumbers=BlankNegAndZero;
                SourceExpr=GLAcc."Credit Amount";
                AutoFormatType=1;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              BalanceDrillDown;
                            END;
                             }

    { 10  ;2   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=Net Change;
                           ESP=Saldo periodo];
                BlankZero=Yes;
                SourceExpr=GLAcc."Net Change";
                AutoFormatType=1;
                Visible=FALSE;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              BalanceDrillDown;
                            END;
                             }

    { 6   ;2   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=Budgeted Debit Amount;
                           ESP=Debe presupuestado];
                BlankNumbers=BlankNegAndZero;
                SourceExpr=GLAcc."Budgeted Debit Amount";
                AutoFormatType=1;
                OnValidate=BEGIN
                             SetDateFilter;
                             GLAcc.VALIDATE("Budgeted Debit Amount");
                             CalcFormFields;
                           END;

                OnDrillDown=BEGIN
                              BudgetDrillDown;
                            END;
                             }

    { 8   ;2   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=Budgeted Credit Amount;
                           ESP=Haber presupuestado];
                BlankNumbers=BlankNegAndZero;
                SourceExpr=GLAcc."Budgeted Credit Amount";
                AutoFormatType=1;
                OnValidate=BEGIN
                             SetDateFilter;
                             GLAcc.VALIDATE("Budgeted Credit Amount");
                             CalcFormFields;
                           END;

                OnDrillDown=BEGIN
                              BudgetDrillDown;
                            END;
                             }

    { 18  ;2   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=Budgeted Amount;
                           ESP=Importe pptdo.];
                BlankZero=Yes;
                SourceExpr=GLAcc."Budgeted Amount";
                AutoFormatType=1;
                Visible=FALSE;
                OnValidate=BEGIN
                             SetDateFilter;
                             GLAcc.VALIDATE("Budgeted Amount");
                             CalcFormFields;
                           END;

                OnDrillDown=BEGIN
                              BudgetDrillDown;
                            END;
                             }

    { 12  ;2   ;Field     ;
                CaptionML=[ENU=Balance/Budget (%);
                           ESP=% Saldo/Presupuesto];
                DecimalPlaces=1:1;
                BlankZero=Yes;
                SourceExpr=BudgetPct;
                Editable=FALSE }

  }
  CODE
  {
    VAR
      AccountingPeriod@1000 : Record 50;
      GLAcc@1001 : Record 15;
      PeriodFormMgt@1002 : Codeunit 359;
      GLPeriodLength@1003 : 'Day,Week,Month,Quarter,Year,Accounting Period';
      BudgetPct@1004 : Decimal;
      AmountType@1005 : 'Net Change,Balance at Date';
      ClosingEntryFilter@1006 : 'Include,Exclude';

    PROCEDURE Set@1(VAR NewGLAcc@1000 : Record 15;NewGLPeriodLength@1001 : Integer;NewAmountType@1002 : 'Net Change,Balance';NewClosingEntryFilter@1003 : 'Include,Exclude');
    BEGIN
      GLAcc.COPY(NewGLAcc);
      GLPeriodLength := NewGLPeriodLength;
      AmountType := NewAmountType;
      ClosingEntryFilter := NewClosingEntryFilter;
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE BalanceDrillDown@3();
    VAR
      GLEntry@1000 : Record 17;
    BEGIN
      SetDateFilter;
      GLEntry.RESET;
      GLEntry.SETCURRENTKEY("G/L Account No.","Posting Date");
      GLEntry.SETRANGE("G/L Account No.",GLAcc."No.");
      IF GLAcc.Totaling <> '' THEN
        GLEntry.SETFILTER("G/L Account No.",GLAcc.Totaling);
      GLEntry.SETFILTER("Posting Date",GLAcc.GETFILTER("Date Filter"));
      GLEntry.SETFILTER("Global Dimension 1 Code",GLAcc.GETFILTER("Global Dimension 1 Filter"));
      GLEntry.SETFILTER("Global Dimension 2 Code",GLAcc.GETFILTER("Global Dimension 2 Filter"));
      GLEntry.SETFILTER("Business Unit Code",GLAcc.GETFILTER("Business Unit Filter"));
      FORM.RUN(0,GLEntry);
    END;

    LOCAL PROCEDURE BudgetDrillDown@4();
    VAR
      GLBudgetEntry@1000 : Record 96;
    BEGIN
      SetDateFilter;
      GLBudgetEntry.RESET;
      GLBudgetEntry.SETCURRENTKEY("Budget Name","G/L Account No.",Date);
      GLBudgetEntry.SETFILTER("Budget Name",GLAcc.GETFILTER("Budget Filter"));
      GLBudgetEntry.SETRANGE("G/L Account No.",GLAcc."No.");
      IF GLAcc.Totaling <> '' THEN
        GLBudgetEntry.SETFILTER("G/L Account No.",GLAcc.Totaling);
      GLBudgetEntry.SETFILTER(Date,GLAcc.GETFILTER("Date Filter"));
      GLBudgetEntry.SETFILTER("Global Dimension 1 Code",GLAcc.GETFILTER("Global Dimension 1 Filter"));
      GLBudgetEntry.SETFILTER("Global Dimension 2 Code",GLAcc.GETFILTER("Global Dimension 2 Filter"));
      GLBudgetEntry.SETFILTER("Business Unit Code",GLAcc.GETFILTER("Business Unit Filter"));
      FORM.RUN(0,GLBudgetEntry);
    END;

    LOCAL PROCEDURE SetDateFilter@2();
    BEGIN
      IF AmountType = AmountType::"Net Change" THEN BEGIN
        GLAcc.SETRANGE("Date Filter","Period Start","Period End");
      END ELSE
        GLAcc.SETRANGE("Date Filter",0D,"Period End");
      IF ClosingEntryFilter = ClosingEntryFilter::Exclude THEN BEGIN
        AccountingPeriod.SETCURRENTKEY("New Fiscal Year");
        AccountingPeriod.SETRANGE("New Fiscal Year",TRUE);
        IF GLAcc.GETRANGEMIN("Date Filter") = 0D THEN
          AccountingPeriod.SETRANGE("Starting Date",0D,GLAcc.GETRANGEMAX("Date Filter"))
        ELSE
          AccountingPeriod.SETRANGE(
            "Starting Date",
            GLAcc.GETRANGEMIN("Date Filter") + 1,
            GLAcc.GETRANGEMAX("Date Filter"));
        IF AccountingPeriod.FIND('-') THEN
          REPEAT
            GLAcc.SETFILTER(
              "Date Filter",GLAcc.GETFILTER("Date Filter") + '&<>%1',
              CLOSINGDATE(AccountingPeriod."Starting Date" - 1));
          UNTIL AccountingPeriod.NEXT = 0;
      END ELSE
        GLAcc.SETRANGE(
          "Date Filter",
          GLAcc.GETRANGEMIN("Date Filter"),
          CLOSINGDATE(GLAcc.GETRANGEMAX("Date Filter")));
    END;

    PROCEDURE CalcFormFields@5();
    BEGIN
      GLAcc.CALCFIELDS("Net Change","Budgeted Amount");
      GLAcc."Debit Amount" := GLAcc."Net Change";
      GLAcc."Credit Amount" := -GLAcc."Net Change";
      GLAcc."Budgeted Debit Amount" := GLAcc."Budgeted Amount";
      GLAcc."Budgeted Credit Amount" := -GLAcc."Budgeted Amount";
      IF GLAcc."Budgeted Amount" = 0 THEN
        BudgetPct := 0
      ELSE
        BudgetPct := GLAcc."Net Change" / GLAcc."Budgeted Amount" * 100;
    END;

    BEGIN
    END.
  }
}
