OBJECT Page 351 Customer Sales Lines
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Lines;
               ESP=L¡neas];
    LinksAllowed=No;
    SourceTable=Table2000000007;
    PageType=ListPart;
    OnOpenPage=BEGIN
                 RESET;
               END;

    OnFindRecord=BEGIN
                   EXIT(PeriodFormMgt.FindDate(Which,Rec,CustPeriodLength));
                 END;

    OnNextRecord=BEGIN
                   EXIT(PeriodFormMgt.NextDate(Steps,Rec,CustPeriodLength));
                 END;

    OnAfterGetRecord=BEGIN
                       SetDateFilter;
                       Cust.CALCFIELDS("Balance Due (LCY)","Sales (LCY)","Profit (LCY)");
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                Editable=FALSE;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                CaptionML=[ENU=Period Start;
                           ESP=Inicio periodo];
                SourceExpr="Period Start" }

    { 6   ;2   ;Field     ;
                CaptionML=[ENU=Period Name;
                           ESP=Nombre periodo];
                SourceExpr="Period Name" }

    { 4   ;2   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=Balance Due (LCY);
                           ESP=Saldo vencido (DL)];
                SourceExpr=Cust."Balance Due (LCY)";
                AutoFormatType=1;
                OnDrillDown=BEGIN
                              ShowCustEntriesDue;
                            END;
                             }

    { 8   ;2   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=Sales (LCY);
                           ESP=Ventas (DL)];
                SourceExpr=Cust."Sales (LCY)";
                AutoFormatType=1;
                OnDrillDown=BEGIN
                              ShowCustEntries;
                            END;
                             }

    { 10  ;2   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=Profit (LCY);
                           ESP=Bf§ bruto (DL)];
                SourceExpr=Cust."Profit (LCY)";
                AutoFormatType=1;
                OnDrillDown=BEGIN
                              ShowCustEntries;
                            END;
                             }

  }
  CODE
  {
    VAR
      Cust@1000 : Record 18;
      CustLedgEntry@1001 : Record 21;
      PeriodFormMgt@1002 : Codeunit 359;
      CustPeriodLength@1003 : 'Day,Week,Month,Quarter,Year,Period';
      AmountType@1004 : 'Net Change,Balance at Date';

    PROCEDURE Set@1(VAR NewCust@1000 : Record 18;NewCustPeriodLength@1001 : Integer;NewAmountType@1002 : 'Net Change,Balance at Date');
    BEGIN
      Cust.COPY(NewCust);
      CustPeriodLength := NewCustPeriodLength;
      AmountType := NewAmountType;
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE ShowCustEntries@2();
    BEGIN
      SetDateFilter;
      CustLedgEntry.RESET;
      CustLedgEntry.SETCURRENTKEY("Customer No.","Posting Date");
      CustLedgEntry.SETRANGE("Customer No.",Cust."No.");
      CustLedgEntry.SETFILTER("Posting Date",Cust.GETFILTER("Date Filter"));
      CustLedgEntry.SETFILTER("Global Dimension 1 Code",Cust.GETFILTER("Global Dimension 1 Filter"));
      CustLedgEntry.SETFILTER("Global Dimension 2 Code",Cust.GETFILTER("Global Dimension 2 Filter"));
      FORM.RUN(0,CustLedgEntry);
    END;

    LOCAL PROCEDURE ShowCustEntriesDue@3();
    VAR
      DtldCustLedgEntry@1000 : Record 379;
    BEGIN
      SetDateFilter;
      DtldCustLedgEntry.RESET;
      DtldCustLedgEntry.SETCURRENTKEY("Customer No.","Initial Entry Due Date","Posting Date","Currency Code");
      DtldCustLedgEntry.SETRANGE("Customer No.",Cust."No.");
      DtldCustLedgEntry.SETFILTER("Initial Entry Due Date",Cust.GETFILTER("Date Filter"));
      DtldCustLedgEntry.SETFILTER("Posting Date",'..%1',Cust.GETRANGEMAX(Cust."Date Filter"));
      DtldCustLedgEntry.SETFILTER("Initial Entry Global Dim. 1",Cust.GETFILTER("Global Dimension 1 Filter"));
      DtldCustLedgEntry.SETFILTER("Initial Entry Global Dim. 2",Cust.GETFILTER("Global Dimension 2 Filter"));
      FORM.RUN(0,DtldCustLedgEntry)
    END;

    LOCAL PROCEDURE SetDateFilter@4();
    BEGIN
      IF AmountType = AmountType::"Net Change" THEN
        Cust.SETRANGE("Date Filter","Period Start","Period End")
      ELSE
        Cust.SETRANGE("Date Filter",0D,"Period End");
    END;

    BEGIN
    END.
  }
}
