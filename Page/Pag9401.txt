OBJECT Page 9401 VAT Amount Lines
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00,NAVES6.00.01;
  }
  PROPERTIES
  {
    CaptionML=[ENU=VAT Amount Lines;
               ESP=L¡ns. importe IVA];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table290;
    PageType=List;
    OnInit=BEGIN
             InvoiceDiscountAmountEditable := TRUE;
             "VAT AmountEditable" := TRUE;
           END;

    OnFindRecord=BEGIN
                   TempVATAmountLine.COPY(Rec);
                   IF TempVATAmountLine.FIND(Which) THEN BEGIN
                     Rec := TempVATAmountLine;
                     EXIT(TRUE);
                   END ELSE
                     EXIT(FALSE);
                 END;

    OnNextRecord=VAR
                   ResultSteps@1001 : Integer;
                 BEGIN
                   TempVATAmountLine.COPY(Rec);
                   ResultSteps := TempVATAmountLine.NEXT(Steps);
                   IF ResultSteps <> 0 THEN
                     Rec := TempVATAmountLine;
                   EXIT(ResultSteps);
                 END;

    OnAfterGetRecord=BEGIN
                       "VAT AmountEditable" := AllowVATDifference AND NOT "Includes Prepayment";
                       InvoiceDiscountAmountEditable := AllowInvDisc AND NOT "Includes Prepayment";
                     END;

    OnModifyRecord=BEGIN
                     ModifyRec;
                     EXIT(FALSE);
                   END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                SourceExpr="VAT Identifier";
                Visible=FALSE }

    { 6   ;2   ;Field     ;
                SourceExpr="VAT %" }

    { 1100000;2;Field     ;
                DecimalPlaces=0:6;
                SourceExpr="EC %" }

    { 8   ;2   ;Field     ;
                SourceExpr="VAT Calculation Type";
                Visible=FALSE }

    { 10  ;2   ;Field     ;
                SourceExpr="Line Amount";
                AutoFormatType=1;
                AutoFormatExpr=CurrencyCode }

    { 12  ;2   ;Field     ;
                SourceExpr="Inv. Disc. Base Amount";
                AutoFormatType=1;
                AutoFormatExpr=CurrencyCode;
                Visible=FALSE }

    { 14  ;2   ;Field     ;
                SourceExpr="Invoice Discount Amount";
                AutoFormatType=1;
                AutoFormatExpr=CurrencyCode;
                Visible=FALSE;
                Editable=InvoiceDiscountAmountEditable;
                OnValidate=BEGIN
                             CalcVATFields(CurrencyCode,PricesIncludingVAT,VATBaseDiscPct);
                             ModifyRec;
                           END;
                            }

    { 16  ;2   ;Field     ;
                SourceExpr="VAT+EC Base";
                AutoFormatType=1;
                AutoFormatExpr=CurrencyCode }

    { 20  ;2   ;Field     ;
                SourceExpr="VAT Amount";
                AutoFormatType=1;
                AutoFormatExpr=CurrencyCode;
                Editable="VAT AmountEditable";
                OnValidate=BEGIN
                             IF AllowVATDifference AND NOT AllowVATDifferenceOnThisTab THEN
                               ERROR(Text000,FIELDCAPTION("VAT Amount"));

                             "Amount Including VAT+EC" := "VAT Amount" + "EC Amount" + "VAT+EC Base";

                             FormCheckVATDifference;
                             ModifyRec;
                           END;
                            }

    { 1100002;2;Field     ;
                SourceExpr="EC Amount";
                OnValidate=BEGIN
                             IF AllowVATDifference AND NOT AllowVATDifferenceOnThisTab THEN
                               ERROR(Text000,FIELDCAPTION("EC Amount"));

                             "Amount Including VAT+EC" := "VAT Amount" + "EC Amount" + "VAT+EC Base";

                             FormCheckVATDifference;
                             ModifyRec;
                           END;
                            }

    { 24  ;2   ;Field     ;
                SourceExpr="Calculated VAT Amount";
                AutoFormatType=1;
                AutoFormatExpr=CurrencyCode;
                Visible=FALSE }

    { 26  ;2   ;Field     ;
                SourceExpr="VAT Difference";
                AutoFormatType=1;
                AutoFormatExpr=CurrencyCode;
                Visible=FALSE }

    { 22  ;2   ;Field     ;
                SourceExpr="Amount Including VAT+EC";
                AutoFormatType=1;
                AutoFormatExpr=CurrencyCode;
                OnValidate=BEGIN
                             FormCheckVATDifference;
                           END;
                            }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 can only be modified on the Invoicing tab.;ESP=%1 puede ser modif. s¢lo en la pest. facturac.';
      Text001@1001 : TextConst 'ENU="The total %1 for a document must not exceed %2 = %3.";ESP="El total %1 para un doc. no debe exceder %2 = %3."';
      TempVATAmountLine@1002 : TEMPORARY Record 290;
      Currency@1003 : Record 4;
      CurrencyCode@1004 : Code[10];
      AllowVATDifference@1005 : Boolean;
      AllowVATDifferenceOnThisTab@1006 : Boolean;
      PricesIncludingVAT@1007 : Boolean;
      AllowInvDisc@1008 : Boolean;
      VATBaseDiscPct@1009 : Decimal;
      "VAT AmountEditable"@19075252 : Boolean INDATASET;
      InvoiceDiscountAmountEditable@19042140 : Boolean INDATASET;

    PROCEDURE SetTempVATAmountLine@1(VAR NewVATAmountLine@1000 : Record 290);
    BEGIN
      TempVATAmountLine.DELETEALL;
      IF NewVATAmountLine.FIND('-') THEN
        REPEAT
          TempVATAmountLine.COPY(NewVATAmountLine);
          IF TempVATAmountLine."VAT Calculation Type" = TempVATAmountLine."VAT Calculation Type"::"Reverse Charge VAT" THEN BEGIN
            TempVATAmountLine."VAT %" := 0;
            TempVATAmountLine."EC %" := 0;
          END;
          TempVATAmountLine.INSERT;
        UNTIL NewVATAmountLine.NEXT = 0;
    END;

    PROCEDURE GetTempVATAmountLine@5(VAR NewVATAmountLine@1000 : Record 290);
    BEGIN
      NewVATAmountLine.DELETEALL;
      IF TempVATAmountLine.FIND('-') THEN
        REPEAT
          NewVATAmountLine.COPY(TempVATAmountLine);
          NewVATAmountLine.INSERT;
        UNTIL TempVATAmountLine.NEXT = 0;
    END;

    PROCEDURE InitGlobals@2(NewCurrencyCode@1000 : Code[10];NewAllowVATDifference@1001 : Boolean;NewAllowVATDifferenceOnThisTab@1002 : Boolean;NewPricesIncludingVAT@1003 : Boolean;NewAllowInvDisc@1004 : Boolean;NewVATBaseDiscPct@1005 : Decimal);
    BEGIN
      CurrencyCode := NewCurrencyCode;
      AllowVATDifference := NewAllowVATDifference;
      AllowVATDifferenceOnThisTab := NewAllowVATDifferenceOnThisTab;
      PricesIncludingVAT := NewPricesIncludingVAT;
      AllowInvDisc := NewAllowInvDisc;
      VATBaseDiscPct := NewVATBaseDiscPct;
      "VAT AmountEditable" := AllowVATDifference;
      InvoiceDiscountAmountEditable := AllowInvDisc;
      IF CurrencyCode = '' THEN
        Currency.InitRoundingPrecision
      ELSE
        Currency.GET(CurrencyCode);
    END;

    PROCEDURE FormCheckVATDifference@4();
    VAR
      VATAmountLine2@1000 : Record 290;
      TotalVATDifference@1001 : Decimal;
    BEGIN
      Rec.CheckVATDifference(CurrencyCode,AllowVATDifference,PricesIncludingVAT);
      VATAmountLine2 := TempVATAmountLine;
      TotalVATDifference := ABS("VAT Difference") - ABS(xRec."VAT Difference");
      IF TempVATAmountLine.FIND('-') THEN
        REPEAT
          TotalVATDifference := TotalVATDifference + ABS(TempVATAmountLine."VAT Difference");
        UNTIL TempVATAmountLine.NEXT = 0;
      TempVATAmountLine := VATAmountLine2;
      IF TotalVATDifference > Currency."Max. VAT Difference Allowed" THEN
        ERROR(
          Text001,FIELDCAPTION("VAT Difference"),
          Currency.FIELDCAPTION("Max. VAT Difference Allowed"),Currency."Max. VAT Difference Allowed");
    END;

    LOCAL PROCEDURE ModifyRec@3();
    BEGIN
      TempVATAmountLine := Rec;
      TempVATAmountLine.Modified := TRUE;
      TempVATAmountLine.MODIFY;
    END;

    BEGIN
    END.
  }
}
