OBJECT Page 5920 Service Document Log
{
  OBJECT-PROPERTIES
  {
    Date=13/02/07;
    Time=12:00:00;
    Version List=NAVW15.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[ENU=Service Document Log;
               ESP=Registro pedido servicio];
    SourceTable=Table5912;
    DataCaptionExpr=GetCaptionHeader;
    SourceTableView=SORTING(Change Date,Change Time)
                    ORDER(Descending);
    PageType=List;
    RefreshOnActivate=Yes;
    OnInit=BEGIN
             "Document No.Visible" := TRUE;
             "Document TypeVisible" := TRUE;
           END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 16      ;1   ;ActionGroup;
                      CaptionML=[ENU=F&unctions;
                                 ESP=Acci&ones] }
      { 17      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[ENU=&Delete Service Document Log;
                                 ESP=Eliminar log &documento servicio];
                      OnAction=VAR
                                 ServOrderLog@1002 : Record 5912;
                                 DeleteServOrderLog@1000 : Report 6002;
                               BEGIN
                                 ServOrderLog.SETRANGE("Document Type","Document Type");
                                 ServOrderLog.SETRANGE("Document No.","Document No.");
                                 DeleteServOrderLog.SETTABLEVIEW(ServOrderLog);
                                 DeleteServOrderLog.RUNMODAL;

                                 IF DeleteServOrderLog.GetOnPostReportStatus THEN BEGIN
                                   ServOrderLog.RESET;
                                   DeleteServOrderLog.GetServDocLog(ServOrderLog);
                                   COPYFILTERS(ServOrderLog);
                                   DELETEALL;
                                   RESET;
                                   SETCURRENTKEY("Change Date","Change Time");
                                   ASCENDING(FALSE);
                                 END;
                               END;
                                }
      { 18      ;1   ;Action    ;
                      CaptionML=[ENU=&Show;
                                 ESP=&Mostrar];
                      Promoted=Yes;
                      Image=View;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ServShptHeader@1001 : Record 5990;
                                 ServInvHeader@1002 : Record 5992;
                                 ServCrMemoHeader@1003 : Record 5994;
                                 isError@1004 : Boolean;
                               BEGIN
                                 IF "Document Type" IN
                                    ["Document Type"::Order,"Document Type"::Quote,
                                     "Document Type"::Invoice,"Document Type"::"Credit Memo"]
                                 THEN
                                   IF ServOrderHeaderRec.GET("Document Type","Document No.") THEN BEGIN
                                     isError := FALSE;
                                     CASE "Document Type" OF
                                       "Document Type"::Quote:
                                         FORM.RUN(FORM::"Service Quote",ServOrderHeaderRec);
                                       "Document Type"::Order:
                                         FORM.RUN(FORM::"Service Order",ServOrderHeaderRec);
                                       "Document Type"::Invoice:
                                         FORM.RUN(FORM::"Service Invoice",ServOrderHeaderRec);
                                       "Document Type"::"Credit Memo":
                                         FORM.RUN(FORM::"Service Credit Memo",ServOrderHeaderRec);
                                     END;
                                   END ELSE
                                     isError := TRUE
                                 ELSE BEGIN // posted documents
                                   isError := TRUE;
                                   CASE "Document Type" OF
                                     "Document Type"::Shipment:
                                       BEGIN
                                         IF ServShptHeader.GET("Document No.") THEN BEGIN
                                           isError := FALSE;
                                           FORM.RUN(FORM::"Posted Service Shipment",ServShptHeader);
                                         END;
                                       END;
                                     "Document Type"::"Posted Invoice":
                                       BEGIN
                                         IF ServInvHeader.GET("Document No.") THEN BEGIN
                                           isError := FALSE;
                                           FORM.RUN(FORM::"Posted Service Invoice",ServInvHeader);
                                         END;
                                       END;
                                     "Document Type"::"Posted Credit Memo":
                                       BEGIN
                                         IF ServCrMemoHeader.GET("Document No.") THEN BEGIN
                                           isError := FALSE;
                                           FORM.RUN(FORM::"Posted Service Credit Memo",ServCrMemoHeader);
                                         END;
                                       END;
                                   END;
                                 END;
                                 IF isError THEN
                                   ERROR(Text001,"Document Type","Document No.");
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 12  ;2   ;Field     ;
                SourceExpr="Document Type";
                Visible="Document TypeVisible" }

    { 29  ;2   ;Field     ;
                SourceExpr="Document No.";
                Visible="Document No.Visible" }

    { 14  ;2   ;Field     ;
                SourceExpr="Service Item Line No.";
                Visible=FALSE }

    { 31  ;2   ;Field     ;
                SourceExpr="Entry No.";
                Visible=FALSE }

    { 33  ;2   ;Field     ;
                CaptionML=[ENU=Description;
                           ESP=Descripci¢n];
                SourceExpr=ServLogMgt.ServOrderEventDescription("Event No.") }

    { 2   ;2   ;Field     ;
                SourceExpr=After }

    { 4   ;2   ;Field     ;
                SourceExpr=Before }

    { 6   ;2   ;Field     ;
                SourceExpr="Change Date" }

    { 8   ;2   ;Field     ;
                SourceExpr="Change Time" }

    { 10  ;2   ;Field     ;
                SourceExpr="User ID" }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      ServOrderHeaderRec@1001 : Record 5900;
      ServLogMgt@1000 : Codeunit 5906;
      Text001@1003 : TextConst 'ENU=Service %1 %2 does not exist.;ESP=El servicio %1 %2 no existe.';
      "Document TypeVisible"@19012068 : Boolean INDATASET;
      "Document No.Visible"@19042234 : Boolean INDATASET;

    PROCEDURE GetCaptionHeader@1() : Text[100];
    VAR
      ServHeader@1000 : Record 5900;
    BEGIN
      IF GETFILTER("Document No.") <> '' THEN BEGIN
        "Document TypeVisible" := FALSE;
        "Document No.Visible" := FALSE;
        IF ServHeader.GET("Document Type","Document No.") THEN BEGIN
          EXIT(FORMAT("Document Type") + ' ' + "Document No." + ' ' + ServHeader.Description)
        END ELSE
          EXIT(FORMAT("Document Type") + ' ' + "Document No.");
      END ELSE BEGIN
        "Document TypeVisible" := TRUE;
        "Document No.Visible" := TRUE;
        EXIT('');
      END;
    END;

    BEGIN
    END.
  }
}
