OBJECT Page 6061 Contract Trend Lines
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Lines;
               ESP=L¡neas];
    LinksAllowed=No;
    SourceTable=Table2000000007;
    PageType=ListPart;
    OnFindRecord=BEGIN
                   EXIT(PeriodFormMgt.FindDate(Which,Rec,ServItemPeriodLength));
                 END;

    OnNextRecord=BEGIN
                   EXIT(PeriodFormMgt.NextDate(Steps,Rec,ServItemPeriodLength));
                 END;

    OnAfterGetRecord=BEGIN
                       SetDateFilter;
                       ServContract.CALCFIELDS(
                         "Contract Invoice Amount",
                         "Contract Discount Amount",
                         "Contract Cost Amount",
                         "Contract Prepaid Amount");
                       ProfitAmount := ServContract."Contract Invoice Amount" - ServContract."Contract Cost Amount";
                       IF ServContract."Contract Invoice Amount" <> 0 THEN
                         "Profit%" := ROUND((ProfitAmount / ServContract."Contract Invoice Amount") * 100,0.01)
                       ELSE
                         "Profit%" := 0;
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                Editable=FALSE;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                CaptionML=[ENU=Period Start;
                           ESP=Inicio periodo];
                SourceExpr="Period Start" }

    { 8   ;2   ;Field     ;
                CaptionML=[ENU=Period Name;
                           ESP=Nombre periodo];
                SourceExpr="Period Name" }

    { 18  ;2   ;Field     ;
                CaptionML=[ENU=Prepaid Income;
                           ESP=Ingresos prepago];
                SourceExpr=ServContract."Contract Prepaid Amount";
                OnDrillDown=BEGIN
                              SetDateFilter;
                              ServLedgEntry.RESET;
                              ServLedgEntry.SETCURRENTKEY(Type,"No.","Entry Type","Moved from Prepaid Acc.","Posting Date");
                              ServLedgEntry.SETRANGE("Service Contract No.",ServContract."Contract No.");
                              ServLedgEntry.SETRANGE("Entry Type",ServLedgEntry."Entry Type"::Sale);
                              ServLedgEntry.SETRANGE("Moved from Prepaid Acc.",FALSE);
                              ServLedgEntry.SETRANGE(Type,ServLedgEntry.Type::"Service Contract");
                              ServLedgEntry.SETRANGE(Open,FALSE);
                              ServLedgEntry.SETRANGE(Prepaid,TRUE);
                              ServLedgEntry.SETFILTER("Posting Date",ServContract.GETFILTER("Date Filter"));
                              FORM.RUNMODAL(0,ServLedgEntry);
                            END;
                             }

    { 4   ;2   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=Posted Income;
                           ESP=Ingresos regis.];
                SourceExpr=ServContract."Contract Invoice Amount";
                OnDrillDown=BEGIN
                              SetDateFilter;
                              ServLedgEntry.RESET;
                              ServLedgEntry.SETCURRENTKEY(Type,"No.","Entry Type","Moved from Prepaid Acc.","Posting Date");
                              ServLedgEntry.SETRANGE("Service Contract No.",ServContract."Contract No.");
                              ServLedgEntry.SETRANGE("Entry Type",ServLedgEntry."Entry Type"::Sale);
                              ServLedgEntry.SETRANGE("Moved from Prepaid Acc.",TRUE);
                              ServLedgEntry.SETRANGE(Open,FALSE);
                              ServLedgEntry.SETFILTER("Posting Date",ServContract.GETFILTER("Date Filter"));
                              FORM.RUNMODAL(0,ServLedgEntry);
                            END;
                             }

    { 6   ;2   ;Field     ;
                CaptionML=[ENU=Posted Cost;
                           ESP=Coste regis.];
                SourceExpr=ServContract."Contract Cost Amount";
                OnDrillDown=BEGIN
                              SetDateFilter;
                              CLEAR(ServLedgEntry);
                              ServLedgEntry.SETCURRENTKEY(Type,"No.","Entry Type","Moved from Prepaid Acc.","Posting Date");
                              ServLedgEntry.SETRANGE("Entry Type",ServLedgEntry."Entry Type"::Usage);
                              ServLedgEntry.SETRANGE("Service Contract No.",ServContract."Contract No.");
                              ServLedgEntry.SETRANGE("Moved from Prepaid Acc.",TRUE);
                              ServLedgEntry.SETRANGE(Open,FALSE);
                              ServLedgEntry.SETFILTER("Posting Date",ServContract.GETFILTER("Date Filter"));
                              FORM.RUNMODAL(0,ServLedgEntry);
                            END;
                             }

    { 12  ;2   ;Field     ;
                CaptionML=[ENU=Discount Amount;
                           ESP=Importe dto.];
                SourceExpr=ServContract."Contract Discount Amount";
                OnDrillDown=BEGIN
                              SetDateFilter;
                              CLEAR(ServLedgEntry);
                              ServLedgEntry.SETCURRENTKEY("Service Contract No.");
                              ServLedgEntry.SETRANGE("Service Contract No.",ServContract."Contract No.");
                              ServLedgEntry.SETRANGE("Entry Type",ServLedgEntry."Entry Type"::Usage);
                              ServLedgEntry.SETRANGE("Moved from Prepaid Acc.",TRUE);
                              ServLedgEntry.SETRANGE(Open,FALSE);
                              ServLedgEntry.SETFILTER("Posting Date",ServContract.GETFILTER("Date Filter"));
                              FORM.RUNMODAL(0,ServLedgEntry);
                            END;
                             }

    { 14  ;2   ;Field     ;
                CaptionML=[ENU=Profit;
                           ESP=Bf§ bruto];
                SourceExpr=ProfitAmount;
                AutoFormatType=1 }

    { 16  ;2   ;Field     ;
                CaptionML=[ENU=Profit %;
                           ESP=% Bf§ bruto];
                SourceExpr="Profit%" }

  }
  CODE
  {
    VAR
      ServContract@1000 : Record 5965;
      ServLedgEntry@1001 : Record 5907;
      PeriodFormMgt@1002 : Codeunit 359;
      ServItemPeriodLength@1003 : 'Day,Week,Month,Quarter,Year,Period';
      AmountType@1004 : 'Net Change,Balance at Date';
      ProfitAmount@1005 : Decimal;
      "Profit%"@1006 : Decimal;

    PROCEDURE Set@1(VAR NewServContract@1000 : Record 5965;NewServItemPeriodLength@1001 : Integer;NewAmountType@1002 : 'Net Change,Balance at Date');
    BEGIN
      ServContract.COPY(NewServContract);
      ServItemPeriodLength := NewServItemPeriodLength;
      AmountType := NewAmountType;
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE SetDateFilter@4();
    BEGIN
      IF AmountType = AmountType::"Net Change" THEN
        ServContract.SETRANGE("Date Filter","Period Start","Period End")
      ELSE
        ServContract.SETRANGE("Date Filter",0D,"Period End");
    END;

    BEGIN
    END.
  }
}
