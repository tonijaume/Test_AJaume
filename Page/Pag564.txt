OBJECT Page 564 Dimension Selection-Level
{
  OBJECT-PROPERTIES
  {
    Date=13/02/07;
    Time=12:00:00;
    Version List=NAVW15.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Dimension Selection;
               ESP=Selecci¢n dimensi¢n];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table368;
    PageType=List;
    SourceTableTemporary=Yes;
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                SourceExpr=Level;
                OnValidate=VAR
                             DimSelectBuffer@1001 : Record 368;
                             LevelExists@1002 : Boolean;
                           BEGIN
                             IF Level <> Level::" " THEN BEGIN
                               DimSelectBuffer.COPY(Rec);
                               RESET;
                               SETFILTER(Code,'<>%1',DimSelectBuffer.Code);
                               SETRANGE(Level,DimSelectBuffer.Level);
                               LevelExists := NOT ISEMPTY;
                               COPY(DimSelectBuffer);

                               IF LevelExists THEN
                                 ERROR(Text000,FIELDCAPTION(Level));
                             END;
                           END;
                            }

    { 4   ;2   ;Field     ;
                SourceExpr=Code;
                Editable=FALSE }

    { 6   ;2   ;Field     ;
                SourceExpr=Description;
                Editable=FALSE }

    { 11  ;2   ;Field     ;
                SourceExpr="Dimension Value Filter" }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=This %1 already exists.;ESP=Este %1 ya existe.';

    PROCEDURE GetDimSelBuf@7(VAR TheDimSelectionBuf@1000 : Record 368);
    BEGIN
      TheDimSelectionBuf.DELETEALL;
      IF FIND('-') THEN
        REPEAT
          TheDimSelectionBuf := Rec;
          TheDimSelectionBuf.INSERT;
        UNTIL NEXT = 0;
    END;

    PROCEDURE InsertDimSelBuf@11(NewSelected@1000 : Boolean;NewCode@1001 : Text[30];NewDescription@1002 : Text[30];NewDimValueFilter@1003 : Text[250];NewLevel@1004 : Option);
    VAR
      Dim@1005 : Record 348;
      GLAcc@1006 : Record 15;
      BusinessUnit@1007 : Record 220;
    BEGIN
      IF NewDescription = '' THEN
        IF Dim.GET(NewCode) THEN
          NewDescription := Dim.GetMLName(GLOBALLANGUAGE);

      INIT;
      Selected := NewSelected;
      Code :=  NewCode;
      Description := NewDescription;
      IF NewSelected THEN BEGIN
        "Dimension Value Filter" := NewDimValueFilter;
        Level := NewLevel;
      END;
      CASE Code OF
        GLAcc.TABLECAPTION:
          "Filter Lookup Table No." := DATABASE::"G/L Account";
        BusinessUnit.TABLECAPTION:
          "Filter Lookup Table No." := DATABASE::"Business Unit";
      END;
      INSERT;
    END;

    BEGIN
    END.
  }
}
