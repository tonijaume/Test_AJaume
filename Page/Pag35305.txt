OBJECT Page 35305 Cartera Payables Statistics FB
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVES6.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[ENU=Cartera Payables Statistics FB;
               ESP=Cuadro info. estad¡s. cartera a pagar];
    SourceTable=Table23;
    PageType=CardPart;
    OnAfterGetRecord=BEGIN

                       IF CurrentDate <> WORKDATE THEN BEGIN
                         CurrentDate := WORKDATE;
                         DateFilterCalc.CreateAccountingPeriodFilter(VendDateFilter[1],VendDateName[1],CurrentDate,0);
                         DateFilterCalc.CreateFiscalYearFilter(VendDateFilter[2],VendDateName[2],CurrentDate,0);
                         DateFilterCalc.CreateFiscalYearFilter(VendDateFilter[3],VendDateName[3],CurrentDate,-1);
                       END;

                       SETRANGE("Date Filter",0D,CurrentDate);
                       CALCFIELDS(
                         Balance,"Balance (LCY)","Balance Due","Balance Due (LCY)",
                         "Outstanding Orders (LCY)","Amt. Rcd. Not Invoiced (LCY)",
                         "Reminder Amounts (LCY)");

                       TotalAmountLCY := "Balance (LCY)" + "Outstanding Orders (LCY)" + "Amt. Rcd. Not Invoiced (LCY)" + "Outstanding Invoices (LCY)";

                       FOR i := 1 TO 4 DO BEGIN
                         SETFILTER("Date Filter",VendDateFilter[i]);
                         CALCFIELDS(
                           "Purchases (LCY)","Inv. Discounts (LCY)","Inv. Amounts (LCY)","Pmt. Discounts (LCY)",
                           "Pmt. Disc. Tolerance (LCY)","Pmt. Tolerance (LCY)",
                           "Fin. Charge Memo Amounts (LCY)","Cr. Memo Amounts (LCY)","Payments (LCY)",
                           "Reminder Amounts (LCY)","Refunds (LCY)","Other Amounts (LCY)");
                         VendPurchLCY[i] := "Purchases (LCY)";
                         VendInvDiscAmountLCY[i] := "Inv. Discounts (LCY)";
                         InvAmountsLCY[i] := "Inv. Amounts (LCY)";
                         VendPaymentDiscLCY[i] := "Pmt. Discounts (LCY)";
                         VendPaymentDiscTolLCY[i] := "Pmt. Disc. Tolerance (LCY)";
                         VendPaymentTolLCY[i] := "Pmt. Tolerance (LCY)";
                         VendReminderChargeAmtLCY[i] := "Reminder Amounts (LCY)";
                         VendFinChargeAmtLCY[i] := "Fin. Charge Memo Amounts (LCY)";
                         VendCrMemoAmountsLCY[i] := "Cr. Memo Amounts (LCY)";
                         VendPaymentsLCY[i] := "Payments (LCY)";
                         VendRefundsLCY[i] := "Refunds (LCY)";
                         VendOtherAmountsLCY[i] := "Other Amounts (LCY)";
                       END;
                       SETRANGE("Date Filter",0D,CurrentDate);

                       UpdateBillStatistics;
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100014;1;Group     ;
                CaptionML=[ENU=No. of Documents;
                           ESP=N§ documentos] }

    { 1100009;2;Field     ;
                CaptionML=[ENU=Open Documents;
                           ESP=Docs. pendientes];
                SourceExpr=NoOpen[1];
                Editable=FALSE }

    { 1100006;2;Field     ;
                CaptionML=[ENU=Open Docs. in Payment Order;
                           ESP=Docs. pdtes. en orden pago];
                SourceExpr=NoOpen[2];
                Editable=FALSE }

    { 1100007;2;Field     ;
                CaptionML=[ENU=Open Docs. in Posted Payment Order;
                           ESP=Docs. pdtes. en orden pago regis.];
                SourceExpr=NoOpen[3];
                Editable=FALSE }

    { 1100008;2;Field     ;
                CaptionML=[ENU=Honored Docs. in Posted Payment Order;
                           ESP=Docs. pagados en orden pago regis.];
                SourceExpr=NoHonored[3];
                Editable=FALSE }

    { 1100012;1;Group     ;
                CaptionML=[ENU=Remaining Amt.  (LCY);
                           ESP=Importe pdte. (DL)] }

    { 1100011;2;Field     ;
                CaptionML=[ENU=Open Documents;
                           ESP=Docs. pendientes];
                SourceExpr=OpenRemainingAmtLCY[1];
                AutoFormatType=1;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              DrillDownOpen(4); //Cartera
                            END;
                             }

    { 1100004;2;Field     ;
                CaptionML=[ENU=Open Docs. in Payment Order;
                           ESP=Docs. pdtes. en orden pago];
                SourceExpr=OpenRemainingAmtLCY[2];
                AutoFormatType=1;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              DrillDownOpen(3); // Payment Order
                            END;
                             }

    { 1100001;2;Field     ;
                CaptionML=[ENU=Open Docs. in Posted Payment Order;
                           ESP=Docs. pdtes. en orden pago regis.];
                SourceExpr=OpenRemainingAmtLCY[3];
                AutoFormatType=1;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              DrillDownOpen(1); // Posted Payment Order
                            END;
                             }

    { 1100000;2;Field     ;
                CaptionML=[ENU=Honored Docs. in Posted Payment Order;
                           ESP=Docs. pagados en orden pago regis.];
                SourceExpr=HonoredRemainingAmtLCY[3];
                AutoFormatType=1;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              DrillDownHonored(1); // Posted Payment Order
                            END;
                             }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Overdue Amounts (LCY) as of %1;ESP=Importes vencidos (DL) a %1';
      DateFilterCalc@1001 : Codeunit 358;
      VendDateFilter@1002 : ARRAY [4] OF Text[30];
      VendDateName@1003 : ARRAY [4] OF Text[30];
      TotalAmountLCY@1004 : Decimal;
      CurrentDate@1005 : Date;
      VendPurchLCY@1006 : ARRAY [4] OF Decimal;
      VendInvDiscAmountLCY@1007 : ARRAY [4] OF Decimal;
      VendPaymentDiscLCY@1008 : ARRAY [4] OF Decimal;
      VendPaymentDiscTolLCY@1015 : ARRAY [4] OF Decimal;
      VendPaymentTolLCY@1016 : ARRAY [4] OF Decimal;
      VendReminderChargeAmtLCY@1009 : ARRAY [4] OF Decimal;
      VendFinChargeAmtLCY@1010 : ARRAY [4] OF Decimal;
      VendCrMemoAmountsLCY@1011 : ARRAY [4] OF Decimal;
      VendPaymentsLCY@1012 : ARRAY [4] OF Decimal;
      VendRefundsLCY@1017 : ARRAY [4] OF Decimal;
      VendOtherAmountsLCY@1018 : ARRAY [4] OF Decimal;
      i@1013 : Integer;
      InvAmountsLCY@1014 : ARRAY [4] OF Decimal;
      Text001@1019 : TextConst 'ENU=Placeholder;ESP=Marcador de posici¢n';
      j@1100007 : Integer;
      NoOpen@1100006 : ARRAY [3] OF Integer;
      NoHonored@1100005 : ARRAY [3] OF Integer;
      OpenAmtLCY@1100004 : ARRAY [3] OF Decimal;
      OpenRemainingAmtLCY@1100003 : ARRAY [3] OF Decimal;
      HonoredAmtLCY@1100002 : ARRAY [3] OF Decimal;
      HonoredRemainingAmtLCY@1100001 : ARRAY [3] OF Decimal;
      DocumentSituationFilter@1100000 : ARRAY [3] OF ' ,Posted BG/PO,Closed BG/PO,BG/PO,Cartera,Closed Documents';

    PROCEDURE UpdateBillStatistics@1100100();
    VAR
      VendLedgEntry@1100000 : Record 25;
    BEGIN
      DocumentSituationFilter[1] := DocumentSituationFilter::Cartera;
      DocumentSituationFilter[2] := DocumentSituationFilter::"BG/PO";
      DocumentSituationFilter[3] := DocumentSituationFilter::"Posted BG/PO";

      WITH VendLedgEntry DO BEGIN
        SETCURRENTKEY("Vendor No.","Document Type","Document Situation","Document Status");
        SETRANGE("Vendor No.",Rec."No.");
        FOR j := 1 TO 3 DO BEGIN
          SETRANGE("Document Situation",DocumentSituationFilter[j]);
          SETRANGE("Document Status","Document Status"::Open);
          CALCSUMS("Amount (LCY) stats.","Remaining Amount (LCY) stats.");
          OpenAmtLCY[j] := "Amount (LCY) stats.";
          OpenRemainingAmtLCY[j] := "Remaining Amount (LCY) stats.";
          NoOpen[j] := COUNT;
          SETRANGE("Document Status");

          SETRANGE("Document Status","Document Status"::Honored);
          CALCSUMS("Amount (LCY) stats.","Remaining Amount (LCY) stats.");
          HonoredAmtLCY[j] := "Amount (LCY) stats.";
          HonoredRemainingAmtLCY[j] := "Remaining Amount (LCY) stats.";
          NoHonored[j] := COUNT;
          SETRANGE("Document Status");

          SETRANGE("Document Situation");
        END;
      END;
    END;

    PROCEDURE DrillDownOpen@1100101(Situation@1100000 : ' ,Posted BG/PO,Closed BG/PO,BG/PO,Cartera,Closed Documents');
    VAR
      VendLedgEntry@1100001 : Record 25;
      VendLedgEntriesForm@1100002 : Form 29;
    BEGIN
      WITH VendLedgEntry DO BEGIN
        SETCURRENTKEY("Vendor No.","Document Type","Document Situation","Document Status");
        SETRANGE("Vendor No.",Rec."No.");
        CASE Situation OF
          Situation::Cartera:
            SETRANGE("Document Situation","Document Situation"::Cartera);
          Situation::"BG/PO":
            SETRANGE("Document Situation","Document Situation"::"BG/PO");
          Situation::"Posted BG/PO":
            SETRANGE("Document Situation","Document Situation"::"Posted BG/PO");
        END;
        SETRANGE("Document Status","Document Status"::Open);
        VendLedgEntriesForm.SETTABLEVIEW(VendLedgEntry);
        VendLedgEntriesForm.SETRECORD(VendLedgEntry);
        VendLedgEntriesForm.RUNMODAL;
        SETRANGE("Document Status");
        SETRANGE("Document Situation");
      END;
    END;

    PROCEDURE DrillDownHonored@1100102(Situation@1100000 : ' ,Posted BG/PO,Closed BG/PO,BG/PO,Cartera,Closed Documents');
    VAR
      VendLedgEntry@1100001 : Record 25;
      VendLedgEntriesForm@1100002 : Form 29;
    BEGIN
      WITH VendLedgEntry DO BEGIN
        SETCURRENTKEY("Vendor No.","Document Type","Document Situation","Document Status");
        SETRANGE("Vendor No.",Rec."No.");
        CASE Situation OF
          Situation::Cartera:
            SETRANGE("Document Situation","Document Situation"::Cartera);
          Situation::"BG/PO":
            SETRANGE("Document Situation","Document Situation"::"BG/PO");
          Situation::"Posted BG/PO":
            SETRANGE("Document Situation","Document Situation"::"Posted BG/PO");
        END;

        SETRANGE("Document Status","Document Status"::Honored);
        VendLedgEntriesForm.SETTABLEVIEW(VendLedgEntry);
        VendLedgEntriesForm.SETRECORD(VendLedgEntry);
        VendLedgEntriesForm.RUNMODAL;
        SETRANGE("Document Status");
        SETRANGE("Document Situation");
      END;
    END;

    BEGIN
    END.
  }
}
