OBJECT Table 7134 Item Budget Entry
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CheckIfBlocked;
               TESTFIELD(Date);

               GetGLSetup;
               IF ("Source No." = '') AND ("Item No." = '') THEN BEGIN
                 GetSalesSetup;
                 GetInventorySetup;

                 IF NOT (CheckGroupDimFilled(SalesSetup."Customer Group Dimension Code") OR
                         CheckGroupDimFilled(SalesSetup."Salesperson Dimension Code") OR
                         CheckGroupDimFilled(InventorySetup."Item Group Dimension Code"))
                 THEN
                   TESTFIELD("Item No.");
               END;

               TESTFIELD("Budget Name");
               LOCKTABLE;
               "User ID" := USERID;
               IF "Entry No." = 0 THEN
                 "Entry No." := GetNextEntryNo;

               UpdateDim(GLSetup."Global Dimension 1 Code","Global Dimension 1 Code");
               UpdateDim(GLSetup."Global Dimension 2 Code","Global Dimension 2 Code");
               UpdateDim(ItemBudgetName."Budget Dimension 1 Code","Budget Dimension 1 Code");
               UpdateDim(ItemBudgetName."Budget Dimension 2 Code","Budget Dimension 2 Code");
               UpdateDim(ItemBudgetName."Budget Dimension 3 Code","Budget Dimension 3 Code");
             END;

    OnModify=BEGIN
               CheckIfBlocked;
               "User ID" := USERID;
               GetGLSetup;
               IF "Global Dimension 1 Code" <> xRec."Global Dimension 1 Code" THEN
                 UpdateDim(GLSetup."Global Dimension 1 Code","Global Dimension 1 Code");
               IF "Global Dimension 2 Code" <> xRec."Global Dimension 2 Code" THEN
                 UpdateDim(GLSetup."Global Dimension 2 Code","Global Dimension 2 Code");
               IF "Budget Dimension 1 Code" <> xRec."Budget Dimension 1 Code" THEN
                 UpdateDim(ItemBudgetName."Budget Dimension 1 Code","Budget Dimension 1 Code");
               IF "Budget Dimension 2 Code" <> xRec."Budget Dimension 2 Code" THEN
                 UpdateDim(ItemBudgetName."Budget Dimension 2 Code","Budget Dimension 2 Code");
               IF "Budget Dimension 3 Code" <> xRec."Budget Dimension 3 Code" THEN
                 UpdateDim(ItemBudgetName."Budget Dimension 3 Code","Budget Dimension 3 Code");
             END;

    OnDelete=VAR
               ItemBudgetDim@1000 : Record 7135;
             BEGIN
               CheckIfBlocked;
               ItemBudgetDim.SETRANGE("Entry No.","Entry No.");
               ItemBudgetDim.DELETEALL;
             END;

    CaptionML=[ENU=Item Budget Entry;
               ESP=Movimiento del presupuesto de productos];
    LookupFormID=Form7133;
    DrillDownFormID=Form7133;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              ESP=N§ mov.] }
    { 2   ;   ;Analysis Area       ;Option        ;CaptionML=[ENU=Analysis Area;
                                                              ESP=µrea an lisis];
                                                   OptionCaptionML=[ENU=Sales,Purchase;
                                                                    ESP=Ventas,Compra];
                                                   OptionString=Sales,Purchase;
                                                   NotBlank=Yes }
    { 3   ;   ;Budget Name         ;Code10        ;TableRelation="Item Budget Name".Name WHERE (Analysis Area=FIELD(Analysis Area));
                                                   CaptionML=[ENU=Budget Name;
                                                              ESP=Nombre ppto.] }
    { 4   ;   ;Date                ;Date          ;CaptionML=[ENU=Date;
                                                              ESP=Fecha];
                                                   ClosingDates=Yes }
    { 5   ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   CaptionML=[ENU=Item No.;
                                                              ESP=N§ producto] }
    { 6   ;   ;Source Type         ;Option        ;OnValidate=BEGIN
                                                                IF "Source Type" <> xRec."Source Type" THEN
                                                                  VALIDATE("Source No.",'');
                                                              END;

                                                   CaptionML=[ENU=Source Type;
                                                              ESP=Tipo procedencia mov.];
                                                   OptionCaptionML=[ENU=" ,Customer,Vendor,Item";
                                                                    ESP=" ,Cliente,Proveedor,Producto"];
                                                   OptionString=[ ,Customer,Vendor,Item] }
    { 7   ;   ;Source No.          ;Code20        ;TableRelation=IF (Source Type=CONST(Customer)) Customer
                                                                 ELSE IF (Source Type=CONST(Vendor)) Vendor
                                                                 ELSE IF (Source Type=CONST(Item)) Item;
                                                   CaptionML=[ENU=Source No.;
                                                              ESP=C¢d. procedencia mov.] }
    { 8   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n] }
    { 9   ;   ;Quantity            ;Decimal       ;CaptionML=[ENU=Quantity;
                                                              ESP=Cantidad];
                                                   DecimalPlaces=0:5 }
    { 10  ;   ;Cost Amount         ;Decimal       ;CaptionML=[ENU=Cost Amount;
                                                              ESP=Importe coste];
                                                   AutoFormatType=1 }
    { 11  ;   ;Sales Amount        ;Decimal       ;CaptionML=[ENU=Sales Amount;
                                                              ESP=Importe ventas];
                                                   AutoFormatType=1 }
    { 13  ;   ;User ID             ;Code20        ;OnLookup=VAR
                                                              LoginMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              LoginMgt.LookupUserID("User ID");
                                                            END;

                                                   CaptionML=[ENU=User ID;
                                                              ESP=Id. de usuario];
                                                   Editable=No }
    { 14  ;   ;Location Code       ;Code10        ;TableRelation=Location;
                                                   CaptionML=[ENU=Location Code;
                                                              ESP=C¢d. almac‚n] }
    { 15  ;   ;Global Dimension 1 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                IF "Global Dimension 1 Code" = '' THEN
                                                                  EXIT;
                                                                GetGLSetup;
                                                                ValidateDimValue(GLSetup."Global Dimension 1 Code","Global Dimension 1 Code");
                                                              END;

                                                   CaptionML=[ENU=Global Dimension 1 Code;
                                                              ESP=C¢d. dimensi¢n global 1];
                                                   CaptionClass='1,1,1' }
    { 16  ;   ;Global Dimension 2 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                IF "Global Dimension 2 Code" = '' THEN
                                                                  EXIT;
                                                                GetGLSetup;
                                                                ValidateDimValue(GLSetup."Global Dimension 2 Code","Global Dimension 2 Code");
                                                              END;

                                                   CaptionML=[ENU=Global Dimension 2 Code;
                                                              ESP=C¢d. dimensi¢n global 2];
                                                   CaptionClass='1,1,2' }
    { 17  ;   ;Budget Dimension 1 Code;Code20     ;OnValidate=BEGIN
                                                                IF "Budget Dimension 1 Code" = '' THEN
                                                                  EXIT;
                                                                IF ItemBudgetName.Name <> "Budget Name" THEN
                                                                  ItemBudgetName.GET("Analysis Area","Budget Name");
                                                                ValidateDimValue(ItemBudgetName."Budget Dimension 1 Code","Budget Dimension 1 Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              "Budget Dimension 1 Code" := OnLookupDimCode(2,"Budget Dimension 1 Code");
                                                            END;

                                                   CaptionML=[ENU=Budget Dimension 1 Code;
                                                              ESP=C¢d. dim. presupuesto 1];
                                                   CaptionClass=GetCaptionClass(1) }
    { 18  ;   ;Budget Dimension 2 Code;Code20     ;OnValidate=BEGIN
                                                                IF "Budget Dimension 2 Code" = '' THEN
                                                                  EXIT;
                                                                IF ItemBudgetName.Name <> "Budget Name" THEN
                                                                  ItemBudgetName.GET("Analysis Area","Budget Name");
                                                                ValidateDimValue(ItemBudgetName."Budget Dimension 2 Code","Budget Dimension 2 Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              "Budget Dimension 2 Code" := OnLookupDimCode(3,"Budget Dimension 2 Code");
                                                            END;

                                                   CaptionML=[ENU=Budget Dimension 2 Code;
                                                              ESP=C¢d. dim. presupuesto 2];
                                                   CaptionClass=GetCaptionClass(2) }
    { 19  ;   ;Budget Dimension 3 Code;Code20     ;OnValidate=BEGIN
                                                                IF "Budget Dimension 3 Code" = '' THEN
                                                                  EXIT;
                                                                IF ItemBudgetName.Name <> "Budget Name" THEN
                                                                  ItemBudgetName.GET("Analysis Area","Budget Name");
                                                                ValidateDimValue(ItemBudgetName."Budget Dimension 3 Code","Budget Dimension 3 Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              "Budget Dimension 3 Code" := OnLookupDimCode(4,"Budget Dimension 3 Code");
                                                            END;

                                                   CaptionML=[ENU=Budget Dimension 3 Code;
                                                              ESP=C¢d. dim. presupuesto 3];
                                                   CaptionClass=GetCaptionClass(3) }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Analysis Area,Budget Name,Item No.,Source Type,Source No.,Date,Location Code,Global Dimension 1 Code,Global Dimension 2 Code,Budget Dimension 1 Code,Budget Dimension 2 Code,Budget Dimension 3 Code;
                                                   SumIndexFields=Cost Amount,Sales Amount,Quantity }
    {    ;Analysis Area,Budget Name,Source Type,Source No.,Item No.,Date,Location Code,Global Dimension 1 Code,Global Dimension 2 Code,Budget Dimension 1 Code,Budget Dimension 2 Code,Budget Dimension 3 Code;
                                                   SumIndexFields=Cost Amount,Sales Amount,Quantity }
    {    ;Analysis Area,Budget Name,Item No.,Date  }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=The dimension value %1 has not been set up for dimension %2.;ESP=El valor dimensi¢n %1 no ha sido config. para la dimensi¢n %2.';
      Text001@1001 : TextConst 'ENU=1,5,,Budget Dimension 1 Code;ESP=1,5,,C¢digo Dimensi¢n Presupuesto 1';
      Text002@1002 : TextConst 'ENU=1,5,,Budget Dimension 2 Code;ESP=1,5,,C¢digo Dimensi¢n Presupuesto 2';
      Text003@1003 : TextConst 'ENU=1,5,,Budget Dimension 3 Code;ESP=1,5,,C¢digo Dimensi¢n Presupuesto 3';
      ItemBudgetName@1005 : Record 7132;
      GLSetup@1006 : Record 98;
      InventorySetup@1008 : Record 313;
      SalesSetup@1011 : Record 311;
      GLSetupRetrieved@1007 : Boolean;
      InventorySetupRetrieved@1010 : Boolean;
      SalesSetupRetrieved@1012 : Boolean;

    PROCEDURE CheckIfBlocked@1();
    BEGIN
      IF "Budget Name" = ItemBudgetName.Name THEN
        EXIT;
      IF ItemBudgetName.Name <> "Budget Name" THEN
        ItemBudgetName.GET("Analysis Area","Budget Name");
      ItemBudgetName.TESTFIELD(Blocked,FALSE);
    END;

    LOCAL PROCEDURE ValidateDimValue@5(DimCode@1000 : Code[20];VAR DimValueCode@1001 : Code[20]) : Boolean;
    VAR
      DimValue@1002 : Record 349;
    BEGIN
      DimValue."Dimension Code" := DimCode;
      DimValue.Code := DimValueCode;
      DimValue.FIND('=><');
      IF DimValueCode <> COPYSTR(DimValue.Code,1,STRLEN(DimValueCode)) THEN
        ERROR(Text000,DimValueCode,DimCode);
      DimValueCode := DimValue.Code;
    END;

    PROCEDURE GetGLSetup@2();
    BEGIN
      IF NOT GLSetupRetrieved THEN BEGIN
        GLSetup.GET;
        GLSetupRetrieved := TRUE;
      END;
    END;

    PROCEDURE GetInventorySetup@8();
    BEGIN
      IF NOT InventorySetupRetrieved THEN BEGIN
        InventorySetup.GET;
        InventorySetupRetrieved := TRUE;
      END;
    END;

    PROCEDURE GetSalesSetup@9();
    BEGIN
      IF NOT SalesSetupRetrieved THEN BEGIN
        SalesSetup.GET;
        SalesSetupRetrieved := TRUE;
      END;
    END;

    PROCEDURE OnLookupDimCode@3(DimOption@1000 : 'Global Dimension 1,Global Dimension 2,Budget Dimension 1,Budget Dimension 2,Budget Dimension 3,Budget Dimension 4';DefaultValue@1001 : Code[20]) : Code[20];
    VAR
      DimValue@1002 : Record 349;
      DimValueList@1003 : Form 560;
    BEGIN
      IF DimOption IN [DimOption::"Global Dimension 1",DimOption::"Global Dimension 2"] THEN
        GetGLSetup
      ELSE
        IF ItemBudgetName.Name <> "Budget Name" THEN
          ItemBudgetName.GET("Analysis Area","Budget Name");
      CASE DimOption OF
        DimOption::"Global Dimension 1" :
          DimValue."Dimension Code" := GLSetup."Global Dimension 1 Code";
        DimOption::"Global Dimension 2" :
          DimValue."Dimension Code" := GLSetup."Global Dimension 2 Code";
        DimOption::"Budget Dimension 1" :
          DimValue."Dimension Code" := ItemBudgetName."Budget Dimension 1 Code";
        DimOption::"Budget Dimension 2" :
          DimValue."Dimension Code" := ItemBudgetName."Budget Dimension 2 Code";
        DimOption::"Budget Dimension 3" :
          DimValue."Dimension Code" := ItemBudgetName."Budget Dimension 3 Code";
      END;
      DimValue.SETRANGE("Dimension Code",DimValue."Dimension Code");
      IF DimValue.GET(DimValue."Dimension Code",DefaultValue) THEN;
      DimValueList.SETTABLEVIEW(DimValue);
      DimValueList.SETRECORD(DimValue);
      DimValueList.LOOKUPMODE := TRUE;
      IF DimValueList.RUNMODAL = ACTION::LookupOK THEN BEGIN
        DimValueList.GETRECORD(DimValue);
        EXIT(DimValue.Code);
      END ELSE
        EXIT(DefaultValue);
    END;

    PROCEDURE UpdateDim@6(DimCode@1000 : Code[20];DimValueCode@1001 : Code[20]);
    VAR
      ItemBudgetDim@1002 : Record 7135;
    BEGIN
      IF DimCode = '' THEN
        EXIT;
      WITH ItemBudgetDim DO BEGIN
        IF GET(Rec."Entry No.",DimCode) THEN
          DELETE;
        IF DimValueCode <> '' THEN BEGIN
          INIT;
          "Entry No." := Rec."Entry No.";
          "Dimension Code" := DimCode;
          "Dimension Value Code" := DimValueCode;
          INSERT;
        END;
      END;
    END;

    LOCAL PROCEDURE GetNextEntryNo@4() : Integer;
    VAR
      ItemBudgetEntry@1000 : Record 7134;
    BEGIN
      ItemBudgetEntry.SETCURRENTKEY("Entry No.");
      IF ItemBudgetEntry.FIND('+') THEN
        EXIT(ItemBudgetEntry."Entry No." + 1)
      ELSE
        EXIT(1);
    END;

    PROCEDURE GetCaptionClass@7(BudgetDimType@1000 : Integer) : Text[250];
    BEGIN
      IF (ItemBudgetName."Analysis Area" <> "Analysis Area") OR
         (ItemBudgetName.Name <> "Budget Name")
      THEN
        IF NOT ItemBudgetName.GET("Analysis Area","Budget Name") THEN
          EXIT('');
      CASE BudgetDimType OF
        1:
          BEGIN
            IF ItemBudgetName."Budget Dimension 1 Code" <> '' THEN
              EXIT('1,5,' + ItemBudgetName."Budget Dimension 1 Code");
            EXIT(Text001);
          END;
        2:
          BEGIN
            IF ItemBudgetName."Budget Dimension 2 Code" <> '' THEN
              EXIT('1,5,' + ItemBudgetName."Budget Dimension 2 Code");
            EXIT(Text002);
          END;
        3:
          BEGIN
            IF ItemBudgetName."Budget Dimension 3 Code" <> '' THEN
              EXIT('1,5,' + ItemBudgetName."Budget Dimension 3 Code");
            EXIT(Text003);
          END;
      END;
    END;

    PROCEDURE CheckGroupDimFilled@10(GroupDimCode@1000 : Code[20]) : Boolean;
    BEGIN
      IF GroupDimCode <> '' THEN
        CASE GroupDimCode OF
          GLSetup."Global Dimension 1 Code":
            EXIT("Global Dimension 1 Code" <> '');
          GLSetup."Global Dimension 2 Code":
            EXIT("Global Dimension 2 Code" <> '');
          ItemBudgetName."Budget Dimension 1 Code":
            EXIT("Budget Dimension 1 Code" <> '');
          ItemBudgetName."Budget Dimension 2 Code":
            EXIT("Budget Dimension 1 Code" <> '');
          ItemBudgetName."Budget Dimension 3 Code":
            EXIT("Budget Dimension 1 Code" <> '');
        END;
    END;

    PROCEDURE GetCaption@11() : Text[1024];
    VAR
      GLSetup@1008 : Record 98;
      ItemBudgetName@1009 : Record 7132;
      Cust@1000 : Record 18;
      Vend@1004 : Record 23;
      Item@1005 : Record 27;
      Dimension@1007 : Record 348;
      DimValue@1006 : Record 349;
      SourceTableCaption@1001 : Text[250];
      SourceFilter@1002 : Text[250];
      Description@1003 : Text[250];
    BEGIN
      CASE TRUE OF
        GETFILTER("Source No.") <> '':
          CASE "Source Type" OF
            "Source Type"::Customer:
              BEGIN
                SourceTableCaption := Cust.TABLECAPTION;
                SourceFilter := GETFILTER("Source No.");
                IF MAXSTRLEN(Cust."No.") >= STRLEN(SourceFilter) THEN
                  IF Cust.GET(SourceFilter) THEN
                    Description := Cust.Name;
              END;
            "Source Type"::Vendor:
              BEGIN
                SourceTableCaption := Vend.TABLECAPTION;
                SourceFilter := GETFILTER("Source No.");
                IF MAXSTRLEN(Vend."No.") >= STRLEN(SourceFilter) THEN
                  IF Vend.GET(SourceFilter) THEN
                    Description := Vend.Name;
              END;
          END;
        GETFILTER("Item No.") <> '':
          BEGIN
            SourceTableCaption := Item.TABLECAPTION;
            SourceFilter := GETFILTER("Item No.");
            IF MAXSTRLEN(Item."No.") >= STRLEN(SourceFilter) THEN
              IF Item.GET(SourceFilter) THEN
                Description := Item.Description;
          END;
        GETFILTER("Global Dimension 1 Code") <> '':
          BEGIN
            GLSetup.GET;
            Dimension.Code := GLSetup."Global Dimension 1 Code";
            SourceFilter := GETFILTER("Global Dimension 1 Code");
            SourceTableCaption := Dimension.GetMLName(GLOBALLANGUAGE);
            IF MAXSTRLEN(DimValue.Code) >= STRLEN(SourceFilter) THEN
              IF DimValue.GET(GLSetup."Global Dimension 1 Code",SourceFilter) THEN
                Description := DimValue.Name;
          END;
        GETFILTER("Global Dimension 2 Code") <> '':
          BEGIN
            GLSetup.GET;
            Dimension.Code := GLSetup."Global Dimension 2 Code";
            SourceFilter := GETFILTER("Global Dimension 2 Code");
            SourceTableCaption := Dimension.GetMLName(GLOBALLANGUAGE);
            IF MAXSTRLEN(DimValue.Code) >= STRLEN(SourceFilter) THEN
              IF DimValue.GET(GLSetup."Global Dimension 2 Code",SourceFilter) THEN
                Description := DimValue.Name;
          END;
        GETFILTER("Budget Dimension 1 Code") <> '':
          IF ItemBudgetName.GET("Analysis Area","Budget Name") THEN BEGIN
            Dimension.Code := ItemBudgetName."Budget Dimension 1 Code";
            SourceFilter := GETFILTER("Budget Dimension 1 Code");
            SourceTableCaption := Dimension.GetMLName(GLOBALLANGUAGE);
            IF MAXSTRLEN(DimValue.Code) >= STRLEN(SourceFilter) THEN
              IF DimValue.GET(ItemBudgetName."Budget Dimension 1 Code",SourceFilter) THEN
                Description := DimValue.Name;
          END;
        GETFILTER("Budget Dimension 2 Code") <> '':
          IF ItemBudgetName.GET("Analysis Area","Budget Name") THEN BEGIN
            Dimension.Code := ItemBudgetName."Budget Dimension 2 Code";
            SourceFilter := GETFILTER("Budget Dimension 2 Code");
            SourceTableCaption := Dimension.GetMLName(GLOBALLANGUAGE);
            IF MAXSTRLEN(DimValue.Code) >= STRLEN(SourceFilter) THEN
              IF DimValue.GET(ItemBudgetName."Budget Dimension 2 Code",SourceFilter) THEN
                Description := DimValue.Name;
          END;
        GETFILTER("Budget Dimension 3 Code") <> '':
          IF ItemBudgetName.GET("Analysis Area","Budget Name") THEN BEGIN
            Dimension.Code := ItemBudgetName."Budget Dimension 3 Code";
            SourceFilter := GETFILTER("Budget Dimension 3 Code");
            SourceTableCaption := Dimension.GetMLName(GLOBALLANGUAGE);
            IF MAXSTRLEN(DimValue.Code) >= STRLEN(SourceFilter) THEN
              IF DimValue.GET(ItemBudgetName."Budget Dimension 3 Code",SourceFilter) THEN
                Description := DimValue.Name;
          END;
      END;

      EXIT(
        DELCHR(
          STRSUBSTNO('%1 %2 %3 %4',SourceTableCaption,SourceFilter,Description,"Budget Name"),
          '>'));
    END;

    BEGIN
    END.
  }
}
