OBJECT Table 5104 Segment Interaction Language
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               Cont@1003 : Record 5050;
               SegHeader@1002 : Record 5076;
               SegLine@1001 : Record 5077;
               SegInteractLanguage@1000 : Record 5104;
               FirstSegIntLanguage@1004 : Boolean;
             BEGIN
               IF "Segment Line No." = 0 THEN BEGIN
                 SegInteractLanguage.SETRANGE("Segment No.","Segment No.");
                 SegInteractLanguage.SETRANGE("Segment Line No.",0);
                 FirstSegIntLanguage := NOT SegInteractLanguage.FIND('-');

                 SegLine.SETRANGE("Segment No.","Segment No.");
                 SegHeader.GET("Segment No.");
                 SegLine.SETRANGE("Interaction Template Code",SegHeader."Interaction Template Code");
                 IF SegLine.FIND('-') THEN
                   REPEAT
                     IF FirstSegIntLanguage THEN BEGIN
                       IF SegLine.AttachmentInherited OR
                          (SegLine."Attachment No." = 0)
                       THEN BEGIN
                         SegLine."Language Code" := "Language Code";
                         SegLine.Subject := Subject;
                         SegLine."Attachment No." := "Attachment No.";
                         SegLine.MODIFY;
                       END
                     END ELSE
                       IF SegLine.AttachmentInherited OR (SegLine."Attachment No." = 0) THEN
                         IF Cont.GET(SegLine."Contact No.") THEN
                           IF Cont."Language Code" = "Language Code" THEN BEGIN
                             SegLine."Language Code" := "Language Code";
                             SegLine.Subject := Subject;
                             SegLine."Attachment No." := "Attachment No.";
                             SegLine.MODIFY;
                           END;
                   UNTIL SegLine.NEXT = 0;
               END ELSE BEGIN
                 SegLine.GET("Segment No.","Segment Line No.");
                 SegLine."Language Code" := "Language Code";
                 SegLine.Subject := Subject;
                 SegLine."Attachment No." := "Attachment No.";
                 SegLine.MODIFY;
               END;
             END;

    OnDelete=VAR
               SegHeader@1000 : Record 5076;
               SegLine@1001 : Record 5077;
               Cont@1002 : Record 5050;
               SegInteractLanguage@1003 : Record 5104;
             BEGIN
               SegHeader.GET("Segment No.");
               IF ("Segment Line No." = 0) AND
                  ("Language Code" = SegHeader."Language Code (Default)")
               THEN BEGIN
                 SegInteractLanguage.SETRANGE("Segment No.","Segment No.");
                 SegInteractLanguage.SETRANGE("Segment Line No.",0);
                 SegInteractLanguage.SETFILTER("Language Code",'<>%1',"Language Code");
                 IF SegInteractLanguage.FIND('-') THEN BEGIN
                   SegHeader."Language Code (Default)" := SegInteractLanguage."Language Code";
                   SegHeader."Subject (Default)" := SegInteractLanguage.Subject;
                 END ELSE BEGIN
                   SegHeader."Language Code (Default)" := '';
                   SegHeader."Subject (Default)" := '';
                 END;
                 SegHeader.MODIFY;
               END;
               SegHeader.CALCFIELDS("Attachment No.");

               IF "Segment Line No." = 0 THEN BEGIN
                 SegLine.SETRANGE("Segment No.","Segment No.");
                 SegLine.SETRANGE("Attachment No.","Attachment No.");
                 IF SegLine.FIND('-') THEN
                   REPEAT
                     IF Cont.GET(SegLine."Contact No.") THEN BEGIN
                       IF (Cont."Language Code" <> "Language Code") AND
                         SegInteractLanguage.GET("Segment No.",0,Cont."Language Code")
                       THEN BEGIN
                         SegLine."Language Code" := Cont."Language Code";
                         SegLine.Subject := SegInteractLanguage.Subject;
                         SegLine."Attachment No." := SegInteractLanguage."Attachment No.";
                       END ELSE BEGIN
                         SegLine."Language Code" := SegHeader."Language Code (Default)";
                         SegLine.Subject := SegHeader."Subject (Default)";
                         SegLine."Attachment No." := SegHeader."Attachment No.";
                       END;
                       SegLine.MODIFY;
                     END;
                   UNTIL SegLine.NEXT = 0;
               END ELSE BEGIN //UNIQUE Attachment
                 SegLine.GET("Segment No.","Segment Line No.");
                 IF SegLine."Attachment No." = "Attachment No." THEN BEGIN
                   SegInteractLanguage.SETRANGE("Segment No.","Segment No.");
                   SegInteractLanguage.SETRANGE("Segment Line No.","Segment Line No.");
                   SegInteractLanguage.SETFILTER("Language Code",'<>%1',"Language Code");
                   IF SegInteractLanguage.FIND('-') THEN BEGIN
                     SegLine."Language Code" := SegInteractLanguage."Language Code";
                     SegLine.Subject := SegInteractLanguage.Subject;
                     SegLine."Attachment No." := SegInteractLanguage."Attachment No.";
                   END ELSE BEGIN
                     IF SegLine."Interaction Template Code" = SegHeader."Interaction Template Code" THEN BEGIN
                       Cont.GET(SegLine."Contact No.");
                       IF SegInteractLanguage.GET("Segment No.",0,Cont."Language Code") THEN BEGIN
                         SegLine."Language Code" := Cont."Language Code";
                         SegLine.Subject := SegInteractLanguage.Subject;
                         SegLine."Attachment No." := SegInteractLanguage."Attachment No.";
                       END ELSE BEGIN
                         SegLine."Language Code" := SegHeader."Language Code (Default)";
                         SegLine.Subject := SegHeader."Subject (Default)";
                         SegLine."Attachment No." := SegHeader."Attachment No.";
                       END;
                     END ELSE BEGIN
                       SegLine."Language Code" := '';
                       SegLine.Subject := '';
                       SegLine."Attachment No." := 0;
                     END;
                   END;
                   SegLine.MODIFY;
                 END;
               END;

               RemoveAttachment(FALSE);
             END;

    OnRename=VAR
               SegHeader@1001 : Record 5076;
               SegLine@1000 : Record 5077;
               Cont@1002 : Record 5050;
             BEGIN
               SegHeader.GET("Segment No.");
               SegHeader.CALCFIELDS("Attachment No.");
               IF ("Segment Line No." = 0) AND
                  (SegHeader."Language Code (Default)" = xRec."Language Code")
               THEN BEGIN
                 SegHeader."Language Code (Default)" := "Language Code";
                 SegHeader.MODIFY;
               END;

               IF "Segment Line No." = 0 THEN BEGIN
                 SegLine.SETRANGE("Segment No.","Segment No.");
                 SegLine.SETRANGE("Attachment No.","Attachment No.");
                 IF SegLine.FIND('-') THEN
                   REPEAT
                     SegLine."Language Code" := SegHeader."Language Code (Default)";
                     SegLine."Attachment No." := SegHeader."Attachment No.";
                     SegLine.MODIFY;
                   UNTIL SegLine.NEXT = 0;

                 SegLine.RESET;
                 SegLine.SETRANGE("Segment No.","Segment No.");
                 SegLine.SETRANGE("Interaction Template Code",SegHeader."Interaction Template Code");
                 IF SegLine.FIND('-') THEN
                   REPEAT
                     IF SegLine.AttachmentInherited OR (SegLine."Attachment No." = 0) THEN
                       IF Cont.GET(SegLine."Contact No.") THEN
                         IF Cont."Language Code" = "Language Code" THEN BEGIN
                           SegLine."Language Code" := "Language Code";
                           SegLine.Subject := Subject;
                           SegLine."Attachment No." := "Attachment No.";
                           SegLine.MODIFY;
                         END;
                   UNTIL SegLine.NEXT = 0;
               END ELSE BEGIN
                 SegLine.GET("Segment No.","Segment Line No.");
                 IF SegLine."Language Code" = xRec."Language Code" THEN BEGIN
                   SegLine."Language Code" := "Language Code";
                   SegLine."Attachment No." := "Attachment No.";
                   SegLine.MODIFY;
                 END;
               END;
             END;

    CaptionML=[ENU=Segment Interaction Language;
               ESP=Idioma interacci¢n segmento];
    LookupFormID=Form5155;
  }
  FIELDS
  {
    { 1   ;   ;Segment No.         ;Code20        ;TableRelation="Segment Header";
                                                   CaptionML=[ENU=Segment No.;
                                                              ESP=N§ segmento] }
    { 2   ;   ;Segment Line No.    ;Integer       ;TableRelation="Segment Line"."Line No." WHERE (Segment No.=FIELD(Segment No.));
                                                   CaptionML=[ENU=Segment Line No.;
                                                              ESP=N§ l¡n. segmento] }
    { 3   ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   CaptionML=[ENU=Language Code;
                                                              ESP=C¢d. idioma] }
    { 4   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n] }
    { 5   ;   ;Attachment No.      ;Integer       ;CaptionML=[ENU=Attachment No.;
                                                              ESP=N§ anexo] }
    { 6   ;   ;Subject             ;Text50        ;OnValidate=VAR
                                                                SegHeader@1000 : Record 5076;
                                                                SegLine@1001 : Record 5077;
                                                                UpdateLines@1002 : Boolean;
                                                              BEGIN
                                                                SegHeader.GET("Segment No.");
                                                                IF "Segment Line No." = 0 THEN
                                                                  IF SegHeader.SegLinesExist('') THEN
                                                                    UpdateLines := CONFIRM(STRSUBSTNO(Text005+Text006,FIELDCAPTION(Subject)),TRUE);

                                                                IF SegHeader."Language Code (Default)" = "Language Code" THEN
                                                                  IF SegHeader."Subject (Default)"  = xRec.Subject THEN BEGIN
                                                                    SegHeader."Subject (Default)" := Subject;
                                                                    SegHeader.MODIFY;
                                                                    MODIFY;
                                                                  END;

                                                                IF NOT UpdateLines THEN
                                                                  EXIT;

                                                                SegLine.SETRANGE("Segment No.","Segment No.");
                                                                IF "Segment Line No." = 0 THEN
                                                                  SegLine.SETRANGE("Interaction Template Code",SegHeader."Interaction Template Code")
                                                                ELSE BEGIN
                                                                  SegLine.GET("Segment No.","Segment Line No.");
                                                                  SegLine.SETRANGE("Interaction Template Code",SegLine."Interaction Template Code");
                                                                END;
                                                                SegLine.SETRANGE("Language Code","Language Code");
                                                                SegLine.SETRANGE(Subject,xRec.Subject);
                                                                SegLine.MODIFYALL(Subject,Subject);
                                                              END;

                                                   CaptionML=[ENU=Subject;
                                                              ESP=Asunto] }
  }
  KEYS
  {
    {    ;Segment No.,Segment Line No.,Language Code;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You have canceled the create process.;ESP=Ha cancelado el proceso crear.';
      Text001@1004 : TextConst 'ENU=Replace existing attachment?;ESP=¨Sustituir el anexo actual?';
      Text002@1003 : TextConst 'ENU=You have canceled the import process.;ESP=Ha cancelado el proceso importar.';
      Text003@1002 : TextConst 'ENU=Inherited;ESP=Heredado';
      Text004@1001 : TextConst 'ENU=Unique;ESP=Unico';
      Text005@1005 : TextConst 'ENU=You have modified %1.\\;ESP=Ha modificado %1.\\';
      Text006@1006 : TextConst 'ENU=Do you want to update the segment lines with the same Interaction Template Code and Language Code?;ESP=¨Confirma que desea actualizar las l¡ns. segmento con el mismo C¢d. plant. interacci¢n y C¢d. idioma?';

    PROCEDURE CreateAttachment@1();
    VAR
      SegInteractLanguage@1000 : Record 5104;
      AttachmentManagement@1001 : Codeunit 5052;
      WordManagement@1002 : Codeunit 5054;
      NewAttachmentNo@1003 : Integer;
    BEGIN
      IF "Attachment No." <> 0 THEN BEGIN
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
        RemoveAttachment(FALSE);
      END;

      IF AttachmentManagement.UseComServer('DOC',TRUE) THEN;

      NewAttachmentNo := WordManagement.CreateWordAttachment("Segment No." + ' ' + Description);
      IF NewAttachmentNo <> 0 THEN BEGIN
        "Attachment No." := NewAttachmentNo;
        IF SegInteractLanguage.GET("Segment No.","Segment Line No.","Language Code") THEN
          MODIFY
        ELSE
          INSERT(TRUE);
        UpdateSegLineAttachment(0);
      END ELSE
        ERROR(Text000);
    END;

    PROCEDURE OpenAttachment@2();
    VAR
      Attachment@1000 : Record 5062;
    BEGIN
      IF "Attachment No." = 0 THEN
        EXIT;
      Attachment.GET("Attachment No.");
      Attachment.OpenAttachment("Segment No." + ' ' + Description,FALSE);
    END;

    PROCEDURE CopyFromAttachment@3();
    VAR
      SegInteractLanguage@1004 : Record 5104;
      AttachmentManagement@1002 : Codeunit 5052;
      NewAttachNo@1001 : Integer;
    BEGIN
      IF "Attachment No." <> 0 THEN BEGIN
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
        RemoveAttachment(FALSE);
        COMMIT;
      END;

      SegInteractLanguage.SETRANGE("Segment No.","Segment No.");
      SegInteractLanguage.SETFILTER("Attachment No.",'<>%1&<>%2',0, "Attachment No.");
      IF FORM.RUNMODAL(0,SegInteractLanguage) = ACTION::LookupOK THEN BEGIN
        NewAttachNo := AttachmentManagement.InsertAttachment(SegInteractLanguage."Attachment No.");
        IF NewAttachNo <> 0 THEN BEGIN
          "Attachment No." := NewAttachNo;
          MODIFY;
          UpdateSegLineAttachment(0);
        END;
      END;
    END;

    PROCEDURE ImportAttachment@4();
    VAR
      Attachment@1003 : Record 5062;
      AttachmentManagement@1001 : Codeunit 5052;
    BEGIN
      IF "Attachment No." <> 0 THEN BEGIN
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
        RemoveAttachment(FALSE);
      END;

      IF Attachment.ImportAttachment('',FALSE,FALSE) THEN BEGIN
        "Attachment No." := Attachment."No.";
        MODIFY;
        UpdateSegLineAttachment(0);
      END ELSE
        ERROR(Text002);
    END;

    PROCEDURE ExportAttachment@5();
    VAR
      Attachment@1000 : Record 5062;
      FileName@1001 : Text[1024];
    BEGIN
      IF Attachment.GET("Attachment No.") THEN
        Attachment.ExportAttachment(FileName);
    END;

    PROCEDURE RemoveAttachment@6(Prompt@1000 : Boolean);
    VAR
      Attachment@1002 : Record 5062;
      OldAttachmentNo@1001 : Integer;
    BEGIN
      OldAttachmentNo := "Attachment No.";
      IF Attachment.GET("Attachment No.") AND (NOT Attachment."Read Only") THEN
        IF Attachment.RemoveAttachment(Prompt) THEN BEGIN
          "Attachment No." := 0;
          MODIFY;
        END;
      UpdateSegLineAttachment(OldAttachmentNo);
    END;

    PROCEDURE UpdateSegLineAttachment@7(OldAttachmentNo@1000 : Integer);
    VAR
      SegLine@1001 : Record 5077;
      SegHeader@1002 : Record 5076;
    BEGIN
      IF "Segment Line No." = 0 THEN BEGIN
        SegHeader.GET("Segment No.");
        SegLine.SETRANGE("Segment No.","Segment No.");
        SegLine.SETRANGE("Interaction Template Code",SegHeader."Interaction Template Code");
        SegLine.SETRANGE("Language Code","Language Code");
        SegLine.SETRANGE("Attachment No.",OldAttachmentNo);
        SegLine.MODIFYALL("Attachment No.","Attachment No.");
      END ELSE BEGIN
        SegLine.SETRANGE("Segment No.","Segment No.");
        SegLine.SETRANGE("Line No.","Segment Line No.");
        SegLine.SETRANGE("Attachment No.",OldAttachmentNo);
        IF SegLine.FIND('-') THEN BEGIN
          SegLine."Attachment No." := "Attachment No.";
          SegLine.MODIFY;
        END;
      END;
    END;

    PROCEDURE AttachmentText@9() : Text[30];
    BEGIN
      IF "Attachment No." = 0 THEN
        EXIT('')
      ELSE
        IF "Segment Line No." = 0 THEN
          EXIT(Text003)
        ELSE
          EXIT(Text004);
    END;

    PROCEDURE Caption@8() CaptionText@1002 : Text[260];
    VAR
      SegHeader@1003 : Record 5076;
      SegLine@1000 : Record 5077;
      Cont@1001 : Record 5050;
    BEGIN
      SegHeader.GET("Segment No.");
      CaptionText := FORMAT("Segment No.") +  ' ' + SegHeader.Description;
      IF "Segment Line No." <> 0 THEN BEGIN
        SegLine.GET("Segment No.","Segment Line No.");
        IF Cont.GET(SegLine."Contact No.") THEN;
        CaptionText := CaptionText + ' ' + FORMAT(SegLine."Contact No.") + ' ' + Cont.Name;
      END;
    END;

    BEGIN
    END.
  }
}
