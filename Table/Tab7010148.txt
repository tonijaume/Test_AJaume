OBJECT Table 7010148 Precios Venta Excursion
{
  OBJECT-PROPERTIES
  {
    Date=22/12/14;
    Time=12:27:10;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    OnInsert=VAR
               rPar@1103355000 : Record 7009700;
               rPrecio_Vta@1103355001 : Record 7010148;
               rTar@1103355002 : Record 7010147;
             BEGIN
               rPar.FINDFIRST;
               Delegacion := rPar.Delegacion;

               rPrecio_Vta.RESET;
               rPrecio_Vta.SETRANGE(Delegacion        , Delegacion);
               rPrecio_Vta.SETRANGE("Tipo precio"     , "Tipo precio");
               rPrecio_Vta.SETRANGE(Excursion         , Excursion);
               rPrecio_Vta.SETRANGE("Temporada Tarifa", "Temporada Tarifa");
               rPrecio_Vta.SETRANGE("ID Tarifa"       , "ID Tarifa");  //+$006
               IF rPrecio_Vta.FINDLAST THEN
                 Orden := rPrecio_Vta.Orden + 10000
               ELSE
                 Orden := 10000;

               IF NOT rPar."Busca tarifa por fecha venta" THEN
               BEGIN
                 IF rTar.GET(Delegacion, Excursion, "Temporada Tarifa") THEN //+$006
                 BEGIN
                   //-$005"C¢d. divisa" := rTar."C¢d. divisa";
                   IF "Fecha Desde" = 0D THEN
                     "Fecha Desde" := rTar."Fecha Inicio";
                   IF "Fecha Hasta" = 0D THEN
                     "Fecha Hasta" := rTar."Fecha Final";
                 END;
               END;

               Comisionable := TRUE;

               _ComparaPrecioExento; //+$001
             END;

    OnModify=BEGIN
               _ComparaPrecioExento; //+$001
             END;

    OnDelete=BEGIN
               cPar.intro_historico_precios_tarifa(Excursion, "Temporada Tarifa", rHisP.Tipo::Venta, "Tipo precio",
                                                   Orden, Text001, Text002, '', "ID Tarifa");
             END;

    CaptionML=[ENU=Excursion Sale prices;
               ESP=Precios Venta Excursion];
  }
  FIELDS
  {
    { 1   ;   ;Tipo precio         ;Option        ;CaptionML=[ENU=Price type;
                                                              ESP=Tipo Precio];
                                                   OptionCaptionML=[ENU=Base rate,Misc. Supplement,Location Supplement;
                                                                    ESP=Precio Base,Suplemento Varios,Suplemento Zona];
                                                   OptionString=Precio Base,Suplemento Varios,Suplemento Zona;
                                                   Description=PK;
                                                   Editable=No }
    { 2   ;   ;Delegacion          ;Code10        ;TableRelation=Delegacion.Codigo;
                                                   CaptionML=[ENU=Local Office;
                                                              ESP=Delegacion];
                                                   Description=PK, FK Delegacion;
                                                   Editable=No }
    { 3   ;   ;Excursion           ;Code10        ;TableRelation="Producto agencia".Codigo WHERE (Tipo Producto=CONST(Excursion));
                                                   CaptionML=[ENU=Product code;
                                                              ESP=Codigo producto];
                                                   Description=PK, FK producto agencia;
                                                   Editable=No }
    { 5   ;   ;Temporada Tarifa    ;Code10        ;TableRelation="Tarifa Excursion".Temporada WHERE (Excursion=FIELD(Excursion));
                                                   CaptionML=[ENU=Tariff Season;
                                                              ESP=Temporada Tarifa];
                                                   Description=PK, FK Tarifa excursion;
                                                   Editable=No }
    { 6   ;   ;Orden               ;Integer       ;CaptionML=[ENU=Order;
                                                              ESP=Orden];
                                                   Editable=No }
    { 7   ;   ;Fecha Desde         ;Date          ;OnValidate=BEGIN
                                                                IF "Fecha Hasta" <> 0D THEN
                                                                  IF "Fecha Desde" > "Fecha Hasta" THEN
                                                                    ERROR(Text003);

                                                                IF "Fecha Desde" <> xRec."Fecha Desde" THEN
                                                                  cPar.intro_historico_precios_tarifa(Excursion, "Temporada Tarifa", rHisP.Tipo::Venta, "Tipo precio", Orden,
                                                                                                      FIELDCAPTION("Fecha Desde"), FORMAT(xRec."Fecha Desde"), FORMAT("Fecha Desde"), "ID Tarifa");
                                                                VALIDATE(Activo, FALSE);
                                                              END;

                                                   CaptionML=[ENU=Date From;
                                                              ESP=Fecha desde] }
    { 9   ;   ;Fecha Hasta         ;Date          ;OnValidate=BEGIN
                                                                IF "Fecha Desde" <> 0D THEN
                                                                  IF "Fecha Hasta" < "Fecha Desde" THEN
                                                                    ERROR(Text004);

                                                                IF "Fecha Hasta" <> xRec."Fecha Hasta" THEN
                                                                  cPar.intro_historico_precios_tarifa(Excursion, "Temporada Tarifa", rHisP.Tipo::Venta, "Tipo precio", Orden,
                                                                                                      FIELDCAPTION("Fecha Hasta"), FORMAT(xRec."Fecha Hasta"), FORMAT("Fecha Hasta"), "ID Tarifa");
                                                                VALIDATE(Activo, FALSE);
                                                              END;

                                                   CaptionML=[ENU=Date To;
                                                              ESP=Fecha hasta] }
    { 15  ;   ;Codigo              ;Code10        ;TableRelation=IF (Tipo precio=CONST(Precio Base)) "Tipo elemento"."Tipo elemento" WHERE (Humano=CONST(Yes))
                                                                 ELSE IF (Tipo precio=CONST(Suplemento Varios)) "Suplemento Excursiones"."Codigo Suplemento" WHERE (Tipo Producto=CONST(Excursion))
                                                                 ELSE IF (Tipo precio=CONST(Suplemento Zona)) "Zonas Excursiones".Zona WHERE (Codigo Contrato=FIELD(Temporada Tarifa),
                                                                                                                                              Excursion=FIELD(Excursion),
                                                                                                                                              Tipo Contrato=CONST(Venta));
                                                   OnValidate=VAR
                                                                lrSuplem@1100217000 : Record 7010143;
                                                              BEGIN
                                                                IF Codigo <> xRec.Codigo THEN BEGIN
                                                                  //-$005IF rTar.GET(Delegacion, Excursion, "Temporada Tarifa") THEN BEGIN
                                                                  //-$005  "C¢d. divisa" := rTar."C¢d. divisa";
                                                                  //-$005  "Fecha cambio divisa" := rTar."Fecha cambio divisa";
                                                                  //-$005END;

                                                                  IF "Tipo precio" = "Tipo precio"::"Suplemento Varios" THEN
                                                                    VALIDATE("Permite precio manual", lrSuplem.ComprobarPrecioManual(Codigo));

                                                                  cPar.intro_historico_precios_tarifa(Excursion, "Temporada Tarifa", rHisP.Tipo::Venta, "Tipo precio", Orden,
                                                                                                      FIELDCAPTION(Codigo), FORMAT(xRec.Codigo), FORMAT(Codigo), "ID Tarifa");
                                                                  VALIDATE(Activo, FALSE);
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              lrTipoElem@1100253000 : Record 7009715;
                                                              lrSuplem@1100253001 : Record 7010143;
                                                              lrZonas@1100253002 : Record 7010149;
                                                            BEGIN
                                                              CASE "Tipo precio" OF
                                                                "Tipo precio"::"Precio Base":
                                                                BEGIN
                                                                  lrTipoElem.RESET;
                                                                  IF FORM.RUNMODAL(7009844, lrTipoElem) = ACTION::LookupOK THEN
                                                                    VALIDATE(Codigo, lrTipoElem."Tipo elemento");
                                                                END;
                                                                "Tipo precio"::"Suplemento Varios":
                                                                BEGIN
                                                                  lrSuplem.RESET;
                                                                  lrSuplem.SETRANGE("Tipo Producto", lrSuplem."Tipo Producto"::Excursion);
                                                                  IF FORM.RUNMODAL(7010147, lrSuplem) = ACTION::LookupOK THEN
                                                                    VALIDATE(Codigo, lrSuplem."Codigo Suplemento");
                                                                END;
                                                                "Tipo precio"::"Suplemento Zona":
                                                                BEGIN
                                                                  lrZonas.RESET;
                                                                  lrZonas.SETRANGE("Codigo Contrato", "Temporada Tarifa");
                                                                  lrZonas.SETRANGE(Excursion        , Excursion);
                                                                  lrZonas.SETRANGE("Tipo Contrato"  , lrZonas."Tipo Contrato"::Venta);
                                                                  lrZonas.SETRANGE("ID Tarifa"      , "ID Tarifa"); //+$006
                                                                  IF FORM.RUNMODAL(7010157, lrZonas) = ACTION::LookupOK THEN
                                                                    VALIDATE(Codigo, lrZonas.Zona);
                                                                END;
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Code;
                                                              ESP=Codigo];
                                                   Description=FK varios }
    { 16  ;   ;Precio              ;Decimal       ;OnValidate=BEGIN
                                                                cPar.intro_historico_precios_tarifa(Excursion, "Temporada Tarifa", rHisP.Tipo::Venta, "Tipo precio", Orden,
                                                                                                    FIELDCAPTION(Precio), FORMAT(xRec.Precio), FORMAT(Precio), "ID Tarifa");
                                                              END;

                                                   CaptionML=[ENU=Price;
                                                              ESP=Precio];
                                                   AutoFormatType=2;
                                                   AutoFormatExpr=_GetDivisa }
    { 17  ;   ;O_Precio (DL)       ;Decimal       ;CaptionML=[ENU=Price (LCY);
                                                              ESP=Precio (DL)];
                                                   Description=$005;
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 19  ;   ;O_C¢d. divisa       ;Code10        ;TableRelation=Currency.Code;
                                                   CaptionML=[ENU=Currency code;
                                                              ESP=C¢d. divisa];
                                                   Description=$005, FK Divisa;
                                                   Editable=No }
    { 21  ;   ;Acumula al precio   ;Boolean       ;OnValidate=BEGIN
                                                                IF "Acumula al precio" <> xRec."Acumula al precio" THEN
                                                                  cPar.intro_historico_precios_tarifa(Excursion, "Temporada Tarifa", rHisP.Tipo::Venta, "Tipo precio", Orden,
                                                                                                      FIELDCAPTION("Acumula al precio"), FORMAT(xRec."Acumula al precio"),
                                                                                                      FORMAT("Acumula al precio"), "ID Tarifa");
                                                              END;

                                                   CaptionML=[ENU=Sum to price;
                                                              ESP=Acumula al precio] }
    { 23  ;   ;% Porcentaje        ;Boolean       ;OnValidate=BEGIN
                                                                // $003
                                                                IF Free AND (NOT "% Porcentaje") THEN
                                                                  ERROR(Text011);


                                                                IF "% Porcentaje" <> xRec."% Porcentaje" THEN
                                                                  cPar.intro_historico_precios_tarifa(Excursion, "Temporada Tarifa", rHisP.Tipo::Venta, "Tipo precio", Orden,
                                                                                                      FIELDCAPTION("% Porcentaje"), FORMAT(xRec."% Porcentaje"),
                                                                                                      FORMAT("% Porcentaje"), "ID Tarifa");
                                                              END;

                                                   CaptionML=[ENU=% Percentage;
                                                              ESP=% Porcentaje] }
    { 25  ;   ;Comisionable        ;Boolean       ;OnValidate=BEGIN
                                                                IF Comisionable <> xRec.Comisionable THEN
                                                                  cPar.intro_historico_precios_tarifa(Excursion, "Temporada Tarifa", rHisP.Tipo::Venta, "Tipo precio", Orden,
                                                                                                      FIELDCAPTION(Comisionable), FORMAT(xRec.Comisionable), FORMAT(Comisionable), "ID Tarifa");
                                                              END;

                                                   CaptionML=[ENU=Commissionable;
                                                              ESP=Comisionable] }
    { 27  ;   ;Tipo Persona        ;Code10        ;OnValidate=BEGIN
                                                                IF "Tipo Persona" <> xRec."Tipo Persona" THEN
                                                                  cPar.intro_historico_precios_tarifa(Excursion, "Temporada Tarifa", rHisP.Tipo::Venta, "Tipo precio", Orden,
                                                                                                      FIELDCAPTION("Tipo Persona"), FORMAT(xRec."Tipo Persona"),
                                                                                                      FORMAT("Tipo Persona"), "ID Tarifa");
                                                              END;

                                                   CaptionML=[ENU=Person type;
                                                              ESP=Tipo Persona] }
    { 30  ;   ;Importe Gastos Anulacion;Decimal   ;OnValidate=BEGIN
                                                                cPar.intro_historico_precios_tarifa(Excursion, "Temporada Tarifa", rHisP.Tipo::Venta, "Tipo precio", Orden,
                                                                                                    FIELDCAPTION("Importe Gastos Anulacion"), FORMAT(xRec."Importe Gastos Anulacion"),
                                                                                                    FORMAT("Importe Gastos Anulacion"), "ID Tarifa");
                                                              END;

                                                   CaptionML=[ENU=Cancellation Expenses amount;
                                                              ESP=Importe Gastos Anulacion];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=_GetDivisa }
    { 31  ;   ;O_Impor Gastos Anulacion (DL);Decimal;
                                                   CaptionML=[ENU=Cancellation Expenses amount (LCY);
                                                              ESP=Importe Gastos Anulacion (DL)];
                                                   Description=$005;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 32  ;   ;Precio Adulto       ;Decimal       ;OnValidate=BEGIN
                                                                // $003
                                                                IF Free AND ("Precio Adulto" <> -100) THEN
                                                                  ERROR(Text011);

                                                                cPar.intro_historico_precios_tarifa(Excursion, "Temporada Tarifa", rHisP.Tipo::Venta, "Tipo precio", Orden,
                                                                                                    FIELDCAPTION("Precio Adulto"), FORMAT(xRec."Precio Adulto"),
                                                                                                    FORMAT("Precio Adulto"), "ID Tarifa");
                                                              END;

                                                   CaptionML=[ENU=Adult price;
                                                              ESP=Precio Adulto];
                                                   AutoFormatType=2;
                                                   AutoFormatExpr=_GetDivisa }
    { 33  ;   ;O_Precio Adulto (DL);Decimal       ;CaptionML=[ENU=Adult price (LCY);
                                                              ESP=Precio Adulto (DL)];
                                                   Description=$005;
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 34  ;   ;Precio Ni¤o         ;Decimal       ;OnValidate=BEGIN
                                                                // $003
                                                                IF Free AND ("Precio Ni¤o" <> -100) THEN
                                                                  ERROR(Text011);


                                                                cPar.intro_historico_precios_tarifa(Excursion, "Temporada Tarifa", rHisP.Tipo::Venta, "Tipo precio", Orden,
                                                                                                    FIELDCAPTION("Precio Ni¤o"), FORMAT(xRec."Precio Ni¤o"),
                                                                                                    FORMAT("Precio Ni¤o"), "ID Tarifa");
                                                              END;

                                                   CaptionML=[ENU=Children price;
                                                              ESP=Precio Ni¤o];
                                                   AutoFormatType=2;
                                                   AutoFormatExpr=_GetDivisa }
    { 35  ;   ;O_Precio Ni¤o (DL)  ;Decimal       ;CaptionML=[ENU=Child Price (LCY);
                                                              ESP=Precio Ni¤o (DL)];
                                                   Description=$005;
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 37  ;   ;Automatico          ;Boolean       ;OnValidate=BEGIN
                                                                IF Automatico <> xRec.Automatico THEN
                                                                  cPar.intro_historico_precios_tarifa(Excursion, "Temporada Tarifa", rHisP.Tipo::Venta, "Tipo precio", Orden,
                                                                                                      FIELDCAPTION(Automatico), FORMAT(xRec.Automatico), FORMAT(Automatico), "ID Tarifa");
                                                              END;

                                                   CaptionML=[ENU=Automatic;
                                                              ESP=Automatico] }
    { 39  ;   ;Activo              ;Boolean       ;InitValue=No;
                                                   OnValidate=BEGIN
                                                                Comprobar_Tarifa;
                                                                Comprobar_Solape;
                                                              END;

                                                   CaptionML=[ENU=Active;
                                                              ESP=Activo] }
    { 41  ;   ;O_Fecha cambio divisa;Date         ;CaptionML=[ENU=Currency exchange date;
                                                              ESP=Fecha cambio divisa];
                                                   Description=$005;
                                                   Editable=No }
    { 42  ;   ;Precio exento       ;Decimal       ;OnValidate=BEGIN
                                                                IF "Precio exento" <> xRec."Precio exento" THEN
                                                                  cPar.intro_historico_precios_tarifa(Excursion, "Temporada Tarifa", rHisP.Tipo::Venta, "Tipo precio", Orden,
                                                                                                      FIELDCAPTION("Precio exento"), FORMAT(xRec."Precio exento"),
                                                                                                      FORMAT("Precio exento"), "ID Tarifa");
                                                              END;

                                                   CaptionML=[ENU=Exempted price;
                                                              ESP=Precio exento];
                                                   Description=$001;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr=_GetDivisa }
    { 43  ;   ;Precio adulto exento;Decimal       ;OnValidate=BEGIN
                                                                IF "% Porcentaje" THEN
                                                                  ERROR(Text010);

                                                                IF "Precio adulto exento" <> xRec."Precio adulto exento" THEN
                                                                  cPar.intro_historico_precios_tarifa(Excursion, "Temporada Tarifa", rHisP.Tipo::Venta, "Tipo precio", Orden,
                                                                                                      FIELDCAPTION("Precio adulto exento"), FORMAT(xRec."Precio adulto exento"),
                                                                                                      FORMAT("Precio adulto exento"), "ID Tarifa");
                                                              END;

                                                   CaptionML=[ENU=Exempted adult price;
                                                              ESP=Precio adulto exento];
                                                   Description=$001 }
    { 44  ;   ;Precio ni¤o exento  ;Decimal       ;OnValidate=BEGIN
                                                                IF "% Porcentaje" THEN
                                                                  ERROR(Text010);

                                                                IF "Precio ni¤o exento" <> xRec."Precio ni¤o exento" THEN
                                                                  cPar.intro_historico_precios_tarifa(Excursion, "Temporada Tarifa", rHisP.Tipo::Venta, "Tipo precio", Orden,
                                                                                                      FIELDCAPTION("Precio ni¤o exento"), FORMAT(xRec."Precio ni¤o exento"),
                                                                                                      FORMAT("Precio ni¤o exento"), "ID Tarifa");
                                                              END;

                                                   CaptionML=[ENU=Exempted child price;
                                                              ESP=Precio ni¤o exento];
                                                   Description=$001;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr=_GetDivisa }
    { 50  ;   ;Free                ;Boolean       ;OnValidate=BEGIN
                                                                IF Free THEN BEGIN
                                                                  IF NOT CONFIRM(Text012) THEN
                                                                    EXIT;
                                                                  "% Porcentaje"         := TRUE;
                                                                  "Precio Adulto"        := -100;
                                                                  "Precio Ni¤o"          := -100;
                                                                  "Precio adulto exento" := -100;
                                                                  "Precio ni¤o exento"   := -100;
                                                                  "Acumula al precio"    := TRUE;
                                                                  Comisionable           := TRUE;
                                                                  Automatico             := FALSE;
                                                                END;

                                                                cPar.intro_historico_precios_tarifa(Excursion, "Temporada Tarifa", rHisP.Tipo::Venta, "Tipo precio", Orden,
                                                                                                    FIELDCAPTION(Free), FORMAT(xRec.Free), FORMAT(Free), "ID Tarifa");
                                                              END;

                                                   Description=$003 }
    { 60  ;   ;Permite precio manual;Boolean      ;OnValidate=BEGIN
                                                                IF "Permite precio manual" THEN BEGIN
                                                                  Free                   := FALSE;
                                                                  "% Porcentaje"         := FALSE;
                                                                  "Precio Adulto"        := 0;
                                                                  "Precio Ni¤o"          := 0;
                                                                  "Precio adulto exento" := 0;
                                                                  "Precio ni¤o exento"   := 0;
                                                                  "Acumula al precio"    := TRUE;
                                                                  Comisionable           := TRUE;
                                                                  Automatico             := FALSE;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Permits manual price;
                                                              ESP=Permite Precio manual] }
    { 61  ;   ;Mostrar en hoja servicio;Boolean   ;OnValidate=BEGIN
                                                                IF "Mostrar en hoja servicio" <> xRec."Mostrar en hoja servicio" THEN
                                                                  cPar.intro_historico_precios_tarifa(Excursion, "Temporada Tarifa", rHisP.Tipo::Venta, "Tipo precio", Orden,
                                                                                                      FIELDCAPTION("Mostrar en hoja servicio"),
                                                                                                      FORMAT(xRec."Mostrar en hoja servicio"), FORMAT("Mostrar en hoja servicio"), "ID Tarifa");
                                                              END;

                                                   CaptionML=[ENU=Show in workorder;
                                                              ESP=Mostrar en hoja servicio];
                                                   Description=$004 }
    { 62  ;   ;ID Tarifa           ;Integer       ;CaptionML=[ENU=Tariff ID;
                                                              ESP=ID Tarifa] }
    { 63  ;   ;Publicar suplemento ;Boolean       ;CaptionML=[ENU=Publish supplement;
                                                              ESP=Publicar suplemento];
                                                   Description=$011 }
  }
  KEYS
  {
    {    ;Delegacion,Tipo precio,Excursion,Temporada Tarifa,Orden;
                                                   Clustered=Yes }
    {    ;Delegacion,Tipo precio,Excursion,Temporada Tarifa,Codigo,Fecha Desde,Fecha Hasta;
                                                   KeyGroups=RENUMERING }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1103355005 : TextConst 'ENU=Agency parameters folder does not exist;ESP=No existe el fichero de par mentros de agencia';
      Text001@1103355006 : TextConst 'ENU=Deleted;ESP=Borrado';
      Text002@1103355007 : TextConst 'ENU=Register deleted;ESP=Registro Borrado';
      Text003@1103355008 : TextConst 'ENU=Date from cannot be higher than date to;ESP=La fecha desde no puede ser superior a la fecha hasta';
      Text004@1103355009 : TextConst 'ENU=Date to cannot be lower than date from;ESP=La fecha hasta no puede ser inferior a la fecha desde';
      Text005@1103355010 : TextConst 'ENU=Dates overlap with existent dates;ESP=Las fechas se solapan con otras fechas existentes';
      Text006@1103355011 : TextConst 'ENU=Tariff does not exist for this register;ESP=No existe la Tarifa para este registro';
      Text007@1103355012 : TextConst 'ENU=Date from is not within the period of the tariff;ESP=La fecha desde no esta entre el periodo de la tarifa';
      Text008@1103355013 : TextConst 'ENU=Date to is not within the period of the tariff;ESP=La fecha hasta no esta entre el periodo de la tarifa';
      cPar@1103355000 : Codeunit 7009700;
      cFunVar@1103355001 : Codeunit 7009720;
      rPar@1100217001 : Record 7009700;
      rHisP@1103355002 : Record 7010160;
      rTar@1103355003 : Record 7010147;
      rTipoCambioDivisa@1103355004 : Record 330;
      Text009@1100253000 : TextConst 'ENU=The value of %1 cannot be higger than the total price of the excursion;ESP=El valor de %1 no puede ser superior al precio total';
      Text010@1100253001 : TextConst 'ENU=Exempted prices cannot be indicated in the supplements defined as percentage.;ESP=No pueden indicar precios exentos los suplementos porcentuales.';
      Text011@1100217000 : TextConst 'ESP=Los Frees tiene que tener un porcentaje del -100%';
      Text012@1100217002 : TextConst 'ENU=Do you want to setup this supplement as Free.;ESP=Desea configurar este suplemento como Free.';

    PROCEDURE Comprobar_Solape@2();
    VAR
      rPrecioVta@1103355000 : Record 7010148;
    BEGIN
      // Comprobar_Solape

      rPrecioVta.RESET;
      rPrecioVta.SETRANGE ("Tipo precio"  , "Tipo precio");
      rPrecioVta.SETRANGE (Excursion , Excursion);
      rPrecioVta.SETRANGE ("Temporada Tarifa", "Temporada Tarifa");
      rPrecioVta.SETRANGE (Codigo            , Codigo);
      rPrecioVta.SETRANGE (Activo            , TRUE);
      rPrecioVta.SETRANGE ("ID Tarifa"       , "ID Tarifa"); //+$006
      rPrecioVta.SETFILTER(Orden             , '<>%1', Orden);

      // Comprobar solape de fechas

      rPrecioVta.SETFILTER("Fecha Desde"     , '<=%1', "Fecha Hasta");
      rPrecioVta.SETFILTER("Fecha Hasta"     , '>=%1', "Fecha Desde");
      IF rPrecioVta.FIND('-') THEN
        ERROR(Text005);

      Activo := TRUE;
    END;

    PROCEDURE Comprobar_Tarifa@1();
    VAR
      rTarifa@1103355000 : Record 7010147;
    BEGIN
      // Comprobar_Tarifa

      IF NOT rTarifa.GET(Delegacion, Excursion, "Temporada Tarifa") THEN
        ERROR(Text006);

      rPar.FINDFIRST;
      IF rPar."Busca tarifa por fecha venta" THEN
        EXIT;

      IF "Fecha Desde" <> 0D THEN
        IF ("Fecha Desde" < rTarifa."Fecha Inicio") OR
           ("Fecha Desde" > rTarifa."Fecha Final") THEN
          ERROR(Text007);

      IF "Fecha Hasta" <> 0D THEN
        IF ("Fecha Hasta" < rTarifa."Fecha Inicio") OR
           ("Fecha Hasta" > rTarifa."Fecha Final") THEN
          ERROR(Text008);
    END;

    LOCAL PROCEDURE _ComparaPrecioExento@1100253000();
    BEGIN
      // _ComparaPrecioExento

      IF "% Porcentaje" THEN
      BEGIN
        "Precio exento"        := 0;
        "Precio adulto exento" := 0;
        "Precio ni¤o exento"   := 0;

        EXIT;
      END;

      _ComparaPrecio(Precio         , "Precio exento", FIELDCAPTION("Precio exento"));
      _ComparaPrecio("Precio Adulto", "Precio adulto exento", FIELDCAPTION("Precio adulto exento"));
      _ComparaPrecio("Precio Ni¤o"  , "Precio ni¤o exento", FIELDCAPTION("Precio ni¤o exento"));
    END;

    LOCAL PROCEDURE _ComparaPrecio@1100217000(pwPrec@1100217000 : Decimal;pwPrecExc@1100217001 : Decimal;pwCampo@1100217002 : Text[30]);
    BEGIN
      // _ComparaPrecio

      // Para la comprobacion invertimos los campos cuando el importe es negativo
      IF pwPrec < 0 THEN BEGIN
        pwPrec     := - pwPrec;
        pwPrecExc  := - pwPrecExc;
      END;

      IF pwPrecExc > pwPrec THEN
        ERROR(Text009, pwCampo);
    END;

    LOCAL PROCEDURE _GetDivisa@1100217001() : Code[10];
    VAR
      lrTar@1100217000 : Record 7010147;
    BEGIN
      // _GetDivisa

      //-$008lrTar.RESET;
      //-$008lrTar.SETRANGE (Delegacion    , Delegacion);
      //-$008lrTar.SETRANGE (Excursion     , Excursion);
      //-$008lrTar.SETFILTER("Fecha Inicio", '<=%1', "Fecha Desde");
      //-$008lrTar.SETFILTER("Fecha Final" , '>=%1', "Fecha Desde");
      //-$008IF lrTar.FINDFIRST THEN
      //-$008  EXIT(lrTar."o_C¢d. divisa");

      //+$008 <
      rPar.FINDFIRST;
      EXIT(rPar."Divisa venta excursiones");
      //+$008 >
    END;

    BEGIN
    {
      $001 AJS 15052012 Nuevos campos para indicar la parte del precio que se considera exenta de impuestos y una nueva
                        funcion para no permitir que esa parte exenta sea superior al precio total

      $002 AJS 31052012 Iniciar las fechas con las fechas del contrato
      $003 JPT 17/08/12 GOC-LP-12025 Hacer que los free no se reflejen en las facturas de venta de excursiones
                        Nuevo Campo Free para suplementos

      $004 AJS 30112012 Nuevo campo "Mostrar hoja servicio"

      $005 AJS 21122012 Los campos de divisa y precio DL son obsoletos

      $006 AJS 28122012 Nuevo campo "ID Tarifa" por cambio en clave primaria de las tarifas

      $007 AJS 28122012 Si la tarifa se aplica sobre las fechas de venta no hay que controlar fecha de precios

      $008 AJS 13022013 Modifico la funcion _GetDivisa para que busque en parametros y no en tarifa

      $009 AJS 10032014 Elimino el campo Contrato Proveedor porque es obsoleto

      $010 AJS 21032014 Pongo un confirm para evitar que marcar el campo Free me desconfigure los precios de venta de un suplemento

      $011 AJS 11052014 Nuevo campo Publicar suplemento para permtir indicar los suplementos que se exportaran con el servicio
                        web de venta

      $012 AJS 22122014 Modifico traduccion de Optionstring de "Tipo Precio"
    }
    END.
  }
}
