OBJECT Table 7009787 Contrato compra transfer
{
  OBJECT-PROPERTIES
  {
    Date=07/07/15;
    Time=[ 6:20:29];
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    OnInsert=VAR
               rContrato@1103355000 : Record 7009787;
             BEGIN
               rPar.LOCKTABLE;
               rPar.FINDFIRST;
               IF NOT rPar."Empresa Delegacion" THEN
                 ERROR(Text001 + Text002);

               IF rPar."Contador Contrato compra trans" = '' THEN
                 rPar."Contador Contrato compra trans" := Text013 + '0000000'; //+$005
                 //-$005ERROR(Text003);

               IF "N§ Contrato compra" = '' THEN BEGIN
                 REPEAT
                   rPar."Contador Contrato compra trans" := INCSTR(rPar."Contador Contrato compra trans");
                   rPar.MODIFY;
                 UNTIL NOT rContrato.GET(rPar.Delegacion, rPar."Contador Contrato compra trans");
                 "N§ Contrato compra" := rPar."Contador Contrato compra trans";
               END;

               Delegacion := rPar.Delegacion;
             END;

    OnModify=BEGIN
               rPar.FINDFIRST;
               IF NOT rPar."Empresa Delegacion" THEN
                 ERROR(Text004 + Text002);

               Confirmado := cCon.confirmar_contrato_compra(Rec);
             END;

    OnDelete=BEGIN
               rPar.FINDFIRST;
               IF NOT rPar."Empresa Delegacion" THEN
                 ERROR(Text005 + Text002);

               _CheckCosteConfirmado; //+$010

               _borrar_registro;

               cPar.intro_historico_contrato(Delegacion, "N§ Contrato compra", rHis.Tipo::"Transfer Compra", Text006,
                                             Text007, '');
             END;

    OnRename=BEGIN
               rPar.FINDFIRST;
               IF NOT rPar."Empresa Delegacion" THEN
                 ERROR(Text008 + Text002);
             END;

    CaptionML=[ENU=Transfer purchase contract;
               ESP=Contrato compra transfer];
    LookupFormID=Form7009899;
    DrillDownFormID=Form7009899;
  }
  FIELDS
  {
    { 1   ;   ;Delegacion          ;Code10        ;TableRelation=Delegacion.Codigo;
                                                   CaptionML=[ENU=Local Office;
                                                              ESP=Delegacion];
                                                   Description=PK, FK Delegaciones;
                                                   Editable=No }
    { 3   ;   ;N§ Contrato compra  ;Code10        ;CaptionML=[ENU=Purchase contract No.;
                                                              ESP=N§ Contrato compra];
                                                   Description=PK }
    { 5   ;   ;Fecha firma contrato;Date          ;OnValidate=BEGIN
                                                                IF "Fecha firma contrato" <> xRec."Fecha firma contrato" THEN
                                                                BEGIN
                                                                  IF "Fecha inicio contrato" = 0D THEN
                                                                    VALIDATE("Fecha inicio contrato", "Fecha firma contrato");

                                                                  _CheckDates;

                                                                  cPar.intro_historico_contrato(Delegacion, "N§ Contrato compra", rHis.Tipo::"Transfer Compra",
                                                                                                FIELDCAPTION("Fecha firma contrato"),
                                                                                                FORMAT(xRec."Fecha firma contrato"), FORMAT("Fecha firma contrato"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Enrollment date;
                                                              ESP=Fecha firma contrato] }
    { 7   ;   ;Fecha inicio contrato;Date         ;OnValidate=BEGIN
                                                                IF "Fecha inicio contrato" <> xRec."Fecha inicio contrato" THEN
                                                                BEGIN
                                                                  _CheckDates;

                                                                  _CheckFechasPrecios;

                                                                  VALIDATE(Confirmado, FALSE);

                                                                  cPar.intro_historico_contrato(Delegacion, "N§ Contrato compra", rHis.Tipo::"Transfer Compra",
                                                                                                FIELDCAPTION("Fecha inicio contrato"),
                                                                                                FORMAT(xRec."Fecha inicio contrato"), FORMAT("Fecha inicio contrato"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Starting Contract Date;
                                                              ESP=Fecha inicio contrato] }
    { 9   ;   ;Fecha final contrato;Date          ;OnValidate=BEGIN
                                                                IF "Fecha final contrato" <> xRec."Fecha final contrato" THEN
                                                                BEGIN
                                                                  _CheckDates;

                                                                  _CheckFechasPrecios;

                                                                  VALIDATE(Confirmado, FALSE);

                                                                  cPar.intro_historico_contrato(Delegacion, "N§ Contrato compra", rHis.Tipo::"Transfer Compra",
                                                                                                FIELDCAPTION("Fecha final contrato"),
                                                                                                FORMAT(xRec."Fecha final contrato"), FORMAT("Fecha final contrato"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Ending Contract Date;
                                                              ESP=Fecha final contrato] }
    { 11  ;   ;Transportista       ;Code10        ;TableRelation="Transportista transfer".Codigo;
                                                   OnValidate=BEGIN
                                                                IF Transportista <> xRec.Transportista THEN BEGIN
                                                                  //+$007
                                                                  _CheckCosteConfirmado;

                                                                  IF rTra.GET(Transportista) THEN
                                                                    VALIDATE(Proveedor, rTra.Proveedor);

                                                                  VALIDATE(Confirmado, FALSE);

                                                                  cPar.intro_historico_contrato(Delegacion, "N§ Contrato compra", rHis.Tipo::"Transfer Compra", FIELDCAPTION(Transportista),
                                                                                                  xRec.Transportista, Transportista);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Carrier;
                                                              ESP=Transportista];
                                                   NotBlank=Yes;
                                                   Description=FK Transportista }
    { 13  ;   ;Proveedor           ;Code20        ;TableRelation=Vendor.No.;
                                                   OnValidate=BEGIN
                                                                IF Proveedor <> xRec.Proveedor THEN
                                                                BEGIN
                                                                  //+$007
                                                                  _CheckCosteConfirmado;

                                                                  VALIDATE(Confirmado, FALSE);

                                                                  rVendor.GET(Proveedor);
                                                                  "Cod. Terminos Pago"  := rVendor."Payment Terms Code";
                                                                  "Cod. Formas Pago"    := rVendor."Payment Method Code";
                                                                  "Impuestos incluidos" := rVendor."Prices Including VAT"; //+$012
                                                                  VALIDATE("C¢d. divisa", rVendor."Currency Code");

                                                                  cPar.intro_historico_contrato(Delegacion, "N§ Contrato compra", rHis.Tipo::"Transfer Compra", FIELDCAPTION(Proveedor),
                                                                                                xRec.Proveedor, Proveedor);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Supplier;
                                                              ESP=Proveedor];
                                                   Description=FK Proveedor }
    { 15  ;   ;Impuestos incluidos ;Boolean       ;OnValidate=BEGIN
                                                                IF "Impuestos incluidos" <> xRec."Impuestos incluidos" THEN
                                                                BEGIN
                                                                  _CheckCosteConfirmado;

                                                                  cPar.intro_historico_contrato(Delegacion, "N§ Contrato compra", rHis.Tipo::"Transfer Compra",
                                                                                                FIELDCAPTION("Impuestos incluidos"),
                                                                                                FORMAT(xRec."Impuestos incluidos"), FORMAT("Impuestos incluidos"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Included Taxes;
                                                              ESP=Impuestos incluidos] }
    { 17  ;   ;OLD_Precios por     ;Option        ;CaptionML=[ENU=Prices per;
                                                              ESP=Precios por];
                                                   OptionCaptionML=[ENU=1 Travel, with return;
                                                                    ESP=1 Viaje,Ida y Vuelta];
                                                   OptionString=1 Viaje,Ida y Vuelta;
                                                   Description=$002. 1 Viaje, Ida y vuelta }
    { 19  ;   ;Gr. registro IVA Producto;Code10   ;TableRelation="VAT Product Posting Group".Code;
                                                   OnValidate=BEGIN
                                                                IF "Gr. registro IVA Producto" <> xRec."Gr. registro IVA Producto" THEN
                                                                BEGIN
                                                                  //+$007
                                                                  _CheckCosteConfirmado;
                                                                  //IF xRec."Gr. registro IVA Producto" <> '' THEN
                                                                  //  switch;

                                                                    cPar.intro_historico_contrato(Delegacion, "N§ Contrato compra", rHis.Tipo::"Transfer Compra",
                                                                                                FIELDCAPTION("Gr. registro IVA Producto"),
                                                                                                xRec."Gr. registro IVA Producto", "Gr. registro IVA Producto");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=VAT Prod. posting group;
                                                              ESP=Gr. Registro IVA Producto];
                                                   NotBlank=Yes;
                                                   Description=FK Gr. Registro IVA Producto }
    { 21  ;   ;Cod. Terminos Pago  ;Code10        ;TableRelation="Payment Terms".Code;
                                                   OnValidate=BEGIN
                                                                IF "Cod. Terminos Pago" <> xRec."Cod. Terminos Pago" THEN BEGIN
                                                                  //IF xRec."Cod. Terminos Pago" <> '' THEN
                                                                  //  switch;

                                                                    cPar.intro_historico_contrato(Delegacion, "N§ Contrato compra", rHis.Tipo::"Transfer Compra",
                                                                                                FIELDCAPTION("Cod. Terminos Pago"),
                                                                                                xRec."Cod. Terminos Pago", "Cod. Terminos Pago");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Payment terms code;
                                                              ESP=Cod. Terminos Pago];
                                                   Description=FK Terminos Pago }
    { 23  ;   ;Cod. Formas Pago    ;Code10        ;TableRelation="Payment Method".Code;
                                                   OnValidate=BEGIN
                                                                IF "Cod. Formas Pago" <> xRec."Cod. Formas Pago" THEN BEGIN
                                                                  //IF xRec."Cod. Formas Pago" <> '' THEN
                                                                  //  switch;

                                                                    cPar.intro_historico_contrato(Delegacion, "N§ Contrato compra", rHis.Tipo::"Transfer Compra",
                                                                                                FIELDCAPTION("Cod. Formas Pago"),
                                                                                                xRec."Cod. Formas Pago", "Cod. Formas Pago");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Payment methods code;
                                                              ESP=Cod. Formas Pago];
                                                   Description=FK Cod. Forma Pago }
    { 25  ;   ;OLD_Cod. Serie      ;Code10        ;TableRelation="No. Series".Code;
                                                   CaptionML=[ENU=Serial code;
                                                              ESP=Cod. Serie];
                                                   Description=FK N§ Serie }
    { 27  ;   ;Confirmado          ;Boolean       ;OnValidate=BEGIN
                                                                IF Confirmado <> xRec.Confirmado THEN
                                                                BEGIN
                                                                  cPar.intro_historico_contrato(Delegacion, "N§ Contrato compra", rHis.Tipo::"Transfer Compra",
                                                                                                FIELDCAPTION(Confirmado),
                                                                                                FORMAT(xRec.Confirmado), FORMAT(Confirmado));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Confirmed;
                                                              ESP=Confirmado];
                                                   Editable=No }
    { 31  ;   ;Minimos             ;Integer       ;OnValidate=BEGIN
                                                                IF Minimos <> xRec.Minimos THEN
                                                                BEGIN
                                                                  cPar.intro_historico_contrato(Delegacion, "N§ Contrato compra", rHis.Tipo::"Transfer Compra",
                                                                                                FIELDCAPTION(Minimos),
                                                                                                FORMAT(xRec.Minimos), FORMAT(Minimos));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Minimums;
                                                              ESP=Minimos] }
    { 33  ;   ;OLD_Punto Origen/Destino;Code10    ;TableRelation=Aeropuerto.Aeropuerto WHERE (Aeropuerto Local=CONST(Yes));
                                                   CaptionML=[ENU=Origin/Destination zone;
                                                              ESP=Punto Origen/Destino];
                                                   Description=FK Aeropuerto }
    { 35  ;   ;C¢d. divisa         ;Code10        ;TableRelation=Currency;
                                                   OnValidate=BEGIN
                                                                IF "C¢d. divisa" <> xRec."C¢d. divisa" THEN
                                                                BEGIN
                                                                  //+$006
                                                                  _CheckCosteConfirmado;

                                                                  cPar.intro_historico_contrato(Delegacion, "N§ Contrato compra", rHis.Tipo::"Transfer Compra",
                                                                                                FIELDCAPTION("C¢d. divisa"),
                                                                                                FORMAT(xRec."C¢d. divisa"), FORMAT("C¢d. divisa"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Currency code;
                                                              ESP=C¢d. divisa];
                                                   Description=FK Divisa }
    { 37  ;   ;Buscar precios por  ;Option        ;OnValidate=BEGIN
                                                                IF "Buscar precios por" <> xRec."Buscar precios por" THEN BEGIN
                                                                  IF "Buscar precios por" = "Buscar precios por"::"Capacidad solicitada" THEN
                                                                    _ComprobarPreciosPax;

                                                                  cPar.intro_historico_contrato(Delegacion, "N§ Contrato compra", rHis.Tipo::"Transfer Compra",
                                                                                                FIELDCAPTION("Buscar precios por"),
                                                                                                FORMAT(xRec."Buscar precios por"), FORMAT("Buscar precios por"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Search prices by;
                                                              ESP=Buscar precios por];
                                                   OptionCaptionML=[ENU=Service Passage,Requested capacity;
                                                                    ESP=Pasaje servicio,Capacidad solicitada];
                                                   OptionString=Pasaje servicio,Capacidad solicitada;
                                                   Description=$003. Pasaje servicio,Capacidad solicitada }
  }
  KEYS
  {
    {    ;Delegacion,N§ Contrato compra           ;Clustered=Yes }
    {    ;Proveedor,Fecha inicio contrato,Fecha final contrato;
                                                   KeyGroups=RENUMERING }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1103355008 : TextConst 'ENU=Only contracts of transfer can be signed up\;ESP=Solo se pueden dar de alta contratos de transfer\';
      Text002@1103355009 : TextConst 'ENU=in the local offices;ESP=en las delegaciones.';
      Text004@1103355011 : TextConst 'ENU=Only transfer contracts can be modified\;ESP=Solo se pueden modificar contratos de transfer\';
      Text005@1103355012 : TextConst 'ENU=Only transfer contracts can be cancelled\;ESP=Solo se pueden dar de baja contratos de transfer\';
      Text006@1103355013 : TextConst 'ENU=Deleted;ESP=Borrado';
      Text007@1103355014 : TextConst 'ENU=Deleted register;ESP=Borrado registro';
      Text008@1103355015 : TextConst 'ENU=Only transfer contract can be renamed\;ESP=Solo se pueden renombrar contratos de transfer\';
      Text009@1103355016 : TextConst 'ENU=Starting date of the contract must be after;ESP=La fecha de inicio del contrato debe ser posterior a la';
      Text010@1103355017 : TextConst 'ENU=the signature date of the contract.;ESP=fecha de firma del contrato.';
      Text011@1103355018 : TextConst 'ENU=Starting date cannot be higher than final date;ESP=La fecha de inicio no puede ser posterior a la de final.';
      rPar@1103355000 : Record 7009700;
      rHis@1103355002 : Record 7009764;
      rTra@1103355003 : Record 7009801;
      rVendor@1100217002 : Record 23;
      cPar@1100217001 : Codeunit 7009700;
      cCon@1100217000 : Codeunit 7009718;
      Text012@1100217003 : TextConst 'ENU=Prices per pax cannot be used in the contracts defined by capacity.;ESP=No se pueden indicar precios por pax en los contratos por capacidad.';
      Text013@1100217004 : TextConst 'ENU=TPC;ESP=CCT';
      Text014@1100217005 : TextConst 'ENU=You can''t change sensitive data of a contract with costs generated.;ESP=No se pueden cambiar datos sensibles de un contrato con costes confirmados.';
      Text015@1000000000 : TextConst 'ENU=You can not apply the change of dates because some prices would fall outside the contract period;ESP=No se puede aplicar el cambio de fechas porque algunos precios quedarian fuera del periodo del contrato';

    LOCAL PROCEDURE _borrar_registro@4();
    VAR
      lrCom@1100253000 : Record 7009784;
      lrPre@1100217000 : Record 7009789;
      lrZona@1100217001 : Record 7009791;
    BEGIN
      lrPre.RESET;
      lrPre.SETRANGE(Delegacion   , Delegacion);
      lrPre.SETRANGE("N§ Contrato", "N§ Contrato compra");
      IF lrPre.FINDSET(TRUE) THEN
        lrPre.DELETEALL;

      lrZona.RESET;
      lrZona.SETRANGE(Contrato       , "N§ Contrato compra");
      lrZona.SETRANGE("Tipo contrato", lrZona."Tipo contrato"::Compra);
      IF lrZona.FINDSET(TRUE) THEN
        lrZona.DELETEALL;

      //+$001
      lrCom.RESET;
      lrCom.SETRANGE(Delegacion     , Delegacion);
      lrCom.SETRANGE("N§ Contrato"  , "N§ Contrato compra");
      lrCom.SETRANGE("Tipo Contrato", lrCom."Tipo Contrato"::"C Transfer");
      IF lrCom.FINDSET(TRUE) THEN
        lrCom.DELETEALL;
      //+$001
    END;

    LOCAL PROCEDURE _ComprobarPreciosPax@1100217000();
    VAR
      lrPre@1100217000 : Record 7009789;
    BEGIN
      // _ComprobarPreciosPax

      //. Si indicamos precios por capacidad no pueden existir precios por pax

      lrPre.RESET;
      lrPre.SETRANGE(Delegacion       , Delegacion);
      lrPre.SETRANGE("N§ Contrato"    , "N§ Contrato compra");
      lrPre.SETRANGE("Codigo precio"  , lrPre."Codigo precio"::"Precio base");
      lrPre.SETRANGE("Tipo aplicacion", lrPre."Tipo aplicacion"::Pax);
      IF NOT lrPre.ISEMPTY THEN
        ERROR(Text012);

      //+$008 <
      lrPre.RESET;
      lrPre.SETRANGE(Delegacion       , Delegacion);
      lrPre.SETRANGE("N§ Contrato"    , "N§ Contrato compra");
      lrPre.SETRANGE("Codigo precio"  , lrPre."Codigo precio"::Soporte);
      lrPre.SETRANGE("Tipo aplicacion", lrPre."Tipo aplicacion"::Pax);
      IF NOT lrPre.ISEMPTY THEN
        ERROR(Text012);
      //+$008 >
    END;

    LOCAL PROCEDURE _CheckCosteConfirmado@1100217001();
    VAR
      lrCoste@1100217000 : Record 7009805;
    BEGIN
      // _CheckCosteConfirmado

      lrCoste.RESET;
      lrCoste.SETRANGE(Concepto                 , 'TRNC');
      lrCoste.SETRANGE("Num. Contrato"          , "N§ Contrato compra"); //+$009
      //-$009lrCoste.SETRANGE("Proveedor contable"     , Proveedor);
      //-$009lrCoste.SETRANGE(Fecha                    , "Fecha inicio contrato", "Fecha final contrato");
      //-$009lrCoste.SETRANGE("Origen/Destino transfer", "Punto Origen/Destino");
      IF NOT lrCoste.ISEMPTY THEN
        ERROR(Text014);
    END;

    LOCAL PROCEDURE _CheckDates@1000000000();
    BEGIN
      // _CheckDates

      IF ("Fecha firma contrato" <> 0D) AND ("Fecha inicio contrato" <>0D) THEN
        IF ("Fecha firma contrato" > "Fecha inicio contrato") THEN
          ERROR(Text009 + ' ' + Text010);

      IF ("Fecha final contrato" <> 0D) AND ("Fecha inicio contrato" <> 0D) THEN
        IF "Fecha inicio contrato" > "Fecha final contrato" THEN
          ERROR(Text011);
    END;

    LOCAL PROCEDURE _CheckFechasPrecios@1000000002();
    VAR
      lrPre@1000000000 : Record 7009789;
    BEGIN
      // _CheckFechasPrecios

      //. Vamos a comprobar que las fechas de los precios no queden fuera del contrato con un cambio de fechas

      lrPre.RESET;
      lrPre.SETRANGE (Delegacion   , Delegacion);
      lrPre.SETRANGE ("N§ Contrato", "N§ Contrato compra");
      lrPre.SETFILTER("Fecha desde", '<%1', "Fecha inicio contrato");
      IF lrPre.FINDFIRST THEN
        ERROR(Text015);

      lrPre.RESET;
      lrPre.SETRANGE (Delegacion   , Delegacion);
      lrPre.SETRANGE ("N§ Contrato", "N§ Contrato compra");
      lrPre.SETFILTER("Fecha hasta", '>%1', "Fecha final contrato");
      IF lrPre.FINDFIRST THEN
        ERROR(Text015);
    END;

    BEGIN
    {
      $001 AJS 20122011 Modifico la funcion borrar_registro para incluir los comentarios

      $002 AJS 10092012 Marco el campo "Precios Por" como obsoleto porque lo paso a los precios

      $003 AJS 14092012 Nuevo campo "Buscar precios por" para indicar si el proveedor nos cobra
                        la capacidad del vehiculo solicitado o el pasaje realmente transportado

      $004 AJS 22092012 Marco el campo "Cod. Serie" como obsoleto para utilizar la serie estandar

      $005 AJS 24122012 Automatizo el parametro de contador de contratos de compra

      $006 AJS 21022013 Comprobar si existen costes generados al cambiar de divisa

      $007 AJS 07032013 Comprobar si existen costes generados al cambiar el proveedor, punto de origen
                        o grupo registro iva producto

      $008 AJS 26112013 Nuevo valor Sopote del campo [Codigo precio] en los precios

      $009 AJS 26112013 La comprobacion de costes confirmados la hago por numero de contrato

      $010 AJS 12052014 No dejamos borrar un contrato con costes confirmados

      $011 AJS 10062014 CNT-OC-14046, Los contratos dejan de tener aeropuerto, se pasa a los precios

      $012 AJS 04102014 La marca de impuestos incluidos viene del proveedor

      $013 AJS 05102014 Nueva funcion para controlar que los cambios de fecha de los contratos no dejen precios
                        huerfanos
    }
    END.
  }
}
