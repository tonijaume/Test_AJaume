OBJECT Table 5991 Service Shipment Line
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00,NAVES5.00;
  }
  PROPERTIES
  {
    Permissions=TableData 32=r,
                TableData 5802=r;
    CaptionML=[ENU=Service Shipment Line;
               ESP=L¡nea env¡o servicio];
    PasteIsValid=No;
    LookupFormID=Form5949;
  }
  FIELDS
  {
    { 2   ;   ;Customer No.        ;Code20        ;TableRelation=Customer;
                                                   CaptionML=[ENU=Customer No.;
                                                              ESP=N§ cliente];
                                                   Editable=No }
    { 3   ;   ;Document No.        ;Code20        ;TableRelation="Service Shipment Header".No.;
                                                   CaptionML=[ENU=Document No.;
                                                              ESP=N§ documento] }
    { 4   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ESP=N§ l¡nea] }
    { 5   ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              ESP=Tipo];
                                                   OptionCaptionML=[ENU=" ,Item,Resource,Cost,G/L Account";
                                                                    ESP=" ,Producto,Recurso,Coste,Cuenta"];
                                                   OptionString=[ ,Item,Resource,Cost,G/L Account] }
    { 6   ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST(" ")) "Standard Text"
                                                                 ELSE IF (Type=CONST(Item)) Item
                                                                 ELSE IF (Type=CONST(Resource)) Resource
                                                                 ELSE IF (Type=CONST(Cost)) "Service Cost"
                                                                 ELSE IF (Type=CONST(G/L Account)) "G/L Account";
                                                   CaptionML=[ENU=No.;
                                                              ESP=N§] }
    { 7   ;   ;Location Code       ;Code10        ;TableRelation=Location;
                                                   CaptionML=[ENU=Location Code;
                                                              ESP=C¢d. almac‚n] }
    { 8   ;   ;Posting Group       ;Code10        ;TableRelation=IF (Type=CONST(Item)) "Inventory Posting Group";
                                                   CaptionML=[ENU=Posting Group;
                                                              ESP=Grupo contable] }
    { 11  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n] }
    { 12  ;   ;Description 2       ;Text50        ;CaptionML=[ENU=Description 2;
                                                              ESP=Descripci¢n 2] }
    { 13  ;   ;Unit of Measure     ;Text10        ;CaptionML=[ENU=Unit of Measure;
                                                              ESP=Unidad medida] }
    { 15  ;   ;Quantity            ;Decimal       ;CaptionML=[ENU=Quantity;
                                                              ESP=Cantidad];
                                                   DecimalPlaces=0:5 }
    { 22  ;   ;Unit Price          ;Decimal       ;CaptionML=[ENU=Unit Price;
                                                              ESP=Precio venta];
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Currency Code";
                                                   CaptionClass=GetCaptionClass(FIELDNO("Unit Price")) }
    { 23  ;   ;Unit Cost (LCY)     ;Decimal       ;CaptionML=[ENU=Unit Cost (LCY);
                                                              ESP=Coste unitario (DL)];
                                                   AutoFormatType=2 }
    { 25  ;   ;VAT %               ;Decimal       ;CaptionML=[ENU=VAT %;
                                                              ESP=% IVA];
                                                   DecimalPlaces=0:6;
                                                   Editable=No }
    { 27  ;   ;Line Discount %     ;Decimal       ;CaptionML=[ENU=Line Discount %;
                                                              ESP=% Descuento l¡nea];
                                                   DecimalPlaces=0:6;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 32  ;   ;Allow Invoice Disc. ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[ENU=Allow Invoice Disc.;
                                                              ESP=Permitir dto. factura] }
    { 34  ;   ;Gross Weight        ;Decimal       ;CaptionML=[ENU=Gross Weight;
                                                              ESP=Peso bruto];
                                                   DecimalPlaces=0:6 }
    { 35  ;   ;Net Weight          ;Decimal       ;CaptionML=[ENU=Net Weight;
                                                              ESP=Peso neto];
                                                   DecimalPlaces=0:6 }
    { 36  ;   ;Units per Parcel    ;Decimal       ;CaptionML=[ENU=Units per Parcel;
                                                              ESP=Unidades por lote];
                                                   DecimalPlaces=0:6 }
    { 37  ;   ;Unit Volume         ;Decimal       ;CaptionML=[ENU=Unit Volume;
                                                              ESP=Volumen];
                                                   DecimalPlaces=0:6 }
    { 38  ;   ;Appl.-to Item Entry ;Integer       ;CaptionML=[ENU=Appl.-to Item Entry;
                                                              ESP=Liq. por n§ orden producto] }
    { 39  ;   ;Item Shpt. Entry No.;Integer       ;CaptionML=[ENU=Item Shpt. Entry No.;
                                                              ESP=N§ orden mov. producto asoc.] }
    { 40  ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   CaptionML=[ENU=Shortcut Dimension 1 Code;
                                                              ESP=C¢d. dim. acceso dir. 1];
                                                   CaptionClass='1,2,1' }
    { 41  ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   CaptionML=[ENU=Shortcut Dimension 2 Code;
                                                              ESP=C¢d. dim. acceso dir. 2];
                                                   CaptionClass='1,2,2' }
    { 42  ;   ;Customer Price Group;Code10        ;TableRelation="Customer Price Group";
                                                   CaptionML=[ENU=Customer Price Group;
                                                              ESP=Grupo precio cliente];
                                                   Editable=No }
    { 52  ;   ;Work Type Code      ;Code10        ;TableRelation="Work Type";
                                                   CaptionML=[ENU=Work Type Code;
                                                              ESP=C¢d. tipo trabajo] }
    { 58  ;   ;Qty. Shipped Not Invoiced;Decimal  ;CaptionML=[ENU=Qty. Shipped Not Invoiced;
                                                              ESP=Cantidad enviada no facturada];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 61  ;   ;Quantity Invoiced   ;Decimal       ;CaptionML=[ENU=Quantity Invoiced;
                                                              ESP=Cantidad facturada];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 65  ;   ;Order No.           ;Code20        ;CaptionML=[ENU=Order No.;
                                                              ESP=N§ pedido] }
    { 66  ;   ;Order Line No.      ;Integer       ;CaptionML=[ENU=Order Line No.;
                                                              ESP=N§ l¡nea pedido] }
    { 68  ;   ;Bill-to Customer No.;Code20        ;TableRelation=Customer;
                                                   CaptionML=[ENU=Bill-to Customer No.;
                                                              ESP=Factura-a N§ cliente];
                                                   Editable=No }
    { 74  ;   ;Gen. Bus. Posting Group;Code10     ;TableRelation="Gen. Business Posting Group";
                                                   CaptionML=[ENU=Gen. Bus. Posting Group;
                                                              ESP=Grupo contable negocio] }
    { 75  ;   ;Gen. Prod. Posting Group;Code10    ;TableRelation="Gen. Product Posting Group";
                                                   CaptionML=[ENU=Gen. Prod. Posting Group;
                                                              ESP=Grupo contable producto] }
    { 77  ;   ;VAT Calculation Type;Option        ;CaptionML=[ENU=VAT Calculation Type;
                                                              ESP=Tipo c lculo IVA];
                                                   OptionCaptionML=[ENU=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax,No Taxable VAT;
                                                                    ESP=Normal,Reversi¢n,Total,Impto. venta,No sujeto];
                                                   OptionString=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax,No Taxable VAT;
                                                   Editable=No }
    { 78  ;   ;Transaction Type    ;Code10        ;TableRelation="Transaction Type";
                                                   CaptionML=[ENU=Transaction Type;
                                                              ESP=Naturaleza transacci¢n] }
    { 79  ;   ;Transport Method    ;Code10        ;TableRelation="Transport Method";
                                                   CaptionML=[ENU=Transport Method;
                                                              ESP=Modo transporte] }
    { 80  ;   ;Attached to Line No.;Integer       ;CaptionML=[ENU=Attached to Line No.;
                                                              ESP=N§ l¡nea texto adicional];
                                                   Editable=No }
    { 81  ;   ;Exit Point          ;Code10        ;TableRelation="Entry/Exit Point";
                                                   CaptionML=[ENU=Exit Point;
                                                              ESP=Puerto/Aerop. carga] }
    { 82  ;   ;Area                ;Code10        ;TableRelation=Area;
                                                   CaptionML=[ENU=Area;
                                                              ESP=C¢d. provincia] }
    { 83  ;   ;Transaction Specification;Code10   ;TableRelation="Transaction Specification";
                                                   CaptionML=[ENU=Transaction Specification;
                                                              ESP=Especificaci¢n transacci¢n] }
    { 85  ;   ;Tax Area Code       ;Code20        ;TableRelation="Tax Area";
                                                   CaptionML=[ENU=Tax Area Code;
                                                              ESP=C¢d.  rea impuesto] }
    { 86  ;   ;Tax Liable          ;Boolean       ;CaptionML=[ENU=Tax Liable;
                                                              ESP=Sujeto a impuesto] }
    { 87  ;   ;Tax Group Code      ;Code10        ;TableRelation="Tax Group";
                                                   CaptionML=[ENU=Tax Group Code;
                                                              ESP=C¢d. grupo impuesto] }
    { 89  ;   ;VAT Bus. Posting Group;Code10      ;TableRelation="VAT Business Posting Group";
                                                   CaptionML=[ENU=VAT Bus. Posting Group;
                                                              ESP=Grupo registro IVA neg.] }
    { 90  ;   ;VAT Prod. Posting Group;Code10     ;TableRelation="VAT Product Posting Group";
                                                   CaptionML=[ENU=VAT Prod. Posting Group;
                                                              ESP=Grupo registro IVA prod.] }
    { 91  ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=[ENU=Currency Code;
                                                              ESP=C¢d. divisa];
                                                   Editable=No }
    { 99  ;   ;VAT Base Amount     ;Decimal       ;CaptionML=[ENU=VAT Base Amount;
                                                              ESP=Importe base IVA];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 100 ;   ;Unit Cost           ;Decimal       ;CaptionML=[ENU=Unit Cost;
                                                              ESP=Coste unitario];
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Currency Code" }
    { 5402;   ;Variant Code        ;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Variant".Code WHERE (Item No.=FIELD(No.));
                                                   CaptionML=[ENU=Variant Code;
                                                              ESP=C¢d. variante] }
    { 5403;   ;Bin Code            ;Code20        ;TableRelation=Bin.Code WHERE (Location Code=FIELD(Location Code));
                                                   CaptionML=[ENU=Bin Code;
                                                              ESP=C¢d. ubicaci¢n] }
    { 5404;   ;Qty. per Unit of Measure;Decimal   ;InitValue=1;
                                                   CaptionML=[ENU=Qty. per Unit of Measure;
                                                              ESP=Cdad. por unidad medida];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 5407;   ;Unit of Measure Code;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Unit of Measure".Code WHERE (Item No.=FIELD(No.))
                                                                 ELSE "Unit of Measure";
                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              ESP=C¢d. unidad medida] }
    { 5415;   ;Quantity (Base)     ;Decimal       ;CaptionML=[ENU=Quantity (Base);
                                                              ESP=Cantidad (base)];
                                                   DecimalPlaces=0:5 }
    { 5458;   ;Qty. Shipped Not Invd. (Base);Decimal;
                                                   CaptionML=[ENU=Qty. Shipped Not Invd. (Base);
                                                              ESP=Cdad. enviada no fact. (base)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 5461;   ;Qty. Invoiced (Base);Decimal       ;CaptionML=[ENU=Qty. Invoiced (Base);
                                                              ESP=Cdad. facturada (base)];
                                                   DecimalPlaces=0:5 }
    { 5700;   ;Responsibility Center;Code10       ;TableRelation="Responsibility Center";
                                                   CaptionML=[ENU=Responsibility Center;
                                                              ESP=Centro responsabilidad] }
    { 5709;   ;Item Category Code  ;Code10        ;TableRelation="Item Category".Code;
                                                   CaptionML=[ENU=Item Category Code;
                                                              ESP=C¢d. categor¡a producto] }
    { 5710;   ;Nonstock            ;Boolean       ;CaptionML=[ENU=Nonstock;
                                                              ESP=Prod. sin stock] }
    { 5712;   ;Product Group Code  ;Code10        ;TableRelation="Product Group".Code WHERE (Item Category Code=FIELD(Item Category Code));
                                                   CaptionML=[ENU=Product Group Code;
                                                              ESP=C¢d. grupo producto] }
    { 5817;   ;Correction          ;Boolean       ;CaptionML=[ENU=Correction;
                                                              ESP=Correcci¢n] }
    { 5901;   ;Appl.-to Warranty Entry;Integer    ;CaptionML=[ENU=Appl.-to Warranty Entry;
                                                              ESP=Liq. por mov. garant¡a] }
    { 5902;   ;Service Item No.    ;Code20        ;TableRelation="Service Item".No.;
                                                   CaptionML=[ENU=Service Item No.;
                                                              ESP=N§ prod. servicio] }
    { 5903;   ;Appl.-to Service Entry;Integer     ;CaptionML=[ENU=Appl.-to Service Entry;
                                                              ESP=Liquidar a-mov. servicio] }
    { 5904;   ;Service Item Line No.;Integer      ;CaptionML=[ENU=Service Item Line No.;
                                                              ESP=N§ l¡n. prod. servicio] }
    { 5905;   ;Service Item Serial No.;Code20     ;CaptionML=[ENU=Service Item Serial No.;
                                                              ESP=N§ serie prod. servicio] }
    { 5906;   ;Service Item Line Description;Text30;
                                                   CaptionML=[ENU=Service Item Line Description;
                                                              ESP=Descripci¢n l¡nea prod. servicio] }
    { 5908;   ;Posting Date        ;Date          ;CaptionML=[ENU=Posting Date;
                                                              ESP=Fecha registro] }
    { 5909;   ;Order Date          ;Date          ;CaptionML=[ENU=Order Date;
                                                              ESP=Fecha pedido] }
    { 5916;   ;Ship-to Code        ;Code10        ;TableRelation="Ship-to Address".Code WHERE (Customer No.=FIELD(Customer No.));
                                                   CaptionML=[ENU=Ship-to Code;
                                                              ESP=C¢d. direcci¢n env¡o cliente] }
    { 5918;   ;Quantity Consumed   ;Decimal       ;CaptionML=[ENU=Quantity Consumed;
                                                              ESP=Cantidad consumida];
                                                   DecimalPlaces=0:5 }
    { 5920;   ;Qty. Consumed (Base);Decimal       ;CaptionML=[ENU=Qty. Consumed (Base);
                                                              ESP=Cantidad consumida (base)];
                                                   DecimalPlaces=0:5 }
    { 5928;   ;Service Price Group Code;Code10    ;TableRelation="Service Price Group";
                                                   CaptionML=[ENU=Service Price Group Code;
                                                              ESP=C¢d. grupo precio serv.] }
    { 5929;   ;Fault Area Code     ;Code10        ;TableRelation="Fault Area";
                                                   CaptionML=[ENU=Fault Area Code;
                                                              ESP=C¢d.  rea defecto] }
    { 5930;   ;Symptom Code        ;Code10        ;TableRelation="Symptom Code";
                                                   CaptionML=[ENU=Symptom Code;
                                                              ESP=C¢d. s¡ntoma] }
    { 5931;   ;Fault Code          ;Code10        ;TableRelation="Fault Code".Code WHERE (Fault Area Code=FIELD(Fault Area Code),
                                                                                          Symptom Code=FIELD(Symptom Code));
                                                   CaptionML=[ENU=Fault Code;
                                                              ESP=C¢d. defecto] }
    { 5932;   ;Resolution Code     ;Code10        ;TableRelation="Resolution Code";
                                                   CaptionML=[ENU=Resolution Code;
                                                              ESP=C¢d. resoluci¢n] }
    { 5933;   ;Exclude Warranty    ;Boolean       ;CaptionML=[ENU=Exclude Warranty;
                                                              ESP=Excluye garant¡a] }
    { 5934;   ;Warranty            ;Boolean       ;CaptionML=[ENU=Warranty;
                                                              ESP=Garant¡a] }
    { 5936;   ;Contract No.        ;Code20        ;TableRelation="Service Contract Header"."Contract No." WHERE (Contract Type=CONST(Contract));
                                                   CaptionML=[ENU=Contract No.;
                                                              ESP=N§ contrato] }
    { 5938;   ;Contract Disc. %    ;Decimal       ;CaptionML=[ENU=Contract Disc. %;
                                                              ESP=% Dto. contrato];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 5939;   ;Warranty Disc. %    ;Decimal       ;CaptionML=[ENU=Warranty Disc. %;
                                                              ESP=% Desc. garant¡a];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 5965;   ;Component Line No.  ;Integer       ;CaptionML=[ENU=Component Line No.;
                                                              ESP=N§ l¡nea componente] }
    { 5966;   ;Spare Part Action   ;Option        ;CaptionML=[ENU=Spare Part Action;
                                                              ESP=Acci¢n componente];
                                                   OptionCaptionML=[ENU=" ,Permanent,Temporary,Component Replaced,Component Installed";
                                                                    ESP=" ,Permanente,Temporal,Componente repuesto,Componente instalado"];
                                                   OptionString=[ ,Permanent,Temporary,Component Replaced,Component Installed] }
    { 5967;   ;Fault Reason Code   ;Code10        ;TableRelation="Fault Reason Code";
                                                   CaptionML=[ENU=Fault Reason Code;
                                                              ESP=C¢d. raz¢n defecto] }
    { 5968;   ;Replaced Item No.   ;Code20        ;TableRelation=IF (Replaced Item Type=CONST(Item)) Item
                                                                 ELSE IF (Replaced Item Type=CONST(Service Item)) "Service Item";
                                                   CaptionML=[ENU=Replaced Item No.;
                                                              ESP=N§ prod. repuesto] }
    { 5969;   ;Exclude Contract Discount;Boolean  ;CaptionML=[ENU=Exclude Contract Discount;
                                                              ESP=Excluye descuento contrato] }
    { 5970;   ;Replaced Item Type  ;Option        ;CaptionML=[ENU=Replaced Item Type;
                                                              ESP=Tipo producto reemplazado];
                                                   OptionCaptionML=[ENU=" ,Service Item,Item";
                                                                    ESP=" ,Producto servicio,Producto"];
                                                   OptionString=[ ,Service Item,Item] }
    { 5994;   ;Price Adjmt. Status ;Option        ;CaptionML=[ENU=Price Adjmt. Status;
                                                              ESP=Estado ajuste precio];
                                                   OptionCaptionML=[ENU=" ,Adjusted,Modified";
                                                                    ESP=" ,Ajustado,Modificado"];
                                                   OptionString=[ ,Adjusted,Modified] }
    { 5997;   ;Line Discount Type  ;Option        ;CaptionML=[ENU=Line Discount Type;
                                                              ESP=Tipo dto. l¡nea];
                                                   OptionCaptionML=[ENU=" ,Warranty Disc.,Contract Disc.,Line Disc.,Manual";
                                                                    ESP=" ,Dto. garant¡a,Dto. contrato,Dto. l¡nea,Manual"];
                                                   OptionString=[ ,Warranty Disc.,Contract Disc.,Line Disc.,Manual] }
    { 5999;   ;Copy Components From;Option        ;CaptionML=[ENU=Copy Components From;
                                                              ESP=Copiar componentes de];
                                                   OptionCaptionML=[ENU=None,Item BOM,Old Service Item,Old Serv.Item w/o Serial No.;
                                                                    ESP=Ninguno,LM productos,Prod. servicio antiguo,Prod. servicio antiguo sin n§ serie];
                                                   OptionString=None,Item BOM,Old Service Item,Old Serv.Item w/o Serial No. }
    { 6608;   ;Return Reason Code  ;Code10        ;TableRelation="Return Reason";
                                                   CaptionML=[ENU=Return Reason Code;
                                                              ESP=C¢d. motivo dev.] }
    { 7001;   ;Allow Line Disc.    ;Boolean       ;CaptionML=[ENU=Allow Line Disc.;
                                                              ESP=Permite dto. l¡nea] }
    { 7002;   ;Customer Disc. Group;Code10        ;TableRelation="Customer Discount Group";
                                                   CaptionML=[ENU=Customer Disc. Group;
                                                              ESP=Grupo dto. cliente] }
  }
  KEYS
  {
    {    ;Document No.,Line No.                   ;Clustered=Yes }
    {    ;Order No.,Order Line No.                 }
    {    ;Item Shpt. Entry No.                     }
    {    ;Service Item No.,Type,Posting Date       }
    {    ;Customer No.                             }
    {    ;Bill-to Customer No.                     }
    {    ;Fault Reason Code                        }
    {    ;Contract No.                             }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1001 : TextConst 'ENU=Shipment No. %1:;ESP=N§ albar n %1:';
      Text001@1000 : TextConst 'ENU=The program cannot find this Service line.;ESP=El programa no puede encontrar esta l¡nea de servicio.';
      Currency@1005 : Record 4;
      ServShptHeader@1004 : Record 5990;
      CurrencyRead@1002 : Boolean;

    PROCEDURE GetCurrencyCode@1() : Code[10];
    BEGIN
      IF "Document No." = ServShptHeader."No." THEN
        EXIT(ServShptHeader."Currency Code");
      IF ServShptHeader.GET("Document No.") THEN
        EXIT(ServShptHeader."Currency Code");
      EXIT('');
    END;

    PROCEDURE ShowDimensions@25();
    VAR
      PostedDocDim@1000 : Record 359;
      PostedDocDimensions@1001 : Form 547;
    BEGIN
      TESTFIELD("No.");
      TESTFIELD("Line No.");
      PostedDocDim.SETRANGE("Table ID",DATABASE::"Service Shipment Line");
      PostedDocDim.SETRANGE("Document No.","Document No.");
      PostedDocDim.SETRANGE("Line No.","Line No.");
      PostedDocDimensions.SETTABLEVIEW(PostedDocDim);
      PostedDocDimensions.RUNMODAL;
    END;

    PROCEDURE ShowItemTrackingLines@6500();
    VAR
      ItemTrackingMgt@1000 : Codeunit 6500;
    BEGIN
      ItemTrackingMgt.CallPostedItemTrackingForm(DATABASE::"Service Shipment Line",0,"Document No.",'',0,"Line No.");
    END;

    PROCEDURE InsertInvLineFromShptLine@2(VAR ServiceLine@1000 : Record 5902;VAR TempFromDocDim@1012 : TEMPORARY Record 357);
    VAR
      ServiceInvHeader@1011 : Record 5900;
      ServiceOrderHeader@1008 : Record 5900;
      ServiceOrderLine@1005 : Record 5902;
      Currency@1010 : Record 4;
      TempServiceLine@1002 : TEMPORARY Record 5902;
      FromDocDim@1003 : Record 357;
      ToDocDim@1004 : Record 357;
      TransferOldExtLines@1007 : Codeunit 379;
      ItemTrackingMgt@1009 : Codeunit 6500;
      ExtTextLine@1006 : Boolean;
      NextLineNo@1001 : Integer;
    BEGIN
      SETRANGE("Document No.","Document No.");

      TempServiceLine := ServiceLine;
      IF ServiceLine.FIND('+') THEN
        NextLineNo := ServiceLine."Line No." + 10000
      ELSE
        NextLineNo := 10000;

      IF ServiceInvHeader."No." <> TempServiceLine."Document No." THEN
        ServiceInvHeader.GET(TempServiceLine."Document Type",TempServiceLine."Document No.");

      IF ServiceLine."Shipment No." <> "Document No." THEN BEGIN
        ServiceLine.INIT;
        ServiceLine."Line No." := NextLineNo;
        ServiceLine."Document Type" := TempServiceLine."Document Type";
        ServiceLine."Document No." := TempServiceLine."Document No.";
        ServiceLine.Description := STRSUBSTNO(Text000,"Document No.");
        ServiceLine."Shipment No." := "Document No.";
        ServiceLine.INSERT;
        NextLineNo := NextLineNo + 10000;
      END;

      TransferOldExtLines.ClearLineNumbers;

      REPEAT
        ExtTextLine := (TransferOldExtLines.GetNewLineNumber("Attached to Line No.") <> 0);

        IF ServiceOrderLine.GET(ServiceOrderLine."Document Type"::Order,"Order No.","Order Line No.") THEN BEGIN
          IF (ServiceOrderHeader."Document Type" <> ServiceOrderLine."Document Type"::Order) OR
             (ServiceOrderHeader."No." <> ServiceOrderLine."Document No.")
          THEN
            ServiceOrderHeader.GET(ServiceOrderLine."Document Type"::Order,"Order No.");

          IF ServiceInvHeader."Prices Including VAT" <> ServiceOrderHeader."Prices Including VAT" THEN
            InitCurrency("Currency Code");

          IF ServiceInvHeader."Prices Including VAT" THEN BEGIN
            IF NOT ServiceOrderHeader."Prices Including VAT" THEN
              ServiceOrderLine."Unit Price" :=
                ROUND(
                  ServiceOrderLine."Unit Price" * (1 + ServiceOrderLine."VAT %" / 100),
                  Currency."Unit-Amount Rounding Precision");
          END ELSE BEGIN
            IF ServiceOrderHeader."Prices Including VAT" THEN
              ServiceOrderLine."Unit Price" :=
                ROUND(
                  ServiceOrderLine."Unit Price" / (1 + ServiceOrderLine."VAT %" / 100),
                  Currency."Unit-Amount Rounding Precision");
          END;
        END ELSE BEGIN
          ServiceOrderHeader.INIT;
          IF ExtTextLine THEN BEGIN
            ServiceOrderLine.INIT;
            ServiceOrderLine."Line No." := "Order Line No.";
            ServiceOrderLine.Description := Description;
            ServiceOrderLine."Description 2" := "Description 2";
          END ELSE
            ERROR(Text001);
        END;

        ServiceLine := ServiceOrderLine;
        ServiceLine."Line No." := NextLineNo;
        ServiceLine."Document Type" := TempServiceLine."Document Type";
        ServiceLine."Document No." := TempServiceLine."Document No.";
        ServiceLine."Variant Code" := "Variant Code";
        ServiceLine."Location Code" := "Location Code";

        ServiceLine.Quantity := 0;
        ServiceLine."Quantity (Base)" := 0;
        ServiceLine."Outstanding Qty. (Base)" := 0;
        ServiceLine."Outstanding Quantity" := 0;
        ServiceLine."Quantity Shipped" := 0;
        ServiceLine."Qty. Shipped (Base)" := 0;
        ServiceLine."Quantity Invoiced" := 0;
        ServiceLine."Qty. Invoiced (Base)" := 0;
        ServiceLine."Quantity Consumed" := 0;
        ServiceLine."Qty. Consumed (Base)" := 0;
        ServiceLine."Qty. to Consume" := 0;

        ServiceLine."Shipment No." := "Document No.";
        ServiceLine."Shipment Line No." := "Line No.";

        IF NOT ExtTextLine THEN BEGIN
          ServiceLine.VALIDATE(Quantity,Quantity - "Quantity Invoiced" - "Quantity Consumed");
          ServiceLine.VALIDATE("Unit Price",ServiceOrderLine."Unit Price");
          ServiceLine."Allow Line Disc." := ServiceOrderLine."Allow Line Disc.";
          ServiceLine."Allow Invoice Disc." := ServiceOrderLine."Allow Invoice Disc.";
          ServiceLine.VALIDATE("Line Discount %",ServiceOrderLine."Line Discount %");
        END;
        ServiceLine."Attached to Line No." :=
          TransferOldExtLines.TransferExtendedText(
            ServiceOrderLine."Line No.",
            NextLineNo,
            ServiceOrderLine."Attached to Line No.");
        ServiceLine."Shortcut Dimension 1 Code" := ServiceOrderLine."Shortcut Dimension 1 Code";
        ServiceLine."Shortcut Dimension 2 Code" := ServiceOrderLine."Shortcut Dimension 2 Code";
        ServiceLine.INSERT;

        ItemTrackingMgt.CopyHandledItemTrkgToServLine(ServiceOrderLine,ServiceLine);

        FromDocDim.SETRANGE("Table ID",DATABASE::"Service Line");
        FromDocDim.SETRANGE("Document Type",ServiceOrderLine."Document Type"::Order);
        FromDocDim.SETRANGE("Document No.",ServiceOrderLine."Document No.");
        FromDocDim.SETRANGE("Line No.",ServiceOrderLine."Line No.");

        ToDocDim.SETRANGE("Table ID",DATABASE::"Service Line");
        ToDocDim.SETRANGE("Document Type",ServiceLine."Document Type");
        ToDocDim.SETRANGE("Document No.",ServiceLine."Document No.");
        ToDocDim.SETRANGE("Line No.", ServiceLine."Line No.");
        ToDocDim.DELETEALL;

        IF FromDocDim.FIND('-') THEN
          REPEAT
            TempFromDocDim.INIT;
            TempFromDocDim := FromDocDim;
            TempFromDocDim."Table ID" := DATABASE::"Service Line";
            TempFromDocDim."Document Type" := ServiceLine."Document Type";
            TempFromDocDim."Document No." := ServiceLine."Document No.";
            TempFromDocDim."Line No." := ServiceLine."Line No.";
            TempFromDocDim.INSERT;
          UNTIL FromDocDim.NEXT = 0;

        NextLineNo := NextLineNo + 10000;
        IF "Attached to Line No." = 0 THEN
          SETRANGE("Attached to Line No.","Line No.");
      UNTIL (NEXT = 0) OR ("Attached to Line No." = 0);

      IF ServiceOrderHeader.GET(ServiceOrderHeader."Document Type"::Order,"Order No.") THEN BEGIN
        ServiceOrderHeader.MODIFY;
      END;
    END;

    PROCEDURE GetServInvLines@4(VAR TempServInvLine@1000 : TEMPORARY Record 5993);
    VAR
      ServInvLine@1003 : Record 5993;
      ItemLedgEntry@1002 : Record 32;
      ValueEntry@1001 : Record 5802;
    BEGIN
      TempServInvLine.RESET;
      TempServInvLine.DELETEALL;

      IF Type <> Type::Item THEN
        EXIT;

      FilterPstdDocLnItemLedgEntries(ItemLedgEntry);
      ItemLedgEntry.SETFILTER("Invoiced Quantity",'<>0');
      IF ItemLedgEntry.FINDFIRST THEN BEGIN
        ValueEntry.SETCURRENTKEY("Item Ledger Entry No.","Entry Type");
        ValueEntry.SETRANGE("Entry Type",ValueEntry."Entry Type"::"Direct Cost");
        ValueEntry.SETFILTER("Invoiced Quantity",'<>0');
        REPEAT
          ValueEntry.SETRANGE("Item Ledger Entry No.",ItemLedgEntry."Entry No.");
          IF ValueEntry.FINDFIRST THEN
            REPEAT
              IF ValueEntry."Document Type" = ValueEntry."Document Type"::"Service Invoice" THEN
                IF ServInvLine.GET(ValueEntry."Document No.",ValueEntry."Document Line No.") THEN BEGIN
                  TempServInvLine.INIT;
                  TempServInvLine := ServInvLine;
                  IF TempServInvLine.INSERT THEN;
                END;
            UNTIL ValueEntry.NEXT = 0;
        UNTIL ItemLedgEntry.NEXT = 0;
      END;
    END;

    PROCEDURE CalcShippedSaleNotReturned@3(VAR ShippedQtyNotReturned@1003 : Decimal;VAR RevUnitCostLCY@1005 : Decimal;ExactCostReverse@1006 : Boolean);
    VAR
      ItemLedgEntry@1000 : Record 32;
      TotalCostLCY@1007 : Decimal;
      TotalQtyBase@1002 : Decimal;
    BEGIN
      ShippedQtyNotReturned := 0;
      IF (Type <> Type::Item) OR (Quantity <= 0) THEN BEGIN
        RevUnitCostLCY := "Unit Cost (LCY)";
        EXIT;
      END;

      RevUnitCostLCY := 0;
      FilterPstdDocLnItemLedgEntries(ItemLedgEntry);
      IF ItemLedgEntry.FIND('-') THEN
        REPEAT
          ShippedQtyNotReturned := ShippedQtyNotReturned - ItemLedgEntry."Shipped Qty. Not Returned";
          IF ExactCostReverse THEN BEGIN
            ItemLedgEntry.CALCFIELDS("Cost Amount (Expected)","Cost Amount (Actual)");
            TotalCostLCY :=
              TotalCostLCY + ItemLedgEntry."Cost Amount (Expected)" + ItemLedgEntry."Cost Amount (Actual)";
            TotalQtyBase := TotalQtyBase + ItemLedgEntry.Quantity;
          END;
        UNTIL ItemLedgEntry.NEXT = 0;

      IF ExactCostReverse AND (ShippedQtyNotReturned <> 0) AND (TotalQtyBase <> 0) THEN
        RevUnitCostLCY := ABS(TotalCostLCY / TotalQtyBase) * "Qty. per Unit of Measure"
      ELSE
        RevUnitCostLCY := "Unit Cost (LCY)";

      ShippedQtyNotReturned := CalcQty(ShippedQtyNotReturned);
    END;

    LOCAL PROCEDURE CalcQty@9(QtyBase@1000 : Decimal) : Decimal;
    BEGIN
      IF "Qty. per Unit of Measure" = 0 THEN
        EXIT(QtyBase);
      EXIT(ROUND(QtyBase / "Qty. per Unit of Measure",0.00001));
    END;

    PROCEDURE FilterPstdDocLnItemLedgEntries@5(VAR ItemLedgEntry@1000 : Record 32);
    BEGIN
      ItemLedgEntry.RESET;
      ItemLedgEntry.SETCURRENTKEY("Document No.","Document Type","Document Line No.");
      ItemLedgEntry.SETRANGE("Document No.","Document No.");
      ItemLedgEntry.SETRANGE("Document Type",ItemLedgEntry."Document Type"::"Service Shipment");
      ItemLedgEntry.SETRANGE("Document Line No.","Line No.");
    END;

    PROCEDURE ShowItemLedgEntries@7();
    VAR
      ItemLedgEntry@1000 : Record 32;
    BEGIN
      IF Type = Type::Item THEN BEGIN
        FilterPstdDocLnItemLedgEntries(ItemLedgEntry);
        FORM.RUNMODAL(0,ItemLedgEntry);
      END;
    END;

    PROCEDURE ShowItemServInvLines@8();
    VAR
      TempServInvLine@1001 : TEMPORARY Record 5993;
    BEGIN
      IF Type = Type::Item THEN BEGIN
        GetServInvLines(TempServInvLine);
        FORM.RUNMODAL(FORM::"Posted Service Invoice Lines",TempServInvLine);
      END;
    END;

    LOCAL PROCEDURE InitCurrency@6(CurrencyCode@1001 : Code[10]);
    BEGIN
      IF (Currency.Code = CurrencyCode) AND CurrencyRead THEN
        EXIT;

      IF CurrencyCode <> '' THEN
        Currency.GET(CurrencyCode)
      ELSE
        Currency.InitRoundingPrecision;
      CurrencyRead := TRUE;
    END;

    LOCAL PROCEDURE GetCaptionClass@34(FieldNumber@1000 : Integer) : Text[80];
    VAR
      ServShipmentHeader@1001 : Record 5990;
    BEGIN
      IF NOT ServShipmentHeader.GET("Document No.") THEN
        ServShipmentHeader.INIT;
      IF ServShipmentHeader."Prices Including VAT" THEN
        EXIT('2,1,' + GetFieldCaption(FieldNumber));
      EXIT('2,0,' + GetFieldCaption(FieldNumber));
    END;

    LOCAL PROCEDURE GetFieldCaption@31(FieldNumber@1000 : Integer) : Text[100];
    VAR
      Field@1001 : Record 2000000041;
    BEGIN
      Field.GET(DATABASE::"Service Shipment Line",FieldNumber);
      EXIT(Field."Field Caption");
    END;

    PROCEDURE Navigate@10();
    VAR
      NavigateForm@1000 : Form 344;
    BEGIN
      NavigateForm.SetDoc("Posting Date","Document No.");
      NavigateForm.RUN;
    END;

    BEGIN
    END.
  }
}
