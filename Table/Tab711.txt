OBJECT Table 711 BA Db. Cube Measure
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("BA Db. Code");
               TESTFIELD("BA Db. Cube Line No.");
               TESTFIELD("Line No.");
               TESTFIELD("Table No.");
               TESTFIELD("Field No.");
             END;

    CaptionML=[ENU=BA Db. Cube Measure;
               ESP=Medida de cubo de la BD de BA];
  }
  FIELDS
  {
    { 1   ;   ;BA Db. Code         ;Code10        ;TableRelation="BA Database";
                                                   CaptionML=[ENU=BA Db. Code;
                                                              ESP=C¢digo BD de BA] }
    { 2   ;   ;BA Db. Cube Line No.;Integer       ;CaptionML=[ENU=BA Db. Cube Line No.;
                                                              ESP=N§ l¡nea del cubo de BD BA] }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ESP=N§ l¡nea] }
    { 4   ;   ;Name                ;Text50        ;OnValidate=VAR
                                                                BAOvwNewElem@1000 : Codeunit 701;
                                                              BEGIN
                                                                Name := BAOvwNewElem.ConvToDSOName(Name);
                                                              END;

                                                   CaptionML=[ENU=Name;
                                                              ESP=Nombre] }
    { 5   ;   ;Table No.           ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   OnValidate=VAR
                                                                Object@1000 : Record 2000000001;
                                                              BEGIN
                                                                IF "Table No." <> 0 THEN
                                                                  Object.GET(Object.Type::Table,'',"Table No.");
                                                                IF "Table No." <> xRec."Table No." THEN
                                                                  "Field No." := 0;
                                                                CALCFIELDS("Table Name");
                                                              END;

                                                   OnLookup=VAR
                                                              Object@1000 : Record 2000000001;
                                                              Objects@1001 : Form 358;
                                                            BEGIN
                                                              IF "Table No." <> 0 THEN BEGIN
                                                                Object.GET(Object.Type::Table,'',"Table No.");
                                                                Objects.SETRECORD(Object);
                                                              END;
                                                              Object.SETRANGE(Type,Object.Type::Table);
                                                              Objects.SETTABLEVIEW(Object);
                                                              Objects.LOOKUPMODE := TRUE;
                                                              IF Objects.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                Objects.GETRECORD(Object);
                                                                "Table No." := Object.ID;
                                                              END;
                                                              IF "Table No." <> xRec."Table No." THEN
                                                                "Field No." := 0;
                                                              CALCFIELDS("Table Name");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Table No.;
                                                              ESP=Tabla n§] }
    { 6   ;   ;Field No.           ;Integer       ;OnValidate=VAR
                                                                Field@1000 : Record 2000000041;
                                                              BEGIN
                                                                IF "Field No." <> 0 THEN BEGIN
                                                                  TESTFIELD("Table No.");
                                                                  Field.GET("Table No.","Field No.");
                                                                END;
                                                                CALCFIELDS("Field Name");
                                                                IF Name = '' THEN
                                                                  IF STRLEN(STRSUBSTNO(Text001,"Table Name","Field Name")) > MAXSTRLEN(Name) THEN
                                                                    VALIDATE(Name,"Field Name")
                                                                  ELSE
                                                                    VALIDATE(Name,STRSUBSTNO(Text001,"Table Name","Field Name"));
                                                              END;

                                                   OnLookup=VAR
                                                              Field@1000 : Record 2000000041;
                                                              Fields@1001 : Form 7702;
                                                            BEGIN
                                                              TESTFIELD("Table No.");
                                                              IF "Field No." <> 0 THEN BEGIN
                                                                Field.GET("Table No.","Field No.");
                                                                Fields.SETRECORD(Field);
                                                              END;
                                                              Field.SETRANGE(TableNo,"Table No.");
                                                              Field.SETRANGE(Class,Field.Class::Normal);
                                                              Field.SETRANGE(RelationTableNo,0);
                                                              Field.SETFILTER(Type,'%1|%2',Field.Type::Decimal,Field.Type::Integer);
                                                              Fields.SETTABLEVIEW(Field);
                                                              Fields.LOOKUPMODE := TRUE;
                                                              IF Fields.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                Fields.GETRECORD(Field);
                                                                VALIDATE("Field No.",Field."No.");
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Field No.;
                                                              ESP=N§ campo] }
    { 7   ;   ;Aggregation         ;Option        ;CaptionML=[ENU=Aggregation;
                                                              ESP=Agregaci¢n];
                                                   OptionCaptionML=[ENU=Sum,Min,Max,Count;
                                                                    ESP=Suma,M¡n,M x,Contar];
                                                   OptionString=Sum,Min,Max,Count }
    { 8   ;   ;Expression          ;Text250       ;OnValidate=VAR
                                                                Field@1000 : Record 2000000041;
                                                                e@1001 : Text[250];
                                                                i@1002 : Integer;
                                                                j@1003 : Integer;
                                                                Parenthesis@1004 : Integer;
                                                                Brackets@1005 : Integer;
                                                                FieldState@1006 : ' ,BeginTable,EndTable,Period,BeginField,EndField';
                                                              BEGIN
                                                                e := Expression;
                                                                FOR i := 1 TO STRLEN(e) DO BEGIN
                                                                  IF (FieldState = FieldState::EndTable) AND (e[i] <> '.') OR
                                                                     (FieldState = FieldState::Period) AND (e[i] <> '[') THEN
                                                                    ERROR(Text003);
                                                                  IF e[i] = '(' THEN
                                                                    Parenthesis := Parenthesis + 1;
                                                                  IF e[i] = ')' THEN
                                                                    Parenthesis := Parenthesis - 1;
                                                                  IF Parenthesis < 0 THEN
                                                                    ERROR(Text002);
                                                                  IF e[i] = '[' THEN BEGIN
                                                                    Brackets := Brackets + 1;
                                                                    IF FieldState > FieldState::EndField THEN
                                                                      ERROR(Text003);
                                                                  END;
                                                                  IF e[i] = ']' THEN BEGIN
                                                                    Brackets := Brackets - 1;
                                                                    IF NOT (FieldState IN [FieldState::BeginTable,FieldState::BeginField]) THEN
                                                                      ERROR(Text003);
                                                                  END;
                                                                  IF  NOT (e[i] IN ['[',']']) THEN
                                                                    IF FieldState = FieldState::BeginTable THEN BEGIN
                                                                      IF STRLEN(Field.TableName) = MAXSTRLEN(Field.TableName) THEN
                                                                        ERROR(Text004,Field.TableName);
                                                                      Field.TableName := Field.TableName + FORMAT(e[i]);
                                                                    END ELSE
                                                                      IF FieldState = FieldState::BeginField THEN BEGIN
                                                                        IF STRLEN(Field.FieldName) = MAXSTRLEN(Field.FieldName) THEN
                                                                          ERROR(Text004,Field.FieldName);
                                                                        Field.FieldName := Field.FieldName + FORMAT(e[i]);
                                                                      END;

                                                                  IF (e[i] IN ['[',']']) OR (FieldState = FieldState::EndTable) AND (e[i] = '.') THEN
                                                                    FieldState := FieldState + 1;
                                                                  IF FieldState = FieldState::EndField THEN BEGIN
                                                                    IF (Field.TableName = '') OR (Field.FieldName = '') THEN
                                                                      ERROR(Text003);
                                                                    Field.SETRANGE(TableName,Field.TableName);
                                                                    Field.SETRANGE(FieldName,Field.FieldName);
                                                                    Field.FIND('-');
                                                                    CLEAR(Field);
                                                                    FieldState := FieldState::" ";
                                                                  END;
                                                                  IF (Brackets < 0) OR (Brackets > 1) THEN
                                                                    ERROR(Text003);
                                                                END;
                                                                IF FieldState <> FieldState::" " THEN
                                                                  ERROR(Text003);
                                                                IF Parenthesis <> 0 THEN
                                                                  ERROR(Text002);
                                                                IF Brackets <> 0 THEN
                                                                  ERROR(Text003);
                                                              END;

                                                   CaptionML=[ENU=Expression;
                                                              ESP=Expresi¢n] }
    { 9   ;   ;Display Format      ;Text50        ;CaptionML=[ENU=Display Format;
                                                              ESP=Formato de presentaci¢n] }
    { 10  ;   ;Table Name          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table No.)));
                                                   CaptionML=[ENU=Table Name;
                                                              ESP=Nombre tabla] }
    { 11  ;   ;Field Name          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Table No.),
                                                                                                   No.=FIELD(Field No.)));
                                                   CaptionML=[ENU=Field Name;
                                                              ESP=Nombre de campo] }
  }
  KEYS
  {
    {    ;BA Db. Code,BA Db. Cube Line No.,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=%1 %2;ESP=%1 %2';
      Text002@1001 : TextConst 'ENU=Parentheses do not match.;ESP=Los par‚ntesis no coinciden.';
      Text003@1002 : TextConst 'ENU=Square brackets do not match.\Use the format [table name].[field name].;ESP=Los corchetes no coinciden.\Utilice el formato [nombre tabla].[nombre campo].';
      Text004@1003 : TextConst 'ENU=Name too long (%1).;ESP=Nombre demasiado largo (%1).';

    BEGIN
    END.
  }
}
