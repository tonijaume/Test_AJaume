OBJECT Table 7009996 Oferta Precio Hotel
{
  OBJECT-PROPERTIES
  {
    Date=24/06/14;
    Time=16:10:14;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    OnInsert=VAR
               lrOfert@1103355000 : Record 7009996;
             BEGIN
               "Fecha Creacion"   := CURRENTDATETIME;
               "Usuario Creacion" := USERID;

               // Insertamos la siguiente linea de prioridad

               IF Prioridad = 0 THEN BEGIN
                 CLEAR(lrOfert);
                 lrOfert.ASCENDING(FALSE);
                 lrOfert.SETCURRENTKEY(Prioridad);
                 IF NOT lrOfert.FINDFIRST THEN
                   CLEAR(lrOfert);
                 Prioridad := lrOfert.Prioridad + 10000;
               END;
             END;

    OnModify=BEGIN
               "Fecha Ultima Modificacion"   := CURRENTDATETIME;;
               "Usuario Ultima Modificacion" := USERID;
             END;

    OnDelete=VAR
               lwCuerpo@1103355000 : Text[250];
             BEGIN

               // Se envia un correo a Supervisor

               rParAg.FINDFIRST;
               IF rParAg."Direccion Correo Supervisor" <> '' THEN
               BEGIN
                 lwCuerpo := STRSUBSTNO(Text003, Codigo, Descripcion, "Tipo Oferta",USERID, CURRENTDATETIME);
                 cMail.NewMessage(rParAg."Direccion Correo Supervisor",'', Text002, lwCuerpo, '', FALSE);
               END;
             END;

    OnRename=BEGIN
               ERROR(Text001);
             END;

    CaptionML=[ENU=Hotel price Offer;
               ESP=Oferta Precio Hotel];
    LookupFormID=Form7009988;
    DrillDownFormID=Form7009988;
  }
  FIELDS
  {
    { 1   ;   ;Codigo              ;Code10        ;CaptionML=[ENU=Code;
                                                              ESP=Codigo];
                                                   NotBlank=Yes }
    { 10  ;   ;Descripcion         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESP=Descripcion] }
    { 20  ;   ;Prioridad           ;Integer       ;OnValidate=VAR
                                                                lrOfert@1103355000 : Record 7009996;
                                                              BEGIN
                                                                IF Prioridad = 0 THEN
                                                                  ERROR(Text005);

                                                                CLEAR(lrOfert);
                                                                lrOfert.SETCURRENTKEY(Prioridad);
                                                                lrOfert.SETFILTER(Codigo  , '<>%1', Codigo);
                                                                lrOfert.SETRANGE(Prioridad, Prioridad);
                                                                IF lrOfert.FINDFIRST THEN
                                                                  ERROR(Text004, lrOfert.Prioridad, lrOfert.Codigo);
                                                              END;

                                                   CaptionML=[ENU=Priority;
                                                              ESP=Prioridad];
                                                   MinValue=0;
                                                   NotBlank=Yes }
    { 25  ;   ;Fecha Creacion      ;DateTime      ;CaptionML=[ENU=Creation Date;
                                                              ESP=Fecha creacion];
                                                   Editable=No }
    { 26  ;   ;Usuario Creacion    ;Code20        ;CaptionML=[ENU=Creation user;
                                                              ESP=Usuario creacion];
                                                   Editable=No }
    { 29  ;   ;Tipo Fechas         ;Option        ;InitValue=Fecha estancia;
                                                   CaptionML=[ENU=Dates type;
                                                              ESP=Tipo Fechas];
                                                   OptionCaptionML=[ENU="Sale date,Check-in date,Stay date,Sale+stay date,Sale+check-in date,Check-out date,Sale+check-out date,Check-out+check-in date ";
                                                                    ESP=Fecha venta,Fecha entrada,Fecha estancia,Fecha venta+estancia,Fecha venta+entrada,Fecha salida,Fecha venta+salida,Fecha entrada+salida];
                                                   OptionString=Fecha venta,Fecha entrada,Fecha estancia,Fecha venta+estancia,Fecha venta+entrada,Fecha salida,Fecha venta+salida,Fecha entrada+salida }
    { 30  ;   ;Fecha Ultima Modificacion;DateTime ;CaptionML=[ENU=Last modification date;
                                                              ESP=Fecha Ultima Modificacion];
                                                   Editable=No }
    { 31  ;   ;Usuario Ultima Modificacion;Code20 ;CaptionML=[ENU=Last modification user;
                                                              ESP=Usuario Ultima Modificacion];
                                                   Editable=No }
    { 50  ;   ;Porcentaje          ;Boolean       ;OnValidate=BEGIN
                                                                "Aplica Habitacion" := Porcentaje;
                                                                "Aplica Regimen"    := Porcentaje;
                                                                "Aplica Suplementos":= Porcentaje;
                                                                "Aplica Extras"     := Porcentaje;
                                                                "Aplica Ofertas"    := Porcentaje;
                                                              END;

                                                   CaptionML=[ENU=Percentage;
                                                              ESP=Porcentaje] }
    { 51  ;   ;Aplica Habitacion   ;Boolean       ;OnValidate=BEGIN
                                                                IF "Aplica Habitacion" THEN
                                                                  TESTFIELD(Porcentaje);
                                                              END;

                                                   CaptionML=[ENU=Apply room;
                                                              ESP=Aplica Habitacion] }
    { 52  ;   ;Aplica Regimen      ;Boolean       ;OnValidate=BEGIN
                                                                IF "Aplica Regimen" THEN
                                                                  TESTFIELD(Porcentaje);
                                                              END;

                                                   CaptionML=[ENU=Apply board;
                                                              ESP=Aplica Regimen] }
    { 53  ;   ;Aplica Suplementos  ;Boolean       ;OnValidate=BEGIN
                                                                IF "Aplica Suplementos" THEN
                                                                  TESTFIELD(Porcentaje);
                                                              END;

                                                   CaptionML=[ENU=Apply Surcharges;
                                                              ESP=Aplica Suplementos] }
    { 54  ;   ;Aplica Extras       ;Boolean       ;OnValidate=BEGIN
                                                                IF "Aplica Extras" THEN
                                                                  TESTFIELD(Porcentaje);
                                                              END;

                                                   CaptionML=[ENU=Apply Extras;
                                                              ESP=Aplica Extras] }
    { 55  ;   ;Aplica Ofertas      ;Boolean       ;OnValidate=BEGIN
                                                                IF "Aplica Ofertas" THEN
                                                                  TESTFIELD(Porcentaje);
                                                              END;

                                                   CaptionML=[ENU=Apply Offers;
                                                              ESP=Aplica Ofertas] }
    { 60  ;   ;Acumular al Total   ;Boolean       ;OnValidate=BEGIN
                                                                IF "Acumular al Total" THEN BEGIN
                                                                  "Anula Habitacion" := FALSE;
                                                                  "Anula Regimen"    := FALSE;
                                                                  "Anula Suplementos":= FALSE;
                                                                  "Anula Extras"     := FALSE;
                                                                  "Anula Ofertas"    := FALSE;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Accumulate to total;
                                                              ESP=Acumular al total] }
    { 65  ;   ;Anula Habitacion    ;Boolean       ;OnValidate=BEGIN
                                                                IF "Anula Habitacion" THEN
                                                                  TESTFIELD("Acumular al Total", FALSE);
                                                              END;

                                                   CaptionML=[ENU=Cancel Room;
                                                              ESP=Anula Habitacion] }
    { 66  ;   ;Anula Regimen       ;Boolean       ;OnValidate=BEGIN
                                                                IF "Anula Regimen" THEN
                                                                  TESTFIELD("Acumular al Total", FALSE);
                                                              END;

                                                   CaptionML=[ENU=Cancel Board;
                                                              ESP=Anula Regimen] }
    { 67  ;   ;Anula Suplementos   ;Boolean       ;OnValidate=BEGIN
                                                                IF "Anula Suplementos" THEN
                                                                  TESTFIELD("Acumular al Total", FALSE);
                                                              END;

                                                   CaptionML=[ENU=Cancel Supplements;
                                                              ESP=Anula Suplementos] }
    { 68  ;   ;Anula Extras        ;Boolean       ;OnValidate=BEGIN
                                                                IF "Anula Extras" THEN
                                                                  TESTFIELD("Acumular al Total", FALSE);
                                                              END;

                                                   CaptionML=[ENU=Cancel Extras;
                                                              ESP=Anula Extras] }
    { 80  ;   ;Tipo Oferta         ;Option        ;CaptionML=[ENU=Offer type;
                                                              ESP=Tipo Oferta];
                                                   OptionCaptionML=[ENU=",EBB,SPO,Arrival,Free Night,DA=DZ";
                                                                    ESP=",EBB,SPO,Llegada,Free Night,DA=DZ"];
                                                   OptionString=,EBB,SPO,Llegada,Free Night,DA=DZ;
                                                   BlankZero=Yes }
    { 85  ;   ;Oferta Excluyente   ;Boolean       ;CaptionML=[ENU=Exclusive offer;
                                                              ESP=Oferta excluyente] }
    { 86  ;   ;Frecuencia          ;Integer       ;CaptionML=[ENU=Frequency;
                                                              ESP=Frecuencia];
                                                   MinValue=0 }
    { 87  ;   ;Anula Ofertas       ;Boolean       ;OnValidate=BEGIN
                                                                IF "Anula Ofertas" THEN
                                                                  TESTFIELD("Acumular al Total", FALSE);
                                                              END;

                                                   CaptionML=[ENU=Cancel offers;
                                                              ESP=Anula Ofertas] }
  }
  KEYS
  {
    {    ;Codigo                                  ;Clustered=Yes }
    {    ;Prioridad                               ;KeyGroups=RENUMERING }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1103355007 : TextConst 'ENU=XXXXXX;ESP=XXXXXX';
      Text001@1103355000 : TextConst 'ENU=This register cannot be renamed;ESP=Este registro no puede renombrarse';
      rParAg@1103355001 : Record 7009700;
      cMail@1103355002 : Codeunit 397;
      Text002@1103355003 : TextConst 'ENU=Deleted Offer;ESP=Oferta Eliminada';
      Text003@1103355004 : TextConst 'ENU=Offer %1 %2 %3 has been deleted by %4 %5;ESP=Se ha eliminado la oferta %1 %2  %3 por %4 %5';
      Text004@1103355005 : TextConst 'ENU=Priority %1 has already been assigned to the offer %2;ESP=La prioridad %1 ya se ha asignado a la oferta %2';
      Text005@1103355006 : TextConst 'ENU=A priority must be indicated;ESP=Debe de indicar alguna prioridad';

    PROCEDURE CopiaDatosCompra@1103355000(VAR prPrec@1103355000 : Record 7009738);
    VAR
      lrPreCom@1103355001 : Record 7009738;
      lrIncmpt@1103355004 : Record 7009829;
      lrIncmpt2@1103355005 : Record 7009829;
      lwNum@1103355002 : Integer;
      lwOk@1103355003 : Boolean;
    BEGIN
      // CopiaDatosCompra

      IF prPrec."Codigo Precio" <>  prPrec."Codigo Precio"::Ofertas THEN
        EXIT;

      prPrec.Porcentaje            := Porcentaje;

      prPrec."Oferta excluyente"   := "Oferta Excluyente";

      prPrec."Aplica a Habitacion" := "Aplica Habitacion";
      prPrec."Aplica a Regimen"    := "Aplica Regimen";
      prPrec."Aplica a Suplementos":= "Aplica Suplementos";
      prPrec."Aplica a Extras"     := "Aplica Extras";
      prPrec."Aplica a Ofertas"    := "Aplica Ofertas";

      prPrec."Anula a Habitacion"  := "Anula Habitacion";
      prPrec."Anula a Regimen"     := "Anula Regimen";
      prPrec."Anula a Suplementos" := "Anula Suplementos";
      prPrec."Anula a Extras"      := "Anula Extras";

      // AJS 06.10.2008
      // Incluyo el campo Anula Ofertas

      prPrec."Anula a Ofertas" := "Anula Ofertas";

      prPrec."Acumular al total"   := "Acumular al Total";
      prPrec."Subtipo Oferta"      := "Tipo Oferta";
      prPrec."Tipo Fechas"         := "Tipo Fechas";
      prPrec.Frecuencia            := Frecuencia;

      // Insertamos el orden precio

      lwNum := Prioridad;
      CLEAR(lrPreCom);
      REPEAT
        lwOk := NOT lrPreCom.GET(prPrec.Delegacion, prPrec."N§ Contrato", prPrec."Codigo Precio", lwNum);
        IF NOT lwOk THEN
          lwNum +=1;
      UNTIL lwOk;
      prPrec."Orden Precio" := lwNum;

      // Tambien las incompatibilidades
      // Primero borramos lo que ya existia

      CLEAR(lrIncmpt);
      lrIncmpt.SETRANGE(Delegacion     , prPrec.Delegacion);
      lrIncmpt.SETRANGE("N§ Contrato"  , prPrec."N§ Contrato");
      lrIncmpt.SETRANGE("Codigo precio", FORMAT(prPrec."Codigo Precio"));
      lrIncmpt.SETRANGE("Orden precio" , prPrec."Orden Precio");
      IF lrIncmpt.FINDSET THEN
        lrIncmpt.DELETEALL;

      // Luego copiamos la del maestro

      CLEAR(lrIncmpt);
      lrIncmpt.SETRANGE("N§ Contrato"  , Text000);
      lrIncmpt.SETRANGE("Codigo precio", Codigo);
      IF lrIncmpt.FINDSET THEN BEGIN
        REPEAT
          CLEAR(lrIncmpt2);
          lrIncmpt2.Delegacion            := prPrec.Delegacion;
          lrIncmpt2."N§ Contrato"         := prPrec."N§ Contrato";
          lrIncmpt2."Codigo precio"       := FORMAT(prPrec."Codigo Precio");
          lrIncmpt2."Orden precio"        := prPrec."Orden Precio";
          lrIncmpt2."Oferta Incompatible" := lrIncmpt."Oferta Incompatible";
          lrIncmpt2.INSERT(TRUE);
        UNTIL lrIncmpt.NEXT=0;
      END;
    END;

    PROCEDURE CopiaDatosVenta@1100217000(VAR prPrec@1103355000 : Record 7009732);
    VAR
      lrPreCom@1103355001 : Record 7009732;
      lrIncmpt@1103355004 : Record 7009829;
      lrIncmpt2@1103355005 : Record 7009829;
      lwNum@1103355002 : Integer;
      lwOk@1103355003 : Boolean;
    BEGIN
      // CopiaDatosVenta

      IF prPrec."Codigo Precio" <>  prPrec."Codigo Precio"::Ofertas THEN
        EXIT;

      prPrec.Porcentaje            := Porcentaje;

      prPrec."Oferta excluyente"   := "Oferta Excluyente";

      prPrec."Aplica a Habitacion" := "Aplica Habitacion";
      prPrec."Aplica a Regimen"    := "Aplica Regimen";
      prPrec."Aplica a Suplementos":= "Aplica Suplementos";
      prPrec."Aplica a Ofertas"    := "Aplica Ofertas";

      prPrec."Anula a Habitacion"  := "Anula Habitacion";
      prPrec."Anula a Regimen"     := "Anula Regimen";
      prPrec."Anula a Suplementos" := "Anula Suplementos";
      prPrec."Anula a Ofertas"     := "Anula Ofertas";

      prPrec."Acumular al total"   := "Acumular al Total";
      prPrec."Subtipo Oferta"      := "Tipo Oferta";
      prPrec."Tipo Fechas"         := "Tipo Fechas";
      prPrec.Frecuencia            := Frecuencia;

      // Insertamos el orden precio

      lwNum := Prioridad;
      CLEAR(lrPreCom);
      REPEAT
        lwOk := NOT lrPreCom.GET(prPrec.Delegacion, prPrec."N§ Contrato", prPrec."Codigo Precio", lwNum);
        IF NOT lwOk THEN
          lwNum +=1;
      UNTIL lwOk;
      prPrec."Orden Precio" := lwNum;

      // Tambien las incompatibilidades
      // Primero borramos lo que ya existia

      CLEAR(lrIncmpt);
      lrIncmpt.SETRANGE(Delegacion     , prPrec.Delegacion);
      lrIncmpt.SETRANGE("N§ Contrato"  , prPrec."N§ Contrato");
      lrIncmpt.SETRANGE("Codigo precio", FORMAT(prPrec."Codigo Precio"));
      lrIncmpt.SETRANGE("Orden precio" , prPrec."Orden Precio");
      IF lrIncmpt.FINDSET THEN
        lrIncmpt.DELETEALL;

      // Luego copiamos la del maestro

      CLEAR(lrIncmpt);
      lrIncmpt.SETRANGE("N§ Contrato"  , Text000);
      lrIncmpt.SETRANGE("Codigo precio", Codigo);
      IF lrIncmpt.FINDSET THEN
      BEGIN
        REPEAT
          CLEAR(lrIncmpt2);
          lrIncmpt2.Delegacion            := prPrec.Delegacion;
          lrIncmpt2."N§ Contrato"         := prPrec."N§ Contrato";
          lrIncmpt2."Codigo precio"       := FORMAT(prPrec."Codigo Precio");
          lrIncmpt2."Orden precio"        := prPrec."Orden Precio";
          lrIncmpt2."Oferta Incompatible" := lrIncmpt."Oferta Incompatible";
          lrIncmpt2.INSERT(TRUE);
        UNTIL lrIncmpt.NEXT=0;
      END;
    END;

    PROCEDURE ShowIncompatibilidades@1103355001();
    VAR
      lrIcpt@1103355000 : Record 7009829;
    BEGIN
      // ShowIncompatibilidades

      CLEAR(lrIcpt);
      lrIcpt.FILTERGROUP(2);
      lrIcpt.SETRANGE("N§ Contrato"  , Text000);
      lrIcpt.SETRANGE("Codigo precio", Codigo);
      lrIcpt.FILTERGROUP(0);

      FORM.RUN(7009989,lrIcpt);
    END;

    BEGIN
    {
      $001 AJS 24062014 Nueva funcion CopiaDatosVenta para la gestion de ofertas de venta hotel
    }
    END.
  }
}
