OBJECT Table 5430 Planning Error Log
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Planning Error Log;
               ESP=Registro error planificaci¢n];
    LookupFormID=Form5430;
    DrillDownFormID=Form5430;
  }
  FIELDS
  {
    { 1   ;   ;Worksheet Template Name;Code10     ;TableRelation="Req. Wksh. Template";
                                                   CaptionML=[ENU=Worksheet Template Name;
                                                              ESP=Nombre libro hoja dem.] }
    { 2   ;   ;Journal Batch Name  ;Code10        ;TableRelation="Requisition Wksh. Name".Name WHERE (Worksheet Template Name=FIELD(Worksheet Template Name));
                                                   CaptionML=[ENU=Journal Batch Name;
                                                              ESP=Nombre secci¢n diario] }
    { 3   ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              ESP=N§ mov.] }
    { 4   ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   CaptionML=[ENU=Item No.;
                                                              ESP=N§ producto] }
    { 5   ;   ;Error Description   ;Text250       ;CaptionML=[ENU=Error Description;
                                                              ESP=Descripci¢n error] }
    { 6   ;   ;Table ID            ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   CaptionML=[ENU=Table ID;
                                                              ESP=Id. tabla] }
    { 7   ;   ;Table Position      ;Text250       ;CaptionML=[ENU=Table Position;
                                                              ESP=Posici¢n tabla] }
  }
  KEYS
  {
    {    ;Worksheet Template Name,Journal Batch Name,Entry No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE Caption@5() : Text[100];
    VAR
      ReqWkshName@1000 : Record 245;
    BEGIN
      CASE TRUE OF
        GETFILTERS = '':
          EXIT('');
        NOT ReqWkshName.GET("Worksheet Template Name","Journal Batch Name"):
          EXIT('');
        ELSE
          EXIT(STRSUBSTNO('%1 %2',"Journal Batch Name",ReqWkshName.Description));
      END;
    END;

    PROCEDURE SetJnlBatch@1(WkshTemplName@1000 : Code[10];JnlBatchName@1001 : Code[10];ItemNo@1002 : Code[20]);
    BEGIN
      SETRANGE("Worksheet Template Name",WkshTemplName);
      SETRANGE("Journal Batch Name",JnlBatchName);
      IF FIND('+') THEN;
      "Worksheet Template Name" := WkshTemplName;
      "Journal Batch Name" := JnlBatchName;
      "Item No." := ItemNo;
    END;

    PROCEDURE SetError@40(TheError@1000 : Text[250];TheTableID@1002 : Integer;TheTablePosition@1003 : Text[250]);
    BEGIN
      "Entry No." := "Entry No." + 1;
      "Error Description" := TheError;
      "Table ID" := TheTableID;
      "Table Position" := TheTablePosition;
      INSERT;
    END;

    PROCEDURE GetError@47(VAR PlanningErrorLog@1000 : Record 5430) : Boolean;
    BEGIN
      IF NOT FIND('-') THEN
        EXIT(FALSE);
      DELETE;
      PlanningErrorLog.SETRANGE("Worksheet Template Name","Worksheet Template Name");
      PlanningErrorLog.SETRANGE("Journal Batch Name","Journal Batch Name");
      IF PlanningErrorLog.FIND('+') THEN
        "Entry No." := PlanningErrorLog."Entry No." + 1;

      PlanningErrorLog := Rec;
      PlanningErrorLog.INSERT;
      EXIT(TRUE);
    END;

    PROCEDURE ShowError@2();
    VAR
      NoSeries@1012 : Record 308;
      Item@1010 : Record 27;
      SKU@1013 : Record 5700;
      MfgSetup@1009 : Record 99000765;
      Vendor@1008 : Record 23;
      Currency@1007 : Record 4;
      ProdBOMHeader@1006 : Record 99000771;
      ProdBOMVersion@1005 : Record 99000779;
      RtngHeader@1004 : Record 99000763;
      RtngVersion@1003 : Record 99000786;
      WorkCenter@1002 : Record 99000754;
      MachCenter@1001 : Record 99000758;
      RecRef@1000 : RecordRef;
    BEGIN
      IF "Table ID" = 0 THEN
        EXIT;

      RecRef.OPEN("Table ID");
      RecRef.SETPOSITION("Table Position");

      CASE "Table ID" OF
        DATABASE::Item:
          BEGIN
            RecRef.SETTABLE(Item);
            Item.SETRECFILTER;
            FORM.RUNMODAL(FORM::"Item Card",Item);
          END;
        DATABASE::"Stockkeeping Unit":
          BEGIN
            RecRef.SETTABLE(SKU);
            SKU.SETRECFILTER;
            FORM.RUNMODAL(FORM::"Stockkeeping Unit Card",SKU);
          END;
        DATABASE::Currency:
          BEGIN
            RecRef.SETTABLE(Currency);
            Currency.SETRECFILTER;
            FORM.RUNMODAL(FORM::Currencies,Currency);
          END;
        DATABASE::Vendor:
          BEGIN
            RecRef.SETTABLE(Vendor);
            Vendor.SETRECFILTER;
            FORM.RUNMODAL(FORM::"Vendor Card",Vendor);
          END;
        DATABASE::"No. Series":
          BEGIN
            RecRef.SETTABLE(NoSeries);
            NoSeries.SETRECFILTER;
            FORM.RUNMODAL(FORM::"No. Series",NoSeries);
          END;
        DATABASE::"Production BOM Header":
          BEGIN
            RecRef.SETTABLE(ProdBOMHeader);
            ProdBOMHeader.SETRECFILTER;
            FORM.RUNMODAL(FORM::"Production BOM",ProdBOMHeader);
          END;
        DATABASE::"Routing Header":
          BEGIN
            RecRef.SETTABLE(RtngHeader);
            RtngHeader.SETRECFILTER;
            FORM.RUNMODAL(FORM::Routing,RtngHeader);
          END;
        DATABASE::"Production BOM Version":
          BEGIN
            RecRef.SETTABLE(ProdBOMVersion);
            ProdBOMVersion.SETRECFILTER;
            FORM.RUNMODAL(FORM::"Production BOM Version",ProdBOMVersion);
          END;
        DATABASE::"Routing Version":
          BEGIN
            RecRef.SETTABLE(RtngVersion);
            RtngVersion.SETRECFILTER;
            FORM.RUNMODAL(FORM::"Routing Version",RtngVersion);
          END;
        DATABASE::"Machine Center":
          BEGIN
            RecRef.SETTABLE(MachCenter);
            MachCenter.SETRECFILTER;
            FORM.RUNMODAL(FORM::"Machine Center Card",MachCenter);
          END;
        DATABASE::"Work Center":
          BEGIN
            RecRef.SETTABLE(WorkCenter);
            WorkCenter.SETRECFILTER;
            FORM.RUNMODAL(FORM::"Work Center Card",WorkCenter);
          END;
        DATABASE::"Manufacturing Setup":
          BEGIN
            RecRef.SETTABLE(MfgSetup);
            FORM.RUNMODAL(0,MfgSetup);
          END;
        DATABASE::"Transfer Route":
          FORM.RUNMODAL(FORM::"Transfer Routes");
      END;
    END;

    BEGIN
    END.
  }
}
