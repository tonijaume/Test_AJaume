OBJECT Table 7010372 Seguro Vehiculo
{
  OBJECT-PROPERTIES
  {
    Date=16/10/09;
    Time=12:30:43;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               rSeguro.RESET;
               IF rSeguro.FINDLAST THEN BEGIN
                 Codigo := INCSTR(rSeguro.Codigo);

               END
               ELSE
                 Codigo := '1';
               IF("Fecha Expiracion"<>0D) AND ("Fecha Inicio"<>0D) THEN
                 act_seguro;
             END;

    OnModify=BEGIN
               IF("Fecha Expiracion"<>0D) AND ("Fecha Inicio"<>0D) THEN
                 act_seguro;
             END;

    CaptionML=ESP=Seguro Vehiculo;
    LookupFormID=Form7010450;
    DrillDownFormID=Form7010450;
  }
  FIELDS
  {
    { 1   ;   ;Compa¤ia Aseguradora;Text50        ;CaptionML=ESP=Compa¤ia Aseguradora }
    { 2   ;   ;Fecha Inicio        ;Date          ;OnValidate=BEGIN
                                                                IF "Fecha Inicio" <> xRec."Fecha Inicio" THEN
                                                                  comp_fecha;
                                                              END;

                                                   CaptionML=ESP=Fecha Inicio }
    { 3   ;   ;Fecha Expiracion    ;Date          ;OnValidate=BEGIN
                                                                IF "Fecha Expiracion" <> xRec."Fecha Expiracion" THEN
                                                                  comp_fecha;
                                                              END;

                                                   CaptionML=ESP=Fecha Expiracion }
    { 4   ;   ;Num Poliza          ;Text50        ;CaptionML=ESP=Num Poliza }
    { 5   ;   ;Importe Seguro      ;Decimal       ;CaptionML=ESP=Importe Seguro }
    { 6   ;   ;Codigo              ;Code10        ;CaptionML=ESP=Codigo }
    { 7   ;   ;Matricula           ;Code10        ;TableRelation=IF (Matricula=CONST()) Vehiculo.Matricula;
                                                   CaptionML=ESP=Matricula }
  }
  KEYS
  {
    {    ;Codigo                                  ;Clustered=Yes }
    {    ;Matricula,Fecha Inicio                   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      rSeguro@1103350000 : Record 7010372;
      rVehiculo@1103350001 : Record 7010346;
      text001@1103350002 : TextConst 'ESP=''La fecha de expiraci¢n ha de ser posterior a la fecha de inicio del seguro''';
      text002@1103350003 : TextConst 'ESP=''La fecha de inicio est  dentro de otra p¢liza''';
      text003@1103350004 : TextConst 'ESP=''La fecha de expiraci¢n est  dentro de otra p¢liza''';
      text004@1103350005 : TextConst 'ESP=''Hay otra p¢liza que cubre estas fechas''';
      text005@1103350006 : TextConst 'ESP=''No se encuentra ese veh¡culo''';

    PROCEDURE comp_fecha@1103350002();
    BEGIN
      // comp_fecha

      //Comprobamos que la fecha de expiraci¢n sea posterior a la fecha de inicio
      IF("Fecha Expiracion"<>0D) AND ("Fecha Inicio"<>0D) THEN BEGIN
        IF "Fecha Expiracion" < "Fecha Inicio" THEN
          MESSAGE(text001);
      END;

      //Comprobamos que la fecha de inicio de esta p¢liza no est‚ dentro de otra ya existente
      IF "Fecha Inicio" <> 0D THEN BEGIN
        rSeguro.RESET;
        rSeguro.SETRANGE(Matricula,Matricula);
        rSeguro.SETFILTER(Codigo,'<>%1',Codigo);
        rSeguro.SETFILTER("Fecha Inicio",'<=%1',"Fecha Inicio");
        rSeguro.SETFILTER("Fecha Expiracion",'>=%1',"Fecha Inicio");
        IF rSeguro.FINDFIRST THEN
          ERROR(text002);
      END;

      //Comprobamos que la fecha de expiraci¢n de esta p¢liza no est‚ dentro de otra ya existente
      IF "Fecha Expiracion" <> 0D THEN BEGIN
        rSeguro.RESET;
        rSeguro.SETRANGE(Matricula,Matricula);
        rSeguro.SETFILTER(Codigo,'<>%1',Codigo);
        rSeguro.SETFILTER("Fecha Inicio",'<=%1',"Fecha Expiracion");
        rSeguro.SETFILTER("Fecha Expiracion",'>=%1',"Fecha Expiracion");
        IF rSeguro.FINDFIRST THEN
          ERROR(text003);
      END;

      //Comprobamos que no estamos introduciendo la p¢liza dentro de otra
      IF ("Fecha Inicio" <> 0D) AND ("Fecha Expiracion" <> 0D) THEN BEGIN
        rSeguro.RESET;
        rSeguro.SETRANGE(Matricula,Matricula);
        rSeguro.SETFILTER(Codigo,'<>%1',Codigo);
        rSeguro.SETFILTER("Fecha Inicio",'>=%1',"Fecha Inicio");
        rSeguro.SETFILTER("Fecha Expiracion",'<=%1',"Fecha Expiracion");
        IF rSeguro.FINDFIRST THEN
            ERROR(text004);
      END;
    END;

    PROCEDURE act_seguro@1103350000();
    BEGIN
      // act_seguro
      IF rVehiculo.GET(Matricula) THEN BEGIN
        IF rVehiculo."Fecha expiracion seguro" <= "Fecha Expiracion" THEN BEGIN
          rVehiculo."Compa¤ia aseguradora":="Compa¤ia Aseguradora";
          rVehiculo."N§ Poliza":="Num Poliza";
          rVehiculo."Fecha expiracion seguro":="Fecha Expiracion";
          rVehiculo."Importe seguro":="Importe Seguro";
          rVehiculo.MODIFY;
        END;
      END

      ELSE
        ERROR(text005);
    END;

    BEGIN
    END.
  }
}
