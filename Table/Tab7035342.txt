OBJECT Table 7035342 Registro diario
{
  OBJECT-PROPERTIES
  {
    Date=23/07/15;
    Time=[ 3:05:03];
    Modified=Yes;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    OnInsert=VAR
               rDiasAbiertos@1100244000 : Record 7035341;
             BEGIN
               IF "No Importacion"=0 THEN
                 comprobar_dia_abierto;

               rReg.RESET;
               rReg.SETCURRENTKEY(id);
               rReg.ASCENDING(FALSE);
               IF rReg.FINDFIRST THEN
                 id := rReg.id + 1
               ELSE
                 id := 1;

               "Usuario introduccion" := USERID;
               "Fecha introduccion"   := TODAY;
             END;

    OnModify=BEGIN
               comprobar_dia_abierto;
               comprobarkms;
               comprobar_hora_respostaje();

               "Fecha ult modificacion"   := TODAY;
               "Usuario ult modificacion" := USERID;
             END;

    OnDelete=BEGIN

               comprobar_dia_abierto;
             END;

    CaptionML=ESP=Registro diario;
    LookupFormID=Form7035346;
    DrillDownFormID=Form7035346;
  }
  FIELDS
  {
    { 1   ;   ;Dia                 ;Date          ;CaptionML=ESP=Dia }
    { 2   ;   ;Vehiculo            ;Code10        ;OnValidate=VAR
                                                                rDiasabiertos@1100244000 : Record 7035341;
                                                              BEGIN
                                                                comprobar_dia_abierto;

                                                                IF Vehiculo <> '' THEN BEGIN
                                                                  rVeh.RESET;
                                                                  rVeh.SETCURRENTKEY(Garaje, Numero);
                                                                  rVeh.SETRANGE(Numero     , Vehiculo);
                                                                  IF rVeh.FINDFIRST THEN BEGIN
                                                                    IF rVeh."Vehiculo propio" = FALSE THEN
                                                                      ERROR(err10);
                                                                    Matricula        := rVeh.Matricula;
                                                                  END
                                                                  ELSE BEGIN
                                                                    rVeh.RESET;
                                                                    rVeh.SETCURRENTKEY(Garaje, Numero);
                                                                    rVeh.SETFILTER(Numero     , '@' + Vehiculo + '*');
                                                                    IF rVeh.FINDFIRST THEN BEGIN
                                                                      IF rVeh."Vehiculo propio" = FALSE THEN
                                                                        ERROR(err10);
                                                                      Matricula        := rVeh.Matricula;
                                                                      Vehiculo         := rVeh.Numero;
                                                                    END
                                                                    ELSE
                                                                      ERROR(err7,Vehiculo);
                                                                  END;
                                                                  Garaje := rVeh.Garaje;

                                                                  buscar_chofer;
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              rVeh.RESET;
                                                              rVeh.SETRANGE("Vehiculo propio", TRUE);
                                                              // rVeh.SETRANGE("No Imprimir estadisticas", FALSE);
                                                              IF FORM.RUNMODAL(7010373, rVeh) = ACTION::LookupOK THEN
                                                                VALIDATE(Vehiculo , rVeh.Numero);
                                                            END;

                                                   CaptionML=ESP=Vehiculo }
    { 3   ;   ;Conductor           ;Code20        ;OnValidate=BEGIN
                                                                // Si empresa en TRN no combrobamos que el conductor sea externo
                                                                rPar.FINDFIRST;
                                                                IF Vehiculo = '' THEN
                                                                BEGIN
                                                                  Conductor := '';
                                                                  MESSAGE(err9);
                                                                  EXIT;
                                                                END;

                                                                IF Conductor <> '' THEN
                                                                BEGIN
                                                                  rCon.RESET;
                                                                  rCon.SETCURRENTKEY("Nombre Abreviado" , Garaje);
                                                                  rCon.SETRANGE("Nombre Abreviado"      , Conductor);
                                                                  IF rCon.FINDFIRST THEN BEGIN
                                                                    IF rPar."Empresa Real" <> rPar."Empresa Real"::TRN THEN
                                                                    BEGIN
                                                                      IF rCon.Externo = TRUE THEN
                                                                        ERROR(err11);
                                                                    END;
                                                                    "Cod. Conductor" := rCon.Conductor;
                                                                  END
                                                                  ELSE
                                                                  BEGIN
                                                                    rCon.RESET;
                                                                    rCon.SETCURRENTKEY("Nombre Abreviado" , Garaje);
                                                                    rCon.SETFILTER("Nombre Abreviado"     , '@'+Conductor+'*');
                                                                    IF rCon.FINDFIRST THEN
                                                                    BEGIN
                                                                      IF rPar."Empresa Real" <> rPar."Empresa Real"::TRN THEN
                                                                      BEGIN
                                                                        IF rCon.Externo = TRUE THEN
                                                                          ERROR(err11);
                                                                      END;
                                                                      "Cod. Conductor" := rCon.Conductor;
                                                                      Conductor        := rCon."Nombre Abreviado";
                                                                    END
                                                                    ELSE
                                                                      ERROR(err8,Conductor);
                                                                  END;
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              rCon.RESET;
                                                              rCon.SETRANGE(Externo, FALSE);
                                                              IF FORM.RUNMODAL(7010341, rCon) = ACTION::LookupOK THEN
                                                                VALIDATE(Conductor , rCon."Nombre Abreviado");
                                                            END;

                                                   CaptionML=ESP=Conductor }
    { 4   ;   ;id                  ;Integer       ;CaptionML=ESP=id }
    { 5   ;   ;Kms                 ;Decimal       ;OnValidate=BEGIN
                                                                comprobarkms;
                                                              END;

                                                   CaptionML=ESP=Kms;
                                                   DecimalPlaces=0:0 }
    { 6   ;   ;Litros repostados   ;Decimal       ;OnValidate=BEGIN
                                                                IF "Litros repostados" > 1000 THEN
                                                                  ERROR(err14);

                                                                IF "Hora repostaje" = 0T THEN
                                                                  ERROR(err4);

                                                                IF Kms = 0 THEN
                                                                  ERROR(err6);
                                                              END;

                                                   CaptionML=ESP=Litros repostados;
                                                   DecimalPlaces=0:2 }
    { 7   ;   ;Rotura Tacografo    ;Boolean       ;CaptionML=ESP=Rotura Tacografo }
    { 8   ;   ;Incidencias         ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist(Incidencia WHERE (Matricula=FIELD(Matricula),
                                                                                       Fecha inicio reparacion=FIELD(Filtro fecha inicio),
                                                                                       Fecha final reparacion=FIELD(Filtro fecha fin)));
                                                   CaptionML=ESP=Incidencias;
                                                   Description=Sobre incidencias;
                                                   Editable=No }
    { 9   ;   ;Accidentes          ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist(Accidente WHERE (Matricula=FIELD(Matricula),
                                                                                      Fecha=FIELD(Dia)));
                                                   CaptionML=ESP=Accidentes;
                                                   Description=Sobre accidentes;
                                                   Editable=No }
    { 10  ;   ;Usuario introduccion;Code20        ;CaptionML=ESP=Usuario introduccion }
    { 11  ;   ;Fecha introduccion  ;Date          ;CaptionML=ESP=Fecha introduccion }
    { 12  ;   ;Hora repostaje      ;Time          ;OnValidate=BEGIN
                                                                {*
                                                                IF (Vehiculo = '') OR (Conductor = '') THEN
                                                                  ERROR(err5);
                                                                *}

                                                                comprobar_hora_respostaje;

                                                                buscar_chofer;
                                                              END;

                                                   CaptionML=ESP=Hora repostaje }
    { 13  ;   ;Anulado             ;Boolean       ;OnValidate=BEGIN
                                                                IF Anulado THEN BEGIN
                                                                  "Usuario anulacion" := USERID;
                                                                  "Fecha anulacion"   := TODAY;
                                                                END;
                                                              END;

                                                   CaptionML=ESP=Anulado }
    { 14  ;   ;Usuario anulacion   ;Code20        ;CaptionML=ESP=Usuario anulacion }
    { 15  ;   ;Fecha anulacion     ;Date          ;CaptionML=ESP=Fecha anulacion }
    { 16  ;   ;Cod. Conductor      ;Code10        ;TableRelation=Conductor.Conductor;
                                                   CaptionML=ESP=Cod. Conductor }
    { 17  ;   ;Matricula           ;Code10        ;TableRelation=Vehiculo.Matricula;
                                                   OnValidate=VAR
                                                                lrVeh@1103355000 : Record 7010346;
                                                              BEGIN
                                                                CLEAR(lrVeh);
                                                                Carroceria:='';
                                                                IF lrVeh.GET(Matricula) THEN
                                                                  VALIDATE(Carroceria, lrVeh.Carroceria);
                                                              END;

                                                   CaptionML=ESP=Matricula }
    { 18  ;   ;Filtro fecha inicio ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=ESP=Filtro fecha inicio }
    { 19  ;   ;Filtro fecha fin    ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=ESP=Filtro fecha fin }
    { 20  ;   ;Kilometros Reales   ;Decimal       ;CaptionML=ESP=Kilometros Reales;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 21  ;   ;Garaje              ;Code10        ;TableRelation=Garaje.Garaje;
                                                   CaptionML=ESP=Garaje;
                                                   Editable=No }
    { 23  ;   ;Kms realizados      ;Decimal       ;CaptionML=ESP=Kms realizados;
                                                   Description=Son los kilometros realizados desde el ultimo repostaje;
                                                   Editable=No }
    { 25  ;   ;Fecha ult modificacion;Date        ;CaptionML=ESP=Fecha ult modificacion;
                                                   Description=Informacion auditoria;
                                                   Editable=No }
    { 27  ;   ;Usuario ult modificacion;Code20    ;CaptionML=ESP=Usuario ult modificacion;
                                                   Description=Informacion auditoria;
                                                   Editable=No }
    { 28  ;   ;No Importacion      ;Integer       ;CaptionML=ESP=No Importacion;
                                                   Description=Numero de importacion }
    { 29  ;   ;No Estadisticas     ;Boolean       ;CaptionML=ESP=No Estadisticas }
    { 30  ;   ;Kms Reg Anterior    ;Decimal       ;CaptionML=ESP=Kms Reg Anterior }
    { 40  ;   ;Conductor Importacion;Text30       ;CaptionML=ESP=Conductor Importacion;
                                                   Editable=No }
    { 41  ;   ;Matricula Importacion;Code10       ;CaptionML=ESP=Matricula Importacion;
                                                   Editable=No }
    { 60  ;   ;Posicion            ;Code10        ;CaptionML=ESP=Posicion;
                                                   Description=TER }
    { 61  ;   ;Folio               ;Code20        ;CaptionML=ESP=Folio;
                                                   Description=TER }
    { 62  ;   ;Proveedor           ;Code10        ;TableRelation=Vendor.No.;
                                                   OnValidate=VAR
                                                                lrTar@1103355000 : Record 7035383;
                                                              BEGIN
                                                                IF Proveedor <> '' THEN BEGIN

                                                                  // TER Obligamos a que el proveedor tenga una tarifa
                                                                  // En principio solo para Terramar o los que utilicen tarifas

                                                                  CLEAR(lrTar);
                                                                  lrTar.SETRANGE(Concepto, lrTar.Concepto::Gasoil);
                                                                  IF lrTar.FINDFIRST THEN BEGIN
                                                                    lrTar.SETCURRENTKEY(Concepto,"Fecha Desde","Fecha Hasta");
                                                                    lrTar.SETFILTER("Fecha Desde" ,'<=%1', Dia);
                                                                    lrTar.SETFILTER("Fecha Hasta" ,'>=%1', Dia);
                                                                    lrTar.SETRANGE(Proveedor      , Proveedor);
                                                                    IF NOT lrTar.FINDFIRST THEN
                                                                     ERROR(Text0002, Proveedor, Dia);
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=ESP=Proveedor;
                                                   Description=TER }
    { 100 ;   ;Sospechoso          ;Boolean       ;CaptionML=ESP=Sospechoso;
                                                   Description=Registros con alg£n fallo de importacion TRN }
    { 101 ;   ;Tipo Contador       ;Code1         ;CaptionML=ESP=Tipo Contador;
                                                   Description=TRN Tipo Contador en la importacion K=Kilometros H=Horas;
                                                   Editable=No }
    { 102 ;   ;Tipo Conductor      ;Code1         ;CaptionML=ESP=Tipo Conductor;
                                                   Description=TRN Importacion.Debe de ser C.;
                                                   Editable=No }
    { 103 ;   ;Garaje Borna        ;Code10        ;TableRelation=Garaje.Garaje;
                                                   CaptionML=ESP=Garaje Borna }
    { 104 ;   ;Marca               ;Text30        ;FieldClass=Normal;
                                                   CaptionML=ESP=Marca }
    { 105 ;   ;Potencia fiscal     ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vehiculo."Potencia fiscal/real (CVF/KW)" WHERE (Matricula=FIELD(Matricula)));
                                                   CaptionML=ESP=Potencia fiscal }
    { 106 ;   ;Plazas              ;Integer       ;FieldClass=Normal;
                                                   CaptionML=ESP=Plazas }
    { 107 ;   ;Carroceria          ;Text30        ;FieldClass=Normal;
                                                   CaptionML=ESP=Carroceria }
    { 108 ;   ;ID Borna            ;Code10        ;CaptionML=ESP=ID Borna }
    { 109 ;   ;Descripcion Borna   ;Text30        ;CaptionML=ESP=Descripcion Borna }
  }
  KEYS
  {
    {    ;Dia,Vehiculo,Conductor,id               ;Clustered=Yes }
    {    ;Matricula,Anulado                        }
    {    ;id                                       }
    {    ;Vehiculo,Dia,Hora repostaje,Anulado,Kilometros Reales;
                                                   SumIndexFields=Kms realizados }
    {    ;Matricula,Dia,Anulado                   ;SumIndexFields=Litros repostados,Kms realizados }
    {    ;Dia,Anulado,id                           }
    {    ;Fecha introduccion,Dia                   }
    {    ;Sospechoso                               }
    {    ;Garaje Borna,Marca,Carroceria,Plazas,Hora repostaje }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      rVeh@1100244000 : Record 7010346;
      rCon@1100244001 : Record 7010327;
      rReg@1100244002 : Record 7035342;
      err1@1100244003 : TextConst 'ESP=Los kilometros del tac¢grafo no pueden ser inferiores a los que tenia en el ultimo recorrido.\\Fecha anterior %1 kms. anteriores %2.\\Si los datos son correctos especfique primero rotura de taquigrafo';
      cGesdiaria@1100244004 : Codeunit 7035321;
      rPar@1103350000 : Record 7010311;
      wInci@1100244005 : Boolean;
      wAcci@1100244006 : Boolean;
      err2@1100244007 : TextConst 'ESP=El dia %1 no esta abierto.\\Dirigase a Servicios , Actividades periodicas ,Abrir dia.';
      err3@1100244008 : TextConst 'ESP=La hora de repostaje para el vehiculo %1 a fecha %2 no puede ser inferior a la del ultimo recorrido %3';
      err4@1100244009 : TextConst 'ESP=Debe especificar la hora de repostaje';
      err5@1100244010 : TextConst 'ESP=Debe especificar vehiculo y conductor';
      err6@1100244011 : TextConst 'ESP=Debe especificar los kilometros.';
      err7@1100244012 : TextConst 'ESP=No existe vehiculo con numero %1';
      err8@1100244013 : TextConst 'ESP=No existe conductor %1';
      err9@1100244014 : TextConst 'ESP=Debe especificar vehiculo';
      err10@1100244015 : TextConst 'ESP=Solo puede seleccionar vehiculos propios';
      err11@1100244016 : TextConst 'ESP=No est  permitido la seleccion de conductores externos';
      err12@1100244017 : TextConst 'ESP=No se pueden modificar datos de un dia ya cerrado (%1)';
      err13@1100244018 : TextConst 'ESP=No se pueden insertar datos a fecha %1 , ya que se han encontrado datos en fechas posteriores. \ Debe introducir los datos en orden correlativo.';
      txt1@1100244019 : TextConst 'ESP=Ha introducido un repostaje que representa 1000 o mas kms realizados.';
      err14@1100244020 : TextConst 'ESP=Se han introducido mas de 1000 litros de repostaje.';
      Text0001@1100244021 : TextConst 'ESP=Ya se encuentra el registro %1';
      Text0002@1103355000 : TextConst 'ESP=No existe Tarifa Gasoil para proveedor %1 fecha %2';
      Text0003@1103355001 : TextConst 'ESP=Indique nuevo Kilometraje #1##########';
      wVentana@1103355002 : Dialog;
      Text0004@1103355003 : TextConst 'ESP=¨Desea Cambiar el Kilometraje a %1?';

    PROCEDURE comprobarkms@1();
    BEGIN
      // comprobarkms
      //

      comprkmsE(TRUE);
    END;

    PROCEDURE comprkmsE@1103355000(pwError@1103355000 : Boolean);
    VAR
      rreg2@1100244000 : Record 7035342;
    BEGIN
      // comprkmsE
      //
      // pwError indica si debe de generarse error o unicamente actualizar los datos de Kms

      {*
      IF (Vehiculo = '') OR (Conductor = '' ) THEN
        IF pwError THEN
          ERROR(err5);
      *}

      CLEAR(rReg);
      rReg.ASCENDING(FALSE);
      rReg.SETCURRENTKEY(Vehiculo,
                         Dia,
                         "Hora repostaje",
                         Anulado);
      rReg.SETRANGE (Vehiculo , Vehiculo);
      rReg.SETFILTER(Dia      , '<=%1', Dia);
      rReg.SETRANGE (Anulado  , FALSE);
      rReg.SETFILTER(id       ,'<>%1' ,id);
      IF rReg.FINDFIRST THEN BEGIN

        "Kms Reg Anterior" := rReg.Kms;
        // ----  Si no hay rotura se controla que los kms sean superior al anterior repostaje del vehiculo

        IF rReg.Dia <> Dia THEN BEGIN
          IF NOT "Rotura Tacografo" THEN
            IF Kms < rReg.Kms THEN
              IF pwError THEN
                ERROR(err1,rReg.Dia, rReg.Kms)
        END
        ELSE BEGIN
          rReg.SETRANGE(Dia, Dia);
          rReg.SETFILTER("Hora repostaje", '<%1', "Hora repostaje");
          IF rReg.FINDFIRST THEN BEGIN
            IF NOT "Rotura Tacografo" THEN
              IF Kms < rReg.Kms THEN
                IF pwError THEN
                  ERROR(err1,rReg.Dia, rReg.Kms)
          END
          ELSE BEGIN
            rReg.SETFILTER(Dia, '<%1', Dia);
            rReg.SETRANGE("Hora repostaje");
            IF rReg.FINDFIRST THEN BEGIN
              IF NOT "Rotura Tacografo" THEN
                IF Kms < rReg.Kms THEN
                  IF pwError THEN
                    ERROR(err1,rReg.Dia, rReg.Kms)
            END;
          END;
        END;
      END;

      // ----  AJS 27.05.2003 El calculo de los kilometros reales se hace sumando los kilometros realizados a los kilometros  ----
      // ----  reales del repostaje anterior ya que este campo no tiene en cuenta las roturas de tacografos                   ----

      IF NOT "Rotura Tacografo" THEN
        "Kms realizados" := Kms - rReg.Kms
      ELSE
        "Kms realizados" := Kms;

      //IF NOT "Rotura Tacografo" THEN
      //  "Kilometros Reales" := Kms
      //ELSE
      //  "Kilometros Reales" := rReg."Kilometros Reales" + Kms;

      IF rReg."Kilometros Reales" = 0 THEN
        "Kilometros Reales" := Kms
      ELSE
        "Kilometros Reales" := rReg."Kilometros Reales" + "Kms realizados";

      // ----  FIN AJS 27.05.2003  ----

      IF ("Kms realizados" >= 1000) AND ("No Importacion"=0) THEN
        MESSAGE(txt1);
    END;

    PROCEDURE comprobar_hora_respostaje@2();
    BEGIN
      // comprobar_hora_repostaje
      //
      IF "No Importacion"<>0 THEN
        EXIT;

      rReg.RESET;
      rReg.SETFILTER(Dia    , '=%1', Dia);
      rReg.SETRANGE(Vehiculo, Vehiculo);
      rReg.SETRANGE(Anulado , FALSE);
      rReg.SETFILTER(id     , '<>%1', id);
      IF rReg.FINDLAST THEN BEGIN
          IF "Hora repostaje" < rReg."Hora repostaje" THEN
            ERROR(err3, Vehiculo, Dia,rReg."Hora repostaje");
      END;
    END;

    PROCEDURE comprobar_dia_abierto@4();
    VAR
      rDiasAbiertos@1100244000 : Record 7035341;
    BEGIN
      // comprobar_dia_abierto
      //
      rPar.FINDFIRST;

      rReg.RESET;
      rReg.SETRANGE(Conductor ,'');
      rReg.SETRANGE(Vehiculo ,'');
      IF rReg.FINDFIRST THEN
        rReg.DELETEALL;

      rDiasAbiertos.RESET;
      IF NOT rDiasAbiertos.GET(Dia) THEN
        ERROR(err2,Dia)
      ELSE
        IF NOT rPar."No control Registro en Cierre" THEN BEGIN // Parametro para no controlar el registro en el cierre
        IF rDiasAbiertos.Cerrado THEN
          ERROR(err12,Dia);
        END
        ELSE BEGIN // JPT 20/02/07 TER - Si no control el cierre tiene que controlar que no se haya generado el gasto de gasoil del dia
          cGesdiaria.GetDiaGeneradoGasoil(Dia,1);
        END;

      //Se comprueba que no haya informacion con fecha superior.
      rReg.RESET;
      rReg.SETCURRENTKEY(Vehiculo         ,
                         Dia              ,
                         "Hora repostaje" ,
                         Anulado         );
      rReg.SETFILTER(Dia    , '>%1',Dia);
      rReg.SETRANGE(Anulado , FALSE);


      // AJS 24.05.2005
      // Hago que no tenga en cuenta los repostajes introducidos cuando un vehiculo vuelve a base despues de haber
      // estado en otra base durante un tiempo

      rReg.SETRANGE("No Estadisticas", FALSE);

      IF rReg.FINDFIRST THEN
        ERROR(err13,Dia);
    END;

    PROCEDURE buscar_chofer@3();
    VAR
      lrParte@1100244000 : Record 7010360;
    BEGIN
      // buscar_chofer
      //
      // AJS 17.03.2003  A traves de la fecha, vehiculo y hora trataremos de obtener el conductor
      //

      IF (Vehiculo = '') OR ("Hora repostaje" = 0T) THEN
        EXIT;

      lrParte.RESET;
      lrParte.SETCURRENTKEY(Fecha,
                            Vehiculo,
                            "Hora inicio");
      lrParte.SETRANGE (Fecha        , Dia);
      lrParte.SETRANGE (Vehiculo     , Matricula);
      lrParte.SETFILTER("Hora inicio", '<=%1', "Hora repostaje");
      lrParte.ASCENDING(FALSE);
      IF lrParte.FINDFIRST THEN
        VALIDATE(Conductor, lrParte.Conductor)

      // AJS 22.05.2006
      // Para los casos en que un conductor reposte de madrugada buscaremos el ultimo parte sin especificar filtro de hora

      ELSE BEGIN
        lrParte.SETRANGE("Hora inicio");
        IF lrParte.FINDFIRST THEN
          VALIDATE(Conductor, lrParte.Conductor)
      END;
    END;

    PROCEDURE ComprobarDuplicidad@1100244000();
    BEGIN
      // ComprobarDuplicidad


      CLEAR(rReg);
      rReg.SETCURRENTKEY(Vehiculo,Dia,"Hora repostaje",Anulado,"Kilometros Reales");
      rReg.SETRANGE(Anulado    , FALSE);
      rReg.SETRANGE(Dia        , Dia);
      rReg.SETRANGE(Vehiculo   , Vehiculo);
      rReg.SETRANGE(Conductor  , Conductor);
      rReg.SETRANGE(Kms        , Kms);
      rReg.SETRANGE("Litros repostados","Litros repostados");

      IF rReg.FINDFIRST THEN
        ERROR(Text0001,rReg.GETFILTERS);
    END;

    PROCEDURE CambiaKms@1103355001();
    VAR
      lwKm@1103355000 : Decimal;
      lrReg@1103355001 : Record 7035342;
    BEGIN
      // CambiaKms

      wVentana.OPEN(Text0003);
      wVentana.UPDATE(1, Kms);
      wVentana.INPUT(1 , lwKm);
      wVentana.CLOSE;

      IF NOT CONFIRM(Text0004,FALSE,lwKm) THEN
        EXIT;

      Kms := lwKm;
      comprkmsE(FALSE);
      MODIFY;

      // Buscamos si existenregistros posteriorre a ese para cambiarle el campo "Kms realizados"
      CLEAR(lrReg);
      lrReg.SETCURRENTKEY(Matricula,Dia,Anulado);
      lrReg.SETRANGE(Matricula, Matricula);
      lrReg.SETFILTER(Dia,'>=%1', Dia);
      lrReg.SETRANGE(Anulado,FALSE);
      lrReg.SETFILTER(id, '<>%1' , id);
      IF lrReg.FINDSET(TRUE) THEN BEGIN
        REPEAT
          lrReg.comprkmsE(FALSE); // No generamos errores
          lrReg.MODIFY;
        UNTIL lrReg.NEXT=0;
      END;
    END;

    PROCEDURE LitrosA100@1103355002(pwAnt@1103355001 : Integer) : Decimal;
    VAR
      lrRegD@1103355000 : Record 7035342;
      lwKms@1103355002 : Decimal;
      lwLtrs@1103355003 : Decimal;
      lwN@1103355004 : Integer;
      lwTotal@1103355005 : Decimal;
    BEGIN
      // LitrosA100
      // pwAnt indica con cuantos registros de anterioridad se quiere realizar el calculo

      IF pwAnt <=0 THEN
        pwAnt := 1;

      CLEAR(lrRegD);
      lrRegD := Rec;
      lrRegD.ASCENDING(FALSE);
      lrRegD.SETCURRENTKEY(Vehiculo,Dia,"Hora repostaje",Anulado,"Kilometros Reales");
      lrRegD.SETRANGE(Vehiculo, Vehiculo);
      CLEAR(lwLtrs);
      CLEAR(lwKms);

      // AJS 21.05.2008, Gori Carvajal
      // El calculo de la media a los 100 se hace entre los litros y kms de cada repostaje

      // lwKms  += "Kms realizados";

      FOR lwN := 1 TO pwAnt DO BEGIN

        // AJS 21.05.2008, Gori Carvajal
        // El calculo de la media a los 100 se hace entre los litros y kms de cada repostaje

        { {codigo eliminado}
        IF lrRegD.NEXT <> 0 THEN BEGIN
          lwLtrs += lrRegD."Litros repostados";
          IF lwN <> pwAnt THEN
            lwKms  += lrRegD."Kms realizados";
        END
        ELSE
          lwN := pwAnt; // Provocamos la salida del bucle
        }

        lwLtrs += lrRegD."Litros repostados";
        lwKms  += lrRegD."Kms realizados";

        IF lrRegD.NEXT = 0 THEN
          lwN := pwAnt;
      END;

      CLEAR(lwTotal);
      IF lwKms <> 0 THEN
        lwTotal := ROUND((lwLtrs*100)/lwKms);

      EXIT(lwTotal);
    END;

    BEGIN
    {
      // ALM 131102
      // Ultramar express.
    }
    END.
  }
}
