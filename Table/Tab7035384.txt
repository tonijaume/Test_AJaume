OBJECT Table 7035384 Lista informes Transfer
{
  OBJECT-PROPERTIES
  {
    Date=16/10/09;
    Time=12:30:49;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               rListaInforme.RESET;
               IF rListaInforme.FINDLAST THEN
                 "Num Linea" := rListaInforme."Num Linea" + 10000
               ELSE
                 "Num Linea" := 10000;

               Visible := TRUE;
             END;

    CaptionML=ESP=Lista informes Transfer;
    LookupFormID=Form7035357;
    DrillDownFormID=Form7035357;
  }
  FIELDS
  {
    { 1   ;   ;Num Linea           ;Integer       ;CaptionML=ESP=Num Linea;
                                                   Description=PK, Autonum‚rico }
    { 3   ;   ;Texto               ;Text100       ;CaptionML=ESP=Texto }
    { 5   ;   ;Id. Informe         ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Report));
                                                   OnValidate=BEGIN
                                                                rObjeto.SETRANGE(Type, rObjeto.Type::Report);
                                                                rObjeto.SETRANGE(ID  , "Id. Informe");
                                                                IF rObjeto.FINDFIRST THEN BEGIN
                                                                  rObjeto.CALCFIELDS(Caption);
                                                                  "Nombre Informe" := rObjeto.Caption;
                                                                  IF Texto = '' THEN
                                                                    Texto := rObjeto.Caption;
                                                                END;
                                                              END;

                                                   CaptionML=ESP=Id. Informe }
    { 7   ;   ;Nombre Informe      ;Text100       ;CaptionML=ESP=Nombre Informe;
                                                   Editable=No }
    { 9   ;   ;Menu                ;Code20        ;CaptionML=ESP=Menu }
    { 11  ;   ;Tipo                ;Option        ;CaptionML=ESP=Tipo;
                                                   OptionCaptionML=ESP=Informe,Excel;
                                                   OptionString=Informe,Excel;
                                                   Description=Informe,Excel }
    { 13  ;   ;Empresa Real        ;Code10        ;CaptionML=ESP=Empresa Real;
                                                   Description=Empresa propietaria si es un informe personalizada }
    { 15  ;   ;Tipo objeto         ;Option        ;CaptionML=ESP=Tipo objeto;
                                                   OptionCaptionML=ESP=TableData,Table,Form,Report,Dataport,Codeunit,XMLPort,MenuSuite,System,FieldNumber;
                                                   OptionString=TableData,Table,Form,Report,Dataport,Codeunit,XMLPort,MenuSuite,System,FieldNumber }
    { 17  ;   ;Visible             ;Boolean       ;CaptionML=ESP=Visible }
  }
  KEYS
  {
    {    ;Num Linea                               ;Clustered=Yes }
    {    ;Menu                                     }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      rListaInforme@1100253000 : Record 7035384;
      rObjeto@1100253001 : Record 2000000001;

    PROCEDURE Imprimir@1100253000(prListaInf@1100253000 : Record 7035384);
    BEGIN
      // Imprimir
      // Esta funcion se encarga de lanzar los informes, llamando a Reports, Forms o Codeunits, para las codeunits
      // pasaremos por parametro el registro de lista de informe para indicar a que rutina llamamos.

      CASE prListaInf."Tipo objeto" OF

        prListaInf."Tipo objeto"::Form:
        BEGIN
          FORM.RUN(prListaInf."Id. Informe");
        END;

        prListaInf."Tipo objeto"::Report:
        BEGIN
          REPORT.RUN(prListaInf."Id. Informe");
        END;

        prListaInf."Tipo objeto"::Codeunit:
        BEGIN
          CODEUNIT.RUN(prListaInf."Id. Informe", prListaInf);
        END;

      END;
    END;

    PROCEDURE Muestra@1103355000(pwMenu@1103355000 : Code[20]);
    VAR
      lrList@1103355001 : Record 7035384;
    BEGIN
      // Muestra

      IF pwMenu='' THEN
        EXIT;

      CLEAR(lrList);
      lrList.FILTERGROUP(2);
      lrList.SETCURRENTKEY(Menu);
      lrList.SETRANGE(Menu,pwMenu);
      lrList.FILTERGROUP(0);
      FORM.RUN(0,lrList);
    END;

    BEGIN
    END.
  }
}
