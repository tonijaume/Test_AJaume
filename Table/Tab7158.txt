OBJECT Table 7158 Analysis Dim. Selection Buffer
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Analysis Dim. Selection Buffer;
               ESP=B£fer de selecci¢n de dimensi¢n de an lisis];
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Text30        ;CaptionML=[ENU=Code;
                                                              ESP=C¢digo] }
    { 2   ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n] }
    { 3   ;   ;Selected            ;Boolean       ;OnValidate=BEGIN
                                                                "New Dimension Value Code" := '';
                                                                "Dimension Value Filter" := '';
                                                                Level := Level::" ";
                                                              END;

                                                   CaptionML=[ENU=Selected;
                                                              ESP=Selecc.] }
    { 4   ;   ;New Dimension Value Code;Code20    ;TableRelation=IF (Code=CONST(Item)) Item.No.
                                                                 ELSE IF (Code=CONST(Location)) Location.Code
                                                                 ELSE "Dimension Value".Code WHERE (Dimension Code=FIELD(Code));
                                                   OnValidate=BEGIN
                                                                Selected := TRUE;
                                                              END;

                                                   CaptionML=[ENU=New Dimension Value Code;
                                                              ESP=Nuevo c¢d. valor dim.] }
    { 5   ;   ;Dimension Value Filter;Code250     ;TableRelation=IF (Code=CONST(Item)) Item.No.
                                                                 ELSE IF (Code=CONST(Location)) Location.Code
                                                                 ELSE "Dimension Value".Code WHERE (Dimension Code=FIELD(Code));
                                                   OnValidate=BEGIN
                                                                Selected := (Level <> Level::" ") OR ("Dimension Value Filter" <> '');
                                                              END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Dimension Value Filter;
                                                              ESP=Filtro valor dimensi¢n] }
    { 6   ;   ;Level               ;Option        ;OnValidate=BEGIN
                                                                Selected := (Level <> Level::" ") OR ("Dimension Value Filter" <> '');
                                                              END;

                                                   CaptionML=[ENU=Level;
                                                              ESP=Nivel];
                                                   OptionCaptionML=[ENU=" ,Level 1,Level 2,Level 3";
                                                                    ESP=" ,Nivel 1,Nivel 2,Nivel 3"];
                                                   OptionString=[ ,Level 1,Level 2,Level 3] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
    {    ;Level,Code                               }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Another user has modified the selected dimensions for the %1 field after you retrieved it from the database.\;ESP=Otro usuario ha modificado las dimensiones seleccionadas para el campo %1 despu‚s de que usted las sacara .\';
      Text002@1002 : TextConst 'ENU="Enter your changes again in the Dimension Selection window by clicking the AssistButton in the %1 field. ";ESP="Haga clic en el bot¢n AssistButton del campo %1 para introducir de nuevo las modificaciones en la ventana Selecci¢n de dimensi¢n. "';
      AnalysisSelectedDim@1004 : Record 7159;

    PROCEDURE CompareDimText@2(ObjectType@1000 : Integer;ObjectID@1001 : Integer;AnalysisArea@1007 : Integer;AnalysisViewCode@1002 : Code[10];SelectedDimText@1003 : Text[250];DimTextFieldName@1004 : Text[100]);
    VAR
      AnalysisSelectedDim@1005 : Record 7159;
      SelectedDimTextFromDb@1006 : Text[250];
    BEGIN
      SelectedDimTextFromDb := '';
      AnalysisSelectedDim.SETCURRENTKEY(
        "User ID","Object Type","Object ID","Analysis Area","Analysis View Code",Level,"Dimension Code");
      AnalysisSelectedDim.SETRANGE("User ID",USERID);
      AnalysisSelectedDim.SETRANGE("Object Type",ObjectType);
      AnalysisSelectedDim.SETRANGE("Object ID",ObjectID);
      AnalysisSelectedDim.SETRANGE("Analysis Area",AnalysisArea);
      AnalysisSelectedDim.SETRANGE("Analysis View Code",AnalysisViewCode);
      IF AnalysisSelectedDim.FIND('-') THEN
        REPEAT
          AddDimCodeToText(AnalysisSelectedDim."Dimension Code",SelectedDimTextFromDb);
        UNTIL AnalysisSelectedDim.NEXT = 0;
      IF SelectedDimTextFromDb <> SelectedDimText THEN
        ERROR(
          Text000 +
          Text002,
          DimTextFieldName);
    END;

    PROCEDURE AddDimCodeToText@3(DimCode@1000 : Code[20];VAR Text@1001 : Text[250]);
    BEGIN
      IF Text = '' THEN
        Text := DimCode
      ELSE
        IF (STRLEN(Text) + STRLEN(DimCode)) <= (MAXSTRLEN(Text) - 4) THEN
          Text := STRSUBSTNO('%1; %2',Text,DimCode)
        ELSE
          Text := STRSUBSTNO('%1;...',Text)
    END;

    PROCEDURE SetDimSelection@5(ObjectType@1000 : Integer;ObjectID@1001 : Integer;AnalysisArea@1005 : Integer;AnalysisViewCode@1002 : Code[10];VAR SelectedDimText@1003 : Text[250];VAR AnalysisDimSelBuf@1004 : Record 7158);
    BEGIN
      AnalysisSelectedDim.SETRANGE("User ID",USERID);
      AnalysisSelectedDim.SETRANGE("Object Type",ObjectType);
      AnalysisSelectedDim.SETRANGE("Object ID",ObjectID);
      AnalysisSelectedDim.SETRANGE("Analysis Area",AnalysisArea);
      AnalysisSelectedDim.SETRANGE("Analysis View Code",AnalysisViewCode);
      AnalysisSelectedDim.DELETEALL;
      SelectedDimText := '';
      AnalysisDimSelBuf.SETCURRENTKEY(Level,Code);
      AnalysisDimSelBuf.SETRANGE(Selected,TRUE);
      IF AnalysisDimSelBuf.FIND('-') THEN
        REPEAT
          AnalysisSelectedDim."User ID" := USERID;
          AnalysisSelectedDim."Object Type" := ObjectType;
          AnalysisSelectedDim."Object ID" := ObjectID;
          AnalysisSelectedDim."Analysis Area" := AnalysisArea;
          AnalysisSelectedDim."Analysis View Code" := AnalysisViewCode;
          AnalysisSelectedDim."Dimension Code" := AnalysisDimSelBuf.Code;
          AnalysisSelectedDim."New Dimension Value Code" := AnalysisDimSelBuf."New Dimension Value Code";
          AnalysisSelectedDim."Dimension Value Filter" := AnalysisDimSelBuf."Dimension Value Filter";
          AnalysisSelectedDim.Level := AnalysisDimSelBuf.Level;
          AnalysisSelectedDim.INSERT;
          AddDimCodeToText(AnalysisSelectedDim."Dimension Code",SelectedDimText);
        UNTIL AnalysisDimSelBuf.NEXT = 0;
    END;

    PROCEDURE SetDimSelectionLevel@40(ObjectType@1000 : Integer;ObjectID@1001 : Integer;AnalysisArea@1009 : Integer;AnalysisViewCode@1002 : Code[10];VAR SelectedDimText@1003 : Text[250]);
    VAR
      Item@1007 : Record 27;
      Location@1010 : Record 14;
      ItemAnalysisView@1004 : Record 7152;
      Dim@1005 : Record 348;
      TempAnalysisDimSelBuf@1006 : TEMPORARY Record 7158;
      AnalysisDimSelectionLevel@1008 : Form 7161;
    BEGIN
      CLEAR(AnalysisDimSelectionLevel);
      IF ItemAnalysisView.GET(AnalysisArea,AnalysisViewCode) THEN BEGIN
        IF Dim.GET(ItemAnalysisView."Dimension 1 Code") THEN BEGIN
          AnalysisDimSelectionLevel.InsertDimSelBuf(
            AnalysisSelectedDim.GET(USERID,ObjectType,ObjectID,AnalysisArea,AnalysisViewCode,Dim.Code),
            Dim.Code,Dim.GetMLName(GLOBALLANGUAGE),
            AnalysisSelectedDim."Dimension Value Filter",AnalysisSelectedDim.Level);
        END;
        IF Dim.GET(ItemAnalysisView."Dimension 2 Code") THEN BEGIN
          AnalysisDimSelectionLevel.InsertDimSelBuf(
            AnalysisSelectedDim.GET(USERID,ObjectType,ObjectID,AnalysisArea,AnalysisViewCode,Dim.Code),
            Dim.Code,Dim.GetMLName(GLOBALLANGUAGE),
            AnalysisSelectedDim."Dimension Value Filter",AnalysisSelectedDim.Level);
        END;
        IF Dim.GET(ItemAnalysisView."Dimension 3 Code") THEN BEGIN
          AnalysisDimSelectionLevel.InsertDimSelBuf(
            AnalysisSelectedDim.GET(USERID,ObjectType,ObjectID,AnalysisArea,AnalysisViewCode,Dim.Code),
            Dim.Code,Dim.GetMLName(GLOBALLANGUAGE),
            AnalysisSelectedDim."Dimension Value Filter",AnalysisSelectedDim.Level);
        END;
        AnalysisDimSelectionLevel.InsertDimSelBuf(
          AnalysisSelectedDim.GET(USERID,ObjectType,ObjectID,AnalysisArea,AnalysisViewCode,Item.TABLECAPTION),
          Item.TABLECAPTION,Item.TABLECAPTION,
          AnalysisSelectedDim."Dimension Value Filter",AnalysisSelectedDim.Level);
        AnalysisDimSelectionLevel.InsertDimSelBuf(
          AnalysisSelectedDim.GET(USERID,ObjectType,ObjectID,AnalysisArea,AnalysisViewCode,Location.TABLECAPTION),
          Location.TABLECAPTION,Location.TABLECAPTION,
          AnalysisSelectedDim."Dimension Value Filter",AnalysisSelectedDim.Level);
      END;

      AnalysisDimSelectionLevel.LOOKUPMODE := TRUE;
      IF AnalysisDimSelectionLevel.RUNMODAL = ACTION::LookupOK THEN BEGIN
        AnalysisDimSelectionLevel.GetDimSelBuf(TempAnalysisDimSelBuf);
        SetDimSelection(ObjectType,ObjectID,AnalysisArea,AnalysisViewCode,SelectedDimText,TempAnalysisDimSelBuf);
      END;
    END;

    PROCEDURE GetDimSelectionText@6(ObjectType@1000 : Integer;ObjectID@1001 : Integer;AnalysisArea@1005 : Integer;AnalysisViewCode@1002 : Code[10]) : Text[250];
    VAR
      AnalysisSelectedDim@1003 : Record 7159;
      SelectedDimText@1004 : Text[250];
    BEGIN
      WITH AnalysisSelectedDim DO BEGIN
        SETRANGE("User ID",USERID);
        SETRANGE("Object Type",ObjectType);
        SETRANGE("Object ID",ObjectID);
        SETRANGE("Analysis Area",AnalysisArea);
        SETRANGE("Analysis View Code",AnalysisViewCode);
        IF FIND('-') THEN
          REPEAT
            AddDimCodeToText("Dimension Code",SelectedDimText);
          UNTIL NEXT = 0;
      END;
      EXIT(SelectedDimText);
    END;

    BEGIN
    END.
  }
}
