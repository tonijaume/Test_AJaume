OBJECT Table 7035354 Asociacion Recursos
{
  OBJECT-PROPERTIES
  {
    Date=16/10/09;
    Time=12:30:48;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    OnInsert=BEGIN

               rAsoc.RESET;
               rAsoc.ASCENDING(FALSE);
               IF rAsoc.FINDFIRST THEN
                 "N§ mov." := rAsoc."N§ mov." + 1
               ELSE
                 "N§ mov." := 1;

               GuardaMod
             END;

    OnModify=BEGIN
               GuardaMod
             END;

    CaptionML=ESP=Asociacion Recursos;
  }
  FIELDS
  {
    { 1   ;   ;N§ mov.             ;Integer       ;CaptionML=ESP=N§ mov.;
                                                   Editable=No }
    { 2   ;   ;Cod. Conductor      ;Code10        ;TableRelation=Conductor;
                                                   OnValidate=BEGIN

                                                                // AJF 11/05/09
                                                                ActualizarNombreAbreviado;

                                                                Comprobaciones;
                                                              END;

                                                   CaptionML=ESP=Cod. Conductor }
    { 3   ;   ;Matricula           ;Code10        ;TableRelation=Vehiculo;
                                                   OnValidate=BEGIN

                                                                // AJF 11/05/09
                                                                ActualizarNumeroVehiculo;
                                                              END;

                                                   CaptionML=ESP=Matricula }
    { 4   ;   ;Usuario Modificacion;Code20        ;CaptionML=ESP=Usuario Modificacion;
                                                   Editable=No }
    { 5   ;   ;Fecha Modificacion  ;Date          ;CaptionML=ESP=Fecha Modificacion;
                                                   Editable=No }
    { 6   ;   ;Hora Modificacion   ;Time          ;CaptionML=ESP=Hora Modificacion;
                                                   Editable=No }
    { 7   ;   ;Vehiculo Preferente ;Boolean       ;OnValidate=BEGIN
                                                                Comprobaciones;
                                                              END;

                                                   CaptionML=ESP=Vehiculo Preferente }
    { 8   ;   ;Fecha Desde         ;Date          ;OnValidate=BEGIN
                                                                Comprobaciones;
                                                              END;

                                                   CaptionML=ESP=Fecha Desde }
    { 9   ;   ;Fecha Hasta         ;Date          ;OnValidate=BEGIN
                                                                Comprobaciones;
                                                              END;

                                                   CaptionML=ESP=Fecha Hasta }
    { 10  ;   ;Numero Vehiculo     ;Text10        ;CaptionML=ESP=Numero Vehiculo;
                                                   Editable=No }
    { 11  ;   ;Nombre Abreviado    ;Code20        ;CaptionML=ESP=Nombre Abreviado;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;N§ mov.                                 ;Clustered=Yes }
    {    ;Cod. Conductor,Matricula                 }
    {    ;Fecha Desde,Fecha Hasta                  }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      rAsoc@1100244000 : Record 7035354;
      Text0001@1100244002 : TextConst 'ESP=Fecha desde no puede ser posterior que fecha hasta.';
      Text0002@1100244001 : TextConst 'ESP=Existe otro vehiculo asignado como Vehiculo Preferente a este conductor para las mismas fechas.';

    PROCEDURE GuardaMod@3();
    BEGIN
      //GuardaMod

      "Usuario Modificacion" := USERID;
      "Fecha Modificacion"   := TODAY;
      "Hora Modificacion"    := TIME;
    END;

    PROCEDURE Comprobaciones@1();
    BEGIN
      // Comprobaciones

      // Comprueba fechas
      IF ("Fecha Hasta"<>0D) AND ("Fecha Desde" > "Fecha Hasta") THEN
        ERROR(Text0001);

      // Que solo exista un "Vehiculo Preferente" por conductor y rango
      IF "Vehiculo Preferente" THEN BEGIN
        rAsoc.RESET;
        rAsoc.SETCURRENTKEY("Cod. Conductor",
                            Matricula);
        rAsoc.SETRANGE("Cod. Conductor","Cod. Conductor");
        rAsoc.SETRANGE("Vehiculo Preferente", TRUE);
        rAsoc.SETFILTER("Fecha Desde",'<=%1',"Fecha Hasta");
        rAsoc.SETFILTER("Fecha Hasta",'>=%1',"Fecha Desde");
        rAsoc.SETFILTER("N§ mov.",'<>%1',"N§ mov.");
        IF rAsoc.FINDFIRST THEN
          ERROR(Text0002);
      END;
    END;

    PROCEDURE ActualizarNombreAbreviado@1103355000();
    VAR
      lrConductor@1103355000 : Record 7010327;
    BEGIN
      // ActualizarNombreAbreviado

      IF lrConductor.GET("Cod. Conductor") THEN
        "Nombre Abreviado" := lrConductor."Nombre Abreviado";
    END;

    PROCEDURE ActualizarNumeroVehiculo@1103355001();
    VAR
      lrVehiculo@1103355000 : Record 7010346;
    BEGIN
      // ActualizarNumeroVehiculo

      IF lrVehiculo.GET(Matricula) THEN
        "Numero Vehiculo" := lrVehiculo.Numero;
    END;

    BEGIN
    {
      // AML 251103 por DRF 03-00011
      // Punto EST-001

      ## AJF 11/05/2009

        -. Nuevos campos "Numero Vehiculo" y "Nombre Abreviado" para substituir las funciones que los visualizan en los formularios
           y que se pueda filtrar por ellos. Se actualizan al introducir Matricula y "Cod. Conductor" respectivamente.
    }
    END.
  }
}
