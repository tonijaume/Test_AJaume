OBJECT Table 7010379 Servicios Regulares
{
  OBJECT-PROPERTIES
  {
    Date=27/11/14;
    Time=13:48:14;
    Modified=Yes;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CLEAR(rSerRep);
               IF rSerRep.FINDLAST THEN
                 "No." := rSerRep."No." +1
               ELSE
                 "No." := 1;
             END;

    OnModify=BEGIN
               IF "Servicios Creados" THEN
                 Modificado:= TRUE;
             END;

    CaptionML=ESP=Servicios Regulares;
    LookupFormID=Form7010492;
    DrillDownFormID=Form7010492;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Integer       ;CaptionML=ESP=No.;
                                                   Editable=No }
    { 2   ;   ;Garaje              ;Code10        ;TableRelation=Garaje.Garaje;
                                                   CaptionML=ESP=Garaje }
    { 3   ;   ;Cliente             ;Code20        ;TableRelation=Customer.No.;
                                                   CaptionML=ESP=Cliente }
    { 4   ;   ;TTOO                ;Code10        ;TableRelation=Cliente/TTOO.TTOO WHERE (Cliente=FIELD(Cliente));
                                                   CaptionML=ESP=TTOO }
    { 5   ;   ;Descripcion         ;Text30        ;CaptionML=ESP=Descripcion }
    { 20  ;   ;Fecha Inicio        ;Date          ;OnValidate=BEGIN
                                                                CompruebaFechas;
                                                              END;

                                                   CaptionML=ESP=Fecha Inicio }
    { 21  ;   ;Fecha Final         ;Date          ;OnValidate=BEGIN
                                                                CompruebaFechas;
                                                              END;

                                                   CaptionML=ESP=Fecha Final }
    { 22  ;   ;Frecuencia          ;Integer       ;InitValue=1;
                                                   CaptionML=ESP=Frecuencia;
                                                   MinValue=1;
                                                   MaxValue=24 }
    { 24  ;   ;Calendario          ;Code10        ;TableRelation="Calendario Transfer".Code;
                                                   CaptionML=ESP=Calendario;
                                                   Description="Calendario Transfer".Code }
    { 26  ;   ;Codigo Servicio     ;Code10        ;TableRelation="Codigo servicio".Codigo WHERE (Filtro Cliente=FIELD(Cliente),
                                                                                                 Filtro Touroperador=FIELD(TTOO),
                                                                                                 Tiene precio x cliente=CONST(Yes));
                                                   CaptionML=ESP=Codigo Servicio }
    { 27  ;   ;Origen/Destino      ;Code10        ;TableRelation="Punto de recogida".Codigo;
                                                   CaptionML=ESP=Origen/Destino }
    { 28  ;   ;Presentacion        ;Text30        ;CaptionML=ESP=Presentacion }
    { 29  ;   ;Precio Medio        ;Decimal       ;CaptionML=ESP=Precio Medio;
                                                   BlankZero=Yes }
    { 30  ;   ;Importe             ;Decimal       ;CaptionML=ESP=Importe;
                                                   BlankZero=Yes }
    { 31  ;   ;Tipo Aplicacion     ;Option        ;OnValidate=VAR
                                                                lrRuta@1100253000 : Record 7035387;
                                                              BEGIN
                                                                IF "Tipo Aplicacion" = "Tipo Aplicacion"::"Servicio Contratado" THEN BEGIN
                                                                  "Precio Medio" := 0;

                                                                  CLEAR(lrRuta);  // Tambien ponemos a 0 los precios de las lineas de ruta
                                                                  lrRuta.SETRANGE("No. Servicio regular", "No.");
                                                                  IF lrRuta.FINDSET(TRUE) THEN
                                                                    lrRuta.MODIFYALL("Precio Medio",0);

                                                                END;

                                                                IF "Tipo Aplicacion" <> "Tipo Aplicacion"::"Total Factura" THEN
                                                                  Importe := 0;
                                                              END;

                                                   CaptionML=ESP=Tipo Aplicacion;
                                                   OptionCaptionML=ESP=Servicio Manual,Servicio Contratado,Total Factura;
                                                   OptionString=Servicio Manual,Servicio Contratado,Total Factura }
    { 32  ;   ;Periodo             ;Option        ;CaptionML=ESP=Periodo;
                                                   OptionCaptionML=ESP=Diario,Semanal,Quincenal,Mensual;
                                                   OptionString=Diario,Semanal,Quincenal,Mensual }
    { 34  ;   ;Observaciones       ;Text250       ;CaptionML=ESP=Observaciones }
    { 99  ;   ;Servicios Creados   ;Boolean       ;CaptionML=ESP=Servicios Creados }
    { 100 ;   ;Usuario Creacion    ;Code20        ;CaptionML=ESP=Usuario Creacion;
                                                   Editable=No }
    { 101 ;   ;Fecha Creacion      ;Date          ;CaptionML=ESP=Fecha Creacion;
                                                   Editable=No }
    { 102 ;   ;Hora Creacion       ;Time          ;CaptionML=ESP=Hora Creacion;
                                                   Editable=No }
    { 103 ;   ;No Partes           ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count(Parte WHERE (No Servicio Regular=FIELD(No.)));
                                                   OnLookup=VAR
                                                              lfPartes@1100244000 : Form 7010401;
                                                              lrParte@1100244001 : Record 7010360;
                                                            BEGIN

                                                              lrParte.RESET;
                                                              lrParte.SETCURRENTKEY("No Servicio Regular",Fecha);
                                                              lrParte.SETRANGE("No Servicio Regular", "No.");
                                                              lfPartes.SETTABLEVIEW(lrParte);
                                                              lfPartes.LlamadaAsignacion(TRUE);
                                                              lfPartes.LOOKUPMODE:= TRUE;
                                                              lfPartes.RUNMODAL;
                                                            END;

                                                   CaptionML=ESP=No Partes;
                                                   Editable=No }
    { 104 ;   ;Modificado          ;Boolean       ;CaptionML=ESP=Modificado }
    { 105 ;   ;Calcular Maletas    ;Boolean       ;CaptionML=ESP=Calcular Maletas }
    { 106 ;   ;Texto Registro      ;Text90        ;CaptionML=ESP=Texto Registro;
                                                   Description=Texto de registro de la factura, se puede cambiar para cada caso }
    { 107 ;   ;Texto Alternativo   ;Text250       ;CaptionML=ESP=Texto Alternativo;
                                                   Description=Texto Alternativo de la factura, }
    { 108 ;   ;Tipo factura        ;Option        ;InitValue=Texto alternativo;
                                                   CaptionML=ESP=Tipo factura;
                                                   OptionCaptionML=ESP=Sin resumen,Resumen x Codigo,Resumen x Zona,Resumen x Vuelo,Resumen x Dia-Codigo,Resumen x Zona-Detalle,Resumen x Zona Dia-Codigo,Texto alternativo,Resumen x Referencia;
                                                   OptionString=Sin resumen,Resumen x Codigo,Resumen x Zona,Resumen x Vuelo,Resumen x Dia-Codigo,Resumen x Zona-Detalle,Resumen x Zona Dia-Codigo,Texto alternativo,Resumen x Referencia }
    { 200 ;   ;Nombre Colegio      ;Text30        ;CaptionML=ESP=Nombre Colegio }
    { 202 ;   ;No. Expediente      ;Code20        ;CaptionML=ESP=No. Expediente }
    { 203 ;   ;Ultima Fecha Factura;Date          ;FieldClass=FlowField;
                                                   CalcFormula=Max("Prefactura ingreso"."Fecha Registro factura" WHERE (Servicio Regular=FIELD(No.),
                                                                                                                        Facturada=CONST(Yes)));
                                                   CaptionML=ESP=Ultima Fecha Factura;
                                                   Description=Caculada a partir de cabecera prefactura;
                                                   Editable=No }
    { 204 ;   ;Generacion Diaria   ;Boolean       ;OnValidate=VAR
                                                                lwOK@1103355000 : Boolean;
                                                                lrParte@1103355001 : Record 7010360;
                                                              BEGIN
                                                                IF "Generacion Diaria" THEN BEGIN
                                                                  Comprobaciones(TRUE);
                                                                END;

                                                                CLEAR(lrParte);
                                                                lrParte.SETCURRENTKEY("No Servicio Regular", Fecha);
                                                                lrParte.SETRANGE("No Servicio Regular"     , "No.");
                                                                lwOK := NOT  lrParte.FINDFIRST;

                                                                "Servicios Creados" := "Generacion Diaria" OR lwOK;
                                                              END;

                                                   CaptionML=ESP=Generacion Diaria;
                                                   Description=Generara los servicios diariamente en la apertura diaria }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      rSerRep@1100244000 : Record 7010379;
      Text0001@1100244001 : TextConst 'ESP=%1 No puede ser superior a %2';
      Text0002@1100244002 : TextConst 'ESP=¨Est  seguro de que quiere iniciar la generaci¢n de todos los servicios?';
      Text0003@1100244003 : TextConst 'ESP=Los servicios ya han sido generados';
      Text0004@1100244004 : TextConst 'ESP=La fecha de inicio no puede ser inferior a ma¤ana';
      Text0005@1100244005 : TextConst 'ESP=No se ha especificado ning£n punto de recogida para este Servicio Regular';
      Text0006@1100244006 : TextConst 'ESP=No ha especificado ning£n Pax en la linea %1';
      rPar@1103350000 : Record 7010311;
      CalendarMgmt@1100244007 : Codeunit 7010319;
      cImpTRN@1100244009 : Codeunit 7010402;
      cFactAuto@1100244022 : Codeunit 7035326;
      cFactIng@1100244026 : Codeunit 7010406;
      cFunParAd@1100253001 : Codeunit 7010410;
      cFunFact@1103355023 : Codeunit 7010321;
      wVentana@1100244010 : Dialog;
      Text0007@1100244011 : TextConst 'ESP=Generando Servicios Regulares @1@@@@@@@@@\#2#####';
      Text0008@1100244012 : TextConst 'ESP=No se puede deshacer los Servicios Regulares ya que no est n creados';
      Text0009@1100244013 : TextConst 'ESP=¨Desea Realmente deshacer todos los Partes relacionados con este Servicio Regular?';
      Text0010@1100244014 : TextConst 'ESP=No se encontr¢ ning£n Parte relacionado';
      Text0011@1100244015 : TextConst 'ESP=Deshaciendo Servicios Regulares @1@@@@@@@@@\#2###########';
      Text0012@1100244016 : TextConst 'ESP=Los servicios a£n no han sido generados';
      Text0013@1100244017 : TextConst 'ESP=Esta funci¢n no genera nuevos partes ni elimina ninguno, solo modifica los existentes.\¨Est  seguro que quiere iniciar la Modificaci¢n de todos los servicios?';
      Text0014@1100244018 : TextConst 'ESP=Este Servicio Regular No ha sido modificado.¨Desea Continuar?';
      Text0015@1100244019 : TextConst 'ESP=&Prefacturar,&Facturar';
      Text0016@1100244020 : TextConst 'ESP=¨Desea realmente %1 este Servicio Regular?';
      Text0017@1100244021 : TextConst 'ESP=El Parte %1 No esta confirmado';
      Text0018@1100244023 : TextConst 'ESP=Indicar Fecha Factura #1########';
      Text0019@1100244024 : TextConst 'ESP=No se puede indicar como fecha de factura %1 ya que Hay facturas con fecha inferior (%2)';
      Text0020@1100244025 : TextConst 'ESP=Debe de indicarse una fecha de Factura';
      Text0021@1100244027 : TextConst 'ESP=No se encontr¢ ninguna linea de ingreso';
      Text0022@1103350001 : TextConst 'ESP=Indicar Fecha #1########';
      Text0023@1103350002 : TextConst 'ESP=No se ha indicado ninguna fecha';
      Text0024@1103350003 : TextConst 'ESP=La fecha indicada no est  en el rango temporal del servicio regular';
      Text0025@1103350004 : TextConst 'ESP=¨Desea realmente generar los servicios para el d¡a %1 Ruta %2?';
      Text0026@1103350005 : TextConst 'ESP=Servicios Generados';
      Text0027@1103350006 : TextConst 'ESP=Ya existe alg£n parte para el d¡a %1 del servicio regular %2, ¨Desea Seguir?';
      Text0028@1103350007 : TextConst 'ESP=Se va a proceder a ampliar los servicios %1 d¡as. ¨Desea seguir?';
      Text0029@1103350008 : TextConst 'ESP=Se va a proceder a eliminar los servicios de %1 d¡as. ¨Desea seguir?';
      Text0030@1103350009 : TextConst 'ESP=Ya hay servicios creados. Los nuevos servicios se empezar n a crear a partir del %1';
      Text0031@1100253000 : TextConst 'ESP=RUTA %1';
      Text0032@1100253002 : TextConst 'ESP=No se ha encontrado ning£na Ruta pendiente de Generar Servicios';
      Text0033@1103355001 : TextConst 'ESP=Generando Servicios Regulares\#1################################\@2@@@@@@@@@@@@@@';
      Text0035@1103355000 : TextConst 'ESP=Est  activada la generaci¢n diaria de servicios regulares.\Los Servicios se generaran en la apertura del d¡a';
      Text0036@1103355002 : TextConst 'ESP=La Fecha de inicio NO puede ser superior a la Fecha Final.(%1-%2)';
      cGestionParte@1103355003 : Codeunit 7010318;

    PROCEDURE CompruebaFechas@1100244000();
    BEGIN
      // CompruebaFechas

      IF ("Fecha Final" <> 0D) AND ("Fecha Inicio" > "Fecha Final") THEN
        ERROR(Text0001, FIELDCAPTION("Fecha Inicio"), FIELDCAPTION("Fecha Final"));
    END;

    PROCEDURE Comprobaciones@1100244004(pwModificacion@1100244001 : Boolean);
    VAR
      lrCalendario@1100244000 : Record 7035359;
      lrRuta@1100253000 : Record 7035387;
    BEGIN
      // Comprobaciones
      // Antes de proceder a la generaci¢n partes se realizan una serie de comprobaciones propias

      TESTFIELD("Fecha Inicio");
      TESTFIELD("Fecha Final");

      { // Ahora la Creaci¢n de Servicios va por Ruta
      IF pwModificacion THEN BEGIN
        IF NOT "Servicios Creados" THEN
          ERROR(Text0012);
      END
      ELSE BEGIN
        IF "Servicios Creados" THEN
          ERROR(Text0003);
      }

        { JPT 30/11/04 Si se ha deshecho parte de los servicios debemos dejar volver a crear
          en el momento de la creaci¢n hay sistemas de control de fechas

        IF "Fecha Inicio" <= TODAY THEN
          ERROR(Text0004);

      END;
      }

      TESTFIELD(Garaje);
      TESTFIELD(Cliente);
      TESTFIELD(TTOO);

      CLEAR(lrRuta);
      lrRuta.SETRANGE("No. Servicio regular"         , "No.");

      // AJS 15.03.2006
      // Solo trabajamos con las rutas habilitadas

      lrRuta.SETRANGE("Ruta inhabilitada", FALSE);
      IF lrRuta.FINDSET THEN BEGIN
        REPEAT
          lrRuta.TESTFIELD("Codigo Servicio");
          lrRuta.TESTFIELD(Presentacion);
        UNTIL lrRuta.NEXT=0;
      END;

      // AJS 26.10.2004
      // El campo Origen/Destino no es obligatorio
      //TESTFIELD("Origen/Destino");

      IF Calendario <> '' THEN
        lrCalendario.GET(Calendario);
    END;

    PROCEDURE CrearServicios@1100244001(pwCrear@1103350005 : Boolean;pwFechaDesde@1103350003 : Date;pwFechaHasta@1103350004 : Date);
    VAR
      lrPuntos@1100244005 : TEMPORARY Record 7010394;
      lrRuta@1100253000 : Record 7035387;
      lwFecha@1100244003 : Date;
      lwNoSD@1100244004 : Integer;
      lwLaborable@1100244006 : Boolean;
      lwDescripcion@1100244007 : Text[50];
      lwOrAnt@1100244008 : Integer;
      lwNumLinea@1100244010 : Integer;
      lwFin@1100244011 : Boolean;
      lwLMax@1103350000 : Integer;
      lwTotal@1103350001 : Integer;
      lwCont@1103350002 : Integer;
    BEGIN
      // CrearServicios
      // Proceso por el cual se generan todos los servicios repetitivos indicados en el archivo

      rPar.FINDFIRST;

      IF rPar."Generar Servicios Automaticos" THEN
        ERROR(Text0035);


      { JPT 16/02/06 Ahora los servicios se crean por ruta
      IF pwCrear AND "Servicios Creados" THEN
        ERROR(Text0003);
      }

      Comprobaciones(NOT pwCrear);
      CreaTemp(lrPuntos, 0);

      IF NOT CONFIRM (Text0002) THEN
        EXIT;

      IF (pwFechaDesde = 0D) OR pwCrear THEN BEGIN
        pwFechaDesde := "Fecha Inicio";
        // JPT 30/11/04 Si ya se han creado servicios, se empezar  a crear con fecha de dos dias despues de hoy
        CALCFIELDS("No Partes");
        IF ("No Partes" > 0) AND (pwFechaDesde <= TODAY) THEN BEGIN

          pwFechaDesde := CALCDATE('+2D', TODAY);

          IF NOT CONFIRM (Text0030, TRUE, pwFechaDesde) THEN
            EXIT;
        END;
      END;

      IF (pwFechaHasta = 0D) OR pwCrear THEN
        pwFechaHasta := "Fecha Final";

      IF pwFechaDesde > pwFechaHasta THEN
        ERROR(Text0036,pwFechaDesde, pwFechaHasta);

      lwFecha:= pwFechaDesde;
      lrPuntos.RESET;
      wVentana.OPEN(Text0007);
      lwTotal := (pwFechaHasta - pwFechaDesde) + 1;
      REPEAT
        LOCKTIMEOUT := FALSE; // JPT 15/02/07 Para evitar bloqueos
        // Seg£n el calendario y el d¡a ser  laborable o no

        IF Calendario = '' THEN
          lwLaborable := TRUE
        ELSE
          lwLaborable := NOT CalendarMgmt.CheckDateStatus(Calendario, lwFecha, lwDescripcion);

        IF lwLaborable THEN BEGIN
          CrearServiciosRuta(lwFecha, lrPuntos, pwCrear);
        END;
        lwCont := lwFecha - pwFechaDesde + 1;
        wVentana.UPDATE(1, ROUND(lwCont / lwTotal * 10000, 1));
        wVentana.UPDATE(2, lwFecha);
        lwFecha := lwFecha + Frecuencia;
      UNTIL lwFecha > pwFechaHasta;

      // Se graban los datos de creacion

      IF pwCrear THEN BEGIN
        "Servicios Creados":= TRUE;
        "Usuario Creacion" := USERID;
        "Fecha Creacion"   := TODAY;
        "Hora Creacion"    := TIME;
        MODIFY;

        CLEAR(lrRuta);
        lrRuta.SETRANGE("No. Servicio regular", "No.");

        // AJS 15.03.2006
        // Solo trabajamos con las rutas habilitadas

        lrRuta.SETRANGE("Ruta inhabilitada", FALSE);
        IF lrRuta.FINDSET(TRUE) THEN
          lrRuta.MODIFYALL("Servicios Creados", TRUE);
      END;

      wVentana.CLOSE;
    END;

    PROCEDURE CrearServiciosRuta@1100253008(pwFecha@1100253001 : Date;VAR prTempPuntos@1100253000 : TEMPORARY Record 7010394;pwTodos@1100253003 : Boolean);
    VAR
      lrRuta@1100253002 : Record 7035387;
    BEGIN
      // CrearServiciosRuta
      // Crea Todos los servicios de Todas las Rutas

      CLEAR(lrRuta);
      lrRuta.SETRANGE("No. Servicio regular", "No.");
      IF pwTodos THEN
        lrRuta.SETRANGE("Servicios Creados", FALSE);

      // AJS 15.03.2006
      // Solo trabajamos con las rutas habilitadas

      lrRuta.SETRANGE("Ruta inhabilitada", FALSE);

      IF lrRuta.FINDSET THEN BEGIN
        REPEAT
          CrearServicio(pwFecha, prTempPuntos, lrRuta);
        UNTIL lrRuta.NEXT=0;
      END
      ELSE
        ERROR(Text0032);
    END;

    PROCEDURE CreaTemp@1100244002(VAR prTemp@1100244000 : TEMPORARY Record 7010394;pwRuta@1100253001 : Integer);
    VAR
      lrPuntos@1100244001 : Record 7010394;
      lrRuta@1100253000 : Record 7035387;
      lwOrd@1100244002 : Integer;
    BEGIN
      // CreaTemp
      // Crea un temporal con los puntos de recogida de este registro, ordenados por orden del primer inicio
      // De paso comprobamos que todos los puntos se han rellenado bien
      // Si pwRuta <> 0 filtrar  solo por esa ruta

      prTemp.RESET;
      IF prTemp.FINDSET(TRUE) THEN
        prTemp.DELETEALL;


      lrPuntos.RESET;
      lrPuntos.SETRANGE("No Servicio", "No.");
      CLEAR(lrRuta);
      lrRuta.SETRANGE("No. Servicio regular", "No.");
      IF pwRuta <> 0 THEN
        lrRuta.SETRANGE("No. Ruta", pwRuta);

      // AJS 15.03.2006
      // Solo trabajamos con las rutas habilitadas

      lrRuta.SETRANGE("Ruta inhabilitada", FALSE);

      IF lrRuta.FINDSET THEN BEGIN
        REPEAT
          lrPuntos.SETRANGE("No Ruta", lrRuta."No. Ruta");
          IF lrPuntos.FINDSET THEN BEGIN
            REPEAT
              lrPuntos.TESTFIELD("Punto Recogida");
              // JPT 15/02/05 . En ibizatours les interesaba poder insertar la hora en la primera linea y dejar las otras en blanco
              IF NOT (rPar."Empresa Real" IN [rPar."Empresa Real"::IBZ, rPar."Empresa Real"::SJO])  THEN
                lrPuntos.TESTFIELD("Hora Recogida");
              IF lrPuntos.Adultos + lrPuntos.Ni¤os = 0 THEN
                ERROR(Text0006, lrPuntos."No Punto");

              prTemp := lrPuntos;
              prTemp."Id Ruta" := lrRuta."Id Ruta";
              prTemp.INSERT;
            UNTIL lrPuntos.NEXT=0;
          END;
        UNTIL lrRuta.NEXT=0;
      END;

      lrPuntos.SETRANGE("No Ruta");
      IF NOT lrPuntos.FINDFIRST THEN
        ERROR(Text0005);
    END;

    PROCEDURE Retroceder@1100244003(pwTodo@1103350004 : Boolean;pwFechaDesde@1103350003 : Date;pwFechaHasta@1103350002 : Date;pwRuta@1100253001 : Integer);
    VAR
      lrParte@1100244000 : Record 7010360;
      lrParte2@1103350000 : Record 7010360;
      lrSerPar@1100244001 : Record 7010359;
      lrSerPar2@1100244003 : Record 7010359;
      lrServicio@1100244002 : Record 7010358;
      rElePto@1103350001 : Record 7010363;
      rSerPar@1103350005 : Record 7010359;
      lrRuta@1100253000 : Record 7035387;
      lwTotal@1100244005 : Integer;
      lwCont@1100244004 : Integer;
      lwTexto@1100253002 : Text[1024];
      lwFecha@1100253003 : Date;
    BEGIN
      // Retroceder
      // Deshace todos los partes creados
      // Si se rellena el parametro pwIdRuta se retrocede solo la ruta especificada

      rPar.FINDFIRST;
      CLEAR(lrRuta);
      IF NOT "Servicios Creados" THEN
        ERROR(Text0008);

      lwTexto := Text0009;

      IF pwRuta <> 0 THEN BEGIN
        lrRuta.GET("No.", pwRuta);
        lwTexto += ' ' +STRSUBSTNO(Text0031, lrRuta."Id Ruta");
      END;

      IF pwTodo THEN BEGIN
        IF NOT CONFIRM(lwTexto,FALSE) THEN
          EXIT;
      END;

      lrSerPar.RESET;
      lrSerPar.SETCURRENTKEY("N§ Parte","N§ Linea");
      lrSerPar2.RESET;
      lrServicio.RESET;

      lrParte.RESET;
      lrParte.SETCURRENTKEY("No Servicio Regular");
      lrParte.SETRANGE("No Servicio Regular", "No.");
      IF lrRuta."Id Ruta" <> '' THEN // Si se rellena el codigo de ruta
        lrParte.SETRANGE(Referencia, lrRuta."Id Ruta");
      IF ((pwFechaDesde <> 0D) OR (pwFechaHasta <> 0D)) AND (NOT pwTodo) THEN BEGIN
        IF pwFechaHasta = 0D THEN
          lrParte.SETFILTER(Fecha,'=>%1',pwFechaDesde)
        ELSE
          IF pwFechaDesde = 0D THEN
            lrParte.SETFILTER(Fecha, '<=%1', pwFechaHasta)
          ELSE
            lrParte.SETRANGE(Fecha, pwFechaDesde, pwFechaHasta);
      END;
      IF lrParte.FINDSET THEN BEGIN
        wVentana.OPEN(Text0011);
        CLEAR(lwCont);
        lwTotal := lrParte.COUNT;
        // JPT 10/03/06 TransUnion ha pedido una diferencia de 1 d¡a en vez de 2

        // AJS 22.09.2006
        // TRANSUNION Quiere que no se borren los servicios de ma¤ana, lo dejamos para todos los clientes igual

        lwFecha := TODAY + 1;

        REPEAT

          IF lrParte.Fecha > lwFecha  THEN BEGIN

            // ----  Borrar los elementos a transportar asociados a esta linea  ----

            CLEAR(rElePto);
            rElePto.SETCURRENTKEY("N§ Parte" , "N§ Linea");
            rElePto.SETRANGE("N§ Parte" , lrParte."N§ Parte");
            IF rElePto.FINDSET(TRUE) THEN
              rElePto.DELETEALL;


            // ----  Borrar las recogidas del parte

            lrSerPar.SETRANGE("N§ Parte", lrParte."N§ Parte");
            IF lrSerPar.FINDSET(TRUE) THEN
              lrSerPar.DELETEALL;

            // ---- Borrar el servicio

            lrSerPar2.SETRANGE("N§ Servicio", lrSerPar."N§ Servicio");
            IF NOT lrSerPar2.FINDFIRST THEN BEGIN // Borramos el servicio
              IF lrServicio.GET(lrSerPar."N§ Servicio") THEN
                lrServicio.DELETE;
            END;

            // AJS 14.09.2004
            // Borramos la hoja de parte

            lrParte.GET(lrParte."N§ Parte");
            lrParte2 := lrParte;
            lrParte2.SetBol(3, TRUE); // Para que no se cree el backup
            lrParte2.DELETE(TRUE);

          END;
          lwCont +=1;
          wVentana.UPDATE(1, ROUND(lwCont / lwTotal * 10000, 1));
          wVentana.UPDATE(2, lrParte."N§ Parte");
        UNTIL lrParte.NEXT = 0;
        wVentana.CLOSE;
      END
      ELSE MESSAGE(Text0010);

      IF pwTodo THEN BEGIN
        CLEAR(lrRuta);
        lrRuta.SETRANGE("No. Servicio regular", "No.");
        IF pwRuta <> 0 THEN
          lrRuta.SETRANGE("No. Ruta", pwRuta);
        IF lrRuta.FINDSET(TRUE) THEN BEGIN
          REPEAT
            lrRuta.Modificada          := FALSE;
            lrRuta."Servicios Creados" := FALSE;
            lrRuta.MODIFY;
          UNTIL lrRuta.NEXT=0;
        END;

        // Marcamos las lineas de cabecera dependiendo de como esten las lineas

        "Servicios Creados":= FALSE;
        Modificado         := FALSE;
        "Generacion Diaria":= FALSE;

        lrRuta.SETRANGE("No. Ruta");

        // AJS 15.03.2006
        // Solo trabajamos con las rutas habilitadas

        lrRuta.SETRANGE("Ruta inhabilitada", FALSE);

        IF lrRuta.FINDSET(TRUE) THEN BEGIN
          REPEAT
            "Servicios Creados" := "Servicios Creados" OR lrRuta."Servicios Creados";
            Modificado          := Modificado OR lrRuta.Modificada;
          UNTIL lrRuta.NEXT=0;
        END;
        MODIFY;

      END;
    END;

    PROCEDURE ModificaServicios@1100244009();
    VAR
      lrParte@1100244013 : Record 7010360;
      lrPuntos@1100244010 : TEMPORARY Record 7010394;
      lrLinPar@1100244009 : Record 7010359;
      lrServicio@1100244008 : Record 7010358;
      lrRuta@1100253000 : Record 7035387;
      lrRecurs@1103355000 : Record 7035396;
      lwNoSD@1100244007 : Integer;
      lwDescripcion@1100244005 : Text[50];
      lwNumLinea@1100244003 : Integer;
      lwTotal@1100244001 : Integer;
      lwCont@1100244000 : Integer;
      lwFechaAnt@1100244002 : Date;
      lwFin@1100244006 : Boolean;
      lwOk@1100253001 : Boolean;
      lwOkModTod@1100253002 : Boolean;
    BEGIN
      // ModificaServicios
      // Modifica los partes ya creados
      // La funci¢n de modificar No elimina los partes anteriores ni crea nuevos. Si se ha modificado el calendario o los d¡as
      // deber  previamente retroceder el servicio repetitivo y volverlo a crear

      rPar.FINDFIRST;

      CLEAR(lwOkModTod); // Indica que se quiere modificar todo aunque no est‚ marcado como modificado
      IF NOT Modificado THEN BEGIN
        IF NOT CONFIRM(Text0014) THEN
          EXIT;
        lwOkModTod := TRUE;
      END;

      Comprobaciones(TRUE);
      CreaTemp(lrPuntos,0);

      IF NOT CONFIRM (Text0013) THEN
        EXIT;

      lrPuntos.RESET;

      lrParte.RESET;
      lrParte.SETCURRENTKEY("No Servicio Regular", Fecha);
      lrParte.SETRANGE("No Servicio Regular", "No.");
      CASE rPar."Empresa Real" OF // JPT 10/03/06 TransUnion ha pedido una diferencia de 1 d¡a en vez de 2
        rPar."Empresa Real":: TRN: lrParte.SETFILTER(Fecha, '>%1', TODAY);
        ELSE lrParte.SETFILTER(Fecha, '>%1', CALCDATE('+1D', TODAY));
      END;
      IF lrParte.FINDSET(TRUE) THEN BEGIN
        wVentana.OPEN(Text0007);
        lwTotal := lrParte.COUNT;
        CLEAR(lwCont);
        CLEAR(lwFechaAnt);
        REPEAT
          lrPuntos.SETRANGE("Id Ruta", lrParte.Referencia);
          lwOk := lrPuntos.FINDFIRST ;
          lwOk := lwOk AND (lrRuta.GET(lrPuntos."No Servicio", lrPuntos."No Ruta")); // Buscamos la Ruta
          lwOk := lwOk AND (lrRuta.Modificada OR lwOkModTod);
          IF lwOk THEN BEGIN
            IF lrRuta."Orden Hora Recogida" THEN
              lrPuntos.SETCURRENTKEY("Hora Recogida")
            ELSE
              lrPuntos.SETCURRENTKEY(Orden);
            lrPuntos.FINDFIRST ;

            // Se eliminan las lineas Servicios-Partes
            lrLinPar.RESET;
            lrLinPar.SETCURRENTKEY("N§ Parte","N§ Linea");
            lrLinPar.SETRANGE("N§ Parte", lrParte."N§ Parte");
            IF lrLinPar.FINDSET(TRUE) THEN BEGIN
              lrLinPar.DELETEALL;
            END;

            cImpTRN.BuscaServicio(lrParte,lrServicio);

            lrParte.VALIDATE("Codigo Servicio", lrRuta."Codigo Servicio");
            lrParte.VALIDATE(Presentacion     , lrRuta.Presentacion);
            lrParte.Observaciones             := lrRuta.Observaciones;
            lrParte.VALIDATE("Hora inicio"    , lrPuntos."Hora Recogida");
            lrParte.VALIDATE("Calcular maletas", "Calcular Maletas");
            lrParte.VALIDATE("Peticion Servicio Cliente", lrRuta."Peticion Servicio Cliente");
            // ValidaCond(lrParte    , lrRuta."Cod Conductor");
            // ValidaVehiculo(lrParte, lrRuta.Vehiculo);
            // JPT 04/06/07 Seleccionamos la tabla de rescursos
            // JPT 03/01/08 Si se han eliminado los recursos tambien se tiene que reflejar. P.O. Toni Carvajal
            BuscaRecursos(lrRuta, lrParte.Fecha, lrRecurs);
            ValidaCond(lrParte    , lrRecurs.Conductor);
            ValidaVehiculo(lrParte, lrRecurs.Vehiculo);


            CLEAR (lrParte."Adultos trans. soporte");
            CLEAR (lrParte."Ni¤os trans. soporte");
            lrParte.MODIFY(TRUE);

            lrServicio."Codigo Servicio" :=  lrRuta."Codigo Servicio";
            lrServicio.VALIDATE(Presentacion, lrRuta.Presentacion);
            lrServicio.MODIFY;

            // Buscamos el primer numero de linea
            CLEAR(lwNumLinea);
            lrLinPar.RESET;
            lrLinPar.SETRANGE("N§ Servicio" , lrServicio."N§ Servicio");
            lrLinPar.ASCENDING(FALSE);
            IF lrLinPar.FINDFIRST THEN
              lwNumLinea := lrLinPar."N§ Linea"
            ELSE
              lwNumLinea := 0;
            lrLinPar.RESET;

            REPEAT
              // Ahora introducimos las lineas
              CLEAR(lrLinPar);
              lwNumLinea += 1000;
              lrLinPar."N§ Linea" := lwNumLinea;
              lrLinPar.VALIDATE("N§ Servicio"               , lrServicio."N§ Servicio");
              lrLinPar.VALIDATE("Punto recogida"            , lrPuntos."Punto Recogida");
              lrLinPar.VALIDATE("Descripcion Punto Recogida", lrPuntos."Descripcion Punto Recogida");
              lrLinPar.VALIDATE("Adultos transportados"     , lrPuntos.Adultos);
              lrLinPar.VALIDATE("Ni¤os transportados"       , lrPuntos.Ni¤os);
              lrLinPar.VALIDATE("Hora recogida"             , lrPuntos."Hora Recogida");
              lrLinPar.es_alta(TRUE);
              lrLinPar.INSERT(TRUE);

              lrLinPar.VALIDATE ("N§ Parte"              , lrParte."N§ Parte");
              lrLinPar.VALIDATE (Touroperador            , TTOO);
              lrLinPar.VALIDATE ("Codigo Servicio"       , lrParte."Codigo Servicio");
              lrLinPar.VALIDATE (Vehiculo                , lrPuntos.Vehiculo);
              IF lrPuntos."Imprimir Documento Descriptivo" THEN
                lrLinPar.VALIDATE ("Imprimir Documento Descriptivo", TRUE);
              lrLinPar.MODIFY(TRUE);
              lwFin   := lrPuntos.NEXT=0;
            UNTIL lwFin;
            lwCont += 1;
            lrLinPar.ultima_zona_fisica(lrParte,FALSE,FALSE);
            A¤adePrecioManual(lrParte,lrRuta);
          END;

          wVentana.UPDATE(1,ROUND(lwCont/lwTotal*10000,1));
          wVentana.UPDATE(2,lrParte.Fecha);
          COMMIT; // JPT 04/10/06 para evitar bloqueos
        UNTIL lrParte.NEXT=0;
      END;

      // Se graban los datos de creacion

      "Servicios Creados":= TRUE;
      "Usuario Creacion" := USERID;
      "Fecha Creacion"   := TODAY;
      "Hora Creacion"    := TIME;
      Modificado         := FALSE;
      MODIFY;

      CLEAR(lrRuta);
      lrRuta.SETRANGE("No. Servicio regular", "No.");

      // AJS 15.03.2006
      // Solo trabajamos con las rutas habilitadas

      lrRuta.SETRANGE("Ruta inhabilitada", FALSE);

      IF lrRuta.FINDSET(TRUE) THEN
        lrRuta.MODIFYALL(Modificada, FALSE);

      wVentana.CLOSE;
    END;

    PROCEDURE A¤adePrecioManual@1100244005(prParte@1100244000 : Record 7010360;prRuta@1100253000 : Record 7035387);
    VAR
      lrTTOOParte@1100244001 : Record 7010365;
      lwImporte@1100244002 : Decimal;
    BEGIN
      // A¤adePrecioManual

      CASE "Tipo Aplicacion" OF
        "Tipo Aplicacion"::"Servicio Manual","Tipo Aplicacion"::"Total Factura" : lwImporte := prRuta."Precio Medio"
        ELSE lwImporte := 0;
      END;

      lrTTOOParte.RESET;
      IF lrTTOOParte.GET(prParte."N§ Parte",TTOO) THEN BEGIN
        lrTTOOParte."Precio manual venta" := lwImporte;
        lrTTOOParte.VALIDATE("Cod. Divisa venta");
        IF lwImporte <> 0 THEN
          lrTTOOParte."Ingreso Precios Por" := lrTTOOParte."Ingreso Precios Por"::"Zona mas alejada"
        ELSE
          lrTTOOParte."Ingreso Precios Por" := lrTTOOParte."Ingreso Precios Por"::"Cada zona";
        lrTTOOParte.MODIFY;
      END;
    END;

    PROCEDURE Facturacion@1100244006();
    VAR
      lwOpc@1100244000 : ',Prefacturar,Facturar';
      lrCond@1100244001 : Record 7010420;
      lrPrefac@1100244005 : Record 7010415;
      lrPI@1100244006 : Record 7035392;
      lrTempTextAlt@1103355000 : TEMPORARY Record 7035394;
      lfSelF@1100244002 : Form 7010457;
      lText00001@1100244003 : TextConst 'ESP=No se puede dejar %1 en blanco';
      lwFecha@1100244004 : Date;
      lwAjuste@1100244007 : Decimal;
    BEGIN
      // Facturacion

      rPar.FINDFIRST;

      lwOpc := STRMENU(Text0015,1);

      IF lwOpc=0 THEN
        EXIT;

      IF NOT CONFIRM (Text0016,TRUE,lwOpc) THEN
        EXIT;

      // AJF 07/01/09
      // Evito borrar el registro, ya que ahora tiene datos permanentes.
      IF lrCond.GET(USERID) THEN
        lrCond.LimpiaRegistro
      ELSE BEGIN
        lrCond.INIT;
        lrCond.Usuario := USERID;
        lrCond.INSERT;
      END;

      lrCond."Tipo factura"           := "Tipo factura";
      lrCond."Tipo prefactura"        := "Tipo factura";
      lrCond.MODIFY;

      lrCond.FILTERGROUP(2);
      lrCond.SETRECFILTER;
      lrCond.FILTERGROUP(0);
      lfSelF.SETTABLEVIEW(lrCond);
      CreaTempSerReg(lrTempTextAlt); // JPT 07/12/06 Traspasamos las lineas a temporal
      lfSelF.SetLinTextoAlternativo(lrTempTextAlt); // Las pasamos a la ventana
      COMMIT;
      IF lfSelF.RUNMODAL <> ACTION::OK THEN
        EXIT;

      // JPT 05/12/06 Traspasamos las lineas de texto alternativo
      lfSelF.GetLinTextoAlternativo(lrTempTextAlt);

      lfSelF.GETRECORD(lrCond);

      IF lrCond."Fecha desde" = 0D THEN
        ERROR(lText00001, lrCond.FIELDCAPTION("Fecha desde"));
      IF lrCond."Fecha hasta" = 0D THEN
        ERROR(lText00001, lrCond.FIELDCAPTION("Fecha hasta"));

      ComprobarConfirmados(lrCond."Fecha desde", lrCond."Fecha hasta");

      // Se pide la fecha de registro de factura

      lwFecha:= cFactAuto.BuscaFechaFactura;

      wVentana.OPEN(Text0018);
      wVentana.UPDATE(1, lwFecha);
      wVentana.INPUT(1, lwFecha);
      wVentana.CLOSE;

      IF lwFecha = 0D THEN
        ERROR(Text0020);

      // Comprueba que no exista ninguna factura con fecha posterior a la indicada

      lrPrefac.RESET;
      lrPrefac.SETCURRENTKEY(Facturada, "Fecha Registro factura");
      lrPrefac.SETRANGE (Facturada               , TRUE);
      lrPrefac.SETFILTER("Fecha Registro factura", '>%1', lwFecha);
      IF lrPrefac.FINDLAST THEN
        ERROR(Text0019, lwFecha, lrPrefac."Fecha Registro factura");

      lrCond.Cliente                  := Cliente;
      lrCond.Touroperador             := TTOO;
      lrCond."Fecha Factura"          := lwFecha;
      // lrCond.Garaje                   := Garaje;
      // lrCond."Codigos Servicio"       := "Codigo Servicio"; // Ahora puede haber varios en un servicio repetitivo
      lrCond."Texto Registro"         := "Texto Registro";
      lrCond."Texto alternativo"      := "Texto Alternativo";
      lrCond."Tipo factura"           := "Tipo factura";
      lrCond."Tipo prefactura"        := "Tipo factura";
      lrCond."Prefactura Prueba"      := lwOpc=1;
      lrCond."Servicio Repetitivo"    := "No.";
      lrCond."Facturacion Automatica" := TRUE;
      lrCond.Imprimir                 := lrCond."Prefactura Prueba";
      lrCond.GetCondiciones(FALSE,FALSE); // Esto determina si ser  serie standard o alternativa
      lrCond.MODIFY;

      lwAjuste := Ajustar(lrCond,lwOpc=2);


      CASE rPar."Empresa Real" OF
        rPar."Empresa Real"::TRN, rPar."Empresa Real"::UEX,
        rPar."Empresa Real"::TER, rPar."Empresa Real"::CNT:
        BEGIN
          // JPT 05/12/06 Traspasamos las lineas de texto alternativo
          IF "Tipo factura" = "Tipo factura"::"Texto alternativo" THEN
            cFactIng.SetTextAlternativo(lrTempTextAlt);

          IF cFactIng.CrearPrefactura(lrPI, lrCond, TRUE) THEN
          BEGIN
            IF NOT lrCond."Prefactura Prueba" THEN
            BEGIN
              lrCond.Imprimir := TRUE;
              cFactIng.CrearFactura(lrPI, lrCond, TRUE, FALSE);
            END;
          END;
        END
        ELSE
        BEGIN
          IF cFunFact.crear_prefactura(lrPI, lrCond, TRUE) AND (lwOpc= 2) THEN BEGIN
            lrCond.Imprimir := TRUE;
            cFunFact.crear_factura(lrPI, lrCond, TRUE);
          END;
        END;
      END;
    END;

    PROCEDURE ComprobarConfirmados@1100244007(pwFechaDesde@1100244000 : Date;pwFechaHasta@1100244001 : Date);
    VAR
      lrParte@1100244002 : Record 7010360;
    BEGIN
      // ComprobarConfirmados
      // Esta cosita comprueba que en el rango de fechas dado no haya ning£n parte de este
      // servicio repetitivo sin confirmar

      lrParte.RESET;
      lrParte.SETCURRENTKEY("No Servicio Regular",
                            Fecha);
      lrParte.SETRANGE("No Servicio Regular" , "No.");
      lrParte.SETRANGE(Fecha                 , pwFechaDesde, pwFechaHasta);
      lrParte.SETRANGE(Confirmado            , FALSE);
      IF lrParte.FINDFIRST THEN
        ERROR(Text0017, lrParte."N§ Parte");
    END;

    PROCEDURE Ajustar@1100244014(prCond@1100244000 : Record 7010420;pwInserta@1100244005 : Boolean) : Decimal;
    VAR
      lrPI@1100244001 : Record 7035392;
      lrContV@1100244004 : Record 7010403;
      lwTotal@1100244002 : Decimal;
      lwAjuste@1100244003 : Decimal;
    BEGIN
      // Ajustar
      // La finalidad de esta funci¢n es que en el caso de que tipo de Aplicacion sea "Total Factura" se
      // generar  una linea de ajuste por la diferencia entre el importe de la factura y la suma de los precios medios de los partes

      rPar.FINDFIRST;

      IF "Tipo Aplicacion" <> "Tipo Aplicacion"::"Total Factura" THEN
        EXIT;

      TESTFIELD(Importe);
      CLEAR(lwAjuste);

      lrPI.RESET;
      lrPI.SETCURRENTKEY("Servicio Regular",Fecha);
      lrPI.SETRANGE("Servicio Regular", prCond."Servicio Repetitivo");
      lrPI.SETRANGE(Fecha                , prCond."Fecha desde", prCond."Fecha hasta");
      // lrPI.SETRANGE(Garaje               , prCond.Garaje);
      lrPI.SETRANGE("Cliente Operativo"     , prCond.Cliente);
      lrPI.SETRANGE(Touroperador         , prCond.Touroperador);
      //lrPI.SETRANGE("Codigo servicio"    , prCond."Codigos Servicio");
      lrPI.SETFILTER("N§ Prefactura",'%1','');
      lrPI.SETFILTER("N§ Factura"   ,'%1','');

      CLEAR(lwTotal);
      IF lrPI.FINDSET THEN BEGIN
        // Miramos si el contrato es con impuestos incluidos o no
        lrContV.RESET;
        lrContV.SETFILTER(Garaje,'%1|%2', lrPI.Garaje, rPar."Todos los garajes");
        lrContV.SETRANGE("N§ Contrato" , lrPI."N§ Contrato");
        lrContV.FINDFIRST;
        REPEAT
          IF lrContV."Impuestos incluidos" THEN
            lwTotal += lrPI."Importe con impuesto"
          ELSE
            lwTotal += lrPI."Importe sin impuesto";
          // lwTotal += lrPI.Importe; JPT 03/01/05  Se ha dado el caso de cambio en contrato despues de partes confirmados
        UNTIL lrPI.NEXT=0;
        lwAjuste := Importe - lwTotal;

        // INSERTAMOS LA LINEA DE AJUSTE
        IF pwInserta AND (lwAjuste <> 0) THEN BEGIN
          CLEAR(lrPI."N§ Linea");
          CLEAR(lrPI.Vehiculo);
          CLEAR(lrPI."Pax Transportados");
          CLEAR(lrPI."Pax Facturados");
          lrPI.Importe := lwAjuste;
          lrPI."Precio manual" := TRUE;
          lrPI.Facturable      := TRUE;

          // AJF - 11/06/08
          // Discernimos entre precio vehiculo sin o con impuestos.
          IF lrContV."Impuestos incluidos" THEN BEGIN
            lrPI.VALIDATE(lrPI."Importe con impuesto",lwAjuste);
            lrPI.VALIDATE("Precio Vehiculo",lwAjuste);
          END
          ELSE BEGIN
            lrPI.VALIDATE(lrPI."Importe sin impuesto",lwAjuste);
            lrPI.VALIDATE("Precio Vehiculo sin impuestos",lwAjuste);
          END;
          lrPI.INSERT(TRUE);
        END;

      END ELSE
        ERROR(Text0021);

      EXIT(lwAjuste);
    END;

    PROCEDURE CrearDia@1103350012(pwRuta@1100253000 : Integer);
    VAR
      lrPuntos@1103350000 : TEMPORARY Record 7010394;
      lrParte@1103350002 : Record 7010360;
      lrRuta@1100253001 : Record 7035387;
      lwFecha@1103350001 : Date;
    BEGIN
      // CrearDia
      // Crea un/unos servicio/s para un d¡a deteminado que nos se hubiesen incluido anteriormente
      // Si pwRuta <> 0 filtrar  y crear  los serviicios de esa ruta unicamente

      rPar.FINDFIRST;

      wVentana.OPEN(Text0022);
      wVentana.INPUT(1,lwFecha);
      wVentana.CLOSE;

      IF lwFecha = 0D THEN
        ERROR(Text0023);

      IF (lwFecha < "Fecha Inicio") OR (lwFecha > "Fecha Final") THEN
        ERROR(Text0024);

      CLEAR(lrRuta);
      IF pwRuta <> 0 THEN
        lrRuta.GET("No.",pwRuta);

      IF NOT CONFIRM (Text0025,FALSE,lwFecha, lrRuta."Id Ruta") THEN
        EXIT;

      // Si ya existe alg£n parte para ese d¡a avisamos
      CLEAR(lrParte);
      lrParte.SETCURRENTKEY("No Servicio Regular",
                            Fecha);
      lrParte.SETRANGE("No Servicio Regular", "No.");
      lrParte.SETRANGE(Fecha                , lwFecha);
      IF lrParte.FINDFIRST THEN BEGIN
        IF NOT CONFIRM(Text0027, FALSE, lwFecha, "No.") THEN
          EXIT;
      END;

      Comprobaciones(TRUE);
      CreaTemp(lrPuntos, pwRuta);

      lrPuntos.RESET;
      IF pwRuta <> 0 THEN
        lrPuntos.SETRANGE("No Ruta", pwRuta);
      CrearServiciosRuta(lwFecha, lrPuntos, FALSE);

      MESSAGE(Text0026);
    END;

    PROCEDURE ModificaFechaHasta@1103350006(pwFechaAnterior@1103350000 : Date;VAR pwNuevaFecha@1103350001 : Date);
    BEGIN
      // ModificaFechaHasta

      rPar.FINDFIRST;

      IF NOT "Servicios Creados" THEN
        EXIT;

      IF (pwFechaAnterior=0D) OR (pwNuevaFecha=0D) THEN
        EXIT;

      // Si los servicios se crean en la apertura diaria...
      IF rPar."Generar Servicios Automaticos" THEN
        EXIT;

      IF pwNuevaFecha > pwFechaAnterior THEN BEGIN
        IF CONFIRM(Text0028,FALSE,pwNuevaFecha-pwFechaAnterior) THEN
          CrearServicios(FALSE, pwFechaAnterior+1, pwNuevaFecha)
        ELSE
          pwNuevaFecha := pwFechaAnterior;
      END;
      IF pwNuevaFecha < pwFechaAnterior THEN BEGIN
        IF CONFIRM(Text0029,FALSE,pwFechaAnterior-pwNuevaFecha) THEN
          Retroceder(FALSE, pwNuevaFecha+1, pwFechaAnterior,0)
        ELSE
          pwNuevaFecha := pwFechaAnterior;
      END;
    END;

    PROCEDURE ValidaVehiculo@1103355000(VAR prParte@1103355000 : Record 7010360;pwVehiculo@1103355001 : Code[10]);
    VAR
      lrVeh@1103355002 : Record 7010346;
      lrRecRef@1103355003 : RecordRef;
    BEGIN
      // ValidaVehiculo

      { JPT 03/01/08 Tiene que poderse dejar el vehiculo en blanco en las modificaciones
      IF pwVehiculo='' THEN
        EXIT;
      }

      CLEAR(lrVeh);
      IF NOT lrVeh.GET(pwVehiculo) THEN
        CLEAR(lrVeh);

      prParte.Vehiculo                    := pwVehiculo;
      prParte."N§ Vehiculo"               := lrVeh.Numero;
      prParte."Tipo Vehiculo"             := lrVeh."Tipo vehiculo";

      IF prParte."Tipo Vehiculo facturacion" = '' THEN BEGIN
        // Si prParte es temporary no deberia pasar por el trigger onvalidate.
        lrRecRef.GETTABLE(prParte);
        IF EsTemporary(lrRecRef) THEN
          prParte."Tipo Vehiculo facturacion" := lrVeh."Tipo vehiculo"
        ELSE
          prParte.VALIDATE("Tipo Vehiculo facturacion", lrVeh."Tipo vehiculo");
      END;

      prParte."Vehiculo Agregado":= lrVeh.Agregado;
      prParte.Proveedor          := lrVeh."Empresa propietaria";
    END;

    PROCEDURE ValidaCond@1103355001(VAR prParte@1103355001 : Record 7010360;pwCodCond@1103355000 : Code[10]);
    VAR
      lrCond@1103355002 : Record 7010327;
    BEGIN
      // ValidaCond
      // Valida el campo conductor

      { JPT 03/01/08 Se ha de poder dejar el conductor en blanco en las modificaciones
      IF pwCodCond='' THEN
        EXIT;
      }

      prParte."Cod Conductor" := pwCodCond;
      IF NOT lrCond.GET(pwCodCond) THEN
        CLEAR(lrCond);
      prParte.Conductor := lrCond."Nombre Abreviado";
    END;

    PROCEDURE CreaTempSerReg@1103355002(VAR prTemp@1103355001 : TEMPORARY Record 7035394);
    VAR
      lrTexAlt@1103355000 : Record 7035394;
    BEGIN
      // CreaTempSerReg
      // Crea el temporal de servicios regulares

      CLEAR(prTemp);
      prTemp.DELETEALL;

      IF "Tipo factura" <>  "Tipo factura"::"Texto alternativo" THEN
        EXIT;

      CLEAR(lrTexAlt);
      lrTexAlt.SETRANGE(Tipo, lrTexAlt.Tipo::"Servicio Regular");
      lrTexAlt.SETRANGE(Codigo, FORMAT("No."));
      IF lrTexAlt.FINDSET THEN BEGIN
        REPEAT
          prTemp := lrTexAlt;
          prTemp.Tipo   := prTemp.Tipo::Usuario;
          prTemp.Codigo := USERID;
          prTemp.INSERT;
        UNTIL lrTexAlt.NEXT=0;
      END;
    END;

    PROCEDURE CreacionDiaria@1103355003(pwFecha@1103355003 : Date);
    VAR
      lrParte@1103355004 : Record 7010360;
      lrPuntos@1103355005 : TEMPORARY Record 7010394;
      lwCont@1103355000 : Integer;
      lwTotal@1103355001 : Integer;
      lwOK@1103355002 : Boolean;
      lwDescripcion@1103355006 : Text[30];
    BEGIN
      // CreacionDiaria
      // Este proceso se lanza en la apertura diaria
      // Dado una fecha determinada se crean los partes de los servicios regulares correspondientes marcados como "Generacion Diaria".

      IF pwFecha=0D THEN
        EXIT;

      rPar.FINDFIRST;
      CLEAR(rSerRep);
      rSerRep.SETRANGE("Generacion Diaria",TRUE);
      IF rSerRep.FINDSET THEN BEGIN
        wVentana.OPEN(Text0033);
        lwTotal := rSerRep.COUNT;
        CLEAR(lwCont);
        REPEAT
          lwOK := (pwFecha >= rSerRep."Fecha Inicio") AND (rSerRep."Fecha Inicio" <> 0D);
          IF (rSerRep."Fecha Final" <> 0D) THEN
            lwOK :=  lwOK AND (pwFecha < rSerRep."Fecha Final");

          IF lwOK THEN BEGIN // Comprobaci¢n del calendario
            IF rSerRep.Calendario <> '' THEN
              lwOK := NOT CalendarMgmt.CheckDateStatus(rSerRep.Calendario, pwFecha, lwDescripcion);
          END;

          IF lwOK THEN BEGIN // Si ya existe alg£n parte para ese d¡a
            CLEAR(lrParte);
            lrParte.SETCURRENTKEY("No Servicio Regular", Fecha);
            lrParte.SETRANGE("No Servicio Regular", rSerRep."No.");
            lrParte.SETRANGE(Fecha                , pwFecha);
            lwOK := NOT  lrParte.FINDFIRST;
          END;

          IF lwOK THEN BEGIN
            wVentana.UPDATE(1, STRSUBSTNO('%1-%2',rSerRep."No.",rSerRep.Descripcion));
            Rec := rSerRep;
            Comprobaciones(TRUE);
            CreaTemp(lrPuntos, 0);
            CrearServiciosRuta(pwFecha, lrPuntos, FALSE);
          END;

          lwCont +=1;
          wVentana.UPDATE(2, ROUND(lwCont/lwTotal*10000,1));
        UNTIL rSerRep.NEXT=0;
        wVentana.CLOSE;
      END;
    END;

    PROCEDURE BuscaRecursos@1103355004(prRuta@1103355000 : Record 7035387;pwFecha@1103355001 : Date;VAR prRecuros@1103355002 : Record 7035396) : Boolean;
    VAR
      lwOk@1103355003 : Boolean;
    BEGIN
      // BuscaRecursos
      // Devuelve true si encuentra alg£n recurso para esa ruta y fecha

      IF (prRuta."No. Servicio regular"=0) OR (prRuta."No. Ruta"=0) OR (pwFecha=0D) THEN
        EXIT;

      CLEAR(prRecuros);
      prRecuros.SETRANGE("No. Servicio regular", prRuta."No. Servicio regular");
      prRecuros.SETRANGE("No. Ruta"            , prRuta."No. Ruta");
      prRecuros.SETFILTER("Fecha desde"        ,'<=%1', pwFecha);
      prRecuros.SETFILTER("Fecha hasta"        ,'>=%1', pwFecha);
      lwOk := prRecuros.FINDFIRST;

      EXIT(lwOk);
    END;

    PROCEDURE CrearServicio@1103355005(pwFecha@1103350001 : Date;VAR prTempPuntos@1103350002 : TEMPORARY Record 7010394;prRuta@1100253000 : Record 7035387);
    VAR
      lrParte@1100244000 : Record 7010360;
      lrSerPar@1100244001 : Record 7010359;
      lrServicio@1100244002 : Record 7010358;
      lrLinPar@1100244009 : Record 7010359;
      lrRecurs@1103355000 : Record 7035396;
      lwNoSD@1100244004 : Integer;
      lwLaborable@1100244006 : Boolean;
      lwDescripcion@1100244007 : Text[50];
      lwOrAnt@1100244008 : Integer;
      lwNumLinea@1100244010 : Integer;
      lwFin@1100244011 : Boolean;
      lwTotal@1100244012 : Integer;
      lwCont@1100244013 : Integer;
      lwLMax@1103350000 : Integer;
      lwTieneRstr@1103350003 : Boolean;
      lwOk@1103350004 : Boolean;
      lwNDia@1103350006 : Integer;
      lrParteTMP@1103355001 : TEMPORARY Record 7010360;
      lrLinParTMP@1103355002 : TEMPORARY Record 7010359;
      lrCodServ@1103355003 : Record 7010320;
    BEGIN
      // CrearServicio
      // Proceso por el cual se generan un servicio repetitivo para un d¡a dado
      // prTempPuntos es un Temporal

      lwNDia:= DATE2DWY(pwFecha,1);
      prRuta.TESTFIELD("Id Ruta");
      CLEAR(prTempPuntos);
      prTempPuntos.SETRANGE("No Ruta", prRuta."No. Ruta");
      IF prRuta."Orden Hora Recogida" THEN
        prTempPuntos.SETCURRENTKEY("Hora Recogida")
      ELSE
        prTempPuntos.SETCURRENTKEY(Orden);

      CASE lwNDia OF
        1: prTempPuntos.SETRANGE(Lunes,     TRUE);
        2: prTempPuntos.SETRANGE(Martes,    TRUE);
        3: prTempPuntos.SETRANGE(Miercoles, TRUE);
        4: prTempPuntos.SETRANGE(Jueves,    TRUE);
        5: prTempPuntos.SETRANGE(Viernes,   TRUE);
        6: prTempPuntos.SETRANGE(Sabado,    TRUE);
        7: prTempPuntos.SETRANGE(Domingo,   TRUE);
      END;

      // Tantos Partes como numero de vehiculos tenga

      FOR lwCont := 1 TO prRuta."Num Vehiculos" DO BEGIN
        lwOk := prTempPuntos.FINDSET;
        IF lwOk THEN BEGIN
          // Se crea el Parte y Servicio
          rPar.TESTFIELD("Serie Partes");
          lrCodServ.GET(prRuta."Codigo Servicio");

          CLEAR(lrParteTMP);
          lrParteTMP.Garaje                      := Garaje;
          lrParteTMP.Fecha                       := pwFecha;
          lrParteTMP.Cliente                     := Cliente;
          lrParteTMP."Codigo Servicio"           := prRuta."Codigo Servicio";
          lrParteTMP."Tipo Servicio"             := lrCodServ."Tipo servicio";
          lrParteTMP.Presentacion                := prRuta.Presentacion;
          lrParteTMP.Observaciones               := prRuta.Observaciones;
          lrParteTMP.Touroperadores              := TTOO;
          lrParteTMP."Touroperador principal"    := TTOO;
          lrParteTMP."Hora inicio"               := prTempPuntos."Hora Recogida";
          lrParteTMP."Calcular maletas"          := "Calcular Maletas";
          lrParteTMP."Origen/Destino"            := prRuta."Origen/Destino";
          lrParteTMP."Adultos trans. soporte"    := 0;
          lrParteTMP."Ni¤os trans. soporte"      := 0;
          lrParteTMP."No Servicio Regular"       := "No.";
          lrParteTMP.Referencia                  := prRuta."Id Ruta";
          lrParteTMP."Origen creacion"           := lrParte."Origen creacion"::"Servicios Regulares";
          lrParteTMP.Usuario                     := USERID;
          lrParteTMP."Peticion Servicio Cliente" := prRuta."Peticion Servicio Cliente";
          lrParteTMP.Guia                        := prRuta.Guia;
          lrParteTMP."Vehiculo Escolar"          := prRuta."Vehiculo Escolar";
          lrParteTMP."Vehiculo especial"         := prRuta."Vehiculo Especial";
          lrParteTMP."Vehiculo Minusvalidos"     := prRuta."Vehiculo Minusvalidos";

          // JPT 04/06/07 Seleccionamos la tabla de rescursos
          IF BuscaRecursos(prRuta, pwFecha, lrRecurs) THEN BEGIN
            ValidaCond(lrParteTMP    , lrRecurs.Conductor);
            ValidaVehiculo(lrParteTMP, lrRecurs.Vehiculo);
          END;

          cImpTRN.BuscaServicio(lrParteTMP, lrServicio);

          // Buscamos el primer numero de linea
          CLEAR(lwNumLinea);
          lrLinPar.RESET;
          lrLinPar.SETRANGE("N§ Servicio" , lrServicio."N§ Servicio");
          lrLinPar.ASCENDING(FALSE);
          IF lrLinPar.FINDFIRST THEN
            lwNumLinea := lrLinPar."N§ Linea"
          ELSE
            lwNumLinea := 0;
          lrLinPar.RESET;

          REPEAT

            // Observaciones de linea
            lwOk := prTempPuntos.Observaciones <> '';
            lwOk := lwOk AND (STRPOS(lrParteTMP.Observaciones,prTempPuntos.Observaciones) = 0);
            IF lwOk THEN BEGIN
              // No intentamos insertar m s de lo que quepa
              lwLMax :=  MAXSTRLEN(lrParteTMP.Observaciones) - STRLEN(lrParteTMP.Observaciones) - 2;
              IF lwLMax > 0 THEN BEGIN
                IF lrParteTMP.Observaciones <> '' THEN
                  lrParteTMP.Observaciones += ', ';
                lrParteTMP.Observaciones += COPYSTR(prTempPuntos.Observaciones,1,lwLMax);
              END;
            END;

            // Ahora introducimos las lineas
            CLEAR(lrLinParTMP);
            lwNumLinea += 1000;
            lrLinParTMP."N§ Linea"                      := lwNumLinea;
            lrLinParTMP."N§ Servicio"                   := lrServicio."N§ Servicio";
            lrLinParTMP."Punto recogida"                := prTempPuntos."Punto Recogida";
            lrLinParTMP."Descripcion Punto Recogida"    := prTempPuntos."Descripcion Punto Recogida";
            lrLinParTMP."Adultos transportados"         := prTempPuntos.Adultos;
            lrLinParTMP."Ni¤os transportados"           := prTempPuntos.Ni¤os;
            lrLinParTMP."Adultos invitados ingreso"     := prTempPuntos."Adultos Invitados";
            lrLinParTMP."Ni¤os invitados ingreso"       := prTempPuntos."Ni¤os Invitados";
            lrLinParTMP."N§ Parte"                      := lrParteTMP."N§ Parte";
            lrLinParTMP.Touroperador                    := TTOO;
            lrLinParTMP."Codigo Servicio"               := lrParteTMP."Codigo Servicio";
            lrLinParTMP."Hora recogida"                 := prTempPuntos."Hora Recogida";
            lrLinParTMP.Vehiculo                        := prTempPuntos.Vehiculo;

            IF prTempPuntos."Imprimir Documento Descriptivo" THEN
              lrLinParTMP."Imprimir Documento Descriptivo" := TRUE;

            lrLinParTMP.INSERT;
          UNTIL prTempPuntos.NEXT=0;

          cGestionParte.CrearParte(lrParteTMP, lrLinParTMP, cGestionParte.TipoParteServiciosRegulares, lrParte);

          lrParte.FIND;
          lrLinPar.ultima_zona_fisica(lrParte,FALSE,FALSE);
          lrParte.FIND; // Recuperamos el parte con los nuevos datos.
          A¤adePrecioManual(lrParte, prRuta);
          cFunParAd.GarajeAutomatico(lrParte);
          lrParte.MODIFY(TRUE);

          // AJF 13/01/09
          // Tratamos de generar los subtrayectos al crear el parte para Ibiza Tours.
          IF rPar."Empresa Real" = rPar."Empresa Real"::IBZ THEN BEGIN
            lrParte.CargarSubtrayectos;
          END;

        END;
      END;
    END;

    PROCEDURE EsTemporary@1103355006(VAR lrRecRef@1103355000 : RecordRef) : Boolean;
    VAR
      lrObject@1103355001 : Record 2000000001;
    BEGIN
      // EsTemporary
      // Si un record es temporary este tiene un n£mero distinto al de su tabla.

      lrObject.SETCURRENTKEY(Type, Name);
      lrObject.SETRANGE(Type, lrObject.Type::Table);
      lrObject.SETRANGE(Name, lrRecRef.NAME);
      lrObject.FINDFIRST;

      EXIT(lrObject.ID <> lrRecRef.NUMBER);
    END;

    BEGIN
    {
      $001 AJS 12112014 Cambio la TableRelation del campo Calendario para apuntar a la tabla de Calendario Transfer
    }
    END.
  }
}
