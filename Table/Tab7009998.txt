OBJECT Table 7009998 Control de gestion
{
  OBJECT-PROPERTIES
  {
    Date=01/06/15;
    Time=10:42:13;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
  }
  FIELDS
  {
    { 1   ;   ;Fecha desde         ;Date          ;CaptionML=[ENU=Date from;
                                                              ESP=Fecha desde] }
    { 3   ;   ;Tickets sin servicio;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Reserva Excursion" WHERE (Fecha Excursion=FIELD(FILTER(Filtro Texto)),
                                                                                                Anulada=CONST(No),
                                                                                                Reembolsada=CONST(No),
                                                                                                N§ Servicio=CONST(0),
                                                                                                Producto=FILTER(<>'')));
                                                   CaptionML=[ENU=Tickets w/o services;
                                                              ESP=Tickets sin servicio];
                                                   Editable=No }
    { 5   ;   ;Tickets sin liquidar;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Reserva Excursion" WHERE (Fecha Venta=FIELD(FILTER(Filtro Texto)),
                                                                                                Anulada=CONST(No),
                                                                                                Reembolsada=CONST(No),
                                                                                                Liquidado GV=CONST(No),
                                                                                                Producto=FILTER(<>''),
                                                                                                N§ Ticket=FILTER(<>''),
                                                                                                Reserva a credito=CONST(No)));
                                                   CaptionML=[ENU=No liquidated tickets;
                                                              ESP=Tickets sin liquidar];
                                                   Editable=No }
    { 7   ;   ;Servicios sin confirmar;Integer    ;FieldClass=FlowField;
                                                   CalcFormula=Count("Servicio Excursion" WHERE (Fecha=FIELD(FILTER(Filtro Texto)),
                                                                                                 Estado=CONST(Generada)));
                                                   CaptionML=[ENU=Services w/o confirmation;
                                                              ESP=Servicios sin confirmar];
                                                   Editable=No }
    { 9   ;   ;Llegadas sin vuelo  ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Reserva Transfer" WHERE (Fecha entrada=FIELD(FILTER(Filtro Texto)),
                                                                                               Entrada=CONST(1),
                                                                                               Compa¤ia entrada=CONST(''),
                                                                                               No Show=CONST(No),
                                                                                               Traslado en destino=CONST(No)));
                                                   CaptionML=[ENU=Arrivals w/o flight;
                                                              ESP=Llegadas sin vuelo];
                                                   Editable=No }
    { 11  ;   ;Llegadas sin servicio;Integer      ;FieldClass=FlowField;
                                                   CalcFormula=Count("Reserva Transfer" WHERE (Fecha entrada=FIELD(FILTER(Filtro Texto)),
                                                                                               Entrada=CONST(1),
                                                                                               Compa¤ia entrada=FILTER(<>''),
                                                                                               Agrupacion entrada=CONST(0),
                                                                                               No Show=CONST(No),
                                                                                               Traslado en destino=CONST(No)));
                                                   CaptionML=[ENU=Arrivals w/o service;
                                                              ESP=Llegadas sin servicio];
                                                   Editable=No }
    { 13  ;   ;Salidas sin servicio;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Reserva Transfer" WHERE (Fecha salida=FIELD(FILTER(Filtro Texto)),
                                                                                               Salida=CONST(1),
                                                                                               Compa¤ia salida=FILTER(<>''),
                                                                                               Agrupacion salida=CONST(0),
                                                                                               No Show=CONST(No),
                                                                                               Traslado en destino=CONST(No)));
                                                   CaptionML=[ENU=Departures w/o service;
                                                              ESP=Salidas sin servicio];
                                                   Editable=No }
    { 15  ;   ;Transfers sin generar;Integer      ;FieldClass=FlowField;
                                                   CalcFormula=Count(Agrupacion WHERE (Fecha=FIELD(FILTER(Filtro Texto)),
                                                                                       Estado=CONST(Pendiente)));
                                                   CaptionML=[ENU=Transfers w/o generate;
                                                              ESP=Transfers sin generar];
                                                   Editable=No }
    { 17  ;   ;Interface sin procesar;Integer     ;FieldClass=FlowField;
                                                   CalcFormula=Count("Cabecera interfase" WHERE (Fecha Importacion=FIELD(FILTER(Filtro Texto)),
                                                                                                 Estado=CONST(No procesada)));
                                                   CaptionML=[ENU=Interface w/o process;
                                                              ESP=Interface sin procesar];
                                                   Editable=No }
    { 19  ;   ;Interface en errores;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Cabecera interfase" WHERE (Fecha Importacion=FIELD(FILTER(Filtro Texto)),
                                                                                                 Estado=CONST(Erronea)));
                                                   CaptionML=[ENU=Interface in error status;
                                                              ESP=Interface en errores];
                                                   Editable=No }
    { 21  ;   ;EXC - FPR sin registrar;Integer    ;FieldClass=FlowField;
                                                   CalcFormula=Count("Gen. Journal Line" WHERE (Journal Batch Name=CONST(EXCUR),
                                                                                                Posting Date=FIELD(FILTER(Filtro Texto))));
                                                   OnLookup=BEGIN
                                                              _LookupDiario('EXCUR');
                                                            END;

                                                   CaptionML=[ENU=EXC - IPR pending to post;
                                                              ESP=EXC - FPR sin registrar];
                                                   Editable=No }
    { 23  ;   ;TRANS - FPR sin registrar;Integer  ;FieldClass=FlowField;
                                                   CalcFormula=Count("Gen. Journal Line" WHERE (Journal Batch Name=CONST(TRANSFER),
                                                                                                Posting Date=FIELD(FILTER(Filtro Texto))));
                                                   OnLookup=BEGIN
                                                              _LookupDiario('TRANSFER');
                                                            END;

                                                   CaptionML=[ENU=TRANSFER - IPR pending to post;
                                                              ESP=TRANSFER - FPR sin registrar];
                                                   Editable=No }
    { 25  ;   ;TRANS - Ingresos sin facturar;Integer;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Count("Coste/Ingreso transfer" WHERE (Fecha=FIELD(FILTER(Filtro Texto)),
                                                                                                     Concepto=CONST(TRNI),
                                                                                                     N§ Factura=CONST()));
                                                   OnLookup=BEGIN
                                                              _LookupTRFSinFra;
                                                            END;

                                                   CaptionML=[ENU=TRANSFER - Incomes pending to invoice;
                                                              ESP=TRANSFER - Ingresos sin facturar];
                                                   Editable=No }
    { 27  ;   ;Liquidaciones sin prefacturar;Integer;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Count("Liquidacion Guia venta" WHERE (Fecha=FIELD(FILTER(Filtro Texto)),
                                                                                                     Venta prefacturada=CONST(No)));
                                                   CaptionML=[ENU=Liquidations w/o proforma;
                                                              ESP=Liquidaciones sin prefacturar];
                                                   Editable=No }
    { 29  ;   ;EXC - Reservas sin facturar;Integer;FieldClass=FlowField;
                                                   CalcFormula=Count("Reserva Excursion" WHERE (Fecha Venta=FIELD(FILTER(Filtro Texto)),
                                                                                                Anulada=CONST(No),
                                                                                                Reembolsada=CONST(No),
                                                                                                N§ liquidacion GV=FILTER(<>0),
                                                                                                Facturada=CONST(No)));
                                                   CaptionML=[ENU=EXC - Tickets pending to invoice;
                                                              ESP=EXC - Reservas sin facturar];
                                                   Editable=No }
    { 31  ;   ;EXC - Banco tarjetas sin conc;Integer;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Count("Movs. Tarjeta Importados" WHERE (Fecha=FIELD(FILTER(Filtro Texto)),
                                                                                                       Tipo conciliaci¢n=CONST(" ")));
                                                   CaptionML=[ENU=EXC - Bank movements w/o reconcile;
                                                              ESP=EXC - Banco movimientos sin conciliar];
                                                   Editable=No }
    { 33  ;   ;EXC - Cobros sin conciliar;Integer ;FieldClass=FlowField;
                                                   CalcFormula=Count("Cobro reserva excursion" WHERE (Fecha venta=FIELD(FILTER(Filtro Texto)),
                                                                                                      Tipo pago=CONST(Tarjeta)));
                                                   CaptionML=[ENU=EXC - Payments w/o conciliate;
                                                              ESP=EXC - Cobros sin conciliar];
                                                   Editable=No }
    { 34  ;   ;RSV - Reservas sin generar;Integer ;FieldClass=FlowField;
                                                   CalcFormula=Count("Cabecera reserva" WHERE (Generada=CONST(No),
                                                                                               Fecha recepcion=FIELD(FILTER(Filtro Texto))));
                                                   CaptionML=[ENU=RSV - Non generated reservations;
                                                              ESP=RSV - Reservas sin generar];
                                                   Editable=No }
    { 36  ;   ;Transfers sin confirmar;Integer    ;FieldClass=FlowField;
                                                   CalcFormula=Count(Agrupacion WHERE (Fecha=FIELD(FILTER(Filtro Texto)),
                                                                                       Estado=CONST(Generado)));
                                                   CaptionML=[ENU=Transfers w/o confirm;
                                                              ESP=Transfers sin confirmar];
                                                   Editable=No }
    { 38  ;   ;Tipo periodo        ;Option        ;CaptionML=[ENU=Period Type;
                                                              ESP=Tipo periodo];
                                                   OptionCaptionML=[ENU=Day,Week;
                                                                    ESP=Dia,Semana];
                                                   OptionString=Dia,Semana }
    { 40  ;   ;Fecha hasta         ;Date          ;CaptionML=[ENU=Date to;
                                                              ESP=Fecha hasta] }
    { 44  ;   ;Filtro Texto        ;Text30        ;CaptionML=[ENU=Date Filter;
                                                              ESP=Filtro Fecha] }
  }
  KEYS
  {
    {    ;Tipo periodo,Fecha desde                ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE GenerarPeriodos@1000000002(pwEmpresa@1000000003 : Text[30]);
    VAR
      lrDay@1000000000 : Record 7009998;
      lrWeek@1000000001 : Record 7009998;
      lwFecha@1000000002 : Date;
    BEGIN
      // GenerarPeriodos

      // +$005
      IF pwEmpresa <> '' THEN BEGIN
        lrDay.CHANGECOMPANY(pwEmpresa);
        lrWeek.CHANGECOMPANY(pwEmpresa);
      END;

      FOR lwFecha := CALCDATE('<-90D>', TODAY) TO TODAY DO
      BEGIN
        IF NOT lrDay.GET(lrDay."Tipo periodo"::Dia, lwFecha) THEN
        BEGIN
          lrDay."Tipo periodo" := lrDay."Tipo periodo"::Dia;
          lrDay."Fecha desde"  := lwFecha;
          lrDay."Fecha hasta"  := lwFecha;
          lrDay."Filtro Texto" := FORMAT(lwFecha);
          lrDay.INSERT;
        END
        ELSE
        BEGIN
          lrDay."Filtro Texto" := FORMAT(lwFecha);
          lrDay.MODIFY;
        END;

        IF DATE2DWY(lwFecha, 1) = 1 THEN
        BEGIN
          IF NOT lrWeek.GET(lrWeek."Tipo periodo"::Semana, lwFecha) THEN
          BEGIN
            lrWeek."Tipo periodo" := lrWeek."Tipo periodo"::Semana;
            lrWeek."Fecha desde"  := lwFecha;
            lrWeek."Fecha hasta"  := lwFecha + 6;
            lrWeek."Filtro Texto" := FORMAT(lrWeek."Fecha desde") + '..' + FORMAT(lrWeek."Fecha hasta");
            lrWeek.INSERT;
          END
          ELSE
          BEGIN
            lrWeek."Filtro Texto" := FORMAT(lrWeek."Fecha desde") + '..' + FORMAT(lrWeek."Fecha hasta");
            lrWeek.MODIFY;
          END;
        END;
      END;
    END;

    LOCAL PROCEDURE _LookupTRFSinFra@1100217000();
    VAR
      lrIngreso@1100217000 : Record 7009805;
      lfConsulta@1100217001 : Form 7009943;
    BEGIN
      // _LookupTRFSinFra

      lrIngreso.RESET;
      lrIngreso.FILTERGROUP(2);
      lrIngreso.SETRANGE (Concepto    , 'TRNI');
      lrIngreso.SETFILTER(Fecha       , "Filtro Texto");
      lrIngreso.SETRANGE ("N§ Factura", '');
      lrIngreso.FILTERGROUP(0);
      IF lrIngreso.FINDFIRST THEN
      BEGIN
        CLEAR(lfConsulta);
        lfConsulta.SETTABLEVIEW(lrIngreso);
        lfConsulta.PasaParametros(1); //. Indicamos a la ventana que respete filtros
        lfConsulta.RUNMODAL;
      END;
    END;

    LOCAL PROCEDURE _LookupDiario@1000000000(pwSeccion@1000000000 : Code[20]);
    VAR
      rJournalLine@1000000001 : Record 81;
      fJournal@1000000002 : Form 39;
    BEGIN
      // _LookupDiario

      rJournalLine.RESET;
      rJournalLine.FILTERGROUP(2);
      rJournalLine.SETRANGE("Journal Template Name", 'GENERAL');
      rJournalLine.SETRANGE("Journal Batch Name"   , pwSeccion);
      rJournalLine.SETFILTER("Posting Date"        , "Filtro Texto");
      rJournalLine.FILTERGROUP(0);
      IF rJournalLine.FINDFIRST THEN
      BEGIN
        CLEAR(fJournal);
        fJournal.SETTABLEVIEW(rJournalLine);
        fJournal.RUNMODAL;
      END;
    END;

    PROCEDURE VerDetalleDiario@1000000001();
    VAR
      lrDia@1000000000 : Record 7009998;
      lfConsulta@1000000001 : Form 7009848;
    BEGIN
      // VerDetalleDiario

      lrDia.RESET;
      lrDia.SETRANGE ("Tipo periodo", "Tipo periodo"::Dia);
      lrDia.SETFILTER("Fecha desde" , "Filtro Texto");

      CLEAR(lfConsulta);
      lfConsulta.SETTABLEVIEW(lrDia);
      lfConsulta.RUNMODAL;
    END;

    BEGIN
    {
      $001 AJS 29082013 Actualizo los campos Llegadas sin vuelo, Llegadas sin servicio y Salidas sin servicio para que
                        no tengan en cuenta las reservas No Show

      $002 AJS 04102014 Nueva funcion para hacer lookup en los diarios

      $003 AJS 08102014 Nueva columna para ver los transfers sin confirmar

      $004 AJS 24122014 Agregar el nivel de semana al control de gesti¢n

      $005 ARM 12052015 A¤adimos changecompany para poder crear periodos en otras empresas
    }
    END.
  }
}
