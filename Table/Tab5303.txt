OBJECT Table 5303 Outlook Synch. Filter
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Filter Type;
    OnInsert=BEGIN
               TESTFIELD("Field Name");
               TESTFIELD("Field No.");
               VALIDATE(Value);
             END;

    OnModify=BEGIN
               VALIDATE(Value);
             END;

    CaptionML=[ENU=Outlook Synch. Filter;
               ESP=Filtro sinc. Outlook];
    PasteIsValid=No;
  }
  FIELDS
  {
    { 1   ;   ;Record GUID         ;GUID          ;CaptionML=[ENU=Record GUID;
                                                              ESP=GUID registro];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 2   ;   ;Filter Type         ;Option        ;CaptionML=[ENU=Filter Type;
                                                              ESP=Filtro tipo];
                                                   OptionCaptionML=[ENU=Condition,Table Relation;
                                                                    ESP=Condici¢n,Relaci¢n tablas];
                                                   OptionString=Condition,Table Relation }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ESP=N§ l¡nea] }
    { 4   ;   ;Table No.           ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   CaptionML=[ENU=Table No.;
                                                              ESP=N§ tabla] }
    { 5   ;   ;Field No.           ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table No.));
                                                   OnValidate=VAR
                                                                RecRef@1000 : RecordRef;
                                                              BEGIN
                                                                IF "Field No." = 0 THEN BEGIN
                                                                  CLEAR(RecRef);
                                                                  RecRef.OPEN("Table No.",TRUE);
                                                                  ERROR(Text005,RecRef.CAPTION);
                                                                END;

                                                                IF Field.GET("Table No.","Field No.") THEN
                                                                  VALIDATE("Field Name",Field."Field Caption");

                                                                IF "Field No." <> xRec."Field No." THEN
                                                                  IF Type <> Type::FIELD THEN
                                                                    Value := '';
                                                              END;

                                                   CaptionML=[ENU=Field No.;
                                                              ESP=N§ campo] }
    { 6   ;   ;Field Name          ;Text80        ;OnValidate=VAR
                                                                RecRef@1001 : RecordRef;
                                                                NameString@1000 : Text[80];
                                                              BEGIN
                                                                NameString := "Field Name";
                                                                IF NOT OSynchSetupMgt.ValidateFieldName(NameString,"Table No.") THEN BEGIN
                                                                  RecRef.OPEN("Table No.",TRUE);
                                                                  ERROR(Text005,RecRef.CAPTION);
                                                                END;

                                                                "Field Name" := NameString;
                                                              END;

                                                   OnLookup=VAR
                                                              FieldNo@1000 : Integer;
                                                            BEGIN
                                                              FieldNo := OSynchSetupMgt.ShowTableFieldsList("Table No.");
                                                              IF FieldNo > 0 THEN
                                                                VALIDATE("Field No.",FieldNo);
                                                            END;

                                                   CaptionML=[ENU=Field Name;
                                                              ESP=Nombre de campo] }
    { 7   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF ("Filter Type" = "Filter Type"::Condition) AND (Type = Type::FIELD) THEN
                                                                  ERROR(Text001,FORMAT(Type),FIELDCAPTION("Filter Type"),FORMAT("Filter Type"));

                                                                IF Type <> xRec.Type THEN
                                                                  Value := '';
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              ESP=Tipo];
                                                   OptionCaptionML=[ENU=CONST,FILTER,FIELD;
                                                                    ESP=CONST,FILTRO,CAMPO];
                                                   OptionString=CONST,FILTER,FIELD }
    { 8   ;   ;Value               ;Text250       ;OnValidate=BEGIN
                                                                ValidateFieldValuePair;
                                                              END;

                                                   OnLookup=VAR
                                                              OSynchTypeConversion@1003 : Codeunit 5302;
                                                              RecRef@1002 : RecordRef;
                                                              FldRef@1001 : FieldRef;
                                                              FieldNo@1000 : Integer;
                                                            BEGIN
                                                              IF Type <> Type::FIELD THEN BEGIN
                                                                RecRef.GETTABLE(Rec);
                                                                FldRef := RecRef.FIELD(Rec.FIELDNO(Type));
                                                                ERROR(Text003,FIELDCAPTION(Type),OSynchTypeConversion.GetSubStrByNo(Type::FIELD + 1,FldRef.OPTIONCAPTION));
                                                                RecRef.CLOSE;
                                                              END;

                                                              FieldNo := OSynchSetupMgt.ShowTableFieldsList("Master Table No.");

                                                              IF FieldNo <> 0 THEN
                                                                VALIDATE("Master Table Field No.",FieldNo);
                                                            END;

                                                   CaptionML=[ENU=Value;
                                                              ESP=Valor] }
    { 9   ;   ;Master Table No.    ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   CaptionML=[ENU=Master Table No.;
                                                              ESP=N§ tabla principal] }
    { 10  ;   ;Master Table Field No.;Integer     ;TableRelation=Field.No. WHERE (TableNo=FIELD(Master Table No.));
                                                   OnValidate=BEGIN
                                                                IF Field.GET("Master Table No.","Master Table Field No.") THEN
                                                                  VALIDATE(Value,Field."Field Caption");
                                                              END;

                                                   CaptionML=[ENU=Master Table Field No.;
                                                              ESP=N§ campo tabla principal] }
  }
  KEYS
  {
    {    ;Record GUID,Filter Type,Line No.        ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Field@1004 : Record 2000000041;
      OSynchSetupMgt@1005 : Codeunit 5300;
      OSynchTypeConversion@1006 : Codeunit 5302;
      Text001@1008 : TextConst 'ENU=You cannot select the %1 option when %2 is %3.;ESP=No puede seleccionar la opci¢n %1 cuando %2 es %3.';
      Text002@1007 : TextConst 'ENU=This value cannot be converted to the selected field datatype.;ESP=Este valor no se puede convertir al tipo de datos de campo seleccionado.';
      Text003@1003 : TextConst 'ENU=You can only open a lookup table when the %1 field contains the %2 value.;ESP=S¢lo puede abrir una tabla de b£squeda si el campo %1 contiene el valor %2.';
      Text004@1002 : TextConst 'ENU=This is not a valid option for the %1 field. The possible options are: ''%2''.;ESP=Esta opci¢n no es v lida para el campo %1. Las posibles opciones son: ''%2''.';
      Text005@1001 : TextConst 'ENU=Choose a valid field in the %1 table.;ESP=Elija un campo v lido en la tabla %1.';
      Text006@1009 : TextConst 'ENU=The value in this field cannot be longer than %1.;ESP=El valor de este campo no puede ser mayor que %1.';

    PROCEDURE SetTablesNo@1(TableLeftNo@1000 : Integer;TableRightNo@1001 : Integer);
    BEGIN
      "Table No." := TableLeftNo;
      "Master Table No." := TableRightNo;
    END;

    PROCEDURE ValidateFieldValuePair@2();
    VAR
      RecRef@1000 : RecordRef;
      FldRef@1001 : FieldRef;
      NameString@1004 : Text[250];
      TempBool@1003 : Boolean;
    BEGIN
      TESTFIELD("Table No.");

      CLEAR(RecRef);
      CLEAR(FldRef);
      RecRef.OPEN("Table No.",TRUE);

      IF "Field No." = 0 THEN
        ERROR(Text005,RecRef.CAPTION);

      Field.GET("Table No.","Field No.");
      FldRef := RecRef.FIELD("Field No.");

      CASE Type OF
        Type::CONST:
          BEGIN
            CASE Field.Type OF
              Field.Type::Option:
                IF NOT OSynchTypeConversion.EvaluateOptionField(FldRef,Value) THEN
                  ERROR(Text004,"Field Name",FldRef.OPTIONSTRING);

              Field.Type::Code,Field.Type::Text:
                BEGIN
                  IF STRLEN(Value) > Field.Len THEN
                    ERROR(Text006,Field.Len);
                  IF NOT EVALUATE(FldRef,Value) THEN
                    ERROR(Text002);
                END;

              Field.Type::Boolean:
                BEGIN
                  IF NOT EVALUATE(TempBool,Value) THEN
                    ERROR(Text002);
                  Value := FORMAT(TempBool);
                END;

              ELSE
                IF NOT EVALUATE(FldRef,Value) THEN
                  ERROR(Text002);
            END;
          END;
        Type::FILTER:
          BEGIN
            IF Field.Type = Field.Type::Option THEN BEGIN
              IF NOT OSynchTypeConversion.EvaluateFilterOptionField(FldRef,Value,FALSE) THEN
                ERROR(Text004,"Field Name",FldRef.OPTIONSTRING);
            END;
            FldRef.SETFILTER(Value);
          END;
        Type::FIELD:
          BEGIN
            NameString := Value;
            IF NOT OSynchSetupMgt.ValidateFieldName(NameString,"Master Table No.") THEN BEGIN
              RecRef.CLOSE;
              RecRef.OPEN("Master Table No.",TRUE);
              ERROR(Text005,RecRef.CAPTION);
            END;

            Value := NameString;
          END;
      END;
      RecRef.CLOSE;
    END;

    PROCEDURE RecomposeFilterExpression@3() FilterExpression : Text[250];
    BEGIN
      FilterExpression := OSynchSetupMgt.ComposeFilterExpression("Record GUID","Filter Type");
    END;

    PROCEDURE SuggestFieldName@4(VAR Text@1000 : Text[1024]);
    BEGIN
      IF "Table No." = 0 THEN
        EXIT;

      IF Text = '' THEN BEGIN
        "Field No." := 0;
        EXIT;
      END;

      Field.RESET;
      Field.SETRANGE(TableNo,"Table No.");
      Field.SETRANGE(Enabled,TRUE);
      Field.SETFILTER(Class,'<>%1',Field.Class::FlowFilter);
      Field.SETFILTER(FieldName,'@' + Text + '*');
      IF Field.FIND('-') THEN BEGIN
        Text := Field.FieldName;
        "Field No." := Field."No.";
        EXIT;
      END;
      "Field No." := 0;
    END;

    BEGIN
    END.
  }
}
