OBJECT Table 7010154 Precios Compra Guia Oficial
{
  OBJECT-PROPERTIES
  {
    Date=20/06/13;
    Time=14:57:45;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               rPar.FINDFIRST;
               IF NOT rPar."Empresa Delegacion" THEN
                 ERROR(Text001);

               rPre.RESET;
               rPre.SETRANGE(Delegacion   , Delegacion);
               rPre.SETRANGE("N§ Contrato", "N§ Contrato");
               rPre.SETRANGE("Tipo precio", "Tipo precio");

               IF rPre.FINDLAST THEN
                 "Orden precio" := rPre."Orden precio" + 1
               ELSE
                 "Orden precio" := 1;

               IF (Duracion = Duracion::" ") AND (Excursion = '') THEN
                 ERROR(Text002);
               IF rConC.GET(Delegacion, "N§ Contrato", rConC."Tipo Contrato"::"Guia Oficial") THEN
               BEGIN
                 "Cod. divisa" := rConC."Cod. divisa";
                 //+$001
                 "Fecha desde" := rConC."Fecha inicio contrato";
                 "Fecha hasta" := rConC."Fecha final contrato";
                 //+$001
               END;
             END;

    OnModify=BEGIN
               rPar.FINDFIRST;
               IF NOT rPar."Empresa Delegacion" THEN
                 ERROR(Text003);

               IF (Duracion = Duracion::" ") AND (Excursion = '') THEN
                 MESSAGE(Text002);

               IF "Fecha desde" = 0D THEN
                 MESSAGE(Text004);
               IF "Fecha hasta" = 0D THEN
                 MESSAGE(Text005);

               CASE "Tipo precio" OF
                 "Tipo precio"::"Precio Base": BEGIN
                   IF "Rango desde" = 0 THEN
                     MESSAGE(Text006);
                   IF "Rango hasta" = 0 THEN
                     MESSAGE(Text007);
                 END;
                 "Tipo precio"::Suplemento: BEGIN
                   IF "Codigo Suplemento" = '' THEN
                     MESSAGE(Text008);
                 END;
               END;
             END;

    OnDelete=BEGIN
               rPar.FINDFIRST;
               IF NOT rPar."Empresa Delegacion" THEN
                 ERROR(Text009);

               cPar.intro_historico_precios_guia_o(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio",
                                                   "Orden precio", Text010, Text011, '');
             END;

    OnRename=BEGIN
               rPar.FINDFIRST;
               IF NOT rPar."Empresa Delegacion" THEN
                 ERROR(Text012);
             END;

    CaptionML=[ENU=Offical guide purchase prices;
               ESP=Precios Compra Guia Oficial];
  }
  FIELDS
  {
    { 1   ;   ;Delegacion          ;Code10        ;TableRelation=Delegacion.Codigo;
                                                   CaptionML=[ENU=Local Office;
                                                              ESP=Delegacion];
                                                   Description=PK, FK Delegacion;
                                                   Editable=No }
    { 3   ;   ;N§ Contrato         ;Code10        ;CaptionML=[ENU=Contract No.;
                                                              ESP=N§ Contrato];
                                                   Description=PK;
                                                   Editable=No }
    { 5   ;   ;Tipo precio         ;Option        ;CaptionML=[ENU=Price Type;
                                                              ESP=Tipo precio];
                                                   OptionCaptionML=[ENU=Base Price,Supplement;
                                                                    ESP=Precio Base,Suplemento];
                                                   OptionString=Precio Base,Suplemento;
                                                   Description=PK;
                                                   Editable=No }
    { 7   ;   ;Orden precio        ;Integer       ;CaptionML=[ENU=Price order;
                                                              ESP=Orden Precio];
                                                   Description=PK;
                                                   Editable=No }
    { 9   ;   ;Fecha desde         ;Date          ;OnValidate=BEGIN
                                                                IF "Fecha desde" <> xRec."Fecha desde" THEN BEGIN
                                                                  IF "Fecha hasta" <> 0D THEN BEGIN
                                                                    IF "Fecha desde" > "Fecha hasta" THEN
                                                                      ERROR(Text013);
                                                                  END;

                                                                  VALIDATE(Activo, FALSE);

                                                                  cPar.intro_historico_precios_guia_o(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                      FIELDCAPTION("Fecha desde"),
                                                                                                      FORMAT(xRec."Fecha desde"), FORMAT("Fecha desde"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Date From;
                                                              ESP=Fecha desde] }
    { 11  ;   ;Fecha hasta         ;Date          ;OnValidate=BEGIN
                                                                IF "Fecha hasta" <> xRec."Fecha hasta" THEN BEGIN
                                                                  IF "Fecha desde" <> 0D THEN BEGIN
                                                                    IF "Fecha desde" > "Fecha hasta" THEN
                                                                      ERROR(Text013);
                                                                  END;
                                                                  VALIDATE(Activo, FALSE);

                                                                  cPar.intro_historico_precios_guia_o(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                      FIELDCAPTION("Fecha hasta"),
                                                                                                      FORMAT(xRec."Fecha hasta"), FORMAT("Fecha hasta"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Date To;
                                                              ESP=Fecha hasta] }
    { 14  ;   ;Precio              ;Decimal       ;OnValidate=BEGIN
                                                                IF Precio <> xRec.Precio THEN
                                                                  cPar.intro_historico_precios_guia_o(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                      FIELDCAPTION(Precio),
                                                                                                      FORMAT(xRec.Precio), FORMAT(Precio));

                                                                //-$005IF rConC.GET(Delegacion, "N§ Contrato", rConC."Tipo Contrato"::"Guia Oficial") THEN BEGIN
                                                                //-$005  IF "Cod. divisa" <> '' THEN BEGIN
                                                                //-$005    IF rConC."Fecha firma contrato" = 0D THEN
                                                                //-$005      "Precio (DL)" := 0
                                                                //-$005    ELSE
                                                                //-$005      "Precio (DL)" := rTipoCambioDivisa.ExchangeAmtFCYToLCY(rConC."Fecha firma contrato",
                                                                //-$005                                                        "Cod. divisa",
                                                                //-$005                                                        Precio,
                                                                //-$005                                     rTipoCambioDivisa.ExchangeRate(rConC."Fecha firma contrato", "Cod. divisa"));
                                                                //-$005  END
                                                                //-$005  ELSE
                                                                //-$005    "Precio (DL)" := Precio;
                                                                //-$005END
                                                                //-$005ELSE
                                                                //-$005  "Precio (DL)" := 0;
                                                              END;

                                                   CaptionML=[ENU=Price;
                                                              ESP=Precio];
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Cod. divisa" }
    { 15  ;   ;o_Precio (DL)       ;Decimal       ;CaptionML=[ENU=Price (LCY);
                                                              ESP=Precio (DL)];
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 17  ;   ;Tipo Aplicacion     ;Option        ;OnValidate=BEGIN
                                                                IF "Tipo Aplicacion" <> xRec."Tipo Aplicacion" THEN
                                                                BEGIN
                                                                  _CheckCosteTicket; //+$003

                                                                  cPar.intro_historico_precios_guia_o(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                      FIELDCAPTION("Tipo Aplicacion"),
                                                                                                      FORMAT(xRec."Tipo Aplicacion"), FORMAT("Tipo Aplicacion"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Application type;
                                                              ESP=Tipo Aplicacion];
                                                   OptionCaptionML=[ENU=,Service,Pax;
                                                                    ESP=,Servicio,Pax];
                                                   OptionString=,Servicio,Pax }
    { 19  ;   ;Cod. divisa         ;Code10        ;TableRelation=Currency.Code;
                                                   OnValidate=BEGIN
                                                                IF "Cod. divisa" <> xRec."Cod. divisa" THEN
                                                                  cPar.intro_historico_precios_guia_o(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                      FIELDCAPTION("Cod. divisa"),
                                                                                                      xRec."Cod. divisa", "Cod. divisa");
                                                              END;

                                                   CaptionML=[ENU=Currency code;
                                                              ESP=C¢d. divisa];
                                                   Description=FK Divisa;
                                                   Editable=No }
    { 23  ;   ;Rango desde         ;Integer       ;OnValidate=BEGIN
                                                                IF "Rango desde" <> xRec."Rango desde" THEN BEGIN
                                                                  IF "Rango hasta" <> 0 THEN
                                                                     IF "Rango desde" > "Rango hasta" THEN
                                                                       ERROR(Text014);

                                                                  VALIDATE(Activo, FALSE);

                                                                  cPar.intro_historico_precios_guia_o(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                      FIELDCAPTION("Rango desde"),
                                                                                                      FORMAT(xRec."Rango desde"), FORMAT("Rango desde"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Range From;
                                                              ESP=Rango desde] }
    { 25  ;   ;Rango hasta         ;Integer       ;OnValidate=BEGIN
                                                                IF "Rango hasta" <> xRec."Rango hasta" THEN BEGIN
                                                                  IF "Rango desde" <> 0 THEN
                                                                     IF "Rango desde" > "Rango hasta" THEN
                                                                       ERROR(Text014);

                                                                  VALIDATE(Activo, FALSE);

                                                                  cPar.intro_historico_precios_guia_o(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                      FIELDCAPTION("Rango hasta"),
                                                                                                      FORMAT(xRec."Rango hasta"), FORMAT("Rango hasta"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Range To;
                                                              ESP=Rango hasta] }
    { 35  ;   ;Activo              ;Boolean       ;OnValidate=BEGIN
                                                                Comprobar_Contrato;
                                                                Activo := Activar_Precio;
                                                              END;

                                                   CaptionML=[ENU=Active;
                                                              ESP=Activo];
                                                   Editable=No }
    { 36  ;   ;Precio Adulto       ;Decimal       ;OnValidate=BEGIN
                                                                IF "Precio Adulto" <> xRec."Precio Adulto" THEN
                                                                  cPar.intro_historico_precios_guia_o(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                      FIELDCAPTION("Precio Adulto"),
                                                                                                      FORMAT(xRec."Precio Adulto"), FORMAT("Precio Adulto"));

                                                                //-$005IF rConC.GET(Delegacion, "N§ Contrato", rConC."Tipo Contrato"::"Guia Oficial") THEN BEGIN
                                                                //-$005  IF "Cod. divisa" <> '' THEN BEGIN
                                                                //-$005    IF rConC."Fecha firma contrato" = 0D THEN
                                                                //-$005      "Precio Adulto (DL)" := 0
                                                                //-$005    ELSE
                                                                //-$005      "Precio Adulto (DL)" := rTipoCambioDivisa.ExchangeAmtFCYToLCY(rConC."Fecha firma contrato",
                                                                //-$005                                                        "Cod. divisa",
                                                                //-$005                                                        "Precio Adulto",
                                                                //-$005                                             rTipoCambioDivisa.ExchangeRate(rConC."Fecha firma contrato", "Cod. divisa"));
                                                                //-$005  END
                                                                //-$005  ELSE
                                                                //-$005    "Precio Adulto (DL)" := "Precio Adulto";
                                                                //-$005END
                                                                //-$005ELSE
                                                                //-$005  "Precio Adulto (DL)" := 0;
                                                              END;

                                                   CaptionML=[ENU=Adult price;
                                                              ESP=Precio Adulto];
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Cod. divisa" }
    { 37  ;   ;o_Precio Adulto (DL);Decimal       ;CaptionML=[ENU=Adult price (LCY);
                                                              ESP=Precio Adulto (DL)];
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 38  ;   ;Precio Ni¤o         ;Decimal       ;OnValidate=BEGIN
                                                                IF "Precio Ni¤o" <> xRec."Precio Ni¤o" THEN
                                                                  cPar.intro_historico_precios_guia_o(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                      FIELDCAPTION("Precio Ni¤o"),
                                                                                                      FORMAT(xRec."Precio Ni¤o"), FORMAT("Precio Ni¤o"));

                                                                //-$005IF rConC.GET(Delegacion, "N§ Contrato", rConC."Tipo Contrato"::"Guia Oficial") THEN BEGIN
                                                                //-$005  IF "Cod. divisa" <> '' THEN BEGIN
                                                                //-$005    IF rConC."Fecha firma contrato" = 0D THEN
                                                                //-$005      "Precio Ni¤o (DL)" := 0
                                                                //-$005    ELSE
                                                                //-$005      "Precio Ni¤o (DL)" := rTipoCambioDivisa.ExchangeAmtFCYToLCY(rConC."Fecha firma contrato",
                                                                //-$005                                                        "Cod. divisa",
                                                                //-$005                                                        "Precio Ni¤o",
                                                                //-$005                                         rTipoCambioDivisa.ExchangeRate(rConC."Fecha firma contrato", "Cod. divisa"));
                                                                //-$005  END
                                                                //-$005  ELSE
                                                                //-$005    "Precio Ni¤o (DL)" := "Precio Ni¤o";
                                                                //-$005END
                                                                //-$005ELSE
                                                                //-$005  "Precio Ni¤o (DL)" := 0;
                                                              END;

                                                   CaptionML=[ENU=Children price;
                                                              ESP=Precio Ni¤o];
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Cod. divisa" }
    { 39  ;   ;o_Precio Ni¤o (DL)  ;Decimal       ;CaptionML=[ENU=Child Price (LCY);
                                                              ESP=Precio Ni¤o (DL)];
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 41  ;   ;% Gastos Anulacion  ;Decimal       ;OnValidate=BEGIN
                                                                IF "% Gastos Anulacion" <> xRec."% Gastos Anulacion" THEN
                                                                  cPar.intro_historico_precios_guia_o(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                      FIELDCAPTION("% Gastos Anulacion"),
                                                                                                      FORMAT(xRec."% Gastos Anulacion"), FORMAT("% Gastos Anulacion"));
                                                              END;

                                                   CaptionML=[ENU=% Cancelation expenses;
                                                              ESP=% Gastos Anulacion];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Cod. divisa" }
    { 43  ;   ;Duracion            ;Option        ;OnValidate=BEGIN
                                                                IF Duracion <> xRec.Duracion THEN BEGIN
                                                                  VALIDATE(Activo, FALSE);
                                                                  cPar.intro_historico_precios_guia_o(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                      FIELDCAPTION(Duracion),
                                                                                                      FORMAT(xRec.Duracion), FORMAT(Duracion));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Duration;
                                                              ESP=Duracion];
                                                   OptionCaptionML=[ENU="Full day,Half day,Night,Long duration, ";
                                                                    ESP="Dia Entero,Medio Dia,Nocturna,Larga Duracion, "];
                                                   OptionString=[Dia Entero,Medio Dia,Nocturna,Larga Duracion, ] }
    { 45  ;   ;Codigo Suplemento   ;Code10        ;TableRelation="Suplemento Guia Oficial"."Codigo Suplemento";
                                                   OnValidate=VAR
                                                                rSuplemento@1103355000 : Record 7010145;
                                                              BEGIN
                                                                IF NOT rSuplemento.GET("Codigo Suplemento") THEN
                                                                  ERROR(Text008);

                                                                IF "Codigo Suplemento" <> xRec."Codigo Suplemento" THEN BEGIN
                                                                  VALIDATE(Activo, FALSE);
                                                                  cPar.intro_historico_precios_guia_o(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                      FIELDCAPTION("Codigo Suplemento"),
                                                                                                      xRec."Codigo Suplemento", "Codigo Suplemento");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Surcharge code;
                                                              ESP=Codigo Suplemento];
                                                   NotBlank=Yes;
                                                   Description=FK Suplemento Guia Oficial }
    { 47  ;   ;Excursion           ;Code10        ;TableRelation="Producto agencia".Codigo WHERE (Tipo Producto=CONST(Excursion),
                                                                                                  Caducado=CONST(No));
                                                   OnValidate=BEGIN
                                                                // $002
                                                                cFunExc.ControlOficinaExcursion(Excursion, TRUE);

                                                                IF Excursion <> xRec.Excursion THEN BEGIN
                                                                  VALIDATE(Activo, FALSE);
                                                                  cPar.intro_historico_precios_guia_o(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                      FIELDCAPTION(Excursion),
                                                                                                      xRec.Excursion, Excursion);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Excursion;
                                                              ESP=Excursion];
                                                   Description=FK Producto Agencia }
    { 49  ;   ;Zona                ;Code10        ;TableRelation="Zona fisica"."Zona Fisica";
                                                   OnValidate=BEGIN
                                                                IF Zona <> xRec.Zona THEN BEGIN
                                                                  VALIDATE(Activo, FALSE);
                                                                  cPar.intro_historico_precios_guia_o(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                      FIELDCAPTION(Zona),
                                                                                                      xRec.Zona, Zona);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Location;
                                                              ESP=Zona];
                                                   Description=FK Zona Fisica }
  }
  KEYS
  {
    {    ;Delegacion,N§ Contrato,Tipo precio,Orden precio;
                                                   Clustered=Yes }
    {    ;Excursion,Duracion,Codigo Suplemento,Zona,Activo,Fecha desde,Fecha hasta,Rango desde,Rango hasta;
                                                   KeyGroups=RENUMERING }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1103355007 : TextConst 'ENU=To sign up purchase prices of transfer is only available  in the local offices;ESP=Solo es posible dar de alta precios de compra de transfer en las Delegaciones.';
      Text002@1103355008 : TextConst 'ENU=Must indicate length and/or excursion;ESP=Debe indicarse duracion i/o excursion';
      Text003@1103355009 : TextConst 'ENU=To modify purchase prices of transfer is only available  in the local offices;ESP=Solo es posible modificar precios de compra de transfer en las Delegaciones.';
      Text004@1103355010 : TextConst 'ENU=Indicate a correct date from;ESP=Indique una fecha desde correcta';
      Text005@1103355011 : TextConst 'ENU=Indicate a correct to;ESP=Indique una fecha hasta correcta';
      Text006@1103355012 : TextConst 'ENU=Indicate a range from higher than 0;ESP=Indique un rango desde mayor que 0';
      Text007@1103355013 : TextConst 'ENU=Indicate a range to higher than 0;ESP=Indique un rango hasta mayor que 0';
      Text008@1103355014 : TextConst 'ENU=Indicate a correct surcharge code;ESP=Indique un codigo suplemento correcto';
      Text009@1103355015 : TextConst 'ENU=To cancel purchase prices of transfer is only available  in the local offices;ESP=Solo es posible dar de baja precios de compra de transfer en las Delegaciones.';
      Text010@1103355016 : TextConst 'ENU=Deleted;ESP=Borrado';
      Text011@1103355017 : TextConst 'ENU=Register deleted;ESP=Registro Borrado';
      Text012@1103355018 : TextConst 'ENU=Only is possible to rename transfer purchase prices in the Local Offices;ESP=Solo es posible renombrar precios de compra de transfer en las Delegaciones.';
      Text013@1103355019 : TextConst 'ENU=Date from cannot be higher than date to;ESP=La fecha desde no puede ser posterior a la fecha hasta.';
      Text014@1103355020 : TextConst 'ENU=Range from cannot be higher than range to;ESP=El rango desde no puede ser mayor que el rango hasta.';
      Text015@1103355021 : TextConst 'ENU=Date from is not within the period of the contract;ESP=La fecha desde no esta entre el periodo del contrato';
      Text016@1103355022 : TextConst 'ENU=Date to is not within the period of the contract;ESP=La fecha hasta no esta entre el periodo del contrato';
      Text017@1103355023 : TextConst 'ENU=This price overlaps with another entered price;ESP=Este precio se solapa con otro precio ya introducido.';
      Text018@1103355024 : TextConst 'ENU=Contract does not exist for this register;ESP=No existe el contrato para este registro';
      rPar@1103355000 : Record 7009700;
      rPre@1103355001 : Record 7010154;
      cPar@1103355002 : Codeunit 7009700;
      cFunExc@1100217000 : Codeunit 7010141;
      rHisP@1103355003 : Record 7010159;
      rConC@1103355004 : Record 7010142;
      rTipoCambioDivisa@1103355005 : Record 330;
      Text023@1100217001 : TextConst 'ENU=You cannot indicate price per service because the excursion have cost per ticket.;ESP=No se puede indicar precio por servicio porque la excursion se costea por ticket.';

    PROCEDURE Activar_Precio@2() : Boolean;
    VAR
      rConC@1103355000 : Record 7010142;
      rPrecio@1103355001 : Record 7010154;
    BEGIN
      IF rConC.GET(Delegacion,"N§ Contrato", rConC."Tipo Contrato"::"Guia Oficial") THEN BEGIN
        IF NOT rConC.Confirmado THEN
          EXIT(FALSE);
        IF ("Fecha desde" = 0D) OR ("Fecha hasta" = 0D) THEN
          EXIT(FALSE);
        IF ("Fecha desde" < rConC."Fecha inicio contrato") OR
             ("Fecha desde" > rConC."Fecha final contrato") THEN BEGIN
          MESSAGE(Text015);
          EXIT(FALSE);
        END;

        IF ("Fecha hasta" < rConC."Fecha inicio contrato") OR
             ("Fecha hasta" > rConC."Fecha final contrato") THEN BEGIN
          MESSAGE(Text016);
          EXIT(FALSE);
        END;
      END;

      _CheckCosteTicket; //+$003

      rPrecio.RESET;
      rPrecio.SETCURRENTKEY(Excursion, Duracion, "Codigo Suplemento", Zona, Activo,
                            "Fecha desde", "Fecha hasta", "Rango desde", "Rango hasta");
      rPrecio.SETRANGE (Delegacion   , Delegacion);
      rPrecio.SETRANGE ("N§ Contrato", "N§ Contrato");
      rPrecio.SETRANGE ("Tipo precio", "Tipo precio");
      rPrecio.SETRANGE (Excursion    , Excursion);
      rPrecio.SETRANGE (Duracion     , Duracion);

      IF"Tipo precio" ="Tipo precio"::Suplemento THEN BEGIN
        rPrecio.SETRANGE ("Codigo Suplemento", "Codigo Suplemento");
        rPrecio.SETRANGE (Zona               , Zona);
      END;
      rPrecio.SETFILTER("Orden precio" , '<>%1', "Orden precio");
      rPrecio.SETRANGE (Activo         , TRUE);

      rPrecio.SETFILTER("Fecha desde"  , '<=%1', "Fecha desde");
      rPrecio.SETFILTER("Fecha hasta"  , '>=%1', "Fecha desde");
      IF rPrecio.FINDFIRST THEN BEGIN
        rPrecio.SETFILTER("Rango desde", '<=%1', "Rango desde");
        rPrecio.SETFILTER("Rango hasta", '>=%1', "Rango desde");
        IF rPrecio.FINDFIRST THEN BEGIN
          MESSAGE(Text017);
          EXIT(FALSE);
        END;

        rPrecio.SETFILTER("Rango desde", '<=%1', "Rango hasta");
        rPrecio.SETFILTER("Rango hasta", '>=%1', "Rango hasta");
        IF rPrecio.FINDFIRST THEN BEGIN
          MESSAGE(Text017);
          EXIT(FALSE);
        END;

        rPrecio.SETFILTER("Rango desde", '>=%1', "Rango desde");
        rPrecio.SETFILTER("Rango hasta", '<=%1', "Rango hasta");
        IF rPrecio.FINDFIRST THEN BEGIN
          MESSAGE(Text017);
          EXIT(FALSE);
        END;
      END;

      rPrecio.SETFILTER("Fecha desde"  , '<=%1', "Fecha hasta");
      rPrecio.SETFILTER("Fecha hasta"  , '>=%1', "Fecha hasta");
      IF rPrecio.FINDFIRST THEN BEGIN
        rPrecio.SETFILTER("Rango desde", '<=%1', "Rango desde");
        rPrecio.SETFILTER("Rango hasta", '>=%1', "Rango desde");
        IF rPrecio.FINDFIRST THEN BEGIN
          MESSAGE(Text017);
          EXIT(FALSE);
        END;

        rPrecio.SETFILTER("Rango desde", '<=%1', "Rango hasta");
        rPrecio.SETFILTER("Rango hasta", '>=%1', "Rango hasta");
        IF rPrecio.FINDFIRST THEN BEGIN
          MESSAGE(Text017);
          EXIT(FALSE);
        END;

        rPrecio.SETFILTER("Rango desde", '>=%1', "Rango desde");
        rPrecio.SETFILTER("Rango hasta", '<=%1', "Rango hasta");
        IF rPrecio.FINDFIRST THEN BEGIN
          MESSAGE(Text017);
          EXIT(FALSE);
        END;
      END;

      rPrecio.SETFILTER("Fecha desde"  , '>=%1', "Fecha desde");
      rPrecio.SETFILTER("Fecha hasta"  , '<=%1', "Fecha hasta");
      IF rPrecio.FINDFIRST THEN BEGIN
        rPrecio.SETFILTER("Rango desde", '<=%1', "Rango desde");
        rPrecio.SETFILTER("Rango hasta", '>=%1', "Rango desde");
        IF rPrecio.FINDFIRST THEN BEGIN
          MESSAGE(Text017);
          EXIT(FALSE);
        END;

        rPrecio.SETFILTER("Rango desde", '<=%1', "Rango hasta");
        rPrecio.SETFILTER("Rango hasta", '>=%1', "Rango hasta");
        IF rPrecio.FINDFIRST THEN BEGIN
          MESSAGE(Text017);
          EXIT(FALSE);
        END;

        rPrecio.SETFILTER("Rango desde", '>=%1', "Rango desde");
        rPrecio.SETFILTER("Rango hasta", '<=%1', "Rango hasta");
        IF rPrecio.FINDFIRST THEN BEGIN
          MESSAGE(Text017);
          EXIT(FALSE);
        END;
      END;

      CASE "Tipo precio" OF
        "Tipo precio"::"Precio Base":
          IF ("Fecha desde" = 0D) OR ("Fecha hasta" = 0D) OR ("Rango desde" = 0) OR ("Rango hasta" = 0) OR
             ((Excursion = '') AND (Duracion = Duracion::" ")) THEN
            EXIT(FALSE);
        "Tipo precio"::Suplemento:
          IF ("Codigo Suplemento" = '') OR ("Fecha desde" = 0D) OR ("Fecha hasta" = 0D) OR
             ((Excursion = '') AND (Duracion = Duracion::" ")) THEN
            EXIT(FALSE);
      END;

      EXIT(TRUE);
    END;

    PROCEDURE Comprobar_Contrato@15();
    VAR
      rContrato@1103355000 : Record 7010142;
    BEGIN
      IF NOT rContrato.GET(Delegacion, "N§ Contrato", rContrato."Tipo Contrato"::"Guia Oficial") THEN
        ERROR(Text018);

      IF "Fecha desde" <> 0D THEN
        IF ("Fecha desde" < rContrato."Fecha inicio contrato") OR
           ("Fecha desde" > rContrato."Fecha final contrato") THEN
          MESSAGE(Text015);

      IF "Fecha hasta" <> 0D THEN
        IF ("Fecha hasta" < rContrato."Fecha inicio contrato") OR
           ("Fecha hasta" > rContrato."Fecha final contrato") THEN
          MESSAGE(Text016);
    END;

    PROCEDURE Descripcion_Codigo@1() : Text[30];
    VAR
      rSuplemento@1103355000 : Record 7010145;
    BEGIN
      IF NOT rSuplemento.GET("Codigo Suplemento") THEN
        EXIT('');
      EXIT(rSuplemento.Descripcion);
    END;

    LOCAL PROCEDURE _CheckCosteTicket@1100217000();
    VAR
      lrExc@1100217000 : Record 7009710;
    BEGIN
      IF "Tipo Aplicacion" = "Tipo Aplicacion"::Pax THEN
        EXIT;

      lrExc.GET(Excursion);
      IF lrExc."Coste x ticket" THEN
        ERROR(Text023);
    END;

    BEGIN
    {
      $001 AJS 31052012 Iniciar las fechas con el periodo del contrato

      $002 JPT 08/08/12 CNT-OC-12004 Incluir filtro por oficina en las excursiones. Control de Oficina en Excursion

      $003 AJS 11062013 Nueva funcion _CheckCosteTicket para evitar que se introduzcan precios por servicio en
                        excursiones que se calculan por ticket

      $004 AJS 17062013 Cambio de nombre en los campos de Codigo precio y Tipo precio

      $005 AJS 20062013 Los campos de precio en divisa local son obsoletos
    }
    END.
  }
}
