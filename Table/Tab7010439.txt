OBJECT Table 7010439 Conversiones Exportacion
{
  OBJECT-PROPERTIES
  {
    Date=16/10/09;
    Time=12:30:46;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    OnInsert=BEGIN

               rConv.RESET;
               rConv.SETRANGE(Tipo,Tipo);
               IF rConv.FINDLAST THEN
                 No := rConv.No +1
               ELSE
                 No :=1;
             END;

    CaptionML=ESP=Conversiones Exportacion;
    LookupFormID=Form7035300;
    DrillDownFormID=Form7035300;
  }
  FIELDS
  {
    { 1   ;   ;No                  ;Integer       ;CaptionML=ESP=No }
    { 3   ;   ;Tipo                ;Option        ;CaptionML=ESP=Tipo;
                                                   OptionCaptionML=ESP=Sucursal,Departamento;
                                                   OptionString=Sucursal,Departamento }
    { 4   ;   ;Codigo              ;Code10        ;OnValidate=VAR
                                                                lwMax@1100244000 : Integer;
                                                                lwN@1100244001 : Integer;
                                                                lwChar@1100244002 : Char;
                                                              BEGIN
                                                                CompDupl;

                                                                lwMax:=Max;
                                                                IF STRLEN(Codigo)>lwMax THEN BEGIN
                                                                  Codigo:= xRec.Codigo;
                                                                  MESSAGE(Text0002,Tipo,lwMax);
                                                                END;

                                                                CASE Tipo OF
                                                                  Tipo::Sucursal,Tipo::Departamento: BEGIN
                                                                    FOR lwN:=1 TO STRLEN(Codigo) DO
                                                                      IF NOT (Codigo[lwN] IN ['0'..'9']) THEN
                                                                        ERROR(Text0003);
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=ESP=Codigo;
                                                   NotBlank=Yes }
    { 5   ;   ;Codigo FinBus       ;Code20        ;TableRelation=IF (Tipo=CONST(Sucursal)) "Dimension Value".Code WHERE (Global Dimension No.=CONST(1))
                                                                 ELSE IF (Tipo=CONST(Departamento)) "Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Codigo);
                                                              END;

                                                   CaptionML=ESP=Codigo FinBus }
  }
  KEYS
  {
    {    ;Tipo,No                                 ;Clustered=Yes }
    {    ;Codigo,Tipo,No                           }
    {    ;Codigo FinBus,Tipo                       }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      rConv@1100244000 : Record 7010439;
      Text0001@1100244001 : TextConst 'ESP=Ya existe el codigo %1';
      Text0002@1100244002 : TextConst 'ESP=El Codigo %1 no puede tener m s de %2 Caracteres';
      Text0003@1100244003 : TextConst 'ESP=Solo deben de introducirse caracteres numericos';

    PROCEDURE TextoFinBus@1100244000() : Text[30];
    VAR
      lwTexto@1100244001 : Text[30];
      lrDimVal@1100244000 : Record 349;
    BEGIN
      // TextoFinBus
      // Devuelve el texto del codigo Finbus seleccionado

      CASE Tipo OF
        Tipo::Sucursal: BEGIN
          lrDimVal.RESET;
          lrDimVal.SETCURRENTKEY(Code, "Global Dimension No.");
          lrDimVal.SETRANGE(Code , "Codigo FinBus");
          lrDimVal.SETRANGE("Global Dimension No.", 1);
          IF lrDimVal.FINDFIRST THEN
            lwTexto := lrDimVal.Name;
        END;

        Tipo::Departamento: BEGIN
          lrDimVal.RESET;
          lrDimVal.SETCURRENTKEY(Code, "Global Dimension No.");
          lrDimVal.SETRANGE(Code , "Codigo FinBus");
          lrDimVal.SETRANGE("Global Dimension No.", 2);
          IF lrDimVal.FINDFIRST THEN
            lwTexto := lrDimVal.Name;
        END;
      END;
      EXIT(lwTexto);
    END;

    PROCEDURE CompDupl@1100244001();
    BEGIN
      // CompDupl
      // Comprueba que el codigo no se duplique

      rConv.RESET;
      rConv.SETCURRENTKEY(Codigo,Tipo,No);
      rConv.SETRANGE(Tipo,Tipo);
      rConv.SETFILTER(Codigo,'%1',Codigo);
      rConv.SETFILTER(No,'<>%1',No);
      IF rConv.FINDFIRST THEN
        ERROR(Text0001,Codigo);
    END;

    PROCEDURE Max@1100244002() : Integer;
    VAR
      lwMax@1100244000 : Integer;
    BEGIN
      // Max
      // Devuelve el largo maximo seg£n el tipo
      CASE Tipo OF
        Tipo::Sucursal           :lwMax:=4;
        Tipo::Departamento       :lwMax:=4;
      END;

      EXIT(lwMax);
    END;

    BEGIN
    END.
  }
}
