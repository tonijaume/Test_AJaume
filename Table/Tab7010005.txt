OBJECT Table 7010005 Anticipo/Prestamo
{
  OBJECT-PROPERTIES
  {
    Date=22/07/14;
    Time=14:04:24;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF NOT rPar.FIND('-') THEN
                 ERROR(Text000);
               IF NOT rPar."Empresa Central" THEN
                 ERROR(Text001+
                       Text002);
               "Tipo Anticipo" := "Tipo Anticipo"::"A pagar por agencia(Interm)";
             END;

    OnModify=BEGIN
               IF NOT rPar.FIND('-') THEN
                 ERROR(Text000);
               IF NOT rPar."Empresa Central" THEN
                 ERROR(Text003+
                       Text002);
               TESTFIELD(TourOperador);
               TESTFIELD(Delegacion);
             END;

    OnDelete=VAR
               rRecuperacions@1103355000 : Record 7010006;
             BEGIN
               IF NOT rPar.FIND('-') THEN
                 ERROR(Text000);
               IF NOT rPar."Empresa Central" THEN
                 ERROR(Text004+
                       Text002);

               IF "Pasado ABR" THEN
                 ERROR(Text005);

               // borrar todas las recuperaciones que hab¡a de ese anticipo/pr‚stamo

               rRecuperacions.SETRANGE(Tipo, Tipo);
               rRecuperacions.SETRANGE(Hotel, Hotel);
               rRecuperacions.SETRANGE(Contrato, Contrato);
               rRecuperacions.SETRANGE("Fecha concesion", "Fecha concesion");

               IF rRecuperacions.FIND('-') THEN
                   rRecuperacions.DELETEALL;

               rRecuperacions.SETRANGE(Tipo);
               rRecuperacions.SETRANGE(Hotel);
               rRecuperacions.SETRANGE(Contrato);
               rRecuperacions.SETRANGE("Fecha concesion");
             END;

    OnRename=BEGIN
               IF NOT rPar.FIND('-') THEN
                 ERROR(Text000);
               IF NOT rPar."Empresa Central" THEN
                 ERROR(Text006+
                       Text002);

               IF "Pasado ABR" THEN
                 IF NOT CONFIRM(Text007) THEN
                   EXIT;

               rRec.RESET;
               rRec.SETRANGE(Tipo             , Tipo);
               rRec.SETRANGE(Hotel            , Hotel);
               rRec.SETRANGE(Contrato         , Contrato);
               rRec.SETRANGE("Fecha concesion", "Fecha concesion");
               IF rRec.FIND('-') THEN BEGIN
                 CASE Tipo OF
                   Tipo::Anticipo:
                   BEGIN
                     ERROR(Text008+
                           Text009);
                   END;
                   Tipo::Prestamo:
                   BEGIN
                     ERROR(Text010+
                           Text009);
                   END;
                 END;
               END;
             END;

    CaptionML=[ENU=Advance payment/Loan;
               ESP=Anticipo/Prestamo];
    LookupFormID=Form7010053;
    DrillDownFormID=Form7010053;
  }
  FIELDS
  {
    { 1   ;   ;Tipo                ;Option        ;CaptionML=[ENU=Type;
                                                              ESP=Tipo];
                                                   OptionCaptionML=[ENU=Advance,Loan;
                                                                    ESP=Anticipo,Prestamo];
                                                   OptionString=Anticipo,Prestamo;
                                                   Description=PK }
    { 3   ;   ;Hotel               ;Code20        ;TableRelation=Hotel.Hotel;
                                                   CaptionML=[ENU=Hotel;
                                                              ESP=Hotel];
                                                   NotBlank=Yes;
                                                   Description=PK, FK Hotel }
    { 5   ;   ;Contrato            ;Code10        ;OnLookup=BEGIN
                                                              rCon.SETCURRENTKEY(Hotel);
                                                              rCon.SETRANGE(Hotel, Hotel);
                                                              CLEAR(fTabCon);
                                                              fTabCon.EsCentral(TRUE);
                                                              fTabCon.SETTABLEVIEW(rCon);
                                                              fTabCon.LOOKUPMODE(TRUE);
                                                              IF fTabCon.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                fTabCon.GETRECORD(rCon);
                                                                Contrato := rCon."N§ Contrato compra";
                                                              END;
                                                              rCon.RESET;
                                                            END;

                                                   CaptionML=[ENU=Contract;
                                                              ESP=Contrato];
                                                   NotBlank=Yes;
                                                   Description=PK }
    { 7   ;   ;Fecha concesion     ;Date          ;CaptionML=[ENU=Granting date;
                                                              ESP=Fecha concesion];
                                                   NotBlank=Yes;
                                                   Description=PK }
    { 9   ;   ;Descripcion         ;Text60        ;CaptionML=[ENU=Description;
                                                              ESP=Descripcion] }
    { 11  ;   ;Tipo Anticipo       ;Option        ;CaptionML=[ENU=Advance type;
                                                              ESP=Tipo Anticipo];
                                                   OptionCaptionML=[ENU=Directo TO (Interm),To pay per Agency (interm),On its own behalf;
                                                                    ESP=Directo TO (Interm),A pagar por agencia(Interm),Por cuenta propia];
                                                   OptionString=Directo TO (Interm),A pagar por agencia(Interm),Por cuenta propia;
                                                   NotBlank=Yes }
    { 13  ;   ;Tipo Prestamo       ;Option        ;CaptionML=[ENU=Loan type;
                                                              ESP=Tipo Prestamo];
                                                   OptionCaptionML=[ENU=Intermediation,Own behalf;
                                                                    ESP=Intermediacion,Cuenta Propia];
                                                   OptionString=Intermediacion,Cuenta Propia }
    { 15  ;   ;Importe             ;Decimal       ;CaptionML=[ENU=Amount;
                                                              ESP=Importe];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 17  ;   ;Fecha Pago          ;Date          ;CaptionML=[ENU=Payment date;
                                                              ESP=Fecha pago] }
    { 19  ;   ;Forma recuperacion  ;Option        ;OnValidate=BEGIN
                                                                rRec.RESET;
                                                                rRec.SETRANGE(Tipo             , Tipo);
                                                                rRec.SETRANGE(Hotel            , Hotel);
                                                                rRec.SETRANGE(Contrato         , Contrato);
                                                                rRec.SETRANGE("Fecha concesion", "Fecha concesion");
                                                                IF rRec.FIND('-') THEN BEGIN
                                                                  CASE Tipo OF
                                                                    Tipo::Anticipo:
                                                                    BEGIN
                                                                      ERROR(Text011+
                                                                            Text012);
                                                                    END;
                                                                    Tipo::Prestamo:
                                                                    BEGIN
                                                                      ERROR(Text013+
                                                                            Text012);
                                                                    END;
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Recovery form;
                                                              ESP=Forma recuperacion];
                                                   OptionCaptionML=[ENU=Fixed amounts,Variable amounts;
                                                                    ESP=Importes fijos,Importe variables];
                                                   OptionString=Importes fijos,Importe variables }
    { 21  ;   ;Importe intereses   ;Decimal       ;CaptionML=[ENU=Interest amount;
                                                              ESP=Importe intereses];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 23  ;   ;Importes prefijados ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum(Recuperacion.Importe WHERE (Tipo=FIELD(Tipo),
                                                                                               Hotel=FIELD(Hotel),
                                                                                               Contrato=FIELD(Contrato),
                                                                                               Fecha concesion=FIELD(Fecha concesion)));
                                                   CaptionML=[ENU=Prefixed amounts;
                                                              ESP=Importes prefijados];
                                                   Description=Campo calculado;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 25  ;   ;Pasado ABR          ;Boolean       ;CaptionML=[ENU=ABR Passed;
                                                              ESP=Pasado ABR];
                                                   Editable=No }
    { 27  ;   ;C¢d. divisa         ;Code10        ;TableRelation=Currency.Code;
                                                   CaptionML=[ENU=Currency exchange;
                                                              ESP=C¢d. divisa];
                                                   Description=FK Divisa }
    { 29  ;   ;TourOperador        ;Code10        ;TableRelation=TourOperador.TourOperador;
                                                   OnValidate=BEGIN
                                                                //$001-
                                                                rTTOO.RESET;
                                                                rTTOO.GET(TourOperador);
                                                                rTTOO.TESTFIELD(Bloqueado, FALSE);
                                                                //$001+
                                                              END;

                                                   CaptionML=[ENU=TourOperator;
                                                              ESP=TourOperador];
                                                   Description=FK TourOperador }
    { 31  ;   ;Delegacion          ;Code10        ;TableRelation=Delegacion.Codigo;
                                                   CaptionML=[ENU=Local Office;
                                                              ESP=Delegacion];
                                                   Description=FK Delegacion }
    { 33  ;   ;Importe pendiente grabar;Decimal   ;FieldClass=FlowField;
                                                   CalcFormula=Sum(Recuperacion.Importe WHERE (Tipo=FIELD(Tipo),
                                                                                               Hotel=FIELD(Hotel),
                                                                                               Contrato=FIELD(Contrato),
                                                                                               Fecha concesion=FIELD(Fecha concesion),
                                                                                               Pasado ABR=CONST(No)));
                                                   CaptionML=[ENU=Amount remaining to be recorded;
                                                              ESP=Importe pendiente grabar];
                                                   Description=Campo calculado;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 35  ;   ;Importe grabado     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum(Recuperacion.Importe WHERE (Tipo=FIELD(Tipo),
                                                                                               Hotel=FIELD(Hotel),
                                                                                               Contrato=FIELD(Contrato),
                                                                                               Fecha concesion=FIELD(Fecha concesion),
                                                                                               Pasado ABR=CONST(Yes)));
                                                   CaptionML=[ENU=Recorded amount;
                                                              ESP=Importe grabado];
                                                   Description=Campo calculado;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 37  ;   ;Sobre Fras. Early booking;Boolean  ;CaptionML=[ENU=On Early Booking invoices;
                                                              ESP=Sobre Fras. Early booking] }
    { 39  ;   ;Documento contabilizacion;Code10   ;CaptionML=[ENU=Accounting document;
                                                              ESP=Documento contabilizacion];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Tipo,Hotel,Contrato,Fecha concesion     ;Clustered=Yes }
    {    ;Tipo,Hotel,Fecha concesion              ;KeyGroups=RENUMERING }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1103355004 : TextConst 'ENU=Parameters company folder does not exist;ESP=No existe el fichero de parametros de la empresa.';
      Text001@1103355005 : TextConst 'ENU=Only Advances/Loans can be entered\;ESP=Solo es posible dar de alta Anticipos/Prestamos\';
      Text002@1103355006 : TextConst 'ENU=in the central;ESP=en la central';
      Text003@1103355007 : TextConst 'ENU=Only Advances/Loans can be modified\;ESP=Solo es posible modificar Anticipos/Prestamos\';
      Text004@1103355008 : TextConst 'ENU=Only Advances/Loans can be cancelled\;ESP=Solo es posible dar de baja Anticipos/Prestamos\';
      Text005@1103355009 : TextConst 'ENU=Unable to delete a register because it has already been recorded in ABR;ESP=No se puede eliminar un registro que ya ha sido grabado en el ABR';
      Text006@1103355010 : TextConst 'ENU=Only Advances/Loans can be renamed\;ESP=Solo es posible renombrar Anticipos/Prestamos\';
      Text007@1103355011 : TextConst 'ENU=This Advance/Loan is after the ABR diary. Erase it? ##;ESP=Este Anticipo/Prestamo esta pasado al diario ABR. Borrar? ##';
      Text008@1103355012 : TextConst 'ENU=There are recoveries for this advance\;ESP=Existen recuperaciones para este anticipo\';
      Text009@1103355013 : TextConst 'ENU=firstly you must delete them;ESP=primero debe borrarlas.';
      Text010@1103355014 : TextConst 'ENU=There are recoveries for this loan\;ESP=Existen recuperaciones para este prestamo\';
      Text011@1103355015 : TextConst 'ENU=Already exists recoveries for this advance\;ESP=Ya existen recuperaciones para este anticipo\';
      Text012@1103355016 : TextConst 'ENU=unable to change the definition;ESP=no se puede cambiar la definicion.';
      Text013@1103355017 : TextConst 'ENU=Recoveries for this loan already exist\;ESP=Ya existen recuperaciones para este prestamo\';
      rPar@1103355000 : Record 7009700;
      rRec@1103355001 : Record 7010006;
      rCon@1103355002 : Record 7009723;
      rTTOO@1000000000 : Record 7009717;
      fTabCon@1103355003 : Form 7009737;

    PROCEDURE aux@1();
    BEGIN
    END;

    BEGIN
    {
      $001 MNC 22072014 CNT-LP-14054 No se pueden usar TTOO bloqueados
    }
    END.
  }
}
