OBJECT Table 7010147 Tarifa Excursion
{
  OBJECT-PROPERTIES
  {
    Date=05/10/14;
    Time=23:49:48;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    OnInsert=VAR
               rPar@1103355000 : Record 7009700;
               lrTar@1100217000 : Record 7010147;
             BEGIN
               rPar.FINDFIRST;
               rPar.TESTFIELD("Divisa venta excursiones");

               Delegacion := rPar.Delegacion;

               lrTar.RESET;
               lrTar.SETCURRENTKEY("ID Tarifa");
               IF lrTar.FINDLAST THEN
                 "ID Tarifa" := lrTar."ID Tarifa" + 1
               ELSE
                 "ID Tarifa" := 1;

               IF Temporada = '' THEN
                 Temporada := FORMAT("ID Tarifa");

               //+$006
               //-$009rSalesSetup.FINDFIRST;
               //-$009"Codigo Serie" := rSalesSetup."Posted Invoice Nos.";

               "Impuestos Incluidos" := TRUE; //+$007
             END;

    OnDelete=VAR
               rPrecioVta@1103355000 : Record 7010148;
               rZona@1103355001 : Record 7010149;
             BEGIN
               _CheckReservas;

               rPrecioVta.RESET;
               rPrecioVta.SETRANGE(Delegacion        , Delegacion);
               rPrecioVta.SETRANGE(Excursion         , Excursion);
               rPrecioVta.SETRANGE("Temporada Tarifa", Temporada);
               rPrecioVta.SETRANGE("ID Tarifa"       , "ID Tarifa"); //+$002

               rZona.RESET;
               rZona.SETRANGE(Excursion        , Excursion);
               rZona.SETRANGE("Codigo Contrato", Temporada);
               rZona.SETRANGE("Tipo Contrato"  , rZona."Tipo Contrato"::Venta);
               rZona.SETRANGE("ID Tarifa"      , "ID Tarifa"); //+$002

               IF rPrecioVta.FINDFIRST OR rZona.FINDFIRST THEN
               BEGIN
                 IF CONFIRM(Text001, FALSE) THEN
                 BEGIN
                   rPrecioVta.DELETEALL;
                   rZona.DELETEALL;
                 END
                 ELSE
                   ERROR(Text002);
               END;

               cPar.intro_historico_contrato_Lin(Excursion, Temporada, 3, "ID Tarifa", Text003, Text004, '');
             END;

    CaptionML=[ENU=Excursion Tariff;
               ESP=Tarifa Excursion];
    LookupFormID=Form7010155;
    DrillDownFormID=Form7010152;
  }
  FIELDS
  {
    { 1   ;   ;Excursion           ;Code10        ;TableRelation="Producto agencia".Codigo WHERE (Tipo Producto=CONST(Excursion),
                                                                                                  Caducado=CONST(No));
                                                   OnValidate=BEGIN
                                                                IF xRec.Excursion <> '' THEN
                                                                  ERROR(Text005);

                                                                //+$003 <
                                                                rProd.RESET;
                                                                rProd.SETRANGE(Codigo         , Excursion);
                                                                rProd.SETRANGE("Tipo Producto", rProd."Tipo Producto"::Excursion);
                                                                rProd.SETRANGE(Caducado       , FALSE);
                                                                rProd.FINDFIRST;

                                                                Descripcion := rProd.Descripcion;
                                                                //+$003 >

                                                                // $001
                                                                cFuncExc.ControlOficinaExcursion(Excursion, TRUE);
                                                              END;

                                                   CaptionML=[ENU=Excursion;
                                                              ESP=Excursion];
                                                   NotBlank=Yes;
                                                   Description=PK, FK Producto agencia }
    { 2   ;   ;Delegacion          ;Code10        ;TableRelation=Delegacion.Codigo;
                                                   CaptionML=[ENU=Local Office;
                                                              ESP=Delegacion];
                                                   Description=PK, FK Delegacion;
                                                   Editable=No }
    { 3   ;   ;Temporada           ;Code10        ;OnValidate=BEGIN
                                                                IF xRec.Temporada <> '' THEN
                                                                  ERROR(Text006);
                                                              END;

                                                   CaptionML=[ENU=Season;
                                                              ESP=Temporada];
                                                   Description=PK }
    { 5   ;   ;Descripcion         ;Text50        ;OnValidate=BEGIN
                                                                IF Descripcion <> xRec.Descripcion THEN
                                                                  cPar.intro_historico_contrato_Lin(Excursion, Temporada, 3, "ID Tarifa", FIELDCAPTION(Descripcion),
                                                                                                    FORMAT(xRec.Descripcion), FORMAT(Descripcion));
                                                              END;

                                                   CaptionML=[ENU=Description;
                                                              ESP=Descripcion] }
    { 7   ;   ;Fecha Inicio        ;Date          ;OnValidate=BEGIN
                                                                IF "Fecha Inicio" <> xRec."Fecha Inicio" THEN
                                                                BEGIN
                                                                  _CheckDates;

                                                                  _CheckFechasPrecios;

                                                                  Comprobar_Solapamiento;

                                                                  cPar.intro_historico_contrato_Lin(Excursion, Temporada, 3, "ID Tarifa", FIELDCAPTION("Fecha Inicio"),
                                                                                                    FORMAT(xRec."Fecha Inicio"), FORMAT("Fecha Inicio"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Starting Date;
                                                              ESP=Fecha inicio];
                                                   NotBlank=Yes }
    { 9   ;   ;Fecha Final         ;Date          ;OnValidate=BEGIN
                                                                IF "Fecha Final" <> xRec."Fecha Final" THEN
                                                                BEGIN
                                                                  _CheckDates;

                                                                  _CheckFechasPrecios;

                                                                  Comprobar_Solapamiento;

                                                                  cPar.intro_historico_contrato_Lin(Excursion, Temporada, 3, "ID Tarifa", FIELDCAPTION("Fecha Final"),
                                                                                                    FORMAT(xRec."Fecha Final"), FORMAT("Fecha Final"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Ending Date;
                                                              ESP=Fecha Final];
                                                   NotBlank=Yes }
    { 11  ;   ;Impuestos Incluidos ;Boolean       ;InitValue=No;
                                                   OnValidate=BEGIN
                                                                IF "Impuestos Incluidos" <> xRec."Impuestos Incluidos" THEN
                                                                  cPar.intro_historico_contrato_Lin(Excursion, Temporada, 3, "ID Tarifa", FIELDCAPTION("Impuestos Incluidos"),
                                                                                                    FORMAT(xRec."Impuestos Incluidos"), FORMAT("Impuestos Incluidos"));
                                                              END;

                                                   CaptionML=[ENU=Included Taxes;
                                                              ESP=Impuestos incluidos] }
    { 13  ;   ;Gr. Registro IVA Producto;Code10   ;TableRelation="VAT Product Posting Group".Code;
                                                   OnValidate=BEGIN
                                                                IF "Gr. Registro IVA Producto" <> xRec."Gr. Registro IVA Producto" THEN
                                                                  cPar.intro_historico_contrato_Lin(Excursion, Temporada, 3, "ID Tarifa", FIELDCAPTION("Gr. Registro IVA Producto"),
                                                                                                    FORMAT(xRec."Gr. Registro IVA Producto"), FORMAT("Gr. Registro IVA Producto"));
                                                              END;

                                                   CaptionML=[ENU=VAT Prod. posting group;
                                                              ESP=Gr. Registro IVA Producto];
                                                   NotBlank=Yes;
                                                   Description=FK Gr. registro IVA Producto }
    { 15  ;   ;o_Codigo Serie      ;Code10        ;TableRelation="No. Series".Code;
                                                   CaptionML=[ENU=Serial code;
                                                              ESP=Codigo Serie];
                                                   Description=FK N§ Serie }
    { 17  ;   ;o_C¢d. divisa       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=[ENU=Currency code;
                                                              ESP=C¢d. divisa];
                                                   Description=FK Divisa }
    { 19  ;   ;o_Fecha cambio divisa;Date         ;CaptionML=[ENU=Currency exchange date;
                                                              ESP=Fecha cambio divisa] }
    { 21  ;   ;ID Tarifa           ;Integer       ;CaptionML=[ENU=Tariff ID;
                                                              ESP=ID Tarifa] }
  }
  KEYS
  {
    {    ;Delegacion,Excursion,Temporada          ;Clustered=Yes }
    {    ;Excursion,Fecha Inicio,Fecha Final      ;KeyGroups=RENUMERING }
    {    ;ID Tarifa                                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1103355001 : TextConst 'ENU=Agency parameters file does not exist;ESP=No existe el fichero de par mentros de agencia';
      Text001@1103355002 : TextConst 'ENU=Would you like to delete all prices and zones of the tariff?;ESP=Desea borrar todos los precios y zonas de la tarifa';
      Text002@1103355003 : TextConst 'ENU=Tariff has not been deleted;ESP=No se ha borrado la tarifa';
      Text003@1103355004 : TextConst 'ENU=Deleted;ESP=Borrado';
      Text004@1103355005 : TextConst 'ENU=Register deleted;ESP=Registro Borrado';
      Text005@1103355006 : TextConst 'ENU=Product cannot be modified;ESP=No se puede modificar el procucto';
      Text006@1103355007 : TextConst 'ENU=Seasson cannot be modified;ESP=No se puede modificar la temporada';
      Text007@1103355008 : TextConst 'ENU=Initial date cannot be higher than the final one.;ESP=La fecha inicial no puede ser superior a la final';
      Text008@1103355009 : TextConst 'ENU=Final date cannot be lower than the initial one;ESP=La fecha final no puede ser inferior a la inicial';
      Text009@1103355010 : TextConst 'ENU=This dates overlap with existent dates.;ESP=La estas fechas se solapan con fechas ya existentes';
      rProd@1100217001 : Record 7009710;
      rSalesSetup@1100217002 : Record 311;
      rPar@1100217003 : Record 7009700;
      cPar@1103355000 : Codeunit 7009700;
      cFuncExc@1100217000 : Codeunit 7010141;
      Text010@1100217004 : TextConst 'ENU=There are reservations affected by this rate.;ESP=Existen reservas afectadas por esta tarifa.';
      Text011@1000000000 : TextConst 'ENU=You can not apply the change of dates because some prices would fall outside the contract period;ESP=No se puede aplicar el cambio de fechas porque algunos precios quedarian fuera del periodo del contrato';

    PROCEDURE Comprobar_Solapamiento@2();
    VAR
      rTarifa@1103355000 : Record 7010147;
    BEGIN
      // Comprobar_solapamiento

      rTarifa.RESET;
      rTarifa.SETCURRENTKEY(Excursion,
                            "Fecha Inicio",
                            "Fecha Final");
      rTarifa.SETRANGE (Excursion  , Excursion);
      rTarifa.SETFILTER(Temporada  , '<>%1', Temporada);
      rTarifa.SETFILTER("ID Tarifa", '<>%1', "ID Tarifa"); //+$002
      IF "Fecha Inicio" <> 0D THEN
      BEGIN
        rTarifa.SETFILTER("Fecha Inicio", '>=%1&<=%2', "Fecha Inicio", "Fecha Final");
        IF rTarifa.FINDFIRST THEN
          ERROR(Text009);
        rTarifa.SETFILTER("Fecha Inicio", '');
      END;
      IF "Fecha Final" <> 0D THEN
      BEGIN
        rTarifa.SETFILTER("Fecha Final", '>=%1&<=%2', "Fecha Inicio", "Fecha Final");
        IF rTarifa.FINDFIRST THEN
          ERROR(Text009);
      END;
    END;

    LOCAL PROCEDURE _CheckReservas@1100217000();
    VAR
      lrRsv@1100217000 : Record 7010162;
    BEGIN
      // _CheckReservas

      rPar.FINDFIRST;

      lrRsv.RESET;
      lrRsv.SETRANGE(Producto, Excursion);
      IF rPar."Busca tarifa por fecha venta" THEN
        lrRsv.SETRANGE("Fecha Venta", "Fecha Inicio", "Fecha Final")
      ELSE
        lrRsv.SETRANGE("Fecha Excursion", "Fecha Inicio", "Fecha Final");
      lrRsv.SETRANGE(Anulada    , FALSE);
      lrRsv.SETRANGE(Reembolsada, FALSE);
      IF NOT lrRsv.ISEMPTY THEN
        ERROR(Text010);
    END;

    LOCAL PROCEDURE _CheckDates@1000000000();
    BEGIN
      // _CheckDates

      IF ("Fecha Final" <> 0D) AND ("Fecha Inicio" <> 0D) THEN
        IF "Fecha Inicio" > "Fecha Final" THEN
          ERROR(Text007);
    END;

    LOCAL PROCEDURE _CheckFechasPrecios@1000000002();
    VAR
      lrPre@1000000000 : Record 7010148;
    BEGIN
      // _CheckFechasPrecios

      //. Vamos a comprobar que las fechas de los precios no queden fuera del contrato con un cambio de fechas

      lrPre.RESET;
      lrPre.SETRANGE (Delegacion   , Delegacion);
      lrPre.SETRANGE (Excursion    , Excursion);
      lrPre.SETRANGE ("ID Tarifa"  , "ID Tarifa");
      lrPre.SETFILTER("Fecha Desde", '<%1', "Fecha Inicio");
      IF lrPre.FINDFIRST THEN
        ERROR(Text011);

      lrPre.RESET;
      lrPre.SETRANGE (Delegacion   , Delegacion);
      lrPre.SETRANGE (Excursion    , Excursion);
      lrPre.SETRANGE ("ID Tarifa"  , "ID Tarifa");
      lrPre.SETFILTER("Fecha Hasta", '>%1', "Fecha Final");
      IF lrPre.FINDFIRST THEN
        ERROR(Text011);
    END;

    BEGIN
    {
      $001 JPT 08/08/12 CNT-OC-12004 Incluir filtro por oficina en las excursiones. Control de Oficina en Excursion

      $002 AJS 28122012 Nuevo campo ID Tarifa para ser utilizada como clave primaria, momentaneamente se a¤ade como
                        ultimo campo en la clave primaria existente

      $003 AJS 28122012 Rellenar automaticamente la descripcion con el nombre de la excursion

      $004 AJS 22012013 Aumentar el campo Descripcion

      $005 AJS 12022013 Nuevo parametro "Divisa venta excursiones" obligatorio antes de crear la tarifa

      $006 AJS 02042014 Al crear la tarifa relleno el campo de serie facturacion con la definida en Gestion financiera

      $007 AJS 30042014 Rellenar el campo Impuestos incluidos al crear la tarifa porque la mayor parte de los casos es as¡

      $008 AJS 13052014 Incluyo un control para no dejar borrar una tarifa si afecta a reservas existentes

      $009 AJS 23062014 Marco el campo Codigo Serie como obsoleto

      $010 AJS 05102014 Agregar control para que al cambiar fechas no se queden lineas de precio huerfanas
    }
    END.
  }
}
