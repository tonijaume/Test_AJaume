OBJECT Table 8613 Migration Table
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               Fields@1005 : Record 2000000041;
               MigrationField@1004 : Record 8616;
             BEGIN
               Fields.SETRANGE(TableNo,TableID);
               IF Fields.FIND('-') THEN
                 REPEAT
                   MigrationField.TableID := TableID;
                   MigrationField.FieldID := Fields."No.";
                   MigrationField.Include := IsKeyField(MigrationField.TableID,MigrationField.FieldID);
                   MigrationField.Validate := TRUE;
                   MigrationField.INSERT;
                 UNTIL Fields.NEXT = 0;
             END;

    OnDelete=VAR
               MigrationsRecord@1000 : Record 8614;
               MigrationField@1001 : Record 8616;
             BEGIN
               MigrationsRecord.SETRANGE(TableID,TableID);
               MigrationsRecord.DELETEALL(TRUE);

               MigrationField.SETRANGE(TableID,TableID);
               MigrationField.DELETEALL(TRUE);
             END;

    OnRename=VAR
               MigrationField@1004 : Record 8616;
               RecRef@1003 : RecordRef;
               FieldRef@1001 : FieldRef;
               FieldCount@1000 : Integer;
             BEGIN
               MigrationField.SETRANGE(TableID,xRec.TableID);
               MigrationField.DELETEALL(TRUE);

               RecRef.OPEN(TableID);
               FOR FieldCount := 1 TO RecRef.FIELDCOUNT DO BEGIN
                 FieldRef := RecRef.FIELDINDEX(FieldCount);
                 IF NOT MigrationField.GET(RecRef.NUMBER,FieldRef.NUMBER) THEN BEGIN
                   MigrationField.TableID := RecRef.NUMBER;
                   MigrationField.FieldID := FieldRef.NUMBER;
                   MigrationField.Include := IsKeyField(MigrationField.TableID,MigrationField.FieldID);
                   MigrationField.Validate := TRUE;
                   MigrationField.INSERT;
                 END;
               END;
             END;

    CaptionML=[ENU=Migration Table;
               ESP=Tabla migraci¢n];
  }
  FIELDS
  {
    { 1   ;   ;TableID             ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   OnLookup=VAR
                                                              Object@1000000001 : Record 2000000001;
                                                              Objects@1000000000 : Form 358;
                                                            BEGIN
                                                              Object.SETRANGE(Object.Type,Object.Type::Table);
                                                              Objects.SETTABLEVIEW(Object);
                                                              Objects.LOOKUPMODE := TRUE;
                                                              IF Objects.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                Objects.GETRECORD(Object);
                                                                VALIDATE(TableID, Object.ID);
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=TableID;
                                                              ESP=IdTabla];
                                                   NotBlank=Yes }
    { 2   ;   ;Table Name          ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Name" WHERE (Object Type=CONST(Table),
                                                                                                             Object ID=FIELD(TableID)));
                                                   CaptionML=[ENU=Table Name;
                                                              ESP=Nombre tabla];
                                                   Editable=No }
    { 3   ;   ;No. of Migration Records;Integer   ;FieldClass=FlowField;
                                                   CalcFormula=Count("Migration Record" WHERE (TableID=FIELD(TableID),
                                                                                               Invalid=FILTER(No)));
                                                   CaptionML=[ENU=No. of Migration Records;
                                                              ESP=N§ registros migraci¢n];
                                                   Editable=No }
    { 4   ;   ;No. of Migration Errors;Integer    ;FieldClass=FlowField;
                                                   CalcFormula=Count("Migration Record" WHERE (TableID=FIELD(TableID),
                                                                                               Invalid=FILTER(Yes)));
                                                   CaptionML=[ENU=No. of Migration Errors;
                                                              ESP=N§ errores migraci¢n];
                                                   Editable=No }
    { 5   ;   ;No. of Database Records;Integer    ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Table Information"."No. of Records" WHERE (Company Name=FIELD("Company Filter (Source Table)"),
                                                                                                                  Table No.=FIELD(TableID),
                                                                                                                  No. of Records=FILTER(<>0)));
                                                   CaptionML=[ENU=No. of Database Records;
                                                              ESP=N§ registros base de datos];
                                                   Editable=No }
    { 6   ;   ;Fields Included     ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Migration Table Field" WHERE (TableID=FIELD(TableID)));
                                                   CaptionML=[ENU=Fields Included;
                                                              ESP=Campos incluidos];
                                                   Editable=No }
    { 7   ;   ;Imported Date Time  ;DateTime      ;CaptionML=[ENU=Imported Date Time;
                                                              ESP=Fecha/hora importaci¢n];
                                                   Editable=No }
    { 8   ;   ;Exported Date Time  ;DateTime      ;CaptionML=[ENU=Exported Date Time;
                                                              ESP=Fecha/hora exportaci¢n];
                                                   Editable=No }
    { 9   ;   ;Comments            ;Text250       ;CaptionML=[ENU=Comments;
                                                              ESP=Comentarios] }
    { 10  ;   ;Company Type        ;Text250       ;CaptionML=[ENU=Company Type;
                                                              ESP=Tipo empresa];
                                                   Editable=No }
    { 11  ;   ;Company Filter (Source Table);Text30;
                                                   FieldClass=FlowFilter;
                                                   TableRelation=Company;
                                                   CaptionML=[ENU=Company Filter (Source Table);
                                                              ESP=Filtro empresa (tabla origen)] }
    { 12  ;   ;Table Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(TableID)));
                                                   CaptionML=[ENU=Table Caption;
                                                              ESP=T¡tulo tabla];
                                                   Editable=No }
    { 13  ;   ;Data Template       ;Code10        ;CaptionML=[ENU=Data Template;
                                                              ESP=Plantilla datos] }
    { 14  ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              ESP=Estado];
                                                   OptionCaptionML=[ENU=Not Started,In Progress,Completed;
                                                                    ESP=No iniciado,En progreso,Completado];
                                                   OptionString=Not Started,In Progress,Completed }
    { 15  ;   ;Form ID             ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Form));
                                                   CaptionML=[ENU=Form ID;
                                                              ESP=Id. formulario] }
  }
  KEYS
  {
    {    ;TableID                                 ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE IsKeyField@2(TableID@1005 : Integer;FieldID@1006 : Integer) : Boolean;
    VAR
      FieldSelection@1004 : Record 8616;
      RecRef@1003 : RecordRef;
      KeyRef@1002 : KeyRef;
      FieldRef@1001 : FieldRef;
      KeyFieldCount@1000 : Integer;
    BEGIN
      RecRef.OPEN(TableID);
      KeyRef := RecRef.KEYINDEX(1);
      FOR KeyFieldCount := 1 TO KeyRef.FIELDCOUNT DO BEGIN
        FieldRef := KeyRef.FIELDINDEX(KeyFieldCount);
        IF FieldRef.NUMBER = FieldID THEN
          EXIT(TRUE);
      END;

      EXIT(FALSE);
    END;

    BEGIN
    END.
  }
}
