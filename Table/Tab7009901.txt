OBJECT Table 7009901 Tarifa adicional transfer
{
  OBJECT-PROPERTIES
  {
    Date=07/02/13;
    Time=11:41:16;
    Modified=Yes;
    Version List=AIC2009,CNT;
  }
  PROPERTIES
  {
    OnInsert=VAR
               lrTar@1100253000 : Record 7009901;
             BEGIN

               ID :=1;
               CLEAR(lrTar);
               IF lrTar.FINDLAST THEN
                 ID += lrTar.ID;

               rParA.FINDFIRST;
               Delegacion := rParA.Delegacion;

               _CompFechas;
               CompSolap;

               "Usuario Creacion" := USERID;
               "Fecha Creacion"   := CURRENTDATETIME;

               IntroHist(Text004,'','');
             END;

    OnModify=BEGIN

               IF "Fecha Inicial"     = 0D THEN ERROR(Text002, FIELDCAPTION("Fecha Inicial"));
               IF "Fecha Final"       = 0D THEN ERROR(Text002, FIELDCAPTION("Fecha Final"));
               //IF "Limite Edad Ni¤os" = 0 THEN ERROR(Text002, FIELDCAPTION("Limite Edad Ni¤os"));
               IF Delegacion          = '' THEN ERROR(Text002, FIELDCAPTION(Delegacion));

               _CompFechas;

               CompSolap;

               "Usuario Modificacion" := USERID;
               "Fecha Modificacion"   := CURRENTDATETIME;

               ModIntroHist;
             END;

    OnDelete=VAR
               lrPrec@1100253000 : Record 7009902;
             BEGIN

               CLEAR(lrPrec);
               lrPrec.SETRANGE("ID Tarifa", ID);
               lrPrec.DELETEALL;

               IntroHist(Text005,'','');
             END;

    CaptionML=[ENU=Additional Transfer Fee;
               ESP=Tarifa adicional transfer];
    LookupFormID=Form7009773;
    DrillDownFormID=Form7009773;
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;Integer       ;CaptionML=[ENU=ID;
                                                              ESP=ID];
                                                   Editable=No }
    { 5   ;   ;Delegacion          ;Code10        ;TableRelation=Delegacion.Codigo;
                                                   CaptionML=[ENU=Delegation;
                                                              ESP=Delegacion];
                                                   NotBlank=Yes }
    { 10  ;   ;Tipo Tarifa         ;Option        ;CaptionML=[ENU=Tariff Type;
                                                              ESP=Tipo Tarifa];
                                                   OptionCaptionML=[ENU=Porter, Transportation Guide, Support;
                                                                    ESP=Maletero,Guia Traslado,Asistencia];
                                                   OptionString=Maletero,Guia Traslado,Asistencia }
    { 20  ;   ;Fecha Inicial       ;Date          ;CaptionML=[ENU=Starting Date;
                                                              ESP=Fecha Inicial];
                                                   NotBlank=Yes }
    { 30  ;   ;Fecha Final         ;Date          ;CaptionML=[ENU=Ending Date;
                                                              ESP=Fecha Final];
                                                   NotBlank=Yes }
    { 40  ;   ;Limite Edad Ni¤os   ;Integer       ;CaptionML=[ENU=Childs Until Age;
                                                              ESP=Limite Edad Ni¤os];
                                                   NotBlank=Yes }
    { 50  ;   ;Cod. Divisa         ;Code10        ;TableRelation=Currency.Code;
                                                   CaptionML=[ENU=Currency Code;
                                                              ESP=C¢d. Divisa] }
    { 60  ;   ;Usuario Creacion    ;Code20        ;TableRelation=User."User ID";
                                                   CaptionML=[ENU=Creation User;
                                                              ESP=Usuario Creaci¢n];
                                                   Editable=No }
    { 70  ;   ;Fecha Creacion      ;DateTime      ;CaptionML=[ENU=Creation Time;
                                                              ESP=Fecha Creaci¢n];
                                                   Editable=No }
    { 80  ;   ;Usuario Modificacion;Code20        ;TableRelation=User."User ID";
                                                   CaptionML=[ENU=Last Modification User;
                                                              ESP=Usuario Modificaci¢n];
                                                   Editable=No }
    { 90  ;   ;Fecha Modificacion  ;DateTime      ;CaptionML=[ENU=Last Modification Time;
                                                              ESP=Fecha Modificaci¢n];
                                                   Editable=No }
    { 100 ;   ;Tipo aplicacion     ;Option        ;CaptionML=[ENU=Application type;
                                                              ESP=Tipo aplicacion];
                                                   OptionCaptionML=[ENU=Service,Pax;
                                                                    ESP=Servicio,Pax];
                                                   OptionString=Servicio,Pax;
                                                   Description=Servicio,Pax }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100253005 : TextConst 'ENU=%1 could not be later than %2;ESP=%1 No pude ser posterior a %2';
      Text002@1100253000 : TextConst 'ENU=The Field %1 Must be completed;ESP=El campo %1 Debe de estar rellenado';
      Text004@1100253002 : TextConst 'ENU=New;ESP=Nuevo';
      Text005@1100253001 : TextConst 'ENU=Deleted;ESP=Borrado';
      rParA@1100253003 : Record 7009700;
      Text006@1100253004 : TextConst 'ENU=This record is overlapping with another;ESP=Este registro se est  solapando con otro';

    PROCEDURE ModIntroHist@1100253006();
    VAR
      lwRec@1100253000 : RecordRef;
      lwXRec@1100253001 : RecordRef;
      lwField@1100253002 : FieldRef;
      lwXField@1100253003 : FieldRef;
      lwCnt@1100253004 : Integer;
      lwOK@1100253005 : Boolean;
    BEGIN
      // ModIntroHist

      lwRec.GETTABLE(Rec);
      lwXRec.GETTABLE(xRec);

      FOR lwCnt := 1 TO lwRec.FIELDCOUNT DO BEGIN
        lwField  := lwRec.FIELDINDEX(lwCnt);
        lwXField := lwXRec.FIELDINDEX(lwCnt);
        lwOK := NOT (lwField.NUMBER IN [60,70,80,90]);
        IF lwOK  THEN
          IF lwXField.VALUE <> lwField.VALUE THEN
            IntroHist(lwField.CAPTION, FORMAT(lwXField.VALUE), FORMAT(lwField.VALUE));
      END;
    END;

    PROCEDURE IntroHist@1100253002(pwCampo@1100253000 : Text[30];pwValorAnterior@1100253001 : Text[30];pwNuevoValor@1100253002 : Text[30]);
    VAR
      cPar@1100253003 : Codeunit 7009700;
    BEGIN
      // IntroHist

      IF ID = 0 THEN
        EXIT;
      TESTFIELD(Delegacion);
      cPar.intro_historico_contrato(Delegacion, FORMAT(ID), 13, pwCampo, pwValorAnterior, pwNuevoValor);
    END;

    PROCEDURE ShowLog@1100253003();
    VAR
      lrHist@1100253000 : Record 7009764;
    BEGIN
      // ShowLog

      CLEAR(lrHist);
      lrHist.FILTERGROUP(2);
      lrHist.SETRANGE(Delegacion       , Delegacion );
      lrHist.SETRANGE("N§ Contrato"    , FORMAT(ID));
      lrHist.SETRANGE(Tipo             , lrHist.Tipo::"Adicional Transfer");
      lrHist.FILTERGROUP(0);
      FORM.RUN(0, lrHist);
    END;

    PROCEDURE CompSolap@1100253001();
    VAR
      lrTar@1100253000 : Record 7009901;
    BEGIN
      // CompSolap
      // Comprobamos que no se solape ning£n precio en el ranto de pax

      CLEAR(lrTar);
      lrTar.SETRANGE("Tipo Tarifa", "Tipo Tarifa");
      lrTar.SETFILTER(ID, '<>%1', ID);
      lrTar.SETFILTER("Fecha Inicial", '<=%1', "Fecha Final");
      lrTar.SETFILTER("Fecha Final"  , '>=%1', "Fecha Inicial");
      IF lrTar.FINDFIRST THEN
        ERROR(Text006);
    END;

    LOCAL PROCEDURE _CompFechas@1100253005();
    BEGIN
      // _CompFechas

      IF ("Fecha Inicial" > "Fecha Final") THEN
        ERROR(Text001, FIELDCAPTION("Fecha Inicial"),FIELDCAPTION("Fecha Final"));
    END;

    BEGIN
    {
      JPT 02/05/12 CNT-CAR12004-05

      $001 AJS 07022013 Eliminar la obligacion de Limite Edad ni¤os
    }
    END.
  }
}
