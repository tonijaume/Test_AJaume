OBJECT Table 7009791 Zonas transfer
{
  OBJECT-PROPERTIES
  {
    Date=16/05/15;
    Time=21:14:48;
    Modified=Yes;
    Version List=AIC2009,COM;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               _SincronizaComercializadora(0); //+$001
             END;

    OnModify=BEGIN
               _SincronizaComercializadora(1); //+$001
             END;

    OnDelete=BEGIN
               _SincronizaComercializadora(2); //+$001
             END;

    OnRename=BEGIN
               ERROR(Text000);
             END;

    CaptionML=[ENU=Transfer zones;
               ESP=Zonas transfer];
    LookupFormID=Form7009901;
    DrillDownFormID=Form7009901;
  }
  FIELDS
  {
    { 1   ;   ;Zona                ;Code10        ;CaptionML=[ENU=Location;
                                                              ESP=Zona];
                                                   NotBlank=Yes;
                                                   Description=PK }
    { 3   ;   ;Contrato            ;Code10        ;CaptionML=[ENU=Contract;
                                                              ESP=Contrato];
                                                   Description=PK }
    { 5   ;   ;Tipo contrato       ;Option        ;CaptionML=[ENU=Contract type;
                                                              ESP=Tipo contrato];
                                                   OptionCaptionML=[ENU=Purchase,Sale,Service;
                                                                    ESP=Compra,Venta,Servicio];
                                                   OptionString=Compra,Venta,Servicio;
                                                   Description=PK }
    { 7   ;   ;Zona fisica         ;Code10        ;TableRelation="Zona fisica"."Zona Fisica";
                                                   OnValidate=BEGIN
                                                                CLEAR(rZonTra);
                                                                rZonTra.SETRANGE(Delegacion     , Delegacion); //+$001
                                                                rZonTra.SETRANGE("Tipo contrato", rZonTra."Tipo contrato");
                                                                rZonTra.SETRANGE(Contrato       , Contrato);
                                                                rZonTra.SETRANGE("Zona fisica"  , "Zona fisica");
                                                                IF rZonTra.FINDFIRST THEN
                                                                BEGIN
                                                                  IF rZonTra.Zona <> Zona THEN
                                                                  BEGIN
                                                                    "Zona fisica" := xRec."Zona fisica";
                                                                    ERROR(Text001);
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Location Zone;
                                                              ESP=Zona Fisica];
                                                   Description=PK, FK Zona Fisica }
    { 9   ;   ;Orden entrada zona  ;Integer       ;OnValidate=BEGIN
                                                                "Orden salida zona" := 10000 - "Orden entrada zona";
                                                              END;

                                                   CaptionML=[ENU=Arrival Zone Order;
                                                              ESP=Orden entrada zona] }
    { 11  ;   ;Orden salida zona   ;Integer       ;CaptionML=[ENU=Departure zone order;
                                                              ESP=Orden salida zona] }
    { 13  ;   ;Delegacion          ;Code10        ;TableRelation=Delegacion.Codigo;
                                                   CaptionML=[ENU=Destination;
                                                              ESP=Delegacion] }
  }
  KEYS
  {
    {    ;Delegacion,Zona,Contrato,Tipo contrato,Zona fisica;
                                                   Clustered=Yes }
    {    ;Orden entrada zona                      ;KeyGroups=RENUMERING }
    {    ;Orden salida zona                       ;KeyGroups=RENUMERING }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1103355001 : TextConst 'ENU=Unable to rename transfer zones;ESP=No se pueden renombrar zonas de transfer.';
      Text001@1103355002 : TextConst 'ENU=Physical zone cannot be in two zones of transfer;ESP=Una Zona fisica no puede estar en dos zonas de Transfer.';
      rZonTra@1103355000 : Record 7009791;

    LOCAL PROCEDURE _SincronizaComercializadora@1000000003(pwOperacion@1000000003 : Integer);
    VAR
      lrContrato@1000000004 : Record 7009788;
      lrTO@1000000005 : Record 7009717;
      lrZonaFisica@1000000006 : Record 7009713;
      lrRecRef@1000000001 : RecordRef;
      lrxRecRef@1000000000 : RecordRef;
      lcSincroCOM@1000000002 : Codeunit 7009793;
    BEGIN
      // _SincronizaComercializadora

      //+$001 <
      IF "Tipo contrato" <> "Tipo contrato"::Venta THEN
        EXIT;

      IF NOT lrContrato.GET(Delegacion, Contrato) THEN
        EXIT;

      IF lrContrato.Comercializadora THEN
      BEGIN
        lrTO.GET(lrContrato.TourOperador);

        lrRecRef.GETTABLE(Rec);
        lrxRecRef.GETTABLE(xRec);
        CASE pwOperacion OF
          0: lcSincroCOM.SincronizaTabla(lrRecRef, lrxRecRef, lcSincroCOM.OpInsert, lrTO."Empresa comercializadora");
          1: lcSincroCOM.SincronizaTabla(lrRecRef, lrxRecRef, lcSincroCOM.OpModify, lrTO."Empresa comercializadora");
          2: lcSincroCOM.SincronizaTabla(lrRecRef, lrxRecRef, lcSincroCOM.OpDelete, lrTO."Empresa comercializadora");
        END;

        CASE pwOperacion OF
          0, 1:
          BEGIN
            IF lrZonaFisica.GET("Zona fisica") THEN
            BEGIN
              lrRecRef.GETTABLE(lrZonaFisica);
              lrxRecRef.GETTABLE(lrZonaFisica);
              lcSincroCOM.SincronizaTabla(lrRecRef, lrxRecRef, lcSincroCOM.OpModify, lrTO."Empresa comercializadora");
            END;
          END;
        END;
      END;
      //+$001 >
    END;

    BEGIN
    {
      $001 AJS 13032015 CNT-CAR-14096, Sincronizacion contratos comercializadora
    }
    END.
  }
}
