OBJECT Table 7010438 Temporal asignacion partes
{
  OBJECT-PROPERTIES
  {
    Date=16/10/09;
    Time=12:30:46;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    CaptionML=ESP=Temporal asignacion partes;
  }
  FIELDS
  {
    { 1   ;   ;Usuario             ;Code20        ;CaptionML=ESP=Usuario;
                                                   Description=PK }
    { 3   ;   ;Num Parte           ;Code20        ;OnValidate=BEGIN
                                                                rParte.RESET;
                                                                rParte.SETCURRENTKEY(Confirmado,
                                                                                     Fecha);
                                                                rParte.SETRANGE (Confirmado, FALSE);

                                                                IF "Tipo accion" = "Tipo accion"::Asignacion THEN
                                                                  rParte.SETFILTER(Fecha, '%1..%2', "Fecha asignacion", CALCDATE(Text50000, "Fecha asignacion"))
                                                                ELSE BEGIN
                                                                  rParte.SETRANGE(Fecha , "Fecha asignacion");
                                                                  rParte.SETRANGE(Garaje, Garaje);
                                                                END;
                                                                IF STRLEN("Num Parte") <= 10 THEN
                                                                  rParte.SETRANGE ("N§ Parte", "Num Parte")
                                                                ELSE
                                                                  rParte.SETRANGE("Codigo Barras", "Num Parte");

                                                                IF rParte.FINDFIRST THEN BEGIN

                                                                  "Num Parte" := rParte."N§ Parte";

                                                                  // Comprobar que no esta repetido en la asignacion propia o de otro usuario

                                                                  rTemp.SETCURRENTKEY("Num Parte");
                                                                  rTemp.SETRANGE ("Tipo accion", "Tipo accion");
                                                                  rTemp.SETRANGE ("Num Parte"  , "Num Parte");
                                                                  rTemp.SETFILTER("Num Linea"  , '<>%1', "Num Linea");
                                                                  IF rTemp.FINDFIRST THEN BEGIN
                                                                    IF rTemp.Usuario <> Usuario THEN
                                                                      ERROR(Text001, rTemp.Usuario)
                                                                    ELSE
                                                                      ERROR(Text002);
                                                                  END;

                                                                  // Utilizamos el mismo temporal para la asignacion de recursos y el cambio rapido de garaje, la comprobacion por
                                                                  // conductor solo hay que hacerla en el proceso de asignacion

                                                                  IF "Tipo accion" = "Tipo accion"::Asignacion THEN BEGIN

                                                                    // Comprobar si el parte ya esta asignado, preguntamos al usuario y si confirma deshacemos la asignacion anterior

                                                                    IF rParte."Cod Conductor" <> '' THEN BEGIN
                                                                      IF NOT CONFIRM(Text003, FALSE, rParte."Cod Conductor") THEN
                                                                        ERROR(Text004);

                                                                      rParte.VALIDATE("Cod Conductor", '');
                                                                      rParte.VALIDATE(Vehiculo       , '');
                                                                      rParte.MODIFY;
                                                                    END;

                                                                    // Hacemos la comprobacion de fecha, si el parte el del dia siguiente marcamos la asignacion como de dia siguiente

                                                                    Fecha := rParte.Fecha;
                                                                    "Dia siguiente" := rParte."Dia siguiente";

                                                                    IF Fecha > "Fecha asignacion" THEN
                                                                      "Dia siguiente" := TRUE;

                                                                    Garaje := rParte.Garaje;
                                                                  END;

                                                                END
                                                                ELSE
                                                                  ERROR(Text005);
                                                              END;

                                                   OnLookup=BEGIN
                                                              rParte.RESET;
                                                              rParte.SETCURRENTKEY(Confirmado,
                                                                                   Fecha);
                                                              rParte.FILTERGROUP(2);
                                                              rParte.SETRANGE (Confirmado, FALSE);
                                                              IF "Tipo accion" = "Tipo accion"::Asignacion THEN
                                                                rParte.SETFILTER(Fecha     , '%1..%2', "Fecha asignacion", CALCDATE(Text50000, "Fecha asignacion"))
                                                              ELSE BEGIN
                                                                rParte.SETRANGE(Fecha , "Fecha asignacion");
                                                                rParte.SETRANGE(Garaje, Garaje);
                                                              END;
                                                              rParte.FILTERGROUP(0);

                                                              CLEAR(fTabPartes);
                                                              fTabPartes.SETTABLEVIEW(rParte);
                                                              fTabPartes.LOOKUPMODE(TRUE);
                                                              fTabPartes.EDITABLE(FALSE);
                                                              fTabPartes.LlamadaAsignacion(TRUE);
                                                              IF fTabPartes.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                fTabPartes.GETRECORD(rParte);
                                                                VALIDATE("Num Parte", rParte."N§ Parte");
                                                              END;
                                                            END;

                                                   CaptionML=ESP=Num Parte;
                                                   NotBlank=Yes;
                                                   Description=PK }
    { 5   ;   ;Fecha               ;Date          ;CaptionML=ESP=Fecha }
    { 11  ;   ;Dia siguiente       ;Boolean       ;CaptionML=ESP=Dia siguiente }
    { 13  ;   ;Num Linea           ;Integer       ;CaptionML=ESP=Num Linea }
    { 15  ;   ;Fecha asignacion    ;Date          ;CaptionML=ESP=Fecha asignacion }
    { 17  ;   ;Tipo accion         ;Option        ;CaptionML=ESP=Tipo accion;
                                                   OptionCaptionML=ESP=Asignacion,Garaje;
                                                   OptionString=Asignacion,Garaje;
                                                   Description=Para indicar si utilizamos esta tabla para asignar recursos o cambiar garaje }
    { 19  ;   ;Garaje              ;Code10        ;CaptionML=ESP=Garaje;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Usuario,Tipo accion,Num Linea           ;Clustered=Yes }
    {    ;Num Parte                                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text50000@1103355000 : TextConst 'ESP=+1D';
      rTemp@1100244002 : Record 7010438;
      Text001@1100244003 : TextConst 'ESP=El usuario %1 esta trabajando en la asignacion de este parte.';
      Text002@1100244004 : TextConst 'ESP=No se puede asignar 2 veces el mismo parte.';
      Text003@1100244005 : TextConst 'ESP=Este parte ya esta asignado al chofer %1, desea cambiar esta asignacion.';
      Text004@1100244006 : TextConst 'ESP=Asignacion anulada.';
      rParte@1100244000 : Record 7010360;
      fTabPartes@1100244001 : Form 7010401;
      Text005@1100244007 : TextConst 'ESP=El parte de trabajo no corresponde con la fecha de trabajo.';

    BEGIN
    END.
  }
}
