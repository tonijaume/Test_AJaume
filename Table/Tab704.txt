OBJECT Table 704 BA Db. Numbered List
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("BA Db. Code");
             END;

    OnDelete=VAR
               BAEnumVal@1000 : Record 705;
             BEGIN
               BAEnumVal.SETRANGE("BA Db. Code",Rec."BA Db. Code");
               BAEnumVal.SETRANGE("BA Db. Num. List Line No.",Rec."Line No.");
               BAEnumVal.DELETEALL;
             END;

    OnRename=BEGIN
               TESTFIELD("BA Db. Code");
             END;

    CaptionML=[ENU=BA Db. Numbered List;
               ESP=Lista numerada BD BA];
    LookupFormID=Form709;
    DrillDownFormID=Form709;
  }
  FIELDS
  {
    { 1   ;   ;BA Db. Code         ;Code10        ;TableRelation="BA Database";
                                                   CaptionML=[ENU=BA Db. Code;
                                                              ESP=C¢digo BD de BA] }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ESP=N§ l¡nea] }
    { 3   ;   ;Name                ;Text50        ;OnValidate=VAR
                                                                BAOvNewElem@1000 : Codeunit 701;
                                                              BEGIN
                                                                Name := BAOvNewElem.ConvToDSOName(Name);
                                                              END;

                                                   CaptionML=[ENU=Name;
                                                              ESP=Nombre] }
    { 4   ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              ESP=Tipo];
                                                   OptionCaptionML=[ENU=Field,Custom;
                                                                    ESP=Campo,Personalizado];
                                                   OptionString=Field,Custom }
    { 5   ;   ;Table No.           ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   OnValidate=VAR
                                                                Object@1000 : Record 2000000001;
                                                              BEGIN
                                                                TESTFIELD(Type,Type::Field);
                                                                IF "Table No." <> 0 THEN
                                                                  Object.GET(Object.Type::Table,'',"Table No.");
                                                                IF "Table No." <> xRec."Table No." THEN
                                                                  "Field No." := 0;
                                                                CALCFIELDS("Table Name","Field Name");
                                                              END;

                                                   OnLookup=VAR
                                                              Object@1000 : Record 2000000001;
                                                              Objects@1001 : Form 358;
                                                            BEGIN
                                                              TESTFIELD(Type,Type::Field);
                                                              IF "Table No." <> 0 THEN BEGIN
                                                                Object.GET(Object.Type::Table,'',"Table No.");
                                                                Objects.SETRECORD(Object);
                                                              END;
                                                              Object.SETRANGE(Type,Object.Type::Table);
                                                              Objects.SETTABLEVIEW(Object);
                                                              Objects.LOOKUPMODE := TRUE;
                                                              IF Objects.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                Objects.GETRECORD(Object);
                                                                "Table No." := Object.ID;
                                                              END;
                                                              IF "Table No." <> xRec."Table No." THEN
                                                                "Field No." := 0;
                                                              CALCFIELDS("Table Name","Field Name");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Table No.;
                                                              ESP=Tabla n§] }
    { 6   ;   ;Field No.           ;Integer       ;OnValidate=VAR
                                                                Field@1000 : Record 2000000041;
                                                              BEGIN
                                                                TESTFIELD(Type,Type::Field);
                                                                IF "Field No." <> 0 THEN BEGIN
                                                                  TESTFIELD("Table No.");
                                                                  Field.GET("Table No.","Field No.");
                                                                  IF NOT (Field.Type IN [Field.Type::Option,Field.Type::Boolean]) THEN
                                                                    Field.TESTFIELD(Type,Field.Type::Option);
                                                                  CreateEnumeration;
                                                                END;
                                                                CALCFIELDS("Field Name");
                                                                IF Name = '' THEN
                                                                  IF STRLEN(STRSUBSTNO(Text001,"Table Name","Field Name")) > MAXSTRLEN(Name) THEN
                                                                    VALIDATE(Name,"Field Name")
                                                                  ELSE
                                                                    VALIDATE(Name,STRSUBSTNO(Text001,"Table Name","Field Name"));
                                                              END;

                                                   OnLookup=VAR
                                                              Field@1000 : Record 2000000041;
                                                              Fields@1001 : Form 7702;
                                                            BEGIN
                                                              TESTFIELD(Type,Type::Field);
                                                              TESTFIELD("Table No.");
                                                              IF "Field No." <> 0 THEN BEGIN
                                                                Field.GET("Table No.","Field No.");
                                                                Fields.SETRECORD(Field);
                                                              END;
                                                              Field.SETRANGE(TableNo,"Table No.");
                                                              Field.SETRANGE(Type,Field.Type::Option);
                                                              Fields.SETTABLEVIEW(Field);
                                                              Fields.LOOKUPMODE := TRUE;
                                                              IF Fields.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                Fields.GETRECORD(Field);
                                                                VALIDATE("Field No.",Field."No.");
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Field No.;
                                                              ESP=N§ campo] }
    { 7   ;   ;Table Name          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table No.)));
                                                   CaptionML=[ENU=Table Name;
                                                              ESP=Nombre tabla] }
    { 8   ;   ;Field Name          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Table No.),
                                                                                                   No.=FIELD(Field No.)));
                                                   CaptionML=[ENU=Field Name;
                                                              ESP=Nombre de campo] }
  }
  KEYS
  {
    {    ;BA Db. Code,Line No.                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=%1 %2;ESP=%1 %2';

    PROCEDURE CreateEnumeration@1();
    VAR
      BACubeEnum@1000 : Record 705;
      RecRef@1001 : RecordRef;
      FieldRef@1002 : FieldRef;
      OptText@1005 : Text[1024];
      i@1003 : Integer;
      j@1004 : Integer;
      DummyBool@1006 : Boolean;
    BEGIN
      TESTFIELD("Table No.");
      TESTFIELD("Field No.");
      BACubeEnum.SETRANGE("BA Db. Code","BA Db. Code");
      BACubeEnum.SETRANGE("BA Db. Num. List Line No.","Line No.");
      BACubeEnum.DELETEALL;

      BACubeEnum."BA Db. Code" := "BA Db. Code";
      BACubeEnum."BA Db. Num. List Line No." := "Line No.";

      RecRef.OPEN("Table No.");
      FieldRef := RecRef.FIELD("Field No.");
      IF FieldRef.OPTIONSTRING = '' THEN BEGIN
        DummyBool := FALSE;
        BACubeEnum.Value := 0;
        BACubeEnum.Name := FORMAT(DummyBool);
        BACubeEnum.INSERT;
        DummyBool := TRUE;
        BACubeEnum.Value := 1;
        BACubeEnum.Name := FORMAT(DummyBool);
        BACubeEnum.INSERT;
      END ELSE BEGIN
        OptText := FieldRef.OPTIONCAPTION;
        i := STRPOS(OptText,',');
        WHILE i > 0 DO BEGIN
          BACubeEnum.Value := j;
          BACubeEnum.Name := COPYSTR(OptText,1,i-1);
          BACubeEnum.INSERT;
          OptText := COPYSTR(OptText,i+1);
          i := STRPOS(OptText,',');
          j := j + 1;
        END;
        BACubeEnum.Value := j;
        BACubeEnum.Name := OptText;
        BACubeEnum.INSERT;
      END;
    END;

    BEGIN
    END.
  }
}
