OBJECT Table 7009724 Hotel
{
  OBJECT-PROPERTIES
  {
    Date=16/03/15;
    Time=23:30:19;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               rParametros.FINDFIRST;
               IF NOT rParametros."Empresa Delegacion" THEN
                 ERROR(Text001);

               _NewHotel; //+$011

               IF COPYSTR(Hotel, 1, 3) <> COPYSTR(rParametros.Delegacion, 1, 3) THEN
                 ERROR(Text002 + Text003);

               //-$005rBooking.RESET;
               //-$005rBooking.Delegacion := COPYSTR(rParametros.Delegacion, 1, 3);
               //-$005rBooking.Hotel      := Hotel;
               //-$005rBooking.INSERT;

               VALIDATE(Replicado);
             END;

    OnModify=BEGIN
               rParametros.FINDFIRST;
               IF NOT rParametros."Empresa Delegacion" THEN
                 ERROR(Text004);

               VALIDATE(Replicado);

               "Fecha ultima modificacion" := CURRENTDATETIME; //+$006
             END;

    OnDelete=VAR
               lrActED@1103355000 : Record 7010001;
             BEGIN
               rParametros.FINDFIRST;
               IF NOT rParametros."Empresa Delegacion" THEN
                 ERROR(Text005);

               rHotelTO.RESET;
               rHotelTO.SETRANGE("Hotel estancia", Rec.Hotel);
               IF rHotelTO.FINDFIRST THEN
                 ERROR(Text006 + Text007);

               //-$005IF rBooking.GET(COPYSTR(rParametros.Delegacion, 1, 3), Hotel) THEN
               //-$005  rBooking.DELETE;

               InsertaLinAction;
             END;

    OnRename=BEGIN
               rParametros.FINDFIRST;
               IF NOT rParametros."Empresa Delegacion" THEN
                 ERROR(Text010);

               VALIDATE(Replicado);
             END;

    CaptionML=[ENU=Hotel;
               ESP=Hotel];
    LookupFormID=Form7009731;
    DrillDownFormID=Form7009731;
  }
  FIELDS
  {
    { 1   ;   ;Hotel               ;Code20        ;AltSearchField=Nombre;
                                                   CaptionML=[ENU=Hotel;
                                                              ESP=Hotel];
                                                   Description=PK }
    { 3   ;   ;Nombre              ;Text30        ;OnValidate=BEGIN
                                                                IF Nombre <> xRec.Nombre THEN
                                                                  _UpdateNombreRecogidas;
                                                              END;

                                                   CaptionML=[ENU=Name;
                                                              ESP=Nombre] }
    { 5   ;   ;NoName              ;Boolean       ;OnValidate=VAR
                                                                lrResH@1103355000 : Record 7009743;
                                                                lwFecha@1103355001 : Date;
                                                                lwCont@1103355002 : Integer;
                                                                lwTotal@1103355003 : Integer;
                                                              BEGIN
                                                                IF NoName THEN BEGIN
                                                                  "Proveedor Intermediacion" := '';
                                                                  "Proveedor Cuenta Propia" := '';
                                                                END;

                                                                IF NoName <> xRec.NoName THEN
                                                                BEGIN
                                                                  // Se modificaran el campo Pendiente Asignar en todas las reservas afectadas
                                                                  wVentana.OPEN(Text016);
                                                                  CLEAR(lrResH);
                                                                  lrResH.SETCURRENTKEY("Hotel booking","Fecha entrada","Fecha salida",Bono,Responsable);
                                                                  lrResH.SETRANGE("Hotel reservado", Hotel);
                                                                  lrResH.SETRANGE(Responsable, TRUE);
                                                                  lrResH.SETFILTER("Fecha entrada", '>%1', TODAY +1);
                                                                  IF lrResH.FINDSET THEN BEGIN
                                                                    lwTotal := lrResH.COUNT;
                                                                    CLEAR(lwCont);
                                                                    REPEAT
                                                                      IF NOT lrResH."Pendiente asignar" THEN BEGIN
                                                                        lrResH.VALIDATE("Pendiente asignar", TRUE);
                                                                        lrResH.MODIFY;
                                                                      END;
                                                                      lwCont +=1;
                                                                      wVentana.UPDATE(1, ROUND(lwCont/lwTotal*10000,1));
                                                                    UNTIL lrResH.NEXT=0;
                                                                  END;
                                                                  wVentana.CLOSE;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=NoName;
                                                              ESP=NoName];
                                                   Description=Indica si es de oferta }
    { 7   ;   ;Categoria           ;Code10        ;TableRelation="Categoria Hotel".Codigo;
                                                   CaptionML=[ENU=Category;
                                                              ESP=Categoria];
                                                   NotBlank=Yes;
                                                   Description=FK Categorias hotel }
    { 9   ;   ;Zona Fisica         ;Code10        ;TableRelation="Zona fisica"."Zona Fisica";
                                                   OnValidate=BEGIN
                                                                {* Modificar las zonas en las reservas de transfer *}

                                                                rRsvTra.RESET;
                                                                rRsvTra.SETCURRENTKEY("Hotel entrada");
                                                                rRsvTra.SETRANGE("Hotel entrada", Hotel);
                                                                IF rRsvTra.FINDSET(TRUE) THEN
                                                                  rRsvTra.MODIFYALL("Zona entrada", "Zona Fisica");

                                                                rRsvTra.RESET;
                                                                rRsvTra.SETCURRENTKEY("Hotel salida");
                                                                rRsvTra.SETRANGE("Hotel salida", Hotel);
                                                                IF rRsvTra.FINDSET(TRUE) THEN
                                                                  rRsvTra.MODIFYALL("Zona salida", "Zona Fisica");

                                                                rPer.RESET;
                                                                rPer.SETCURRENTKEY("Hotel Booking");
                                                                rPer.SETRANGE("Hotel Booking", Hotel);
                                                                IF rPer.FINDSET(TRUE) THEN
                                                                  rPer.MODIFYALL("Zona hotel", "Zona Fisica");

                                                                rRsvHot.RESET;
                                                                rRsvHot.SETCURRENTKEY("Hotel booking");
                                                                rRsvHot.SETRANGE("Hotel booking", Hotel);
                                                                IF rRsvHot.FINDSET(TRUE) THEN
                                                                  rRsvHot.MODIFYALL("Zona hotel", "Zona Fisica");

                                                                //+$002
                                                                rRsvExc.RESET;
                                                                rRsvExc.SETCURRENTKEY("Fecha Excursion", Producto, "Zona Hotel", "Hotel Cliente");
                                                                rRsvExc.SETFILTER("Fecha Excursion", '>=%1', TODAY);
                                                                rRsvExc.SETRANGE ("Hotel Cliente"  , Hotel);
                                                                IF rRsvExc.FINDSET(TRUE) THEN
                                                                  rRsvExc.MODIFYALL("Zona Hotel", "Zona Fisica");
                                                                //+$002
                                                              END;

                                                   CaptionML=[ENU=Location Zone;
                                                              ESP=Zona Fisica];
                                                   NotBlank=Yes;
                                                   Description=FK Zona Fisica }
    { 11  ;   ;Orden Entrada       ;Integer       ;OnValidate=BEGIN
                                                                IF "Orden Entrada" <> xRec."Orden Entrada" THEN BEGIN
                                                                  "Orden Salida" := 10000 - "Orden Entrada";

                                                                  {* Modificar el orden en las reservas de transfer *}

                                                                  rRsvTra.RESET;
                                                                  rRsvTra.SETCURRENTKEY("Hotel entrada");
                                                                  rRsvTra.SETRANGE ("Hotel entrada", Hotel);
                                                                  rRsvTra.SETFILTER("Fecha entrada", '>=%1', TODAY);
                                                                  IF rRsvTra.FINDSET(TRUE) THEN
                                                                    rRsvTra.MODIFYALL("Orden entrada", "Orden Entrada");

                                                                  rRsvTra.RESET;
                                                                  rRsvTra.SETCURRENTKEY("Hotel salida");
                                                                  rRsvTra.SETRANGE ("Hotel salida", Hotel);
                                                                  rRsvTra.SETFILTER("Fecha salida", '>=%1', TODAY);
                                                                  IF rRsvTra.FINDSET(TRUE) THEN
                                                                    rRsvTra.MODIFYALL("Orden salida", "Orden Salida");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Arrival order;
                                                              ESP=Orden Entrada] }
    { 13  ;   ;Orden Salida        ;Integer       ;OnValidate=BEGIN
                                                                {* Modificar el orden en las reservas de transfer *}

                                                                rRsvTra.RESET;
                                                                rRsvTra.SETCURRENTKEY("Hotel salida");
                                                                rRsvTra.SETRANGE("Hotel salida", Hotel);
                                                                rRsvTra.SETFILTER("Fecha salida", '>=%1', TODAY);
                                                                IF rRsvTra.FINDSET(TRUE) THEN
                                                                  rRsvTra.MODIFYALL("Orden salida", "Orden Salida");
                                                              END;

                                                   CaptionML=[ENU=Departure order;
                                                              ESP=Orden Salida] }
    { 15  ;   ;Cadena Hotelera     ;Code10        ;TableRelation="Cadena hotelera".Cadena;
                                                   CaptionML=[ENU=Hotel Chain;
                                                              ESP=Cadena Hotelera];
                                                   NotBlank=Yes;
                                                   Description=FK Cadena hotelera }
    { 17  ;   ;Catalogo            ;Boolean       ;CaptionML=[ENU=Catalog;
                                                              ESP=Catalogo] }
    { 19  ;   ;Proveedor Intermediacion;Code20    ;TableRelation=Vendor.No.;
                                                   CaptionML=[ENU=Intermediary supplier;
                                                              ESP=Proveedor Intermediacion];
                                                   NotBlank=Yes;
                                                   Description=FK Proveedor, Contratos Intermediacion }
    { 21  ;   ;Proveedor Cuenta Propia;Code20     ;TableRelation=Vendor.No.;
                                                   CaptionML=[ENU=Own account supplier;
                                                              ESP=Proveedor Cuenta Propia];
                                                   Description=FK Proveedor, Contratos Cuenta Propia }
    { 23  ;   ;Fax                 ;Text15        ;CaptionML=[ENU=Fax;
                                                              ESP=Fax] }
    { 25  ;   ;Punto Recogida      ;Text30        ;CaptionML=[ENU=Picking point;
                                                              ESP=Punto Recogida] }
    { 27  ;   ;E-Mail              ;Text80        ;CaptionML=[ENU=E-mail;
                                                              ESP=E-Mail] }
    { 50  ;   ;Touroperadores      ;Text250       ;OnLookup=VAR
                                                              lrTTOO@1103355000 : Record 7009717;
                                                              lwText@1103355001 : Text[100];
                                                            BEGIN
                                                              CLEAR(lrTTOO);
                                                              CLEAR(lwText);
                                                              IF FORM.RUNMODAL(0, lrTTOO) = ACTION::LookupOK THEN BEGIN
                                                                IF STRPOS(Touroperadores, lrTTOO.TourOperador) = 0 THEN BEGIN
                                                                  IF Touroperadores <> '' THEN
                                                                    lwText +='|';
                                                                  lwText += lrTTOO.TourOperador;
                                                                  IF MAXSTRLEN(Touroperadores) < (STRLEN(Touroperadores) + STRLEN(lwText)) THEN
                                                                    ERROR(Text011,lrTTOO.TourOperador);
                                                                  Touroperadores:= Touroperadores + lwText;
                                                                END
                                                                ELSE
                                                                  MESSAGE(Text012, lrTTOO.TourOperador);
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=TourOperators;
                                                              ESP=Touroperadores] }
    { 52  ;   ;Tiempo hasta proximo hotel;Integer ;OnValidate=BEGIN
                                                                IF "Orden Salida" = 0 THEN
                                                                  ERROR(Text017);
                                                              END;

                                                   CaptionML=[ENU=Pickup - Time to next hotel;
                                                              ESP=Pickup - Tiempo hasta proximo hotel];
                                                   MinValue=0;
                                                   Description=$003, Pickup Transfer }
    { 54  ;   ;Tiempo hasta aeropuerto;Integer    ;OnValidate=BEGIN
                                                                IF "Orden Salida" = 0 THEN
                                                                  ERROR(Text017);
                                                              END;

                                                   CaptionML=[ENU=Pickup - Time to airport;
                                                              ESP=Pickup - Tiempo hasta aeropuerto];
                                                   MinValue=0;
                                                   Description=$003, Pickup Transfer }
    { 50000;  ;N§ Reserva          ;Integer       ;CaptionML=[ENU=Booking No.;
                                                              ESP=N§ Reserva] }
    { 50002;  ;Fichero exportacion ;Text250       ;CaptionML=[ENU=Exportation file;
                                                              ESP=Fichero exportacion];
                                                   Description=Fichero para la exportacion de reservas }
    { 50003;  ;Replicado           ;Boolean       ;InitValue=No;
                                                   OnValidate=BEGIN
                                                                Replicado := FALSE;
                                                              END;

                                                   CaptionML=[ENU=Replicated;
                                                              ESP=Replicado];
                                                   Description=Campo usado por el replicador }
    { 50005;  ;Telefono            ;Text30        ;CaptionML=[ENU=Telephone;
                                                              ESP=Telefono] }
    { 50007;  ;Persona Contacto    ;Text80        ;CaptionML=[ENU=Contact person;
                                                              ESP=Persona Contacto] }
    { 50009;  ;Texto catalogo      ;Text60        ;CaptionML=[ENU=Catalog text message;
                                                              ESP=Texto catalogo] }
    { 50011;  ;Guia venta          ;Code20        ;TableRelation="Representante excursiones".Codigo WHERE (Bloqueado=CONST(No));
                                                   CaptionML=[ENU=Sale guide;
                                                              ESP=Guia venta];
                                                   Description=FK Proveedor }
    { 50012;  ;Aeropuerto Asociado ;Code10        ;TableRelation=Aeropuerto.Aeropuerto;
                                                   CaptionML=[ENU=Associated airport;
                                                              ESP=Aeropuerto Asociado] }
    { 50013;  ;Ahorro Hotel Payment;Decimal       ;OnValidate=BEGIN
                                                                IF "Ultimo cierre bonos" <> 0D THEN
                                                                  ERROR(Text013);
                                                              END;

                                                   CaptionML=[ENU=Saving Hotel payment;
                                                              ESP=Ahorro Hotel Payment] }
    { 50014;  ;Ultimo cierre bonos ;Date          ;CaptionML=[ENU=Last voucher closure;
                                                              ESP=Ultimo cierre bonos] }
    { 50015;  ;Hotel con Prepago   ;Boolean       ;CaptionML=[ENU=Hotel with payment in advance;
                                                              ESP=Hotel con Prepago] }
    { 50050;  ;Hotel Ficticio      ;Boolean       ;CaptionML=[ENU=Fictitious Hotel;
                                                              ESP=Hotel Ficticio];
                                                   Description=Determinado servicios se montan como si fueran hoteles }
    { 50051;  ;Pais                ;Code10        ;TableRelation=Country/Region.Code;
                                                   CaptionML=[ENU=Country;
                                                              ESP=Pais];
                                                   Description=Pais del Hotel }
    { 50053;  ;Fecha ultima modificacion;DateTime ;CaptionML=[ENU=Last modification date;
                                                              ESP=Fecha ultima modificacion] }
    { 50055;  ;Codigo postal       ;Code10        ;TableRelation="Post Code".Code;
                                                   OnValidate=BEGIN
                                                                PostCode.ValidatePostCode(Poblacion, "Codigo postal", Provincia);
                                                              END;

                                                   OnLookup=BEGIN
                                                              PostCode.LookUpPostCode(Poblacion, "Codigo postal", Provincia, TRUE);
                                                            END;

                                                   CaptionML=[ENU=Post Code;
                                                              ESP=Codigo postal] }
    { 50057;  ;Poblacion           ;Text30        ;OnValidate=BEGIN
                                                                PostCode.ValidatePostCode(Poblacion, "Codigo postal", Provincia);
                                                              END;

                                                   OnLookup=BEGIN
                                                              PostCode.LookUpPostCode(Poblacion, "Codigo postal", Provincia, TRUE);
                                                            END;

                                                   CaptionML=[ENU=City;
                                                              ESP=Poblacion] }
    { 50058;  ;Provincia           ;Text30        ;CaptionML=[ENU=County;
                                                              ESP=Provincia] }
    { 50059;  ;Descripcion comercial;Text250      ;OnLookup=BEGIN
                                                              _DescripcionIdiomas;
                                                            END;

                                                   CaptionML=[ENU=Commercial description;
                                                              ESP=Descripcion comercial] }
    { 50060;  ;Google Latitude     ;Code20         }
    { 50061;  ;Google Longitude    ;Code20         }
    { 50062;  ;Direccion           ;Text50        ;CaptionML=[ENU=Address;
                                                              ESP=Direccion] }
    { 50063;  ;Direccion 2         ;Text50        ;CaptionML=[ENU=Address 2;
                                                              ESP=Direccion 2] }
    { 60000;  ;Filtro fecha        ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Date filter;
                                                              ESP=Filtro fecha] }
    { 60001;  ;Filtro touroperador ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation=TourOperador.TourOperador;
                                                   CaptionML=[ENU=Touroperator filter;
                                                              ESP=Filtro touroperador];
                                                   Description=TourOperador.TourOperador }
    { 60002;  ;Filtro Habitacion   ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation="Conversion Habitacion"."Habitacion cupo";
                                                   CaptionML=[ENU=Room filter;
                                                              ESP=Filtro Habitacion];
                                                   Description="Conversion Habitacion"."Habitacion cupo" }
    { 60005;  ;Cupo compra         ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Cupos x dia compra"."Cupo contratado" WHERE (Hotel=FIELD(Hotel),
                                                                                                                 Dia=FIELD(Filtro fecha),
                                                                                                                 Grupo TourOperador=FIELD(Filtro touroperador),
                                                                                                                 Habitacion basica=FIELD(Filtro Habitacion)));
                                                   CaptionML=[ENU=Purchase Allotment;
                                                              ESP=Cupo compra];
                                                   Description=Sum("Cupos x dia compra"."Cupo contratado" WHERE (Hotel=FIELD(Hotel),Dia=FIELD(Filtro fecha),TourOperador=FIELD(Filtro touroperador)));
                                                   Editable=No }
    { 60006;  ;Reservas compra     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Reserva x dia compra"."Cantidad booking" WHERE (Hotel booking=FIELD(Hotel),
                                                                                                                    Dia=FIELD(Filtro fecha),
                                                                                                                    Grupo touroperador=FIELD(Filtro touroperador),
                                                                                                                    Habitacion=FIELD(Filtro Habitacion)));
                                                   CaptionML=[ENU=Purchase reservations;
                                                              ESP=Reservas compra];
                                                   Description=Sum("Reserva x dia compra"."Cantidad booking" WHERE (Hotel booking=FIELD(Hotel),Dia=FIELD(Filtro fecha),TourOperador=FIELD(Filtro touroperador)));
                                                   Editable=No }
    { 90000;  ;Procesado           ;Boolean       ;Description=TEMPORAL }
    { 7009700;;Codigo anterior     ;Code20        ;Description=$012 }
  }
  KEYS
  {
    {    ;Hotel                                   ;Clustered=Yes }
    {    ;Zona Fisica,Orden Entrada               ;KeyGroups=RENUMERING }
    {    ;Zona Fisica,Orden Salida                ;KeyGroups=RENUMERING }
    {    ;NoName,Proveedor Intermediacion,Proveedor Cuenta Propia;
                                                   KeyGroups=RENUMERING }
    {    ;N§ Reserva                              ;KeyGroups=RENUMERING }
    {    ;Replicado                               ;KeyGroups=RENUMERING }
    {    ;Zona Fisica,Categoria                   ;KeyGroups=RENUMERING }
    {    ;Pais                                    ;KeyGroups=RENUMERING }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1103355011 : TextConst 'ENU=Only to enter hotels in local offices is permited;ESP=Solo es posible dar de alta hoteles en las delegaciones.';
      Text002@1103355012 : TextConst 'ENU=The first three digits of the code must coincide\;ESP=Las tres primeras letras del codigo deben coincidir\';
      Text003@1103355013 : TextConst 'ENU=with the local offices code;ESP=con el codigo de la delegacion.';
      Text004@1103355014 : TextConst 'ENU=Only is possible to modifiy hotels in the local offices;ESP=Solo es posible modificar hoteles en las delegaciones.';
      Text005@1103355015 : TextConst 'ENU=Only is possible to cancel hotels in the local offices;ESP=Solo es posible dar de baja hoteles en las delegaciones.';
      Text006@1103355016 : TextConst 'ENU=There are conversions for this hotel,\;ESP=Existen conversiones para este hotel,\';
      Text007@1103355017 : TextConst 'ENU=previously you must delete them.;ESP=debe borrarlas antes.';
      Text008@1103355018 : TextConst 'ENU=Deleted;ESP=Borrado';
      Text009@1103355019 : TextConst 'ENU=Deleted register;ESP=Borrado registro';
      Text010@1103355020 : TextConst 'ENU=Only is possible to rename hotels in local offices.;ESP=Solo es posible renombrar hoteles en delegaciones.';
      Text011@1103355021 : TextConst 'ENU=Code %1 does not fill in the chain;ESP=El codigo %1 no cabe en la cadena';
      Text012@1103355022 : TextConst 'ENU=%1 touroperator already is in the chain;ESP=El touroperador %1 ya se encuentra en la cadena';
      Text013@1103355023 : TextConst 'ENU=The saving per hotel only can be modified manually before the first closure of vouchers;ESP=El ahorro por hotel solo puede modificarse manualmente antes del primer cierre de bonos.';
      Text014@1103355024 : TextConst 'ENU=Hotel %1 already has the arrival order indicated\;ESP=El hotel %1 ya tiene el orden de entrada indicado\';
      Text015@1103355025 : TextConst 'ENU=in the physical location %2.;ESP=en la zona fisica %2.';
      rDelegacion@1103355000 : Record 7009718;
      rParametros@1103355001 : Record 7009700;
      rHotelTO@1103355002 : Record 7009725;
      rRsvTra@1103355005 : Record 7009744;
      rPer@1103355006 : Record 7009742;
      rRsvHot@1103355007 : Record 7009743;
      rRsvExc@1100253000 : Record 7010162;
      PostCode@1100217003 : Record 225;
      cNom@1103355008 : Codeunit 7009702;
      cPar@1103355009 : Codeunit 7009700;
      wVentana@1103355026 : Dialog;
      Text016@1103355027 : TextConst 'ENU=Modifying future reservations\@1@@@@@@@@@;ESP=Modificando reservas futuras\@1@@@@@@@@@';
      Text017@1100217000 : TextConst 'ENU=You must indicate the departure order before entering the pickup information.;ESP=Debe indicar el orden de salida antes de introducir la informacion de pickup';
      Text018@1100217001 : TextConst 'ENU=Do you want to apply this hotel to all the excursions.;ESP=Desea aplicar este hotel a todas las excursiones.';
      Text019@1100217002 : TextConst 'ENU=Process completed.;ESP=Proceso completado.';

    PROCEDURE comp_duplica_orden@7();
    VAR
      rHotel@1103355000 : Record 7009724;
    BEGIN
      rHotel.SETCURRENTKEY(rHotel."Zona Fisica",
                           rHotel."Orden Entrada");
      rHotel.SETRANGE("Zona Fisica"  , Rec."Zona Fisica");
      rHotel.SETRANGE("Orden Entrada", Rec."Orden Entrada");
      IF rHotel.FINDFIRST THEN
        ERROR(Text014 +
              Text015, rHotel.Nombre, cNom.nom_zonafisica("Zona Fisica"));
    END;

    PROCEDURE Ense¤aTTOOs@1();
    VAR
      lwText@1103355000 : Text[250];
    BEGIN
      // Ense¤aTTOOs

      lwText := CONVERTSTR(Touroperadores,'|','\');
      MESSAGE(lwText);
    END;

    PROCEDURE InsertaLinAction@1103355000();
    VAR
      lwRec@1103355000 : RecordRef;
      lcFunExp@1103355001 : Codeunit 7009911;
    BEGIN
      // InsertaLinAction
      // Inserta una linea para el borrado de la sincronizacion a la empresa Hotel Payment

      lwRec.GETTABLE(Rec);
      lcFunExp.InsertaLinAct(lwRec);
    END;

    PROCEDURE AgregarHotelRecogida@1100217000();
    VAR
      lrExc@1100217000 : Record 7009710;
      lrRecogida@1100217001 : Record 7010184;
      lrCalendario@1000000000 : Record 7010254;
      vTurnoAnt@1000000001 : Integer;
    BEGIN
      // AgregarHotelRecogida

      TESTFIELD(NoName  , FALSE);
      TESTFIELD(Catalogo);
      TESTFIELD("Zona Fisica");

      IF NOT CONFIRM(Text018) THEN
        EXIT;

      lrExc.RESET;
      lrExc.SETRANGE(Caducado, FALSE);
      IF lrExc.FINDSET THEN
      BEGIN
        REPEAT
          lrCalendario.RESET;
          lrCalendario.SETCURRENTKEY(Excursion, Turno);
          lrCalendario.SETRANGE (Excursion     , lrExc.Codigo);
          lrCalendario.SETFILTER("Fecha inicio", '<=%1', TODAY);
          lrCalendario.SETFILTER("Fecha final" , '>=%1', TODAY);
          IF lrCalendario.FINDFIRST THEN
          BEGIN
            REPEAT
              IF lrCalendario.Turno <> vTurnoAnt THEN
              BEGIN
                vTurnoAnt := lrCalendario.Turno;

                IF NOT lrRecogida.GET(Hotel, lrExc.Codigo, lrCalendario.Turno) THEN
                BEGIN
                  CLEAR(lrRecogida);
                  lrRecogida.VALIDATE(Hotel    , Hotel);
                  lrRecogida.VALIDATE(Excursion, lrExc.Codigo);
                  lrRecogida.VALIDATE(Turno    , lrCalendario.Turno);
                  EVALUATE(lrRecogida."Hora recogida", '00:00');
                  lrRecogida.INSERT;
                END;
              END;
            UNTIL lrCalendario.NEXT = 0;
          END;
        UNTIL lrExc.NEXT = 0;
      END;

      MESSAGE(Text019);
    END;

    LOCAL PROCEDURE _DescripcionIdiomas@1100217001();
    VAR
      lrDescrip@1100217000 : Record 7009922;
      lfDescrip@1100217001 : Form 7009758;
    BEGIN
      // _DescripcionIdiomas

      lrDescrip.RESET;
      lrDescrip.SETRANGE(Tabla , DATABASE::Hotel);
      lrDescrip.SETRANGE(Code20, Hotel);

      CLEAR(lfDescrip);
      lfDescrip.SETTABLEVIEW(lrDescrip);
      lfDescrip.RUN;
    END;

    LOCAL PROCEDURE _UpdateNombreRecogidas@1000000000();
    VAR
      lrRecogida@1000000000 : Record 7010184;
    BEGIN
      // _UpdateNombreRecogidas

      lrRecogida.RESET;
      lrRecogida.SETRANGE(Hotel, Hotel);
      IF lrRecogida.FINDSET(TRUE) THEN
        lrRecogida.MODIFYALL("Nombre hotel", Nombre);
    END;

    LOCAL PROCEDURE _NewHotel@1000000001();
    VAR
      lrZona@1000000000 : Record 7009713;
      lwNumero@1000000002 : Integer;
      lwNumeroTexto@1000000003 : Code[10];
      lwCeros@1000000004 : Code[10];
    BEGIN
      // _NewHotel

      lrZona.RESET;
      IF FORM.RUNMODAL(0, lrZona) <> ACTION::LookupOK THEN
        EXIT;

      lrZona.TESTFIELD("ID zona para hoteles");

      rParametros.FINDFIRST;
      rDelegacion.GET(rParametros.Delegacion);
      lwNumero := rDelegacion."Ultimo ID hotel";
      lwNumero += 1;
      rDelegacion."Ultimo ID hotel" := lwNumero;
      rDelegacion.MODIFY;

      lwNumeroTexto := FORMAT(lwNumero);
      lwNumeroTexto := PADSTR(lwCeros, 5 - STRLEN(lwNumeroTexto), '0') + lwNumeroTexto;

      Hotel         := rDelegacion.Codigo + lrZona."ID zona para hoteles" + lwNumeroTexto;
      "Zona Fisica" := lrZona."Zona Fisica";
    END;

    BEGIN
    {
      // AJS 23.01.2008

        - Incluimos un nuevo campo "Hotel con Prepago" para marcar aquellos hoteles cuyas facturas hay
          que prepagar

      ## AJF 15/06/09

        .- (09/00279) A¤adido campo para indicar el pa¡s del hotel y Clave de ordenaci¢n.

      $001 AJS 03082010 Creados nuevos campos Filtro fecha, Filtro touroperador y Filtro Habitacion para utilizar en los campos calculados
                        nuevos Cupo compra y Reservas compra

      $002 AJS 23062011 Cuando se modifique la zona fisica de un hotel, se actualizan las reservas de excursion

      $003 AJS 25022013 Nuevos campos "Tiempo hasta proximo hotel" y "Tiempo hasta aeropuerto" para el calculo de los tiempos de recogida
                        en los traslados de salida

      $004 AJS 06052014 Nuevo funcion AgregarHotelRecogida para agregar un hotel a todas las excursiones activas

      $005 AJS 27062014 Elimino la variable Booking compra

      $006 AJS 02072014 Nuevo campo "Fecha ultima modificacion"

      $007 AJS 03072014 Nuevos campos "Codigo postal", Poblacion, Provincia y "Descripcion comercial", para la integracion con SIAR

      $008 AJS 03072014 Nuevos campos "Google Latitude" y "Google Longitude" para la geoposicion

      $009 AJS 03072014 Nuevos campos Direccion y "Direccion 2"

      $010 AJS 25092014 Modifico la funcion AgregarHotelRecogida para crear recogidas para todos los turnos y corregir un error que no
                        rellenaba nombre de hotel

      $011 AJS 09032015 Nueva funcion _NewHotel para codificar los hoteles a partir de la zona fisica

      $012 AJS 09032015 Nuevo campo "Codigo anterior" que emplearemos temporalmente en aquellas empresas en que se lance
                        el proceso de recodificaci¢n
    }
    END.
  }
}
