OBJECT Table 5718 Nonstock Item
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               NonStockItem.LOCKTABLE;
               IF "Entry No." = '' THEN BEGIN
                 GetInvtSetup;
                 InvtSetup.TESTFIELD("Nonstock Item Nos.");
                 NoSeriesMgt.InitSeries(InvtSetup."Nonstock Item Nos.",xRec."No. Series",0D,"Entry No.","No. Series");
               END;
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;
               MfrLength := STRLEN("Manufacturer Code");
               VenLength := STRLEN("Vendor Item No.");

               NonStockItemSetup.GET;
               CASE NonStockItemSetup."No. Format" OF
                 NonStockItemSetup."No. Format"::"Entry No.":
                   ItemNo := "Entry No.";
                 NonStockItemSetup."No. Format"::"Vendor Item No.":
                   ItemNo := "Vendor Item No.";
                 NonStockItemSetup."No. Format"::"Mfr. + Vendor Item No.":
                   BEGIN
                     IF NonStockItemSetup."No. Format Separator" = '' THEN BEGIN
                       IF MfrLength + VenLength <= 20 THEN
                         ItemNo := INSSTR("Manufacturer Code","Vendor Item No.",6)
                       ELSE
                         ItemNo := INSSTR("Manufacturer Code","Entry No.",6);
                     END ELSE BEGIN
                       TempItemNo :=
                         INSSTR("Manufacturer Code",NonStockItemSetup."No. Format Separator",6);
                       IF MfrLength + VenLength < 20 THEN
                         ItemNo := INSSTR(TempItemNo,"Vendor Item No.",7)
                       ELSE
                         ItemNo := INSSTR(TempItemNo,"Entry No.",7);
                     END;
                   END;
                 NonStockItemSetup."No. Format"::"Vendor Item No. + Mfr.":
                   BEGIN
                     IF NonStockItemSetup."No. Format Separator" = '' THEN BEGIN
                       IF VenLength + MfrLength <= 20 THEN
                         ItemNo := INSSTR("Vendor Item No.","Manufacturer Code",11)
                       ELSE
                         ItemNo := INSSTR("Entry No.","Manufacturer Code",11);
                     END ELSE BEGIN
                       TempItemNo :=
                         INSSTR("Vendor Item No.",NonStockItemSetup."No. Format Separator",10);
                       IF VenLength + MfrLength < 20 THEN
                         ItemNo := INSSTR(TempItemNo,"Manufacturer Code",11);
                     END;
                   END;
               END;
               Item.SETRANGE(Item."No.",ItemNo);
               IF Item.FIND('-') THEN
                 ERROR(Text001);
             END;

    CaptionML=[ENU=Nonstock Item;
               ESP=Producto sin stock];
    LookupFormID=Form5726;
    DrillDownFormID=Form5726;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Code20        ;OnValidate=BEGIN
                                                                IF "Entry No." <> xRec."Entry No." THEN BEGIN
                                                                  GetInvtSetup;
                                                                  NoSeriesMgt.TestManual(InvtSetup."Nonstock Item Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Entry No.;
                                                              ESP=N§ mov.];
                                                   Editable=Yes }
    { 2   ;   ;Manufacturer Code   ;Code10        ;TableRelation=Manufacturer.Code;
                                                   OnValidate=BEGIN
                                                                IF ("Manufacturer Code" <> xRec."Manufacturer Code") AND
                                                                   ("Item No." <> '')THEN
                                                                  ERROR(Text001);
                                                              END;

                                                   CaptionML=[ENU=Manufacturer Code;
                                                              ESP=C¢d. fabricante] }
    { 3   ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor.No.;
                                                   OnValidate=BEGIN
                                                                IF ("Vendor No." <> xRec."Vendor No.") AND
                                                                   ("Item No." <> '')THEN
                                                                  ERROR(Text001);

                                                                IF "Vendor No." <> xRec."Vendor No." THEN
                                                                  IF CheckVendorItemNo("Vendor No.","Vendor Item No.") THEN
                                                                    ERROR(Text002,"Vendor No.","Vendor Item No.");
                                                              END;

                                                   CaptionML=[ENU=Vendor No.;
                                                              ESP=N§ proveedor] }
    { 4   ;   ;Vendor Item No.     ;Code20        ;OnValidate=BEGIN
                                                                IF ("Vendor Item No." <> xRec."Vendor Item No.") AND
                                                                   ("Item No." <> '')THEN
                                                                  ERROR(Text001);

                                                                IF "Vendor Item No." <> xRec."Vendor Item No." THEN
                                                                  IF CheckVendorItemNo("Vendor No.","Vendor Item No.") THEN
                                                                    ERROR(Text002,"Vendor No.","Vendor Item No.");
                                                              END;

                                                   CaptionML=[ENU=Vendor Item No.;
                                                              ESP=C¢d. producto proveedor] }
    { 5   ;   ;Description         ;Text30        ;OnValidate=BEGIN
                                                                IF (Description <> xRec.Description) AND
                                                                   ("Item No." <> '')THEN
                                                                  ERROR(Text001);
                                                              END;

                                                   CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n];
                                                   Editable=Yes }
    { 6   ;   ;Unit of Measure     ;Code10        ;TableRelation="Unit of Measure";
                                                   OnValidate=BEGIN
                                                                IF ("Unit of Measure" <> xRec."Unit of Measure") AND
                                                                   ("Item No." <> '')THEN
                                                                  ERROR(Text001);
                                                              END;

                                                   CaptionML=[ENU=Unit of Measure;
                                                              ESP=Unidad medida] }
    { 7   ;   ;Published Cost      ;Decimal       ;OnValidate=BEGIN
                                                                IF ("Published Cost" <> xRec."Published Cost") AND
                                                                   ("Item No." <> '')THEN
                                                                  ERROR(Text001);
                                                              END;

                                                   CaptionML=[ENU=Published Cost;
                                                              ESP=Coste publicado];
                                                   AutoFormatType=1 }
    { 8   ;   ;Negotiated Cost     ;Decimal       ;OnValidate=BEGIN
                                                                IF ("Negotiated Cost" <> xRec."Negotiated Cost") AND
                                                                   ("Item No." <> '') THEN
                                                                  ERROR(Text001);
                                                              END;

                                                   CaptionML=[ENU=Negotiated Cost;
                                                              ESP=Coste negociado];
                                                   AutoFormatType=1 }
    { 9   ;   ;Unit Price          ;Decimal       ;OnValidate=BEGIN
                                                                IF ("Unit Price" <> xRec."Unit Price") AND
                                                                   ("Item No." <> '') THEN
                                                                  ERROR(Text001);
                                                              END;

                                                   CaptionML=[ENU=Unit Price;
                                                              ESP=Precio venta];
                                                   AutoFormatType=1 }
    { 10  ;   ;Gross Weight        ;Decimal       ;OnValidate=BEGIN
                                                                IF ("Gross Weight" <> xRec."Gross Weight") AND
                                                                   ("Item No." <> '') THEN
                                                                  ERROR(Text001);
                                                              END;

                                                   CaptionML=[ENU=Gross Weight;
                                                              ESP=Peso bruto];
                                                   DecimalPlaces=0:5 }
    { 11  ;   ;Net Weight          ;Decimal       ;OnValidate=BEGIN
                                                                IF ("Net Weight" <> xRec."Net Weight") AND
                                                                   ("Item No." <> '') THEN
                                                                  ERROR(Text001);
                                                              END;

                                                   CaptionML=[ENU=Net Weight;
                                                              ESP=Peso neto];
                                                   DecimalPlaces=0:5 }
    { 12  ;   ;Item Category Code  ;Code10        ;TableRelation="Item Category";
                                                   OnValidate=BEGIN
                                                                IF ("Item Category Code" <> xRec."Item Category Code") AND
                                                                   ("Item No." <> '')THEN
                                                                  ERROR(Text001);

                                                                "Product Group Code" := '';
                                                              END;

                                                   CaptionML=[ENU=Item Category Code;
                                                              ESP=C¢d. categor¡a producto] }
    { 13  ;   ;Product Group Code  ;Code10        ;TableRelation="Product Group".Code WHERE (Item Category Code=FIELD(Item Category Code));
                                                   OnValidate=BEGIN
                                                                IF ("Product Group Code" <> xRec."Product Group Code") AND
                                                                   ("Item No." <> '')THEN
                                                                  ERROR(Text001);
                                                              END;

                                                   CaptionML=[ENU=Product Group Code;
                                                              ESP=C¢d. grupo producto] }
    { 14  ;   ;Last Date Modified  ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              ESP=Fecha £lt. modificaci¢n];
                                                   Editable=No }
    { 15  ;   ;Bar Code            ;Code20        ;OnValidate=BEGIN
                                                                IF ("Bar Code" <> xRec."Bar Code") AND
                                                                   ("Item No." <> '')THEN
                                                                  ERROR(Text001);
                                                              END;

                                                   CaptionML=[ENU=Bar Code;
                                                              ESP=C¢d. barras] }
    { 16  ;   ;Item No.            ;Code20        ;TableRelation=Item.No.;
                                                   OnValidate=BEGIN
                                                                IF ("Item No." <> xRec."Item No.") AND
                                                                   ("Item No." <> '')THEN
                                                                  ERROR(Text001);
                                                              END;

                                                   CaptionML=[ENU=Item No.;
                                                              ESP=N§ producto];
                                                   Editable=No }
    { 53  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Comment Line" WHERE (Table Name=CONST(Nonstock Item),
                                                                                           No.=FIELD(Entry No.)));
                                                   CaptionML=[ENU=Comment;
                                                              ESP=Comentario];
                                                   Editable=No }
    { 97  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              ESP=Nos. serie];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Vendor Item No.,Manufacturer Code        }
    {    ;Item No.                                 }
    {    ;Vendor No.,Vendor Item No.               }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=Modification not allowed, item record already exists.;ESP=Modificaci¢n no permitida, el producto ya existe.';
      NonStockItem@1002 : Record 5718;
      NonStockItemSetup@1003 : Record 5719;
      Item@1004 : Record 27;
      InvtSetup@1013 : Record 313;
      NoSeriesMgt@1011 : Codeunit 396;
      ItemNo@1005 : Code[20];
      TempItemNo@1006 : Code[20];
      MfrLength@1008 : Integer;
      VenLength@1009 : Integer;
      Text002@1010 : TextConst 'ENU="No.=<%1> and Vendor Item No.=<%2> already exists.";ESP="Ya existen N§=<%1> y C¢d. producto proveedor.=<%2>."';
      HasInvtSetup@1012 : Boolean;

    PROCEDURE AssistEdit@2() : Boolean;
    BEGIN
      GetInvtSetup;
      InvtSetup.TESTFIELD("Nonstock Item Nos.");
      IF NoSeriesMgt.SelectSeries(InvtSetup."Nonstock Item Nos.",xRec."No. Series","No. Series") THEN BEGIN
        NoSeriesMgt.SetSeries("Entry No.");
        EXIT(TRUE);
      END;
    END;

    PROCEDURE CheckVendorItemNo@1(VendorNo@1000 : Code[20];VendorItemNo@1001 : Code[20]) : Boolean;
    BEGIN
      NonStockItem.RESET;
      NonStockItem.SETCURRENTKEY("Vendor No.","Vendor Item No.");
      NonStockItem.SETRANGE("Vendor No.",VendorNo);
      NonStockItem.SETRANGE("Vendor Item No.",VendorItemNo);
      EXIT(NonStockItem.FIND('-'));
    END;

    PROCEDURE GetInvtSetup@14();
    BEGIN
      IF NOT HasInvtSetup THEN BEGIN
        InvtSetup.GET;
        HasInvtSetup := TRUE;
      END;
    END;

    BEGIN
    END.
  }
}
