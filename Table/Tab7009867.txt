OBJECT Table 7009867 Linea Proforma
{
  OBJECT-PROPERTIES
  {
    Date=11/10/12;
    Time=18:00:47;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ProformaPendiente;
             END;

    OnModify=BEGIN
               ProformaPendiente;
             END;

    OnDelete=BEGIN
               ProformaPendiente;

               // AJF 08/06/09
               // Si es una l¡nea de excursi¢n la desvinculamos de la proforma.
               DesmarcarExcursion;
             END;

    CaptionML=[ENU=Proforma invoice Line;
               ESP=Linea Proforma];
    LookupFormID=Form7010004;
    DrillDownFormID=Form7010004;
  }
  FIELDS
  {
    { 1   ;   ;Num. Proforma       ;Code20        ;TableRelation="Cabecera Proforma"."Num. Proforma";
                                                   CaptionML=[ENU=Pro-forma invoice No.;
                                                              ESP=Num. Proforma] }
    { 3   ;   ;Num. Linea          ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[ENU=Line No.;
                                                              ESP=Num. Linea] }
    { 5   ;   ;Tipo producto       ;Option        ;OnValidate=BEGIN
                                                                // AJF 26/05/08
                                                                // Para otros no se debe especificar producto.
                                                                IF "Tipo producto" = "Tipo producto"::Otros THEN
                                                                  Producto := '';
                                                              END;

                                                   CaptionML=[ENU=Item Type;
                                                              ESP=Tipo Producto];
                                                   OptionCaptionML=[ENU=Hotel,Transfer,Excursions,Others,Circuit;
                                                                    ESP=Hotel,Transfer,Excursiones,Otros,Circuito];
                                                   OptionString=Hotel,Transfer,Excursiones,Otros,Circuito }
    { 7   ;   ;Producto            ;Code10        ;OnValidate=BEGIN
                                                                // AJF 26/05/08
                                                                // Para otros no se debe especificar producto.
                                                                IF "Tipo producto" = "Tipo producto"::Otros THEN
                                                                  Producto := '';

                                                                // AJF 22/12/08
                                                                // A¤ado las descripciones de los productos como est n en el report de proforma.

                                                                IF Producto <> '' THEN BEGIN
                                                                  CASE "Tipo producto" OF
                                                                    "Tipo producto"::Circuito :
                                                                       Descripcion := cNom.nom_circuito(Producto);
                                                                    ELSE
                                                                       Descripcion := cNom.nom_producto(Producto);
                                                                  END;
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              lfProducto@1103355000 : Form 7009710;
                                                              lrProducto@1103355001 : Record 7009710;
                                                            BEGIN
                                                              IF "Tipo producto" <> "Tipo producto"::Circuito THEN BEGIN

                                                                CASE "Tipo producto" OF
                                                                "Tipo producto"::Hotel:     lrProducto.SETRANGE("Tipo Producto", lrProducto."Tipo Producto"::Hotel);
                                                                "Tipo producto"::Transfer:  lrProducto.SETRANGE("Tipo Producto", lrProducto."Tipo Producto"::Transfer);
                                                                "Tipo producto"::Excursiones: lrProducto.SETRANGE("Tipo Producto", lrProducto."Tipo Producto"::Excursion);
                                                                END;

                                                                lfProducto.SETRECORD    (lrProducto);
                                                                lfProducto.SETTABLEVIEW (lrProducto);
                                                                lfProducto.LOOKUPMODE := TRUE;
                                                                IF lfProducto.RUNMODAL IN [ACTION::LookupOK, ACTION::OK] THEN BEGIN
                                                                  lrProducto.RESET;
                                                                  lfProducto.GETRECORD(lrProducto);
                                                                  Producto := lrProducto.Codigo;
                                                                END;
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Item;
                                                              ESP=Producto];
                                                   Description=Multi-Relacion (producto o circuito) }
    { 9   ;   ;Num. Reserva        ;Integer       ;TableRelation=IF (Tipo producto=CONST(Hotel)) "Reserva Hotel"."N§ Reserva"
                                                                 ELSE IF (Tipo producto=CONST(Transfer)) "Reserva Transfer"."N§ Reserva"
                                                                 ELSE IF (Tipo producto=CONST(Excursiones)) "Reserva Excursion"."N§ Reserva";
                                                   CaptionML=[ENU=Reservation No.;
                                                              ESP=Num. Reserva];
                                                   Description=IF (Tipo producto=CONST(Hotel)) "Reserva Hotel"."N§ Reserva" ELSE IF (Tipo producto=CONST(Transfer)) "Reserva Transfer"."N§ Reserva" ELSE IF (Tipo producto=CONST(Excursiones)) "Reserva Excursion"."N§ Reserva" }
    { 11  ;   ;Fecha               ;Date          ;CaptionML=[ENU=Date;
                                                              ESP=Fecha] }
    { 13  ;   ;Habitacion          ;Code10        ;CaptionML=[ENU=Room;
                                                              ESP=Habitacion] }
    { 15  ;   ;Regimen             ;Code10        ;CaptionML=[ENU=Board;
                                                              ESP=Regimen] }
    { 17  ;   ;Estancia            ;Integer       ;CaptionML=[ENU=Stay;
                                                              ESP=Estancia] }
    { 19  ;   ;Importe             ;Decimal       ;CaptionML=[ENU=Amount;
                                                              ESP=Importe];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Cod. Divisa" }
    { 21  ;   ;Cod. Divisa         ;Code10        ;TableRelation=Currency.Code;
                                                   CaptionML=[ENU=Currency Code;
                                                              ESP=Cod. divisa];
                                                   Description=Currency.Code;
                                                   Editable=No }
    { 23  ;   ;Zona Hotel          ;Code10        ;TableRelation="Zona fisica"."Zona Fisica";
                                                   CaptionML=[ENU=Hotel zone;
                                                              ESP=Zona hotel];
                                                   Description="Zona fisica"."Zona Fisica" }
    { 24  ;   ;Cod. Hotel          ;Code20        ;TableRelation=Hotel.Hotel;
                                                   CaptionML=[ENU=Hotel Code;
                                                              ESP=Cod. Hotel];
                                                   Description=Hotel.Hotel }
    { 25  ;   ;Adultos             ;Integer       ;CaptionML=[ENU=Adults;
                                                              ESP=Adultos] }
    { 27  ;   ;Ni¤os               ;Integer       ;CaptionML=[ENU=Children;
                                                              ESP=Ni¤os] }
    { 29  ;   ;Bono                ;Code10        ;CaptionML=[ENU=Voucher;
                                                              ESP=Bono] }
    { 33  ;   ;Descripcion         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESP=Descripcion];
                                                   Description=Descripci¢n para el tipo producto Otros. }
    { 34  ;   ;N§ Interno          ;Integer       ;CaptionML=[ENU=Internal No.;
                                                              ESP=N§ Interno];
                                                   Description=Mantenemos N§ Interno, N§ Periodo, para facilitar el tratamiento de lineas }
    { 35  ;   ;N§ Periodo          ;Integer       ;CaptionML=[ENU=Period No.;
                                                              ESP=N§ Periodo] }
    { 36  ;   ;N§ Habitaciones     ;Integer       ;CaptionML=[ENU=Rooms No.;
                                                              ESP=N§ Habitaciones] }
    { 37  ;   ;Voucher propio      ;Boolean       ;CaptionML=[ENU=Own Voucher;
                                                              ESP=Voucher propio];
                                                   Description=En caso de estar marcado ignora los adjuntos y las configuraciones x pais y touroperador para la impresion del voucher. }
  }
  KEYS
  {
    {    ;Num. Proforma,Num. Linea                ;SumIndexFields=Importe;
                                                   Clustered=Yes }
    {    ;Tipo producto,Num. Reserva              ;KeyGroups=RENUMERING }
    {    ;N§ Interno,N§ Periodo                   ;KeyGroups=RENUMERING }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1103355000 : TextConst 'ENU="Proforma invoice in status %1 cannot be modified ";ESP=No se pueden modificar las proformas en estado %1';
      cNom@1103355001 : Codeunit 7009702;

    PROCEDURE ProformaPendiente@1103355000();
    VAR
      lrCab@1103355000 : Record 7009866;
    BEGIN
      // ProformaPendiente

      lrCab.GET("Num. Proforma");

      IF lrCab.Estado <> lrCab.Estado::Pendiente THEN
        ERROR(Text001, lrCab.Estado);
    END;

    LOCAL PROCEDURE DesmarcarExcursion@1103355001();
    VAR
      lrResExc@1103355000 : Record 7010162;
    BEGIN
      // DesmarcarExcursion
      //: Desmarcamos la reserva excursion correspondiente a la l¡nea de proforma
      //: para indicar que ya no forma parte de la misma.

      IF "Tipo producto" <> "Tipo producto"::Excursiones THEN
        EXIT;

      lrResExc.SETRANGE("N§ Reserva", "Num. Reserva");

      IF lrResExc.FINDFIRST THEN BEGIN
        lrResExc."N§ Proforma" := '';
        lrResExc.MODIFY;
      END;
    END;

    PROCEDURE TieneAdjuntos@1103355002() rvTieneAdj : Boolean;
    VAR
      lrAdjts@1103355000 : Record 7009886;
    BEGIN
      // TieneAdjuntos.

      lrAdjts.SETCURRENTKEY("Num. Proforma", "Lin. Proforma");
      lrAdjts.SETRANGE ("Num. Proforma", "Num. Proforma");
      lrAdjts.SETRANGE ("Lin. Proforma", "Num. Linea");

      EXIT(NOT lrAdjts.ISEMPTY);
    END;

    PROCEDURE VerAdjuntos@1103355003();
    VAR
      lrAdjts@1103355000 : Record 7009886;
      lfAdjts@1103355001 : Form 7010017;
    BEGIN
      // VerAdjuntos.

      IF ("Num. Proforma" = '') OR ("Num. Linea" = 0) THEN
        EXIT;

      lrAdjts.SETCURRENTKEY("Num. Proforma", "Lin. Proforma");
      lrAdjts.SETRANGE ("Num. Proforma", "Num. Proforma");
      lrAdjts.SETRANGE ("Lin. Proforma", "Num. Linea");

      lfAdjts.SETTABLEVIEW(lrAdjts);
      lfAdjts.RUNMODAL;
    END;

    BEGIN
    {

      ## AJF 08/06/09

        .- Al borrar una l¡nea de proforma de excursiones se debe desvincular la reserva de la proforma.
    }
    END.
  }
}
