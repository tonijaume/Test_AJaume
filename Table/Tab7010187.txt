OBJECT Table 7010187 Circuito
{
  OBJECT-PROPERTIES
  {
    Date=04/03/14;
    Time=[ 9:51:35];
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    OnInsert=VAR
               rCir@1103355000 : Record 7010187;
             BEGIN
               rPar.RESET;
               IF NOT rPar.FIND('-') THEN
                 ERROR(Text000);

               Delegacion := rPar.Delegacion;

               rCir.RESET;
               rCir.SETRANGE(Delegacion, Delegacion);
               rCir.SETRANGE(Circuito  , Circuito);
               IF rCir.FIND('+') THEN
                 Orden := rCir.Orden + 1
               ELSE
                 Orden := 1;

               Crear_Prestaciones_Circuito;
             END;

    OnModify=VAR
               rPaxTO@1103355000 : Record 7010188;
             BEGIN
               IF Fecha <> xRec.Fecha THEN BEGIN
                 rPaxTO.RESET;
                 rPaxTO.SETRANGE(Delegacion, Delegacion);
                 rPaxTO.SETRANGE(Circuito  , Circuito);
                 rPaxTO.SETRANGE(Orden     , Orden);
                 IF rPaxTO.FIND('-') THEN
                   rPaxTO.MODIFYALL(Fecha, Fecha);
               END;
             END;

    OnDelete=VAR
               rPaxTO@1103355000 : Record 7010188;
               rCosCir@1103355001 : Record 7010190;
               rResCir@1103355002 : Record 7010186;
               rResCir2@1103355003 : Record 7010186;
               rHotCir@1103355004 : Record 7010189;
               rPresXcir@1103355005 : Record 7010211;
               rExtCir@1103355006 : Record 7010213;
             BEGIN
               IF Facturado THEN
                 ERROR(Text001);

               rPaxTO.RESET;
               rPaxTO.SETRANGE(Delegacion, Delegacion);
               rPaxTO.SETRANGE(Circuito  , Circuito);
               rPaxTO.SETRANGE(Orden     , Orden);
               IF rPaxTO.FIND('-') THEN BEGIN
                 IF CONFIRM(Text002, FALSE, rResCir.TABLECAPTION, FIELDCAPTION(Circuito), FIELDCAPTION(Fecha)) THEN
                   rPaxTO.DELETEALL(TRUE)
                 ELSE BEGIN
                   rResCir.SETCURRENTKEY(Delegacion, Circuito, Orden, "N§ linea");
                   rResCir.SETRANGE(Delegacion, Delegacion);
                   rResCir.SETRANGE(Circuito  , Circuito);
                   rResCir.SETRANGE(Orden     , Orden);
                   IF rResCir.FIND('-') THEN
                     REPEAT
                       rResCir2 := rResCir;
                       rResCir2."N§ Interno" := 0;
                       rResCir2.Circuito     := '';
                       rResCir2.Orden        := 0;
                       rResCir2."N§ linea"   := 0;
                       rResCir2.MODIFY;
                     UNTIL rResCir.NEXT = 0;
                   rHotCir.RESET;
                   rHotCir.SETRANGE(Delegacion, Delegacion);
                   rHotCir.SETRANGE(Circuito  , Circuito);
                   rHotCir.SETRANGE(Orden     , Orden);
                   IF rHotCir.FIND('-') THEN
                     rHotCir.DELETEALL;
                   rPaxTO.DELETEALL;
                 END;
               END;

               rPresXcir.RESET;
               rPresXcir.SETRANGE(Circuito, Circuito);
               rPresXcir.SETRANGE(Orden   , Orden);
               IF rPresXcir.FIND('-') THEN
                 rPresXcir.DELETEALL;

               rCosCir.RESET;
               rCosCir.SETRANGE(Delegacion, Delegacion);
               rCosCir.SETRANGE(Circuito  , Circuito);
               rCosCir.SETRANGE(Orden     , Orden);
               IF rCosCir.FIND('-') THEN
                 rCosCir.DELETEALL;

               rExtCir.RESET;
               rExtCir.SETRANGE(Delegacion, Delegacion);
               rExtCir.SETRANGE(Circuito  , Circuito);
               rExtCir.SETRANGE(Orden     , Orden);
               IF rExtCir.FIND('-') THEN
                 rExtCir.DELETEALL;
             END;

    OnRename=BEGIN
               ERROR(Text003);
             END;

    CaptionML=[ENU=Roundtrip service;
               ESP=Circuito];
    LookupFormID=Form7010256;
    DrillDownFormID=Form7010256;
  }
  FIELDS
  {
    { 1   ;   ;Delegacion          ;Code10        ;TableRelation=Delegacion.Codigo;
                                                   CaptionML=[ENU=Local Office;
                                                              ESP=Delegacion];
                                                   Description=PK, FK Delegaciones;
                                                   Editable=No }
    { 3   ;   ;Circuito            ;Code10        ;TableRelation="Maestro circuitos".Circuito;
                                                   CaptionML=[ENU=Roundtrip;
                                                              ESP=Circuito];
                                                   NotBlank=Yes;
                                                   Description=PK, FK Maestro Circuitos }
    { 5   ;   ;Orden               ;Integer       ;CaptionML=[ENU=Order;
                                                              ESP=Orden];
                                                   Description=PK;
                                                   Editable=No }
    { 7   ;   ;Fecha               ;Date          ;OnValidate=VAR
                                                                rPaxTOcir@1103355000 : Record 7010188;
                                                              BEGIN
                                                                IF "Documentacion Emitida" THEN BEGIN
                                                                  IF NOT CONFIRM(Text004) THEN
                                                                    Fecha := xRec.Fecha
                                                                  ELSE BEGIN
                                                                    "Documentacion Emitida" := FALSE;
                                                                    "Fecha Emision" := 0D;
                                                                  END;
                                                                END;

                                                                IF Fecha <> xRec.Fecha THEN
                                                                  Crear_Prestaciones_Circuito;

                                                                rPaxTOcir.RESET;
                                                                rPaxTOcir.SETRANGE(Delegacion, Delegacion);
                                                                rPaxTOcir.SETRANGE(Circuito  , Circuito);
                                                                rPaxTOcir.SETRANGE(Orden     , Orden);
                                                                IF rPaxTOcir.FIND('-') THEN
                                                                  REPEAT
                                                                    rPaxTOcir.VALIDATE(Fecha, Fecha);
                                                                    rPaxTOcir.MODIFY;
                                                                  UNTIL rPaxTOcir.NEXT = 0;
                                                              END;

                                                   CaptionML=[ENU=Date;
                                                              ESP=Fecha];
                                                   NotBlank=Yes }
    { 9   ;   ;Descripcion         ;Text30        ;CaptionML=[ENU=Description;
                                                              ESP=Descripcion] }
    { 10  ;   ;Filtro TO           ;Code10        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=TO Filter;
                                                              ESP=Filtro TO];
                                                   Description=FlowFilter }
    { 11  ;   ;Ingreso             ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("C/I Circuito"."Importe Con Impuesto" WHERE (Delegacion=FIELD(Delegacion),
                                                                                                                Circuito=FIELD(Circuito),
                                                                                                                Orden=FIELD(Orden),
                                                                                                                Coste/Ingreso=CONST(Ingreso),
                                                                                                                TourOperador=FIELD(Filtro TO),
                                                                                                                N§ Factura=FIELD(Filtro N§ Factura)));
                                                   CaptionML=[ENU=Income;
                                                              ESP=Ingreso];
                                                   Description=Calculado sobre C/I Circuito;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 13  ;   ;Coste               ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("C/I Circuito"."Importe Con Impuesto" WHERE (Delegacion=FIELD(Delegacion),
                                                                                                                Circuito=FIELD(Circuito),
                                                                                                                Orden=FIELD(Orden),
                                                                                                                Coste/Ingreso=CONST(Coste)));
                                                   CaptionML=[ENU=Cost;
                                                              ESP=Coste];
                                                   Description=Calculado sobre C/I Circuito;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 17  ;   ;Paxes               ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Reserva Circuito" WHERE (Delegacion=FIELD(Delegacion),
                                                                                               Circuito=FIELD(Circuito),
                                                                                               Orden=FIELD(Orden)));
                                                   CaptionML=[ENU=Paxes;
                                                              ESP=Paxes];
                                                   Description=Calculado sobre Reserva Circuito;
                                                   Editable=No }
    { 18  ;   ;Paxes Calculo       ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Reserva Circuito" WHERE (Delegacion=FIELD(Delegacion),
                                                                                               Circuito=FIELD(Circuito),
                                                                                               Orden=FIELD(Orden),
                                                                                               TourOperador=FIELD(Filtro TO),
                                                                                               Fecha Entrada=FIELD(Filtro Fecha Entrada),
                                                                                               Fecha Salida=FIELD(Filtro Fecha Salida)));
                                                   CaptionML=[ENU=Calculation Paxes;
                                                              ESP=Paxes Calculo];
                                                   Description=Calculado sobre Reserva Circuito;
                                                   Editable=No }
    { 19  ;   ;C¢d. divisa         ;Code10        ;TableRelation=Currency.Code;
                                                   CaptionML=[ENU=Currency exchange;
                                                              ESP=C¢d. divisa];
                                                   Description=FK Divisa }
    { 21  ;   ;Documentacion Emitida;Boolean      ;CaptionML=[ENU=Documents printed;
                                                              ESP=Documentacion Emitida];
                                                   Editable=No }
    { 23  ;   ;Facturado           ;Boolean       ;CaptionML=[ENU=Invoiced;
                                                              ESP=Facturado];
                                                   Editable=No }
    { 25  ;   ;Fecha Emision       ;Date          ;CaptionML=[ENU=Date of issue;
                                                              ESP=Fecha Emision];
                                                   Editable=No }
    { 27  ;   ;N§ Vehiculos        ;Integer       ;InitValue=1;
                                                   CaptionML=[ENU=Vehicles No.;
                                                              ESP=N§ vehiculos] }
    { 29  ;   ;Coste Hoteles       ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Pax TO-Circuito"."Coste Hoteles" WHERE (Delegacion=FIELD(Delegacion),
                                                                                                            Circuito=FIELD(Circuito),
                                                                                                            Orden=FIELD(Orden)));
                                                   CaptionML=[ENU=Hotels cost;
                                                              ESP=Coste Hoteles];
                                                   Description=Calculado sobre Pax TO-Circuito;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 31  ;   ;Coste Transfer      ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Pax TO-Circuito"."Coste Transfer" WHERE (Delegacion=FIELD(Delegacion),
                                                                                                             Circuito=FIELD(Circuito),
                                                                                                             Orden=FIELD(Orden)));
                                                   CaptionML=[ENU=Transfer cost;
                                                              ESP=Coste Transfer];
                                                   Description=Calculado sobre Pax TO-Circuito;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 33  ;   ;Filtro N§ Factura   ;Code10        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Invoice No. Filter;
                                                              ESP=Filtro N§ Factura] }
    { 35  ;   ;Efectivo Gu¡a       ;Decimal       ;CaptionML=[ENU=Guide cash;
                                                              ESP=Efectivo Gu¡a];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 37  ;   ;Guia Efectivo       ;Code20        ;TableRelation=Vendor.No.;
                                                   CaptionML=[ENU=Guide cash;
                                                              ESP=Guia Efectivo];
                                                   Description=FK Proveedor }
    { 39  ;   ;Filtro Fecha Entrada;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Check-in date filter;
                                                              ESP=Filtro Fecha Entrada];
                                                   Description=FlowFilter }
    { 41  ;   ;Filtro Fecha Salida ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Check-out Filter;
                                                              ESP=Filtro Fecha Salida];
                                                   Description=FlowFilter }
    { 43  ;   ;Pendiente generar   ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Pax TO-Circuito" WHERE (Delegacion=FIELD(Delegacion),
                                                                                              Circuito=FIELD(Circuito),
                                                                                              Orden=FIELD(Orden),
                                                                                              N§ Interno=CONST(0)));
                                                   CaptionML=[ENU=Pending to generate;
                                                              ESP=Pendiente generar];
                                                   Description=Calculado sobre Pax TO-Circuito }
    { 45  ;   ;Pendiente calcular  ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[ENU=Pending to calculate;
                                                              ESP=Pendiente calcular] }
  }
  KEYS
  {
    {    ;Delegacion,Circuito,Orden               ;Clustered=Yes }
    {    ;Circuito,Fecha                          ;KeyGroups=RENUMERING }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1103355001 : TextConst 'ENU=Agency parameters folder does not exist;ESP=No existe el fichero par metros agencia';
      Text001@1103355002 : TextConst 'ENU=This circuit has been invoiced, cannot be deleted.;ESP=Este circuito esta facturado, no se puede borrar.';
      Text002@1103355003 : TextConst 'ENU=Would you like to delete the %1 of the circuit %2 of %3;ESP=Desea borrar las %1 del circuito %2 de %3.';
      Text003@1103355004 : TextConst 'ENU=Unable to rename the circuit;ESP=No se puede renombrar este circuito';
      Text004@1103355005 : TextConst 'ENU=Data is already issued, in case you continue you will have to issue it again;ESP=La documentaci¢n ya est  emitida, si contin£a tendr  que emitir nuevamente la documentaci¢n';
      Text005@1103355006 : TextConst 'ENU=+%1D-1D;ESP=+%1D-1D';
      Text006@1103355007 : TextConst 'ENU=Review the dates of the manual provisions of the circuit;ESP=Revise las fechas de las prestaciones manuales del circuito.';
      rPar@1103355000 : Record 7009700;

    PROCEDURE Crear_Prestaciones_Circuito@2();
    VAR
      rPresXcir@1103355000 : Record 7010211;
      rPresCir@1103355001 : Record 7010207;
      PrestacionesNuevas@1103355002 : Boolean;
    BEGIN
      // Crear_Prestaciones_Circuito
      //
      IF (Delegacion = '') OR (Circuito = '') OR (Orden = 0) OR (Fecha = 0D) THEN
        EXIT;

      PrestacionesNuevas := FALSE;
      rPresXcir.SETRANGE(Circuito, Circuito);
      rPresXcir.SETRANGE(Orden   , Orden);
      IF rPresXcir.FIND('-') THEN BEGIN
        REPEAT
          IF rPresCir.GET(Circuito, rPresXcir.Linea) THEN BEGIN
            rPresXcir."Dia Inicio prestacion" := CALCDATE(STRSUBSTNO(Text005, rPresCir."Dia Inicio"), Fecha);
            rPresXcir.MODIFY;
          END
          ELSE
            PrestacionesNuevas := TRUE;
        UNTIL rPresXcir.NEXT = 0;
      END
      ELSE BEGIN
        rPresCir.RESET;
        rPresCir.SETRANGE(Circuito, Circuito);
        rPresCir.FIND('-');
        REPEAT
          CLEAR(rPresXcir);
          rPresXcir.Circuito         := Circuito;
          rPresXcir.Orden            := Orden;
          rPresXcir.Linea            := rPresCir.Linea;
          rPresXcir."Tipo Proveedor" := rPresCir."Tipo Proveedor";
          rPresXcir.Tarifa           := rPresCir.Tarifa;
          rPresXcir."Dia Inicio prestacion"     := CALCDATE(STRSUBSTNO(Text005, rPresCir."Dia Inicio"), Fecha);
          rPresXcir.Codigo           := rPresCir.Codigo;
          rPresXcir.INSERT;
        UNTIL rPresCir.NEXT = 0;
      END;

      IF PrestacionesNuevas THEN
        MESSAGE(Text006);
    END;

    BEGIN
    END.
  }
}
