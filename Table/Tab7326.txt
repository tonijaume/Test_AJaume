OBJECT Table 7326 Whse. Worksheet Line
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    OnDelete=VAR
               WhseWkshTemplate@1001 : Record 7328;
               ItemTrackingMgt@1000 : Codeunit 6500;
             BEGIN
               WhseWkshTemplate.GET("Worksheet Template Name");
               IF WhseWkshTemplate.Type = WhseWkshTemplate.Type::Movement THEN BEGIN
                 UpdateMovActivLines;
                 ItemTrackingMgt.DeleteWhseItemTrkgLines(
                   DATABASE::"Whse. Worksheet Line",0,Name,"Worksheet Template Name",0,"Line No.","Location Code",TRUE);
               END;
             END;

    CaptionML=[ENU=Whse. Worksheet Line;
               ESP=L¡n. hoja trabajo almac‚n];
  }
  FIELDS
  {
    { 1   ;   ;Worksheet Template Name;Code10     ;TableRelation="Whse. Worksheet Template";
                                                   CaptionML=[ENU=Worksheet Template Name;
                                                              ESP=Nombre libro hoja dem.];
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Code10        ;CaptionML=[ENU=Name;
                                                              ESP=Nombre];
                                                   NotBlank=Yes }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ESP=N§ l¡nea];
                                                   Editable=No }
    { 4   ;   ;Source Type         ;Integer       ;CaptionML=[ENU=Source Type;
                                                              ESP=Tipo procedencia mov.];
                                                   Editable=No }
    { 5   ;   ;Source Subtype      ;Option        ;CaptionML=[ENU=Source Subtype;
                                                              ESP=Subtipo origen];
                                                   OptionCaptionML=[ENU=0,1,2,3,4,5,6,7,8,9,10;
                                                                    ESP=0,1,2,3,4,5,6,7,8,9,10];
                                                   OptionString=0,1,2,3,4,5,6,7,8,9,10;
                                                   Editable=No }
    { 6   ;   ;Source No.          ;Code20        ;CaptionML=[ENU=Source No.;
                                                              ESP=C¢d. procedencia mov.];
                                                   Editable=No }
    { 7   ;   ;Source Line No.     ;Integer       ;CaptionML=[ENU=Source Line No.;
                                                              ESP=N§ l¡n. origen];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 8   ;   ;Source Subline No.  ;Integer       ;CaptionML=[ENU=Source Subline No.;
                                                              ESP=N§ subl¡nea origen];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 9   ;   ;Source Document     ;Option        ;CaptionML=[ENU=Source Document;
                                                              ESP=Documento origen];
                                                   OptionCaptionML=[ENU=,Sales Order,,,Sales Return Order,Purchase Order,,,Purchase Return Order,Inbound Transfer,Outbound Transfer,Prod. Consumption;
                                                                    ESP=,Pedido venta,,,Ped. devoluc. venta,Pedido compra,,,Ped. devoluc. compra,Transf. entrada,Transf. salida,Consumo produc.];
                                                   OptionString=,Sales Order,,,Sales Return Order,Purchase Order,,,Purchase Return Order,Inbound Transfer,Outbound Transfer,Prod. Consumption;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 10  ;   ;Location Code       ;Code10        ;TableRelation=Location;
                                                   CaptionML=[ENU=Location Code;
                                                              ESP=C¢d. almac‚n];
                                                   Editable=No }
    { 11  ;   ;Shelf No.           ;Code10        ;CaptionML=[ENU=Shelf No.;
                                                              ESP=N§ estante] }
    { 12  ;   ;From Zone Code      ;Code10        ;TableRelation=Zone.Code WHERE (Location Code=FIELD(Location Code));
                                                   OnValidate=BEGIN
                                                                IF "From Zone Code" <> xRec."From Zone Code" THEN
                                                                  "From Bin Code" := '';
                                                              END;

                                                   CaptionML=[ENU=From Zone Code;
                                                              ESP=Desde c¢d. zona] }
    { 13  ;   ;From Bin Code       ;Code20        ;OnValidate=VAR
                                                                WMSMgt@1000 : Codeunit 7302;
                                                              BEGIN
                                                                IF "From Bin Code" <> '' THEN
                                                                  WMSMgt.FindBinContent("Location Code","From Bin Code","Item No.","Variant Code","From Zone Code");

                                                                IF CurrFieldNo = FIELDNO("From Bin Code") THEN
                                                                  CheckBin("Location Code","From Bin Code",FALSE);

                                                                IF "From Bin Code" <> '' THEN BEGIN
                                                                  GetBin("Location Code","From Bin Code");
                                                                  "From Zone Code" := Bin."Zone Code";
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              WMSMgt@1000 : Codeunit 7302;
                                                              BinCode@1001 : Code[20];
                                                            BEGIN
                                                              BinCode := WMSMgt.BinContentLookUp("Location Code","Item No.","Variant Code","From Zone Code","From Bin Code");
                                                              IF BinCode <> '' THEN
                                                                VALIDATE("From Bin Code",BinCode);
                                                            END;

                                                   CaptionML=[ENU=From Bin Code;
                                                              ESP=Desde c¢d. ubicaci¢n] }
    { 14  ;   ;To Bin Code         ;Code20        ;TableRelation=IF (To Zone Code=FILTER('')) Bin.Code WHERE (Location Code=FIELD(Location Code),
                                                                                                              Code=FIELD(To Bin Code))
                                                                                                              ELSE IF (To Zone Code=FILTER(<>'')) Bin.Code WHERE (Location Code=FIELD(Location Code),
                                                                                                                                                                  Zone Code=FIELD(To Zone Code),
                                                                                                                                                                  Code=FIELD(To Bin Code));
                                                   OnValidate=BEGIN
                                                                IF CurrFieldNo = FIELDNO("To Bin Code") THEN
                                                                  CheckBin("Location Code","To Bin Code",TRUE);

                                                                IF "To Bin Code" <> '' THEN BEGIN
                                                                  GetBin("Location Code","To Bin Code");
                                                                  "To Zone Code" := Bin."Zone Code";
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=To Bin Code;
                                                              ESP=Hasta c¢d. ubicaci¢n] }
    { 15  ;   ;To Zone Code        ;Code10        ;TableRelation=Zone.Code WHERE (Location Code=FIELD(Location Code));
                                                   OnValidate=BEGIN
                                                                IF "To Zone Code" <> xRec."To Zone Code" THEN
                                                                  "To Bin Code" := '';
                                                              END;

                                                   CaptionML=[ENU=To Zone Code;
                                                              ESP=Hasta c¢d. zona] }
    { 16  ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   OnValidate=BEGIN
                                                                IF "Item No." <> '' THEN BEGIN
                                                                  GetItemUnitOfMeasure;
                                                                  Description := Item.Description;
                                                                  VALIDATE("Unit of Measure Code",ItemUnitOfMeasure.Code);
                                                                END ELSE BEGIN
                                                                  Description := '';
                                                                  "Variant Code" := '';
                                                                  VALIDATE("Unit of Measure Code",'');
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Item No.;
                                                              ESP=N§ producto] }
    { 17  ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                IF Quantity < "Qty. Handled" THEN
                                                                  FIELDERROR(Quantity,STRSUBSTNO(Text010,"Qty. Handled"))
                                                                ELSE
                                                                  VALIDATE("Qty. Outstanding",(Quantity - "Qty. Handled"));

                                                                "Qty. (Base)" := CalcBaseQty(Quantity);
                                                              END;

                                                   CaptionML=[ENU=Quantity;
                                                              ESP=Cantidad];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 18  ;   ;Qty. (Base)         ;Decimal       ;CaptionML=[ENU=Qty. (Base);
                                                              ESP=Cantidad (Base)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 19  ;   ;Qty. Outstanding    ;Decimal       ;OnValidate=BEGIN
                                                                "Qty. Outstanding (Base)" := CalcBaseQty("Qty. Outstanding");
                                                                VALIDATE("Qty. to Handle","Qty. Outstanding");
                                                              END;

                                                   CaptionML=[ENU=Qty. Outstanding;
                                                              ESP=Cdad. pendiente];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 20  ;   ;Qty. Outstanding (Base);Decimal    ;CaptionML=[ENU=Qty. Outstanding (Base);
                                                              ESP=Cdad. pendiente (base)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 21  ;   ;Qty. to Handle      ;Decimal       ;OnValidate=VAR
                                                                WhseWkshTemplate@1002 : Record 7328;
                                                                Confirmed@1000 : Boolean;
                                                                AvailableQty@1001 : Decimal;
                                                              BEGIN
                                                                IF "Qty. to Handle" > "Qty. Outstanding" THEN
                                                                  ERROR(
                                                                    Text000,
                                                                    "Qty. Outstanding");

                                                                "Qty. to Handle (Base)" := CalcBaseQty("Qty. to Handle");
                                                                IF "Qty. to Handle (Base)" > 0 THEN BEGIN
                                                                  WhseWkshTemplate.GET("Worksheet Template Name");
                                                                  IF WhseWkshTemplate.Type = WhseWkshTemplate.Type::Pick THEN BEGIN
                                                                    Confirmed := TRUE;
                                                                    IF (CurrFieldNo = FIELDNO("Qty. to Handle")) AND
                                                                       ("Shipping Advice" = "Shipping Advice"::Complete) AND
                                                                       ("Qty. to Handle" <> "Qty. Outstanding")
                                                                    THEN
                                                                      Confirmed := CONFIRM(
                                                                        Text001 +
                                                                        Text002,
                                                                        FALSE,
                                                                        FIELDCAPTION("Shipping Advice"),
                                                                        "Shipping Advice",
                                                                        FIELDCAPTION("Qty. to Handle"),
                                                                        "Qty. Outstanding");

                                                                    IF NOT Confirmed THEN
                                                                      ERROR(Text003);

                                                                    GetLocation("Location Code");
                                                                    IF Location."Bin Mandatory" THEN BEGIN
                                                                      IF CurrFieldNo <> FIELDNO("Qty. to Handle") THEN BEGIN
                                                                        AvailableQty := CheckAvailableQtyBase;
                                                                        IF NOT Location."Always Create Pick Line" THEN
                                                                          IF "Qty. to Handle (Base)" > AvailableQty THEN BEGIN
                                                                            IF ("Shipping Advice" = "Shipping Advice"::Complete) OR
                                                                               (AvailableQty < 0)
                                                                            THEN
                                                                              "Qty. to Handle (Base)" := 0
                                                                            ELSE
                                                                              "Qty. to Handle (Base)" := AvailableQty;
                                                                          END;
                                                                      END
                                                                    END ELSE BEGIN
                                                                      AvailableQty := CheckAvailableQtyBase;
                                                                      IF "Qty. to Handle (Base)" > AvailableQty THEN BEGIN
                                                                        IF ("Shipping Advice" = "Shipping Advice"::Complete) OR
                                                                           (AvailableQty < 0)
                                                                        THEN
                                                                          "Qty. to Handle (Base)" := 0
                                                                        ELSE
                                                                          "Qty. to Handle (Base)" := AvailableQty;

                                                                      IF (NOT HideValidationDialog) AND (CurrFieldNo = FIELDNO("Qty. to Handle")) THEN
                                                                          ERROR(
                                                                            Text004,
                                                                            AvailableQty);
                                                                      END;
                                                                    END;
                                                                  END ELSE
                                                                    IF WhseWkshTemplate.Type = WhseWkshTemplate.Type::Movement THEN
                                                                      IF CurrFieldNo <> FIELDNO("Qty. to Handle") THEN BEGIN
                                                                        AvailableQty := CheckAvailQtytoMove;
                                                                        IF AvailableQty < 0 THEN
                                                                          "Qty. to Handle (Base)" := 0
                                                                        ELSE
                                                                          IF "Qty. to Handle (Base)" > AvailableQty THEN
                                                                            "Qty. to Handle (Base)" := AvailableQty;
                                                                      END;

                                                                  CheckBin("Location Code","From Bin Code",FALSE);
                                                                  CheckBin("Location Code","To Bin Code",TRUE);
                                                                END;

                                                                TESTFIELD("Qty. per Unit of Measure");
                                                                "Qty. to Handle" := ROUND("Qty. to Handle (Base)" / "Qty. per Unit of Measure",0.00001);
                                                              END;

                                                   CaptionML=[ENU=Qty. to Handle;
                                                              ESP=Cdad.a manipular];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 22  ;   ;Qty. to Handle (Base);Decimal      ;OnValidate=BEGIN
                                                                TESTFIELD("Qty. per Unit of Measure",1);
                                                                VALIDATE("Qty. to Handle","Qty. to Handle (Base)");
                                                              END;

                                                   CaptionML=[ENU=Qty. to Handle (Base);
                                                              ESP=Cdad. a manipular (base)];
                                                   DecimalPlaces=0:5 }
    { 23  ;   ;Qty. Handled        ;Decimal       ;OnValidate=BEGIN
                                                                "Qty. Handled (Base)" := CalcBaseQty("Qty. Handled");
                                                                VALIDATE("Qty. Outstanding",Quantity - "Qty. Handled");
                                                              END;

                                                   CaptionML=[ENU=Qty. Handled;
                                                              ESP=Cdad. manipulada];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 24  ;   ;Qty. Handled (Base) ;Decimal       ;OnValidate=BEGIN
                                                                "Qty. Handled" := CalcQty("Qty. Handled (Base)");
                                                                VALIDATE("Qty. Outstanding",Quantity - "Qty. Handled");
                                                              END;

                                                   CaptionML=[ENU=Qty. Handled (Base);
                                                              ESP=Cdad. manipulada (base)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 27  ;   ;From Unit of Measure Code;Code10   ;TableRelation="Item Unit of Measure".Code WHERE (Item No.=FIELD(Item No.));
                                                   OnValidate=VAR
                                                                FromItemUnitOfMeasure@1000 : Record 5404;
                                                              BEGIN
                                                                IF "Item No." <> '' THEN BEGIN
                                                                  FromItemUnitOfMeasure.GET(Item."No.","From Unit of Measure Code");
                                                                  "Qty. per From Unit of Measure" := FromItemUnitOfMeasure."Qty. per Unit of Measure";
                                                                END ELSE
                                                                  "Qty. per From Unit of Measure" := 1;
                                                              END;

                                                   CaptionML=[ENU=From Unit of Measure Code;
                                                              ESP=Desde c¢d. unidad medida];
                                                   NotBlank=Yes }
    { 28  ;   ;Qty. per From Unit of Measure;Decimal;
                                                   InitValue=1;
                                                   CaptionML=[ENU=Qty. per From Unit of Measure;
                                                              ESP=Cdad. por unidad de medida];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 29  ;   ;Unit of Measure Code;Code10        ;TableRelation="Item Unit of Measure".Code WHERE (Item No.=FIELD(Item No.));
                                                   OnValidate=BEGIN
                                                                IF "Item No." <> '' THEN BEGIN
                                                                  GetItemUnitOfMeasure;
                                                                  "Qty. per Unit of Measure" := ItemUnitOfMeasure."Qty. per Unit of Measure";
                                                                END ELSE
                                                                  "Qty. per Unit of Measure" := 1;

                                                                "From Unit of Measure Code" := "Unit of Measure Code";
                                                                "Qty. per From Unit of Measure" := "Qty. per Unit of Measure";
                                                                VALIDATE(Quantity);
                                                              END;

                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              ESP=C¢d. unidad medida];
                                                   NotBlank=Yes }
    { 30  ;   ;Qty. per Unit of Measure;Decimal   ;InitValue=1;
                                                   CaptionML=[ENU=Qty. per Unit of Measure;
                                                              ESP=Cdad. por unidad medida];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 31  ;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   OnValidate=VAR
                                                                ItemVariant@1000 : Record 5401;
                                                              BEGIN
                                                                IF "Variant Code" <> '' THEN BEGIN
                                                                  ItemVariant.GET("Item No.","Variant Code");
                                                                  Description := ItemVariant.Description;
                                                                END ELSE
                                                                  GetItem("Item No.",Description);
                                                              END;

                                                   CaptionML=[ENU=Variant Code;
                                                              ESP=C¢d. variante] }
    { 32  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n] }
    { 33  ;   ;Description 2       ;Text50        ;CaptionML=[ENU=Description 2;
                                                              ESP=Descripci¢n 2] }
    { 35  ;   ;Sorting Sequence No.;Integer       ;CaptionML=[ENU=Sorting Sequence No.;
                                                              ESP=N§ secuencia clasif.];
                                                   Editable=No }
    { 36  ;   ;Due Date            ;Date          ;CaptionML=[ENU=Due Date;
                                                              ESP=Fecha vencimiento] }
    { 39  ;   ;Destination Type    ;Option        ;CaptionML=[ENU=Destination Type;
                                                              ESP=Tipo destino];
                                                   OptionCaptionML=[ENU=" ,Customer,Vendor,Location";
                                                                    ESP=" ,Cliente,Proveedor,Almac‚n"];
                                                   OptionString=[ ,Customer,Vendor,Location];
                                                   Editable=No }
    { 40  ;   ;Destination No.     ;Code20        ;TableRelation=IF (Destination Type=CONST(Customer)) Customer.No.
                                                                 ELSE IF (Destination Type=CONST(Vendor)) Vendor.No.
                                                                 ELSE IF (Destination Type=CONST(Location)) Location.Code;
                                                   CaptionML=[ENU=Destination No.;
                                                              ESP=N§ destino];
                                                   Editable=No }
    { 41  ;   ;Shipping Agent Code ;Code10        ;TableRelation="Shipping Agent";
                                                   CaptionML=[ENU=Shipping Agent Code;
                                                              ESP=C¢d. transportista] }
    { 42  ;   ;Shipping Agent Service Code;Code10 ;TableRelation="Shipping Agent Services".Code WHERE (Shipping Agent Code=FIELD(Shipping Agent Code));
                                                   CaptionML=[ENU=Shipping Agent Service Code;
                                                              ESP=C¢d. servicio transportista] }
    { 43  ;   ;Shipment Method Code;Code10        ;TableRelation="Shipment Method";
                                                   CaptionML=[ENU=Shipment Method Code;
                                                              ESP=C¢d. condiciones env¡o] }
    { 44  ;   ;Shipping Advice     ;Option        ;CaptionML=[ENU=Shipping Advice;
                                                              ESP=Aviso env¡o];
                                                   OptionCaptionML=[ENU=Partial,Complete;
                                                                    ESP=Parcial,Completo];
                                                   OptionString=Partial,Complete;
                                                   Editable=No }
    { 45  ;   ;Shipment Date       ;Date          ;CaptionML=[ENU=Shipment Date;
                                                              ESP=Fecha env¡o] }
    { 46  ;   ;Whse. Document Type ;Option        ;CaptionML=[ENU=Whse. Document Type;
                                                              ESP=Tipo documento almac‚n];
                                                   OptionCaptionML=[ENU=" ,Receipt,Shipment,Internal Put-away,Internal Pick,Production,Whse. Mov.-Worksheet";
                                                                    ESP=" ,Recepci¢n,Env¡o,Ubicaci¢n interna,Picking interno,Producci¢n,Mov. alm.-hoja trab."];
                                                   OptionString=[ ,Receipt,Shipment,Internal Put-away,Internal Pick,Production,Whse. Mov.-Worksheet];
                                                   Editable=No }
    { 47  ;   ;Whse. Document No.  ;Code20        ;TableRelation=IF (Whse. Document Type=CONST(Receipt)) "Posted Whse. Receipt Header".No. WHERE (No.=FIELD(Whse. Document No.))
                                                                 ELSE IF (Whse. Document Type=CONST(Shipment)) "Warehouse Shipment Header".No. WHERE (No.=FIELD(Whse. Document No.))
                                                                 ELSE IF (Whse. Document Type=CONST(Internal Put-away)) "Whse. Internal Put-away Header".No. WHERE (No.=FIELD(Whse. Document No.))
                                                                 ELSE IF (Whse. Document Type=CONST(Internal Pick)) "Whse. Internal Pick Header".No. WHERE (No.=FIELD(Whse. Document No.))
                                                                 ELSE IF (Whse. Document Type=CONST(Production)) "Production Order".No. WHERE (No.=FIELD(Whse. Document No.));
                                                   CaptionML=[ENU=Whse. Document No.;
                                                              ESP=N§ documento almac‚n];
                                                   Editable=No }
    { 48  ;   ;Whse. Document Line No.;Integer    ;TableRelation=IF (Whse. Document Type=CONST(Receipt)) "Posted Whse. Receipt Line"."Line No." WHERE (No.=FIELD(Whse. Document No.),
                                                                                                                                                       Line No.=FIELD(Whse. Document Line No.))
                                                                                                                                                       ELSE IF (Whse. Document Type=CONST(Shipment)) "Warehouse Shipment Line"."Line No." WHERE (No.=FIELD(Whse. Document No.),
                                                                                                                                                                                                                                                 Line No.=FIELD(Whse. Document Line No.))
                                                                                                                                                                                                                                                 ELSE IF (Whse. Document Type=CONST(Internal Put-away)) "Whse. Internal Put-away Line"."Line No." WHERE (No.=FIELD(Whse. Document No.),
                                                                                                                                                                                                                                                                                                                                                         Line No.=FIELD(Whse. Document Line No.))
                                                                                                                                                                                                                                                                                                                                                         ELSE IF (Whse. Document Type=CONST(Internal Pick)) "Whse. Internal Pick Line"."Line No." WHERE (No.=FIELD(Whse. Document No.),
                                                                                                                                                                                                                                                                                                                                                                                                                                                         Line No.=FIELD(Whse. Document Line No.))
                                                                                                                                                                                                                                                                                                                                                                                                                                                         ELSE IF (Whse. Document Type=CONST(Production)) "Prod. Order Line"."Line No." WHERE (Status=CONST(Released),
                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                              Prod. Order No.=FIELD(Whse. Document No.),
                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                              Line No.=FIELD(Line No.));
                                                   CaptionML=[ENU=Whse. Document Line No.;
                                                              ESP=N§ l¡n. documento almac‚n];
                                                   BlankZero=Yes;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Worksheet Template Name,Name,Location Code,Line No.;
                                                   Clustered=Yes }
    {    ;Worksheet Template Name,Name,Location Code,Sorting Sequence No. }
    {    ;Item No.,Location Code,Worksheet Template Name,Variant Code,Unit of Measure Code;
                                                   SumIndexFields=Qty. to Handle (Base);
                                                   MaintainSIFTIndex=No }
    {    ;Whse. Document Type,Whse. Document No.,Whse. Document Line No. }
    {    ;Worksheet Template Name,Name,Location Code,Item No.;
                                                   MaintainSQLIndex=No }
    {    ;Worksheet Template Name,Name,Location Code,Due Date;
                                                   MaintainSQLIndex=No }
    {    ;Worksheet Template Name,Name,Location Code,Destination Type,Destination No.;
                                                   MaintainSQLIndex=No }
    {    ;Worksheet Template Name,Name,Location Code,Source Document,Source No.;
                                                   MaintainSQLIndex=No }
    {    ;Worksheet Template Name,Name,Location Code,To Bin Code;
                                                   MaintainSQLIndex=No }
    {    ;Worksheet Template Name,Name,Location Code,Shelf No.;
                                                   MaintainSQLIndex=No }
    {    ;Source Type,Source Subtype,Source No.,Source Line No.,Source Subline No. }
    {    ;Item No.,From Bin Code,Location Code,Variant Code,From Unit of Measure Code;
                                                   SumIndexFields=Qty. to Handle,Qty. to Handle (Base);
                                                   MaintainSIFTIndex=No }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You cannot handle more than the outstanding %1 units.;ESP=No puede manipular m s de %1 unidades pendientes.';
      Text001@1003 : TextConst 'ENU=%1 is set to %2. %3 should be %4.\\;ESP=%1 se fija a %2. %3 deber¡a ser %4.\\';
      Text002@1002 : TextConst 'ENU=Accept the entered value?;ESP=Acepta el valor introducido?';
      Text003@1001 : TextConst 'ENU=The update was interrupted to respect the warning.;ESP=Se ha interrumpido la actualiz. para respetar la advertencia.';
      TempWhseWorksheetLine@1018 : Record 7326;
      Location@1005 : Record 14;
      Item@1011 : Record 27;
      Bin@1006 : Record 7354;
      BinType@1017 : Record 7303;
      ItemUnitOfMeasure@1014 : Record 5404;
      Text004@1004 : TextConst 'ENU=You cannot handle more than the available %1 units.;ESP=No puede manipular m s de %1 unidades disponibles.';
      Text005@1008 : TextConst 'ENU=DEFAULT;ESP=GENERICO';
      Text006@1007 : TextConst 'ENU=Default %1 Worksheet;ESP=%1 hoja trabajo gen‚rica';
      Text007@1009 : TextConst 'ENU=You must first set up user %1 as a warehouse employee.;ESP=Primero debe configurar usuario %1 como empleado almac‚n.';
      Text008@1010 : TextConst 'ENU=%1 Worksheet;ESP=Hoja %1';
      Text009@1012 : TextConst 'ENU=The location %1 of %2 %3 is not enabled for user %4.;ESP=El almac‚n %1 de %2 %3 no est  habilitado para usuario %4.';
      Text010@1013 : TextConst 'ENU=must not be less than %1 units;ESP=no debe ser menor que %1 unidades';
      WMSMgt@1020 : Codeunit 7302;
      CreatePick@1021 : Codeunit 7312;
      LastLineNo@1019 : Integer;
      HideValidationDialog@1015 : Boolean;
      Text011@1016 : TextConst 'ENU=Quantity available to pick is not enough to fill in all the lines.;ESP=Cdad. disp. para picking no es suficiente para cubrir todas las l¡neas.';
      OpenFromBatch@1022 : Boolean;

    PROCEDURE CalcBaseQty@14(Qty@1000 : Decimal) : Decimal;
    BEGIN
      TESTFIELD("Qty. per Unit of Measure");
      EXIT(ROUND(Qty * "Qty. per Unit of Measure",0.00001));
    END;

    PROCEDURE CalcQty@5(QtyBase@1000 : Decimal) : Decimal;
    BEGIN
      TESTFIELD("Qty. per Unit of Measure");
      EXIT(ROUND(QtyBase / "Qty. per Unit of Measure",0.00001));
    END;

    PROCEDURE AutofillQtyToHandle@10(VAR WhseWkshLine@1000 : Record 7326);
    VAR
      NotEnough@1001 : Boolean;
    BEGIN
      WITH WhseWkshLine DO BEGIN
        NotEnough := FALSE;
        SetHideValidationDialog(TRUE);
        LOCKTABLE;
        IF FIND('-') THEN
          REPEAT
            VALIDATE("Qty. to Handle","Qty. Outstanding");
            MODIFY;
            IF NOT NotEnough THEN
              IF "Qty. to Handle" < "Qty. Outstanding" THEN
                NotEnough := TRUE;
          UNTIL NEXT = 0;
        SetHideValidationDialog(FALSE);
        IF NotEnough THEN
          MESSAGE(Text011);
      END;
    END;

    PROCEDURE DeleteQtyToHandle@11(VAR WhseWkshLine@1000 : Record 7326);
    BEGIN
      WITH WhseWkshLine DO BEGIN
        LOCKTABLE;
        IF FIND('-') THEN
          REPEAT
            VALIDATE("Qty. to Handle",0);
            MODIFY;
          UNTIL NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE CheckAvailableQtyBase@2() AvailableQtyToPickBase : Decimal;
    VAR
      AvailableQtyBase@1000 : Decimal;
      LineReservedQtyBase@1001 : Decimal;
    BEGIN
      LineReservedQtyBase :=
        WMSMgt.CalcLineReservedQtyonInvt(
          "Source Type","Source Subtype","Source No.",
          "Source Line No.","Source Subline No.",
          '','');

      AvailableQtyBase := CalcAvailableQtyBase(FALSE);
      AvailableQtyToPickBase := AvailableQtyBase + LineReservedQtyBase + xRec."Qty. to Handle (Base)";
    END;

    PROCEDURE AssignedQtyOnReservedLines@29() : Decimal;
    VAR
      WhseWkshLine@1000 : Record 7326;
      LineReservedQtyBase@1001 : Decimal;
      TotalReservedAndAssignedBase@1003 : Decimal;
      ReservedAndAssignedBase@1004 : Decimal;
    BEGIN
      WhseWkshLine.SETCURRENTKEY(
        "Item No.","Location Code","Worksheet Template Name","Variant Code");
      WhseWkshLine.SETRANGE("Item No.","Item No.");
      WhseWkshLine.SETRANGE("Location Code","Location Code");
      WhseWkshLine.SETRANGE("Worksheet Template Name","Worksheet Template Name");
      WhseWkshLine.SETRANGE("Variant Code","Variant Code");
      IF WhseWkshLine.FIND('-') THEN
        REPEAT
          LineReservedQtyBase :=
            ABS(
              WMSMgt.CalcLineReservedQtyonInvt(
                WhseWkshLine."Source Type",WhseWkshLine."Source Subtype",
                WhseWkshLine."Source No.",WhseWkshLine."Source Line No.",
                WhseWkshLine."Source Subline No.",
                '',''));
          IF LineReservedQtyBase > 0 THEN BEGIN
            IF LineReservedQtyBase <= WhseWkshLine."Qty. to Handle (Base)" THEN
              ReservedAndAssignedBase := LineReservedQtyBase
            ELSE
              ReservedAndAssignedBase := WhseWkshLine."Qty. to Handle (Base)";
            TotalReservedAndAssignedBase := TotalReservedAndAssignedBase + ReservedAndAssignedBase;
          END;
        UNTIL WhseWkshLine.NEXT = 0;
      EXIT(TotalReservedAndAssignedBase);
    END;

    PROCEDURE CalcAvailableQtyBase@1(ExcludeLine@1102601000 : Boolean) AvailableQty@1000 : Decimal;
    VAR
      Item2@1002 : Record 27;
      AvailQtyBase@1003 : Decimal;
      QtyAssgndOnWkshBase@1006 : Decimal;
      QtyReservedOnPickShip@1007 : Decimal;
    BEGIN
      GetItem("Item No.",Description);
      Item2 := Item;
      GetLocation("Location Code");

      IF Location."Directed Put-away and Pick" THEN BEGIN
        Item2.SETRANGE("Location Filter","Location Code");
        Item2.SETRANGE("Variant Filter","Variant Code");
        Item2.CALCFIELDS("Reserved Qty. on Inventory");
        QtyReservedOnPickShip :=
          CreatePick.CalcReservQtyOnPicksShips("Location Code","Item No.","Variant Code");
        QtyAssgndOnWkshBase := WMSMgt.CalcQtyAssgndOnWksh(Rec,NOT Location."Allow Breakbulk",ExcludeLine);
        AvailQtyBase :=
          CalcAvailWhseQtyBase("Location Code","Item No.","Variant Code","Unit of Measure Code") -
          ABS(Item2."Reserved Qty. on Inventory") + QtyReservedOnPickShip;
      END ELSE BEGIN
        IF Location."Require Pick" THEN
          QtyReservedOnPickShip :=
            CreatePick.CalcReservQtyOnPicksShips("Location Code","Item No.","Variant Code") + QtyReservedOnPickShip;

        QtyAssgndOnWkshBase := WMSMgt.CalcQtyAssgndOnWksh(Rec,TRUE,FALSE);
        AvailQtyBase := WMSMgt.CalcInvtAvailQty(Item2,Location,"Variant Code");
      END;

      AvailableQty := AvailQtyBase - QtyAssgndOnWkshBase + AssignedQtyOnReservedLines;
    END;

    LOCAL PROCEDURE CalcAvailWhseQtyBase@6(LocationCode@1000 : Code[10];ItemNo@1001 : Code[20];VariantCode@1002 : Code[10];UOMCode@1003 : Code[10]) : Decimal;
    VAR
      WhseEntry@1004 : Record 7312;
      WhseActivLine@1005 : Record 5767;
      WhseJnlLine@1006 : Record 7311;
      BinType@1008 : Record 7303;
      BinTypeFilter@1007 : Text[250];
    BEGIN
      BinType.CreateBinTypeFilter(BinTypeFilter,3);

      WhseEntry.SETCURRENTKEY("Item No.","Location Code","Variant Code","Bin Type Code","Unit of Measure Code");
      WhseEntry.SETRANGE("Item No.",ItemNo);
      WhseEntry.SETRANGE("Location Code",LocationCode);
      WhseEntry.SETRANGE("Variant Code",VariantCode);
      IF NOT Location."Allow Breakbulk" THEN
        WhseEntry.SETRANGE("Unit of Measure Code",UOMCode);
      WhseEntry.SETFILTER("Bin Type Code",BinTypeFilter);
      WhseEntry.CALCSUMS("Qty. (Base)");

      WhseActivLine.SETCURRENTKEY(
        "Item No.","Location Code","Activity Type","Bin Type Code",
        "Unit of Measure Code","Variant Code","Breakbulk No.","Action Type");
      WhseActivLine.SETRANGE("Item No.",ItemNo);
      WhseActivLine.SETRANGE("Location Code",LocationCode);
      WhseActivLine.SETFILTER("Bin Type Code",BinTypeFilter);
      WhseActivLine.SETRANGE("Variant Code",VariantCode);
      IF NOT Location."Allow Breakbulk" THEN
        WhseActivLine.SETRANGE("Unit of Measure Code",UOMCode)
      ELSE
        WhseActivLine.SETRANGE("Breakbulk No.",0);
      WhseActivLine.SETRANGE("Action Type",WhseActivLine."Action Type"::Take);
      WhseActivLine.CALCSUMS("Qty. Outstanding (Base)");

      WhseJnlLine.SETCURRENTKEY(
        "Item No.","Location Code","Entry Type","From Bin Type Code","Variant Code","Unit of Measure Code");
      WhseJnlLine.SETRANGE("Item No.",ItemNo);
      WhseJnlLine.SETRANGE("Location Code",LocationCode);
      WhseJnlLine.SETRANGE("Variant Code",VariantCode);
      WhseJnlLine.SETRANGE("Entry Type",WhseJnlLine."Entry Type"::"Negative Adjmt.");
      WhseJnlLine.SETFILTER("From Bin Type Code",BinTypeFilter);
      IF NOT Location."Allow Breakbulk" THEN
        WhseJnlLine.SETRANGE("Unit of Measure Code",UOMCode);
      WhseJnlLine.CALCSUMS("Qty. (Absolute, Base)");

      EXIT(
        WhseEntry."Qty. (Base)" - WhseActivLine."Qty. Outstanding (Base)" -
        WhseJnlLine."Qty. (Absolute, Base)");
    END;

    PROCEDURE CheckAvailQtytoMove@25() AvailableQtyToMoveBase@1000 : Decimal;
    BEGIN
      AvailableQtyToMoveBase := CalcAvailQtyToMove + xRec."Qty. to Handle (Base)";
    END;

    LOCAL PROCEDURE CalcAvailQtyToMove@23() : Decimal;
    VAR
      BinContent@1000 : Record 7302;
      WhseWkshLine@1002 : Record 7326;
      QtyAvailToMoveBase@1001 : Decimal;
    BEGIN
      IF ("Location Code" <> '') AND ("From Bin Code" <> '') AND
         ("Item No." <> '') AND ("From Unit of Measure Code" <> '')
      THEN BEGIN
        GetLocation("Location Code");
        IF BinContent.GET(
          "Location Code","From Bin Code","Item No.","Variant Code","From Unit of Measure Code")
        THEN BEGIN
          QtyAvailToMoveBase := BinContent.CalcQtyAvailToPick(0);
          WhseWkshLine.SETCURRENTKEY(
            "Item No.","From Bin Code","Location Code","Variant Code","From Unit of Measure Code");
          WhseWkshLine.SETRANGE("Item No.","Item No.");
          WhseWkshLine.SETRANGE("From Bin Code","From Bin Code");
          WhseWkshLine.SETRANGE("Location Code","Location Code");
          WhseWkshLine.SETRANGE("Variant Code","Variant Code");
          WhseWkshLine.SETRANGE("From Unit of Measure Code","From Unit of Measure Code");
          WhseWkshLine.CALCSUMS("Qty. to Handle (Base)");
          QtyAvailToMoveBase := QtyAvailToMoveBase - WhseWkshLine."Qty. to Handle (Base)";
        END;
      END;
      EXIT(QtyAvailToMoveBase);
    END;

    PROCEDURE SortWhseWkshLines@3(WhseWkshTemplate@1005 : Code[10];WhseWkshName@1002 : Code[10];LocationCode@1004 : Code[10];SortingMethod@1003 : ' ,Item,Document,Shelf/Bin No.,Due Date,Ship-To');
    VAR
      WhseWkshLine@1001 : Record 7326;
      SequenceNo@1000 : Integer;
    BEGIN
      WhseWkshLine.SETRANGE("Worksheet Template Name",WhseWkshTemplate);
      WhseWkshLine.SETRANGE(Name,WhseWkshName);
      WhseWkshLine.SETRANGE("Location Code",LocationCode);
      CASE SortingMethod OF
        SortingMethod::Item:
          WhseWkshLine.SETCURRENTKEY(
            "Worksheet Template Name",Name,"Location Code","Item No.");
        SortingMethod::Document:
          WhseWkshLine.SETCURRENTKEY(
            "Worksheet Template Name",Name,"Location Code","Source Document","Source No.");
        SortingMethod::"Shelf/Bin No.":
          BEGIN
            GetLocation(LocationCode);
            IF Location."Bin Mandatory" THEN
              WhseWkshLine.SETCURRENTKEY(
                "Worksheet Template Name",Name,"Location Code","To Bin Code")
            ELSE
              WhseWkshLine.SETCURRENTKEY(
                "Worksheet Template Name",Name,"Location Code","Shelf No.");
          END;
        SortingMethod::"Due Date":
          WhseWkshLine.SETCURRENTKEY(
            "Worksheet Template Name",Name,"Location Code","Due Date");
        SortingMethod::"Ship-To":
          WhseWkshLine.SETCURRENTKEY(
            "Worksheet Template Name",Name,"Location Code","Destination Type","Destination No.");
      END;

      IF WhseWkshLine.FIND('-') THEN BEGIN
        SequenceNo := 10000;
        REPEAT
          WhseWkshLine."Sorting Sequence No." := SequenceNo;
          WhseWkshLine.MODIFY;
          SequenceNo := SequenceNo + 10000;
        UNTIL WhseWkshLine.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE GetLocation@4(LocationCode@1000 : Code[10]);
    BEGIN
      IF Location.Code <> LocationCode THEN BEGIN
        IF LocationCode = '' THEN
          Location.GetLocationSetup(LocationCode,Location)
        ELSE
          Location.GET(LocationCode);
      END;
    END;

    PROCEDURE GetItem@13(ItemNo@1000 : Code[20];VAR ItemDescription@1001 : Text[50]);
    BEGIN
      IF ItemNo = '' THEN
        ItemDescription := ''
      ELSE
        IF ItemNo <> Item."No." THEN BEGIN
          ItemDescription := '';
          IF Item.GET(ItemNo) THEN
            ItemDescription := Item.Description;
        END ELSE
          ItemDescription := Item.Description;
    END;

    LOCAL PROCEDURE GetItemUnitOfMeasure@18();
    BEGIN
      GetItem("Item No.",Description);
      IF (Item."No." <> ItemUnitOfMeasure."Item No.") OR
         ("Unit of Measure Code" <> ItemUnitOfMeasure.Code)
      THEN
        IF NOT ItemUnitOfMeasure.GET(Item."No.","Unit of Measure Code") THEN
          ItemUnitOfMeasure.GET(Item."No.",Item."Base Unit of Measure");
    END;

    LOCAL PROCEDURE GetBin@19(LocationCode@1000 : Code[10];BinCode@1002 : Code[20]);
    BEGIN
      IF (LocationCode = '') OR (BinCode = '') THEN
        Bin.INIT
      ELSE
        IF (Bin."Location Code" <> LocationCode) OR
           (Bin.Code <> BinCode)
        THEN
          Bin.GET(LocationCode,BinCode);
    END;

    PROCEDURE CheckBin@17(LocationCode@1000 : Code[10];BinCode@1001 : Code[20];Inbound@1002 : Boolean);
    VAR
      WhseWkshTemplate@1008 : Record 7328;
      BinContent@1006 : Record 7302;
      WMSMgt@1003 : Codeunit 7302;
      Cubage@1004 : Decimal;
      Weight@1005 : Decimal;
    BEGIN
      GetLocation(LocationCode);
      IF NOT Location."Directed Put-away and Pick" THEN
        EXIT;

      GetBin(LocationCode,BinCode);
      Bin.CALCFIELDS("Adjustment Bin");
      Bin.TESTFIELD("Adjustment Bin",FALSE);
      IF (BinCode <> '') AND ("Item No." <> '') THEN BEGIN
        IF Bin."Bin Type Code" <> '' THEN BEGIN
          WhseWkshTemplate.GET("Worksheet Template Name");
          IF WhseWkshTemplate.Type = WhseWkshTemplate.Type::Movement THEN BEGIN
            GetBinType(Bin."Bin Type Code");
            BinType.TESTFIELD(Receive,FALSE);
          END;
        END;

        IF Inbound THEN BEGIN
          WMSMgt.CalcCubageAndWeight(
            "Item No.","From Unit of Measure Code","Qty. to Handle",Cubage,Weight);
          IF BinContent.GET(
            "Location Code",BinCode,"Item No.","Variant Code","Unit of Measure Code")
          THEN
            BinContent.CheckIncreaseBinContent(
              "Qty. to Handle (Base)",0,0,0,Cubage,Weight,FALSE)
          ELSE
            Bin.CheckIncreaseBin(BinCode,"Item No.","Qty. to Handle",0,0,Cubage,Weight,FALSE);
        END ELSE BEGIN
          BinContent.GET(
            "Location Code",BinCode,"Item No.","Variant Code","From Unit of Measure Code");
          IF BinContent."Block Movement" IN [
             BinContent."Block Movement"::Outbound,BinContent."Block Movement"::All]
          THEN
            BinContent.FIELDERROR("Block Movement");
        END;
      END;
    END;

    PROCEDURE GetBinType@26(BinTypeCode@1000 : Code[10]);
    BEGIN
      IF BinTypeCode = '' THEN
        BinType.INIT
      ELSE
        IF BinType.Code <> BinTypeCode THEN
          BinType.GET(BinTypeCode);
    END;

    PROCEDURE PutAwayCreate@8(VAR WhsePutAwayWkshLine@1000 : Record 7326);
    VAR
      CreatePutAwayFromWhseSource@1001 : Report 7305;
    BEGIN
      CreatePutAwayFromWhseSource.SetWhseWkshLine(WhsePutAwayWkshLine);
      CreatePutAwayFromWhseSource.RUNMODAL;
      CreatePutAwayFromWhseSource.GetResultMessage(1);
      CLEAR(CreatePutAwayFromWhseSource);
    END;

    PROCEDURE MovementCreate@22(VAR WhseWkshLine@1000 : Record 7326);
    VAR
      CreateMovFromWhseSource@1001 : Report 7305;
    BEGIN
      CreateMovFromWhseSource.SetWhseWkshLine(WhseWkshLine);
      CreateMovFromWhseSource.RUNMODAL;
      CreateMovFromWhseSource.GetResultMessage(3);
      CLEAR(CreateMovFromWhseSource);
    END;

    PROCEDURE TemplateSelection@16(FormID@1006 : Integer;FormTemplate@1000 : 'Put-away,Pick,Movement';VAR WhseWkshLine@1005 : Record 7326;VAR WhseWkshSelected@1001 : Boolean);
    VAR
      WhseWkshTemplate@1002 : Record 7328;
    BEGIN
      WhseWkshSelected := TRUE;

      WhseWkshTemplate.RESET;
      WhseWkshTemplate.SETRANGE("Form ID",FormID);
      WhseWkshTemplate.SETRANGE(Type,FormTemplate);

      CASE WhseWkshTemplate.COUNT OF
        0:
          BEGIN
            WhseWkshTemplate.INIT;
            WhseWkshTemplate.VALIDATE(Type,FormTemplate);
            WhseWkshTemplate.VALIDATE("Form ID");
            WhseWkshTemplate.Name :=
              FORMAT(WhseWkshTemplate.Type,MAXSTRLEN(WhseWkshTemplate.Name));
            WhseWkshTemplate.Description := STRSUBSTNO(Text008,WhseWkshTemplate.Type);
            WhseWkshTemplate.INSERT;
            COMMIT;
          END;
        1:
          WhseWkshTemplate.FIND('-');
        ELSE
          WhseWkshSelected := FORM.RUNMODAL(0,WhseWkshTemplate) = ACTION::LookupOK;
      END;
      IF WhseWkshSelected THEN BEGIN
        WhseWkshLine.FILTERGROUP := 2;
        WhseWkshLine.SETRANGE("Worksheet Template Name",WhseWkshTemplate.Name);
        WhseWkshLine.FILTERGROUP := 0;
        IF OpenFromBatch THEN BEGIN
          WhseWkshLine."Worksheet Template Name" := '';
          FORM.RUN(WhseWkshTemplate."Form ID",WhseWkshLine);
        END;
      END;
    END;

    PROCEDURE TemplateSelectionFromBatch@36(VAR WhseWkshName@1003 : Record 7327);
    VAR
      WhseWkshLine@1000 : Record 7326;
      WhseWkshTemplate@1002 : Record 7328;
      JnlSelected@1001 : Boolean;
    BEGIN
      OpenFromBatch := TRUE;
      WhseWkshTemplate.GET(WhseWkshName."Worksheet Template Name");
      WhseWkshTemplate.TESTFIELD("Form ID");
      WhseWkshName.TESTFIELD(Name);

      WhseWkshLine.FILTERGROUP := 2;
      WhseWkshLine.SETRANGE("Worksheet Template Name",WhseWkshTemplate.Name);
      WhseWkshLine.FILTERGROUP := 0;

      WhseWkshLine."Worksheet Template Name" := '';
      WhseWkshLine.Name := WhseWkshName.Name;
      WhseWkshLine."Location Code" := WhseWkshName."Location Code";
      FORM.RUN(WhseWkshTemplate."Form ID",WhseWkshLine);
    END;

    PROCEDURE OpenWhseWksh@12(VAR WhseWkshLine@1001 : Record 7326;VAR CurrentWkshTemplateName@1003 : Code[10];VAR CurrentWkshName@1000 : Code[10];VAR CurrentLocationCode@1002 : Code[10]);
    BEGIN
      CheckWhseEmployee;
      CurrentWkshTemplateName := WhseWkshLine.GETRANGEMAX("Worksheet Template Name");
      CheckTemplateName(CurrentWkshTemplateName,CurrentWkshName,CurrentLocationCode);
      WhseWkshLine.FILTERGROUP := 2;
      WhseWkshLine.SETRANGE(Name,CurrentWkshName);
      IF CurrentLocationCode <> '' THEN
        WhseWkshLine.SETRANGE("Location Code",CurrentLocationCode);
      WhseWkshLine.FILTERGROUP := 0;
    END;

    PROCEDURE OpenWhseWkshBatch@37(VAR WhseWkshName@1005 : Record 7327);
    VAR
      WhseWkshTemplate@1003 : Record 7328;
      WhseWkshLine@1001 : Record 7326;
      WmsMgt@1002 : Codeunit 7302;
      JnlSelected@1004 : Boolean;
      LocationCode@1006 : Code[10];
    BEGIN
      IF WhseWkshName.GETFILTER("Worksheet Template Name") <> '' THEN
        EXIT;
      WhseWkshName.FILTERGROUP(2);
      IF WhseWkshName.GETFILTER("Worksheet Template Name") <> '' THEN BEGIN
        WhseWkshName.FILTERGROUP(0);
        EXIT;
      END;
      WhseWkshName.FILTERGROUP(0);

      IF NOT WhseWkshName.FIND('-') THEN BEGIN
        FOR WhseWkshTemplate.Type := WhseWkshTemplate.Type::"Put-away" TO WhseWkshTemplate.Type::Movement DO BEGIN
          WhseWkshTemplate.SETRANGE(Type,WhseWkshTemplate.Type);
          IF NOT WhseWkshTemplate.FIND('-') THEN
            TemplateSelection(0,WhseWkshTemplate.Type,WhseWkshLine,JnlSelected);
          IF WhseWkshTemplate.FIND('-') THEN BEGIN
            IF WhseWkshName."Location Code" = '' THEN
              WhseWkshName."Location Code" := WmsMgt.GetDefaultLocation();
            CheckTemplateName(WhseWkshTemplate.Name,WhseWkshName.Name,WhseWkshName."Location Code");
          END;
        END;
      END;
      WhseWkshName.FIND('-');
      JnlSelected := TRUE;
      WhseWkshName.CALCFIELDS("Template Type");
      WhseWkshTemplate.SETRANGE(Type,WhseWkshName."Template Type");
      IF WhseWkshName.GETFILTER("Worksheet Template Name") <> '' THEN
         WhseWkshTemplate.SETRANGE(Name,WhseWkshName.GETFILTER("Worksheet Template Name"));
      CASE WhseWkshTemplate.COUNT OF
        1:
          WhseWkshTemplate.FIND('-');
        ELSE
          JnlSelected := FORM.RUNMODAL(0,WhseWkshTemplate) = ACTION::LookupOK;
      END;
      IF NOT JnlSelected THEN
        ERROR('');

      WhseWkshName.FILTERGROUP(0);
      WhseWkshName.SETRANGE("Worksheet Template Name",WhseWkshTemplate.Name);
      WhseWkshName.FILTERGROUP(2);
    END;

    PROCEDURE CheckTemplateName@20(CurrentWkshTemplateName@1000 : Code[10];VAR CurrentWkshName@1001 : Code[10];VAR CurrentLocationCode@1005 : Code[10]);
    VAR
      WhseWkshTemplate@1006 : Record 7328;
      WhseWkshName@1002 : Record 7327;
      WhseEmployee@1004 : Record 7301;
      WmsMgt@1003 : Codeunit 7302;
      FoundLocation@1007 : Boolean;
    BEGIN
      WhseWkshTemplate.GET(CurrentWkshTemplateName);
      WhseWkshName.SETRANGE("Worksheet Template Name",CurrentWkshTemplateName);
      IF NOT WhseWkshName.GET(CurrentWkshTemplateName,CurrentWkshName,CurrentLocationCode) OR
         ((USERID <> '') AND NOT WhseEmployee.GET(USERID,CurrentLocationCode))
      THEN BEGIN
        IF USERID <> '' THEN BEGIN
          CurrentLocationCode := WmsMgt.GetDefaultLocation;
          WhseWkshName.SETRANGE("Location Code",CurrentLocationCode);
        END;
        IF NOT WhseWkshName.FIND('-') THEN BEGIN
          IF USERID <> '' THEN BEGIN
            WhseEmployee.SETCURRENTKEY(Default);
            WhseEmployee.SETRANGE(Default,FALSE);
            WhseEmployee.SETRANGE("User ID",USERID);
            IF WhseEmployee.FIND('-') THEN
              REPEAT
                WhseWkshName.SETRANGE("Location Code",WhseEmployee."Location Code");
                FoundLocation := WhseWkshName.FIND('-');
              UNTIL (WhseEmployee.NEXT = 0) OR FoundLocation;
          END;
          IF NOT FoundLocation THEN BEGIN
            WhseWkshName.INIT;
            WhseWkshName."Worksheet Template Name" := CurrentWkshTemplateName;
            WhseWkshName.SetupNewName;
            WhseWkshName.Name := Text005;
            WhseWkshName.Description :=
              STRSUBSTNO(Text006,WhseWkshTemplate.Type);
            WhseWkshName.INSERT(TRUE);
          END;
          CurrentLocationCode := WhseWkshName."Location Code";
          COMMIT;
        END;
        CurrentWkshName := WhseWkshName.Name;
        CurrentLocationCode := WhseWkshName."Location Code";
      END;
    END;

    PROCEDURE CheckWhseWkshName@21(CurrentWkshName@1000 : Code[10];CurrentLocationCode@1003 : Code[10];VAR WhseWkshLine@1001 : Record 7326);
    VAR
      WhseWkshName@1002 : Record 7327;
      WhseEmployee@1004 : Record 7301;
    BEGIN
      WhseWkshName.GET(
        WhseWkshLine.GETRANGEMAX("Worksheet Template Name"),CurrentWkshName,CurrentLocationCode);
      IF (USERID <> '') AND NOT WhseEmployee.GET(USERID,CurrentLocationCode) THEN
        ERROR(Text009,CurrentLocationCode,WhseWkshName.TABLECAPTION,CurrentWkshName,USERID);
    END;

    LOCAL PROCEDURE CheckWhseEmployee@15();
    VAR
      WhseEmployee@1000 : Record 7301;
    BEGIN
      IF USERID <> '' THEN BEGIN
        WhseEmployee.SETRANGE("User ID",USERID);
        IF NOT WhseEmployee.FIND('-') THEN
          ERROR(Text007,USERID);
      END;
    END;

    PROCEDURE SetWhseWkshName@7(CurrentWkshName@1000 : Code[10];CurrentLocationCode@1002 : Code[10];VAR WhseWkshLine@1001 : Record 7326);
    BEGIN
      WhseWkshLine.FILTERGROUP := 2;
      WhseWkshLine.SETRANGE(Name,CurrentWkshName);
      WhseWkshLine.SETRANGE("Location Code",CurrentLocationCode);
      WhseWkshLine.FILTERGROUP := 0;
      IF WhseWkshLine.FIND('-') THEN;
    END;

    PROCEDURE LookupWhseWkshName@9(VAR WhseWkshLine@1001 : Record 7326;VAR CurrentWkshName@1000 : Code[10];VAR CurrentLocationCode@1003 : Code[10]) : Boolean;
    VAR
      WhseWkshName@1002 : Record 7327;
    BEGIN
      COMMIT;
      WhseWkshName."Worksheet Template Name" := WhseWkshLine.GETRANGEMAX("Worksheet Template Name");
      WhseWkshName.Name := WhseWkshLine.GETRANGEMAX(Name);
      WhseWkshName.FILTERGROUP(2);
      WhseWkshName.SETRANGE("Worksheet Template Name",WhseWkshName."Worksheet Template Name");
      WhseWkshName.FILTERGROUP(0);
      IF FORM.RUNMODAL(0,WhseWkshName) = ACTION::LookupOK THEN BEGIN
        CurrentWkshName := WhseWkshName.Name;
        CurrentLocationCode := WhseWkshName."Location Code";
        SetWhseWkshName(CurrentWkshName,WhseWkshName."Location Code",WhseWkshLine);
      END;
    END;

    LOCAL PROCEDURE UpdateMovActivLines@35();
    VAR
      WhseActivLine@1001 : Record 5767;
      WhseActivLine2@1000 : Record 5767;
    BEGIN
      WhseActivLine.SETCURRENTKEY(
        "Whse. Document No.","Whse. Document Type","Activity Type","Whse. Document Line No.");
      WhseActivLine.SETRANGE("Whse. Document No.",Name);
      WhseActivLine.SETRANGE("Whse. Document Type",WhseActivLine."Whse. Document Type"::"Movement Worksheet");
      WhseActivLine.SETRANGE("Activity Type",WhseActivLine."Activity Type"::Movement);
      WhseActivLine.SETRANGE("Whse. Document Line No.","Line No.");
      WhseActivLine.SETRANGE("Source Type",DATABASE::"Whse. Worksheet Line");
      WhseActivLine.SETRANGE("Source No.","Worksheet Template Name");
      WhseActivLine.SETRANGE("Location Code","Location Code");
      IF WhseActivLine.FIND('-') THEN
        REPEAT
          WhseActivLine2.COPY(WhseActivLine);
          WhseActivLine2."Source Type" := 0;
          WhseActivLine2."Source No." := '';
          WhseActivLine2."Source Line No." := 0;
          WhseActivLine2.MODIFY;
        UNTIL WhseActivLine.NEXT = 0;
    END;

    PROCEDURE SetHideValidationDialog@24(NewHideValidationDialog@1000 : Boolean);
    BEGIN
      HideValidationDialog := NewHideValidationDialog;
    END;

    PROCEDURE OpenItemTrackingLines@6500();
    VAR
      WhseItemTrackingForm@1000 : Form 6550;
    BEGIN
      TESTFIELD("Item No.");
      TESTFIELD("Qty. (Base)");
      CASE "Whse. Document Type" OF
        "Whse. Document Type"::Receipt:
          WhseItemTrackingForm.SetSource(Rec,DATABASE::"Posted Whse. Receipt Line");
        "Whse. Document Type"::Shipment:
          WhseItemTrackingForm.SetSource(Rec,DATABASE::"Warehouse Shipment Line");
        "Whse. Document Type"::"Internal Put-away":
          WhseItemTrackingForm.SetSource(Rec,DATABASE::"Whse. Internal Put-away Line");
        "Whse. Document Type"::"Internal Pick":
          WhseItemTrackingForm.SetSource(Rec,DATABASE::"Whse. Internal Pick Line");
        "Whse. Document Type"::Production:
          WhseItemTrackingForm.SetSource(Rec,DATABASE::"Prod. Order Component");
        ELSE
          WhseItemTrackingForm.SetSource(Rec,DATABASE::"Whse. Worksheet Line");
      END;

      WhseItemTrackingForm.RUNMODAL;
    END;

    PROCEDURE AvailableQtyToPickBase@27() : Decimal;
    VAR
      LineReservedQtyBase@1001 : Decimal;
      TotalAvailQtyToPickBase@1000 : Decimal;
      QtyAssgndOnWkshBase@1004 : Decimal;
      AvailQtyBase@1005 : Decimal;
      ReservedAndAssignedBase@1002 : Decimal;
      ItemDescription@1003 : Text[50];
    BEGIN
      GetLocation("Location Code");
      LineReservedQtyBase :=
        WMSMgt.CalcLineReservedQtyonInvt(
          "Source Type","Source Subtype","Source No.",
          "Source Line No.","Source Subline No.",
          '','');
      IF NOT Location."Directed Put-away and Pick" THEN BEGIN
        GetItem("Item No.",ItemDescription);
        Item.CALCFIELDS("Reserved Qty. on Inventory");

        IF (LineReservedQtyBase > 0) AND (LineReservedQtyBase > "Qty. to Handle (Base)") THEN
          EXIT(CalcAvailableQtyBase(FALSE) + CalcQty(LineReservedQtyBase));
        EXIT(CalcAvailableQtyBase(FALSE) + "Qty. to Handle (Base)");
      END;
      CreatePick.SetCalledFromPickWksh(TRUE);
      TotalAvailQtyToPickBase :=
        CreatePick.CalcTotalAvailQtyToPick(
          "Location Code","Item No.","Variant Code",'','',
          "Source Type","Source Subtype","Source No.","Source Line No.","Source Subline No.",0);

      AvailQtyBase :=
        CalcAvailWhseQtyBase("Location Code","Item No.","Variant Code","Unit of Measure Code");

      IF TotalAvailQtyToPickBase > AvailQtyBase THEN
        TotalAvailQtyToPickBase := AvailQtyBase;

      QtyAssgndOnWkshBase := WMSMgt.CalcQtyAssgndOnWksh(Rec,TRUE,TRUE);

      IF LineReservedQtyBase > 0 THEN BEGIN
        IF LineReservedQtyBase <= "Qty. to Handle (Base)" THEN
          ReservedAndAssignedBase := LineReservedQtyBase
        ELSE
          ReservedAndAssignedBase := "Qty. to Handle (Base)";
      END;

      TotalAvailQtyToPickBase :=
        TotalAvailQtyToPickBase + AssignedQtyOnReservedLines - QtyAssgndOnWkshBase - ReservedAndAssignedBase;
      IF TotalAvailQtyToPickBase > 0 THEN
        EXIT(TotalAvailQtyToPickBase);
      EXIT(0);
    END;

    PROCEDURE AvailableQtyToPick@38() : Decimal;
    BEGIN
      IF "Qty. per Unit of Measure" <> 0 THEN
        EXIT(ROUND(AvailableQtyToPickBase / "Qty. per Unit of Measure",0.00001));
      EXIT(0);
    END;

    PROCEDURE SetUpNewLine@28(WhseWkshTemplate@1003 : Code[10];WhseWkshName@1002 : Code[10];LocationCode@1001 : Code[10];SortingMethod@1000 : ' ,Item,Document,Shelf/Bin No.,Due Date,Ship-To';LineNo@1004 : Integer);
    BEGIN
      TempWhseWorksheetLine.RESET;
      TempWhseWorksheetLine.SETRANGE("Worksheet Template Name",WhseWkshTemplate);
      TempWhseWorksheetLine.SETRANGE(Name,WhseWkshName);
      TempWhseWorksheetLine.SETRANGE("Location Code",LocationCode);
      IF TempWhseWorksheetLine.COUNT = 0 THEN
        LastLineNo := 0
      ELSE
        LastLineNo := LineNo;

      "Worksheet Template Name" := WhseWkshTemplate;
      Name := WhseWkshName;
      "Location Code" := LocationCode;
      "Line No." := GetNextLineNo(SortingMethod);
      "Whse. Document Type" := "Whse. Document Type"::"Whse. Mov.-Worksheet";
      "Whse. Document No." := WhseWkshName;
      "Whse. Document Line No." := "Line No.";
    END;

    PROCEDURE GetNextLineNo@31(SortMethod@1000 : ' ,Item,Document,Shelf/Bin No.,Due Date,Ship-To') : Integer;
    VAR
      WhseWorksheetLine2@1003 : Record 7326;
      HigherLineNo@1002 : Integer;
      LowerLineNo@1001 : Integer;
    BEGIN
      WhseWorksheetLine2.COPY(TempWhseWorksheetLine);
      IF SortMethod <> SortMethod::" " THEN
        EXIT(GetLastLineNo + 10000)
      ELSE BEGIN
        WhseWorksheetLine2 := Rec;
        WhseWorksheetLine2."Line No." := LastLineNo;
        IF WhseWorksheetLine2.FIND('<') THEN
          LowerLineNo := WhseWorksheetLine2."Line No."
        ELSE IF WhseWorksheetLine2.FIND('>') THEN
          EXIT(LastLineNo DIV 2)
        ELSE
          EXIT(LastLineNo + 10000);

        WhseWorksheetLine2 := Rec;
        WhseWorksheetLine2."Line No." := LastLineNo;
        IF WhseWorksheetLine2.FIND('>') THEN
          HigherLineNo := LastLineNo
        ELSE
          EXIT(LastLineNo + 10000);
        EXIT(LowerLineNo + (HigherLineNo - LowerLineNo) DIV 2);
      END;
    END;

    PROCEDURE GetLastLineNo@30() : Integer;
    VAR
      WhseWorksheetLine2@1000 : Record 7326;
    BEGIN
      WhseWorksheetLine2.COPYFILTERS(TempWhseWorksheetLine);
      IF WhseWorksheetLine2.FIND('+') THEN
        EXIT(WhseWorksheetLine2."Line No.");
      EXIT(0);
    END;

    PROCEDURE GetSortSeqNo@33(SortMethod@1000 : ' ,Item,Document,Shelf/Bin No.,Due Date,Ship-To') : Integer;
    VAR
      WhseWorksheetLine2@1004 : Record 7326;
      HigherSeqNo@1002 : Integer;
      LowerSeqNo@1003 : Integer;
      LastSeqNo@1001 : Integer;
    BEGIN
      WhseWorksheetLine2 := Rec;
      WhseWorksheetLine2.SETRECFILTER;
      WhseWorksheetLine2.SETRANGE("Line No.");

      CASE SortMethod OF
        SortMethod::" ":
          WhseWorksheetLine2.SETCURRENTKEY(
            "Worksheet Template Name",Name,"Location Code","Line No.");
        SortMethod::Item:
          WhseWorksheetLine2.SETCURRENTKEY(
            "Worksheet Template Name",Name,"Location Code","Item No.");
        SortMethod::Document:
          WhseWorksheetLine2.SETCURRENTKEY(
            "Worksheet Template Name",Name,"Location Code","Source Document","Source No.");
        SortMethod::"Shelf/Bin No.":
          BEGIN
            GetLocation("Location Code");
            IF Location."Bin Mandatory" THEN
              WhseWorksheetLine2.SETCURRENTKEY(
                "Worksheet Template Name",Name,"Location Code","To Bin Code")
            ELSE
              WhseWorksheetLine2.SETCURRENTKEY(
                "Worksheet Template Name",Name,"Location Code","Shelf No.");
          END;
        SortMethod::"Due Date":
          WhseWorksheetLine2.SETCURRENTKEY(
            "Worksheet Template Name",Name,"Location Code","Due Date");
        SortMethod::"Ship-To":
          WhseWorksheetLine2.SETCURRENTKEY(
            "Worksheet Template Name",Name,"Location Code","Destination Type","Destination No.")
      END;

      LastSeqNo := GetLastSeqNo(WhseWorksheetLine2);
      IF WhseWorksheetLine2.FIND('<') THEN
        LowerSeqNo := WhseWorksheetLine2."Sorting Sequence No."
      ELSE IF WhseWorksheetLine2.FIND('>') THEN
        EXIT(WhseWorksheetLine2."Sorting Sequence No." DIV 2)
      ELSE
        LowerSeqNo := 10000;

      WhseWorksheetLine2 := Rec;
      IF WhseWorksheetLine2.FIND('>') THEN
        HigherSeqNo := WhseWorksheetLine2."Sorting Sequence No."
      ELSE IF WhseWorksheetLine2.FIND('<') THEN
        EXIT(LastSeqNo + 10000)
      ELSE
        HigherSeqNo := LastSeqNo;
      EXIT(LowerSeqNo + (HigherSeqNo - LowerSeqNo) DIV 2);
    END;

    PROCEDURE GetLastSeqNo@32(WhseWorksheetLine2@1000 : Record 7326) : Integer;
    BEGIN
      WhseWorksheetLine2.SETRECFILTER;
      WhseWorksheetLine2.SETRANGE("Line No.");
      WhseWorksheetLine2.SETCURRENTKEY("Worksheet Template Name",Name,"Location Code","Sorting Sequence No.");
      IF WhseWorksheetLine2.FIND('+') THEN
        EXIT(WhseWorksheetLine2."Sorting Sequence No.");
      EXIT(0);
    END;

    PROCEDURE SetItemTrackingLines@34(SerialNo@1000 : Code[20];LotNo@1001 : Code[20];ExpirationDate@1004 : Date;QtyToEmpty@1002 : Decimal);
    VAR
      WhseItemTrackingForm@1003 : Form 6550;
    BEGIN
      TESTFIELD("Item No.");
      TESTFIELD("Qty. (Base)");
      CASE "Whse. Document Type" OF
        "Whse. Document Type"::Receipt:
          WhseItemTrackingForm.SetSource(Rec,DATABASE::"Posted Whse. Receipt Line");
        "Whse. Document Type"::Shipment:
          WhseItemTrackingForm.SetSource(Rec,DATABASE::"Warehouse Shipment Line");
        "Whse. Document Type"::"Internal Put-away":
          WhseItemTrackingForm.SetSource(Rec,DATABASE::"Whse. Internal Put-away Line");
        "Whse. Document Type"::"Internal Pick":
          WhseItemTrackingForm.SetSource(Rec,DATABASE::"Whse. Internal Pick Line");
        "Whse. Document Type"::Production:
          WhseItemTrackingForm.SetSource(Rec,DATABASE::"Prod. Order Component");
        ELSE
          WhseItemTrackingForm.SetSource(Rec,DATABASE::"Whse. Worksheet Line");
      END;

      WhseItemTrackingForm.InsertItemTrackingLine(Rec,SerialNo,LotNo,ExpirationDate,QtyToEmpty);
    END;

    BEGIN
    END.
  }
}
