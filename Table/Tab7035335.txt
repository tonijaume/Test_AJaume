OBJECT Table 7035335 Vuelo x Parte
{
  OBJECT-PROPERTIES
  {
    Date=23/02/10;
    Time=12:31:26;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               Mantener_Vuelos_Parte('');
             END;

    OnModify=VAR
               lrParte@1103355000 : Record 7010360;
             BEGIN
               Mantener_Vuelos_Parte("Referencia Vuelo");
             END;

    OnDelete=VAR
               lrParte@1103355000 : Record 7010360;
             BEGIN
               Mantener_Vuelos_Parte("Referencia Vuelo");
             END;

    OnRename=BEGIN
               ERROR(err1);
             END;

    CaptionML=ESP=Vuelo x Parte;
    LookupFormID=Form7035352;
  }
  FIELDS
  {
    { 2   ;   ;Codigo Touroperador ;Code10        ;TableRelation=TTOO.Codigo;
                                                   ValidateTableRelation=Yes;
                                                   CaptionML=ESP=Codigo Touroperador }
    { 4   ;   ;Referencia Vuelo    ;Code10        ;CaptionML=ESP=Referencia Vuelo;
                                                   NotBlank=Yes }
    { 5   ;   ;Fecha Traslado      ;Date          ;CaptionML=ESP=Fecha Traslado }
    { 6   ;   ;Fecha Llegada Vuelo ;Date          ;OnValidate=BEGIN
                                                                IF ("Fecha Llegada Vuelo" <> 0D) AND (Hora <> 0T) THEN BEGIN
                                                                  // JPT 02/02/10 Datetime a Biginteger. Validamos para asignar "FechaHora Vuelo BI"
                                                                  // "FechaHora Vuelo" := CREATEDATETIME("Fecha Llegada Vuelo", Hora);
                                                                  VALIDATE("FechaHora Vuelo", CREATEDATETIME("Fecha Llegada Vuelo", Hora));
                                                                END;
                                                              END;

                                                   CaptionML=ESP=Fecha Llegada Vuelo }
    { 7   ;   ;Hora                ;Time          ;OnValidate=BEGIN
                                                                IF ("Fecha Llegada Vuelo" <> 0D) AND (Hora <> 0T) THEN BEGIN
                                                                  // JPT 02/02/10 Datetime a Biginteger. Validamos para asignar "FechaHora Vuelo BI"
                                                                  // "FechaHora Vuelo" := CREATEDATETIME("Fecha Llegada Vuelo", Hora);
                                                                  VALIDATE("FechaHora Vuelo", CREATEDATETIME("Fecha Llegada Vuelo", Hora));
                                                                END;
                                                              END;

                                                   CaptionML=ESP=Hora }
    { 9   ;   ;IATA                ;Code15        ;CaptionML=ESP=IATA }
    { 11  ;   ;Terminal            ;Code1         ;CaptionML=ESP=Terminal }
    { 12  ;   ;N§Parte             ;Code10        ;TableRelation=Parte."N§ Parte";
                                                   ValidateTableRelation=No;
                                                   CaptionML=ESP=N§Parte;
                                                   NotBlank=Yes }
    { 13  ;   ;E/S                 ;Option        ;CaptionML=ESP=E/S;
                                                   OptionCaptionML=ESP=,Entrada,Salida;
                                                   OptionString=,Entrada,Salida;
                                                   NotBlank=Yes;
                                                   BlankZero=Yes }
    { 14  ;   ;Codigo Planing      ;Code10        ;CaptionML=ESP=Codigo Planing }
    { 15  ;   ;Estado              ;Option        ;CaptionML=ESP=Estado;
                                                   OptionCaptionML=ESP=Normal,Retrasado,Confirmado;
                                                   OptionString=Normal,Retrasado,Confirmado;
                                                   Description=Normal,Retrasado,Confirmado }
    { 16  ;   ;FechaHora Vuelo     ;DateTime      ;OnValidate=BEGIN
                                                                // JPT 02/02/10 Datetime a Biginteger
                                                                "FechaHora Vuelo BI" := cFunBas.DTtoBIGINT("FechaHora Vuelo");
                                                              END;

                                                   CaptionML=ESP=FechaHora Vuelo }
    { 17  ;   ;FechaHora Vuelo BI  ;BigInteger    ;OnValidate=BEGIN
                                                                // JPT 02/02/10 Datetime a Biginteger
                                                                "FechaHora Vuelo" := cFunBas.BIGINTtoDT("FechaHora Vuelo BI");
                                                              END;
                                                               }
  }
  KEYS
  {
    {    ;N§Parte,Referencia Vuelo,Codigo Touroperador;
                                                   Clustered=Yes }
    { No ;E/S                                      }
    {    ;Codigo Planing,Hora                      }
    {    ;Codigo Touroperador                      }
    {    ;N§Parte,Hora                             }
    { No ;FechaHora Vuelo                          }
    {    ;FechaHora Vuelo BI                       }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      err1@1100244000 : TextConst 'ESP=No es posible cambiar los registro de la Clave Primaria';
      err2@1103355001 : TextConst 'ESP=La hora del vuelo solo se puede actualizar para vuelos de llegada.';
      cFunBas@1103355000 : Codeunit 7010310;

    PROCEDURE Mantener_Vuelos_Parte@1(pwMenos@1100244000 : Code[10]);
    VAR
      lrVueloxPart@1100244001 : Record 7035335;
      lwVuelos@1100244002 : Text[250];
      lwPrimeraHora@1100244003 : Time;
      lrParte@1100244004 : Record 7010360;
      lrTVueloxPart2@1100244005 : TEMPORARY Record 7035335;
    BEGIN
      // Mantener_Vuelos_Parte
      //
      // Mantenimiento. Realiza la concatenaci¢n del campo vuelos en la tabla Partes
      // pwMenos representa un Codigo de Vuelo que no debe concatenerase, se utiliza para el mantenimiento
      // en el On Delete
      // Utiliza una tabla temporal puente para colocar y ordenar los elementos a concatenar y evita asi
      // el introducir un Commit


      lrVueloxPart.RESET;
      lrVueloxPart.SETRANGE("N§Parte", "N§Parte");
      CLEAR(lwVuelos);

      // Introduce la informaci¢n en la tabla temporal
      IF lrVueloxPart.FINDSET THEN
      REPEAT
        IF lrVueloxPart."Referencia Vuelo" <> pwMenos THEN BEGIN

          lrTVueloxPart2."N§Parte"          := lrVueloxPart."N§Parte";
          lrTVueloxPart2."Referencia Vuelo" := lrVueloxPart."Referencia Vuelo";
          lrTVueloxPart2.Hora               := lrVueloxPart.Hora;

          // ----  AJS 26.08.2002 Solo nos interesa insertar una linea por referencia vuelo en el fichero temporal  ----

          lrTVueloxPart2.SETRANGE("N§Parte"         , lrVueloxPart."N§Parte");
          lrTVueloxPart2.SETRANGE("Referencia Vuelo", lrVueloxPart."Referencia Vuelo");
          IF NOT lrTVueloxPart2.FINDFIRST THEN
            lrTVueloxPart2.INSERT;

          // ----  Fin AJS 26.08.2002  ----

        END;
      UNTIL lrVueloxPart.NEXT = 0;

      // Si no se ha de borrar y no est  el registro Rec, lo introduce
      IF (pwMenos = '') THEN BEGIN

        // ----  Este GET funciona aunque solo tenga 2 campos porque el tercer campo de la clave primaria esta en blanco  ----

        IF NOT lrTVueloxPart2.GET("N§Parte", "Referencia Vuelo") THEN BEGIN
          lrTVueloxPart2."N§Parte"          := "N§Parte";
          lrTVueloxPart2."Referencia Vuelo" := "Referencia Vuelo";
          lrTVueloxPart2.Hora               := Hora;
          lrTVueloxPart2.INSERT;
        END
        ELSE BEGIN
          lrTVueloxPart2.Hora := Hora;
          lrTVueloxPart2.MODIFY;
        END;
      END;

      // ----  Se ordena por hora, concatena y obtiene la primera hora  ----

      lrTVueloxPart2.RESET;
      lrTVueloxPart2.SETCURRENTKEY("N§Parte",
                                   Hora);
      lrTVueloxPart2.SETRANGE("N§Parte", "N§Parte");
      IF lrTVueloxPart2.FINDSET THEN BEGIN
        lwPrimeraHora := lrTVueloxPart2.Hora;
        REPEAT
          IF lwVuelos <> '' THEN
            lwVuelos := lwVuelos + ',';
          lwVuelos := lwVuelos + lrTVueloxPart2."Referencia Vuelo";
        UNTIL lrTVueloxPart2.NEXT = 0;
      END;

      IF lrParte.GET("N§Parte") THEN BEGIN
        lrParte.VALIDATE(Vuelos          , lwVuelos);
        lrParte.VALIDATE("Hora 1er Vuelo", lwPrimeraHora);
        lrParte.MODIFY;
      END;
    END;

    BEGIN
    {
      JPT 02/02/10 Datetime a Biginteger
      El campo "FechaHora Vuelo" ha sido sustituido por "FechaHora Vuelo BI".
      Se ha sustituido la clave en la que aparece este campo por una analoga
    }
    END.
  }
}
