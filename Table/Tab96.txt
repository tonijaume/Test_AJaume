OBJECT Table 96 G/L Budget Entry
{
  OBJECT-PROPERTIES
  {
    Date=13/04/15;
    Time=13:39:22;
    Modified=Yes;
    Version List=AIC2009,NAVW16.00,NAVES6.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CheckIfBlocked;
               TESTFIELD(Date);
               TESTFIELD("G/L Account No.");
               TESTFIELD("Budget Name");
               LOCKTABLE;
               "User ID" := USERID;
               IF "Entry No." = 0 THEN
                 "Entry No." := GetNextEntryNo;

               GetGLSetup;
               UpdateDim(GLSetup."Global Dimension 1 Code","Global Dimension 1 Code");
               UpdateDim(GLSetup."Global Dimension 2 Code","Global Dimension 2 Code");
               UpdateDim(GLBudgetName."Budget Dimension 1 Code","Budget Dimension 1 Code");
               UpdateDim(GLBudgetName."Budget Dimension 2 Code","Budget Dimension 2 Code");
               UpdateDim(GLBudgetName."Budget Dimension 3 Code","Budget Dimension 3 Code");
               UpdateDim(GLBudgetName."Budget Dimension 4 Code","Budget Dimension 4 Code");
             END;

    OnModify=BEGIN
               CheckIfBlocked;
               "User ID" := USERID;
               GetGLSetup;
               IF "Global Dimension 1 Code" <> xRec."Global Dimension 1 Code" THEN
                 UpdateDim(GLSetup."Global Dimension 1 Code","Global Dimension 1 Code");
               IF "Global Dimension 2 Code" <> xRec."Global Dimension 2 Code" THEN
                 UpdateDim(GLSetup."Global Dimension 2 Code","Global Dimension 2 Code");
               IF "Budget Dimension 1 Code" <> xRec."Budget Dimension 1 Code" THEN
                 UpdateDim(GLBudgetName."Budget Dimension 1 Code","Budget Dimension 1 Code");
               IF "Budget Dimension 2 Code" <> xRec."Budget Dimension 2 Code" THEN
                 UpdateDim(GLBudgetName."Budget Dimension 2 Code","Budget Dimension 2 Code");
               IF "Budget Dimension 3 Code" <> xRec."Budget Dimension 3 Code" THEN
                 UpdateDim(GLBudgetName."Budget Dimension 3 Code","Budget Dimension 3 Code");
               IF "Budget Dimension 4 Code" <> xRec."Budget Dimension 4 Code" THEN
                 UpdateDim(GLBudgetName."Budget Dimension 4 Code","Budget Dimension 4 Code");
             END;

    OnDelete=VAR
               GLBudgetDim@1000 : Record 361;
             BEGIN
               CheckIfBlocked;
               GLBudgetDim.SETRANGE("Entry No.","Entry No.");
               GLBudgetDim.DELETEALL;
             END;

    CaptionML=[ENU=G/L Budget Entry;
               ESP=Mov. presupuesto];
    LookupFormID=Form120;
    DrillDownFormID=Form120;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              ESP=N§ mov.] }
    { 2   ;   ;Budget Name         ;Code10        ;TableRelation="G/L Budget Name";
                                                   CaptionML=[ENU=Budget Name;
                                                              ESP=Nombre ppto.] }
    { 3   ;   ;G/L Account No.     ;Text20        ;TableRelation="G/L Account";
                                                   CaptionML=[ENU=G/L Account No.;
                                                              ESP=N§ cuenta] }
    { 4   ;   ;Date                ;Date          ;CaptionML=[ENU=Date;
                                                              ESP=Fecha];
                                                   ClosingDates=Yes }
    { 5   ;   ;Global Dimension 1 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                IF "Global Dimension 1 Code" = '' THEN
                                                                  EXIT;
                                                                GetGLSetup;
                                                                ValidateDimValue(GLSetup."Global Dimension 1 Code","Global Dimension 1 Code");
                                                              END;

                                                   CaptionML=[ENU=Global Dimension 1 Code;
                                                              ESP=C¢d. dimensi¢n global 1];
                                                   CaptionClass='1,1,1' }
    { 6   ;   ;Global Dimension 2 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                IF "Global Dimension 2 Code" = '' THEN
                                                                  EXIT;
                                                                GetGLSetup;
                                                                ValidateDimValue(GLSetup."Global Dimension 2 Code","Global Dimension 2 Code");
                                                              END;

                                                   CaptionML=[ENU=Global Dimension 2 Code;
                                                              ESP=C¢d. dimensi¢n global 2];
                                                   CaptionClass='1,1,2' }
    { 7   ;   ;Amount              ;Decimal       ;CaptionML=[ENU=Amount;
                                                              ESP=Importe];
                                                   AutoFormatType=1 }
    { 9   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n] }
    { 10  ;   ;Business Unit Code  ;Code10        ;TableRelation="Business Unit";
                                                   CaptionML=[ENU=Business Unit Code;
                                                              ESP=C¢d. empresa] }
    { 11  ;   ;User ID             ;Code20        ;TableRelation=User;
                                                   OnLookup=VAR
                                                              LoginMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              LoginMgt.LookupUserID("User ID");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=User ID;
                                                              ESP=Id. usuario];
                                                   Editable=No }
    { 12  ;   ;Budget Dimension 1 Code;Code20     ;OnValidate=BEGIN
                                                                IF "Budget Dimension 1 Code" = '' THEN
                                                                  EXIT;
                                                                IF GLBudgetName.Name <> "Budget Name" THEN
                                                                  GLBudgetName.GET("Budget Name");
                                                                ValidateDimValue(GLBudgetName."Budget Dimension 1 Code","Budget Dimension 1 Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              "Budget Dimension 1 Code" := OnLookupDimCode(2,"Budget Dimension 1 Code");
                                                            END;

                                                   CaptionML=[ENU=Budget Dimension 1 Code;
                                                              ESP=C¢d. dim. presupuesto 1];
                                                   CaptionClass=GetCaptionClass(1) }
    { 13  ;   ;Budget Dimension 2 Code;Code20     ;OnValidate=BEGIN
                                                                IF "Budget Dimension 2 Code" = '' THEN
                                                                  EXIT;
                                                                IF GLBudgetName.Name <> "Budget Name" THEN
                                                                  GLBudgetName.GET("Budget Name");
                                                                ValidateDimValue(GLBudgetName."Budget Dimension 2 Code","Budget Dimension 2 Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              "Budget Dimension 2 Code" := OnLookupDimCode(3,"Budget Dimension 2 Code");
                                                            END;

                                                   CaptionML=[ENU=Budget Dimension 2 Code;
                                                              ESP=C¢d. dim. presupuesto 2];
                                                   CaptionClass=GetCaptionClass(2) }
    { 14  ;   ;Budget Dimension 3 Code;Code20     ;OnValidate=BEGIN
                                                                IF "Budget Dimension 3 Code" = '' THEN
                                                                  EXIT;
                                                                IF GLBudgetName.Name <> "Budget Name" THEN
                                                                  GLBudgetName.GET("Budget Name");
                                                                ValidateDimValue(GLBudgetName."Budget Dimension 3 Code","Budget Dimension 3 Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              "Budget Dimension 3 Code" := OnLookupDimCode(4,"Budget Dimension 3 Code");
                                                            END;

                                                   CaptionML=[ENU=Budget Dimension 3 Code;
                                                              ESP=C¢d. dim. presupuesto 3];
                                                   CaptionClass=GetCaptionClass(3) }
    { 15  ;   ;Budget Dimension 4 Code;Code20     ;OnValidate=BEGIN
                                                                IF "Budget Dimension 4 Code" = '' THEN
                                                                  EXIT;
                                                                IF GLBudgetName.Name <> "Budget Name" THEN
                                                                  GLBudgetName.GET("Budget Name");
                                                                ValidateDimValue(GLBudgetName."Budget Dimension 4 Code","Budget Dimension 4 Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              "Budget Dimension 4 Code" := OnLookupDimCode(5,"Budget Dimension 4 Code");
                                                            END;

                                                   CaptionML=[ENU=Budget Dimension 4 Code;
                                                              ESP=C¢d. dim. presupuesto 4];
                                                   CaptionClass=GetCaptionClass(4) }
    { 10720;  ;Old G/L Account No. ;Text20        ;TableRelation="Historic G/L Account".No.;
                                                   CaptionML=[ENU=Old G/L Account No.;
                                                              ESP=N§ de cuenta antiguo] }
    { 10721;  ;Updated             ;Boolean       ;CaptionML=[ENU=Updated;
                                                              ESP=Actualizado] }
    { 7009700;;Group account       ;Text20        ;TableRelation="G/L Account - corporative".No.;
                                                   CaptionML=[ENU=Group Account;
                                                              ESP=Cuenta corporativa];
                                                   Description=$001 }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Budget Name,G/L Account No.,Date        ;SumIndexFields=Amount }
    {    ;Budget Name,G/L Account No.,Business Unit Code,Global Dimension 1 Code,Global Dimension 2 Code,Budget Dimension 1 Code,Budget Dimension 2 Code,Budget Dimension 3 Code,Budget Dimension 4 Code,Date;
                                                   SumIndexFields=Amount }
    {    ;Budget Name,G/L Account No.,Description,Date }
    {    ;Budget Name,Old G/L Account No.,Date    ;SumIndexFields=Amount }
    {    ;Budget Name,Old G/L Account No.,Business Unit Code,Global Dimension 1 Code,Global Dimension 2 Code,Budget Dimension 1 Code,Budget Dimension 2 Code,Budget Dimension 3 Code,Budget Dimension 4 Code,Date;
                                                   SumIndexFields=Amount }
    {    ;Budget Name,Group account,Date          ;SumIndexFields=Amount }
    {    ;Budget Name,Group account,Business Unit Code,Global Dimension 1 Code,Global Dimension 2 Code,Budget Dimension 1 Code,Budget Dimension 2 Code,Budget Dimension 3 Code,Budget Dimension 4 Code,Date;
                                                   SumIndexFields=Amount }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=The dimension value %1 has not been set up for dimension %2.;ESP=El valor dimensi¢n %1 no ha sido config. para la dimensi¢n %2.';
      Text001@1001 : TextConst 'ENU=1,5,,Budget Dimension 1 Code;ESP=1,5,,C¢digo Dimensi¢n Presupuesto 1';
      Text002@1002 : TextConst 'ENU=1,5,,Budget Dimension 2 Code;ESP=1,5,,C¢digo Dimensi¢n Presupuesto 2';
      Text003@1003 : TextConst 'ENU=1,5,,Budget Dimension 3 Code;ESP=1,5,,C¢digo Dimensi¢n Presupuesto 3';
      Text004@1004 : TextConst 'ENU=1,5,,Budget Dimension 4 Code;ESP=1,5,,C¢digo Dimensi¢n Presupuesto 4';
      GLBudgetName@1005 : Record 95;
      GLSetup@1006 : Record 98;
      GLSetupRetrieved@1007 : Boolean;

    PROCEDURE CheckIfBlocked@1();
    BEGIN
      IF "Budget Name" = GLBudgetName.Name THEN
        EXIT;
      IF GLBudgetName.Name <> "Budget Name" THEN
        GLBudgetName.GET("Budget Name");
      GLBudgetName.TESTFIELD(Blocked,FALSE);
    END;

    LOCAL PROCEDURE ValidateDimValue@5(DimCode@1000 : Code[20];VAR DimValueCode@1001 : Code[20]) : Boolean;
    VAR
      DimValue@1002 : Record 349;
    BEGIN
      DimValue."Dimension Code" := DimCode;
      DimValue.Code := DimValueCode;
      DimValue.FIND('=><');
      IF DimValueCode <> COPYSTR(DimValue.Code,1,STRLEN(DimValueCode)) THEN
        ERROR(Text000,DimValueCode,DimCode);
      DimValueCode := DimValue.Code;
    END;

    PROCEDURE GetGLSetup@2();
    BEGIN
      IF NOT GLSetupRetrieved THEN BEGIN
        GLSetup.GET;
        GLSetupRetrieved := TRUE;
      END;
    END;

    PROCEDURE OnLookupDimCode@3(DimOption@1000 : 'Global Dimension 1,Global Dimension 2,Budget Dimension 1,Budget Dimension 2,Budget Dimension 3,Budget Dimension 4';DefaultValue@1001 : Code[20]) : Code[20];
    VAR
      DimValue@1002 : Record 349;
      DimValueList@1003 : Form 560;
    BEGIN
      IF DimOption IN [DimOption::"Global Dimension 1",DimOption::"Global Dimension 2"] THEN
        GetGLSetup
      ELSE
        IF GLBudgetName.Name <> "Budget Name" THEN
          GLBudgetName.GET("Budget Name");
      CASE DimOption OF
        DimOption::"Global Dimension 1" :
          DimValue."Dimension Code" := GLSetup."Global Dimension 1 Code";
        DimOption::"Global Dimension 2" :
          DimValue."Dimension Code" := GLSetup."Global Dimension 2 Code";
        DimOption::"Budget Dimension 1" :
          DimValue."Dimension Code" := GLBudgetName."Budget Dimension 1 Code";
        DimOption::"Budget Dimension 2" :
          DimValue."Dimension Code" := GLBudgetName."Budget Dimension 2 Code";
        DimOption::"Budget Dimension 3" :
          DimValue."Dimension Code" := GLBudgetName."Budget Dimension 3 Code";
        DimOption::"Budget Dimension 4" :
          DimValue."Dimension Code" := GLBudgetName."Budget Dimension 4 Code";
      END;
      DimValue.SETRANGE("Dimension Code",DimValue."Dimension Code");
      IF DimValue.GET(DimValue."Dimension Code",DefaultValue) THEN;
      DimValueList.SETTABLEVIEW(DimValue);
      DimValueList.SETRECORD(DimValue);
      DimValueList.LOOKUPMODE := TRUE;
      IF DimValueList.RUNMODAL = ACTION::LookupOK THEN BEGIN
        DimValueList.GETRECORD(DimValue);
        EXIT(DimValue.Code);
      END ELSE
        EXIT(DefaultValue);
    END;

    PROCEDURE UpdateDim@6(DimCode@1000 : Code[20];DimValueCode@1001 : Code[20]);
    VAR
      GLBudgetDim@1002 : Record 361;
    BEGIN
      IF DimCode = '' THEN
        EXIT;
      WITH GLBudgetDim DO BEGIN
        IF GET(Rec."Entry No.",DimCode) THEN
          DELETE;
        IF DimValueCode <> '' THEN BEGIN
          INIT;
          "Entry No." := Rec."Entry No.";
          "Dimension Code" := DimCode;
          "Dimension Value Code" := DimValueCode;
          INSERT;
        END;
      END;
    END;

    LOCAL PROCEDURE GetNextEntryNo@4() : Integer;
    VAR
      GLBudgetEntry@1000 : Record 96;
    BEGIN
      GLBudgetEntry.SETCURRENTKEY("Entry No.");
      IF GLBudgetEntry.FIND('+') THEN
        EXIT(GLBudgetEntry."Entry No." + 1)
      ELSE
        EXIT(1);
    END;

    PROCEDURE GetCaptionClass@7(BudgetDimType@1000 : Integer) : Text[250];
    BEGIN
      IF GLBudgetName.Name <> "Budget Name" THEN
        IF NOT GLBudgetName.GET("Budget Name") THEN
          EXIT('');
      CASE BudgetDimType OF
        1:
          BEGIN
            IF GLBudgetName."Budget Dimension 1 Code" <> '' THEN
              EXIT('1,5,' + GLBudgetName."Budget Dimension 1 Code")
            ELSE
              EXIT(Text001);
          END;
        2:
          BEGIN
            IF GLBudgetName."Budget Dimension 2 Code" <> '' THEN
              EXIT('1,5,' + GLBudgetName."Budget Dimension 2 Code")
            ELSE
              EXIT(Text002);
          END;
        3:
          BEGIN
            IF GLBudgetName."Budget Dimension 3 Code" <> '' THEN
              EXIT('1,5,' + GLBudgetName."Budget Dimension 3 Code")
            ELSE
              EXIT(Text003);
          END;
        4:
          BEGIN
            IF GLBudgetName."Budget Dimension 4 Code" <> '' THEN
              EXIT('1,5,' + GLBudgetName."Budget Dimension 4 Code")
            ELSE
              EXIT(Text004);
          END;
      END;
    END;

    BEGIN
    {
      $001 ARM 09042015 CNT-OC-15129 Nuevo campo cuenta coporporativa
    }
    END.
  }
}
