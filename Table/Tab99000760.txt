OBJECT Table 99000760 Calendar Absence Entry
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               CalAbsenceMgt.RemoveAbsence(Rec);
             END;

    OnRename=BEGIN
               CalAbsenceMgt.RemoveAbsence(xRec);
               Updated := FALSE;
             END;

    CaptionML=[ENU=Calendar Absence Entry;
               ESP=Calendario mov. ausencia];
    LookupFormID=Form99000772;
    DrillDownFormID=Form99000772;
  }
  FIELDS
  {
    { 1   ;   ;Capacity Type       ;Option        ;CaptionML=[ENU=Capacity Type;
                                                              ESP=Tipo capacidad];
                                                   OptionCaptionML=[ENU=Work Center,Machine Center;
                                                                    ESP=Centro trabajo,Centro m quina];
                                                   OptionString=Work Center,Machine Center;
                                                   Editable=No }
    { 2   ;   ;No.                 ;Code20        ;TableRelation=IF (Capacity Type=CONST(Work Center)) "Work Center"
                                                                 ELSE IF (Capacity Type=CONST(Machine Center)) "Machine Center";
                                                   OnValidate=BEGIN
                                                                IF "No." = '' THEN
                                                                  EXIT;

                                                                CASE "Capacity Type" OF
                                                                  "Capacity Type"::"Work Center":
                                                                    BEGIN
                                                                      Workcenter.GET("No.");
                                                                      Workcenter.TESTFIELD("Work Center Group Code");
                                                                      "Work Center No." := Workcenter."No.";
                                                                      "Work Center Group Code" := Workcenter."Work Center Group Code";
                                                                      Capacity := Workcenter.Capacity;
                                                                    END;
                                                                  "Capacity Type"::"Machine Center":
                                                                    BEGIN
                                                                      Machinecenter.GET("No.");
                                                                      Machinecenter.TESTFIELD("Work Center No.");
                                                                      Workcenter.GET(Machinecenter."Work Center No.");
                                                                      Workcenter.TESTFIELD("Work Center Group Code");
                                                                      "Work Center No." := Workcenter."No.";
                                                                      "Work Center Group Code" := Workcenter."Work Center Group Code";
                                                                      Capacity := Machinecenter.Capacity;
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              ESP=N§];
                                                   NotBlank=Yes }
    { 4   ;   ;Date                ;Date          ;OnValidate=BEGIN
                                                                UpdateDatetime;
                                                              END;

                                                   CaptionML=[ENU=Date;
                                                              ESP=Fecha];
                                                   NotBlank=Yes }
    { 6   ;   ;Starting Time       ;Time          ;OnValidate=BEGIN
                                                                IF ("Ending Time" = 0T) OR
                                                                   ("Ending Time" < "Starting Time" )
                                                                THEN
                                                                  VALIDATE("Ending Time","Starting Time")
                                                              END;

                                                   CaptionML=[ENU=Starting Time;
                                                              ESP=Hora inicial] }
    { 7   ;   ;Ending Time         ;Time          ;OnValidate=BEGIN
                                                                IF "Ending Time" < "Starting Time" THEN
                                                                  ERROR(Text000,FIELDCAPTION("Ending Time"),FIELDCAPTION("Starting Time"));

                                                                UpdateDatetime;
                                                              END;

                                                   CaptionML=[ENU=Ending Time;
                                                              ESP=Hora final] }
    { 8   ;   ;Work Center No.     ;Code20        ;TableRelation="Work Center";
                                                   CaptionML=[ENU=Work Center No.;
                                                              ESP=N§ centro trabajo];
                                                   Editable=No }
    { 9   ;   ;Work Center Group Code;Code10      ;TableRelation="Work Center Group";
                                                   CaptionML=[ENU=Work Center Group Code;
                                                              ESP=C¢d. grupo centro trab.];
                                                   Editable=No }
    { 21  ;   ;Capacity            ;Decimal       ;OnValidate=BEGIN
                                                                IF Rec.Capacity <> xRec.Capacity THEN BEGIN
                                                                  CalAbsenceMgt.RemoveAbsence(xRec);
                                                                  Updated := FALSE;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Capacity;
                                                              ESP=Capacidad];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 24  ;   ;Starting Date-Time  ;DateTime      ;OnValidate=BEGIN
                                                                "Starting Time" := DT2TIME("Starting Date-Time");
                                                                Date := DT2DATE("Starting Date-Time");
                                                                VALIDATE("Starting Time");
                                                              END;

                                                   CaptionML=[ENU=Starting Date-Time;
                                                              ESP=Fecha-hora inicial] }
    { 25  ;   ;Ending Date-Time    ;DateTime      ;OnValidate=BEGIN
                                                                "Ending Time" := DT2TIME("Ending Date-Time");
                                                                Date := DT2DATE("Ending Date-Time");
                                                                VALIDATE("Ending Time");
                                                              END;

                                                   CaptionML=[ENU=Ending Date-Time;
                                                              ESP=Fecha-hora final] }
    { 31  ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n] }
    { 32  ;   ;Updated             ;Boolean       ;CaptionML=[ENU=Updated;
                                                              ESP=Actualizado];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Capacity Type,No.,Date,Starting Time,Ending Time;
                                                   Clustered=Yes }
    {    ;Work Center No.                          }
    {    ;Capacity Type,No.,Date,Starting Date-Time,Ending Date-Time }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 must be higher than %2.;ESP=%1 debe ser mayor que %2.';
      Workcenter@1001 : Record 99000754;
      Machinecenter@1002 : Record 99000758;
      CalAbsenceMgt@1003 : Codeunit 99000759;

    PROCEDURE Caption@3() : Text[100];
    BEGIN
      IF "Capacity Type" = "Capacity Type"::"Machine Center" THEN BEGIN
        IF NOT Machinecenter.GET(GETFILTER("No.")) THEN
          EXIT('');
        EXIT(
          STRSUBSTNO('%1 %2',
            Machinecenter."No.",Machinecenter.Name));
      END ELSE BEGIN
        IF NOT Workcenter.GET(GETFILTER("No.")) THEN
          EXIT('');
        EXIT(
          STRSUBSTNO('%1 %2',
            Workcenter."No.",Workcenter.Name));
      END;
    END;

    PROCEDURE UpdateDatetime@11();
    BEGIN
      "Starting Date-Time" := CREATEDATETIME(Date,"Starting Time");
      "Ending Date-Time" := CREATEDATETIME(Date,"Ending Time");
    END;

    BEGIN
    END.
  }
}
