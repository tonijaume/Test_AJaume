OBJECT Table 170 Standard Sales Code
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Code,Description;
    OnDelete=BEGIN
               StdSalesLine.RESET;
               StdSalesLine.SETRANGE("Standard Sales Code",Code);
               StdSalesLine.DELETEALL(TRUE);
             END;

    OnRename=VAR
               DimDoc@1000 : Record 357;
               DimDoc2@1001 : Record 357;
             BEGIN
               DimDoc.SETRANGE("Table ID",DATABASE::"Standard Sales Line");
               DimDoc.SETRANGE("Document Type",DimDoc."Document Type"::" ");
               DimDoc.SETRANGE("Document No.",xRec.Code);
               IF DimDoc.FIND('-') THEN
                 REPEAT
                   DimDoc2 := DimDoc;
                   DimDoc2."Document No." := Code;
                   IF DimDoc2.INSERT THEN;
                 UNTIL DimDoc.NEXT = 0;
               DimDoc.DELETEALL;
             END;

    CaptionML=[ENU=Standard Sales Code;
               ESP=C¢digo ventas est ndar];
    PasteIsValid=No;
    LookupFormID=Form172;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              ESP=C¢digo];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n] }
    { 3   ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   OnValidate=VAR
                                                                Currency@1000 : Record 4;
                                                                Currency2@1001 : Record 4;
                                                              BEGIN
                                                                IF NOT Currency.GET("Currency Code") THEN
                                                                  Currency.InitRoundingPrecision;
                                                                IF NOT Currency2.GET(xRec."Currency Code") THEN
                                                                  Currency2.InitRoundingPrecision;

                                                                IF Currency."Amount Rounding Precision" <> Currency2."Amount Rounding Precision" THEN BEGIN
                                                                  StdSalesLine.RESET;
                                                                  StdSalesLine.SETRANGE("Standard Sales Code",Code);
                                                                  StdSalesLine.SETRANGE(Type,StdSalesLine.Type::"G/L Account");
                                                                  StdSalesLine.SETFILTER("Amount Excl. VAT",'<>%1',0);
                                                                  IF StdSalesLine.FIND('-') THEN BEGIN
                                                                    IF NOT CONFIRM(
                                                                      STRSUBSTNO(
                                                                        Text001,FIELDCAPTION("Currency Code"),StdSalesLine.FIELDCAPTION("Amount Excl. VAT"),
                                                                        FIELDCAPTION("Currency Code")),TRUE)
                                                                    THEN
                                                                      ERROR(Text002);
                                                                    REPEAT
                                                                      StdSalesLine."Amount Excl. VAT" :=
                                                                        ROUND(StdSalesLine."Amount Excl. VAT",Currency."Amount Rounding Precision");
                                                                      StdSalesLine.MODIFY;
                                                                    UNTIL StdSalesLine.NEXT = 0;
                                                                  END;
                                                                END;
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Currency Code;
                                                              ESP=C¢d. divisa] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      StdSalesLine@1000 : Record 171;
      Text001@1002 : TextConst 'ENU=If you change the %1, the %2 will be rounded according to the new %3.;ESP=Si cambia el %1, el %2 ser  redondeado con el nuevo %3.';
      Text002@1003 : TextConst 'ENU=The update has been interrupted to respect the warning.;ESP=Se ha interrumpido la actualizaci¢n para respetar la advertencia.';

    BEGIN
    END.
  }
}
