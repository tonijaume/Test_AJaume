OBJECT Table 5998 Standard Service Item Gr. Code
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    DataCaptionFields=Service Item Group Code;
    OnDelete=BEGIN
               StdServCode.RESET;
               StdServCode.SETRANGE(Code,Code);
               StdServCode.DELETEALL;

               StdServiceLine.RESET;
               StdServiceLine.SETRANGE("Standard Service Code",Code);
               IF FINDSET THEN
                 StdServiceLine.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=Standard Service Item Gr. Code;
               ESP=C¢d. grupo prod. serv. est.];
  }
  FIELDS
  {
    { 1   ;   ;Service Item Group Code;Code10     ;TableRelation="Service Item Group";
                                                   CaptionML=[ENU=Service Item Group Code;
                                                              ESP=C¢d. grupo prod. servicio] }
    { 2   ;   ;Code                ;Code10        ;TableRelation="Standard Service Code";
                                                   OnValidate=VAR
                                                                StdServCode@1000 : Record 5996;
                                                              BEGIN
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              ESP=C¢digo];
                                                   NotBlank=Yes }
    { 3   ;   ;Description         ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Standard Service Code".Description WHERE (Code=FIELD(Code)));
                                                   CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Service Item Group Code,Code            ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=%1 of the standard service code must be equal to %2 on the %3.;ESP=%1 del c¢digo servicio est ndar debe ser igual a %2 en %3.';
      StdServCode@1001 : Record 5996;
      StdServiceLine@1002 : Record 5997;

    PROCEDURE InsertServiceLines@1(ServItemLine@1000 : Record 5901);
    VAR
      Currency@1001 : Record 4;
      DimDoc@1002 : Record 357;
      ServHeader@1003 : Record 5900;
      ServLine@1004 : Record 5902;
      StdServCode@1005 : Record 5996;
      StdServLine@1006 : Record 5997;
      StdServItemGrCode@1007 : Record 5998;
      StdServItemGrCodesForm@1009 : Form 5959;
      Factor@1008 : Integer;
    BEGIN
      ServItemLine.TESTFIELD("Line No.");

      CLEAR(StdServItemGrCodesForm);
      StdServItemGrCode.RESET;
      IF ServItemLine."Service Item Group Code" <> '' THEN
        StdServItemGrCode.SETRANGE("Service Item Group Code",ServItemLine."Service Item Group Code");
      StdServItemGrCodesForm.SETRECORD(StdServItemGrCode);
      StdServItemGrCodesForm.SETTABLEVIEW(StdServItemGrCode);
      StdServItemGrCodesForm.LOOKUPMODE := TRUE;

      IF NOT (StdServItemGrCodesForm.RUNMODAL = ACTION::LookupOK) THEN
        EXIT;
      StdServItemGrCodesForm.GETRECORD(StdServItemGrCode);
      StdServItemGrCode.TESTFIELD(Code);
      StdServCode.GET(StdServItemGrCode.Code);

      StdServCode.TESTFIELD(Code);
      ServHeader.GET(ServItemLine."Document Type",ServItemLine."Document No.");
      IF StdServCode."Currency Code" <> ServHeader."Currency Code" THEN
        ERROR(
          Text001,
          StdServCode.FIELDCAPTION("Currency Code"),
          ServHeader.FIELDCAPTION("Currency Code"),ServHeader.TABLECAPTION);
      StdServLine.SETRANGE("Standard Service Code",StdServCode.Code);
      ServLine."Document Type" := ServItemLine."Document Type";
      ServLine."Document No." := ServItemLine."Document No.";
      ServLine.SETRANGE("Document Type",ServItemLine."Document Type");
      ServLine.SETRANGE("Document No.",ServItemLine."Document No.");
      IF ServHeader."Prices Including VAT" THEN
        Factor := 1
      ELSE
        Factor := 0;
      DimDoc.LOCKTABLE;
      ServLine.LOCKTABLE;
      StdServLine.LOCKTABLE;
      IF StdServLine.FIND('-') THEN
        REPEAT
          ServLine.INIT;
          ServLine."Line No." := 0;
          ServLine.VALIDATE(Type,StdServLine.Type);
          IF ServHeader."Link Service to Service Item" THEN
            ServLine.VALIDATE("Service Item Line No.",ServItemLine."Line No.");
          IF StdServLine.Type = StdServLine.Type::" " THEN BEGIN
            ServLine.VALIDATE("No.",StdServLine."No.");
            ServLine.Description := StdServLine.Description
          END ELSE BEGIN
            IF NOT StdServLine.EmptyLine THEN BEGIN
              StdServLine.TESTFIELD("No.");
              ServLine.VALIDATE("No.",StdServLine."No.");
              IF StdServLine."Variant Code" <> '' THEN
                ServLine.VALIDATE("Variant Code",StdServLine."Variant Code");
              ServLine.VALIDATE(Quantity,StdServLine.Quantity);
              IF StdServLine."Unit of Measure Code" <> '' THEN
                ServLine.VALIDATE("Unit of Measure Code",StdServLine."Unit of Measure Code");
              ServLine.Description := StdServLine.Description;
              IF StdServLine.Type = StdServLine.Type::"G/L Account" THEN
                ServLine.VALIDATE(
                  "Unit Price",
                  ROUND(StdServLine."Amount Excl. VAT" *
                    (ServLine."VAT %" / 100 * Factor + 1),Currency."Unit-Amount Rounding Precision"));
            END;
          END;
          IF StdServLine.InsertLine THEN BEGIN
            ServLine."Line No." := GetNextLineNo(ServLine);
            ServLine.INSERT(TRUE);
            UpDateDim(ServLine,StdServLine);
            InsertExtendedText(ServLine);
          END;
        UNTIL StdServLine.NEXT = 0;
    END;

    LOCAL PROCEDURE UpDateDim@2(ServLine@1000 : Record 5902;StdServLine@1001 : Record 5997);
    VAR
      DimDoc@1002 : Record 357;
      DimDoc2@1003 : Record 357;
    BEGIN
      DimDoc."Table ID" := DATABASE::"Service Line";
      DimDoc."Document Type" := ServLine."Document Type";
      DimDoc."Document No." := ServLine."Document No.";
      DimDoc."Line No." := ServLine."Line No.";
      DimDoc2."Table ID" := DATABASE::"Standard Service Line";
      DimDoc2."Document Type" := DimDoc2."Document Type"::" ";
      DimDoc2."Document No." := StdServLine."Standard Service Code";
      DimDoc2."Line No." := StdServLine."Line No.";

      DimDoc.SETRANGE("Table ID",DATABASE::"Service Line");
      DimDoc.SETRANGE("Document Type",ServLine."Document Type");
      DimDoc.SETRANGE("Document No.",ServLine."Document No.");
      DimDoc.SETRANGE("Line No.",ServLine."Line No.");
      DimDoc2.SETRANGE("Table ID",DATABASE::"Standard Service Line");
      DimDoc2.SETRANGE("Document Type",DimDoc2."Document Type"::" ");
      DimDoc2.SETRANGE("Document No.",StdServLine."Standard Service Code");
      DimDoc2.SETRANGE("Line No.",StdServLine."Line No.");

      IF DimDoc2.FIND('-') THEN
        REPEAT
          DimDoc."Dimension Code" := DimDoc2."Dimension Code";
          DimDoc."Dimension Value Code" := '';
          IF DimDoc.FIND THEN BEGIN
            IF DimDoc."Dimension Value Code" <> DimDoc2."Dimension Value Code" THEN BEGIN
              DimDoc."Dimension Value Code" := DimDoc2."Dimension Value Code";
              DimDoc.MODIFY(TRUE);
            END;
          END ELSE BEGIN
            DimDoc."Dimension Value Code" := DimDoc2."Dimension Value Code";
            DimDoc.INSERT(TRUE);
          END;
        UNTIL DimDoc2.NEXT = 0;
    END;

    PROCEDURE InsertExtendedText@3(ServLine@1000 : Record 5902);
    VAR
      TransferExtendedText@1001 : Codeunit 378;
    BEGIN
      IF TransferExtendedText.ServCheckIfAnyExtText(ServLine,FALSE) THEN
        TransferExtendedText.InsertServExtText(ServLine);
    END;

    PROCEDURE GetNextLineNo@4(ServLine@1001 : Record 5902) : Integer;
    BEGIN
      ServLine.SETRANGE("Document Type",ServLine."Document Type");
      ServLine.SETRANGE("Document No.",ServLine."Document No.");
      IF ServLine.FIND('+') THEN
        EXIT(ServLine."Line No." + 10000);
      EXIT(10000);
    END;

    BEGIN
    END.
  }
}
