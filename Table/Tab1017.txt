OBJECT Table 1017 Job Buffer
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Job Buffer;
               ESP=Mem. interna proyecto];
  }
  FIELDS
  {
    { 1   ;   ;Account No. 1       ;Code20        ;CaptionML=[ENU=Account No. 1;
                                                              ESP=N§ cuenta 1] }
    { 2   ;   ;Account No. 2       ;Code20        ;CaptionML=[ENU=Account No. 2;
                                                              ESP=N§ cuenta 2] }
    { 3   ;   ;Amount 1            ;Decimal       ;CaptionML=[ENU=Amount 1;
                                                              ESP=Importe 1] }
    { 4   ;   ;Amount 2            ;Decimal       ;CaptionML=[ENU=Amount 2;
                                                              ESP=Importe 2] }
    { 5   ;   ;Amount 3            ;Decimal       ;CaptionML=[ENU=Amount 3;
                                                              ESP=Importe 3] }
    { 6   ;   ;Amount 4            ;Decimal       ;CaptionML=[ENU=Amount 4;
                                                              ESP=Importe 4] }
    { 7   ;   ;Amount 5            ;Decimal       ;CaptionML=[ENU=Amount 5;
                                                              ESP=Importe 5] }
    { 10  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n] }
    { 11  ;   ;New Total           ;Boolean       ;CaptionML=[ENU=New Total;
                                                              ESP=Nuevo total] }
  }
  KEYS
  {
    {    ;Account No. 1,Account No. 2             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      JobBuffer@1000 : ARRAY [2] OF TEMPORARY Record 1017;

    PROCEDURE InsertWorkInProgress@14(VAR Job@1000 : Record 167);
    VAR
      JobWIPGLEntry@1001 : Record 1005;
    BEGIN
      CLEAR(JobBuffer);
      JobWIPGLEntry.SETCURRENTKEY("Job No.");
      JobWIPGLEntry.SETRANGE("Job No.",Job."No.");
      JobWIPGLEntry.SETRANGE(Reversed,FALSE);
      JobWIPGLEntry.SETRANGE("Job Complete",FALSE);

      JobWIPGLEntry.SETFILTER("Posting Date",Job.GETFILTER("Posting Date Filter"));
      IF JobWIPGLEntry.FIND('-') THEN
        REPEAT
          CLEAR(JobBuffer);
          IF JobWIPGLEntry."G/L Account No." <> '' THEN BEGIN
            JobBuffer[1]."Account No. 1" := JobWIPGLEntry."G/L Account No.";
            JobBuffer[1]."Account No. 2" := JobWIPGLEntry."Job Posting Group";
            IF (JobWIPGLEntry.Type = JobWIPGLEntry.Type::"WIP Costs") OR
               (JobWIPGLEntry.Type = JobWIPGLEntry.Type::"Recognized Costs") THEN
              JobBuffer[1]."Amount 1" := JobWIPGLEntry."WIP Entry Amount"
            ELSE IF (JobWIPGLEntry.Type = JobWIPGLEntry.Type::"Accrued Costs") THEN
              JobBuffer[1]."Amount 2" := JobWIPGLEntry."WIP Entry Amount";
            IF (JobWIPGLEntry.Type = JobWIPGLEntry.Type::"WIP Sales") OR
               (JobWIPGLEntry.Type = JobWIPGLEntry.Type::"Recognized Sales") THEN
              JobBuffer[1]."Amount 4" := JobWIPGLEntry."WIP Entry Amount"
            ELSE IF (JobWIPGLEntry.Type = JobWIPGLEntry.Type::"Accrued Sales") THEN
              JobBuffer[1]."Amount 5" := JobWIPGLEntry."WIP Entry Amount";
            JobBuffer[2] := JobBuffer[1];
            IF JobBuffer[2].FIND THEN BEGIN
              JobBuffer[2]."Amount 1" :=
                JobBuffer[2]."Amount 1" + JobBuffer[1]."Amount 1";
              JobBuffer[2]."Amount 2" :=
                JobBuffer[2]."Amount 2" + JobBuffer[1]."Amount 2";
              JobBuffer[2]."Amount 4" :=
                JobBuffer[2]."Amount 4" + JobBuffer[1]."Amount 4";
              JobBuffer[2]."Amount 5" :=
                JobBuffer[2]."Amount 5" + JobBuffer[1]."Amount 5";
              JobBuffer[2].MODIFY;
            END ELSE
              JobBuffer[1].INSERT;
          END;
        UNTIL JobWIPGLEntry.NEXT = 0;
    END;

    PROCEDURE InitJobBuffer@15();
    BEGIN
      CLEAR(JobBuffer);
      JobBuffer[1].DELETEALL;
    END;

    PROCEDURE GetJobBuffer@1(VAR Job@1000 : Record 167;VAR JobBuffer2@1001 : Record 1017);
    VAR
      GLEntry@1002 : Record 17;
      OldAcc@1003 : Code[20];
    BEGIN
      JobBuffer2.DELETEALL;
      GLEntry.SETCURRENTKEY("G/L Account No.","Job No.","Posting Date");
      GLEntry.SETFILTER("Posting Date",Job.GETFILTER("Posting Date Filter"));
      OldAcc := '';

      IF JobBuffer[1].FIND('+') THEN
        REPEAT
          IF JobBuffer[1]."Account No. 1" <> OldAcc THEN BEGIN
            GLEntry.SETRANGE("G/L Account No.",JobBuffer[1]."Account No. 1");
            GLEntry.SETFILTER("Job No.",Job.GETFILTER("No."));
            GLEntry.CALCSUMS(Amount);
            JobBuffer[1]."Amount 3" := GLEntry.Amount;
            IF JobBuffer[1]."Amount 3" <> 0 THEN
              JobBuffer[1]."New Total" := TRUE;
            OldAcc := JobBuffer[1]."Account No. 1";
          END;
          JobBuffer2 := JobBuffer[1];
          JobBuffer2.INSERT;
        UNTIL JobBuffer[1].NEXT(-1) = 0;
      JobBuffer[1].DELETEALL;
    END;

    PROCEDURE ReportJobItem@2(VAR Job@1000 : Record 167;VAR item2@1001 : Record 27;VAR JobBuffer2@1002 : Record 1017);
    VAR
      Item@1004 : Record 27;
      Item3@1007 : Record 27;
      JobLedgEntry@1003 : Record 169;
      InFilter@1005 : Boolean;
      Itemfilter@1006 : Boolean;
    BEGIN
      CLEAR(JobBuffer2);
      CLEAR(JobBuffer);
      JobBuffer2.DELETEALL;
      JobBuffer[1].DELETEALL;
      IF Job."No." = '' THEN
        EXIT;
      Item.COPY(item2);
      Itemfilter := Item.GETFILTERS <> '';
      Item.SETCURRENTKEY("No.");

      JobLedgEntry.SETCURRENTKEY("Job No.","Posting Date");
      JobLedgEntry.SETRANGE("Job No.",Job."No.");
      JobLedgEntry.SETFILTER("Posting Date",Job.GETFILTER("Posting Date Filter"));
      IF JobLedgEntry.FIND('-') THEN
        REPEAT
          IF (JobLedgEntry."Entry Type" = JobLedgEntry."Entry Type"::Usage) AND
             (JobLedgEntry.Type = JobLedgEntry.Type::Item) AND
             (JobLedgEntry."No." <> '')
          THEN BEGIN
            InFilter := TRUE;
            IF Itemfilter THEN BEGIN
              Item.INIT;
              Item."No." := JobLedgEntry."No.";
              InFilter := Item.FIND;
            END;
            IF InFilter THEN BEGIN
              Item3.INIT;
              IF Item3.GET(JobLedgEntry."No.") THEN;
              CLEAR(JobBuffer[1]);
              JobBuffer[1]."Account No. 1" := JobLedgEntry."No.";
              JobBuffer[1]."Account No. 2" := JobLedgEntry."Unit of Measure Code";
              JobBuffer[1].Description := Item3.Description;
              JobBuffer[1]."Amount 1" := JobLedgEntry.Quantity;
              JobBuffer[1]."Amount 2" := JobLedgEntry."Total Cost (LCY)";
              JobBuffer[1]."Amount 3" := JobLedgEntry."Line Amount (LCY)";
              JobBuffer[2] := JobBuffer[1];
              IF JobBuffer[2].FIND THEN BEGIN
                JobBuffer[2]."Amount 1" :=
                  JobBuffer[2]."Amount 1" + JobBuffer[1]."Amount 1";
                JobBuffer[2]."Amount 2" :=
                  JobBuffer[2]."Amount 2" + JobBuffer[1]."Amount 2";
                JobBuffer[2]."Amount 3" :=
                  JobBuffer[2]."Amount 3" + JobBuffer[1]."Amount 3";
                JobBuffer[2].MODIFY;
              END ELSE
                JobBuffer[1].INSERT;
            END;
          END;
        UNTIL JobLedgEntry.NEXT = 0;

      IF JobBuffer[1].FIND('-') THEN
        REPEAT
          JobBuffer2 := JobBuffer[1];
          JobBuffer2.INSERT;
        UNTIL JobBuffer[1].NEXT = 0;
      JobBuffer[1].DELETEALL;
    END;

    PROCEDURE ReportItemJob@3(VAR Item@1001 : Record 27;VAR Job2@1000 : Record 167;VAR JobBuffer2@1002 : Record 1017);
    VAR
      JobLedgEntry@1003 : Record 169;
      Job@1004 : Record 167;
      Job3@1007 : Record 167;
      InFilter@1005 : Boolean;
      JobFilter@1006 : Boolean;
    BEGIN
      CLEAR(JobBuffer2);
      CLEAR(JobBuffer);
      JobBuffer2.DELETEALL;
      JobBuffer[1].DELETEALL;
      IF Item."No." = '' THEN
        EXIT;
      Job.COPY(Job2);
      JobFilter := Job.GETFILTERS <> '';
      Job.SETCURRENTKEY("No.");

      JobLedgEntry.SETCURRENTKEY("Entry Type",Type,"No.","Posting Date");
      JobLedgEntry.SETRANGE("Entry Type",JobLedgEntry."Entry Type"::Usage);
      JobLedgEntry.SETRANGE(Type,JobLedgEntry.Type::Item);
      JobLedgEntry.SETRANGE("No.",Item."No.");
      JobLedgEntry.SETFILTER("Posting Date",Job.GETFILTER("Posting Date Filter"));
      IF JobLedgEntry.FIND('-') THEN
        REPEAT
          InFilter := TRUE;
          IF JobFilter THEN BEGIN
            Job.INIT;
            Job."No." := JobLedgEntry."Job No.";
            InFilter := Job.FIND;
          END;
          IF InFilter THEN BEGIN
            Job3.INIT;
            IF Job3.GET(JobLedgEntry."Job No.") THEN;
            CLEAR(JobBuffer[1]);
            JobBuffer[1]."Account No. 1" := JobLedgEntry."Job No.";
            JobBuffer[1]."Account No. 2" := JobLedgEntry."Unit of Measure Code";
            JobBuffer[1].Description := Job3.Description;
            JobBuffer[1]."Amount 1" := JobLedgEntry.Quantity;
            JobBuffer[1]."Amount 2" := JobLedgEntry."Total Cost (LCY)";
            JobBuffer[1]."Amount 3" := JobLedgEntry."Line Amount (LCY)";
            JobBuffer[2] := JobBuffer[1];
            IF JobBuffer[2].FIND THEN BEGIN
              JobBuffer[2]."Amount 1" :=
                JobBuffer[2]."Amount 1" + JobBuffer[1]."Amount 1";
              JobBuffer[2]."Amount 2" :=
                JobBuffer[2]."Amount 2" + JobBuffer[1]."Amount 2";
              JobBuffer[2]."Amount 3" :=
                JobBuffer[2]."Amount 3" + JobBuffer[1]."Amount 3";
              JobBuffer[2].MODIFY;
            END ELSE
              JobBuffer[1].INSERT;
          END;
        UNTIL JobLedgEntry.NEXT = 0;

      IF JobBuffer[1].FIND('-') THEN
        REPEAT
          JobBuffer2 := JobBuffer[1];
          JobBuffer2.INSERT;
        UNTIL JobBuffer[1].NEXT = 0;
      JobBuffer[1].DELETEALL;
    END;

    BEGIN
    END.
  }
}
