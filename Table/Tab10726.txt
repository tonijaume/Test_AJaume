OBJECT Table 10726 G/L Account Buffer
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVES6.00.01;
  }
  PROPERTIES
  {
    DataCaptionFields=No.,Name;
    CaptionML=[ENU=G/L Account Buffer;
               ESP=B£fer de cuenta];
    LookupFormID=Form18;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;CaptionML=[ENU=No.;
                                                              ESP=N§];
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Text50        ;CaptionML=[ENU=Name;
                                                              ESP=Nombre] }
    { 3   ;   ;Include G/L Acc. in 347;Boolean    ;CaptionML=[ENU=Include G/L Acc. in 347;
                                                              ESP=Incluir cuenta en 347] }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      SelectedGLAcc@1100000 : Record 10727;

    PROCEDURE SetGLAccSelectionMultiple@1100000(VAR SelectedGLAccText@1100000 : Text[250];VAR FilterString@1100001 : Text[250]);
    VAR
      GLAccount@1100002 : Record 15;
      TempGLAccBuffer@1100003 : TEMPORARY Record 10726;
      GLAccSelectionMultiple@1100004 : Form 10734;
    BEGIN
      CLEAR(GLAccSelectionMultiple);
      IF GLAccount.FIND('-') THEN
        REPEAT
          GLAccSelectionMultiple.InsertGLAccSelBuf(
            SelectedGLAcc.GET(GLAccount."No."),
            GLAccount."No.",GLAccount.Name);
        UNTIL GLAccount.NEXT = 0;

      GLAccSelectionMultiple.LOOKUPMODE := TRUE;
      IF GLAccSelectionMultiple.RUNMODAL = ACTION::LookupOK THEN BEGIN
        GLAccSelectionMultiple.GetGLAccSelBuf(TempGLAccBuffer);
        SetGLAccSelection(SelectedGLAccText,TempGLAccBuffer,FilterString);
      END;
    END;

    PROCEDURE SetGLAccSelection@1100001(VAR SelectedGLAccText@1100000 : Text[250];VAR GLAccSelectionBuf@1100001 : Record 10726;VAR FilterString@1100002 : Text[250]);
    BEGIN
      SelectedGLAcc.DELETEALL;
      SelectedGLAccText := '';
      GLAccSelectionBuf.SETRANGE("Include G/L Acc. in 347",TRUE);
      IF GLAccSelectionBuf.FIND('-') THEN
        REPEAT
          SelectedGLAcc."No." := GLAccSelectionBuf."No.";
          SelectedGLAcc.Name := GLAccSelectionBuf.Name;
          SelectedGLAcc.INSERT;
          AddGLAccNoToText(SelectedGLAcc."No.",SelectedGLAccText,FilterString);
        UNTIL GLAccSelectionBuf.NEXT = 0;
    END;

    PROCEDURE AddGLAccNoToText@1100002(GLAccNo@1100000 : Code[20];VAR Text@1100001 : Text[250];VAR FilterString@1100002 : Text[250]);
    BEGIN
      IF Text = '' THEN BEGIN
        Text := GLAccNo;
        FilterString := STRSUBSTNO('%1',Text);
      END ELSE
        IF (STRLEN(Text) + STRLEN(GLAccNo)) <= (MAXSTRLEN(Text) - 4) THEN BEGIN
          Text := STRSUBSTNO('%1;%2',Text,GLAccNo);
          FilterString := STRSUBSTNO('%1|%2',FilterString,GLAccNo);
        END ELSE
          Text := STRSUBSTNO('%1;...',Text)
    END;

    PROCEDURE GetGLAccSelectionText@1100003() : Text[250];
    VAR
      SelectedGLAcc@1100000 : Record 10727;
      SelectedGLAccText@1100001 : Text[250];
    BEGIN
      WITH SelectedGLAcc DO BEGIN
        IF FIND('-') THEN
          REPEAT
            AddGLAccNoToText("No.",SelectedGLAccText,SelectedGLAccText);
          UNTIL NEXT = 0;
      END;
      EXIT(SelectedGLAccText);
    END;

    BEGIN
    END.
  }
}
