OBJECT Table 7035352 Historico Produccion C/I
{
  OBJECT-PROPERTIES
  {
    Date=18/06/15;
    Time=18:32:36;
    Modified=Yes;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    OnInsert=VAR
               lrHist@1103355000 : Record 7035352;
             BEGIN
               IF lrHist.FINDLAST THEN
                 "Id Linea" := lrHist."Id Linea" + 10
               ELSE
                 "Id Linea" := 10;

               "Usuario modificacion" := USERID;
               "Fecha modificacion"   := CURRENTDATETIME;
             END;

    CaptionML=ESP=Historico Produccion C/I;
  }
  FIELDS
  {
    { 1   ;   ;Id Linea            ;Integer       ;CaptionML=ESP=Id Linea }
    { 2   ;   ;N§ parte            ;Code10        ;CaptionML=ESP=N§ parte }
    { 3   ;   ;N§ Linea            ;Integer       ;CaptionML=ESP=N§ Linea }
    { 4   ;   ;Concepto            ;Option        ;CaptionML=ESP=Concepto;
                                                   OptionCaptionML=ESP=Produccion ingreso,Produccion coste,Ingreso;
                                                   OptionString=Produccion ingreso,Produccion coste,Ingreso }
    { 5   ;   ;Nombre campo        ;Text30        ;CaptionML=ESP=Nombre campo }
    { 6   ;   ;Valor anterior      ;Text60        ;CaptionML=ESP=Valor anterior }
    { 7   ;   ;Valor nuevo         ;Text60        ;CaptionML=ESP=Valor nuevo }
    { 8   ;   ;Usuario modificacion;Code20        ;CaptionML=ESP=Usuario modificacion }
    { 9   ;   ;Fecha modificacion  ;DateTime      ;CaptionML=ESP=Fecha modificacion }
  }
  KEYS
  {
    {    ;Id Linea                                ;Clustered=Yes }
    {    ;N§ parte,Concepto,N§ Linea               }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE InsertarHistorico@1103355000(pwParte@1103355000 : Code[10];pwLinea@1103355001 : Integer;pwIngreso@1103355006 : Boolean;pwCampo@1103355002 : Text[30];pwAnterior@1103355003 : Text[60];pwNuevo@1103355004 : Text[60]);
    VAR
      lrHist@1103355005 : Record 7035352;
    BEGIN
      // InsertarHistorico

      lrHist.RESET;

      lrHist."N§ parte"        := pwParte;
      lrHist."N§ Linea"        := pwLinea;

      IF pwIngreso THEN
        lrHist.Concepto        := Concepto::"Produccion ingreso"
      ELSE
        lrHist.Concepto        := Concepto::"Produccion coste";

      lrHist."Nombre campo"    := pwCampo;
      lrHist."Valor anterior"  := pwAnterior;
      lrHist."Valor nuevo"     := pwNuevo;

      lrHist.INSERT(TRUE);
    END;

    PROCEDURE MoverHistProdaCI@1103355001(VAR prCI@1103355000 : Record 7010414;VAR prProdIng@1103355001 : Record 7035392);
    VAR
      lrHist@1103355002 : Record 7035352;
    BEGIN
      // MoverHistProdaCI

      lrHist.RESET;

      lrHist.SETCURRENTKEY("N§ parte", Concepto, "N§ Linea");
      lrHist.SETRANGE("N§ parte", prProdIng."N§ Parte");
      lrHist.SETRANGE(Concepto  , Concepto::"Produccion ingreso");
      lrHist.SETRANGE("N§ Linea", prProdIng."N§ Linea");

      IF lrHist.FINDSET(TRUE) THEN BEGIN
        lrHist.MODIFYALL("N§ Linea", prCI."N§ Linea");
      END;

      // Recapturamos con el nuevo n£mero de l¡nea y cambiamos el concepto.

      lrHist.SETRANGE("N§ Linea", prCI."N§ Linea");
      IF lrHist.FINDSET(TRUE) THEN BEGIN
        lrHist.MODIFYALL(Concepto, Concepto::Ingreso);
      END;
    END;

    PROCEDURE MoverHistCIaProd@1103355003(VAR prCI@1103355001 : Record 7010414;VAR prProdIng@1103355000 : Record 7035392);
    VAR
      lrHist@1103355002 : Record 7035352;
    BEGIN
      // MoverHistCIaProd

      lrHist.RESET;

      lrHist.SETCURRENTKEY("N§ parte", Concepto, "N§ Linea");
      lrHist.SETRANGE("N§ parte", prCI."N§ Parte");
      lrHist.SETRANGE(Concepto  , Concepto::Ingreso);
      lrHist.SETRANGE("N§ Linea", prCI."N§ Linea");

      IF lrHist.FINDSET(TRUE) THEN BEGIN
        lrHist.MODIFYALL("N§ Linea", prProdIng."N§ Linea");
      END;

      // Recapturamos con el nuevo n£mero de l¡nea y cambiamos el concepto.

      lrHist.SETRANGE("N§ Linea", prProdIng."N§ Linea");
      IF lrHist.FINDSET(TRUE) THEN BEGIN
        lrHist.MODIFYALL(Concepto, Concepto::"Produccion ingreso");
      END;
    END;

    BEGIN
    {
      $001 ARM 18062015 Ampliado usuario modificaci¢n a 20
    }
    END.
  }
}
