OBJECT Table 7009831 Grupo configuracion NCF
{
  OBJECT-PROPERTIES
  {
    Date=02/10/12;
    Time=12:00:00;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    CaptionML=[ENU=NCF configuration group;
               ESP=Grupo configuracion NCF];
    LookupFormID=Form7009976;
  }
  FIELDS
  {
    { 1   ;   ;Tipo grupo          ;Option        ;CaptionML=[ENU=Group type;
                                                              ESP=Tipo grupo];
                                                   OptionCaptionML=[ENU=Sales,Purchases;
                                                                    ESP=Ventas,Compras];
                                                   OptionString=Ventas,Compras;
                                                   Description=[PK; Ventas,Compras] }
    { 3   ;   ;Codigo              ;Code10        ;CaptionML=[ENU=Code;
                                                              ESP=Codigo];
                                                   NotBlank=Yes;
                                                   Description=PK }
    { 5   ;   ;Descripcion Factura ;Text30        ;CaptionML=[ENU=Invoices Description;
                                                              ESP=Descripcion Factura] }
    { 6   ;   ;Descripcion Abonos  ;Text30        ;CaptionML=[ENU=Cr. Memos Description;
                                                              ESP=Descripcion Abonos];
                                                   Description=$001 }
    { 7   ;   ;Serie NCF Facturas  ;Code10        ;TableRelation="No. Series".Code;
                                                   CaptionML=[ENU=Invoices NCF Serial;
                                                              ESP=Serie NCF Facturas];
                                                   Description="No. Series".Code }
    { 9   ;   ;Serie NCF Abonos    ;Code10        ;TableRelation="No. Series".Code;
                                                   CaptionML=[ENU=Credit memos NCF Serial;
                                                              ESP=Serie NCF Abonos];
                                                   Description="No. Series".Code }
  }
  KEYS
  {
    {    ;Tipo grupo,Codigo                       ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      GLSetup@1103355000 : Record 98;
      Text001@1103355001 : TextConst 'ENU=The length of the proof is not correct, it must be of %1 characters;ESP=La longitud del comprobante es incorrecta, debe ser de %1 caracteres.';
      Vend@1103355002 : Record 23;
      Text002@1103355003 : TextConst 'ENU=Fix part of the proof does not coincide with the used one by the supplier;ESP=La parte fija del comprobante no coincide con la utilizada por el proveedor.';
      VendEntry@1103355004 : Record 25;
      Text003@1103355005 : TextConst 'ENU=This supplier already has a document of date %1 with proof number %2;ESP=Este proveedor ya tiene un documento de fecha %1 con numero comprobante %2';
      Text004@1103355006 : TextConst 'ENU=Proofs must be entered taking into account the sequence of registration dates;ESP=Los comprobantes deben introducirse respetando la secuencia de fechas de registro.';

    PROCEDURE CheckComprobanteFiscalCompra@1103355000(prCabCompra@1103355000 : Record 38);
    BEGIN
      // CheckComprobanteFiscalCompra
      //

      // Comprobar que no este vacio

      prCabCompra.TESTFIELD("Comprobante Fiscal");

      // Comprobar que la longitud sea correcta

      GLSetup.FIND('-');
      IF STRLEN(prCabCompra."Comprobante Fiscal") <> GLSetup."Digitos NCF" THEN
        ERROR(Text001, GLSetup."Digitos NCF");

      // Si el proveedor tiene digitos fijos deben coincidir con el comprobante de la factura

      Vend.GET(prCabCompra."Pay-to Vendor No.");
      IF Vend."Digitos fijos NCF" <> '' THEN BEGIN
        IF Vend."Digitos fijos NCF" <> COPYSTR(prCabCompra."Comprobante Fiscal", 1, 9) THEN
          ERROR(Text002);

        // Control de fechas

        VendEntry.RESET;
        VendEntry.SETCURRENTKEY("Document Type",
                                "Vendor No.",
                                "Posting Date",
                                "Currency Code");
        VendEntry.SETRANGE ("Document Type", prCabCompra."Document Type");
        VendEntry.SETRANGE ("Vendor No."   , prCabCompra."Pay-to Vendor No.");
        VendEntry.SETFILTER("Posting Date" , '<%1', prCabCompra."Posting Date");
        IF VendEntry.FINDLAST THEN
          IF VendEntry."Comprobante Fiscal" > prCabCompra."Comprobante Fiscal" THEN
            ERROR(Text003 + Text004);
      END;
    END;

    BEGIN
    {
      $001 JPT 21/03/12 GOC-LP-12016 Incluir una descripcion de NCF para los abonos
    }
    END.
  }
}
