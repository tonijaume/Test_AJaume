OBJECT Table 7010322 Precio generico maletas
{
  OBJECT-PROPERTIES
  {
    Date=16/10/09;
    Time=12:30:40;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    CaptionML=ESP=Precio generico maletas;
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;Integer       ;CaptionML=ESP=ID;
                                                   Description=PK;
                                                   Editable=No }
    { 3   ;   ;Fecha desde         ;Date          ;OnValidate=BEGIN
                                                                IF "Fecha desde" <> xRec."Fecha desde" THEN BEGIN
                                                                  IF "Fecha hasta" <> 0D THEN BEGIN
                                                                    IF "Fecha desde" > "Fecha hasta" THEN
                                                                      ERROR(err1);
                                                                  END;
                                                                  Activo := comprobar_solape;
                                                                END;
                                                              END;

                                                   CaptionML=ESP=Fecha desde }
    { 5   ;   ;Fecha hasta         ;Date          ;OnValidate=BEGIN
                                                                IF "Fecha hasta" <> xRec."Fecha hasta" THEN BEGIN
                                                                  IF "Fecha desde" <> 0D THEN BEGIN
                                                                    IF "Fecha desde" > "Fecha hasta" THEN
                                                                      ERROR(err1);
                                                                  END;
                                                                  Activo := comprobar_solape;
                                                                END;
                                                              END;

                                                   CaptionML=ESP=Fecha hasta }
    { 7   ;   ;Precio              ;Decimal       ;OnValidate=BEGIN
                                                                IF Precio <> xRec.Precio THEN
                                                                  VALIDATE("Cod. Divisa");
                                                              END;

                                                   CaptionML=ESP=Precio;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Cod. Divisa" }
    { 9   ;   ;Cod. Divisa         ;Code10        ;TableRelation=Currency;
                                                   OnValidate=BEGIN
                                                                IF "Cod. Divisa" <> '' THEN
                                                                  "Precio DL" := rTipo.ExchangeAmtFCYToLCY(TODAY, "Cod. Divisa", Precio,
                                                                                                        rTipo.ExchangeRate(TODAY, "Cod. Divisa"))
                                                                ELSE
                                                                  "Precio DL" := Precio;
                                                              END;

                                                   CaptionML=ESP=Cod. Divisa;
                                                   Description=FK Divisa }
    { 11  ;   ;Precio DL           ;Decimal       ;CaptionML=ESP=Precio DL;
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 13  ;   ;Activo              ;Boolean       ;CaptionML=ESP=Activo;
                                                   Editable=No }
    { 15  ;   ;Impuestos incluidos ;Boolean       ;CaptionML=ESP=Impuestos incluidos }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      rTipo@1100244000 : Record 330;
      err1@1100244001 : TextConst 'ESP=La fecha desde no puede ser posterior a la fecha hasta.';
      text1@1100244002 : TextConst 'ESP=La fecha desde de este precio se solapa con otro ya introducido.';
      text2@1100244003 : TextConst 'ESP=La fecha hasta de este precio se solapa con otro ya introducido.';
      text3@1100244004 : TextConst 'ESP=El precio solapa totalmente otro precio ya introducido.';

    PROCEDURE comprobar_solape@2() : Boolean;
    VAR
      rPre@1100244000 : Record 7010322;
      w_error@1100244001 : Boolean;
    BEGIN
      // comprobar_solape
      //
      w_error := FALSE;
      rPre.RESET; rPre.INIT;
      IF ("Fecha desde" = 0D) OR ("Fecha hasta" = 0D) THEN
        EXIT(FALSE);
      rPre.SETFILTER(ID           , '<>%1', ID);
      rPre.SETRANGE (Activo       , TRUE);

      rPre.SETFILTER("Fecha desde", '<=%1', "Fecha desde");
      rPre.SETFILTER("Fecha hasta", '>=%1', "Fecha desde");
      IF rPre.FINDFIRST THEN BEGIN
        MESSAGE(text1);
        w_error := TRUE;
      END;
      rPre.SETFILTER("Fecha desde", '<=%1', "Fecha hasta");
      rPre.SETFILTER("Fecha hasta", '>=%1', "Fecha hasta");
      IF rPre.FINDFIRST THEN BEGIN
        MESSAGE(text2);
        w_error := TRUE;
      END;
      rPre.SETFILTER("Fecha desde", '>=%1', "Fecha desde");
      rPre.SETFILTER("Fecha hasta", '<=%1', "Fecha hasta");
      IF rPre.FINDFIRST THEN BEGIN
        MESSAGE(text3);
        w_error := TRUE;
      END;

      EXIT(NOT w_error);
    END;

    BEGIN
    END.
  }
}
