OBJECT Table 81002 Cambios en Campos
{
  OBJECT-PROPERTIES
  {
    Date=07/12/07;
    Time=[ 9:00:00];
    Modified=Yes;
    Version List=Ctrl2009;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               rCambio.RESET;
               rCambio.SETRANGE(TableNo, TableNo);
               rCambio.SETRANGE(FieldNo, FieldNo);
               rCambio.ASCENDING(FALSE);
               IF rCambio.FIND('-') THEN
                 "Id Cambio" := rCambio."Id Cambio" + 1
               ELSE
                 "Id Cambio" := 1;
             END;

  }
  FIELDS
  {
    { 1   ;   ;TableNo             ;Integer        }
    { 3   ;   ;TableName           ;Text30         }
    { 5   ;   ;FieldNo             ;Integer        }
    { 7   ;   ;FieldName           ;Text30         }
    { 9   ;   ;Tipo cambio         ;Option        ;OptionString=Nuevo campo,Campo eliminado,Cambio }
    { 11  ;   ;Dato cambiado       ;Text30         }
    { 13  ;   ;Valor antiguo       ;Text30         }
    { 15  ;   ;Valor nuevo         ;Text30         }
    { 17  ;   ;Id Cambio           ;Integer        }
  }
  KEYS
  {
    {    ;TableNo,FieldNo,Id Cambio               ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      rCambio@1100253000 : Record 81002;
      rExcel@1100253001 : Record 7035385;
      Text001@1100253002 : TextConst 'ESP=Campos nuevos';
      rPar@1100253015 : Record 7010311;
      wFila@1100253003 : Integer;
      Text002@1100253004 : TextConst 'ESP=Tabla';
      Text003@1100253005 : TextConst 'ESP=Nombre tabla';
      Text004@1100253006 : TextConst 'ESP=Campo';
      Text005@1100253007 : TextConst 'ESP=Nombre campo';
      Text006@1100253008 : TextConst 'ESP=Campos eliminados';
      Text007@1100253009 : TextConst 'ESP=Cambios en campos';
      Text008@1100253010 : TextConst 'ESP=Dato cambiado';
      Text009@1100253011 : TextConst 'ESP=Valor antiguo';
      Text010@1100253012 : TextConst 'ESP=Valor nuevo';
      Text011@1100253013 : TextConst 'ESP=Control de cambios';
      Text012@1100253014 : TextConst 'ESP=Campos cambiados actualizacion %1';
      Text013@1100253016 : TextConst 'ESP=Exportando cambios en campos';

    PROCEDURE InsertaRegistro@1100253000(prCambio@1100253000 : Record 81002;pwDatoCambiado@1100253001 : Text[30];pwValorAntiguo@1100253002 : Text[30];pwValorNuevo@1100253003 : Text[30]);
    BEGIN
      // InsertaRegistro
      //
      rCambio := prCambio;
      rCambio."Dato cambiado" := pwDatoCambiado;
      rCambio."Valor antiguo" := pwValorAntiguo;
      rCambio."Valor nuevo"   := pwValorNuevo;
      rCambio.INSERT(TRUE);
    END;

    PROCEDURE ExportarExcel@1100253001();
    BEGIN
      // ExportarExcel
      //

      // Borramos el temporal de excel y creamos el libro de EXCEL

      CLEAR(rExcel);
      rExcel.DELETEALL;

      // 1.- Exportamos a Excel los campos nuevos

      rCambio.RESET;
      rCambio.SETRANGE("Tipo cambio", rCambio."Tipo cambio"::"Nuevo campo");
      IF rCambio.FIND('-') THEN BEGIN
        wFila := 1;
        InsertarCelda(1, Text001, TRUE, 14, wFila, 3, FALSE);

        wFila += 1;
        InsertarCabecera(FALSE);
        REPEAT
          wFila += 1;

          InsertarCelda(1, FORMAT(rCambio.TableNo), FALSE, 11, 0, 0, FALSE);
          InsertarCelda(2, FORMAT(rCambio.TableName), FALSE, 11, 0, 0, FALSE);
          InsertarCelda(3, FORMAT(rCambio.FieldNo), FALSE, 11, 0, 0, FALSE);
          InsertarCelda(4, FORMAT(rCambio.FieldName), FALSE, 11, 0, 0, FALSE);
        UNTIL rCambio.NEXT = 0;
      END;

      // 2.- Exportamos a Excel los campos eliminados

      rCambio.RESET;
      rCambio.SETRANGE("Tipo cambio", rCambio."Tipo cambio"::"Campo eliminado");
      IF rCambio.FIND('-') THEN BEGIN
        wFila += 2;
        InsertarCelda(1, Text006, TRUE, 14, wFila, 3, FALSE);

        wFila += 1;
        InsertarCabecera(FALSE);
        REPEAT
          wFila += 1;
          InsertarCelda(1, FORMAT(rCambio.TableNo), FALSE, 11, 0, 0, FALSE);
          InsertarCelda(2, FORMAT(rCambio.TableName), FALSE, 11, 0, 0, FALSE);
          InsertarCelda(3, FORMAT(rCambio.FieldNo), FALSE, 11, 0, 0, FALSE);
          InsertarCelda(4, FORMAT(rCambio.FieldName), FALSE, 11, 0, 0, FALSE);
        UNTIL rCambio.NEXT = 0;
      END;

      // 3.- Exportamos a Excel los campos que han cambiado

      rCambio.RESET;
      rCambio.SETRANGE("Tipo cambio", rCambio."Tipo cambio"::Cambio);
      IF rCambio.FIND('-') THEN BEGIN
        wFila += 2;
        InsertarCelda(1, Text007, TRUE, 14, wFila, 3, FALSE);

        wFila += 1;
        InsertarCabecera(TRUE);
        REPEAT
          wFila += 1;
          InsertarCelda(1, FORMAT(rCambio.TableNo), FALSE, 11, 0, 0, FALSE);
          InsertarCelda(2, FORMAT(rCambio.TableName), FALSE, 11, 0, 0, FALSE);
          InsertarCelda(3, FORMAT(rCambio.FieldNo), FALSE, 11, 0, 0, FALSE);
          InsertarCelda(4, FORMAT(rCambio.FieldName), FALSE, 11, 0, 0, FALSE);
          InsertarCelda(5, FORMAT(rCambio."Dato cambiado"), FALSE, 11, 0, 0, FALSE);
          InsertarCelda(6, FORMAT(rCambio."Valor antiguo"), FALSE, 11, 0, 0, FALSE);
          InsertarCelda(7, FORMAT(rCambio."Valor nuevo"), FALSE, 11, 0, 0, FALSE);
        UNTIL rCambio.NEXT = 0;
      END;

      // Creamos la hoja si hemos insertado al menos una fila

      IF wFila > 1 THEN BEGIN
        rPar.FIND('-');

        rExcel.CreateBook;
        rExcel.CreateSheet(Text011, STRSUBSTNO(Text012, rPar."Empresa Real"), COMPANYNAME, USERID, TRUE, FALSE, Text013);
        rExcel.GiveUserControl;
      END;
    END;

    PROCEDURE InsertarCabecera@1100253005(pwCabeceraLarga@1100253000 : Boolean);
    BEGIN
      // InsertarCabecera
      //
      InsertarCelda(1, Text002, TRUE, 11, 0, 0, TRUE);
      InsertarCelda(2, Text003, TRUE, 11, 0, 0, TRUE);
      InsertarCelda(3, Text004, TRUE, 11, 0, 0, TRUE);
      InsertarCelda(4, Text005, TRUE, 11, 0, 0, TRUE);

      IF pwCabeceraLarga THEN BEGIN
        InsertarCelda(5, Text008, TRUE, 11, 0, 0, TRUE);
        InsertarCelda(6, Text009, TRUE, 11, 0, 0, TRUE);
        InsertarCelda(7, Text010, TRUE, 11, 0, 0, TRUE);
      END;
    END;

    PROCEDURE InsertarCelda@1100253007(pwColumna@1100253001 : Integer;pwTexto@1100253002 : Text[60];pwFontBold@1100253000 : Boolean;pwTama¤o@1100253003 : Integer;pwFilaFusion@1100253004 : Integer;pwColumnaFusion@1100253005 : Integer;pwBordeBajo@1100253006 : Boolean);
    BEGIN
      // InsertarCelda
      //

      CLEAR(rExcel);
      rExcel.VALIDATE("Row No."   , wFila);
      rExcel.VALIDATE("Column No.", pwColumna);

      rExcel."Cell Value as Text"   := pwTexto;
      rExcel.Bold                   := pwFontBold;
      rExcel."Color Fuente"         := 11;
      rExcel."Tama¤o Fuente"        := pwTama¤o;
      rExcel."Tipo fuente"          := 'Verdana';

      IF pwBordeBajo THEN BEGIN
        rExcel."Borde Abaj" := 1;
        rExcel."Color Borde Abaj" := 11;
      END;

      IF pwFilaFusion <> 0 THEN
        rExcel."Fusionar hasta celda" := rExcel.DevuelveCelda(pwFilaFusion, pwColumnaFusion);

      rExcel.INSERT;
    END;

    BEGIN
    END.
  }
}
