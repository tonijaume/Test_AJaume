OBJECT Table 7010249 Equipo guias x hotel
{
  OBJECT-PROPERTIES
  {
    Date=04/11/13;
    Time=13:44:21;
    Modified=Yes;
    Version List=AIC2009,CNT;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               _CheckData;
             END;

    OnModify=BEGIN
               _CheckData;
             END;

    CaptionML=[ENU=Guides team per hotel;
               ESP=Equipo guias x hotel];
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[ENU=ID;
                                                              ESP=ID];
                                                   Description=PK;
                                                   Editable=No }
    { 3   ;   ;Hotel               ;Code20        ;TableRelation=Hotel.Hotel WHERE (NoName=CONST(No));
                                                   OnValidate=BEGIN
                                                                //+$001 <
                                                                IF "Equipo definido por" <> "Equipo definido por"::Hotel THEN
                                                                  ERROR(Text006);
                                                                //+$001 >
                                                              END;

                                                   CaptionML=[ENU=Hotel;
                                                              ESP=Hotel];
                                                   NotBlank=Yes;
                                                   Description=Hotel.Hotel }
    { 5   ;   ;Fecha desde         ;Date          ;CaptionML=[ENU=From date;
                                                              ESP=Fecha desde];
                                                   NotBlank=Yes }
    { 7   ;   ;Fecha hasta         ;Date          ;CaptionML=[ENU=To date;
                                                              ESP=Fecha hasta];
                                                   NotBlank=Yes }
    { 9   ;   ;Guia venta          ;Code20        ;TableRelation="Representante excursiones".Codigo;
                                                   OnValidate=BEGIN
                                                                IF "Guia venta" = "Guia principal" THEN
                                                                  ERROR(Text005);

                                                                _CheckTickets; //+$001
                                                              END;

                                                   CaptionML=[ENU=Sales guide;
                                                              ESP=Guia venta];
                                                   NotBlank=Yes;
                                                   Description="Representante excursiones".Codigo }
    { 11  ;   ;Guia principal      ;Code20        ;TableRelation="Representante excursiones".Codigo;
                                                   CaptionML=[ENU=Main Guide;
                                                              ESP=Guia principal];
                                                   Description="Representante excursiones".Codigo }
    { 13  ;   ;Equipo definido por ;Option        ;CaptionML=[ENU=Team defined by;
                                                              ESP=Equipo definido por];
                                                   OptionCaptionML=[ENU=Hotel,Ticket;
                                                                    ESP=Hotel,Ticket];
                                                   OptionString=Hotel,Ticket;
                                                   Description=$001 }
    { 15  ;   ;Num. Ticket inicial ;Code10        ;OnValidate=BEGIN
                                                                IF "Equipo definido por" <> "Equipo definido por"::Ticket THEN
                                                                  ERROR(Text007);

                                                                _CompletarTicket("Num. Ticket inicial");

                                                                _CheckTickets;
                                                              END;

                                                   CaptionML=[ENU=Initial ticket number;
                                                              ESP=Num. Ticket inicial];
                                                   Description=$001 }
    { 17  ;   ;Num. Ticket final   ;Code10        ;OnValidate=BEGIN
                                                                IF "Equipo definido por" <> "Equipo definido por"::Ticket THEN
                                                                  ERROR(Text007);

                                                                _CompletarTicket("Num. Ticket final");

                                                                _CheckTickets;
                                                              END;

                                                   CaptionML=[ENU=Final ticket number;
                                                              ESP=Num. Ticket final];
                                                   Description=$001 }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100253000 : TextConst 'ENU=The start date of the period can not be later than the end date.;ESP=La fecha de inicio del periodo no puede ser posterior a la fecha de final.';
      Text002@1100253001 : TextConst 'ENU=There is no purchase contract in the hotel for day% 1.;ESP=No existe contrato de compra en el hotel para dia %1.';
      Text003@1100253002 : TextConst 'ENU=This guide has already been assigned to this hotel in this period.;ESP=Este guia ya ha sido asignado a este hotel en este periodo.';
      Text004@1100253003 : TextConst 'ENU=This guide has already been assigned to another hotel in this period.;ESP=Este guia ya ha sido asignado a otro hotel en este periodo.';
      Text005@1100253004 : TextConst 'ENU=This is the main guide of the team.;ESP=Este es el guia principal del equipo.';
      Text006@1100217000 : TextConst 'ENU=The teams are not defined by hotel;ESP=La definicion de equipos no es por hotel';
      Text007@1100217001 : TextConst 'ENU=The teams are not defined by ticket number;ESP=La definicion de equipos no es por ticket';
      Text008@1100217002 : TextConst 'ENU=The final ticket number must be higher than the initial number.;ESP=El numero de ticket final debe ser posterior al inicial.';
      Text009@1100217003 : TextConst 'ENU=The ticket %1 is not defined for this representant.;ESP=El ticket %1 no esta definido para este representante.';
      Text010@1100217004 : TextConst 'ENU=The ticket %1 is into another team definition line.;ESP=El ticket %1 est  definido en otra linea de equipo.';

    LOCAL PROCEDURE _CheckData@1100253000();
    VAR
      lrCont@1100253000 : Record 7009723;
      lrEquipGuia@1100253001 : Record 7010249;
    BEGIN
      // _CheckData

      IF (Hotel = '') OR
         ("Fecha desde" = 0D) OR
         ("Fecha hasta" = 0D) OR
         ("Guia venta" = '') THEN
        EXIT;

      //. Control de que el periodo de fechas sea correcto

      IF "Fecha desde" > "Fecha hasta" THEN
        ERROR(Text001);

      //. Control de que el hotel tenga al menos un contrato de compra en el periodo
      //. Lo hacemos sobre la fecha de inicio del periodo para no tener problemas con
      //. los cambios de temporada

      lrCont.RESET;
      lrCont.SETRANGE (Hotel                  , Hotel);
      lrCont.SETFILTER("Fecha inicio contrato", '<=%1', "Fecha desde");
      lrCont.SETFILTER("Fecha final contrato" , '>=%1', "Fecha desde");
      lrCont.SETRANGE (Confirmado             , TRUE);
      IF NOT lrCont.FINDFIRST THEN
        ERROR(Text002);

      //. Comprobar que no se asigne dos veces el mismo guia al mismo hotel en el mismo periodo

      lrEquipGuia.RESET;
      lrEquipGuia.SETRANGE (Hotel        , Hotel);
      lrEquipGuia.SETFILTER("Fecha desde", '<=%1', "Fecha hasta");
      lrEquipGuia.SETFILTER("Fecha hasta", '>=%1', "Fecha desde");
      lrEquipGuia.SETRANGE ("Guia venta" , "Guia venta");
      lrEquipGuia.SETFILTER(ID           , '<>%1', ID);
      IF lrEquipGuia.FINDFIRST THEN
        ERROR(Text003);
    END;

    LOCAL PROCEDURE _CheckTickets@1100217001();
    VAR
      lrTalonario@1100217000 : Record 7010146;
      lrEquipo@1100217002 : Record 7010249;
      lwTicket@1100217001 : Code[10];
    BEGIN
      // _CheckTickets

      IF ("Num. Ticket inicial" = '') OR
         ("Num. Ticket final" = '') THEN
        EXIT;

      //. Esta funcion realizara las siguientes comprobaciones
      //.   1. Los numeros deben ser coherentes entre si
      //.   2. La numeracion pertenecen a un talonario del guia
      //.   3. La numeracion no esta incluida en ninguna otra linea

      //. Coherencia
      IF "Num. Ticket inicial" > "Num. Ticket final" THEN
        ERROR(Text008);

      lwTicket := "Num. Ticket inicial";
      REPEAT
        //. Dentro de un talonario del guia
        lrTalonario.RESET;
        lrTalonario.SETRANGE ("Guia Venta"       , "Guia venta");
        lrTalonario.SETFILTER("N§ Ticket inicial", '<=%1', lwTicket);
        lrTalonario.SETFILTER("N§ Ticket final"  , '>=%1', lwTicket);
        IF NOT lrTalonario.FINDFIRST THEN
          ERROR(Text009);

        //. No duplicidad
        lrEquipo.RESET;
        lrEquipo.SETFILTER(ID                   , '<>%1', ID);
        lrEquipo.SETFILTER("Num. Ticket inicial", '<=%1', lwTicket);
        lrEquipo.SETFILTER("Num. Ticket final"  , '>=%1', lwTicket);
        IF lrEquipo.FINDFIRST THEN
          ERROR(Text010);

        lwTicket := INCSTR(lwTicket);
      UNTIL lwTicket > "Num. Ticket final";
    END;

    PROCEDURE _CompletarTicket@1100217002(VAR pwTicket@1100217000 : Code[10]);
    VAR
      lrTalonario@1100217001 : Record 7010146;
      lrGuia@1100217002 : Record 7010241;
    BEGIN
      // _CompletarTicket

      lrGuia.GET("Guia venta");

      lrTalonario.a¤ade_ceros(pwTicket, lrGuia."Oficina ventas");
    END;

    BEGIN
    {
      $001 AJS 04112013 CNT-OC-13017 Nuevos campos "Equipo definido por", "Num. Ticket inicial" y "Num. Ticket final"
    }
    END.
  }
}
