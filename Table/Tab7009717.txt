OBJECT Table 7009717 TourOperador
{
  OBJECT-PROPERTIES
  {
    Date=08/06/15;
    Time=[ 9:37:41];
    Modified=Yes;
    Version List=AIC2009,CNT-OC-14035,COM;
  }
  PROPERTIES
  {
    OnInsert=VAR
               lrRecRef@1000000000 : RecordRef;
               lrxRecRef@1000000001 : RecordRef;
             BEGIN
               rParAge.FINDFIRST;

               IF NOT rParAge."Empresa Delegacion" THEN
                 ERROR(Text001);

               // Vamos a crear un valor de la dimension 2 para este nuevo proveedor

               GLSetup.FINDFIRST;
               GLSetup.TESTFIELD("Global Dimension 2 Code");

               IF NOT rDimVal.GET(GLSetup."Global Dimension 2 Code", TourOperador) THEN
               BEGIN
                 rDimVal.INIT;
                 rDimVal.VALIDATE("Dimension Code", GLSetup."Global Dimension 2 Code");
                 rDimVal.VALIDATE(Code            , TourOperador);
                 rDimVal.VALIDATE(Name            , Nombre);
                 rDimVal.INSERT(TRUE);
               END
               ELSE
               BEGIN
                 rDimVal.VALIDATE(Name, Nombre);
                 rDimVal.MODIFY(TRUE);
               END;

               VALIDATE("Source Counter");

               //+$015 <
               IF "Empresa comercializadora" <> '' THEN
               BEGIN
                 lrRecRef.GETTABLE(Rec);
                 lrxRecRef.GETTABLE(xRec);
                 cSincroCOM.SincronizaTabla(lrRecRef, lrxRecRef, cSincroCOM.OpInsert, "Empresa comercializadora");

                 //. Tambien sincronizamos el valor de dimension contable asociado
                 lrRecRef.GETTABLE(rDimVal);
                 lrxRecRef.GETTABLE(rDimVal);
                 cSincroCOM.SincronizaTabla(lrRecRef, lrxRecRef, cSincroCOM.OpInsert, "Empresa comercializadora");
               END;
               //+$015 >
             END;

    OnModify=VAR
               lrRecRef@1000000001 : RecordRef;
               lrxRecRef@1000000000 : RecordRef;
             BEGIN
               rParAge.FINDFIRST;

               IF NOT rParAge."Empresa Delegacion" THEN
                 ERROR(Text003);

               GLSetup.FINDFIRST;
               GLSetup.TESTFIELD("Global Dimension 2 Code");

               IF rDimVal.GET(GLSetup."Global Dimension 2 Code", TourOperador) THEN
               BEGIN
                 rDimVal2 := rDimVal;
                 rDimVal2.VALIDATE(Name, Nombre);
                 rDimVal2.MODIFY(TRUE);
               END
               ELSE
               BEGIN
                 rDimVal2.INIT;
                 rDimVal2.VALIDATE("Dimension Code", GLSetup."Global Dimension 2 Code");
                 rDimVal2.VALIDATE(Code            , TourOperador);
                 rDimVal2.VALIDATE(Name            , Nombre);
                 rDimVal2.INSERT(TRUE);
               END;

               VALIDATE("Source Counter");
               Replicado :=FALSE;

               //+$015 <
               IF "Empresa comercializadora" <> '' THEN
               BEGIN
                 lrRecRef.GETTABLE(Rec);
                 lrxRecRef.GETTABLE(xRec);
                 cSincroCOM.SincronizaTabla(lrRecRef, lrxRecRef, cSincroCOM.OpModify, "Empresa comercializadora");

                 //. Tambien sincronizamos el valor de dimension contable asociado
                 lrRecRef.GETTABLE(rDimVal2);
                 lrxRecRef.GETTABLE(rDimVal);
                 cSincroCOM.SincronizaTabla(lrRecRef, lrxRecRef, cSincroCOM.OpModify, "Empresa comercializadora");
               END;
               //+$015 >
             END;

    OnDelete=VAR
               lrActED@1103355000 : Record 7010001;
               lrCITransfer@1100217000 : Record 7009805;
               lrRsvExc@1100217001 : Record 7010162;
               lrRecRef@1000000001 : RecordRef;
               lrxRecRef@1000000000 : RecordRef;
             BEGIN
               rParAge.FINDFIRST;

               IF NOT rParAge."Empresa Delegacion" THEN
                 ERROR(Text004);

               // Controlamos que no tenga bonos asociados

               rBono.RESET;
               rBono.SETRANGE(Delegacion  , rParAge.Delegacion);
               rBono.SETRANGE(TourOperador, TourOperador);
               IF rBono.FINDFIRST THEN
                 ERROR(Text006);

               //. Controlamos que no tenga coste/ingreso de transfer

               lrCITransfer.RESET;
               lrCITransfer.SETRANGE(TourOperador, TourOperador);
               IF lrCITransfer.FINDFIRST THEN
                 ERROR(Text010);

               //. Controlamos que no tenga reservas de excursiones

               lrRsvExc.RESET;
               lrRsvExc.SETRANGE(TourOperador, TourOperador);
               lrRsvExc.SETRANGE(Anulada     , FALSE);
               lrRsvExc.SETRANGE(Reembolsada , FALSE);
               IF lrRsvExc.FINDFIRST THEN
                 ERROR(Text011);

               //. Borramos la dimension asociada

               GLSetup.FINDFIRST;
               GLSetup.TESTFIELD("Global Dimension 2 Code");

               IF rDimVal.GET(GLSetup."Global Dimension 2 Code", TourOperador) THEN
                 rDimVal.DELETE(TRUE);

               InsertaLinAction;

               //+$015 <
               IF "Empresa comercializadora" <> '' THEN
               BEGIN
                 lrRecRef.GETTABLE(Rec);
                 lrxRecRef.GETTABLE(xRec);
                 cSincroCOM.SincronizaTabla(lrRecRef, lrxRecRef, cSincroCOM.OpDelete, "Empresa comercializadora");

                 //. Tambien sincronizamos el valor de dimension contable asociado
                 lrRecRef.GETTABLE(rDimVal);
                 lrxRecRef.GETTABLE(rDimVal);
                 cSincroCOM.SincronizaTabla(lrRecRef, lrxRecRef, cSincroCOM.OpDelete, "Empresa comercializadora");
               END;
               //+$015 >
             END;

    OnRename=VAR
               lrRecRef@1000000001 : RecordRef;
               lrxRecRef@1000000000 : RecordRef;
             BEGIN
               //ERROR(Text007);
             END;

    CaptionML=[ENU=TourOperator;
               ESP=TourOperador];
    LookupFormID=Form7009722;
    DrillDownFormID=Form7009722;
  }
  FIELDS
  {
    { 1   ;   ;TourOperador        ;Code10        ;CaptionML=[ENU=TourOperator;
                                                              ESP=TourOperador];
                                                   NotBlank=Yes;
                                                   Description=PK }
    { 3   ;   ;Nombre              ;Text30        ;CaptionML=[ENU=Name;
                                                              ESP=Nombre] }
    { 5   ;   ;Direccion           ;Text30        ;CaptionML=[ENU=Address;
                                                              ESP=Direccion] }
    { 7   ;   ;Poblacion           ;Text30        ;CaptionML=[ENU=Town;
                                                              ESP=Poblacion] }
    { 9   ;   ;CIF/NIF             ;Text15        ;CaptionML=[ENU=VAT Identifier;
                                                              ESP=CIF/NIF] }
    { 11  ;   ;Telefono 1          ;Text15        ;CaptionML=[ENU=Telephone 1;
                                                              ESP=Telefono 1] }
    { 13  ;   ;Telefono 2          ;Text15        ;CaptionML=[ENU=Telephone 2;
                                                              ESP=Telefono 2] }
    { 15  ;   ;OLD_Direccion Teleproceso;Text50   ;CaptionML=[ENU=Teleprocessing Address;
                                                              ESP=Direccion Teleproceso];
                                                   Description=$002 }
    { 17  ;   ;Fax                 ;Text15        ;CaptionML=[ENU=Fax;
                                                              ESP=Fax] }
    { 19  ;   ;Idioma              ;Code10        ;TableRelation=Language.Code;
                                                   CaptionML=[ENU=Language;
                                                              ESP=Idioma];
                                                   Description=FK Idioma }
    { 21  ;   ;E-Mail              ;Text80        ;CaptionML=[ENU=E-mail;
                                                              ESP=E-Mail] }
    { 23  ;   ;OLD_Paquete Teleproceso;Code10     ;CaptionML=[ENU=Teleprocessing package;
                                                              ESP=Paquete Teleproceso];
                                                   Description=$002, FK Paquete }
    { 25  ;   ;Filtro Fechas       ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Dates filter;
                                                              ESP=Filtro Fechas] }
    { 27  ;   ;Filtro Hoteles      ;Code20        ;FieldClass=FlowFilter;
                                                   TableRelation=Hotel.Hotel;
                                                   CaptionML=[ENU=Hotels filter;
                                                              ESP=Filtro Hoteles] }
    { 29  ;   ;Ocupacion TTOO      ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Reserva Hotel" WHERE (Hotel booking=FIELD(Filtro Hoteles),
                                                                                            TourOperador Venta=FIELD(TourOperador),
                                                                                            Fecha entrada=FIELD(Filtro Fechas)));
                                                   CaptionML=[ENU=TTOO occupancy;
                                                              ESP=Ocupacion TTOO] }
    { 31  ;   ;Ingresos hotel TTOO ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum(Bono."Ingreso Final" WHERE (Delegacion=FIELD(Filtro Delegaciones),
                                                                                               TourOperador=FIELD(TourOperador),
                                                                                               Hotel=FIELD(Filtro Hoteles),
                                                                                               Fecha llegada=FIELD(Filtro Fechas)));
                                                   CaptionML=[ENU=TTOO hotel incomes;
                                                              ESP=Ingresos hotel TTOO];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=Divisa }
    { 33  ;   ;Filtro Delegaciones ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation=TourOperador.TourOperador;
                                                   CaptionML=[ENU=Local Offices Filter;
                                                              ESP=Filtro Delegaciones] }
    { 35  ;   ;Costes hotel TTOO   ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum(Bono."Coste final" WHERE (Delegacion=FIELD(Filtro Delegaciones),
                                                                                             TourOperador=FIELD(TourOperador),
                                                                                             Hotel=FIELD(Filtro Hoteles),
                                                                                             Fecha llegada=FIELD(Filtro Fechas)));
                                                   CaptionML=[ENU=TTOO hotel costs;
                                                              ESP=Costes hotel TTOO];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=Divisa }
    { 37  ;   ;Dias prepago        ;Integer       ;CaptionML=[ENU=Days to include in proforma;
                                                              ESP=Dias incluir en proforma];
                                                   MinValue=0 }
    { 39  ;   ;Mercado             ;Code10        ;TableRelation=Country/Region.Code;
                                                   CaptionML=[ENU=Market;
                                                              ESP=Mercado] }
    { 50000;  ;Source Counter      ;Integer       ;OnValidate=VAR
                                                                TourOperador@1103355000 : Record 7009717;
                                                                Res@1103355001 : Boolean;
                                                              BEGIN
                                                                IF TourOperador.SETCURRENTKEY("Source Counter") THEN BEGIN
                                                                  Res := TourOperador.FIND('+');
                                                                  "Source Counter" := TourOperador."Source Counter" + 1;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Source Counter;
                                                              ESP=Source Counter];
                                                   Description=Campo usado por el Replicador }
    { 50002;  ;Divisa              ;Code10        ;TableRelation=Currency.Code;
                                                   CaptionML=[ENU=Currency;
                                                              ESP=Divisa];
                                                   Description=FK Divisa }
    { 50004;  ;Grupo Touroperador  ;Code10        ;TableRelation=TourOperador;
                                                   OnValidate=VAR
                                                                lrCupoDiaC@1100253000 : Record 7009757;
                                                                lrResDiaC@1100253001 : Record 7009755;
                                                                lrCupoC@1100253003 : Record 7009739;
                                                                lrResHot@1100253004 : Record 7009743;
                                                                lwGrpTTOO@1100253002 : Code[10];
                                                                lwVent@1100253005 : Dialog;
                                                                lwTotal@1100253006 : Integer;
                                                                lwCont@1100253007 : Integer;
                                                              BEGIN

                                                                IF TourOperador <> '' THEN BEGIN
                                                                  lwGrpTTOO := "Grupo Touroperador";
                                                                  IF lwGrpTTOO = '' THEN
                                                                    lwGrpTTOO := TourOperador;

                                                                  CLEAR(lrCupoC); // Cupos x dia compra
                                                                  lrCupoC.SETRANGE("Touroperador Venta", TourOperador);
                                                                  IF lrCupoC.FINDSET THEN BEGIN
                                                                    lwTotal := lrCupoC.COUNT;
                                                                    CLEAR(lwCont);
                                                                    lwVent.OPEN(STRSUBSTNO(Text009, lrCupoDiaC.TABLECAPTION) + '  @1@@@@@@@@@@@');
                                                                    REPEAT
                                                                      CLEAR(lrCupoDiaC);
                                                                      lrCupoDiaC.SETRANGE(Delegacion       , lrCupoC.Delegacion);
                                                                      lrCupoDiaC.SETRANGE("Contrato compra", lrCupoC."N§ Contrato compra");
                                                                      lrCupoDiaC.SETRANGE(Habitacion       , lrCupoC."Habitacion cupo");
                                                                      lrCupoDiaC.SETRANGE(Dia              , lrCupoC."Fecha desde", lrCupoC."Fecha hasta");
                                                                      lrCupoDiaC.MODIFYALL("Grupo TourOperador", lwGrpTTOO);
                                                                      lwCont +=1;
                                                                      lwVent.UPDATE(1, ROUND(lwCont/lwTotal*10000,1));
                                                                    UNTIL lrCupoC.NEXT=0;
                                                                    lwVent.CLOSE;
                                                                  END;

                                                                  CLEAR(lrResHot); // Reservas x Dia Compra
                                                                  lrResHot.SETCURRENTKEY("TourOperador Venta","Hotel booking",Regimen,Habitacion,"Fecha entrada");
                                                                  lrResHot.SETRANGE("TourOperador Venta", TourOperador);
                                                                  lrResHot.SETRANGE(Responsable, TRUE);
                                                                  IF lrResHot.FINDSET THEN BEGIN
                                                                    lwTotal := lrResHot.COUNT;
                                                                    CLEAR(lwCont);
                                                                    lwVent.OPEN(STRSUBSTNO(Text009, lrResDiaC.TABLECAPTION) + '  @1@@@@@@@@@@@');
                                                                    REPEAT
                                                                      CLEAR(lrResDiaC);
                                                                      lrResDiaC.SETRANGE(Delegacion  , lrResHot.Delegacion);
                                                                      lrResDiaC.SETRANGE("N§ Reserva", lrResHot."N§ Reserva");
                                                                      lrResDiaC.MODIFYALL("Grupo touroperador", lwGrpTTOO);
                                                                      lwCont +=1;
                                                                      IF lwCont MOD 100 = 0 THEN
                                                                        lwVent.UPDATE(1, ROUND(lwCont/lwTotal*10000,1));
                                                                    UNTIL lrResHot.NEXT=0;
                                                                    lwVent.CLOSE;
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Group Touroperator;
                                                              ESP=Touroperador Grupo];
                                                   Description=FK Touroperador }
    { 50006;  ;Descuento solo vta origen;Boolean  ;CaptionML=[ENU=Only discount on origin sales;
                                                              ESP=Descuento solo vta origen] }
    { 50007;  ;Bloqueado           ;Boolean       ;OnValidate=VAR
                                                                rPubCfg@1000000000 : Record 7010268;
                                                              BEGIN

                                                                // $011
                                                                IF Bloqueado = TRUE THEN BEGIN
                                                                  rPubCfg.RESET;
                                                                  rPubCfg.SETRANGE(rPubCfg.TourOperador,TourOperador);
                                                                  IF rPubCfg.FINDFIRST THEN BEGIN
                                                                    rPubCfg.MODIFYALL(rPubCfg."Fecha hasta",TODAY - 1);
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Blocked;
                                                              ESP=Bloqueado] }
    { 50008;  ;Replicado           ;Boolean       ;OnValidate=BEGIN
                                                                Replicado := FALSE;
                                                              END;

                                                   CaptionML=[ENU=Replicated;
                                                              ESP=Replicado] }
    { 50009;  ;Traspasar Hotel Payment;Boolean    ;OnValidate=VAR
                                                                lrEmpresa@1103355000 : Record 2000000006;
                                                              BEGIN

                                                                //-$003IF "Traspasar Hotel Payment" THEN BEGIN
                                                                //-$003  IF FORM.RUNMODAL(0, lrEmpresa) IN [ACTION::LookupOK, ACTION::OK] THEN
                                                                //-$003    "Empresa Hotel Payment" := lrEmpresa.Name
                                                                //-$003  ELSE BEGIN
                                                                //-$003    "Empresa Hotel Payment" := '';
                                                                //-$003    "Traspasar Hotel Payment" := FALSE;
                                                                //-$003  END;
                                                                //-$003END
                                                                //-$003ELSE BEGIN
                                                                //-$003  "Empresa Hotel Payment"       := '';
                                                                //-$003  "Factura ingreso solo margen" := FALSE;
                                                                //-$003END;
                                                              END;

                                                   CaptionML=[ENU=Transfer Hotel Payment;
                                                              ESP=Traspasar Hotel Payment] }
    { 50010;  ;Descuento sobre importes;Option    ;CaptionML=[ENU=Discount on amounts;
                                                              ESP=Descuento sobre importes];
                                                   OptionCaptionML=[ENU=With taxes,Without taxes;
                                                                    ESP=Con impuestos,Sin impuestos];
                                                   OptionString=Con impuestos,Sin impuestos }
    { 50011;  ;Codigo Exp. facturas REWE;Text4    ;CaptionML=[ENU=Exp. Code REWE invoices;
                                                              ESP=Codigo Exp. facturas REWE];
                                                   Description=Codigo para los campos Invoiced to company de la exportaci¢n tiene que ser de 2 o 4 caracteres }
    { 50012;  ;Exportar REWE       ;Boolean       ;CaptionML=[ENU=Export REWE;
                                                              ESP=Exportar REWE];
                                                   Description=Indica si el touroperador se debe incluir en la exportaci¢n de REWE }
    { 50013;  ;Empresa Hotel Payment;Text30       ;TableRelation=Company.Name;
                                                   OnValidate=BEGIN
                                                                IF NOT "Traspasar Hotel Payment" THEN
                                                                  ERROR(Text008, FIELDNAME("Empresa Hotel Payment"));
                                                              END;

                                                   CaptionML=[ENU=Company Hotel Payment;
                                                              ESP=Empresa Hotel Payment];
                                                   Description=Empresa a la que se han de exportar los bonos si hay traspaso hotel payment }
    { 50014;  ;Factura ingreso solo margen;Boolean;OnValidate=BEGIN
                                                                IF NOT "Traspasar Hotel Payment" THEN
                                                                  ERROR(Text008, FIELDNAME("Factura ingreso solo margen"));
                                                              END;

                                                   CaptionML=[ENU=Income invoice only margin;
                                                              ESP=Factura ingreso solo margen];
                                                   Description=Solo si hay traspaso hotel payment }
    { 50015;  ;Proforma x bono     ;Boolean       ;CaptionML=[ENU=Pro-forma x voucher;
                                                              ESP=Proforma x bono];
                                                   Description=Genera una proforma por cada bono distinto para este touroperador }
    { 50016;  ;Logo                ;BLOB          ;CaptionML=[ENU=Logo;
                                                              ESP=Logo];
                                                   Description=Para guardar el Logo del Touroperador en BMP. }
    { 50017;  ;Aplica comision en factura;Boolean ;CaptionML=[ENU=Apply commission in invoice;
                                                              ESP=Aplica comision en factura];
                                                   Description=$001 }
    { 50050;  ;Permitir Validar Sin Conciliar;Boolean;
                                                   Description=GOC-LP-11011 Permite validar factura sin conciliar bonos }
    { 50052;  ;Representante credito;Code20       ;TableRelation="Representante excursiones".Codigo;
                                                   CaptionML=[ENU=Credit sale agent;
                                                              ESP=Representante credito];
                                                   Description=$005 }
    { 50054;  ;Crear asiento comision;Boolean     ;CaptionML=[ENU=Create commission entry;
                                                              ESP=Crear asiento comision] }
    { 50055;  ;Requiere confirmaci¢n por rsv;Boolean;
                                                   CaptionML=[ENU=Requires confirmation per booking;
                                                              ESP=Requiere confirmaci¢n por reserva];
                                                   Description=$009 }
    { 50056;  ;Canal venta x defecto;Code10       ;TableRelation="Canal venta".Codigo;
                                                   CaptionML=[ENU=Default sale channel;
                                                              ESP=Canal venta x defecto];
                                                   Description=$012, "Canal venta".Codigo }
    { 50058;  ;Empresa comercializadora;Text30    ;TableRelation=Company.Name;
                                                   OnValidate=BEGIN
                                                                IF "Empresa comercializadora" <> xRec."Empresa comercializadora" THEN
                                                                BEGIN
                                                                  rParAge.FINDFIRST;
                                                                  IF NOT rParAge."Empresa Delegacion" THEN
                                                                    ERROR(Text012);

                                                                  IF "Empresa comercializadora" = COMPANYNAME THEN
                                                                    ERROR(Text013);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Offshore company;
                                                              ESP=Empresa comercializadora];
                                                   Description=$014, Company.Name }
    { 50059;  ;Codigo en comercializadora;Code10  ;OnValidate=BEGIN
                                                                _ValidateCodigoComercializador;
                                                              END;

                                                   OnLookup=BEGIN
                                                              _LookupCodigoComercializadora;
                                                            END;

                                                   CaptionML=[ENU=Code in Offshore;
                                                              ESP=Codigo en comercializadora];
                                                   Description=Codigo del touroperador en la empresa OffShore (Nexus) }
    { 7009700;;Codigo nuevo        ;Code10        ;Description=TEMPORAL, BORRAR }
    { 7009701;;Codigo anterior     ;Code10        ;Description=TEMPORAL, BORRAR }
    { 7009702;;Conversion aplicada ;Boolean       ;Description=TEMPORAL, BORRAR }
  }
  KEYS
  {
    {    ;TourOperador                            ;Clustered=Yes }
    {    ;Source Counter                          ;KeyGroups=RENUMERING }
    {    ;Replicado                               ;KeyGroups=RENUMERING }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1103355002 : TextConst 'ENU=Agency parameters folder does not exist;ESP=No existe el fichero de Parametros de la Agencia.';
      Text001@1103355003 : TextConst 'ENU=Only local offices registrations are possible;ESP=Solo se pueden dar de alta registros en las delegaciones.';
      Text002@1103355004 : TextConst 'ENU=Would you like to create the associated program to this touroperator;ESP=Desea crear el programa asociado a este touroperador.';
      Text003@1103355005 : TextConst 'ENU=Only local offices registrations can be modified;ESP=Solo se pueden modificar registros en las delegaciones.';
      Text004@1103355006 : TextConst 'ENU=Only local offices registrations can be cancelled;ESP=Solo se pueden dar de baja registros en las delegaciones.';
      Text005@1103355007 : TextConst 'ENU=Only can rename registers in local offices;ESP=Solo se pueden renombrar registros en las delegaciones.';
      rParAge@1103355000 : Record 7009700;
      rDimVal@1103355001 : Record 349;
      rDimVal2@1000000003 : Record 349;
      GLSetup@1103355008 : Record 98;
      rBono@1103355009 : Record 7010013;
      Text006@1103355010 : TextConst 'ENU=Unable to delete a touroperator with associated vouchers;ESP=No se puede borrar un touroperador con bonos asociados.';
      Text007@1103355011 : TextConst 'ENU=Unable to rename touroperators;ESP=No es posible renombrar touroperadores.';
      Text008@1103355012 : TextConst 'ENU=%1 field only can be modified when Hotel payment transfer has been marked;ESP=Solo se puede modificar el campo %1 si se ha marcado Traspasar hotel payment.';
      Text009@1100253000 : TextConst 'ENU=Updating %1;ESP=Actualizando %1';
      Text010@1100217000 : TextConst 'ENU=This touroperator can not be deleted. It''s in use in Transfer.;ESP=No se puede borrar este touroperador. Esta en uso en Transfer.';
      Text011@1100217001 : TextConst 'ENU=This touroperator can not be deleted. It''s in use in Excursions.;ESP=No se puede borrar este touroperador. Esta en uso en Excursiones.';
      cSincroCOM@1000000000 : Codeunit 7009793;
      Text012@1000000001 : TextConst 'ENU=This field can only be modified in the destination companies.;ESP=Este campo solo se puede modificar en las empresas destino.';
      Text013@1000000002 : TextConst 'ENU=You cannot sincronize with this company.;ESP=No se puede sincronizar con esta empresa.';

    PROCEDURE InsertaLinAction@1103355000();
    VAR
      lwRec@1103355000 : RecordRef;
      lcFunExp@1103355001 : Codeunit 7009911;
    BEGIN
      // InsertaLinAction
      // Inserta una linea para el borrado de la sincronizacion a la empresa Hotel Payment

      lwRec.GETTABLE(Rec);
      lcFunExp.InsertaLinAct(lwRec);
    END;

    PROCEDURE GetGrupoTouroperador@1100253000(pwTTOO@1100253000 : Code[10]) : Code[10];
    VAR
      lrTTOO@1100253001 : Record 7009717;
      lwGrpTTOO@1100253002 : Code[10];
    BEGIN
      // GetGrupoTouroperador

      IF lrTTOO.GET(pwTTOO) THEN
        lwGrpTTOO := lrTTOO."Grupo Touroperador";

      IF lwGrpTTOO = '' THEN
        lwGrpTTOO := pwTTOO;

      EXIT(lwGrpTTOO);
    END;

    LOCAL PROCEDURE _LookupCodigoComercializadora@1000000012();
    VAR
      lrTO@1000000000 : Record 7009717;
    BEGIN
      // _LookupCodigoComercializadora

      IF "Empresa comercializadora" = '' THEN
        EXIT;

      lrTO.RESET;
      lrTO.CHANGECOMPANY("Empresa comercializadora");
      lrTO.SETRANGE(Bloqueado, FALSE);
      IF FORM.RUNMODAL(0, lrTO) <> ACTION::LookupOK THEN
        EXIT;

      VALIDATE("Codigo en comercializadora", lrTO.TourOperador);
    END;

    LOCAL PROCEDURE _ValidateCodigoComercializador@1000000009();
    VAR
      lrTO@1000000001 : Record 7009717;
    BEGIN
      // _ValidateCodigoComercializador

      IF "Empresa comercializadora" = '' THEN
        ERROR('');

      IF "Codigo en comercializadora" <> '' THEN
      BEGIN
        lrTO.RESET;
        lrTO.CHANGECOMPANY(lrTO."Empresa comercializadora");
        lrTO.GET("Codigo en comercializadora");
        lrTO.TESTFIELD(Bloqueado, FALSE);
      END;
    END;

    BEGIN
    {
      ## AJF 01/10/09

        .- (GOC09002) A¤adido campo "Logo" para guardar el logo del Touroperador.

      $001 AJS 23022012 Nuevo campo Descuenta comision fra excursi para indicar en aquellos touroperador en que la
                        comision se aplica como un descuento en la factura
      $002 AJS 05032012 Marcamos campos antiguos como obsoletos

      $003 AJS 07062012 Deshabilitar el campo "Empresa Hotel Payment"

      $004 AJS 15102013 Nuevo campo "Representante ventas web" para configurar que representante utilizar cuando se crean
                        reservas excursion desde fuera de la aplicacion

      $005 AJS 02022014 Renombrar el campo "Descuento solo vta origen" en lugar de "Descuento solo sobre origen"

      $006 AJS 02022014 Elimino el confirm para crear la dimension PROGRAMA asociada al Touroperador
                        Compruebo que la dimension 2 este correctamente configurada en contabilidad

      $007 AJS 05022014 Nuevo campo Mercado para segmentar los touroperadores

      $008 AJS 25022014 Nuevo campo "Crear asiento comision"

      $009 AJS 17032014 CNT-OC-14035, Nuevo campo "Requiere confirmaci¢n por rsv"

      $010 AJS 18062014 Cambio el nombre del campo "Representante ventas web" por "Representante credito"

      $011 ARM 28082014 CNT-CAR-14073 Al bloquear TTOO despublicamos las excursiones para ‚l

      $012 AJS 04032015 Nuevo campo Canal venta x defecto para usar en la creaci¢n de reservas

      $013 AJS 09032015 Nuevos campos "Codigo nuevo" y "Codigo anterior" que ser n temporales de un proceso de recodificacion
                        de Nexus

      $014 AJS 12032015 CNT-CAR-14096, Nuevo campo Empresa comercializadora

      $015 AJS 12032015 CNT-CAR-14096, Cuando decimos que trabaja con Empresa comercializadora, sincronizamos la informacion

      $016 AJS 08062015 Nuevo campo "Codigo en comercializadora" para aquellos touroperadores que requieren conversi¢n a la hora
                        de pasar a comercializadora
    }
    END.
  }
}
