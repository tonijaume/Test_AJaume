OBJECT Table 7010268 Excursion publicada online
{
  OBJECT-PROPERTIES
  {
    Date=23/07/15;
    Time=13:39:52;
    Modified=Yes;
    Version List=AIC2009,CNT-CAR-14073;
  }
  PROPERTIES
  {
  }
  FIELDS
  {
    { 1   ;   ;TourOperador        ;Code10        ;TableRelation=TourOperador.TourOperador;
                                                   OnValidate=VAR
                                                                rTO@1103355000 : Record 7009717;
                                                              BEGIN
                                                              END;

                                                   CaptionML=[ENU=TourOperator;
                                                              ESP=TourOperador];
                                                   Description=FK TourOperador }
    { 3   ;   ;Excursion           ;Code10        ;TableRelation="Producto agencia".Codigo WHERE (Tipo Producto=CONST(Excursion),
                                                                                                  Caducado=CONST(No));
                                                   OnValidate=VAR
                                                                rProducto@1103355000 : Record 7009710;
                                                                rCalendario@1000000000 : Record 7010254;
                                                              BEGIN
                                                                _Pedir_Turno;
                                                                _Test_Turno;
                                                              END;

                                                   CaptionML=[ENU=Excursion;
                                                              ESP=Excursion];
                                                   NotBlank=Yes;
                                                   Description=FK Producto }
    { 5   ;   ;Turno Excursion     ;Integer       ;TableRelation="Calendario excursion".Turno WHERE (Excursion=FIELD(Excursion));
                                                   OnValidate=VAR
                                                                rCalendario@1000000000 : Record 7010254;
                                                              BEGIN
                                                                rCalendario.RESET;
                                                                rCalendario.SETRANGE(Excursion, Excursion);
                                                                rCalendario.SETRANGE(Turno    , "Turno Excursion");
                                                                rCalendario.FINDFIRST;

                                                                _Test_Turno;
                                                              END;

                                                   CaptionML=[ENU=Excursion turn;
                                                              ESP=Turno excursion] }
    { 7   ;   ;Fecha desde         ;Date          ;OnValidate=BEGIN
                                                                _CheckDates;

                                                                _Test_Turno;
                                                              END;

                                                   CaptionML=[ENU=Initial date;
                                                              ESP=Fecha desde];
                                                   NotBlank=Yes }
    { 9   ;   ;Fecha hasta         ;Date          ;OnValidate=BEGIN
                                                                _CheckDates;

                                                                _Test_Turno;
                                                              END;

                                                   CaptionML=[ENU=Final date;
                                                              ESP=Fecha hasta];
                                                   NotBlank=Yes }
  }
  KEYS
  {
    {    ;TourOperador,Excursion,Turno Excursion,Fecha desde;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1000000000 : TextConst 'ESP=El turno indicado no est  disponible para el periodo de fechas seleccionado.';
      Text002@1000000001 : TextConst 'ENU=The period of dates is incorrect.;ESP=El periodo de fechas no es correcto.';

    LOCAL PROCEDURE _Pedir_Turno@1000000002();
    VAR
      rCalendario@1000000000 : Record 7010254;
    BEGIN
      // _Pedir_Turno

      IF (Excursion <> '') THEN
      BEGIN
        rCalendario.RESET;
        rCalendario.SETRANGE (Excursion    , Excursion);
        rCalendario.SETFILTER("Fecha final", '>=%1', TODAY);
        IF rCalendario.FINDFIRST THEN
        BEGIN
          IF rCalendario.COUNT = 1 THEN
          BEGIN
            "Turno Excursion" := rCalendario.Turno;
          END
          ELSE
          BEGIN
            IF FORM.RUNMODAL(0, rCalendario) = ACTION::LookupOK THEN
              "Turno Excursion" := rCalendario.Turno;
          END;
        END;
      END;
    END;

    LOCAL PROCEDURE _Test_Turno@1000000000();
    VAR
      rCalendario@1000000000 : Record 7010254;
    BEGIN
      // _Test_Turno

      IF (Excursion <> '') AND
         ("Turno Excursion" <> 0) AND
         ("Fecha desde" <> 0D) AND
         ("Fecha hasta" <> 0D) THEN
      BEGIN
        rCalendario.RESET;
        rCalendario.SETRANGE (Excursion     , Excursion);
        rCalendario.SETRANGE (Turno         , "Turno Excursion");
        rCalendario.SETFILTER("Fecha inicio", '<=%1',"Fecha hasta");
        rCalendario.SETFILTER("Fecha final" , '>=%1',"Fecha desde");
        IF NOT rCalendario.FINDFIRST THEN
          ERROR(Text001);
      END;
    END;

    LOCAL PROCEDURE _CheckDates@1000000003();
    BEGIN
      // _CheckDate

      IF ("Fecha desde" = 0D) OR
         ("Fecha hasta" = 0D) THEN
         EXIT;

      IF ("Fecha desde" > "Fecha hasta") THEN
        ERROR(Text002);
    END;

    BEGIN
    {
      $001 AJS 23072015 Modifico la funcion Pedir_Turno para que solo salgan los turnos que no son historicos
    }
    END.
  }
}
