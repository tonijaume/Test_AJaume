OBJECT Table 7035396 Recursos x ruta
{
  OBJECT-PROPERTIES
  {
    Date=16/10/09;
    Time=12:30:50;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ModificaRuta;
             END;

    OnModify=BEGIN
               ModificaRuta;
             END;

    OnDelete=BEGIN
               ModificaRuta;
             END;

    CaptionML=ESP=Recursos x ruta;
  }
  FIELDS
  {
    { 1   ;   ;No. Servicio regular;Integer       ;TableRelation="Servicios Regulares".No.;
                                                   CaptionML=ESP=No. Servicio regular;
                                                   NotBlank=Yes;
                                                   Description="Servicios Regulares".No }
    { 3   ;   ;No. Ruta            ;Integer       ;TableRelation="Ruta Servicios Regulares"."No. Ruta" WHERE (No. Servicio regular=FIELD(No. Servicio regular));
                                                   CaptionML=ESP=No. Ruta;
                                                   NotBlank=Yes;
                                                   Description="Ruta Servicios Regulares"."No Ruta" WHERE (No Serv.=FIELD(No. Servicio regular)) }
    { 5   ;   ;Fecha desde         ;Date          ;OnValidate=BEGIN
                                                                IF "Fecha desde" <> xRec."Fecha desde" THEN
                                                                  ComprobarFechas;
                                                              END;

                                                   CaptionML=ESP=Fecha desde;
                                                   NotBlank=Yes }
    { 7   ;   ;Fecha hasta         ;Date          ;OnValidate=BEGIN
                                                                IF "Fecha hasta" <> xRec."Fecha hasta" THEN
                                                                  ComprobarFechas;
                                                              END;

                                                   CaptionML=ESP=Fecha hasta;
                                                   NotBlank=Yes }
    { 9   ;   ;Conductor           ;Code10        ;TableRelation=Conductor.Conductor;
                                                   OnValidate=BEGIN
                                                                IF ( "Fecha desde" = 0D ) OR ( "Fecha hasta" = 0D) THEN
                                                                  ERROR(Text006);
                                                              END;

                                                   CaptionML=ESP=Conductor;
                                                   Description=Conductor.Conductor }
    { 11  ;   ;Vehiculo            ;Code10        ;TableRelation=Vehiculo.Matricula;
                                                   OnValidate=BEGIN
                                                                IF ( "Fecha desde" = 0D ) OR ( "Fecha hasta" = 0D) THEN
                                                                  ERROR(Text006);
                                                              END;

                                                   CaptionML=ESP=Vehiculo;
                                                   Description=Vehiculo.Matricula }
  }
  KEYS
  {
    {    ;No. Servicio regular,No. Ruta,Fecha desde,Fecha hasta;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1103355000 : TextConst 'ESP=La fecha de final del periodo no puede ser anterior a la fecha de inicio.';
      Text002@1103355001 : TextConst 'ESP=Las fechas indicadas (%1-%2) estan fuera de las fechas del servicio regular (%3-%4)';
      Text003@1103355002 : TextConst 'ESP=La fecha inicial de este periodo se solapa con otro periodo ya introducido.';
      Text004@1103355003 : TextConst 'ESP=La fecha final de este periodo se solapa con otro periodo ya introducido.';
      Text005@1103355004 : TextConst 'ESP=Este periodo se solapa completamente con otro periodo ya introducido.';
      Text006@1103355005 : TextConst 'ESP=No se pueden asignar recursos sin antes definir el periodo de fechas.';
      Text007@1103355006 : TextConst 'ESP=La fecha de inicio debe ser posterior a hoy';

    PROCEDURE ComprobarFechas@1103355000();
    VAR
      lrServ@1103355000 : Record 7010379;
      lrRecRuta@1103355001 : Record 7035396;
    BEGIN
      // ComprobarFechas
      //

      IF "Fecha desde" < TODAY THEN
        ERROR(Text007);

      IF ( "Fecha desde" = 0D ) OR ( "Fecha hasta" = 0D) THEN
        EXIT;

      IF "Fecha desde" > "Fecha hasta" THEN
        ERROR(Text001);

      // Hay que buscar el servicio para comprobar que las fechas de asignacion de los recursos estan dentro del periodo
      // del servicio

      lrServ.GET("No. Servicio regular");
      lrServ.TESTFIELD("Fecha Inicio");
      lrServ.TESTFIELD("Fecha Final");

      IF ("Fecha desde" < lrServ."Fecha Inicio") OR ("Fecha hasta" > lrServ."Fecha Final") THEN
        ERROR(Text002, "Fecha desde", "Fecha hasta", lrServ."Fecha Inicio", lrServ."Fecha Final");

      // Hay comprobar que no se produzca un solapamiento de periodos

      lrRecRuta.RESET;
      lrRecRuta.SETRANGE("No. Servicio regular", "No. Servicio regular");
      lrRecRuta.SETRANGE("No. Ruta"            , "No. Ruta");
      lrRecRuta.SETFILTER("Fecha desde"        , '<=%1', "Fecha desde");
      lrRecRuta.SETFILTER("Fecha hasta"        , '>=%1', "Fecha desde");
      IF lrRecRuta.FINDFIRST THEN
        ERROR(Text003);

      lrRecRuta.RESET;
      lrRecRuta.SETRANGE("No. Servicio regular", "No. Servicio regular");
      lrRecRuta.SETRANGE("No. Ruta"            , "No. Ruta");
      lrRecRuta.SETFILTER("Fecha desde"        , '<=%1', "Fecha hasta");
      lrRecRuta.SETFILTER("Fecha hasta"        , '>=%1', "Fecha hasta");
      IF lrRecRuta.FINDFIRST THEN
        ERROR(Text004);

      lrRecRuta.RESET;
      lrRecRuta.SETRANGE("No. Servicio regular", "No. Servicio regular");
      lrRecRuta.SETRANGE("No. Ruta"            , "No. Ruta");
      lrRecRuta.SETFILTER("Fecha desde"        , '>=%1', "Fecha desde");
      lrRecRuta.SETFILTER("Fecha hasta"        , '<=%1', "Fecha hasta");
      IF lrRecRuta.FINDFIRST THEN
        ERROR(Text005);
    END;

    PROCEDURE ModificaRuta@1103355003();
    VAR
      lrRuta@1103355000 : Record 7035387;
    BEGIN
      // ModificaRuta
      // Marca la ruta como modificada

      IF lrRuta.GET("No. Servicio regular","No. Ruta") THEN BEGIN
        lrRuta.Modificada := TRUE;
        lrRuta.MODIFY;
      END;
    END;

    BEGIN
    END.
  }
}
