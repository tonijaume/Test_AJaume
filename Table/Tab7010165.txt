OBJECT Table 7010165 Suplemento Rsv. Excursion
{
  OBJECT-PROPERTIES
  {
    Date=23/05/15;
    Time=22:49:08;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ControlEdicion; //$001

               //+$006
               ////rHis.NewRecord(Delegacion, "N§ Reserva", rHis."Tipo Registro"::Venta, Text004, "Codigo Suplemento", '');
             END;

    OnModify=BEGIN
               ControlEdicion; //$001

               //. Si se cambian los adultos o ni¤os hay que comprobar si hay que autorizar otra vez
               IF ("Codigo Suplemento" <> xRec."Codigo Suplemento") OR
                  (Adultos <> xRec.Adultos) OR
                  (Ni¤os <> xRec.Ni¤os) THEN
                 "Pendiente autorizacion" := _ComprobarAutorizacion;
             END;

    OnDelete=VAR
               rPar@1103355000 : Record 7009700;
               rBack@1103355001 : Record 7009856;
             BEGIN
               ControlEdicion; //$001

               //+$006
               rHis.NewRecord(Delegacion, "N§ Reserva", rHis."Tipo Registro"::Venta, Text005, "Codigo Suplemento", '');

               // Gestion Backup

               rPar.FINDFIRST;
               IF rPar."Tipo Anulacion reserva" = rPar."Tipo Anulacion reserva"::Backup THEN BEGIN
                 rBack.TRANSFERFIELDS(Rec);
                 IF NOT rBack.INSERT THEN
                   rBack.MODIFY;
               END;
             END;

    OnRename=BEGIN
               ERROR(Text001);
             END;

    CaptionML=[ENU=Excursion reservation Supplement;
               ESP=Suplemento Reserva Excursion];
    LookupFormID=Form7010188;
    DrillDownFormID=Form7010188;
  }
  FIELDS
  {
    { 1   ;   ;Delegacion          ;Code10        ;TableRelation=Delegacion.Codigo;
                                                   CaptionML=[ENU=Local Office;
                                                              ESP=Delegacion];
                                                   Description=PK, FK Delegacion;
                                                   Editable=No }
    { 3   ;   ;N§ Reserva          ;Integer       ;TableRelation="Reserva Excursion"."N§ Reserva";
                                                   CaptionML=[ENU=Reservation No.;
                                                              ESP=N§ Reserva];
                                                   Description=PK, FK Reserva Excursion;
                                                   Editable=No }
    { 5   ;   ;Codigo Suplemento   ;Code10        ;TableRelation="Suplemento Excursiones"."Codigo Suplemento" WHERE (Tipo Producto=CONST(Excursion));
                                                   OnValidate=VAR
                                                                lrCodSupl@1100217000 : Record 7010143;
                                                              BEGIN
                                                                IF "Codigo Suplemento" <> xRec."Codigo Suplemento" THEN
                                                                BEGIN
                                                                  "Pendiente autorizacion" := _ComprobarAutorizacion;
                                                                  //+$008 <
                                                                  "Permite Precio manual"  := lrCodSupl.ComprobarPrecioManual("Codigo Suplemento");
                                                                  TraerPersonasReserva;
                                                                  //+$008 >
                                                                END;

                                                                //+$006
                                                                //rHis.NewRecord(Delegacion, "N§ Reserva", rHis."Tipo Registro"::Venta, FIELDCAPTION("Codigo Suplemento"),
                                                                //               xRec."Codigo Suplemento", "Codigo Suplemento");
                                                              END;

                                                   OnLookup=VAR
                                                              lrPar@1100217003 : Record 7009700;
                                                              lrRsv@1100217001 : Record 7010162;
                                                              lrPreSupl@1100217000 : Record 7010148;
                                                              lrCodSupl@1100217002 : Record 7010143;
                                                            BEGIN
                                                              lrPar.FINDFIRST; //+$014

                                                              //+$004
                                                              //. Eliminamos las marcas de la variable de codigos de suplemento
                                                              lrCodSupl.RESET;
                                                              lrCodSupl.CLEARMARKS;

                                                              //. Buscamos la reserva para obtener la excursion y la fecha de aplicacion
                                                              lrRsv.RESET;
                                                              lrRsv.GET(Delegacion, "N§ Reserva");

                                                              //. Buscamos los precios de suplemento para marcar los codigos que tienen precio
                                                              lrPreSupl.RESET;
                                                              lrPreSupl.SETRANGE (Delegacion   , Delegacion);
                                                              lrPreSupl.SETRANGE ("Tipo precio", lrPreSupl."Tipo precio"::"Suplemento Varios");
                                                              lrPreSupl.SETRANGE (Excursion    , lrRsv.Producto);
                                                              lrPreSupl.SETRANGE (Automatico   , FALSE);
                                                              lrPreSupl.SETFILTER(Codigo       , '<>%1', '');
                                                              //+$014 <
                                                              IF lrPar."Busca tarifa por fecha venta" THEN
                                                              BEGIN
                                                                lrPreSupl.SETFILTER("Fecha Hasta", '>=%1', lrRsv."Fecha Venta");
                                                                lrPreSupl.SETFILTER("Fecha Desde", '<=%1', lrRsv."Fecha Venta");
                                                              END
                                                              ELSE
                                                              BEGIN
                                                                lrPreSupl.SETFILTER("Fecha Hasta", '>=%1', lrRsv."Fecha Excursion");
                                                                lrPreSupl.SETFILTER("Fecha Desde", '<=%1', lrRsv."Fecha Excursion");
                                                              END;
                                                              //+$014 >
                                                              IF lrPreSupl.FINDSET THEN
                                                              BEGIN
                                                                REPEAT
                                                                  //. Marcamos los codigos de suplemento
                                                                  IF lrCodSupl.GET(lrPreSupl.Codigo) THEN
                                                                    lrCodSupl.MARK(TRUE);
                                                                UNTIL lrPreSupl.NEXT = 0;

                                                              END;

                                                              //. Mostrar los codigos de suplemento marcados
                                                              lrCodSupl.MARKEDONLY(TRUE);
                                                              IF FORM.RUNMODAL(0, lrCodSupl) = ACTION::LookupOK THEN
                                                                VALIDATE("Codigo Suplemento", lrCodSupl."Codigo Suplemento");
                                                            END;

                                                   CaptionML=[ENU=Supplement code;
                                                              ESP=Codigo Suplemento];
                                                   NotBlank=Yes;
                                                   Description=PK, FK Suplemento Tarifa }
    { 7   ;   ;Aplicacion          ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[ENU=Application;
                                                              ESP=Aplicacion] }
    { 9   ;   ;Cantidad            ;Integer       ;InitValue=1;
                                                   CaptionML=[ENU=Quantity;
                                                              ESP=Cantidad];
                                                   Editable=No }
    { 11  ;   ;Adultos             ;Integer       ;OnValidate=BEGIN
                                                                //+$006
                                                                rHis.NewRecord(Delegacion, "N§ Reserva", rHis."Tipo Registro"::Venta,
                                                                               "Codigo Suplemento" +  ' ' + FIELDCAPTION(Adultos),
                                                                               FORMAT(xRec.Adultos), FORMAT(Adultos));
                                                              END;

                                                   CaptionML=[ENU=Adults;
                                                              ESP=Adultos] }
    { 13  ;   ;Ni¤os               ;Integer       ;OnValidate=BEGIN
                                                                //+$006
                                                                rHis.NewRecord(Delegacion, "N§ Reserva", rHis."Tipo Registro"::Venta,
                                                                               "Codigo Suplemento" + ' ' + FIELDCAPTION(Ni¤os),
                                                                               FORMAT(xRec.Ni¤os), FORMAT(Ni¤os));
                                                              END;

                                                   CaptionML=[ENU=Children;
                                                              ESP=Ni¤os] }
    { 15  ;   ;Automatico          ;Boolean       ;CaptionML=[ENU=Automatic;
                                                              ESP=Automatico];
                                                   Editable=No }
    { 20  ;   ;Importe             ;Decimal       ;CaptionML=[ENU=Amount;
                                                              ESP=Importe] }
    { 30  ;   ;Importe base        ;Decimal       ;CaptionML=[ENU=Base Amount;
                                                              ESP=Importe base] }
    { 40  ;   ;Importe neto        ;Decimal       ;CaptionML=[ENU=Net Amount;
                                                              ESP=Importe neto] }
    { 50  ;   ;Pendiente autorizacion;Boolean     ;OnValidate=BEGIN
                                                                IF NOT "Pendiente autorizacion" THEN
                                                                BEGIN
                                                                  "Autorizado por" := USERID;
                                                                  "Autorizado dia" := CURRENTDATETIME;
                                                                END;

                                                                //+$006
                                                                IF xRec."Pendiente autorizacion" <> "Pendiente autorizacion" THEN
                                                                  rHis.NewRecord(Delegacion, "N§ Reserva", rHis."Tipo Registro"::Venta,
                                                                                 "Codigo Suplemento" + ' ' + FIELDCAPTION("Pendiente autorizacion"),
                                                                                 FORMAT(xRec."Pendiente autorizacion"), FORMAT("Pendiente autorizacion"));
                                                              END;

                                                   CaptionML=[ENU=Pending authorization;
                                                              ESP=Pendiente autorizacion];
                                                   Editable=No }
    { 51  ;   ;Autorizado por      ;Text20        ;CaptionML=[ENU=Authorized by;
                                                              ESP=Autorizado por];
                                                   Editable=No }
    { 52  ;   ;Autorizado dia      ;DateTime      ;CaptionML=[ENU=Authorized day;
                                                              ESP=Autorizado dia];
                                                   Editable=No }
    { 60  ;   ;Precio adulto       ;Decimal       ;OnValidate=BEGIN
                                                                IF "Precio adulto" <> xRec."Precio adulto" THEN
                                                                BEGIN
                                                                  AutorizacionPrecioManual;

                                                                  rHis.NewRecord(Delegacion, "N§ Reserva", rHis."Tipo Registro"::Venta,
                                                                                 "Codigo Suplemento" + ' ' + FIELDCAPTION("Precio adulto"),
                                                                                 FORMAT(xRec."Precio adulto"), FORMAT("Precio adulto"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Adult price;
                                                              ESP=Precio adulto];
                                                   Description=$008 }
    { 61  ;   ;Precio ni¤o         ;Decimal       ;OnValidate=BEGIN
                                                                IF "Precio ni¤o" <> xRec."Precio ni¤o" THEN
                                                                BEGIN
                                                                  AutorizacionPrecioManual;

                                                                  rHis.NewRecord(Delegacion, "N§ Reserva", rHis."Tipo Registro"::Venta,
                                                                                 "Codigo Suplemento" + ' ' + FIELDCAPTION("Precio ni¤o"),
                                                                                 FORMAT(xRec."Precio ni¤o"), FORMAT("Precio ni¤o"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Child price;
                                                              ESP=Precio ni¤o];
                                                   Description=$008 }
    { 62  ;   ;Permite Precio manual;Boolean      ;CaptionML=[ENU=Permits manual price;
                                                              ESP=Permite Precio manual];
                                                   Description=$008 }
    { 63  ;   ;Precio adulto exento;Decimal       ;OnValidate=BEGIN
                                                                IF "Precio adulto exento" <> xRec."Precio adulto exento" THEN
                                                                BEGIN
                                                                  rHis.NewRecord(Delegacion, "N§ Reserva", rHis."Tipo Registro"::Venta,
                                                                                 "Codigo Suplemento" + ' ' + FIELDCAPTION("Precio adulto exento"),
                                                                                 FORMAT(xRec."Precio adulto exento"), FORMAT("Precio adulto exento"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Adult price exempted;
                                                              ESP=Precio adulto exento] }
    { 64  ;   ;Precio ni¤o exento  ;Decimal       ;OnValidate=BEGIN
                                                                IF "Precio ni¤o exento" <> xRec."Precio ni¤o exento" THEN
                                                                BEGIN
                                                                  rHis.NewRecord(Delegacion, "N§ Reserva", rHis."Tipo Registro"::Venta,
                                                                                 "Codigo Suplemento" + ' ' + FIELDCAPTION("Precio ni¤o exento"),
                                                                                 FORMAT(xRec."Precio ni¤o exento"), FORMAT("Precio ni¤o exento"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Child price exempted;
                                                              ESP=Precio ni¤o exento] }
    { 70  ;   ;Ticket              ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Reserva Excursion"."N§ Ticket" WHERE (Delegacion=FIELD(Delegacion),
                                                                                                             N§ Reserva=FIELD(N§ Reserva)));
                                                   CaptionML=[ENU=Ticket;
                                                              ESP=Ticket];
                                                   Editable=No }
    { 75  ;   ;Mostrar en hoja servicio;Boolean   ;OnValidate=BEGIN
                                                                IF "Mostrar en hoja servicio" <> xRec."Mostrar en hoja servicio" THEN
                                                                BEGIN
                                                                //  rHis.NewRecord(Delegacion, "N§ Reserva", rHis."Tipo Registro"::Venta,
                                                                //                 "Codigo Suplemento" + ' ' + FIELDCAPTION("Mostrar en hoja servicio"),
                                                                //                 FORMAT(xRec."Mostrar en hoja servicio"), FORMAT("Mostrar en hoja servicio"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Show in workorder;
                                                              ESP=Mostrar en hoja servicio] }
    { 77  ;   ;Fecha venta         ;Date          ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Reserva Excursion"."Fecha Venta" WHERE (Delegacion=FIELD(Delegacion),
                                                                                                               N§ Reserva=FIELD(N§ Reserva)));
                                                   CaptionML=[ENU=Sale Date;
                                                              ESP=Fecha venta];
                                                   Editable=No }
    { 79  ;   ;Fecha excursion     ;Date          ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Reserva Excursion"."Fecha Venta" WHERE (Delegacion=FIELD(Delegacion),
                                                                                                               N§ Reserva=FIELD(N§ Reserva)));
                                                   CaptionML=[ENU=Excursion date;
                                                              ESP=Fecha excursion];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Delegacion,N§ Reserva,Codigo Suplemento ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100253000 : TextConst 'ENU=Unable to rename registers of this table.;ESP=No se pueden renombrar registros de esta tabla.';
      Text002@1100217000 : TextConst 'ENU=There is no price for this supplement in the excursions tariff.;ESP=No existe precio para este suplemento en la tarifa de la excursion.';
      Text003@1100217001 : TextConst 'ENU=Do you want to authorize this supplement.;ESP=Desea autorizar este suplemento.';
      Text004@1100217003 : TextConst 'ENU=New Supplement;ESP=Nuevo suplemento';
      rHis@1100217004 : Record 7010255;
      Text005@1100217005 : TextConst 'ENU=Supplement deleted;ESP=Suplemento borrado';
      Text006@1100217006 : TextConst 'ENU=You must specify adultos and/or childs.;ESP=Debe especificar adultos y/o ni¤os.';
      Text007@1100217007 : TextConst 'ENU=You indicate adults but the reservations have no adults.;ESP=Ha indicado adultos pero la reserva no tiene adultos.';
      Text008@1100217008 : TextConst 'ENU=You indicate childs but the reservations have no childs.;ESP=Ha indicado ni¤os pero la reserva no tiene ni¤os.';
      Text009@1100217002 : TextConst 'ENU=This type of supplements have a limited amount.;ESP=Los suplementos manuales tiene el importe limitado.';
      cFunSeg@1000000000 : Codeunit 7009751;

    PROCEDURE ControlEdicion@1100253001();
    VAR
      lrRsv@1100253000 : Record 7010162;
    BEGIN
      // ControlEdicion

      lrRsv.GET(Delegacion, "N§ Reserva");

      lrRsv.TESTFIELD(Anulada, FALSE);
      lrRsv.TESTFIELD(Reembolsada, FALSE);
      lrRsv.TESTFIELD(Facturada, FALSE);
      lrRsv.TESTFIELD("Liquidado GV", FALSE);
      lrRsv.TESTFIELD(Confirmada, FALSE);

      // $017
      IF (lrRsv."Reserva online") AND (GUIALLOWED) THEN
        cFunSeg.TienePermiso('EXC-ONL-MAN', TRUE);
    END;

    PROCEDURE CalcularReserva@1100217000();
    VAR
      lrRsv@1100217000 : Record 7010162;
    BEGIN
      // CalcularReserva

      IF NOT lrRsv.GET(Delegacion, "N§ Reserva") THEN
        EXIT;

      IF (lrRsv.Anulada) OR
         (lrRsv.Reembolsada) OR
         (lrRsv."Liquidado GV") OR
         (lrRsv.Confirmada) OR
         (lrRsv.Facturada) THEN
        EXIT;

      lrRsv.calcular_reserva(FALSE);
    END;

    LOCAL PROCEDURE _ComprobarAutorizacion@1100217001() : Boolean;
    VAR
      lrPar@1100217003 : Record 7009700;
      lrPre@1100217000 : Record 7010148;
      lrRsv@1100217001 : Record 7010162;
      lrTar@1100217002 : Record 7010147;
    BEGIN
      // _ComprobarAutorizacion

      lrPar.FINDFIRST; //+$014

      lrRsv.GET(Delegacion, "N§ Reserva");
      lrRsv.busca_tarifa(lrTar);

      lrPre.RESET;
      lrPre.SETRANGE (Delegacion        , lrTar.Delegacion);
      lrPre.SETRANGE (Excursion         , lrTar.Excursion);
      lrPre.SETRANGE ("Temporada Tarifa", lrTar.Temporada);
      lrPre.SETRANGE ("ID Tarifa"       , lrTar."ID Tarifa"); //+$012
      lrPre.SETRANGE ("Tipo precio"     , lrPre."Tipo precio"::"Suplemento Varios");
      lrPre.SETRANGE (Codigo            , "Codigo Suplemento");
      //+$014 <
      IF lrPar."Busca tarifa por fecha venta" THEN
      BEGIN
        lrPre.SETFILTER("Fecha Desde", '<=%1', lrRsv."Fecha Venta");
        lrPre.SETFILTER("Fecha Hasta", '>=%1', lrRsv."Fecha Venta");
      END
      ELSE
      BEGIN
        lrPre.SETFILTER("Fecha Desde", '<=%1', lrRsv."Fecha Excursion");
        lrPre.SETFILTER("Fecha Hasta", '>=%1', lrRsv."Fecha Excursion");
      END;
      //+$014 >
      IF NOT lrPre.FINDFIRST THEN
        ERROR(Text002);

      IF lrPre.Automatico THEN
        EXIT(FALSE);

      IF (lrPre."Precio Adulto" < 0) OR
         (lrPre."Precio Ni¤o" < 0) THEN
        EXIT(TRUE);

      IF NOT lrPre."Acumula al precio" THEN
      BEGIN
        IF (lrPre."Precio Adulto" = 0) OR
           (lrPre."Precio Ni¤o" = 0) THEN
          EXIT(TRUE);
      END;
    END;

    PROCEDURE AutorizarSuplemento@1100217002();
    VAR
      lrRsv@1100217001 : Record 7010162;
      lcSegur@1100217000 : Codeunit 7009751;
    BEGIN
      // AutorizarSuplemento

      IF NOT "Pendiente autorizacion" THEN
        EXIT;

      lcSegur.TienePermiso('EXC-AUT-SUP', TRUE);

      //+$007 <
      IF (Adultos = 0) AND (Ni¤os = 0) THEN
        ERROR(Text006);

      lrRsv.GET(Delegacion, "N§ Reserva");
      IF (Adultos <> 0) AND (lrRsv.Adultos = 0) THEN
        ERROR(Text007);
      IF (Ni¤os <> 0) AND (lrRsv.Ni¤os = 0) THEN
        ERROR(Text008);
      //+$007 >

      IF NOT CONFIRM(Text003) THEN
        EXIT;

      VALIDATE("Pendiente autorizacion", FALSE);
      MODIFY;
    END;

    PROCEDURE TraerPersonasReserva@1100217004();
    VAR
      lrRsv@1100217000 : Record 7010162;
      lrDatIns@1100217001 : Record 7009768;
    BEGIN
      // TraerPersonasReserva

      IF (Automatico OR "Permite Precio manual") THEN
        EXIT;

      //+$015 <
      lrDatIns.FINDFIRST;
      IF lrDatIns."Empresa Real" = lrDatIns."Empresa Real"::GOC THEN
        EXIT;
      //+$015 >

      IF NOT lrRsv.GET(Delegacion, "N§ Reserva") THEN
        EXIT;

      Adultos := lrRsv.Adultos;
      Ni¤os   := lrRsv.Ni¤os;
    END;

    PROCEDURE AutorizacionPrecioManual@1100217005();
    BEGIN
      // AutorizacionPrecioManual

      //IF ("Precio adulto" > 20) OR
      //   ("Precio adulto" < -20) OR
      //   ("Precio ni¤o" > 20) OR
      //   ("Precio ni¤o" < -20) THEN
      //  "Pendiente autorizacion" := TRUE
      //ELSE
      //  "Pendiente autorizacion" := FALSE;

      //. Control para que los manuales no puedan ser superiores a un dolar
      IF "Permite Precio manual" THEN
      BEGIN
        IF ("Precio adulto" > 10) OR ("Precio adulto" < -10) THEN
          ERROR(Text009);

        IF ("Precio ni¤o" > 10) OR ("Precio ni¤o" < -10) THEN
          ERROR(Text009);
      END;

      //. Todos los suplementos manuales se tienen que autorizar

      "Pendiente autorizacion" := TRUE;
    END;

    PROCEDURE VerSinAutorizar@1100217003();
    VAR
      lrSupl@1100217000 : Record 7010165;
      lrUser@1100217002 : Record 91;
      lfSupl@1100217001 : Form 7010188;
    BEGIN
      // VerSinAutorizar

      IF NOT lrUser.GET(USERID) THEN
        EXIT;

      IF NOT lrUser."Control Autorizar suplementos" THEN
        EXIT;

      lrSupl.RESET;
      lrSupl.FILTERGROUP(2);
      lrSupl.SETRANGE("Pendiente autorizacion", TRUE);
      lrSupl.FILTERGROUP(0);
      IF lrSupl.FINDFIRST THEN
      BEGIN
        CLEAR(lfSupl);
        lfSupl.SETTABLEVIEW(lrSupl);
        lfSupl.PasaParametros(TRUE);
        lfSupl.RUNMODAL;
      END;
    END;

    BEGIN
    {
      $001 AJS 17012011 No se puede editar la informacion de las reservas que esten dentro de un servicio

      $002 AJS 17012011 No se puede editar la informacion de las reservas anuladas o reembolsadas

      $003 AJS 19072012 Nueva funcion RecalcularReserva

      $004 AJS 12092012 Modifico la seleccion de suplementos para que solo salgan los que esten en tarifa

      $005 AJS 12092012 Nuevos campos para controlar la autorizacion de suplementos que supongan un menos ingreso

      $006 AJS 06112012 Reflejamos en el historico de la reserva

      $007 AJS 13112012 Agregar un control para que no se pueda autorizar un suplemento que no tiene adultos o ni¤os

      $008 AJS 26112012 Nuevos campos "Precio adulto", "Precio ni¤o" y Permite Precio manual

      $009 AJS 29112012 Nuevos campos "Precio adulto exento" y "Precio ni¤o exento"

      $010 AJS 30112012 Nuevo campo "Mostrar en hoja servicio"

      $011 AJS 14122012 Solo guardar en historico si el campo pendiente autorizar ha cambiado de valor

      $012 AJS 28122012 Cambio en la clave primaria de Tarifa excursiones

      $013 AJS 12062013 He creado nueva tabla para el historico de reservas de excursion

      $014 AJS 11072013 Las busquedas de precios deben hacerse por la fecha de excursion o por la fecha de venta
                        en funcion de un parametro general

      $015 AJS 08112013 Go Caribic no quiere que el suplemento se inicialice con las personas de la reserva

      $016 AJS 21012014 Aumento el suplemento manual a 10 por peticion de Luis Jose/Hiralda

      $017 ARM 30102014 Control de modificacion de reservas online

      $018 AJS 21052015 Nuevos campos calculados Fecha venta y Fecha excursion sobre Reserva excursion
    }
    END.
  }
}
