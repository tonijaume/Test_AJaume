OBJECT Table 99000752 Shop Calendar Working Days
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Shop Calendar Code;
    OnInsert=BEGIN
               CheckRedundancy;
             END;

    OnRename=BEGIN
               CheckRedundancy;
             END;

    CaptionML=[ENU=Shop Calendar Working Days;
               ESP=D¡as labor. calendario planta];
  }
  FIELDS
  {
    { 1   ;   ;Shop Calendar Code  ;Code10        ;TableRelation="Shop Calendar";
                                                   CaptionML=[ENU=Shop Calendar Code;
                                                              ESP=C¢d. calend. planta];
                                                   NotBlank=Yes }
    { 2   ;   ;Day                 ;Option        ;CaptionML=[ENU=Day;
                                                              ESP=D¡a];
                                                   OptionCaptionML=[ENU=Monday,Tuesday,Wednesday,Thursday,Friday,Saturday,Sunday;
                                                                    ESP=Lunes,Martes,Mi‚rcoles,Jueves,Viernes,S bado,Domingo];
                                                   OptionString=Monday,Tuesday,Wednesday,Thursday,Friday,Saturday,Sunday }
    { 3   ;   ;Work Shift Code     ;Code10        ;TableRelation="Work Shift";
                                                   CaptionML=[ENU=Work Shift Code;
                                                              ESP=C¢d. turno trabajo];
                                                   NotBlank=Yes }
    { 4   ;   ;Starting Time       ;Time          ;OnValidate=BEGIN
                                                                IF ("Ending Time" = 0T) OR
                                                                   ("Ending Time" < "Starting Time")
                                                                THEN BEGIN
                                                                  ShopCalendar.SETRANGE("Shop Calendar Code","Shop Calendar Code");
                                                                  ShopCalendar.SETRANGE(Day,Day);
                                                                  ShopCalendar.SETRANGE("Starting Time","Starting Time",235959T);
                                                                  IF ShopCalendar.FIND('-') THEN
                                                                    "Ending Time" := ShopCalendar."Starting Time"
                                                                  ELSE
                                                                    "Ending Time" := 235959T;
                                                                END;
                                                                CheckRedundancy;
                                                              END;

                                                   CaptionML=[ENU=Starting Time;
                                                              ESP=Hora inicial] }
    { 5   ;   ;Ending Time         ;Time          ;OnValidate=BEGIN
                                                                IF ("Ending Time" < "Starting Time") AND
                                                                   ("Ending Time" <> 000000T)
                                                                THEN
                                                                  ERROR(Text000,FIELDCAPTION("Ending Time"),FIELDCAPTION("Starting Time"));

                                                                CheckRedundancy;
                                                              END;

                                                   CaptionML=[ENU=Ending Time;
                                                              ESP=Hora final] }
  }
  KEYS
  {
    {    ;Shop Calendar Code,Day,Starting Time,Ending Time,Work Shift Code;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 must be higher than %2.;ESP=%1 debe ser mayor que %2.';
      Text001@1001 : TextConst 'ENU=There is redundancy in the Shop Calendar. Actual work shift %1 from : %2 to %3. Conflicting work shift %4 from : %5 to %6.;ESP=Existe redundancia en el Calendario planta. Turno trabajo %1 desde : %2 a %3. Conflicto turno trabajo %4 desde : %5 a %6.';
      ShopCalendar@1002 : Record 99000752;

    LOCAL PROCEDURE CheckRedundancy@1();
    VAR
      ShopCalendar2@1000 : Record 99000752;
      TempShopCalendar@1001 : TEMPORARY Record 99000752;
    BEGIN
      ShopCalendar2.SETRANGE("Shop Calendar Code","Shop Calendar Code");
      ShopCalendar2.SETRANGE(Day,Day);
      IF ShopCalendar2.FIND('-') THEN
        REPEAT
          TempShopCalendar := ShopCalendar2;
          TempShopCalendar.INSERT;
        UNTIL ShopCalendar2.NEXT = 0;

      TempShopCalendar := xRec;
      IF TempShopCalendar.DELETE THEN ;

      TempShopCalendar.SETRANGE("Shop Calendar Code","Shop Calendar Code");
      TempShopCalendar.SETRANGE(Day,Day);
      TempShopCalendar.SETRANGE("Starting Time",0T,"Ending Time" - 1);
      TempShopCalendar.SETRANGE("Ending Time","Starting Time" + 1,235959T);

      IF TempShopCalendar.FIND('-') THEN
        ERROR(
          Text001,
          "Work Shift Code",
          "Starting Time",
          "Ending Time",
          TempShopCalendar."Work Shift Code",
          TempShopCalendar."Starting Time",
          TempShopCalendar."Ending Time");
    END;

    BEGIN
    END.
  }
}
