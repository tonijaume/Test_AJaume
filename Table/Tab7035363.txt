OBJECT Table 7035363 Restricciones hotel - plazas v
{
  OBJECT-PROPERTIES
  {
    Date=16/10/09;
    Time=12:30:48;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    OnInsert=VAR
               rRHxTV@1100244000 : Record 7035363;
             BEGIN
               IF ("Punto Recogida"='') OR ("Plazas desde" = 0) OR ("Plazas hasta" = 0) THEN
                 ERROR(Text002);

               IF "Plazas desde" > "Plazas hasta" THEN
                 ERROR(Text000);

               ID := 1;
               rRHxTV.RESET;
               IF rRHxTV.FINDLAST THEN
                 ID := rRHxTV.ID + 1;

               ValidarSolape;
             END;

    OnModify=BEGIN
               IF "Plazas desde" > "Plazas hasta" THEN
                 ERROR(Text000);

               ValidarSolape;
             END;

    CaptionML=ESP=Restricciones hotel - plazas v;
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;Integer       ;CaptionML=ESP=ID;
                                                   NotBlank=No;
                                                   Editable=No }
    { 2   ;   ;Punto Recogida      ;Code10        ;TableRelation="Punto de recogida".Codigo;
                                                   CaptionML=ESP=Punto Recogida;
                                                   NotBlank=Yes }
    { 3   ;   ;Plazas desde        ;Integer       ;CaptionML=ESP=Plazas desde;
                                                   MinValue=1;
                                                   NotBlank=Yes }
    { 4   ;   ;Plazas hasta        ;Integer       ;CaptionML=ESP=Plazas hasta;
                                                   MinValue=1;
                                                   NotBlank=Yes }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
    {    ;Punto Recogida,Plazas desde              }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100244002 : TextConst 'ESP=Plazas Desde no puede tener un valor mayor que Plazas Hasta';
      Text001@1100244001 : TextConst 'ESP=Hay solape de rangos con otro registro';
      Text002@1100244000 : TextConst 'ESP=Hay que introducir el punto de recogida y el rango de plazas';

    PROCEDURE ValidarSolape@1100244000();
    VAR
      rRHxTV@1100244001 : Record 7035363;
      cModulo@1100244002 : Codeunit 7010313;
    BEGIN
      rRHxTV.RESET;
      rRHxTV.SETRANGE("Punto Recogida","Punto Recogida");
      IF rRHxTV.FINDSET THEN
        REPEAT
          //Vigilaremos de no validar justamente el registro que estamos modificando.
          IF ID <> rRHxTV.ID THEN
            //Revisaremos si los periodos intersectan.
            IF InterseccionRangos(rRHxTV."Plazas desde",rRHxTV."Plazas hasta",
                                  "Plazas desde","Plazas hasta") THEN

              ERROR(Text001);
        UNTIL rRHxTV.NEXT=0;
    END;

    PROCEDURE InterseccionRangos@1100244001(a1@1100244000 : Integer;a2@1100244001 : Integer;b1@1100244002 : Integer;b2@1100244003 : Integer) : Boolean;
    BEGIN
      //InterseccionRangos().
      // Condicion para detectar la interseccion.
      //    a1--------a2                   a1---------a2
      //         b1--------b2        b1--------b2

      IF ( (a1 >= b1) AND (a1 <= b2) ) OR ( (a2 >= b1) AND (a2 <= b2) ) OR
         ( (b1 >= a1) AND (b1 <= a2) ) OR ( (b2 >= a1) AND (b2 <= a2) ) THEN
        EXIT(TRUE);

      EXIT(FALSE);
    END;

    BEGIN
    END.
  }
}
