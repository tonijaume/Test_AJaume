OBJECT Table 7035393 Produccion Coste
{
  OBJECT-PROPERTIES
  {
    Date=07/06/10;
    Time=17:34:57;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               // AJS --> El n§ de linea se obtiene automaticamente.

               rProdCoste.RESET;
               rProdCoste.SETRANGE("N§ Parte", "N§ Parte");
               IF rProdCoste.FINDLAST THEN
                 "N§ Linea" := rProdCoste."N§ Linea" + 100
               ELSE
                 "N§ Linea" := 100;
             END;

    OnModify=VAR
               lrTTOOxParte@1100244000 : Record 7010365;
             BEGIN
             END;

    OnDelete=VAR
               lrTTOOxParte@1100244000 : Record 7010365;
             BEGIN
             END;

    CaptionML=ESP=Produccion Coste;
    LookupFormID=Form7010518;
    DrillDownFormID=Form7010518;
  }
  FIELDS
  {
    { 1   ;   ;N§ Parte            ;Code10        ;TableRelation=Parte."N§ Parte";
                                                   CaptionML=ESP=N§ Parte;
                                                   SQL Data Type=Varchar;
                                                   Description=PK, FK Parte;
                                                   Editable=No }
    { 3   ;   ;N§ Linea            ;Integer       ;CaptionML=ESP=N§ Linea;
                                                   Description=PK;
                                                   Editable=No }
    { 5   ;   ;Garaje              ;Code10        ;CaptionML=ESP=Garaje;
                                                   SQL Data Type=Varchar }
    { 7   ;   ;Fecha               ;Date          ;CaptionML=ESP=Fecha;
                                                   Editable=No }
    { 9   ;   ;Cliente             ;Code20        ;TableRelation=Customer.No.;
                                                   CaptionML=ESP=Cliente;
                                                   SQL Data Type=Varchar;
                                                   Description=FK Cliente;
                                                   Editable=No }
    { 11  ;   ;Codigo servicio     ;Code10        ;TableRelation="Codigo servicio";
                                                   CaptionML=ESP=Codigo servicio;
                                                   SQL Data Type=Varchar;
                                                   Description=FK Codigo servicio;
                                                   Editable=No }
    { 13  ;   ;Tipo servicio       ;Code10        ;TableRelation="Tipo Servicio".Codigo;
                                                   OnValidate=VAR
                                                                lrTipoSer@1100244000 : Record 7010319;
                                                              BEGIN
                                                                lrTipoSer.RESET;
                                                                IF lrTipoSer.GET("Tipo servicio") THEN
                                                                  "Tipo Servicio Fijo" := lrTipoSer."Tipo fijo";
                                                              END;

                                                   CaptionML=ESP=Tipo servicio;
                                                   SQL Data Type=Varchar;
                                                   Editable=No }
    { 14  ;   ;Tipo Servicio Fijo  ;Option        ;CaptionML=ESP=Tipo fijo;
                                                   OptionCaptionML=ESP=Excursion,Traslado;
                                                   OptionString=Excursion,Traslado;
                                                   Editable=No }
    { 15  ;   ;Proveedor           ;Code20        ;TableRelation=Vendor;
                                                   CaptionML=ESP=Proveedor;
                                                   SQL Data Type=Varchar;
                                                   Description=FK Proveedor;
                                                   Editable=No }
    { 17  ;   ;Zona Precios        ;Code10        ;CaptionML=ESP=Zona Precios;
                                                   SQL Data Type=Varchar;
                                                   Editable=No }
    { 19  ;   ;Tipo elemento       ;Code10        ;CaptionML=ESP=Tipo elemento;
                                                   SQL Data Type=Varchar;
                                                   Editable=No }
    { 21  ;   ;Tipo Vehiculo       ;Code10        ;CaptionML=ESP=Tipo Vehiculo;
                                                   SQL Data Type=Varchar;
                                                   Editable=No }
    { 23  ;   ;Pax                 ;Decimal       ;CaptionML=ESP=Pax;
                                                   DecimalPlaces=0:2 }
    { 25  ;   ;Pax Real            ;Decimal       ;CaptionML=ESP=Pax Real;
                                                   DecimalPlaces=0:2;
                                                   Editable=No }
    { 27  ;   ;Cod. Divisa         ;Code10        ;TableRelation=Currency.Code;
                                                   OnValidate=BEGIN
                                                                IF "Cod. Divisa" <> '' THEN
                                                                  divisa_coste
                                                                ELSE BEGIN
                                                                  "Importe DL"                  := ROUND(Importe);
                                                                  "Importe sin impuesto DL"     := ROUND("Importe sin impuesto");
                                                                  "Importe con impuesto DL"     := ROUND("Importe con impuesto");
                                                                  "Importe validacion DL"       := ROUND("Importe validacion");
                                                                  "Precio Pax DL"               := ROUND("Precio Pax");
                                                                  "Precio Pax DL sin impuestos" := ROUND("Precio Pax sin impuestos");
                                                                  "Precio Vehiculo DL"          := ROUND("Precio Vehiculo");
                                                                  "Precio Vehiculo DL sin imp"  := ROUND("Precio Vehiculo sin impuestos");
                                                                END;
                                                              END;

                                                   CaptionML=ESP=Cod. Divisa;
                                                   SQL Data Type=Varchar;
                                                   Description=FK Divisa }
    { 29  ;   ;Importe             ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Cod. Divisa");
                                                              END;

                                                   CaptionML=ESP=Importe;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Cod. Divisa" }
    { 31  ;   ;Importe DL          ;Decimal       ;CaptionML=ESP=Importe DL;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 33  ;   ;Importe sin impuesto;Decimal       ;OnValidate=BEGIN
                                                                rPro.GET(Proveedor);

                                                                IF NOT rConfIVA.GET (rPro."VAT Bus. Posting Group", "Grupo registro IVA Producto") THEN
                                                                  ERROR(STRSUBSTNO(err2, rPro."VAT Bus. Posting Group", "Grupo registro IVA Producto"));

                                                                "Importe con impuesto" := ROUND("Importe sin impuesto" * (1 + (rConfIVA."VAT+EC %" / 100)));

                                                                SetImporte;

                                                                VALIDATE("Cod. Divisa");
                                                              END;

                                                   CaptionML=ESP=Importe sin impuesto;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Cod. Divisa" }
    { 35  ;   ;Importe sin impuesto DL;Decimal    ;CaptionML=ESP=Importe sin impuesto DL;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 37  ;   ;Importe con impuesto;Decimal       ;OnValidate=BEGIN
                                                                rPro.GET(Proveedor);

                                                                IF NOT rConfIVA.GET (rPro."VAT Bus. Posting Group", "Grupo registro IVA Producto") THEN
                                                                  ERROR(STRSUBSTNO(err2, rPro."VAT Bus. Posting Group", "Grupo registro IVA Producto"));

                                                                "Importe sin impuesto" := ROUND("Importe con impuesto" / (1 + (rConfIVA."VAT+EC %" / 100)));

                                                                SetImporte;

                                                                VALIDATE("Cod. Divisa");
                                                              END;

                                                   CaptionML=ESP=Importe con impuesto;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Cod. Divisa" }
    { 39  ;   ;Importe con impuesto DL;Decimal    ;CaptionML=ESP=Importe con impuesto DL;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 41  ;   ;N§ Prefactura       ;Code20        ;TableRelation="Prefactura ingreso";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ESP=N§ Prefactura;
                                                   SQL Data Type=Varchar }
    { 43  ;   ;N§ Factura          ;Code20        ;CaptionML=ESP=N§ Factura }
    { 45  ;   ;Importe validacion  ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Cod. Divisa");
                                                              END;

                                                   CaptionML=ESP=Importe validacion;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Cod. Divisa" }
    { 47  ;   ;Importe validacion DL;Decimal      ;CaptionML=ESP=Importe validacion DL;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 49  ;   ;Pax validacion      ;Decimal       ;CaptionML=ESP=Pax validacion;
                                                   DecimalPlaces=0:2 }
    { 51  ;   ;Importe sin original;Decimal       ;CaptionML=ESP=Importe sin original;
                                                   Description=Utilizado para crear el reparto del coste por touroperador;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Cod. Divisa" }
    { 53  ;   ;Importe con original;Decimal       ;CaptionML=ESP=Importe con original;
                                                   Description=Utilizado para crear el reparto del coste por touroperador;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Cod. Divisa" }
    { 55  ;   ;Importe validado    ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Validacion coste"."Importe Validado" WHERE (N§ Parte=FIELD(N§ Parte),
                                                                                                                N§ Linea=FIELD(N§ Linea)));
                                                   CaptionML=ESP=Importe validado;
                                                   Description=Calculado sobre Validacion coste;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Cod. Divisa" }
    { 57  ;   ;Paxes validados     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Validacion coste"."Paxes Validados" WHERE (N§ Parte=FIELD(N§ Parte),
                                                                                                               N§ Linea=FIELD(N§ Linea)));
                                                   CaptionML=ESP=Paxes validados;
                                                   DecimalPlaces=0:2;
                                                   Description=Calculado sobre Validacion coste;
                                                   Editable=No }
    { 59  ;   ;Touroperador        ;Code10        ;TableRelation=TTOO.Codigo;
                                                   CaptionML=ESP=Touroperador;
                                                   Description=FK TTOO;
                                                   Editable=No }
    { 61  ;   ;Precio manual       ;Boolean       ;CaptionML=ESP=Precio manual;
                                                   Editable=No;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Cod. Divisa" }
    { 65  ;   ;Origen/Destino      ;Code10        ;CaptionML=ESP=Origen/Destino;
                                                   Editable=No }
    { 67  ;   ;Precio Pax          ;Decimal       ;OnValidate=VAR
                                                                lrHist@1103355000 : Record 7035352;
                                                              BEGIN

                                                                IF CurrFieldNo = FIELDNO("Precio Pax") THEN
                                                                  rModProd.CreaLineCoste(Rec,FIELDNO("Precio Pax"), FORMAT("Precio Pax"), FORMAT(xRec."Precio Pax"));


                                                                // AJF 23/09/08
                                                                // Incluimos en el historico si el campo cambia desde un formulario.
                                                                IF CurrFieldNo = FIELDNO("Precio Pax") THEN BEGIN
                                                                  lrHist.InsertarHistorico("N§ Parte", "N§ Linea", FALSE, FIELDCAPTION("Precio Pax"),
                                                                                           FORMAT(xRec."Precio Pax"), FORMAT("Precio Pax"));
                                                                END;

                                                                rPro.GET(Proveedor);

                                                                IF NOT rConfIVA.GET (rPro."VAT Bus. Posting Group", "Grupo registro IVA Producto") THEN
                                                                  ERROR(STRSUBSTNO(err2, rPro."VAT Bus. Posting Group", "Grupo registro IVA Producto"));

                                                                "Precio Pax sin impuestos" := ROUND("Precio Pax" / (1 + (rConfIVA."VAT+EC %" / 100)));
                                                                VALIDATE("Importe con impuesto", "Precio Pax" * "Pax Facturados");
                                                                VALIDATE("Cod. Divisa");
                                                              END;

                                                   CaptionML=ESP=Precio Pax;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Cod. Divisa" }
    { 69  ;   ;Precio Pax DL       ;Decimal       ;CaptionML=ESP=Precio Pax DL;
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 71  ;   ;Precio Vehiculo     ;Decimal       ;OnValidate=VAR
                                                                lrHist@1103355000 : Record 7035352;
                                                              BEGIN
                                                                IF CurrFieldNo = FIELDNO("Precio Vehiculo") THEN
                                                                  rModProd.CreaLineCoste(Rec,FIELDNO("Precio Vehiculo"), FORMAT("Precio Vehiculo"), FORMAT(xRec."Precio Vehiculo"));


                                                                // AJF 23/09/08
                                                                // Incluimos en el historico si el campo cambia desde un formulario.
                                                                IF CurrFieldNo = FIELDNO("Precio Vehiculo") THEN BEGIN
                                                                  lrHist.InsertarHistorico("N§ Parte", "N§ Linea", FALSE, FIELDCAPTION("Precio Vehiculo"),
                                                                                           FORMAT(xRec."Precio Vehiculo"), FORMAT("Precio Vehiculo"));
                                                                END;

                                                                rPro.GET(Proveedor);

                                                                IF NOT rConfIVA.GET (rPro."VAT Bus. Posting Group", "Grupo registro IVA Producto") THEN
                                                                  ERROR(STRSUBSTNO(err2, rPro."VAT Bus. Posting Group", "Grupo registro IVA Producto"));

                                                                "Precio Vehiculo sin impuestos" := ROUND("Precio Vehiculo" / (1 + (rConfIVA."VAT+EC %" / 100)));
                                                                VALIDATE("Importe con impuesto", "Precio Vehiculo");

                                                                VALIDATE("Cod. Divisa");
                                                              END;

                                                   CaptionML=ESP=Precio Vehiculo;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Cod. Divisa" }
    { 73  ;   ;Precio Vehiculo DL  ;Decimal       ;CaptionML=ESP=Precio Vehiculo DL;
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 75  ;   ;N§ Contrato         ;Code10        ;CaptionML=ESP=N§ Contrato;
                                                   Editable=No }
    { 77  ;   ;Tipo linea          ;Option        ;CaptionML=ESP=Tipo linea;
                                                   OptionCaptionML=ESP=Base,Suplemento,Manual,Extra,Ajuste;
                                                   OptionString=Base,Suplemento,Manual,Extra,Ajuste;
                                                   Editable=No }
    { 79  ;   ;Porcentaje reparto  ;Decimal       ;CaptionML=ESP=Porcentaje reparto;
                                                   Description=No editable;
                                                   Editable=No }
    { 81  ;   ;Marca               ;Code20        ;CaptionML=ESP=Marca;
                                                   Editable=No }
    { 83  ;   ;Generada prevision  ;Boolean       ;OnValidate=VAR
                                                                rParte@1100244000 : Record 7010360;
                                                              BEGIN
                                                                rParte.RESET;
                                                                IF rParte.GET("N§ Parte") THEN BEGIN
                                                                  rParte.VALIDATE("Generada prevision", "Generada prevision");
                                                                  rParte.MODIFY;
                                                                END;
                                                              END;

                                                   CaptionML=ESP=Generada prevision }
    { 85  ;   ;% IVA               ;Decimal       ;CaptionML=ESP=% IVA;
                                                   Editable=No }
    { 87  ;   ;Vuelos              ;Text30        ;CaptionML=ESP=Vuelos }
    { 88  ;   ;Vehiculo            ;Code10        ;TableRelation=Vehiculo.Matricula;
                                                   CaptionML=ESP=Vehiculo;
                                                   Editable=No }
    { 89  ;   ;Pax Facturados      ;Decimal       ;OnValidate=VAR
                                                                lrHist@1103355000 : Record 7035352;
                                                              BEGIN
                                                                IF CurrFieldNo = FIELDNO("Pax Facturados") THEN
                                                                  rModProd.CreaLineCoste(Rec,FIELDNO("Pax Facturados"), FORMAT("Pax Facturados"), FORMAT(xRec."Pax Facturados"));


                                                                // AJF 23/09/08
                                                                // Incluimos en el historico si el campo cambia desde un formulario.
                                                                IF CurrFieldNo = FIELDNO("Pax Facturados") THEN BEGIN
                                                                  lrHist.InsertarHistorico("N§ Parte", "N§ Linea", FALSE, FIELDCAPTION("Pax Facturados"),
                                                                                           FORMAT(xRec."Pax Facturados"), FORMAT("Pax Facturados"));
                                                                END;

                                                                VALIDATE("Precio Pax");
                                                              END;

                                                   CaptionML=ESP=Pax Facturados;
                                                   DecimalPlaces=0:2 }
    { 91  ;   ;Pax Transportados   ;Decimal       ;CaptionML=ESP=Pax Transportados;
                                                   DecimalPlaces=0:2 }
    { 93  ;   ;Grupo registro IVA Producto;Code10 ;TableRelation="VAT Product Posting Group".Code;
                                                   OnValidate=BEGIN
                                                                IF CurrFieldNo = FIELDNO("Grupo registro IVA Producto") THEN
                                                                  rModProd.CreaLineCoste(Rec,FIELDNO("Grupo registro IVA Producto"), FORMAT("Grupo registro IVA Producto"),
                                                                                        FORMAT(xRec."Grupo registro IVA Producto"));
                                                              END;

                                                   CaptionML=ESP=Grupo registro IVA Producto;
                                                   Description="VAT Product Posting Group".Code }
    { 95  ;   ;Hora vuelo          ;Time          ;CaptionML=ESP=Hora vuelo }
    { 97  ;   ;Facturable          ;Boolean       ;CaptionML=ESP=Facturable }
    { 99  ;   ;Linea abono         ;Boolean       ;CaptionML=ESP=Linea abono }
    { 101 ;   ;Precio Pax sin impuestos;Decimal   ;OnValidate=VAR
                                                                lrHist@1103355000 : Record 7035352;
                                                              BEGIN
                                                                IF CurrFieldNo = FIELDNO("Precio Pax sin impuestos") THEN
                                                                  rModProd.CreaLineCoste(Rec,FIELDNO("Precio Pax sin impuestos"),
                                                                                        FORMAT("Precio Pax sin impuestos"), FORMAT(xRec."Precio Pax sin impuestos"));


                                                                // AJF 23/09/08
                                                                // Incluimos en el historico si el campo cambia desde un formulario.
                                                                IF CurrFieldNo = FIELDNO("Precio Pax sin impuestos") THEN BEGIN
                                                                  lrHist.InsertarHistorico("N§ Parte", "N§ Linea", FALSE, FIELDCAPTION("Precio Pax sin impuestos"),
                                                                                           FORMAT(xRec."Precio Pax sin impuestos"), FORMAT("Precio Pax sin impuestos"));
                                                                END;

                                                                rPro.GET(Proveedor);

                                                                IF NOT rConfIVA.GET (rPro."VAT Bus. Posting Group", "Grupo registro IVA Producto") THEN
                                                                  ERROR(STRSUBSTNO(err2, rPro."VAT Bus. Posting Group", "Grupo registro IVA Producto"));

                                                                "Precio Pax" := ROUND("Precio Pax sin impuestos" * (1 + (rConfIVA."VAT+EC %" / 100)));

                                                                VALIDATE("Importe sin impuesto", "Precio Pax sin impuestos" * "Pax Facturados");
                                                                VALIDATE("Cod. Divisa");
                                                              END;

                                                   CaptionML=ESP=Precio Pax sin impuestos;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Cod. Divisa" }
    { 103 ;   ;Precio Pax DL sin impuestos;Decimal;CaptionML=ESP=Precio Pax DL sin impuestos;
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 105 ;   ;Precio Vehiculo sin impuestos;Decimal;
                                                   OnValidate=VAR
                                                                lrHist@1103355000 : Record 7035352;
                                                              BEGIN
                                                                IF CurrFieldNo = FIELDNO("Precio Vehiculo sin impuestos") THEN
                                                                  rModProd.CreaLineCoste(Rec,FIELDNO("Precio Vehiculo sin impuestos"),
                                                                                         FORMAT("Precio Vehiculo sin impuestos"), FORMAT(xRec."Precio Vehiculo sin impuestos"));


                                                                // AJF 23/09/08
                                                                // Incluimos en el historico si el campo cambia desde un formulario.
                                                                IF CurrFieldNo = FIELDNO("Precio Vehiculo sin impuestos") THEN BEGIN
                                                                  lrHist.InsertarHistorico("N§ Parte", "N§ Linea", FALSE, FIELDCAPTION("Precio Vehiculo sin impuestos"),
                                                                                           FORMAT(xRec."Precio Vehiculo sin impuestos"), FORMAT("Precio Vehiculo sin impuestos"));
                                                                END;

                                                                rPro.GET(Proveedor);

                                                                IF NOT rConfIVA.GET (rPro."VAT Bus. Posting Group", "Grupo registro IVA Producto") THEN
                                                                  ERROR(STRSUBSTNO(err2, rPro."VAT Bus. Posting Group", "Grupo registro IVA Producto"));

                                                                "Precio Vehiculo" := ROUND("Precio Vehiculo sin impuestos" * (1 + (rConfIVA."VAT+EC %" / 100)));
                                                                VALIDATE("Importe con impuesto", "Precio Vehiculo");

                                                                VALIDATE("Cod. Divisa");
                                                              END;

                                                   CaptionML=ESP=Precio Vehiculo sin impuestos;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Cod. Divisa" }
    { 107 ;   ;Precio Vehiculo DL sin imp;Decimal ;CaptionML=ESP=Precio Vehiculo DL sin imp;
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 108 ;   ;Seleccion           ;Boolean       ;CaptionML=ESP=Seleccion }
    { 110 ;   ;Servicio propio     ;Boolean       ;CaptionML=ESP=Servicio propio }
    { 111 ;   ;Zona facturacion    ;Code10        ;TableRelation="Zona de trabajo".Codigo;
                                                   CaptionML=ESP=Zona facturacion;
                                                   Description="Zona de trabajo".Codigo;
                                                   Editable=No }
    { 113 ;   ;Concepto facturacion;Code30        ;CaptionML=ESP=Concepto facturacion }
    { 114 ;   ;Paxes Servicio      ;Decimal       ;CaptionML=ESP=Paxes Servicio;
                                                   DecimalPlaces=0:0;
                                                   Description=Es el total del transportado en el parte }
    { 115 ;   ;Referencia          ;Text30        ;CaptionML=ESP=Referencia }
    { 116 ;   ;Cod Grupo           ;Code20        ;TableRelation=Grupos."Codigo Grupo";
                                                   CaptionML=ESP=Cod Grupo;
                                                   Description=FK Grupos }
    { 117 ;   ;Fecha registro factura;Date        ;CaptionML=ESP=Fecha registro factura }
    { 118 ;   ;Servicio Regular    ;Integer       ;TableRelation="Servicios Regulares".No.;
                                                   CaptionML=ESP=Servicio Regular;
                                                   Description=FK Servicios Regulares }
    { 119 ;   ;Gratuito            ;Boolean       ;CaptionML=ESP=Gratuito }
    { 120 ;   ;Fecha Confirmacion  ;DateTime      ;CaptionML=ESP=Fecha Confirmacion;
                                                   Description=Indica la fecha y Hora de confirmacion. Necesario para desconfirmar }
    { 130 ;   ;Impuestos Incluidos ;Boolean       ;CaptionML=ESP=Impuestos Incluidos }
    { 131 ;   ;Tarifa horaria      ;Code10        ;TableRelation="Tarifa horaria x contrato".Codigo;
                                                   CaptionML=ESP=Tarifa horaria;
                                                   Description="Tarifa horaria x contrato".Codigo;
                                                   Editable=No }
    { 132 ;   ;Dia festivo         ;Boolean       ;CaptionML=ESP=Dia festivo }
  }
  KEYS
  {
    {    ;N§ Parte,N§ Linea                       ;SumIndexFields=Importe con impuesto DL;
                                                   Clustered=Yes }
    {    ;Linea abono                             ;MaintainSIFTIndex=No }
    {    ;N§ Parte,Touroperador,Tipo linea,Linea abono,Tipo elemento;
                                                   SumIndexFields=Pax Facturados,Pax Transportados,Importe con impuesto,Importe sin impuesto,Importe sin impuesto DL;
                                                   MaintainSQLIndex=No;
                                                   SIFTLevelsToMaintain=[{N§ Parte,Touroperador},
                                                                         {N§ Parte,Touroperador,Tipo linea,Linea abono,Tipo elemento}] }
    {    ;Garaje,Touroperador,Tipo servicio,Fecha,Generada prevision;
                                                   SumIndexFields=Importe sin impuesto DL;
                                                   MaintainSQLIndex=No;
                                                   SIFTLevelsToMaintain=[{Garaje,Touroperador,Tipo servicio,Fecha:Day,Generada prevision,N§ Parte}] }
    {    ;Garaje,Proveedor,Fecha,Tipo servicio,Codigo servicio }
    {    ;N§ Parte,N§ Prefactura,N§ Factura        }
    {    ;Cliente,Touroperador,Fecha,Tipo linea,Tipo elemento,Garaje,Tipo servicio,Codigo servicio,Pax Transportados,Zona Precios,Servicio propio,Vehiculo;
                                                   SumIndexFields=Pax Facturados,Pax Transportados,Importe sin impuesto DL }
    {    ;Marca                                    }
    {    ;N§ Factura,Proveedor,Fecha,Cliente,Facturable;
                                                   SumIndexFields=Importe sin impuesto DL;
                                                   SIFTLevelsToMaintain=[{N§ Factura,Proveedor,Fecha:Day,Cliente,Facturable}] }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100244009 : TextConst 'ESP=Marca';
      rProdCoste@1100244001 : Record 7035393;
      rTipo@1100244002 : Record 330;
      rConfIVA@1100244003 : Record 325;
      err1@1100244004 : TextConst 'ESP=No existe el proveedor indicado en el parte de trabajo.';
      err2@1100244005 : TextConst 'ESP=No existe el IVA de compra %1-%2.';
      rPro@1100244006 : Record 23;
      rParte@1100244007 : Record 7010360;
      rTTOOxParte@1100244008 : Record 7010365;
      rPrefact@1100244011 : Record 7010415;
      rPar@1103355000 : Record 7010311;
      Text001@1103355001 : TextConst 'ESP=No existe el usuario %1.';
      rModProd@1103355002 : Record 7035330;
      Text002@1103355003 : TextConst 'ESP=¨Desea volver a los valores calculados?';

    PROCEDURE divisa_coste@1();
    BEGIN
      // divisa_coste
      //
      // El tipo de cambio lo tomamos a la fecha del servicio
      //

      "Importe DL" := rTipo.ExchangeAmtFCYToLCY(Fecha, "Cod. Divisa", Importe, rTipo.ExchangeRate(Fecha, "Cod. Divisa"));

      "Importe sin impuesto DL" := rTipo.ExchangeAmtFCYToLCY(Fecha, "Cod. Divisa", "Importe sin impuesto",
                                                             rTipo.ExchangeRate(Fecha, "Cod. Divisa"));
      "Importe con impuesto DL" := rTipo.ExchangeAmtFCYToLCY(Fecha, "Cod. Divisa", "Importe con impuesto",
                                                             rTipo.ExchangeRate(Fecha, "Cod. Divisa"));
      "Precio Pax DL" := rTipo.ExchangeAmtFCYToLCY(Fecha, "Cod. Divisa", "Precio Pax",
                                                   rTipo.ExchangeRate(Fecha, "Cod. Divisa"));
      "Precio Pax DL sin impuestos" := rTipo.ExchangeAmtFCYToLCY(Fecha, "Cod. Divisa", "Precio Pax sin impuestos",
                                                                 rTipo.ExchangeRate(Fecha, "Cod. Divisa"));
      "Precio Vehiculo DL" := rTipo.ExchangeAmtFCYToLCY(Fecha, "Cod. Divisa", "Precio Vehiculo",
                                                        rTipo.ExchangeRate(Fecha, "Cod. Divisa"));
      "Precio Vehiculo DL sin imp" := rTipo.ExchangeAmtFCYToLCY(Fecha, "Cod. Divisa", "Precio Vehiculo sin impuestos",
                                                               rTipo.ExchangeRate(Fecha, "Cod. Divisa"));
    END;

    PROCEDURE GirarImportes@30();
    BEGIN
      // GirarImportes
      //
      Pax                       := - Pax;
      "Pax Real"                := - "Pax Real";
      Importe                   := - Importe;
      "Importe DL"              := - "Importe DL";
      "Importe sin impuesto"    := - "Importe sin impuesto";
      "Importe sin impuesto DL" := - "Importe sin impuesto DL";
      "Importe con impuesto"    := - "Importe con impuesto";
      "Importe con impuesto DL" := - "Importe con impuesto DL";
      "Importe sin original"    := - "Importe sin original";
      "Importe con original"    := - "Importe con original";
      "Precio Pax"              := - "Precio Pax";
      "Precio Pax DL"           := - "Precio Pax DL";
      "Precio Vehiculo"         := - "Precio Vehiculo";
      "Precio Vehiculo DL"      := - "Precio Vehiculo DL";
      "Pax Facturados"          := - "Pax Facturados";
      "Pax Transportados"       := - "Pax Transportados";
    END;

    PROCEDURE EsPrecioPax@1103355003() : Boolean;
    BEGIN
      // EsPrecioPax
      // Devuelve TRUE si la linea tiene precio por pax

      rPar.FINDFIRST;
      EXIT("Tipo elemento" IN [rPar."Codigo Adulto", rPar."Codigo Ni¤o"]);
    END;

    PROCEDURE SetImporte@1103355004();
    BEGIN
      // SetImporte

      IF "Impuestos Incluidos" THEN
        Importe := "Importe con impuesto"
      ELSE
        Importe := "Importe sin impuesto";
    END;

    PROCEDURE LookUpHistorico@1103355005();
    VAR
      lrHistorico@1103355000 : Record 7035352;
      lfHistorico@1103355001 : Form 7010311;
    BEGIN
      // LookUpHistorico

      IF "N§ Parte" = '' THEN
        EXIT;

      lrHistorico.SETCURRENTKEY("N§ parte", Concepto, "N§ Linea");
      lrHistorico.SETRANGE("N§ parte", "N§ Parte");
      lrHistorico.SETRANGE("N§ Linea", "N§ Linea");
      lrHistorico.SETRANGE(Concepto  , lrHistorico.Concepto::"Produccion coste");

      IF NOT lrHistorico.ISEMPTY THEN BEGIN
        lfHistorico.SETRECORD    (lrHistorico);
        lfHistorico.SETTABLEVIEW (lrHistorico);
        lfHistorico.RUNMODAL
      END;
    END;

    PROCEDURE FiltrarGarajeUsuario@1103355000();
    VAR
      lrParGenerales@1103355000 : Record 7010311;
      lrUsuario@1103355001 : Record 7010420;
    BEGIN
      // FiltrarGarajeUsuario.
      // Filtramos los partes por garaje dependiendo de la configuraci¢n del usuario.

      lrParGenerales.FINDFIRST;

      IF NOT lrUsuario.GET(USERID) THEN
        ERROR(Text001, USERID);

      IF (lrUsuario."Garaje de trabajo" <> '') AND (lrUsuario."Garaje de trabajo" <> lrParGenerales."Todos los garajes") THEN BEGIN
        FILTERGROUP(2);
        SETRANGE(Garaje, lrUsuario."Garaje de trabajo");
        FILTERGROUP(0);
      END;
    END;

    PROCEDURE TieneModif@1103355006(pwCampo@1103355000 : Integer) : Boolean;
    BEGIN
      // TieneModif
      // Devuelve true si el campo tiene modificaciones manuales

      CLEAR(rModProd);
      rModProd.SETCURRENTKEY("N§ Parte","N§ Linea",Tipo,"No Campo");
      rModProd.SETRANGE("N§ Parte", "N§ Parte");
      rModProd.SETRANGE("N§ Linea", "N§ Linea");
      rModProd.SETRANGE(Tipo      , rModProd.Tipo::Coste);
      rModProd.SETRANGE("No Campo", pwCampo);
      EXIT(rModProd.FINDFIRST);
    END;

    PROCEDURE ShowModif@1103355007(pwCampo@1103355000 : Integer) : Boolean;
    BEGIN
      // ShowModif
      // Si tiene modificaciones en un determinado campo, las ense¤a

      CLEAR(rModProd);
      rModProd.FILTERGROUP(2);
      rModProd.SETCURRENTKEY("N§ Parte","N§ Linea",Tipo,"No Campo");
      rModProd.SETRANGE("N§ Parte", "N§ Parte");
      rModProd.SETRANGE("N§ Linea", "N§ Linea");
      rModProd.SETRANGE(Tipo      , rModProd.Tipo::Coste);
      rModProd.SETRANGE("No Campo", pwCampo);
      rModProd.FILTERGROUP(0);
      IF rModProd.FINDFIRST THEN
        FORM.RUN(0,rModProd);
    END;

    PROCEDURE BorraModif@1103355008();
    BEGIN
      // BorraModif
      // Borra todas las lineas de modificacion relacionadas

      CLEAR(rModProd);
      rModProd.SETCURRENTKEY("N§ Parte","N§ Linea",Tipo,"No Campo");
      rModProd.SETRANGE("N§ Parte", "N§ Parte");
      rModProd.SETRANGE("N§ Linea", "N§ Linea");
      rModProd.SETRANGE(Tipo      , rModProd.Tipo::Coste);
      rModProd.DELETEALL;
    END;

    PROCEDURE DesmarcaModif@1103355010();
    BEGIN
      // DesmarcaModif
      // Para que las lineas de modificacion puedan ser asignadas a otra linea primero
      // hay que dejar el campo "N§ Linea" a 0;

      CLEAR(rModProd);
      rModProd.SETCURRENTKEY("N§ Parte","N§ Linea",Tipo,"No Campo");
      rModProd.SETRANGE("N§ Parte", "N§ Parte");
      rModProd.SETRANGE("N§ Linea", "N§ Linea");
      rModProd.SETRANGE(Tipo      , rModProd.Tipo::Coste);
      rModProd.MODIFYALL("N§ Linea", 0);
    END;

    PROCEDURE ResersarModif@1103355011(pwConfirma@1103355001 : Boolean);
    VAR
      lwRec@1103355000 : RecordRef;
    BEGIN
      // ResersarModif
      // Devolver los valores calculados a todas las modificaciones de la linea

      lwRec.GETTABLE(Rec);
      CLEAR(rModProd);
      rModProd.SETCURRENTKEY("N§ Parte","N§ Linea",Tipo,"No Campo");
      rModProd.SETRANGE("N§ Parte", "N§ Parte");
      rModProd.SETRANGE("N§ Linea", "N§ Linea");
      rModProd.SETRANGE(Tipo      , rModProd.Tipo::Coste);
      IF rModProd.FINDSET THEN BEGIN
        IF pwConfirma THEN
          IF NOT CONFIRM(Text002) THEN
            EXIT;
        REPEAT
          rModProd.MarcaValor(lwRec, TRUE, TRUE);
        UNTIL rModProd.NEXT=0;
      END;
    END;

    BEGIN
    {
      ## AJS 23.06.2008

        - He cambiado el nombre del campo Zona trabajo por Zona facturacion (TNU08002)
        - He cambiado el FIND('-') + ASCENDING(FALSE) por FINDLAST

      ## AJS 07.06.2010

        .- Establezco la TableRelation del campo Grupo Registro IVA Producto
    }
    END.
  }
}
