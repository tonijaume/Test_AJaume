OBJECT Table 413 IC Partner
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    OnDelete=VAR
               GLEntry@1007 : Record 17;
               GLSetup@1006 : Record 98;
               AccountingPeriod@1004 : Record 50;
               Cust@1003 : Record 18;
               Vend@1002 : Record 23;
               ICInbox@1001 : Record 418;
               ICOutbox@1000 : Record 414;
             BEGIN
               GLEntry.LOCKTABLE;

               GLEntry.SETCURRENTKEY("IC Partner Code");
               GLEntry.SETRANGE("IC Partner Code",Code);
               AccountingPeriod.SETRANGE(Closed,FALSE);
               IF AccountingPeriod.FIND('-') THEN
                 GLEntry.SETFILTER("Posting Date",'>=%1',AccountingPeriod."Starting Date");
               IF GLEntry.FIND('-') THEN
                 ERROR(Text000,xRec.Code);

               GLSetup.GET;
               IF GLSetup."Allow G/L Acc. Deletion Before" <> 0D THEN BEGIN
                 GLEntry.SETFILTER("Posting Date",'>=%1',GLSetup."Allow G/L Acc. Deletion Before");
                 IF GLEntry.FIND('-') THEN
                   ERROR(Text001,Code,GLSetup."Allow G/L Acc. Deletion Before");
               END;

               IF "Customer No." <> '' THEN
                 IF Cust.GET("Customer No.") THEN
                   ERROR(Text002,Code,Cust.TABLECAPTION,Cust."No.");

               IF "Vendor No." <> '' THEN
                 IF Vend.GET("Customer No.") THEN
                   ERROR(Text002,Code,Vend.TABLECAPTION,Vend."No.");

               ICInbox.SETRANGE("IC Partner Code",Code);
               IF ICInbox.FIND('-') THEN
                 ERROR(Text003,Code,ICInbox.TABLECAPTION);

               ICOutbox.SETRANGE("IC Partner Code",Code);
               IF ICOutbox.FIND('-') THEN
                 ERROR(Text003,Code,ICOutbox.TABLECAPTION);

               GLEntry.RESET;
               GLEntry.SETCURRENTKEY("IC Partner Code");
               GLEntry.SETRANGE("IC Partner Code",Code);
               GLEntry.MODIFYALL("IC Partner Code",'');

               CommentLine.SETRANGE("Table Name",CommentLine."Table Name"::"IC Partner");
               CommentLine.SETRANGE("No.",Code);
               CommentLine.DELETEALL;
             END;

    CaptionML=[ENU=IC Partner;
               ESP=Socio IC];
    LookupFormID=Form608;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;CaptionML=[ENU=Code;
                                                              ESP=C¢digo];
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Text50        ;CaptionML=[ENU=Name;
                                                              ESP=Nombre] }
    { 3   ;   ;Currency Code       ;Code10        ;TableRelation=Currency.Code;
                                                   CaptionML=[ENU=Currency Code;
                                                              ESP=C¢d. divisa] }
    { 4   ;   ;Inbox Type          ;Option        ;OnValidate=VAR
                                                                Cust@1000 : Record 18;
                                                                Vend@1001 : Record 23;
                                                              BEGIN
                                                                IF "Inbox Type" <> xRec."Inbox Type" THEN
                                                                  "Inbox Details" := '';
                                                                IF "Inbox Type" = "Inbox Type"::"E-Mail" THEN BEGIN
                                                                  IF "Customer No." <> '' THEN BEGIN
                                                                    IF Cust.GET("Customer No.") THEN
                                                                      "Inbox Details" := Cust."E-Mail";
                                                                  END ELSE
                                                                    IF "Vendor No." <> '' THEN
                                                                      IF Vend.GET("Vendor No.") THEN
                                                                        "Inbox Details" := Vend."E-Mail";
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Inbox Type;
                                                              ESP=Tipo de bandeja de entrada];
                                                   OptionCaptionML=[ENU=File Location,Database,E-Mail,No IC Transfer;
                                                                    ESP=Ubicaci¢n archivo,Base de datos,Correo electr¢nico,No transf. IC];
                                                   OptionString=File Location,Database,E-Mail,No IC Transfer }
    { 5   ;   ;Inbox Details       ;Text250       ;TableRelation=IF (Inbox Type=CONST(Database)) Company.Name;
                                                   OnLookup=VAR
                                                              Company@1000 : Record 2000000006;
                                                              Companies@1001 : Form 357;
                                                              CmDlgMgt@1002 : Codeunit 412;
                                                              i@1003 : Integer;
                                                              InboxDetails@1004 : Text[250];
                                                              InboxDetails2@1005 : Text[250];
                                                            BEGIN
                                                              CASE "Inbox Type" OF
                                                                "Inbox Type"::Database:
                                                                  BEGIN
                                                                    Company.SETFILTER(Name,'<>%1',COMPANYNAME);
                                                                    Companies.SETTABLEVIEW(Company);
                                                                    Companies.LOOKUPMODE := TRUE;
                                                                    IF Companies.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                      Companies.GETRECORD(Company);
                                                                      "Inbox Details" := Company.Name;
                                                                    END;
                                                                  END;
                                                                "Inbox Type"::"File Location":
                                                                  BEGIN
                                                                    IF "Inbox Details" = '' THEN
                                                                      InboxDetails := STRSUBSTNO('%1.xml',Code)
                                                                    ELSE
                                                                      InboxDetails := "Inbox Details" + STRSUBSTNO('%1.xml',Code);
                                                                    InboxDetails2 := CmDlgMgt.OpenFile(Text005,InboxDetails,4,'*.xml|*.xml',1);
                                                                    IF InboxDetails <> InboxDetails2 THEN BEGIN
                                                                      "Inbox Details" := InboxDetails2;
                                                                      IF STRLEN("Inbox Details") > 4 THEN BEGIN
                                                                        i := STRLEN("Inbox Details") - 4;
                                                                        WHILE (i > 1) AND ("Inbox Details"[i] <> '\') DO
                                                                          i := i - 1;
                                                                        IF i > 1 THEN
                                                                          "Inbox Details" := COPYSTR("Inbox Details",1,i - 1);
                                                                      END;
                                                                    END;
                                                                  END;
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Inbox Details;
                                                              ESP=Detalle de bandeja de entrada] }
    { 6   ;   ;Receivables Account ;Code20        ;TableRelation="G/L Account".No.;
                                                   CaptionML=[ENU=Receivables Account;
                                                              ESP=Cta. clientes] }
    { 7   ;   ;Payables Account    ;Code20        ;TableRelation="G/L Account".No.;
                                                   CaptionML=[ENU=Payables Account;
                                                              ESP=Cta. proveedor] }
    { 10  ;   ;Blocked             ;Boolean       ;CaptionML=[ENU=Blocked;
                                                              ESP=Bloqueado] }
    { 11  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Comment Line" WHERE (Table Name=CONST(IC Partner),
                                                                                           No.=FIELD(Code)));
                                                   CaptionML=[ENU=Comment;
                                                              ESP=Comentario];
                                                   Editable=No }
    { 12  ;   ;Customer No.        ;Code20        ;TableRelation=Customer;
                                                   CaptionML=[ENU=Customer No.;
                                                              ESP=N§ cliente] }
    { 13  ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor;
                                                   CaptionML=[ENU=Vendor No.;
                                                              ESP=N§ proveedor] }
    { 14  ;   ;Outbound Sales Item No. Type;Option;CaptionML=[ENU=Outbound Sales Item No. Type;
                                                              ESP=Tipo de n§ pdto. venta salida];
                                                   OptionCaptionML=[ENU=Internal No.,Common Item No.,Cross Reference;
                                                                    ESP=N§ interno,N§ producto com£n,Ref. cruzada];
                                                   OptionString=Internal No.,Common Item No.,Cross Reference }
    { 15  ;   ;Outbound Purch. Item No. Type;Option;
                                                   CaptionML=[ENU=Outbound Purch. Item No. Type;
                                                              ESP=Tipo de n§ pdto. compra salida];
                                                   OptionCaptionML=[ENU=Internal No.,Common Item No.,Cross Reference,Vendor Item No.;
                                                                    ESP=N§ interno,N§ producto com£n,Ref. cruzada,N§ producto prov.];
                                                   OptionString=Internal No.,Common Item No.,Cross Reference,Vendor Item No. }
    { 16  ;   ;Cost Distribution in LCY;Boolean   ;CaptionML=[ENU=Cost Distribution in LCY;
                                                              ESP=Distribuci¢n de costes en DL] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      CommentLine@1001 : Record 97;
      Text000@1005 : TextConst 'ENU=You cannot delete IC Partner %1 because it has ledger entries in a fiscal year that has not been closed yet.;ESP=No es posible eliminar la IC asociada %1, a£n tiene movimientos en el libro de un ejercicio que a£n no est  cerrado.';
      Text001@1004 : TextConst 'ENU=You cannot delete IC Partner %1 because it has ledger entries after %2.;ESP=No es posible eliminar la IC asociada %1, tiene movimientos posteriores al %2.';
      Text002@1003 : TextConst 'ENU=You cannot delete IC Partner %1 because it is used for %2 %3;ESP=No es posible eliminar la IC asociada %1, se utiliza en %2 %3';
      Text003@1002 : TextConst 'ENU=You cannot delete IC Partner %1 because it is used in %2;ESP=No es posible eliminar la IC asociada %1, se utiliza en %2';
      Text004@1006 : TextConst 'ENU=%1 %2 is linked to a blocked IC Partner.;ESP=%1 %2 est  vinculado a una IC asociada bloqueada.';
      Text005@1000 : TextConst 'ENU=File Location for IC files;ESP=Ubicaci¢n de los archivos IC';

    PROCEDURE CheckICPartner@1();
    BEGIN
      TESTFIELD(Blocked,FALSE);
    END;

    PROCEDURE CheckICPartnerIndirect@2(AccountType@1000 : Text[250];AccountNo@1001 : Code[20]);
    BEGIN
      IF Blocked THEN
        ERROR(Text004,AccountType,AccountNo);
    END;

    BEGIN
    END.
  }
}
