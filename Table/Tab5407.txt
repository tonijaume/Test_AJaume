OBJECT Table 5407 Prod. Order Component
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    DataCaptionFields=Status,Prod. Order No.;
    OnInsert=BEGIN
               IF Status = Status::Finished THEN
                 ERROR(Text000,Status,TABLECAPTION);

               ReserveProdOrderComp.VerifyQuantity(Rec,xRec);
               DimMgt.InsertProdDocDim(
                 DATABASE::"Prod. Order Component",
                 Status,"Prod. Order No.","Prod. Order Line No.","Line No.",
                 "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");

               IF Status = Status::Released THEN
                 WhseProdRelease.ReleaseLine(Rec,xRec);
             END;

    OnModify=BEGIN
               IF Status = Status::Finished THEN
                 ERROR(Text000,Status,TABLECAPTION);

               WhseValidateSourceLine.ProdComponentVerifyChange(Rec,xRec);
               ReserveProdOrderComp.VerifyChange(Rec,xRec);
               IF Status = Status::Released THEN
                 WhseProdRelease.ReleaseLine(Rec,xRec);
             END;

    OnDelete=VAR
               ProdOrderLine@1000 : Record 5406;
               ProdOrderBOMComment@1001 : Record 5416;
               ItemTrackingMgt@1002 : Codeunit 6500;
             BEGIN
               IF Status = Status::Finished THEN
                 ERROR(Text000,Status,TABLECAPTION);
               IF Status = Status::Released THEN BEGIN
                 ItemLedgEntry.SETCURRENTKEY("Prod. Order No.","Prod. Order Line No.","Entry Type","Prod. Order Comp. Line No.");
                 ItemLedgEntry.SETRANGE("Prod. Order No.","Prod. Order No.");
                 ItemLedgEntry.SETRANGE("Prod. Order Line No.","Prod. Order Line No.");
                 ItemLedgEntry.SETRANGE("Entry Type",ItemLedgEntry."Entry Type"::Consumption);
                 ItemLedgEntry.SETRANGE("Prod. Order Comp. Line No.","Line No.");
                 IF ItemLedgEntry.FIND('-') THEN
                   ERROR(
                     Text99000000,
                     TABLECAPTION,"Line No.",ItemLedgEntry.TABLECAPTION);
               END;

               WhseValidateSourceLine.ProdComponentDelete(Rec);
               ReserveProdOrderComp.DeleteLine(Rec);

               CALCFIELDS("Reserved Qty. (Base)");
               TESTFIELD("Reserved Qty. (Base)",0);


               IF "Supplied-by Line No." > 0 THEN
                 IF ProdOrderLine.GET(Status,"Prod. Order No.","Supplied-by Line No.") THEN BEGIN
                   ProdOrderLine.VALIDATE(Quantity,ProdOrderLine.Quantity - "Expected Quantity");
                   IF ProdOrderLine.Quantity = 0 THEN BEGIN
                     ProdOrderLine.BlockDynamicTracking(TRUE);
                     ProdOrderLine.DELETE(TRUE);
                   END ELSE
                     ProdOrderLine.MODIFY;
                 END;

               ProdOrderBOMComment.SETRANGE(Status,Status);
               ProdOrderBOMComment.SETRANGE("Prod. Order No.","Prod. Order No.");
               ProdOrderBOMComment.SETRANGE("Prod. Order Line No.","Prod. Order Line No.");
               ProdOrderBOMComment.SETRANGE("Prod. Order BOM Line No.","Line No.");
               ProdOrderBOMComment.DELETEALL;

               WhseProdRelease.DeleteLine(Rec);

               ItemTrackingMgt.DeleteWhseItemTrkgLines(
                 DATABASE::"Prod. Order Component",Status,"Prod. Order No.",'',
                 "Prod. Order Line No.","Line No.","Location Code",TRUE);

               DimMgt.DeleteProdDocDim(
                 DATABASE::"Prod. Order Component",
                 Status,"Prod. Order No.","Prod. Order Line No.","Line No.");
             END;

    OnRename=BEGIN
               ERROR(Text99000001,TABLECAPTION);
             END;

    CaptionML=[ENU=Prod. Order Component;
               ESP=Componente orden producci¢n];
    PasteIsValid=No;
    LookupFormID=Form5407;
    DrillDownFormID=Form5407;
  }
  FIELDS
  {
    { 1   ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              ESP=Estado];
                                                   OptionCaptionML=[ENU=Simulated,Planned,Firm Planned,Released,Finished;
                                                                    ESP=Simulada,Planificada,Planif. en firme,Lanzada,Terminada];
                                                   OptionString=Simulated,Planned,Firm Planned,Released,Finished }
    { 2   ;   ;Prod. Order No.     ;Code20        ;TableRelation="Production Order".No. WHERE (Status=FIELD(Status));
                                                   CaptionML=[ENU=Prod. Order No.;
                                                              ESP=N§ orden producci¢n] }
    { 3   ;   ;Prod. Order Line No.;Integer       ;TableRelation="Prod. Order Line"."Line No." WHERE (Status=FIELD(Status),
                                                                                                      Prod. Order No.=FIELD(Prod. Order No.));
                                                   CaptionML=[ENU=Prod. Order Line No.;
                                                              ESP=N§ l¡n. orden producc.] }
    { 4   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ESP=N§ l¡nea] }
    { 11  ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   OnValidate=BEGIN
                                                                WhseValidateSourceLine.ProdComponentVerifyChange(Rec,xRec);
                                                                ReserveProdOrderComp.VerifyChange(Rec,xRec);
                                                                CALCFIELDS("Reserved Qty. (Base)");
                                                                TESTFIELD("Reserved Qty. (Base)",0);
                                                                TESTFIELD("Remaining Qty. (Base)","Expected Qty. (Base)");
                                                                IF "Item No." = '' THEN BEGIN
                                                                  CreateDim(DATABASE::Item,"Item No.");
                                                                  EXIT;
                                                                END;

                                                                Item.GET("Item No.");
                                                                IF "Item No." <> xRec."Item No." THEN BEGIN
                                                                  "Variant Code" := '';
                                                                  GetDefaultBin;
                                                                  ClearCalcFormula;
                                                                  IF "Quantity per" <> 0 THEN
                                                                    VALIDATE("Quantity per");
                                                                END;
                                                                Description := Item.Description;
                                                                Item.TESTFIELD("Base Unit of Measure");
                                                                VALIDATE("Unit of Measure Code",Item."Base Unit of Measure");
                                                                GetUpdateFromSKU;
                                                                CreateDim(DATABASE::Item,"Item No.");
                                                              END;

                                                   CaptionML=[ENU=Item No.;
                                                              ESP=N§ producto] }
    { 12  ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n] }
    { 13  ;   ;Unit of Measure Code;Code10        ;TableRelation="Item Unit of Measure".Code WHERE (Item No.=FIELD(Item No.));
                                                   OnValidate=BEGIN
                                                                WhseValidateSourceLine.ProdComponentVerifyChange(Rec,xRec);

                                                                Item.GET("Item No.");
                                                                GetGLSetup;

                                                                "Qty. per Unit of Measure" := UOMMgt.GetQtyPerUnitOfMeasure(Item,"Unit of Measure Code");
                                                                "Quantity (Base)" := CalcBaseQty(Quantity);

                                                                UpdateUnitCost;

                                                                VALIDATE("Expected Quantity",Quantity * ProdOrderNeeds);
                                                              END;

                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              ESP=C¢d. unidad medida] }
    { 14  ;   ;Quantity            ;Decimal       ;CaptionML=[ENU=Quantity;
                                                              ESP=Cantidad];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 15  ;   ;Position            ;Code10        ;CaptionML=[ENU=Position;
                                                              ESP=Posici¢n] }
    { 16  ;   ;Position 2          ;Code10        ;CaptionML=[ENU=Position 2;
                                                              ESP=Posici¢n 2] }
    { 17  ;   ;Position 3          ;Code10        ;CaptionML=[ENU=Position 3;
                                                              ESP=Posici¢n 3] }
    { 18  ;   ;Production Lead Time;DateFormula   ;CaptionML=[ENU=Production Lead Time;
                                                              ESP=Plazo producci¢n] }
    { 19  ;   ;Routing Link Code   ;Code10        ;TableRelation="Routing Link";
                                                   OnValidate=VAR
                                                                ProdOrderLine@1000 : Record 5406;
                                                                ProdOrderRtngLine@1001 : Record 5409;
                                                              BEGIN
                                                                VALIDATE("Expected Quantity",Quantity * ProdOrderNeeds);

                                                                ProdOrderLine.GET(Status,"Prod. Order No.","Prod. Order Line No.");

                                                                "Due Date" := ProdOrderLine."Starting Date";
                                                                "Due Time" := ProdOrderLine."Starting Time";
                                                                IF "Routing Link Code" <> '' THEN BEGIN
                                                                  ProdOrderRtngLine.SETRANGE(Status,Status);
                                                                  ProdOrderRtngLine.SETRANGE("Prod. Order No.","Prod. Order No.");
                                                                  ProdOrderRtngLine.SETRANGE("Routing No.",ProdOrderLine."Routing No.");
                                                                  ProdOrderRtngLine.SETRANGE("Routing Reference No.",ProdOrderLine."Routing Reference No.");
                                                                  ProdOrderRtngLine.SETRANGE("Routing Link Code","Routing Link Code");
                                                                  IF ProdOrderRtngLine.FIND('-') THEN BEGIN
                                                                    "Due Date" := ProdOrderRtngLine."Starting Date";
                                                                    "Due Time" := ProdOrderRtngLine."Starting Time";
                                                                  END;
                                                                END;
                                                                IF FORMAT("Production Lead Time") <> '' THEN BEGIN
                                                                  "Due Date" :=
                                                                    "Due Date" -
                                                                    (CALCDATE("Production Lead Time",WORKDATE) - WORKDATE);
                                                                  "Due Time" := 0T;
                                                                END;
                                                                VALIDATE("Due Date");
                                                              END;

                                                   CaptionML=[ENU=Routing Link Code;
                                                              ESP=C¢d. conexi¢n ruta] }
    { 20  ;   ;Scrap %             ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=[ENU=Scrap %;
                                                              ESP=% Rechazo];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 21  ;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   OnValidate=VAR
                                                                ItemVariant@1000 : Record 5401;
                                                              BEGIN
                                                                IF Item."No." <> "Item No." THEN
                                                                  Item.GET("Item No.");
                                                                IF "Variant Code" = '' THEN
                                                                  Description := Item.Description
                                                                ELSE BEGIN
                                                                  ItemVariant.GET("Item No.","Variant Code");
                                                                  Description := ItemVariant.Description;
                                                                END;
                                                                GetDefaultBin;
                                                                WhseValidateSourceLine.ProdComponentVerifyChange(Rec,xRec);
                                                                ReserveProdOrderComp.VerifyChange(Rec,xRec);
                                                                CALCFIELDS("Reserved Qty. (Base)");
                                                                TESTFIELD("Reserved Qty. (Base)",0);
                                                                TESTFIELD("Remaining Qty. (Base)","Expected Qty. (Base)");
                                                                UpdateUnitCost;
                                                                VALIDATE("Expected Quantity");
                                                                GetUpdateFromSKU;
                                                              END;

                                                   CaptionML=[ENU=Variant Code;
                                                              ESP=C¢d. variante] }
    { 25  ;   ;Expected Quantity   ;Decimal       ;OnValidate=BEGIN
                                                                IF Item.GET("Item No.") THEN
                                                                  IF Item."Rounding Precision" > 0 THEN
                                                                    "Expected Quantity" := ROUND("Expected Quantity",Item."Rounding Precision",'>');
                                                                "Expected Qty. (Base)" := "Expected Quantity" * "Qty. per Unit of Measure";
                                                                IF (Status IN [Status::Released,Status::Finished]) AND
                                                                   (xRec."Item No." <> '') AND
                                                                   ("Line No." <> 0)
                                                                THEN
                                                                  CALCFIELDS("Act. Consumption (Qty)");
                                                                "Remaining Quantity" := "Expected Quantity" - "Act. Consumption (Qty)" / "Qty. per Unit of Measure";
                                                                IF ("Remaining Quantity" * "Expected Quantity") < 0 THEN
                                                                  "Remaining Quantity" := 0;
                                                                "Remaining Qty. (Base)" := ROUND("Remaining Quantity" * "Qty. per Unit of Measure",0.00001);
                                                                "Completely Picked" := "Qty. Picked" >= "Expected Quantity";

                                                                ReserveProdOrderComp.VerifyQuantity(Rec,xRec);

                                                                "Cost Amount" := ROUND("Expected Quantity" * "Unit Cost");
                                                                "Overhead Amount" :=
                                                                  ROUND(
                                                                    "Expected Quantity" *
                                                                    (("Direct Unit Cost" * "Indirect Cost %" / 100) + "Overhead Rate"));
                                                                "Direct Cost Amount" := ROUND("Expected Quantity" * "Direct Unit Cost");
                                                              END;

                                                   CaptionML=[ENU=Expected Quantity;
                                                              ESP=Cantidad esperada];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 26  ;   ;Remaining Quantity  ;Decimal       ;CaptionML=[ENU=Remaining Quantity;
                                                              ESP=Cantidad pendiente];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 27  ;   ;Act. Consumption (Qty);Decimal     ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("Item Ledger Entry".Quantity WHERE (Entry Type=CONST(Consumption),
                                                                                                        Prod. Order No.=FIELD(Prod. Order No.),
                                                                                                        Prod. Order Line No.=FIELD(Prod. Order Line No.),
                                                                                                        Prod. Order Comp. Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=Act. Consumption (Qty);
                                                              ESP=Cantidad consumida];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 28  ;   ;Flushing Method     ;Option        ;OnValidate=BEGIN
                                                                IF ("Flushing Method" = "Flushing Method"::Backward) AND (Status = Status::Released) THEN BEGIN
                                                                  ItemLedgEntry.SETCURRENTKEY("Prod. Order No.","Prod. Order Line No.","Entry Type","Prod. Order Comp. Line No.");
                                                                  ItemLedgEntry.SETRANGE("Prod. Order No.","Prod. Order No.");
                                                                  ItemLedgEntry.SETRANGE("Prod. Order Line No.","Prod. Order Line No.");
                                                                  ItemLedgEntry.SETRANGE("Entry Type",ItemLedgEntry."Entry Type"::Consumption);
                                                                  ItemLedgEntry.SETRANGE("Prod. Order Comp. Line No.","Line No.");
                                                                  IF "Line No." = 0 THEN
                                                                    ItemLedgEntry.SETRANGE("Item No.","Item No.");
                                                                  IF ItemLedgEntry.FIND('-') THEN
                                                                    ERROR(
                                                                      Text99000002,
                                                                      FIELDCAPTION("Flushing Method"),
                                                                      "Flushing Method",
                                                                      ItemLedgEntry.TABLECAPTION);
                                                                END;

                                                                IF ("Flushing Method" <> xRec."Flushing Method") AND
                                                                   (xRec."Flushing Method" IN
                                                                     [xRec."Flushing Method"::Manual,
                                                                      xRec."Flushing Method"::"Pick + Forward",
                                                                      xRec."Flushing Method"::"Pick + Backward"])
                                                                THEN BEGIN
                                                                  CALCFIELDS("Pick Qty.");
                                                                  IF "Pick Qty." <> 0 THEN
                                                                    ERROR(
                                                                      Text99000007,
                                                                      FIELDCAPTION("Flushing Method"),
                                                                      "Flushing Method",
                                                                      TABLECAPTION,
                                                                      "Item No.");

                                                                  IF "Qty. Picked" <> 0 THEN
                                                                    ERROR(
                                                                      Text99000008,
                                                                      FIELDCAPTION("Flushing Method"),
                                                                      "Flushing Method",
                                                                      TABLECAPTION,
                                                                      "Item No.");

                                                                  IF (xRec."Flushing Method" IN
                                                                       [xRec."Flushing Method"::Manual,
                                                                        xRec."Flushing Method"::"Pick + Forward",
                                                                        xRec."Flushing Method"::"Pick + Backward"]) AND
                                                                     ("Flushing Method" IN ["Flushing Method"::Forward,"Flushing Method"::Backward])
                                                                  THEN BEGIN
                                                                    PickWkshLine.RESET;
                                                                    PickWkshLine.SETCURRENTKEY("Source Type","Source Subtype","Source No.","Source Line No.","Source Subline No.");
                                                                    PickWkshLine.SETRANGE("Source Type",DATABASE::"Prod. Order Component");
                                                                    PickWkshLine.SETRANGE("Source No.","Prod. Order No.");
                                                                    PickWkshLine.SETRANGE("Source Line No.","Prod. Order Line No.");
                                                                    PickWkshLine.SETRANGE("Source Subline No.","Line No.");
                                                                    IF PickWkshLine.FIND('-') THEN
                                                                      ERROR(
                                                                        Text99000002,
                                                                        FIELDCAPTION("Flushing Method"),
                                                                        "Flushing Method",
                                                                        PickWkshLine.TABLECAPTION);
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Flushing Method;
                                                              ESP=M‚todo de baja];
                                                   OptionCaptionML=[ENU=Manual,Forward,Backward,Pick + Forward,Pick + Backward;
                                                                    ESP=Manual,Adelante,Atr s,Pick + Adelante,Pick + Atr s];
                                                   OptionString=Manual,Forward,Backward,Pick + Forward,Pick + Backward }
    { 30  ;   ;Location Code       ;Code10        ;TableRelation=Location WHERE (Use As In-Transit=CONST(No));
                                                   OnValidate=BEGIN
                                                                IF Item."No." <> "Item No." THEN
                                                                  Item.GET("Item No.");
                                                                UpdateUnitCost;
                                                                VALIDATE("Expected Quantity");

                                                                GetDefaultBin;
                                                                WhseValidateSourceLine.ProdComponentVerifyChange(Rec,xRec);
                                                                ReserveProdOrderComp.VerifyChange(Rec,xRec);
                                                                GetUpdateFromSKU;
                                                              END;

                                                   CaptionML=[ENU=Location Code;
                                                              ESP=C¢d. almac‚n] }
    { 31  ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
                                                              END;

                                                   CaptionML=[ENU=Shortcut Dimension 1 Code;
                                                              ESP=C¢d. dim. acceso dir. 1];
                                                   CaptionClass='1,2,1' }
    { 32  ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");
                                                              END;

                                                   CaptionML=[ENU=Shortcut Dimension 2 Code;
                                                              ESP=C¢d. dim. acceso dir. 2];
                                                   CaptionClass='1,2,2' }
    { 33  ;   ;Bin Code            ;Code20        ;OnValidate=VAR
                                                                WMSManagement@1000 : Codeunit 7302;
                                                              BEGIN
                                                                IF "Bin Code" <> '' THEN BEGIN
                                                                  TESTFIELD("Location Code");
                                                                  GetLocation("Location Code");
                                                                  Location.TESTFIELD("Directed Put-away and Pick",FALSE);
                                                                  IF Quantity > 0 THEN
                                                                    WMSManagement.FindBinContent("Location Code","Bin Code","Item No.","Variant Code",'')
                                                                  ELSE
                                                                    WMSManagement.FindBin("Location Code","Bin Code",'');
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              WMSManagement@1000 : Codeunit 7302;
                                                              BinCode@1001 : Code[20];
                                                            BEGIN
                                                              IF Quantity > 0 THEN
                                                                BinCode := WMSManagement.BinContentLookUp("Location Code","Item No.","Variant Code",'',"Bin Code")
                                                              ELSE
                                                                BinCode := WMSManagement.BinLookUp("Location Code","Item No.","Variant Code",'');

                                                              IF BinCode <> '' THEN
                                                                VALIDATE("Bin Code",BinCode);
                                                            END;

                                                   CaptionML=[ENU=Bin Code;
                                                              ESP=C¢d. ubicaci¢n] }
    { 35  ;   ;Supplied-by Line No.;Integer       ;TableRelation="Prod. Order Line"."Line No." WHERE (Status=FIELD(Status),
                                                                                                      Prod. Order No.=FIELD(Prod. Order No.),
                                                                                                      Line No.=FIELD(Supplied-by Line No.));
                                                   CaptionML=[ENU=Supplied-by Line No.;
                                                              ESP=Suminis. por l¡nea N§] }
    { 36  ;   ;Planning Level Code ;Integer       ;CaptionML=[ENU=Planning Level Code;
                                                              ESP=C¢d. nivel planif.];
                                                   Editable=No }
    { 37  ;   ;Item Low-Level Code ;Integer       ;CaptionML=[ENU=Item Low-Level Code;
                                                              ESP=C¢d. nivel m s bajo prod.] }
    { 40  ;   ;Length              ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=[ENU=Length;
                                                              ESP=Longitud];
                                                   DecimalPlaces=0:5 }
    { 41  ;   ;Width               ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=[ENU=Width;
                                                              ESP=Ancho];
                                                   DecimalPlaces=0:5 }
    { 42  ;   ;Weight              ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=[ENU=Weight;
                                                              ESP=Peso];
                                                   DecimalPlaces=0:5 }
    { 43  ;   ;Depth               ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=[ENU=Depth;
                                                              ESP=Altura];
                                                   DecimalPlaces=0:5 }
    { 44  ;   ;Calculation Formula ;Option        ;OnValidate=BEGIN
                                                                CASE "Calculation Formula" OF
                                                                  "Calculation Formula"::" ":
                                                                    Quantity := "Quantity per";
                                                                  "Calculation Formula"::Length:
                                                                    Quantity := ROUND(Length * "Quantity per",0.00001);
                                                                  "Calculation Formula"::"Length * Width":
                                                                    Quantity := ROUND(Length * Width * "Quantity per",0.00001);
                                                                  "Calculation Formula"::"Length * Width * Depth":
                                                                    Quantity := ROUND(Length * Width * Depth * "Quantity per",0.00001);
                                                                  "Calculation Formula"::Weight:
                                                                    Quantity := ROUND(Weight * "Quantity per",0.00001);
                                                                END;
                                                                "Quantity (Base)" := Quantity * "Qty. per Unit of Measure";
                                                                VALIDATE("Expected Quantity",Quantity * ProdOrderNeeds);
                                                              END;

                                                   CaptionML=[ENU=Calculation Formula;
                                                              ESP=Tipo c lculo];
                                                   OptionCaptionML=[ENU=" ,Length,Length * Width,Length * Width * Depth,Weight";
                                                                    ESP=" ,Largo,Largo * Ancho,Largo * Ancho * Alto,Peso"];
                                                   OptionString=[ ,Length,Length * Width,Length * Width * Depth,Weight] }
    { 45  ;   ;Quantity per        ;Decimal       ;OnValidate=BEGIN
                                                                WhseValidateSourceLine.ProdComponentVerifyChange(Rec,xRec);
                                                                TESTFIELD("Item No.");
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=[ENU=Quantity per;
                                                              ESP=Cantidad por];
                                                   DecimalPlaces=0:5 }
    { 50  ;   ;Unit Cost           ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("Item No.");

                                                                Item.GET("Item No.");
                                                                GetGLSetup;
                                                                IF Item."Costing Method" = Item."Costing Method"::Standard THEN BEGIN
                                                                  IF CurrFieldNo = FIELDNO("Unit Cost") THEN
                                                                    ERROR(
                                                                      Text99000003,
                                                                      FIELDCAPTION("Unit Cost"),Item.FIELDCAPTION("Costing Method"),Item."Costing Method");
                                                                  UpdateUnitCost;
                                                                END;
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=[ENU=Unit Cost;
                                                              ESP=Coste unitario];
                                                   DecimalPlaces=2:5 }
    { 51  ;   ;Cost Amount         ;Decimal       ;CaptionML=[ENU=Cost Amount;
                                                              ESP=Importe coste];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 52  ;   ;Due Date            ;Date          ;OnValidate=VAR
                                                                CheckDateConflict@1000 : Codeunit 99000815;
                                                              BEGIN
                                                                WhseValidateSourceLine.ProdComponentVerifyChange(Rec,xRec);
                                                                IF NOT Blocked THEN
                                                                  CheckDateConflict.ProdOrderComponentCheck(Rec,CurrFieldNo <> 0);
                                                                UpdateDatetime;
                                                              END;

                                                   CaptionML=[ENU=Due Date;
                                                              ESP=Fecha vencimiento] }
    { 53  ;   ;Due Time            ;Time          ;OnValidate=BEGIN
                                                                UpdateDatetime;
                                                              END;

                                                   CaptionML=[ENU=Due Time;
                                                              ESP=Tiempo fin.] }
    { 60  ;   ;Qty. per Unit of Measure;Decimal   ;CaptionML=[ENU=Qty. per Unit of Measure;
                                                              ESP=Cdad. por unidad medida];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 61  ;   ;Remaining Qty. (Base);Decimal      ;CaptionML=[ENU=Remaining Qty. (Base);
                                                              ESP=Cdad. pendiente (base)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 62  ;   ;Quantity (Base)     ;Decimal       ;CaptionML=[ENU=Quantity (Base);
                                                              ESP=Cantidad (base)];
                                                   DecimalPlaces=0:5 }
    { 63  ;   ;Reserved Qty. (Base);Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("Reservation Entry"."Quantity (Base)" WHERE (Source ID=FIELD(Prod. Order No.),
                                                                                                                 Source Ref. No.=FIELD(Line No.),
                                                                                                                 Source Type=CONST(5407),
                                                                                                                 Source Subtype=FIELD(Status),
                                                                                                                 Source Batch Name=CONST(),
                                                                                                                 Source Prod. Order Line=FIELD(Prod. Order Line No.),
                                                                                                                 Reservation Status=CONST(Reservation)));
                                                   CaptionML=[ENU=Reserved Qty. (Base);
                                                              ESP=Cdad. reservada (base)];
                                                   DecimalPlaces=0:5;
                                                   Editable=Yes }
    { 71  ;   ;Reserved Quantity   ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("Reservation Entry".Quantity WHERE (Source ID=FIELD(Prod. Order No.),
                                                                                                        Source Ref. No.=FIELD(Line No.),
                                                                                                        Source Type=CONST(5407),
                                                                                                        Source Subtype=FIELD(Status),
                                                                                                        Source Batch Name=CONST(),
                                                                                                        Source Prod. Order Line=FIELD(Prod. Order Line No.),
                                                                                                        Reservation Status=CONST(Reservation)));
                                                   CaptionML=[ENU=Reserved Quantity;
                                                              ESP=Cantidad reservada];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 73  ;   ;Expected Qty. (Base);Decimal       ;OnValidate=BEGIN
                                                                IF Status <> Status::Simulated THEN BEGIN
                                                                  IF Status IN [Status::Released,Status::Finished] THEN
                                                                    CALCFIELDS("Act. Consumption (Qty)");
                                                                  "Remaining Quantity" := "Expected Quantity" - "Act. Consumption (Qty)";
                                                                  "Remaining Qty. (Base)" := ROUND("Remaining Quantity" * "Qty. per Unit of Measure",0.00001);
                                                                END;
                                                                "Cost Amount" := ROUND("Expected Quantity" * "Unit Cost");
                                                                "Overhead Amount" :=
                                                                  ROUND(
                                                                    "Expected Quantity" *
                                                                    (("Direct Unit Cost" * "Indirect Cost %" / 100) + "Overhead Rate"));
                                                                "Direct Cost Amount" := ROUND("Expected Quantity" * "Direct Unit Cost");
                                                              END;

                                                   CaptionML=[ENU=Expected Qty. (Base);
                                                              ESP=Cdad. esperada (base)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 76  ;   ;Due Date-Time       ;DateTime      ;OnValidate=BEGIN
                                                                "Due Date" := DT2DATE("Due Date-Time");
                                                                "Due Time" := DT2TIME("Due Date-Time");
                                                                VALIDATE("Due Date");
                                                              END;

                                                   CaptionML=[ENU=Due Date-Time;
                                                              ESP=Fecha vto.-Tiempo] }
    { 5702;   ;Substitution Available;Boolean     ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Item Substitution" WHERE (Type=CONST(Item),
                                                                                                Substitute Type=CONST(Item),
                                                                                                No.=FIELD(Item No.),
                                                                                                Variant Code=FIELD(Variant Code)));
                                                   CaptionML=[ENU=Substitution Available;
                                                              ESP=Disponible sustituci¢n];
                                                   Editable=No }
    { 5703;   ;Original Item No.   ;Code20        ;TableRelation=Item;
                                                   CaptionML=[ENU=Original Item No.;
                                                              ESP=N§ de producto original];
                                                   Editable=No }
    { 5704;   ;Original Variant Code;Code10       ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Original Item No.));
                                                   CaptionML=[ENU=Original Variant Code;
                                                              ESP=C¢digo variante original];
                                                   Editable=No }
    { 5750;   ;Pick Qty.           ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Warehouse Activity Line"."Qty. Outstanding" WHERE (Activity Type=FILTER(<>Put-away),
                                                                                                                       Source Type=CONST(5407),
                                                                                                                       Source Subtype=FIELD(Status),
                                                                                                                       Source No.=FIELD(Prod. Order No.),
                                                                                                                       Source Line No.=FIELD(Prod. Order Line No.),
                                                                                                                       Source Subline No.=FIELD(Line No.),
                                                                                                                       Unit of Measure Code=FIELD(Unit of Measure Code),
                                                                                                                       Action Type=FILTER(' '|Place),
                                                                                                                       Original Breakbulk=CONST(No),
                                                                                                                       Breakbulk No.=CONST(0)));
                                                   CaptionML=[ENU=Pick Qty.;
                                                              ESP=Cdad. picking];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 7300;   ;Qty. Picked         ;Decimal       ;OnValidate=BEGIN
                                                                "Qty. Picked (Base)" := CalcBaseQty("Qty. Picked");
                                                                "Completely Picked" := "Qty. Picked" >= "Expected Quantity";
                                                              END;

                                                   CaptionML=[ENU=Qty. Picked;
                                                              ESP=Cdad. preparada pedido];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 7301;   ;Qty. Picked (Base)  ;Decimal       ;CaptionML=[ENU=Qty. Picked (Base);
                                                              ESP=Cdad. preparada pedido (base)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 7302;   ;Completely Picked   ;Boolean       ;CaptionML=[ENU=Completely Picked;
                                                              ESP=Picking completo];
                                                   Editable=No }
    { 7303;   ;Pick Qty. (Base)    ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Warehouse Activity Line"."Qty. Outstanding (Base)" WHERE (Activity Type=FILTER(<>Put-away),
                                                                                                                              Source Type=CONST(5407),
                                                                                                                              Source Subtype=FIELD(Status),
                                                                                                                              Source No.=FIELD(Prod. Order No.),
                                                                                                                              Source Line No.=FIELD(Prod. Order Line No.),
                                                                                                                              Source Subline No.=FIELD(Line No.),
                                                                                                                              Action Type=FILTER(' '|Place),
                                                                                                                              Original Breakbulk=CONST(No),
                                                                                                                              Breakbulk No.=CONST(0)));
                                                   CaptionML=[ENU=Pick Qty. (Base);
                                                              ESP=Cdad. picking (Base)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 99000754;;Direct Unit Cost   ;Decimal       ;CaptionML=[ENU=Direct Unit Cost;
                                                              ESP=Coste unit. directo];
                                                   DecimalPlaces=2:5 }
    { 99000755;;Indirect Cost %    ;Decimal       ;OnValidate=BEGIN
                                                                "Direct Unit Cost" :=
                                                                  ROUND(("Unit Cost" - "Overhead Rate") / (1 + "Indirect Cost %" / 100));

                                                                VALIDATE("Unit Cost");
                                                              END;

                                                   CaptionML=[ENU=Indirect Cost %;
                                                              ESP=% Coste indirecto];
                                                   DecimalPlaces=0:5 }
    { 99000756;;Overhead Rate      ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Indirect Cost %");
                                                              END;

                                                   CaptionML=[ENU=Overhead Rate;
                                                              ESP=Tasa costes generales];
                                                   DecimalPlaces=0:5 }
    { 99000757;;Direct Cost Amount ;Decimal       ;CaptionML=[ENU=Direct Cost Amount;
                                                              ESP=Imp. coste unitario];
                                                   DecimalPlaces=2:2 }
    { 99000758;;Overhead Amount    ;Decimal       ;CaptionML=[ENU=Overhead Amount;
                                                              ESP=Imp. costes gen.];
                                                   DecimalPlaces=2:2 }
  }
  KEYS
  {
    {    ;Status,Prod. Order No.,Prod. Order Line No.,Line No.;
                                                   Clustered=Yes }
    {    ;Prod. Order No.,Prod. Order Line No.,Line No.,Status }
    {    ;Status,Prod. Order No.,Prod. Order Line No.,Due Date;
                                                   SumIndexFields=Expected Quantity,Cost Amount;
                                                   SIFTLevelsToMaintain=[{Status,Prod. Order No.,Prod. Order Line No.}] }
    {    ;Status,Prod. Order No.,Prod. Order Line No.,Item No.,Line No.;
                                                   MaintainSQLIndex=No }
    {    ;Status,Item No.,Variant Code,Location Code,Due Date;
                                                   SumIndexFields=Expected Quantity,Remaining Qty. (Base),Cost Amount,Overhead Amount;
                                                   MaintainSIFTIndex=No }
    { No ;Item No.,Variant Code,Location Code,Shortcut Dimension 1 Code,Shortcut Dimension 2 Code,Due Date;
                                                   SumIndexFields=Expected Quantity,Remaining Qty. (Base),Cost Amount,Overhead Amount;
                                                   KeyGroups=Item(Dim) }
    {    ;Status,Prod. Order No.,Routing Link Code,Flushing Method }
    {    ;Status,Prod. Order No.,Location Code     }
    {    ;Item No.,Variant Code,Location Code,Status,Due Date;
                                                   SumIndexFields=Expected Quantity,Remaining Qty. (Base),Cost Amount,Overhead Amount,Qty. Picked (Base);
                                                   MaintainSIFTIndex=No }
    {    ;Status,Prod. Order No.,Prod. Order Line No.,Item Low-Level Code;
                                                   MaintainSQLIndex=No }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1028 : TextConst 'ENU=A %1 %2 cannot be inserted, modified, or deleted.;ESP=Un %1 %2 no se puede insertar, modificar o eliminar.';
      Text99000000@1000 : TextConst 'ENU=You cannot delete %1 %2, because there exists at least one %3 associated with it.;ESP=No puede borrar %1 %2, porque existe al menos %3 asociado a ‚l.';
      Text99000001@1001 : TextConst 'ENU=You cannot rename a %1.;ESP=No se puede cambiar el nombre a %1.';
      Text99000002@1002 : TextConst 'ENU=You cannot change %1 to %2, when there is at least one %3 associated with it.;ESP=No puede cambiar %1 a %2 si existe por lo menos un %3 asociado con ‚l.';
      Text99000003@1003 : TextConst 'ENU=You cannot change %1 when %2 is %3.;ESP=No se puede cambiar %1 cuando %2 es %3.';
      Text99000004@1004 : TextConst 'ENU=Change %1 from %2 to %3?;ESP=¨Desea cambiar %1 de %2 a %3?';
      Item@1005 : Record 27;
      ItemLedgEntry@1006 : Record 32;
      ReservEntry@1007 : Record 337;
      PickWkshLine@1021 : Record 7326;
      GLSetup@1024 : Record 98;
      Location@1012 : Record 14;
      SKU@1025 : Record 5700;
      ReservMgt@1008 : Codeunit 99000845;
      ReservEngineMgt@1009 : Codeunit 99000831;
      ReserveProdOrderComp@1010 : Codeunit 99000838;
      UOMMgt@1011 : Codeunit 5402;
      DimMgt@1014 : Codeunit 408;
      WhseProdRelease@1016 : Codeunit 5774;
      WhseValidateSourceLine@1027 : Codeunit 5777;
      ItemSubstitutionMgt@1031 : Codeunit 5701;
      Reservation@1017 : Form 498;
      ItemAvailByDate@1018 : Form 157;
      ItemAvailByVar@1019 : Form 5414;
      ItemAvailByLoc@1020 : Form 492;
      Blocked@1023 : Boolean;
      GLSetupRead@1022 : Boolean;
      Text99000007@1029 : TextConst 'ENU=You cannot change %1 to %2 because a pick has already been created for %3 %4.;ESP=No puede cambiar %1 a %2 por que se ha creado ya un picking para %3 %4.';
      Text99000008@1030 : TextConst 'ENU=You cannot change %1 to %2 because %3 %4 has already been picked.;ESP=No puede cambiar %1 a %2 por que ya se ha realizado el picking de %3 %4.';
      Text99000009@1013 : TextConst 'ENU=Automatic reservation is not possible.\Reserve items manually?;ESP=No se puede reservar autom ticamente.\¨Desea reservar los productos manualmente?';

    PROCEDURE Caption@4() : Text[100];
    VAR
      ProdOrder@1000 : Record 5405;
      ProdOrderLine@1001 : Record 5406;
    BEGIN
      IF NOT ProdOrder.GET(Status,"Prod. Order No.") THEN
        EXIT('');

      IF NOT ProdOrderLine.GET(Status,"Prod. Order No.","Prod. Order Line No.") THEN
        CLEAR(ProdOrderLine);

      EXIT(
        STRSUBSTNO('%1 %2 %3',
          "Prod. Order No.",ProdOrder.Description,ProdOrderLine."Item No."));
    END;

    PROCEDURE ProdOrderNeeds@1() : Decimal;
    VAR
      ProdOrderLine@1000 : Record 5406;
      ProdOrderRtngLine@1001 : Record 5409;
    BEGIN
      ProdOrderLine.GET(Status,"Prod. Order No.","Prod. Order Line No.");

      "Due Date" := ProdOrderLine."Starting Date";

      ProdOrderRtngLine.RESET;
      ProdOrderRtngLine.SETRANGE(Status,Status);
      ProdOrderRtngLine.SETRANGE("Prod. Order No.","Prod. Order No.");
      ProdOrderRtngLine.SETRANGE("Routing Reference No.",ProdOrderLine."Routing Reference No.");
      IF "Routing Link Code" <> '' THEN
        ProdOrderRtngLine.SETRANGE("Routing Link Code","Routing Link Code");
      IF ProdOrderRtngLine.FIND('-') THEN BEGIN
        EXIT(
          ProdOrderLine.Quantity *
          (1 + ProdOrderLine."Scrap %" / 100) *
          (1 + ProdOrderRtngLine."Scrap Factor % (Accumulated)") *
          (1 + "Scrap %" / 100) +
          ProdOrderRtngLine."Fixed Scrap Qty. (Accum.)");
      END;

      EXIT(
        ProdOrderLine.Quantity *
        (1 + ProdOrderLine."Scrap %" / 100) * (1 + "Scrap %" / 100));
    END;

    PROCEDURE GetNeededQty@3(CalcBasedOn@1000 : 'Actual Output,Expected Output';IncludePreviousPosting@1001 : Boolean) : Decimal;
    VAR
      ProdOrderLine@1002 : Record 5406;
      ProdOrderRtngLine@1003 : Record 5409;
      CapLedgEntry@1004 : Record 5832;
      CostCalcMgt@1006 : Codeunit 5836;
      OutputQtyBase@1005 : Decimal;
      CompQtyBase@1008 : Decimal;
    BEGIN
      Item.GET("Item No.");
      IF Item."Rounding Precision" = 0 THEN
        Item."Rounding Precision" := 0.00001;

      IF CalcBasedOn = CalcBasedOn::"Actual Output" THEN BEGIN
        ProdOrderLine.GET(Status,"Prod. Order No.","Prod. Order Line No.");

        ProdOrderRtngLine.SETRANGE(Status,Status);
        ProdOrderRtngLine.SETRANGE("Prod. Order No.","Prod. Order No.");
        ProdOrderRtngLine.SETRANGE("Routing No.",ProdOrderLine."Routing No.");
        ProdOrderRtngLine.SETRANGE("Routing Reference No.",ProdOrderLine."Routing Reference No.");
        ProdOrderRtngLine.SETRANGE("Routing Link Code","Routing Link Code");
        IF NOT ProdOrderRtngLine.FIND('-') OR ("Routing Link Code" = '') THEN BEGIN
          ProdOrderRtngLine.SETRANGE("Routing Link Code");
          ProdOrderRtngLine.SETFILTER("Next Operation No.",'%1','');
          IF NOT ProdOrderRtngLine.FIND('-') THEN
            ProdOrderRtngLine."Operation No." := '';
        END;
        IF Status IN [Status::Released,Status::Finished] THEN BEGIN
          CapLedgEntry.SETCURRENTKEY("Prod. Order No.","Prod. Order Line No.");
          CapLedgEntry.SETRANGE("Prod. Order No.","Prod. Order No.");
          CapLedgEntry.SETRANGE("Prod. Order Line No.","Prod. Order Line No.");
          CapLedgEntry.SETRANGE("Operation No.",ProdOrderRtngLine."Operation No.");
          IF CapLedgEntry.FIND('-') THEN
            REPEAT
              OutputQtyBase := OutputQtyBase + CapLedgEntry."Output Quantity" + CapLedgEntry."Scrap Quantity";
            UNTIL CapLedgEntry.NEXT = 0;
        END;

        CompQtyBase := CostCalcMgt.CalcActNeededQtyBase(ProdOrderLine,Rec,OutputQtyBase);

        IF IncludePreviousPosting THEN BEGIN
          IF Status IN [Status::Released,Status::Finished] THEN
            CALCFIELDS("Act. Consumption (Qty)");
          EXIT(
            ROUND(
              (CompQtyBase - "Act. Consumption (Qty)") / "Qty. per Unit of Measure",
              Item."Rounding Precision",'>'))
        END ELSE
          EXIT(ROUND(CompQtyBase / "Qty. per Unit of Measure",Item."Rounding Precision",'>'));
      END ELSE
        EXIT(ROUND("Remaining Quantity",Item."Rounding Precision"));
    END;

    PROCEDURE ShowReservation@8();
    BEGIN
      TESTFIELD("Item No.");
      CLEAR(Reservation);
      Reservation.SetProdOrderComponent(Rec);
      Reservation.RUNMODAL;
    END;

    PROCEDURE ShowReservationEntries@21(Modal@1000 : Boolean);
    BEGIN
      TESTFIELD("Item No.");
      ReservEngineMgt.InitFilterAndSortingLookupFor(ReservEntry,TRUE);
      ReserveProdOrderComp.FilterReservFor(ReservEntry,Rec);
      IF Modal THEN
        FORM.RUNMODAL(FORM::"Reservation Entries",ReservEntry)
      ELSE
        FORM.RUN(FORM::"Reservation Entries",ReservEntry);
    END;

    PROCEDURE ItemAvailability@22(AvailabilityType@1000 : 'Date,Variant,Location,Bin');
    BEGIN
      TESTFIELD("Item No.");
      Item.RESET;
      Item.GET("Item No.");
      Item.SETRANGE("No.","Item No.");
      Item.SETRANGE("Date Filter",0D,"Due Date");

      CASE AvailabilityType OF
        AvailabilityType::Date:
          BEGIN
            Item.SETRANGE("Variant Filter","Variant Code");
            Item.SETRANGE("Location Filter","Location Code");
            CLEAR(ItemAvailByDate);
            ItemAvailByDate.LOOKUPMODE(TRUE);
            ItemAvailByDate.SETRECORD(Item);
            ItemAvailByDate.SETTABLEVIEW(Item);
            IF ItemAvailByDate.RUNMODAL = ACTION::LookupOK THEN
              IF "Due Date" <> ItemAvailByDate.GetLastDate THEN
                IF CONFIRM(
                     Text99000004,TRUE,FIELDCAPTION("Due Date"),
                     "Due Date",ItemAvailByDate.GetLastDate)
                THEN BEGIN
                  VALIDATE("Due Date",ItemAvailByDate.GetLastDate);
                  MODIFY;
                END;
          END;
        AvailabilityType::Variant:
          BEGIN
            CLEAR(ItemAvailByVar);
            ItemAvailByVar.LOOKUPMODE(TRUE);
            ItemAvailByVar.SETRECORD(Item);
            ItemAvailByVar.SETTABLEVIEW(Item);
            IF ItemAvailByVar.RUNMODAL = ACTION::LookupOK THEN
              IF "Variant Code" <> ItemAvailByVar.GetLastVariant THEN
                IF CONFIRM(
                     Text99000004,TRUE,FIELDCAPTION("Variant Code"),"Variant Code",
                     ItemAvailByVar.GetLastVariant)
                THEN BEGIN
                  VALIDATE("Variant Code",ItemAvailByVar.GetLastVariant);
                  MODIFY;
                END;
          END;
        AvailabilityType::Location:
          BEGIN
            CLEAR(ItemAvailByLoc);
            ItemAvailByLoc.LOOKUPMODE(TRUE);
            ItemAvailByLoc.SETRECORD(Item);
            ItemAvailByLoc.SETTABLEVIEW(Item);
            IF ItemAvailByLoc.RUNMODAL = ACTION::LookupOK THEN
              IF "Location Code" <> ItemAvailByLoc.GetLastLocation THEN
                IF CONFIRM(
                     Text99000004,TRUE,FIELDCAPTION("Location Code"),"Location Code",
                     ItemAvailByLoc.GetLastLocation)
                THEN BEGIN
                  VALIDATE("Location Code",ItemAvailByLoc.GetLastLocation);
                  MODIFY;
                END;
          END;
      END;
    END;

    LOCAL PROCEDURE CalcBaseQty@14(Qty@1000 : Decimal) : Decimal;
    BEGIN
      TESTFIELD("Qty. per Unit of Measure");
      EXIT(ROUND(Qty * "Qty. per Unit of Measure",0.00001));
    END;

    PROCEDURE BlockDynamicTracking@17(SetBlock@1000 : Boolean);
    BEGIN
      Blocked := SetBlock;
      ReserveProdOrderComp.Block(Blocked);
    END;

    PROCEDURE CreateDim@5(Type1@1000 : Integer;No1@1001 : Code[20]);
    VAR
      TableID@1002 : ARRAY [10] OF Integer;
      No@1003 : ARRAY [10] OF Code[20];
    BEGIN
      TableID[1] := Type1;
      No[1] := No1;
      "Shortcut Dimension 1 Code" := '';
      "Shortcut Dimension 2 Code" := '';
      DimMgt.GetPreviousProdDocDefaultDim(
        DATABASE::"Prod. Order Line",Status,"Prod. Order No.","Prod. Order Line No.",0,
        "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
      DimMgt.GetDefaultDim(
        TableID,No,'',
        "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
      IF "Line No." <> 0 THEN
        DimMgt.UpdateProdDocDefaultDim(
          DATABASE::"Prod. Order Component",
          Status,"Prod. Order No.","Prod. Order Line No.","Line No.",
          "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
    END;

    PROCEDURE OpenItemTrackingLines@6500();
    BEGIN
      ReserveProdOrderComp.CallItemTracking(Rec);
    END;

    PROCEDURE ValidateShortcutDimCode@6(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateDimValueCode(FieldNumber,ShortcutDimCode);
      IF "Line No." <> 0 THEN BEGIN
        DimMgt.SaveProdDocDim(
          DATABASE::"Prod. Order Component",Status,"Prod. Order No.",
          "Prod. Order Line No.","Line No.",FieldNumber,ShortcutDimCode);
        MODIFY;
      END ELSE
        DimMgt.SaveTempDim(FieldNumber,ShortcutDimCode);
    END;

    PROCEDURE LookupShortcutDimCode@7(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.LookupDimValueCode(FieldNumber,ShortcutDimCode);
      IF "Line No." <> 0 THEN BEGIN
        DimMgt.SaveProdDocDim(
          DATABASE::"Prod. Order Component",Status,"Prod. Order No.",
          "Prod. Order Line No.","Line No.",FieldNumber,ShortcutDimCode);
        MODIFY;
      END ELSE
        DimMgt.SaveTempDim(FieldNumber,ShortcutDimCode);
    END;

    PROCEDURE ShowShortcutDimCode@9(VAR ShortcutDimCode@1000 : ARRAY [8] OF Code[20]);
    BEGIN
      IF "Line No." <> 0 THEN
        DimMgt.ShowProdDocDim(
          DATABASE::"Prod. Order Component",Status,"Prod. Order No.",
          "Prod. Order Line No.","Line No.",ShortcutDimCode)
      ELSE
        DimMgt.ShowTempDim(ShortcutDimCode);
    END;

    PROCEDURE GetUpdateFromSKU@10();
    VAR
      SKU@1000 : Record 5700;
      GetPlanningParameters@1001 : Codeunit 99000855;
    BEGIN
      GetPlanningParameters.AtSKU(SKU,"Item No.","Variant Code","Location Code");
      VALIDATE("Flushing Method",SKU."Flushing Method");
    END;

    PROCEDURE UpdateDatetime@20();
    BEGIN
      "Due Date-Time" := CREATEDATETIME("Due Date","Due Time");
    END;

    LOCAL PROCEDURE GetGLSetup@11();
    BEGIN
      IF NOT GLSetupRead THEN
        GLSetup.GET;
      GLSetupRead := TRUE;
    END;

    PROCEDURE RowID1@44() : Text[250];
    VAR
      ItemTrackingMgt@1000 : Codeunit 6500;
    BEGIN
      EXIT(
        ItemTrackingMgt.ComposeRowID(DATABASE::"Prod. Order Component",Status,
          "Prod. Order No.",'',"Prod. Order Line No.","Line No."));
    END;

    LOCAL PROCEDURE GetLocation@7300(LocationCode@1000 : Code[10]);
    BEGIN
      IF LocationCode = '' THEN
        CLEAR(Location)
      ELSE
        IF Location.Code <> LocationCode THEN
          Location.GET(LocationCode);
    END;

    PROCEDURE GetDefaultBin@50();
    VAR
      WMSManagement@1000 : Codeunit 7302;
    BEGIN
      IF (Quantity * xRec.Quantity > 0) AND
         ("Item No." = xRec."Item No.") AND
         ("Location Code" = xRec."Location Code") AND
         ("Variant Code" = xRec."Variant Code")
      THEN
        EXIT;

      "Bin Code" := '';
      IF ("Location Code" <> '') AND ("Item No." <> '') THEN BEGIN
        GetLocation("Location Code");
        IF Location."Bin Mandatory" AND NOT Location."Directed Put-away and Pick" THEN
          WMSManagement.GetDefaultBin("Item No.","Variant Code","Location Code","Bin Code");
      END;
    END;

    PROCEDURE AutoReserve@2();
    VAR
      Item@1001 : Record 27;
      FullAutoReservation@1000 : Boolean;
    BEGIN
      IF Status IN [Status::Simulated,Status::Finished] THEN
        EXIT;

      TESTFIELD("Item No.");
      Item.GET("Item No.");
      IF Item.Reserve <> Item.Reserve::Always THEN
        EXIT;

      IF "Remaining Qty. (Base)" <> 0 THEN BEGIN
        TESTFIELD("Due Date");
        ReservMgt.SetProdOrderComponent(Rec);
        ReservMgt.AutoReserve(FullAutoReservation,'',"Due Date","Remaining Qty. (Base)");
        CALCFIELDS("Reserved Quantity","Reserved Qty. (Base)");
        FIND;
        IF NOT FullAutoReservation AND
           (CurrFieldNo <> 0) THEN BEGIN
          IF CONFIRM(Text99000009,TRUE) THEN BEGIN
            COMMIT;
            ShowReservation;
            FIND;
          END;
        END;
      END;
    END;

    PROCEDURE ShowItemSub@30();
    BEGIN
      ItemSubstitutionMgt.GetCompSubst(Rec);
    END;

    LOCAL PROCEDURE GetSKU@5802() : Boolean;
    BEGIN
      IF (SKU."Location Code" = "Location Code") AND
         (SKU."Item No." = "Item No.") AND
         (SKU."Variant Code" = "Variant Code")
      THEN
        EXIT(TRUE);
      IF SKU.GET("Location Code","Item No.","Variant Code") THEN
        EXIT(TRUE);

      EXIT(FALSE);
    END;

    LOCAL PROCEDURE ClearCalcFormula@13();
    BEGIN
      "Calculation Formula" := "Calculation Formula"::" ";
      Length := 0;
      Width := 0;
      Weight := 0;
      Depth := 0;
    END;

    LOCAL PROCEDURE UpdateUnitCost@12();
    BEGIN
      IF GetSKU THEN
        "Unit Cost" := SKU."Unit Cost"
      ELSE
        "Unit Cost" := Item."Unit Cost";

      "Unit Cost" :=
        ROUND("Unit Cost" * "Qty. per Unit of Measure",
          GLSetup."Unit-Amount Rounding Precision");

      "Indirect Cost %" := ROUND(Item."Indirect Cost %",0.00001);

      "Overhead Rate" :=
        ROUND(Item."Overhead Rate" * "Qty. per Unit of Measure",
          GLSetup."Unit-Amount Rounding Precision");

      "Direct Unit Cost" :=
        ROUND(
          ("Unit Cost" - "Overhead Rate") / (1 + "Indirect Cost %" / 100),
          GLSetup."Unit-Amount Rounding Precision");
    END;

    BEGIN
    END.
  }
}
