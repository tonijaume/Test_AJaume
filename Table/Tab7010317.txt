OBJECT Table 7010317 TRF-Historico precio
{
  OBJECT-PROPERTIES
  {
    Date=16/10/09;
    Time=12:30:40;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    OnInsert=VAR
               rHist@1100244000 : Record 7010317;
             BEGIN
               rHist.RESET; rHist.INIT;
               rHist.SETRANGE(Garaje          , Garaje);
               rHist.SETRANGE(Contrato        , Contrato);
               rHist.SETRANGE("Tipo Contrato" , "Tipo Contrato");
               rHist.SETRANGE("Tipo Precio"   , "Tipo Precio");
               rHist.SETRANGE("Orden Precio"  , "Orden Precio");
               rHist.ASCENDING(FALSE);
               IF rHist.FINDFIRST THEN
                 Orden := rHist.Orden + 100
               ELSE
                 Orden := 100;
               Fecha   := TODAY;
               Hora    := TIME;
               Usuario := USERID;
             END;

    OnDelete=BEGIN
               ERROR(err1);
             END;

    CaptionML=ESP=Historico precio;
  }
  FIELDS
  {
    { 1   ;   ;Garaje              ;Code10        ;CaptionML=ESP=Garaje;
                                                   Description=PK;
                                                   Editable=No }
    { 3   ;   ;Contrato            ;Code10        ;CaptionML=ESP=Contrato;
                                                   Description=PK;
                                                   Editable=No }
    { 5   ;   ;Tipo Contrato       ;Option        ;CaptionML=ESP=Tipo Contrato;
                                                   OptionCaptionML=ESP=Compra,Venta;
                                                   OptionString=Compra,Venta;
                                                   Description=PK }
    { 7   ;   ;Tipo Precio         ;Option        ;CaptionML=ESP=Tipo Precio;
                                                   OptionCaptionML=ESP=Precio Base,Suplemento,Suplemento zona;
                                                   OptionString=Precio Base,Suplemento,Suplemento zona;
                                                   Description=PK;
                                                   Editable=No }
    { 9   ;   ;Orden Precio        ;Integer       ;CaptionML=ESP=Orden Precio;
                                                   Description=PK;
                                                   Editable=No }
    { 11  ;   ;Orden               ;Integer       ;CaptionML=ESP=Orden;
                                                   Description=PK;
                                                   Editable=No }
    { 13  ;   ;Campo               ;Text30        ;CaptionML=ESP=Campo;
                                                   Editable=No }
    { 15  ;   ;Valor anterior      ;Text50        ;CaptionML=ESP=Valor anterior;
                                                   Editable=No }
    { 17  ;   ;Valor posterior     ;Text50        ;CaptionML=ESP=Valor posterior;
                                                   Editable=No }
    { 19  ;   ;Fecha               ;Date          ;CaptionML=ESP=Fecha;
                                                   Editable=No }
    { 21  ;   ;Hora                ;Time          ;CaptionML=ESP=Hora;
                                                   Editable=No }
    { 23  ;   ;Usuario             ;Code20        ;CaptionML=ESP=Usuario;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Garaje,Contrato,Tipo Contrato,Tipo Precio,Orden Precio,Orden;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      err1@1100244000 : TextConst 'ESP=No se pueden borrar registros en esta tabla';

    PROCEDURE inserta_registro@2(par_garaje@1100244000 : Code[10];par_contrato@1100244001 : Code[10];par_tipocon@1100244002 : 'Compra,Venta';par_tipo@1100244003 : 'Precio Base,Suplemento,Suplemento zona';par_orden@1100244004 : Integer;par_campo@1100244005 : Text[30];par_anterior@1100244006 : Text[250];par_posterior@1100244007 : Text[250]);
    VAR
      rHist@1100244008 : Record 7010317;
    BEGIN
      // inserta_registro
      //
      // AJS --> Encargado de rellenar los datos comunes de una entrada en el historico.
      //         Parametros:
      //           - Garaje, Contrato, Tipo (Precio Base,Suplemento,Suplemento zona), Orden, Campo,
      //             Valor anterior, Valor posterior.
      //
      rHist.RESET;
      rHist.INIT;
      rHist.Garaje            := par_garaje;
      rHist.Contrato          := par_contrato;
      rHist."Tipo Contrato"   := par_tipocon;
      rHist."Tipo Precio"     := par_tipo;
      rHist."Orden Precio"    := par_orden;
      rHist.INSERT(TRUE);
      rHist.Campo             := par_campo;
      rHist."Valor anterior"  := par_anterior;
      rHist."Valor posterior" := par_posterior;
      rHist.MODIFY;
    END;

    BEGIN
    END.
  }
}
