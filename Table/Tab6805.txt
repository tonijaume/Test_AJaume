OBJECT Table 6805 EP Web Part Request
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               EPFunctions@1000 : Codeunit 6800;
             BEGIN
               IF "Group Code" <> '' THEN
                 EPFunctions.InsertGroupReqTypeTabs(Rec);
             END;

    OnModify=BEGIN
               IF "Defined by System" THEN
                 ERROR(Text004,TABLECAPTION);
             END;

    OnDelete=VAR
               EPGrpReqTypeTables@1000000000 : Record 6811;
               EPSearchConfig@1000 : Record 6839;
             BEGIN
               IF "Defined by System" THEN
                 ERROR(Text005,TABLECAPTION);

               EPGrpReqTypeTables.SETRANGE("Group Code","Group Code");
               EPGrpReqTypeTables.SETRANGE("WP Request Code",Code);
               IF EPGrpReqTypeTables.FIND('-') THEN
                 REPEAT
                   EPGrpReqTypeTables.DELETE(TRUE);
                 UNTIL EPGrpReqTypeTables.NEXT = 0;

               EPSearchConfig.SETCURRENTKEY("Group Code","WP Request Code");
               EPSearchConfig.SETRANGE("Group Code","Group Code");
               EPSearchConfig.SETRANGE("WP Request Code",Code);
               IF EPSearchConfig.FIND('-') THEN
                 REPEAT
                   EPSearchConfig.DELETE(TRUE);
                 UNTIL EPSearchConfig.NEXT = 0;
             END;

    OnRename=BEGIN
               IF "Defined by System" THEN
                 ERROR(Text006,TABLECAPTION);
             END;

    CaptionML=[ENU=EP Web Part Request;
               ESP=Solicitud elemento Web EP];
    LookupFormID=Form6845;
  }
  FIELDS
  {
    { 1   ;   ;Group Code          ;Code20        ;TableRelation="EP Group".Code;
                                                   CaptionML=[ENU=Group Code;
                                                              ESP=C¢digo grupo] }
    { 2   ;   ;Code                ;Code20        ;OnValidate=VAR
                                                                EPWebPartRequest@1000 : Record 6805;
                                                              BEGIN
                                                                IF "Group Code" <> '' THEN
                                                                  IF NOT EPWebPartRequest.GET('',Code) THEN
                                                                    ERROR(Text003,FIELDCAPTION(Code),Code);
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              ESP=C¢digo];
                                                   NotBlank=Yes }
    { 4   ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n];
                                                   Editable=Yes }
    { 5   ;   ;Editable            ;Boolean       ;OnValidate=BEGIN
                                                                IF Editable THEN
                                                                  IF (Type = Type::List) OR (Type = Type::System) THEN
                                                                    ERROR(Text007,TABLECAPTION,FIELDCAPTION(Type),Type)
                                                              END;

                                                   CaptionML=[ENU=Editable;
                                                              ESP=Editable] }
    { 8   ;   ;Type                ;Option        ;OnValidate=VAR
                                                                EPWPRTable@1000 : Record 6811;
                                                              BEGIN
                                                                EPWPRTable.SETRANGE("Group Code",Rec."Group Code");
                                                                EPWPRTable.SETRANGE("WP Request Code",Rec.Code);
                                                                CASE Type OF
                                                                  Type::List:
                                                                    BEGIN
                                                                      TESTFIELD(Editable,FALSE);
                                                                      EPWPRTable.SETRANGE("Header or Line",EPWPRTable."Header or Line"::Header);
                                                                      IF EPWPRTable.FIND('-') THEN BEGIN
                                                                        EPWPRTable."Header or Line" := EPWPRTable."Header or Line"::Line;
                                                                        ERROR(Text001,EPWPRTable."Header or Line",
                                                                          TABLECAPTION,FIELDCAPTION(Type),Type);
                                                                      END;
                                                                    END;
                                                                  Type::Card:
                                                                    BEGIN
                                                                      EPWPRTable.SETRANGE("Header or Line",EPWPRTable."Header or Line"::Line);
                                                                      IF EPWPRTable.FIND('-') THEN BEGIN
                                                                        EPWPRTable."Header or Line" := EPWPRTable."Header or Line"::Header;
                                                                        ERROR(Text001,EPWPRTable."Header or Line",
                                                                          TABLECAPTION,FIELDCAPTION(Type),Type);
                                                                      END;
                                                                    END;
                                                                  Type::System:
                                                                    BEGIN
                                                                      TESTFIELD(Editable,FALSE);
                                                                      IF EPWPRTable.FIND('-') THEN
                                                                        ERROR(Text002,TABLECAPTION,FIELDCAPTION(Type),Type);
                                                                    END;
                                                                END
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              ESP=Tipo];
                                                   OptionCaptionML=[ENU=" ,List,Card,Header + Line,System";
                                                                    ESP=" ,Lista,Ficha,Cabecera + L¡nea,Sistema"];
                                                   OptionString=[ ,List,Card,Header + Line,System] }
    { 9   ;   ;Defined by System   ;Boolean       ;CaptionML=[ENU=Defined by System;
                                                              ESP=Definido por sistema];
                                                   Editable=No }
    { 10  ;   ;No. of Where-Used Groups;Integer   ;FieldClass=FlowField;
                                                   CalcFormula=Count("EP Web Part Request" WHERE (Group Code=FILTER(<>''),
                                                                                                  Code=FIELD(Code)));
                                                   CaptionML=[ENU=No. of Where-Used Groups;
                                                              ESP=N§ grupos utilizados];
                                                   Editable=No }
    { 12  ;   ;Caption ID          ;Integer       ;CaptionML=[ENU=Caption ID;
                                                              ESP=Id. t¡tulo];
                                                   Editable=No }
    { 13  ;   ;Caption             ;Text80        ;CaptionML=[ENU=Caption;
                                                              ESP=T¡tulo];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Group Code,Code                         ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Code,Description                         }
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=Only a %1 Table can be defined for a %2 of %3 %4.;ESP=S¢lo se puede definir 1 tabla %1 para %2 de %3 %4.';
      Text002@1000 : TextConst 'ENU=No Tables can be defined for a %1 of %2 %3.;ESP=No puede definirse ninguna tabla para %1 de %2 %3.';
      Text003@1002 : TextConst 'ENU=No template of %1 %2 has been defined.;ESP=No se han definido plantillas de %1 %2.';
      Text004@1003 : TextConst 'ENU=A %1 Defined By System cannot be modified.;ESP=No se puede modificar un %1 definido por sistema.';
      Text005@1004 : TextConst 'ENU=A %1 Defined By System cannot be deleted.;ESP=No se puede eliminar un %1 definido por sistema.';
      Text006@1005 : TextConst 'ENU=A %1 Defined By System cannot be renamed.;ESP=No se puede renombrar un %1 definido por sistema.';
      Text007@1006 : TextConst 'ENU=A %1 of %2 %3 cannot be editable.;ESP=No se puede editar un %1 de %2 %3.';

    BEGIN
    END.
  }
}
