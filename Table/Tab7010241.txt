OBJECT Table 7010241 Representante excursiones
{
  OBJECT-PROPERTIES
  {
    Date=30/03/15;
    Time=15:52:19;
    Modified=Yes;
    Version List=AIC2009,CNT-OC-14047;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               SetCode;
               SetCliente;
             END;

    OnModify=BEGIN
               "Fecha Modificacion"   := CURRENTDATETIME;
               "Usuario Modificacion" := USERID;
             END;

    OnDelete=VAR
               lrSetupC@1100253000 : Record 7010242;
             BEGIN
               //+$013 <
               IF _CheckReservations THEN
                 ERROR(Text006);
               //+$013 >

               // Borramos los registros de configuraci¢n contable
               CLEAR(lrSetupC);
               lrSetupC.SETRANGE(Representante, Codigo);
               IF lrSetupC.FINDSET(TRUE) THEN
                 lrSetupC.DELETEALL;
             END;

    CaptionML=[ENU=Excusions sales representative;
               ESP=Representante excursiones];
    LookupFormID=Form7010232;
    DrillDownFormID=Form7010232;
  }
  FIELDS
  {
    { 1   ;   ;Codigo              ;Code20        ;CaptionML=[ENU=Code;
                                                              ESP=Codigo] }
    { 10  ;   ;Nombre              ;Text50        ;CaptionML=[ENU=Name;
                                                              ESP=Nombre] }
    { 15  ;   ;Apellidos           ;Text50        ;Description=$016 }
    { 20  ;   ;Cliente Facturacion ;Code20        ;TableRelation=Customer.No.;
                                                   CaptionML=[ENU=Billing Customer;
                                                              ESP=Cliente Facturaci¢n] }
    { 30  ;   ;Proveedor Comisiones;Code20        ;TableRelation=Vendor.No. WHERE (Tipo Proveedor=CONST(GV));
                                                   CaptionML=[ENU=Commission Supplier;
                                                              ESP=Proveedor Comisiones] }
    { 40  ;   ;Bloqueado           ;Boolean       ;OnValidate=BEGIN
                                                                IF NOT CONFIRM(Text002) THEN
                                                                  ERROR('');
                                                              END;

                                                   CaptionML=[ENU=Blocked;
                                                              ESP=Bloqueado] }
    { 50  ;   ;Fecha Modificacion  ;DateTime      ;CaptionML=[ENU=Modification Date;
                                                              ESP=Fecha modificacion];
                                                   Editable=No }
    { 60  ;   ;Usuario Modificacion;Text20        ;CaptionML=[ENU=Modification User;
                                                              ESP=Usuario Modificaci¢n];
                                                   Editable=No }
    { 70  ;   ;Ultimo Ticket Liquidado;Code10     ;CaptionML=[ENU=Last Modified Ticket;
                                                              ESP=Ultimo Ticket Liquidado];
                                                   Editable=No }
    { 72  ;   ;Trabaja a credito   ;Boolean       ;OnValidate=BEGIN
                                                                IF "Trabaja a credito" THEN
                                                                  IF "Trabaja en un Pool" OR "Guia principal" THEN
                                                                    ERROR(Text003);

                                                                IF NOT CONFIRM(Text001) THEN
                                                                  ERROR('');

                                                                //. Hay que mantener el campo reserva prepagada de las reservas asociadas a este guia
                                                                _ModificaReservas;

                                                                IF "Trabaja a credito" <> xRec."Trabaja a credito" THEN
                                                                  SetCliente;
                                                              END;

                                                   CaptionML=[ENU=Works on credit;
                                                              ESP=Trabaja a credito] }
    { 73  ;   ;Trabaja en un Pool  ;Boolean       ;OnValidate=BEGIN
                                                                IF "Trabaja en un Pool" THEN
                                                                  IF "Guia principal" THEN
                                                                    ERROR(Text004);
                                                              END;

                                                   CaptionML=[ENU=Works in a Pool;
                                                              ESP=Trabaja en un Pool];
                                                   Description=$002 }
    { 75  ;   ;Guia principal      ;Boolean       ;OnValidate=BEGIN
                                                                IF "Guia principal" THEN
                                                                  IF "Trabaja en un Pool" THEN
                                                                    ERROR(Text005);
                                                              END;

                                                   CaptionML=[ENU=Main guide;
                                                              ESP=Guia principal] }
    { 77  ;   ;Oficina ventas      ;Code10        ;TableRelation="Oficina ventas".Codigo;
                                                   CaptionML=[ENU=Sales office;
                                                              ESP=Oficina ventas];
                                                   Description=$004 }
    { 78  ;   ;Reservas sin liquidar;Boolean      ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Reserva Excursion" WHERE (Guia Venta=FIELD(Codigo),
                                                                                                Liquidado GV=CONST(No),
                                                                                                N§ Ticket=FILTER(<>'')));
                                                   CaptionML=[ENU=Reservations with liquidation;
                                                              ESP=Reservas sin liquidar];
                                                   Description=$005;
                                                   Editable=No }
    { 79  ;   ;Datafono relacionado;Code10        ;CaptionML=[ENU=Related pin pad;
                                                              ESP=Datafono relacionado] }
    { 81  ;   ;Seleccionado        ;Boolean       ;CaptionML=[ENU=Selected;
                                                              ESP=Seleccionado] }
    { 90  ;   ;Filtro fecha        ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Date filter;
                                                              ESP=Filtro fecha] }
    { 92  ;   ;Filtro forma pago   ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation="Payment Method".Code;
                                                   CaptionML=[ENU=Payment method Filter;
                                                              ESP=Filtro forma pago] }
    { 94  ;   ;Ventas              ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Reserva ex - Detalle economico"."Ingreso reserva" WHERE (Guia Venta=FIELD(Codigo),
                                                                                                                             Fecha venta=FIELD(Filtro fecha)));
                                                   CaptionML=[ENU=Sales;
                                                              ESP=Ventas];
                                                   Description=Calculado sobre detalle economico;
                                                   Editable=No }
    { 96  ;   ;Filtro Divisa       ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation=Currency.Code;
                                                   CaptionML=[ENU=Currency filter;
                                                              ESP=Filtro Divisa] }
    { 98  ;   ;Cobros              ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Cobro reserva excursion"."Importe Recibido" WHERE (Guia venta=FIELD(Codigo),
                                                                                                                       Forma de Pago=FIELD(Filtro forma pago),
                                                                                                                       Cod. Divisa=FIELD(Filtro Divisa),
                                                                                                                       Fecha venta=FIELD(Filtro fecha),
                                                                                                                       Fecha reembolso=FIELD(Filtro fecha reembolso)));
                                                   CaptionML=[ENU=Payments;
                                                              ESP=Cobros];
                                                   Description=Calculado sobre cobro reserva excursion;
                                                   Editable=No }
    { 99  ;   ;Filtro fecha reembolso;Date        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Refund date filter;
                                                              ESP=Filtro fecha reembolso] }
    { 100 ;   ;Serie tickets externos;Code10      ;TableRelation="No. Series".Code;
                                                   CaptionML=[ENU=External tickets series;
                                                              ESP=Serie tickets externos];
                                                   Description=$010, "No. Series".Code }
    { 110 ;   ;Reembolsos          ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Reserva ex - Reembolso cobro".Importe WHERE (Guia Venta=FIELD(Codigo),
                                                                                                                 Forma de Pago=FIELD(Filtro forma pago),
                                                                                                                 Cod. Divisa=FIELD(Filtro Divisa),
                                                                                                                 Fecha reembolso=FIELD(Filtro fecha reembolso)));
                                                   CaptionML=[ENU=Refunds;
                                                              ESP=Reembolsos];
                                                   Description=Calculado sobre cobro reserva excursion reembolso;
                                                   Editable=No }
    { 112 ;   ;Ventas sin liquidar ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Reserva Excursion"."Ingreso reserva" WHERE (Guia Venta=FIELD(Codigo),
                                                                                                                Liquidado GV=CONST(No)));
                                                   CaptionML=[ENU=Sales w/o liquidate;
                                                              ESP=Ventas sin liquidar];
                                                   Editable=No }
    { 120 ;   ;Reservas transfer sin liquidar;Boolean;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Exist("Reserva Transfer" WHERE (Guia Venta=FIELD(Codigo),
                                                                                               Liquidado GV=CONST(No)));
                                                   CaptionML=[ENU=Transfer reservations with liquidation;
                                                              ESP=Reservas transfer sin liquidar];
                                                   Editable=No }
    { 122 ;   ;Ultimo bono transfer Liquidado;Code10;
                                                   CaptionML=[ENU=Last Modified Transfer Booking;
                                                              ESP=Ultimo Bono transfer Liquidado];
                                                   Description=$016;
                                                   Editable=No }
    { 150 ;   ;Last Login          ;DateTime      ;Description=$017 }
    { 152 ;   ;APP Registered      ;Boolean       ;Description=$017 }
  }
  KEYS
  {
    {    ;Codigo                                  ;Clustered=Yes }
    {    ;Nombre                                  ;KeyGroups=RENUMERING }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100253000 : TextConst 'ENU=The modification of the fields affects the invoicing and settlement method of the bookings of this sales representative, continue?;ESP=La modificacion de este campo afecta al modo en que se liquidaran y facturaran las reservas de este representante, desea continuar?';
      Text002@1100253001 : TextConst 'ENU=If you do not mark this field you cannot create/modify/delete bookings of this sale representative, continue?;ESP=Si marca este campo no se podran crear/modificar/eliminar reservas de este representante, desea continuar?';
      Text003@1100253002 : TextConst 'ENU=The guides on credit can not work in a Pool or have the mark of Main Guide.;ESP=Los guias a credito no pueden trabajar en un Pool ni tener la marca de Guia Principal.';
      Text004@1100253003 : TextConst 'ENU=A guide working in a Pool cannot be a Main Guide.;ESP=Un guia que trabaja en un Pool no puede tener la marca de Guia Principal.';
      Text005@1100253004 : TextConst 'ENU=A Main Guide cannot work in a Pool.;ESP=Un guia principal no puede trabajar en un pool.';
      Text006@1000000000 : TextConst 'ENU=You cannot delete a representant with reservations.;ESP=No se puede borrar un representante con reservas.';

    PROCEDURE UltimoTicketLiquidado@1100253000() : Code[10];
    VAR
      lrResE@1100253000 : Record 7010162;
    BEGIN
      // UltimoTicketLiquidado

      CLEAR(lrResE);
      lrResE.SETCURRENTKEY("Guia Venta", "N§ Ticket", "Liquidado GV", "N§ liquidacion GV", "N§ liquidacion GV reembolso");
      lrResE.SETRANGE("Guia Venta"   , Codigo);
      lrResE.SETRANGE("Liquidado GV" , TRUE);
      IF lrResE.FINDLAST THEN
        EXIT(lrResE."N§ Ticket");
    END;

    LOCAL PROCEDURE _ModificaReservas@1100217000();
    VAR
      lrResE@1100217000 : Record 7010162;
    BEGIN
      // _ModificaReservas

      CLEAR(lrResE);
      lrResE.SETCURRENTKEY("Guia Venta", "N§ Ticket", "Liquidado GV", "N§ liquidacion GV", "N§ liquidacion GV reembolso");
      lrResE.SETRANGE("Guia Venta"   , Codigo);
      IF lrResE.FINDSET(TRUE) THEN
        lrResE.MODIFYALL("Reserva a credito", "Trabaja a credito");
    END;

    LOCAL PROCEDURE _CheckReservations@1000000000() : Boolean;
    VAR
      lrRsv@1000000000 : Record 7010162;
    BEGIN
      // _CheckReservations

      CLEAR(lrRsv);
      lrRsv.SETCURRENTKEY("Guia Venta");
      lrRsv.SETRANGE("Guia Venta"   , Codigo);
      EXIT(lrRsv.FINDFIRST);
    END;

    PROCEDURE SetCode@1000000001();
    VAR
      rParAg@1000000000 : Record 7009700;
      rDel@1000000001 : Record 7009718;
      vCod@1000000002 : Code[20];
    BEGIN
      // Generamos el c¢digo del representante seg£n indicaciones de CNT
      // $015

      rParAg.FINDFIRST;
      rDel.GET(rParAg.Delegacion);

      rDel."Ult num representante" := rDel."Ult num representante" + 1;
      rDel.MODIFY;

      vCod := FORMAT(rDel."Ult num representante");

      CASE STRLEN(vCod) OF
        1: vCod := 'REP' + rDel.Codigo + '000' + vCod;
        2: vCod := 'REP' + rDel.Codigo + '00' + vCod;
        3: vCod := 'REP' + rDel.Codigo + '0' + vCod;
        ELSE
          vCod := 'REP' + rDel.Codigo + vCod;
      END;
      Codigo := vCod;
    END;

    PROCEDURE SetCliente@1000000002();
    VAR
      rParAg@1000000000 : Record 7009700;
    BEGIN
      // Actualizamos el cliente facturaci¢n
      // $018

      rParAg.FINDFIRST;

      IF "Trabaja a credito" THEN
        "Cliente Facturacion" := ''
      ELSE
        "Cliente Facturacion" := rParAg."Cliente contado";
    END;

    BEGIN
    {
      $001 AJS 08022011 He creado un nuevo campo "Trabaja a credito" para marcar aquellos representantes que no introducen
                        la informacion de cobros ya que se factura a credito

      $002 AJS 01062012 Nuevo campo "Trabaja como Pool" para marcar aquellos guias que pueden trabajar de manera conjunta en
                        un hotel compartiendo la comision

      $003 AJS 05062012 CNT-CAR-12004, EXC-17, Nuevo campo "Guia principal" para poder marcar los guias que son los
                        principales en un equipo de guias que trabaja en un hotel

      $004 AJS 29102012 Nuevo campo Oficina para poder enlazar un guia con una oficina de ventas y de esta manera propagar este
                        valor a las reservas

      $005 AJS 29112012 Nuevo campo "Reservas sin liquidar" para poder ver facilmente los guias que tienen
                        reservas pendientes de liquidar

      $006 AJS 05122012 Ampliacion del campo Nombre a 50 caracteres

      $007 AJS 04032013 Nuevo campo Seleccionado para usar en procesos de seleccion de guias de venta

      $008 AJS 19032013 Nuevo campo para poder tener un campo calculo de ventas por guia

      $009 AJS 16062013 Nuevo campo Filtro fecha reembolso para poder calcular los cobros reembolsados un dia

      $010 AJS 15102013 Nuevo campo "Digitos tickets externos" para numerar los tickets que se crean desde otras aplicaciones

      $011 ARM 02102014 CNT-CAR-14063 Nuevo campo Reservas transfer sin liquidar

      $012 AJS 08102014 Nuevo campo Ventas sin liquidar

      $013 AJS 10102014 Evitamos que se pueda borrar un guia venta con reservas

      $014 AJS 13102014 Cambio en la formula de reservas sin liquidar

      $015 ARM 04022015 CNT-LP-14126 Se codifica los representantes REP+DELEGACIOn+AUTONUMERIO

      $016 ARM 04022015 CNT-LP-14126 Nuevo campo Apellidos

      $017 ARM 06022015 CNT-OC-14087 Nuevos campos Last login y App registered

      $018 ARM 30032015 CNT-OC-14053 Se autorellena el cliente facturaci¢n conel cliente contado a no ser que sea cr‚dito
    }
    END.
  }
}
