OBJECT Table 5055268 Liq. Default Dimension
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=LIQEM6.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               GLSetup.GET;
               IF "Dimension Code" = GLSetup."Global Dimension 1 Code" THEN
                 UpdateGlobalDimCode(1,"Table ID","No.","Dimension Value Code");
               IF "Dimension Code" = GLSetup."Global Dimension 2 Code" THEN
                 UpdateGlobalDimCode(2,"Table ID","No.","Dimension Value Code");
             END;

    OnModify=BEGIN
               GLSetup.GET;
               IF "Dimension Code" = GLSetup."Global Dimension 1 Code" THEN
                 UpdateGlobalDimCode(1,"Table ID","No.","Dimension Value Code");
               IF "Dimension Code" = GLSetup."Global Dimension 2 Code" THEN
                 UpdateGlobalDimCode(2,"Table ID","No.","Dimension Value Code");
             END;

    OnDelete=BEGIN
               GLSetup.GET;
               IF "Dimension Code" = GLSetup."Global Dimension 1 Code" THEN
                 UpdateGlobalDimCode(1,"Table ID","No.",'');
               IF "Dimension Code" = GLSetup."Global Dimension 2 Code" THEN
                 UpdateGlobalDimCode(2,"Table ID","No.",'');
             END;

    OnRename=BEGIN
               ERROR(Text1140000,TABLECAPTION);
             END;

    CaptionML=[ENU=Liq. Default Dimension;
               ESP=Dimensi¢n predet. liq.];
  }
  FIELDS
  {
    { 1   ;   ;Table ID            ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   OnValidate=VAR
                                                                TempObject@1140000 : TEMPORARY Record 2000000001;
                                                              BEGIN
                                                                CALCFIELDS("Table Name");
                                                              END;

                                                   OnLookup=VAR
                                                              TempObject@1140000 : TEMPORARY Record 2000000001;
                                                            BEGIN
                                                              CLEAR(TempObject);
                                                            END;

                                                   CaptionML=[ENU=Table ID;
                                                              ESP=Id. tabla];
                                                   NotBlank=Yes }
    { 2   ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              ESP=Tipo];
                                                   OptionCaptionML=[ENU="Revenue,Expense, ";
                                                                    ESP="Ingreso,Gasto, "];
                                                   OptionString=[Revenue,Expense, ] }
    { 3   ;   ;No.                 ;Code20        ;TableRelation=IF (Table ID=CONST(13)) Salesperson/Purchaser
                                                                 ELSE IF (Table ID=CONST(15)) "G/L Account"
                                                                 ELSE IF (Table ID=CONST(18)) Customer
                                                                 ELSE IF (Table ID=CONST(23)) Vendor
                                                                 ELSE IF (Table ID=CONST(27)) Item
                                                                 ELSE IF (Table ID=CONST(152)) "Resource Group"
                                                                 ELSE IF (Table ID=CONST(156)) Resource
                                                                 ELSE IF (Table ID=CONST(167)) Job
                                                                 ELSE IF (Table ID=CONST(270)) "Bank Account"
                                                                 ELSE IF (Table ID=CONST(5071)) Campaign
                                                                 ELSE IF (Table ID=CONST(5200)) Employee
                                                                 ELSE IF (Table ID=CONST(5600)) "Fixed Asset"
                                                                 ELSE IF (Table ID=CONST(5628)) Insurance;
                                                   CaptionML=[ENU=No.;
                                                              ESP=N§] }
    { 4   ;   ;Dimension Code      ;Code20        ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                IF NOT DimMgt.CheckDim("Dimension Code") THEN
                                                                  ERROR(DimMgt.GetDimErr);
                                                              END;

                                                   CaptionML=[ENU=Dimension Code;
                                                              ESP=C¢d. dimensi¢n];
                                                   NotBlank=Yes }
    { 5   ;   ;Dimension Value Code;Code20        ;TableRelation="Dimension Value".Code WHERE (Dimension Code=FIELD(Dimension Code));
                                                   OnValidate=BEGIN
                                                                IF NOT DimMgt.CheckDimValue("Dimension Code","Dimension Value Code") THEN
                                                                  ERROR(DimMgt.GetDimErr);
                                                              END;

                                                   CaptionML=[ENU=Dimension Value Code;
                                                              ESP=C¢d. valor dimensi¢n] }
    { 6   ;   ;Value Posting       ;Option        ;CaptionML=[ENU=Value Posting;
                                                              ESP=Registro valor];
                                                   OptionCaptionML=[ENU=" ,Code Mandatory,Same Code,No Code";
                                                                    ESP=" ,Obligatorio,Igual c¢digo,Sin c¢digo"];
                                                   OptionString=[ ,Code Mandatory,Same Code,No Code] }
    { 7   ;   ;Table Name          ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table ID)));
                                                   CaptionML=[ENU=Table Name;
                                                              ESP=Nombre tabla];
                                                   Editable=No }
    { 8   ;   ;Multi Selection Action;Option      ;CaptionML=[ENU=Multi Selection Action;
                                                              ESP=Acci¢n multiselecci¢n];
                                                   OptionCaptionML=[ENU=" ,Change,Delete";
                                                                    ESP=" ,Cambiar,Borrar"];
                                                   OptionString=[ ,Change,Delete] }
  }
  KEYS
  {
    {    ;Table ID,Type,No.,Dimension Code        ;Clustered=Yes }
    {    ;Dimension Code                           }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text1140000@1140000 : TextConst 'ENU=You can''t rename a %1.;ESP=No puede renombrar un %1.';
      GLSetup@1140001 : Record 98;
      DimMgt@1140002 : Codeunit 408;

    PROCEDURE GetCaption@1140000() : Text[250];
    VAR
      ObjTransl@1140000 : Record 377;
      CurrTableID@1140001 : Integer;
      NewTableID@1140002 : Integer;
      NewNo@1140003 : Code[20];
      SourceTableName@1140004 : Text[100];
    BEGIN
      IF NOT EVALUATE(NewTableID,GETFILTER("Table ID")) THEN
        EXIT('');

      IF NewTableID = 0 THEN
        IF GETRANGEMIN("Table ID") = GETRANGEMAX("Table ID") THEN
          NewTableID := GETRANGEMIN("Table ID")
        ELSE
          NewTableID := 0;

      IF NewTableID <> CurrTableID THEN
        SourceTableName := ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,NewTableID);
      CurrTableID := NewTableID;

      IF GETFILTER("No.") <> '' THEN
        IF GETRANGEMIN("No.") = GETRANGEMAX("No.") THEN
          NewNo := GETRANGEMIN("No.")
        ELSE
          NewNo := '';

      IF NewTableID <> 0 THEN
        EXIT(STRSUBSTNO('%1 %2',SourceTableName,NewNo))
      ELSE
        EXIT('');
    END;

    LOCAL PROCEDURE UpdateGlobalDimCode@1140001(GlobalDimCodeNo@1140000 : Integer;TableID@1140001 : Integer;No@1140002 : Code[20];NewDimValue@1140003 : Code[20]);
    VAR
      NeutralPayment@1140004 : Record 5055260;
    BEGIN
      CASE "Table ID" OF
        DATABASE::"Neutral Payments": BEGIN
          IF NeutralPayment.GET(Type,"No.") THEN BEGIN
            CASE GlobalDimCodeNo OF
              1:NeutralPayment."Global Dimension 1 Code" := NewDimValue;
              2:NeutralPayment."Global Dimension 2 Code" := NewDimValue;
            END;
            NeutralPayment.MODIFY(TRUE);
          END;
        END;
      END;
    END;

    BEGIN
    END.
  }
}
