OBJECT Table 7010006 Recuperacion
{
  OBJECT-PROPERTIES
  {
    Date=22/07/14;
    Time=14:05:28;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF NOT rPar.FIND('-') THEN
                 ERROR(Text000);
               IF NOT rPar."Empresa Central" THEN
                 ERROR(Text001);

               IF NOT rAnt.GET(Tipo, Hotel, Contrato, "Fecha concesion") THEN
                 ERROR(Text002);

               VALIDATE(Importe);
               VALIDATE("%");

               Delegacion := rAnt.Delegacion;
               TourOperador := rAnt.TourOperador;
               "Forma recuperacion" := rAnt."Forma recuperacion";
               "C¢d. divisa" := rAnt."C¢d. divisa";
             END;

    OnModify=BEGIN
               IF NOT rPar.FIND('-') THEN
                 ERROR(Text000);
               IF NOT rPar."Empresa Central" THEN
                 ERROR(Text003);
             END;

    OnDelete=BEGIN
               IF NOT rPar.FIND('-') THEN
                 ERROR(Text000);
               IF NOT rPar."Empresa Central" THEN
                 ERROR(Text004);

               IF "Pasado ABR" THEN
                 IF NOT CONFIRM(Text005) THEN
                   ERROR(Text006);
             END;

    OnRename=BEGIN
               IF NOT rPar.FIND('-') THEN
                 ERROR(Text000);
               IF NOT rPar."Empresa Central" THEN
                 ERROR(Text007);
             END;

    CaptionML=[ENU=Recovery;
               ESP=Recuperacion];
  }
  FIELDS
  {
    { 1   ;   ;Tipo                ;Option        ;CaptionML=[ENU=Type;
                                                              ESP=Tipo];
                                                   OptionCaptionML=[ENU=Advance,Loan;
                                                                    ESP=Anticipo,Prestamo];
                                                   OptionString=Anticipo,Prestamo;
                                                   Description=PK }
    { 3   ;   ;Hotel               ;Code20        ;TableRelation=Hotel.Hotel;
                                                   CaptionML=[ENU=Hotel;
                                                              ESP=Hotel];
                                                   Description=PK, FK Hotel }
    { 5   ;   ;Contrato            ;Code10        ;CaptionML=[ENU=Contract;
                                                              ESP=Contrato];
                                                   Description=PK }
    { 7   ;   ;Fecha concesion     ;Date          ;CaptionML=[ENU=Granting date;
                                                              ESP=Fecha concesion];
                                                   Description=PK }
    { 9   ;   ;Fecha recuperacion  ;Date          ;OnValidate=BEGIN
                                                                IF "Fecha recuperacion" = 0D THEN
                                                                  ERROR(Text008);
                                                              END;

                                                   CaptionML=[ENU=Recovery date;
                                                              ESP=Fecha recuperacion];
                                                   NotBlank=Yes }
    { 11  ;   ;Importe             ;Decimal       ;OnValidate=BEGIN
                                                                comp_importe;
                                                                IF "Forma recuperacion" = "Forma recuperacion"::"Importes fijos" THEN
                                                                  IF Importe = 0 THEN
                                                                    ERROR(Text009)
                                                              END;

                                                   CaptionML=[ENU=Amount;
                                                              ESP=Importe];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 13  ;   ;%                   ;Decimal       ;OnValidate=BEGIN
                                                                IF "Forma recuperacion" = "Forma recuperacion"::"Importes variables" THEN
                                                                  IF "%" = 0 THEN
                                                                    ERROR(Text010);
                                                              END;

                                                   CaptionML=[ENU=%;
                                                              ESP=%] }
    { 15  ;   ;Fecha final recuperacion;Date      ;CaptionML=[ENU=Recovery final date;
                                                              ESP=Fecha final recuperacion] }
    { 17  ;   ;Fecha fras. desde   ;Date          ;OnValidate=BEGIN
                                                                IF ("Fecha fras. desde" > "Fecha fras. hasta") AND ("Fecha fras. hasta" <> 0D) THEN
                                                                  ERROR(Text011);
                                                              END;

                                                   CaptionML=[ENU=Date of invoices from;
                                                              ESP=Fecha fras. desde] }
    { 19  ;   ;Fecha fras. hasta   ;Date          ;OnValidate=BEGIN
                                                                IF ("Fecha fras. desde" > "Fecha fras. hasta") AND ("Fecha fras. desde" <> 0D) THEN
                                                                  ERROR(Text011);
                                                              END;

                                                   CaptionML=[ENU=Date of invoices to;
                                                              ESP=Fecha fras. hasta] }
    { 21  ;   ;Pasado ABR          ;Boolean       ;CaptionML=[ENU=ABR Passed;
                                                              ESP=Pasado ABR];
                                                   Editable=No }
    { 23  ;   ;Forma recuperacion  ;Option        ;CaptionML=[ENU=Recovery form;
                                                              ESP=Forma recuperacion];
                                                   OptionCaptionML=[ENU=Fixed amounts,Variable amounts;
                                                                    ESP=Importes fijos,Importes variables];
                                                   OptionString=Importes fijos,Importes variables;
                                                   Description=PK }
    { 25  ;   ;C¢d. divisa         ;Code10        ;TableRelation=Currency.Code;
                                                   CaptionML=[ENU=Currency exchange;
                                                              ESP=C¢d. divisa] }
    { 27  ;   ;Delegacion          ;Code10        ;TableRelation=Delegacion.Codigo;
                                                   CaptionML=[ENU=Local Office;
                                                              ESP=Delegacion];
                                                   Description=FK Delegacion }
    { 29  ;   ;TourOperador        ;Code10        ;TableRelation=TourOperador.TourOperador;
                                                   OnValidate=BEGIN
                                                                //$001-
                                                                rTTOO.RESET;
                                                                rTTOO.GET(TourOperador);
                                                                rTTOO.TESTFIELD(Bloqueado, FALSE);
                                                                //$001+
                                                              END;

                                                   CaptionML=[ENU=TourOperator;
                                                              ESP=TourOperador];
                                                   Description=FK TourOperador }
    { 31  ;   ;Documento contabilizacion;Code10   ;CaptionML=[ENU=Accounting document;
                                                              ESP=Documento contabilizacion];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Tipo,Hotel,Contrato,Fecha concesion,Fecha recuperacion,Forma recuperacion;
                                                   SumIndexFields=Importe;
                                                   KeyGroups=RENUMERING;
                                                   Clustered=Yes }
    {    ;Tipo,Hotel,Contrato,Fecha concesion,Pasado ABR;
                                                   SumIndexFields=Importe;
                                                   KeyGroups=RENUMERING }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1103355002 : TextConst 'ENU=Agency parameters folder does not exist;ESP=No existe el fichero de Parametros de la Agencia.';
      Text001@1103355003 : TextConst 'ENU=Only recoveries in central can be entered;ESP=Solo se pueden dar de alta recuperaciones en la Central.';
      Text002@1103355004 : TextConst 'ENU=Corresponding advance has been deleted.;ESP=Se ha borrado el anticipo correspondiente.';
      Text003@1103355005 : TextConst 'ENU=Only recoveries in central can be modified;ESP=Solo se pueden modificar recuperaciones en la Central.';
      Text004@1103355006 : TextConst 'ENU=Only recoveries in central can be resigned;ESP=Solo se pueden dar de baja recuperaciones en la Central.';
      Text005@1103355007 : TextConst 'ENU=This recovery has been transferred to the ABR diary. Delete?;ESP=Esta recuperacion ha pasado al diario ABR. Borrar?';
      Text006@1103355008 : TextConst 'ENU=Non-deleted recovery;ESP=Recuperaci¢n no borrada';
      Text007@1103355009 : TextConst 'ENU=Only recoveries in central can be renamed;ESP=Solo se pueden renombrar recuperaciones en la Central.';
      Text008@1103355010 : TextConst 'ENU=Must indicate a recovery date;ESP=Debe poner una fecha der recuperaci¢n';
      Text009@1103355011 : TextConst 'ENU=An amount has to be entered;ESP=Debe poner alg£n importe';
      Text010@1103355012 : TextConst 'ENU=A percentage has to be entered;ESP=Debe poner alg£n porcentaje';
      Text011@1103355013 : TextConst 'ENU=Date from must be lower than date to;ESP=La fecha desde debe ser anterior a la fecha hasta.';
      Text012@1103355014 : TextConst 'ENU=Unable to recover more amount than the advanced/loan;ESP=No se puede recuperar m s importe del anticipado/prestado.';
      rPar@1103355000 : Record 7009700;
      rAnt@1103355001 : Record 7010005;
      rTTOO@1000000000 : Record 7009717;

    PROCEDURE nom_hotel@1() : Text[30];
    VAR
      rHot@1103355000 : Record 7009724;
    BEGIN
      IF NOT rHot.GET(Hotel) THEN
        EXIT('')
      ELSE
        EXIT(rHot.Nombre);
    END;

    PROCEDURE nom_touroperador@2() : Text[30];
    VAR
      rTO@1103355000 : Record 7009717;
    BEGIN
      IF NOT rTO.GET(TourOperador) THEN
        EXIT('')
      ELSE
        EXIT(rTO.Nombre);
    END;

    PROCEDURE nom_delegacion@3() : Text[30];
    VAR
      rDel@1103355000 : Record 7009718;
    BEGIN
      IF NOT rDel.GET(Delegacion) THEN
        EXIT('')
      ELSE
        EXIT(rDel.Nombre);
    END;

    PROCEDURE comp_importe@4();
    VAR
      import@1103355000 : Decimal;
      rRec@1103355001 : Record 7010006;
    BEGIN
      rAnt.RESET;
      IF NOT rAnt.GET(Tipo, Hotel, Contrato, "Fecha concesion") THEN
        ERROR(Text002);
      import := 0;
      rRec.RESET;
      rRec.SETRANGE (Tipo                , Tipo);
      rRec.SETRANGE (Hotel               , Hotel);
      rRec.SETRANGE (Contrato            , Contrato);
      rRec.SETRANGE ("Fecha concesion"   , "Fecha concesion");
      rRec.SETFILTER("Fecha recuperacion", '<>%1', "Fecha recuperacion");
      IF rRec.FIND('-') THEN BEGIN
        REPEAT
          import := import + rRec.Importe;
        UNTIL rRec.NEXT = 0;
      END;
      IF (import + Importe) > rAnt.Importe THEN
        ERROR(Text012);
    END;

    BEGIN
    {
      $001 MNC 22072014 CNT-LP-14054 No se pueden usar TTOO bloqueados
    }
    END.
  }
}
