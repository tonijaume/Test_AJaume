OBJECT Table 273 Bank Acc. Reconciliation
{
  OBJECT-PROPERTIES
  {
    Date=02/10/12;
    Time=12:00:00;
    Modified=Yes;
    Version List=AIC2009,NAVW16.00.01;
  }
  PROPERTIES
  {
    DataCaptionFields=Bank Account No.,Statement No.;
    OnInsert=BEGIN
               TESTFIELD("Bank Account No.");
               TESTFIELD("Statement No.");
               BankAccRecon.RESET;
               BankAccRecon.SETRANGE("Bank Account No.","Bank Account No.");
               IF PostedBankAccStmt.GET("Bank Account No.","Statement No.") THEN
                 ERROR(Text000,"Statement No.");
             END;

    OnDelete=BEGIN
               BankAccReconLine.RESET;
               BankAccReconLine.SETRANGE("Bank Account No.","Bank Account No.");
               BankAccReconLine.SETRANGE("Statement No.","Statement No.");
               BankAccReconLine.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               ERROR(Text001,TABLECAPTION);
             END;

    CaptionML=[ENU=Bank Acc. Reconciliation;
               ESP=Conciliaci¢n banco];
    LookupFormID=Form388;
  }
  FIELDS
  {
    { 1   ;   ;Bank Account No.    ;Code20        ;TableRelation="Bank Account";
                                                   OnValidate=BEGIN
                                                                IF "Statement No." = '' THEN BEGIN
                                                                  BankAcc.LOCKTABLE;
                                                                  BankAcc.GET("Bank Account No.");
                                                                  "Statement No." := INCSTR(BankAcc."Last Statement No.");
                                                                  "Balance Last Statement" := BankAcc."Balance Last Statement";
                                                                  BankAcc."Last Statement No." := "Statement No.";
                                                                  BankAcc.MODIFY;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Bank Account No.;
                                                              ESP=N§ banco];
                                                   NotBlank=Yes }
    { 2   ;   ;Statement No.       ;Code20        ;OnValidate=BEGIN
                                                                TESTFIELD("Bank Account No.");
                                                                BankAcc.LOCKTABLE;
                                                                BankAcc.GET("Bank Account No.");
                                                                BankAcc."Last Statement No." := "Statement No.";
                                                                BankAcc.MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Statement No.;
                                                              ESP=N§ extracto];
                                                   NotBlank=Yes }
    { 3   ;   ;Statement Ending Balance;Decimal   ;CaptionML=[ENU=Statement Ending Balance;
                                                              ESP=Saldo final extracto];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 4   ;   ;Statement Date      ;Date          ;CaptionML=[ENU=Statement Date;
                                                              ESP=Fecha extracto] }
    { 5   ;   ;Balance Last Statement;Decimal     ;OnValidate=BEGIN
                                                                BankAcc.GET("Bank Account No.");
                                                                IF "Balance Last Statement" <> BankAcc."Balance Last Statement" THEN
                                                                  IF NOT
                                                                     CONFIRM(
                                                                       Text002,FALSE,
                                                                       FIELDCAPTION("Balance Last Statement"),BankAcc.FIELDCAPTION("Balance Last Statement"),
                                                                       BankAcc.TABLECAPTION)
                                                                  THEN
                                                                    "Balance Last Statement" := xRec."Balance Last Statement";
                                                              END;

                                                   CaptionML=[ENU=Balance Last Statement;
                                                              ESP=Saldo £lt. extracto];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 50000;  ;Codigo barras PDF   ;Code20        ;CaptionML=ESP=Codigo barras PDF;
                                                   Description=$001 }
  }
  KEYS
  {
    {    ;Bank Account No.,Statement No.          ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Statement %1 already exists.;ESP=El extracto %1 ya existe.';
      Text001@1001 : TextConst 'ENU=You cannot rename a %1.;ESP=No se puede cambiar el nombre a %1.';
      Text002@1002 : TextConst 'ENU=%1 is different from %2 on the %3. Do you want to change the value?;ESP=%1 es diferente del %2 en el %3. ¨Confirma que desea cambiar el valor?';
      BankAcc@1003 : Record 270;
      BankAccRecon@1004 : Record 273;
      BankAccReconLine@1005 : Record 274;
      PostedBankAccStmt@1006 : Record 275;
      cFunVar@1103355000 : Codeunit 7009720;
      Text50000@1103355001 : TextConst 'ESP=Desea marcar todas las lineas de este extracto';

    PROCEDURE GetCurrencyCode@1() : Code[10];
    VAR
      BankAcc2@1000 : Record 270;
    BEGIN
      IF ("Bank Account No." = BankAcc2."No.") THEN
        EXIT(BankAcc2."Currency Code")
      ELSE
        IF BankAcc2.GET("Bank Account No.") THEN
          EXIT(BankAcc2."Currency Code")
        ELSE
          EXIT('');
    END;

    PROCEDURE MarcarLineas@1103355000();
    VAR
      lrLin@1103355000 : Record 274;
    BEGIN
      // MarcarLineas
      //  $003
      IF NOT CONFIRM(Text50000) THEN
        EXIT;

      lrLin.RESET;
      lrLin.SETRANGE("Bank Account No.", "Bank Account No.");
      lrLin.SETRANGE("Statement No."   , "Statement No.");
      IF lrLin.FINDSET(TRUE) THEN
        lrLin.MODIFYALL("Ready for Application", TRUE);
    END;

    BEGIN
    {
      $001 AJS 16.04.2007, Escaneo documentos
         Nuevo campo para almacenar el nombre del PDF correspondiente al extracto bancario

          50000 Codigo barras PDF Code20

         En el OnValidate de "Statement Date" borramos el fichero PDF que tenga asignado y vaciar el campo codigo de barras

      $002 AJS 17.05.2007, Escaneo documentos
         Se ha eliminado el codigo del OnValidate de "Statement Date" porque el codigo de barras no incluye la fecha

      $003 AJS 18.01.2008, Peticion Ion
         Nueva funcion MarcarLineas para marcar el campo Ready for Application en todas las lineas
    }
    END.
  }
}
