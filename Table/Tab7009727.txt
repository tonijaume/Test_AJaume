OBJECT Table 7009727 Contrato venta hotel
{
  OBJECT-PROPERTIES
  {
    Date=22/07/14;
    Time=13:48:47;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    OnInsert=VAR
               rContrato@1103355000 : Record 7009727;
             BEGIN
               rPar.LOCKTABLE;
               rPar.FINDFIRST;
               IF NOT rPar."Empresa Delegacion" THEN
                 ERROR(Text001 + '\' + Text002);

               IF rPar."Contador Contrato venta hotel" = '' THEN
                 rPar."Contador Contrato venta hotel" := Text032 + '0000000';

               IF "N§ Contrato venta" = '' THEN BEGIN
                 REPEAT
                   rPar."Contador Contrato venta hotel" := INCSTR(rPar."Contador Contrato venta hotel");
                   rPar.MODIFY;
                 UNTIL NOT rContrato.GET(rPar.Delegacion, rPar."Contador Contrato venta hotel");
                 "N§ Contrato venta" := rPar."Contador Contrato venta hotel";
               END;

               Delegacion := rPar.Delegacion;

               //+$006
               //-$007rSalesSetup.FINDFIRST;
               //-$007"Cod. Serie" := rSalesSetup."Posted Invoice Nos.";

               VALIDATE(Replicado);
             END;

    OnModify=BEGIN
               rPar.FINDFIRST;
               IF NOT rPar."Empresa Delegacion" THEN
                 ERROR(Text004 + '\' + Text002);

               Confirmado := cCon.confirmar_contrato_venta(Rec);

               IF "Cambio Booking" THEN
                 cambiar_booking;

               "Cambio Booking" := FALSE;

               VALIDATE(Replicado);
             END;

    OnDelete=VAR
               lrActED@1103355000 : Record 7010001;
             BEGIN
               rPar.FINDFIRST;
               IF NOT rPar."Empresa Delegacion" THEN
                 ERROR(Text005 + '\' + Text002);

               cFunSeg.TienePermiso(Text007, TRUE);

               borrado_registro;

               //. Recuperamos el registro para evitar error en SQL
               Rec.GET(Delegacion, "N§ Contrato venta");

               cPar.intro_historico_contrato(Delegacion, "N§ Contrato venta", 1, Text009, Text010, '');

               //. Replicacion
               InsertaLinAction;
             END;

    OnRename=BEGIN
               VALIDATE(Replicado);
             END;

    CaptionML=[ENU=Hotel sale Contract;
               ESP=Contrato venta hotel];
    LookupFormID=Form7009743;
    DrillDownFormID=Form7009743;
  }
  FIELDS
  {
    { 1   ;   ;Delegacion          ;Code10        ;TableRelation=Delegacion.Codigo;
                                                   CaptionML=[ENU=Local Office;
                                                              ESP=Delegacion];
                                                   Description=PK, FK Delegacion;
                                                   Editable=No }
    { 3   ;   ;N§ Contrato venta   ;Code10        ;CaptionML=[ENU=Sale contract No.;
                                                              ESP=N§ Contrato venta];
                                                   Description=PK }
    { 5   ;   ;Cliente             ;Code20        ;TableRelation=Customer.No.;
                                                   OnValidate=VAR
                                                                rCli@1103355000 : Record 18;
                                                              BEGIN
                                                                IF Cliente <> xRec.Cliente THEN BEGIN

                                                                  // ---- Inicializar los campos del contrato con los del cliente  ----

                                                                  rCli.RESET;
                                                                  rCli.GET(Cliente);
                                                                  "Cod. Terminos pago" := rCli."Payment Terms Code";
                                                                  "Cod. Formas pago"   := rCli."Payment Method Code";
                                                                  "C¢d. divisa"        := rCli."Currency Code";

                                                                  _ControlReservas; //+$002

                                                                  switch;

                                                                  cPar.intro_historico_contrato(Delegacion, "N§ Contrato venta", 1, FIELDCAPTION(Cliente),
                                                                                                xRec.Cliente, Cliente);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Client;
                                                              ESP=Cliente];
                                                   NotBlank=Yes;
                                                   Description=FK Cliente }
    { 7   ;   ;Fecha firma contrato;Date          ;OnValidate=BEGIN
                                                                IF "Fecha firma contrato" <> xRec."Fecha firma contrato" THEN BEGIN
                                                                  IF "Fecha inicio contrato" <> 0D THEN
                                                                    VALIDATE("Fecha inicio contrato");

                                                                  switch;

                                                                  cPar.intro_historico_contrato(Delegacion, "N§ Contrato venta", 1, FIELDCAPTION("Fecha firma contrato"),
                                                                                                FORMAT(xRec."Fecha firma contrato"), FORMAT("Fecha firma contrato"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Enrollment date;
                                                              ESP=Fecha firma contrato] }
    { 9   ;   ;Fecha inicio contrato;Date         ;OnValidate=BEGIN
                                                                IF "Fecha inicio contrato" <> xRec."Fecha inicio contrato" THEN BEGIN
                                                                  IF "Fecha firma contrato" > "Fecha inicio contrato" THEN
                                                                    ERROR(Text011);

                                                                  IF "Fecha final contrato" <> 0D THEN
                                                                    VALIDATE("Fecha final contrato");

                                                                  switch;

                                                                  cPar.intro_historico_contrato(Delegacion, "N§ Contrato venta", 1, FIELDCAPTION("Fecha inicio contrato"),
                                                                                                FORMAT(xRec."Fecha inicio contrato"), FORMAT("Fecha inicio contrato"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Starting Contract Date;
                                                              ESP=Fecha inicio contrato] }
    { 11  ;   ;Fecha final contrato;Date          ;OnValidate=BEGIN
                                                                IF "Fecha final contrato" <> xRec."Fecha final contrato" THEN BEGIN
                                                                  IF "Fecha inicio contrato" > "Fecha final contrato" THEN
                                                                    ERROR(Text012);

                                                                  switch;

                                                                  cPar.intro_historico_contrato(Delegacion, "N§ Contrato venta", 1, FIELDCAPTION("Fecha final contrato"),
                                                                                                STRSUBSTNO('%1', xRec."Fecha final contrato"), STRSUBSTNO('%1', "Fecha final contrato"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Ending Contract Date;
                                                              ESP=Fecha final contrato] }
    { 13  ;   ;TourOperador        ;Code10        ;TableRelation=TourOperador.TourOperador;
                                                   OnValidate=BEGIN
                                                                //$009-
                                                                rTTOO.RESET;
                                                                rTTOO.GET(TourOperador);
                                                                rTTOO.TESTFIELD(Bloqueado, FALSE);
                                                                //$009+

                                                                IF TourOperador <> xRec.TourOperador THEN BEGIN
                                                                  IF xRec.TourOperador <> '' THEN BEGIN
                                                                    IF NOT CONFIRM(Text013) THEN
                                                                      ERROR(Text014);
                                                                  END;

                                                                  _BuscarCliente; //+$003

                                                                  "Cambio Booking" := TRUE;

                                                                  _ControlReservas; //+$002

                                                                  switch;

                                                                  cPar.intro_historico_contrato(Delegacion, "N§ Contrato venta", 1, FIELDCAPTION(TourOperador), xRec.TourOperador, TourOperador);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=TourOperator;
                                                              ESP=TourOperador];
                                                   Description=FK TourOperador }
    { 15  ;   ;Hotel               ;Code20        ;TableRelation=Hotel.Hotel;
                                                   OnValidate=BEGIN
                                                                IF Hotel <> xRec.Hotel THEN BEGIN
                                                                  IF xRec.Hotel <> '' THEN BEGIN
                                                                    IF NOT CONFIRM(Text015) THEN
                                                                      ERROR(Text014);
                                                                  END;

                                                                  "Cambio Booking" := TRUE;

                                                                  _ControlReservas; //+$002

                                                                  switch;
                                                                  cPar.intro_historico_contrato(Delegacion, "N§ Contrato venta", 1, FIELDCAPTION(Hotel), xRec.Hotel, Hotel);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Hotel;
                                                              ESP=Hotel];
                                                   Description=FK Hotel }
    { 17  ;   ;Impuestos incluidos ;Boolean       ;OnValidate=BEGIN
                                                                IF "Impuestos incluidos" <> xRec."Impuestos incluidos" THEN BEGIN
                                                                  switch;

                                                                  cPar.intro_historico_contrato(Delegacion, "N§ Contrato venta", 1, FIELDCAPTION("Impuestos incluidos"),
                                                                                                FORMAT(xRec."Impuestos incluidos"), FORMAT("Impuestos incluidos"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Included Taxes;
                                                              ESP=Impuestos incluidos] }
    { 19  ;   ;Tipo garantias      ;Option        ;OnValidate=BEGIN
                                                                IF "Tipo garantias" <> xRec."Tipo garantias" THEN BEGIN
                                                                  switch;

                                                                  cPar.intro_historico_contrato(Delegacion, "N§ Contrato venta", 1, FIELDCAPTION("Tipo garantias"),
                                                                                                FORMAT(xRec."Tipo garantias"), FORMAT("Tipo garantias"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Guarantees type;
                                                              ESP=Tipo garantias];
                                                   OptionCaptionML=[ENU=Without guarantees,Total,Partial;
                                                                    ESP=Sin Garant¡as,Total,Parcial];
                                                   OptionString=Sin garant¡as,Total,Parcial }
    { 21  ;   ;Gr. registro IVA producto;Code10   ;TableRelation="VAT Product Posting Group".Code;
                                                   OnValidate=BEGIN
                                                                IF "Gr. registro IVA producto" <> xRec."Gr. registro IVA producto" THEN BEGIN
                                                                  switch;

                                                                  cPar.intro_historico_contrato(Delegacion, "N§ Contrato venta", 1, FIELDCAPTION("Gr. registro IVA producto"),
                                                                                                xRec."Gr. registro IVA producto", "Gr. registro IVA producto");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=VAT Prod. posting group;
                                                              ESP=Gr. Registro IVA Producto];
                                                   Description=FK Gr. registro IVA producto }
    { 23  ;   ;Cod. Terminos pago  ;Code10        ;TableRelation="Payment Terms".Code;
                                                   OnValidate=BEGIN
                                                                IF "Cod. Terminos pago" <> xRec."Cod. Terminos pago" THEN BEGIN

                                                                  switch;

                                                                  cPar.intro_historico_contrato(Delegacion, "N§ Contrato venta", 1, FIELDCAPTION(Cliente),
                                                                                                xRec.Cliente, Cliente);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Payment terms code;
                                                              ESP=Cod. Terminos Pago];
                                                   Description=FK Terminos Pago }
    { 25  ;   ;Cod. Formas pago    ;Code10        ;TableRelation="Payment Method".Code;
                                                   OnValidate=BEGIN
                                                                IF "Cod. Formas pago" <> xRec."Cod. Formas pago" THEN BEGIN
                                                                  switch;

                                                                  cPar.intro_historico_contrato(Delegacion, "N§ Contrato venta", 1, FIELDCAPTION("Cod. Formas pago"),
                                                                                                xRec."Cod. Formas pago", "Cod. Formas pago");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Payment methods code;
                                                              ESP=Cod. Formas Pago];
                                                   Description=FK Cod. Forma Pago }
    { 27  ;   ;O_Cod. Serie        ;Code10        ;TableRelation="No. Series".Code;
                                                   CaptionML=[ENU=Serial code;
                                                              ESP=Cod. Serie];
                                                   Description=FK N§ Serie }
    { 29  ;   ;Enviar              ;Boolean       ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                IF Enviar <> xRec.Enviar THEN BEGIN
                                                                  cPar.intro_historico_contrato(Delegacion, "N§ Contrato venta", 1, FIELDCAPTION(Enviar),
                                                                                                STRSUBSTNO('%1', xRec.Enviar), STRSUBSTNO('%1', Enviar));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Send;
                                                              ESP=Enviar];
                                                   Editable=Yes }
    { 31  ;   ;Confirmado          ;Boolean       ;OnValidate=BEGIN
                                                                IF Enviar <> xRec.Enviar THEN
                                                                BEGIN
                                                                  cPar.intro_historico_contrato(Delegacion, "N§ Contrato venta", 1, FIELDCAPTION(Confirmado),
                                                                                                STRSUBSTNO('%1', xRec.Confirmado), STRSUBSTNO('%1', Confirmado));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Confirmed;
                                                              ESP=Confirmado];
                                                   Editable=No }
    { 33  ;   ;O_Total garantia    ;Decimal       ;CaptionML=[ENU=Total guarantee;
                                                              ESP=Total Garantia];
                                                   Description=Calculado sobre pagos garantia;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 35  ;   ;O_Garantia Pendiente;Decimal       ;CaptionML=[ENU=Remaining guarantee;
                                                              ESP=Garantia Pendiente];
                                                   Description=Calculado sobre pagos garantia;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 37  ;   ;Cambio Booking      ;Boolean       ;CaptionML=[ENU=Booking change;
                                                              ESP=Cambio Booking] }
    { 39  ;   ;Replicado           ;Boolean       ;InitValue=No;
                                                   OnValidate=BEGIN
                                                                Replicado:= FALSE;
                                                              END;

                                                   CaptionML=[ENU=Replicated;
                                                              ESP=Replicado];
                                                   Description=Campo usado por el replicador }
    { 41  ;   ;C¢d. divisa         ;Code10        ;TableRelation=Currency.Code;
                                                   CaptionML=[ENU=Currency code;
                                                              ESP=C¢d. divisa];
                                                   Description=FK Divisa }
    { 50  ;   ;Categoria Hotel     ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Hotel.Categoria WHERE (Hotel=FIELD(Hotel)));
                                                   CaptionML=[ENU=Hotel category;
                                                              ESP=Categoria Hotel];
                                                   Editable=No }
    { 101 ;   ;Max. Personas x reserva;Integer    ;OnValidate=BEGIN
                                                                IF "Max. Personas x reserva" <> xRec."Max. Personas x reserva" THEN
                                                                  cPar.intro_historico_contrato(Delegacion, "N§ Contrato venta", 1, FIELDCAPTION("Max. Personas x reserva"),
                                                                                                FORMAT(xRec."Max. Personas x reserva"), FORMAT("Max. Personas x reserva"));
                                                              END;

                                                   CaptionML=[ENU=Max. Persons x booking;
                                                              ESP=Max. Personas x reserva];
                                                   MinValue=0 }
    { 103 ;   ;Max. Habitaciones x reserva;Integer;OnValidate=BEGIN
                                                                IF "Max. Habitaciones x reserva" <> xRec."Max. Habitaciones x reserva" THEN
                                                                  cPar.intro_historico_contrato(Delegacion, "N§ Contrato venta", 1, FIELDCAPTION("Max. Habitaciones x reserva"),
                                                                                                FORMAT(xRec."Max. Habitaciones x reserva"), FORMAT("Max. Habitaciones x reserva"));
                                                              END;

                                                   CaptionML=[ENU=Max. Rooms x Booking;
                                                              ESP=Max. Habitaciones x reserva];
                                                   MinValue=0 }
  }
  KEYS
  {
    {    ;Delegacion,N§ Contrato venta            ;Clustered=Yes }
    {    ;TourOperador                            ;KeyGroups=RENUMERING }
    {    ;Hotel,TourOperador,Fecha inicio contrato;KeyGroups=RENUMERING }
    {    ;Enviar                                  ;KeyGroups=RENUMERING }
    {    ;Delegacion,TourOperador,Hotel,Fecha inicio contrato,Fecha final contrato;
                                                   KeyGroups=RENUMERING }
    {    ;Replicado                               ;KeyGroups=RENUMERING }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1103355018 : TextConst 'ENU=Only hotel contracts can be entered\;ESP=Solo se pueden dar de alta contratos de hotel';
      Text002@1103355019 : TextConst 'ENU=in the local offices;ESP=en las delegaciones.';
      Text004@1103355021 : TextConst 'ENU=Only hotel contracts can be modified\;ESP=Solo se pueden modificar contratos de hotel';
      Text005@1103355022 : TextConst 'ENU=Only hotel contracts can be cancelled\;ESP=Solo se pueden dar de baja contratos de hotel';
      Text007@1103355024 : TextConst 'ENU=FUN-ACT-COV;ESP=FUN-ACT-COV';
      Text009@1103355026 : TextConst 'ENU=Deleted;ESP=Borrado';
      Text010@1103355027 : TextConst 'ENU=Deleted register;ESP=Borrado registro';
      Text011@1103355028 : TextConst 'ENU=Starting date must be higher than the signature;ESP=La fecha de inicio debe ser posterior a la de firma.';
      Text012@1103355029 : TextConst 'ENU=Final date must be higher than the initial one;ESP=La fecha de final debe ser posterior a la de inicio.';
      Text013@1103355030 : TextConst 'ENU=Are you sure about to change the TourOperator of the contract;ESP=Esta seguro de que desea cambiar el TourOperador del contrato.';
      Text014@1103355031 : TextConst 'ENU=Cancelled change;ESP=Cambio anulado.';
      Text015@1103355032 : TextConst 'ENU=Are you sure about to change the Hotel of the contract;ESP=Esta seguro de que desea cambiar el Hotel del contrato.';
      Text016@1103355033 : TextConst 'ENU=Unable to delete this contract because it has linked bookings;ESP=No puede borrar este contrato porque tiene reservas dependientes de ‚l.';
      Text017@1103355034 : TextConst 'ENU=" CHANGING THE BOOKING OF THE HOTEL \";ESP=CAMBIANDO EL BOOKING DE HOTEL';
      Text018@1103355035 : TextConst 'ENU="  Room: #1########.";ESP="  Habitacion: #1########."';
      rPar@1103355002 : Record 7009700;
      rPre@1103355003 : Record 7009732;
      rRsv@1103355004 : Record 7009743;
      rGar@1103355005 : Record 7009729;
      rCupo@1103355007 : Record 7009730;
      rCupoDia@1103355008 : Record 7009758;
      rHis@1103355010 : Record 7009764;
      rHisP@1103355011 : Record 7009765;
      rConHab@1103355012 : Record 7009731;
      rPrev@1103355013 : Record 7009747;
      rTTOO@1000000000 : Record 7009717;
      cPar@1103355014 : Codeunit 7009700;
      cCon@1103355015 : Codeunit 7009701;
      cFunSeg@1100253001 : Codeunit 7009751;
      ventana@1103355016 : Dialog;
      Text030@1100253000 : TextConst 'ENU=The hotel or tour operator cannot be changed in a contract with reservations associated.;ESP=No se puede cambiar el hotel o touroperador de un contrato con reservas asociadas.';
      Text031@1100217000 : TextConst 'ENU=You must configure the accounts of the touroperator %1.;ESP=Debe configurar las cuentas del touroperador %1.';
      Text032@1100217001 : TextConst 'ENU=SHC;ESP=CVH';

    PROCEDURE switch@1();
    BEGIN
      // switch

      VALIDATE(Confirmado, FALSE);
      VALIDATE(Enviar, TRUE);
    END;

    PROCEDURE borrado_registro@2();
    VAR
      lrCom@1100253000 : Record 7009784;
    BEGIN
      // borrado_registro

      rRsv.RESET;
      rRsv.SETCURRENTKEY(Delegacion,
                         "Contrato venta");
      rRsv.SETRANGE(Delegacion      , Delegacion);
      rRsv.SETRANGE("Contrato venta", "N§ Contrato venta");
      IF rRsv.FINDSET(TRUE) THEN
        ERROR(Text016);

      rPre.RESET;
      rPre.SETRANGE(Delegacion   , Delegacion);
      rPre.SETRANGE("N§ Contrato", "N§ Contrato venta");
      IF rPre.FINDSET(TRUE) THEN
        rPre.DELETEALL(TRUE);

      rCupo.RESET;
      rCupo.SETRANGE(Delegacion         , Delegacion);
      rCupo.SETRANGE("N§ Contrato venta", "N§ Contrato venta");
      IF rCupo.FINDSET(TRUE) THEN
        rCupo.DELETEALL(TRUE);

      rCupoDia.RESET;
      rCupoDia.SETRANGE(Delegacion      , Delegacion);
      rCupoDia.SETRANGE("Contrato venta", "N§ Contrato venta");
      IF rCupoDia.FINDSET(TRUE) THEN
        rCupoDia.DELETEALL(TRUE);

      rConHab.RESET;
      rConHab.SETRANGE(Delegacion     , Delegacion);
      rConHab.SETRANGE("N§ Contrato"  , "N§ Contrato venta");
      rConHab.SETRANGE("Tipo contrato", rConHab."Tipo contrato"::Venta);
      IF rConHab.FINDSET(TRUE) THEN
        rConHab.DELETEALL(TRUE);

      rPrev.RESET;
      rPrev.SETRANGE(Delegacion     , Delegacion);
      rPrev.SETRANGE("Tipo Contrato", rPrev."Tipo Contrato"::Venta);
      rPrev.SETRANGE("N§ Contrato"  , "N§ Contrato venta");
      IF rPrev.FINDSET(TRUE) THEN
        rPrev.DELETEALL(TRUE);

      //+$001
      lrCom.RESET;
      lrCom.SETRANGE(Delegacion     , Delegacion);
      lrCom.SETRANGE("N§ Contrato"  , "N§ Contrato venta");
      lrCom.SETRANGE("Tipo Contrato", lrCom."Tipo Contrato"::"V Hotel");
      IF lrCom.FINDSET(TRUE) THEN
        lrCom.DELETEALL;
      //+$001
    END;

    PROCEDURE cambiar_booking@3();
    BEGIN
      // cambiar_booking

      rCupo.RESET;
      rCupo.SETRANGE(Delegacion         , Delegacion);
      rCupo.SETRANGE("N§ Contrato venta", "N§ Contrato venta");
      IF rCupo.FINDSET THEN BEGIN
        ventana.OPEN(Text017 +
                     '\' +
                     Text018);
        REPEAT
          ventana.UPDATE(1, rCupo."Habitacion cupo");

          //. Borrar registros de booking

          rCupoDia.RESET;
          rCupoDia.SETRANGE(Delegacion      , Delegacion);
          rCupoDia.SETRANGE("Contrato venta", "N§ Contrato venta");
          rCupoDia.SETRANGE(Hotel           , xRec.Hotel);
          rCupoDia.SETRANGE(TourOperador    , xRec.TourOperador);
          rCupoDia.SETRANGE(Dia             , rCupo."Fecha desde", rCupo."Fecha hasta");
          rCupoDia.SETRANGE(Habitacion      , rCupo."Habitacion cupo");
          IF rCupoDia.FINDSET(TRUE) THEN
            rCupoDia.DELETEALL;

          cCon.activar_cupo_venta(rCupo, Rec);
        UNTIL rCupo.NEXT = 0;
        ventana.CLOSE;
      END;
      rCupo.RESET;
    END;

    PROCEDURE InsertaLinAction@1103355000();
    VAR
      lwRec@1103355000 : RecordRef;
      lcFunExp@1103355001 : Codeunit 7009911;
    BEGIN
      // InsertaLinAction
      // Inserta una linea para el borrado de la sincronizacion a la empresa Hotel Payment

      lwRec.GETTABLE(Rec);
      lcFunExp.InsertaLinAct(lwRec);
    END;

    LOCAL PROCEDURE _ControlReservas@1100253000();
    BEGIN
      // _ControlReservas

      rRsv.RESET;
      rRsv.SETCURRENTKEY(Delegacion, "Contrato venta", Responsable);
      rRsv.SETRANGE(Delegacion      , Delegacion);
      rRsv.SETRANGE("Contrato venta", "N§ Contrato venta");
      IF NOT rRsv.ISEMPTY THEN
        ERROR(Text030);
    END;

    LOCAL PROCEDURE _BuscarCliente@1100217000();
    VAR
      lrCtaTO@1100217000 : Record 7009705;
    BEGIN
      // _BuscarCliente

      lrCtaTO.RESET;
      IF NOT lrCtaTO.GET(Delegacion, TourOperador) THEN
      BEGIN
        IF NOT lrCtaTO.GET('', TourOperador) THEN
        BEGIN
          IF NOT lrCtaTO.GET(Delegacion, '') THEN
            ERROR(Text031);
        END;
      END;

      IF lrCtaTO."Cliente Hotel" <> '' THEN
        VALIDATE(Cliente, lrCtaTO."Cliente Hotel");
    END;

    BEGIN
    {
      $001 AJS 20122011 Modifico la funcion borrado_registro para incluir los comentarios

      $003 AJS 28082012 El cliente se obtiene a partir de la tabla de Cuentas TO-Delegacion

      $004 AJS 09102012 Marca como obsoletos los campos calculados que iban sobre pago garantia venta

      $005 AJS 21122012 Eliminar table relation en el campo "N§ Contrato venta"

      $006 AJS 02042014 Al crear el contrato rellenamos la serie con la definida en gestion financiera

      $007 AJS 23062014 Marcamos el campo Cod. Serie como obsoleto

      $008 AJS 30062014 QNT-CAR-14001, Nuevos campos "Max. Personas x reserva" y "Max. Habitaciones x reserva" para
                        la integracion con SIAR

      $008 MNC 22072014 CNT-LP-14054 No se pueden usar TTOO bloqueados
    }
    END.
  }
}
