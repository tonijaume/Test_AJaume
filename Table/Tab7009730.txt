OBJECT Table 7009730 Cupos contrato venta
{
  OBJECT-PROPERTIES
  {
    Date=27/06/14;
    Time=12:25:33;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               rParAge.FINDFIRST;
               IF NOT rParAge."Empresa Delegacion" THEN
                 ERROR(Text001+
                       Text002);

               IF rConV.GET(Delegacion, "N§ Contrato venta") THEN
                 Activo := cCon.activar_cupo_venta(Rec, rConV);

               VALIDATE( Replicado );
             END;

    OnModify=BEGIN
               rParAge.FINDFIRST;
               IF NOT rParAge."Empresa Delegacion" THEN
                 ERROR(Text003+
                       Text002);

               rConV.RESET;
               IF rConV.GET(Delegacion, "N§ Contrato venta") THEN BEGIN
                 // Borrar registros de booking
                 //
                 rCupoDia.RESET;
                 rCupoDia.SETRANGE(Delegacion      , rConV.Delegacion);
                 rCupoDia.SETRANGE("Contrato venta", rConV."N§ Contrato venta");
                 rCupoDia.SETRANGE(Hotel           , rConV.Hotel);
                 rCupoDia.SETRANGE(TourOperador    , rConV.TourOperador);
                 rCupoDia.SETRANGE(Dia             , xRec."Fecha desde", xRec."Fecha hasta");
                 rCupoDia.SETRANGE(Habitacion      , xRec."Habitacion cupo");
                 IF rCupoDia.FINDSET(TRUE) THEN
                   rCupoDia.DELETEALL;
                 rCupoDia.RESET;
               END;

               Activo := cCon.activar_cupo_venta(Rec, rConV);

               VALIDATE( Replicado );
             END;

    OnDelete=BEGIN
               rParAge.FINDFIRST;
               IF NOT rParAge."Empresa Delegacion" THEN
                 ERROR(Text004+
                       Text002);

               cFunSeg.TienePermiso(Text006, TRUE);

               rConV.RESET;
               IF rConV.GET(Delegacion, "N§ Contrato venta") THEN BEGIN
                 // Borrar registros de booking
                 //
                 rCupoDia.RESET;
                 rCupoDia.SETRANGE(Delegacion      , rConV.Delegacion);
                 rCupoDia.SETRANGE("Contrato venta", rConV."N§ Contrato venta");
                 rCupoDia.SETRANGE(Hotel           , rConV.Hotel);
                 rCupoDia.SETRANGE(TourOperador    , rConV.TourOperador);
                 rCupoDia.SETRANGE(Dia             , "Fecha desde", "Fecha hasta");
                 rCupoDia.SETRANGE(Habitacion      , "Habitacion cupo");
                 IF rCupoDia.FINDSET(TRUE) THEN
                   rCupoDia.DELETEALL;
                 rCupoDia.RESET;
               END;
             END;

    OnRename=BEGIN
               rParAge.FINDFIRST;
               IF NOT rParAge."Empresa Delegacion" THEN
                 ERROR(Text008);

               cFunSeg.TienePermiso(Text006, TRUE);

               rConV.RESET;
               IF rConV.GET(Delegacion, "N§ Contrato venta") THEN BEGIN
                 // Borrar registros de booking
                 //
                 rCupoDia.RESET;
                 rCupoDia.SETRANGE(Delegacion      , rConV.Delegacion);
                 rCupoDia.SETRANGE("Contrato venta", rConV."N§ Contrato venta");
                 rCupoDia.SETRANGE(Hotel           , rConV.Hotel);
                 rCupoDia.SETRANGE(TourOperador    , rConV.TourOperador);
                 rCupoDia.SETRANGE(Dia             , xRec."Fecha desde", xRec."Fecha hasta");
                 rCupoDia.SETRANGE(Habitacion      , xRec."Habitacion cupo");
                 IF rCupoDia.FINDSET(TRUE) THEN
                   rCupoDia.DELETEALL;
                 rCupoDia.RESET;
               END;
               Activo := cCon.activar_cupo_venta(Rec, rConV);
               VALIDATE(Replicado);
             END;

    CaptionML=[ENU=Sale contract allotments;
               ESP=Cupos contrato venta];
    LookupFormID=Form7009742;
    DrillDownFormID=Form7009742;
  }
  FIELDS
  {
    { 1   ;   ;Delegacion          ;Code10        ;TableRelation=Delegacion.Codigo;
                                                   CaptionML=[ENU=Local Office;
                                                              ESP=Delegacion];
                                                   Description=PK, FK Delegacion;
                                                   Editable=No }
    { 3   ;   ;N§ Contrato venta   ;Code10        ;TableRelation="Contrato venta hotel"."N§ Contrato venta";
                                                   CaptionML=[ENU=Sale contract No.;
                                                              ESP=N§ Contrato venta];
                                                   Description=PK;
                                                   Editable=No }
    { 5   ;   ;Habitacion cupo     ;Code10        ;OnValidate=BEGIN
                                                                rConHab.RESET;
                                                                rConHab.SETRANGE(Delegacion       , Delegacion);
                                                                rConHab.SETRANGE("N§ Contrato"    , "N§ Contrato venta");
                                                                rConHab.SETRANGE("Habitacion cupo", "Habitacion cupo");
                                                                IF rConHab.FINDFIRST THEN
                                                                BEGIN
                                                                  "Habitacion basica" := rConHab."Habitacion basica";
                                                                  "Pax ocupacion"     := rConHab."Ocupacion Maxima";
                                                                END
                                                                ELSE
                                                                  ERROR(Text009);
                                                                switch;
                                                              END;

                                                   OnLookup=BEGIN
                                                              rConHab.RESET;
                                                              rConHab.SETRANGE(Delegacion     , Delegacion);
                                                              rConHab.SETRANGE("N§ Contrato"  , "N§ Contrato venta");
                                                              rConHab.SETRANGE("Tipo contrato", rConHab."Tipo contrato"::Venta);
                                                              IF FORM.RUNMODAL(7009741, rConHab) = ACTION::LookupOK THEN
                                                                VALIDATE("Habitacion cupo", rConHab."Habitacion cupo");
                                                              rConHab.SETRANGE(Delegacion);
                                                              rConHab.SETRANGE("N§ Contrato");
                                                              rConHab.SETRANGE("Tipo contrato");
                                                            END;

                                                   CaptionML=[ENU=Allotment room;
                                                              ESP=Habitacion cupo];
                                                   NotBlank=Yes;
                                                   Description=PK }
    { 7   ;   ;Fecha desde         ;Date          ;OnValidate=BEGIN
                                                                IF "Fecha hasta" <> 0D THEN
                                                                  IF "Fecha desde" > "Fecha hasta" THEN
                                                                    ERROR(Text010);
                                                                IF rConV.GET(Delegacion, "N§ Contrato venta") THEN BEGIN
                                                                  IF ("Fecha desde" < rConV."Fecha inicio contrato") OR ("Fecha desde" > rConV."Fecha final contrato") THEN
                                                                    ERROR(Text011);
                                                                END;
                                                                switch;
                                                              END;

                                                   CaptionML=[ENU=Date From;
                                                              ESP=Fecha desde];
                                                   NotBlank=Yes;
                                                   Description=PK }
    { 9   ;   ;Fecha hasta         ;Date          ;OnValidate=BEGIN
                                                                IF "Fecha hasta" <> 0D THEN
                                                                  IF "Fecha desde" > "Fecha hasta" THEN
                                                                    ERROR(Text010);
                                                                IF rConV.GET(Delegacion, "N§ Contrato venta") THEN BEGIN
                                                                  IF ("Fecha hasta" < rConV."Fecha inicio contrato") OR ("Fecha hasta" > rConV."Fecha final contrato") THEN
                                                                    ERROR(Text011);
                                                                END;
                                                                switch;
                                                              END;

                                                   CaptionML=[ENU=Date To;
                                                              ESP=Fecha hasta];
                                                   NotBlank=Yes;
                                                   Description=PK }
    { 11  ;   ;Cupo                ;Integer       ;OnValidate=BEGIN
                                                                switch;
                                                              END;

                                                   CaptionML=[ENU=Allotment;
                                                              ESP=Cupo] }
    { 13  ;   ;Release             ;Integer       ;OnValidate=BEGIN
                                                                switch;
                                                              END;

                                                   CaptionML=[ENU=Release;
                                                              ESP=Release] }
    { 15  ;   ;Pax Legal           ;Integer       ;OnValidate=BEGIN
                                                                switch;
                                                              END;

                                                   CaptionML=[ENU=Legal pax;
                                                              ESP=Pax Legal] }
    { 17  ;   ;Habitacion basica   ;Code10        ;TableRelation="Habitacion basica"."Codigo Habitacion";
                                                   CaptionML=[ENU=Basic Room;
                                                              ESP=Habitacion basica] }
    { 19  ;   ;Activo              ;Boolean       ;InitValue=No;
                                                   CaptionML=[ENU=Active;
                                                              ESP=Activo];
                                                   Editable=No }
    { 20  ;   ;Replicado           ;Boolean       ;InitValue=No;
                                                   OnValidate=BEGIN
                                                                Replicado:= FALSE;

                                                                IF rCVenta.GET(Delegacion,"N§ Contrato venta") THEN
                                                                   rCVenta.MODIFY(TRUE);
                                                              END;

                                                   CaptionML=[ENU=Replicated;
                                                              ESP=Replicado];
                                                   Description=Campo usado por el replicador }
    { 21  ;   ;Pax ocupacion       ;Integer       ;CaptionML=[ENU=Occupancy pax;
                                                              ESP=Pax ocupacion];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Delegacion,N§ Contrato venta,Habitacion cupo,Fecha desde,Fecha hasta;
                                                   Clustered=Yes }
    {    ;Delegacion,N§ Contrato venta,Fecha desde,Fecha hasta,Habitacion cupo;
                                                   KeyGroups=RENUMERING }
    { No ;Replicado                                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1103355010 : TextConst 'ENU=Only is possible to enter sale allotments in\;ESP=Solo es posible dar de alta cupos de venta en\';
      Text002@1103355011 : TextConst 'ENU=the local offices.;ESP=las delegaciones.';
      Text003@1103355012 : TextConst 'ENU=Only is possible to modify sale allotments in\;ESP=Solo es posible modificar cupos de venta en\';
      Text004@1103355013 : TextConst 'ENU=Only is possible to cancel sale allotments in\;ESP=Solo es posible dar de baja cupos de venta en\';
      Text006@1103355015 : TextConst 'ENU=FUN-ACT-COV;ESP=FUN-ACT-COV';
      Text008@1103355017 : TextConst 'ENU=Only is possible to rename sale allotments in the local offices\;ESP=Solo es posible renombrar cupos de venta en las delegaciones.';
      Text009@1103355018 : TextConst 'ENU=Conversion of this code does not exist;ESP=No existe la conversion de este codigo.';
      Text010@1103355019 : TextConst 'ENU=Date from must be lower than date to;ESP=La fecha desde debe ser anterior a la fecha hasta.';
      Text011@1103355020 : TextConst 'ENU=Date from is out of the range of the contract;ESP=La fecha desde esta fuera de los rangos del contrato.';
      rConV@1103355002 : Record 7009727;
      rConHab@1103355003 : Record 7009731;
      rParAge@1103355004 : Record 7009700;
      rCupoDia@1103355006 : Record 7009758;
      cCon@1103355007 : Codeunit 7009701;
      cFunSeg@1100253000 : Codeunit 7009751;
      rCVenta@1103355008 : Record 7009727;

    PROCEDURE switch@1();
    BEGIN
      Activo := FALSE;
    END;

    BEGIN
    {
      $001 AJS 27062014 Elimino la variable Booking Venta
    }
    END.
  }
}
