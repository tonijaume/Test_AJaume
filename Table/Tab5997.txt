OBJECT Table 5997 Standard Service Line
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               LOCKTABLE;
               DimDoc.LOCKTABLE;
               StdServCode.GET("Standard Service Code");
               DimMgt.InsertDocDim(
                 DATABASE::"Standard Service Line",DimDoc."Document Type"::" ","Standard Service Code","Line No.",
                 "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
             END;

    OnDelete=BEGIN
               DimMgt.DeleteDocDim(
                 DATABASE::"Standard Service Line",DimDoc."Document Type"::" ","Standard Service Code","Line No.");
             END;

    OnRename=BEGIN
               ERROR(Text000,TABLECAPTION);
             END;

    CaptionML=[ENU=Standard Service Line;
               ESP=L¡nea servicio est ndar];
    PasteIsValid=No;
  }
  FIELDS
  {
    { 1   ;   ;Standard Service Code;Code10       ;TableRelation="Standard Service Code";
                                                   CaptionML=[ENU=Standard Service Code;
                                                              ESP=C¢digo servicio est ndar];
                                                   Editable=No }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ESP=N§ l¡nea];
                                                   Editable=No }
    { 3   ;   ;Type                ;Option        ;OnValidate=VAR
                                                                OldType@1000 : Integer;
                                                              BEGIN
                                                                OldType := Type;
                                                                INIT;
                                                                Type := OldType;
                                                                DimMgt.DeleteDocDim(
                                                                  DATABASE::"Standard Service Line",DimDoc."Document Type"::" ","Standard Service Code","Line No.");
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              ESP=Tipo];
                                                   OptionCaptionML=[ENU=" ,Item,Resource,Cost,G/L Account";
                                                                    ESP=" ,Producto,Recurso,Coste,Cuenta"];
                                                   OptionString=[ ,Item,Resource,Cost,G/L Account] }
    { 4   ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST(" ")) "Standard Text"
                                                                 ELSE IF (Type=CONST(Item)) Item
                                                                 ELSE IF (Type=CONST(Resource)) Resource
                                                                 ELSE IF (Type=CONST(Cost)) "Service Cost"
                                                                 ELSE IF (Type=CONST(G/L Account)) "G/L Account";
                                                   OnValidate=VAR
                                                                StdTxt@1000 : Record 7;
                                                                GLAcc@1001 : Record 15;
                                                                Item@1002 : Record 27;
                                                                Res@1003 : Record 156;
                                                                ServCost@1004 : Record 5905;
                                                              BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  Quantity := 0;
                                                                  "Amount Excl. VAT" := 0;
                                                                  "Unit of Measure Code" := '';
                                                                  Description := '';
                                                                  IF "No." = '' THEN
                                                                    EXIT;
                                                                  StdServCode.GET("Standard Service Code");
                                                                  CASE Type OF
                                                                    Type::" ":
                                                                      BEGIN
                                                                        StdTxt.GET("No.");
                                                                        Description := StdTxt.Description;
                                                                      END;
                                                                    Type::Item:
                                                                      BEGIN
                                                                        Item.GET("No.");
                                                                        Item.TESTFIELD(Blocked,FALSE);
                                                                        Item.TESTFIELD("Inventory Posting Group");
                                                                        Item.TESTFIELD("Gen. Prod. Posting Group");
                                                                        Description := Item.Description;
                                                                        "Unit of Measure Code" := Item."Sales Unit of Measure";
                                                                        "Variant Code" := '';
                                                                      END;
                                                                    Type::Resource:
                                                                      BEGIN
                                                                        Res.GET("No.");
                                                                        Res.TESTFIELD(Blocked,FALSE);
                                                                        Res.TESTFIELD("Gen. Prod. Posting Group");
                                                                        Description := Res.Name;
                                                                        "Unit of Measure Code" := Res."Base Unit of Measure";
                                                                      END;
                                                                    Type::Cost:
                                                                      BEGIN
                                                                        ServCost.GET("No.");
                                                                        GLAcc.GET(ServCost."Account No.");
                                                                        GLAcc.TESTFIELD("Gen. Prod. Posting Group");
                                                                        Description := ServCost.Description;
                                                                        Quantity := ServCost."Default Quantity";
                                                                        "Unit of Measure Code" := ServCost."Unit of Measure Code";
                                                                      END;
                                                                    Type::"G/L Account":
                                                                      BEGIN
                                                                        GLAcc.GET("No.");
                                                                        GLAcc.CheckGLAcc;
                                                                        GLAcc.TESTFIELD("Direct Posting",TRUE);
                                                                        Description := GLAcc.Name;
                                                                      END;
                                                                  END;
                                                                END;

                                                                CreateDim(DimMgt.TypeToTableID5(Type),"No.");
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              ESP=N§] }
    { 5   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n] }
    { 6   ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD(Type);
                                                                TESTFIELD("No.");
                                                                IF Quantity < 0 THEN
                                                                  FIELDERROR(Quantity,Text002);
                                                              END;

                                                   CaptionML=[ENU=Quantity;
                                                              ESP=Cantidad];
                                                   DecimalPlaces=0:5;
                                                   BlankZero=Yes }
    { 7   ;   ;Amount Excl. VAT    ;Decimal       ;OnValidate=BEGIN
                                                                IF Type <> Type::"G/L Account" THEN
                                                                  FIELDERROR(Type,STRSUBSTNO(Text001,Type));
                                                              END;

                                                   CaptionML=[ENU=Amount Excl. VAT;
                                                              ESP=Importe excl. IVA];
                                                   BlankZero=Yes;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr=GetCurrency }
    { 8   ;   ;Unit of Measure Code;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Unit of Measure".Code WHERE (Item No.=FIELD(No.))
                                                                 ELSE "Unit of Measure";
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Type);
                                                              END;

                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              ESP=C¢d. unidad medida] }
    { 9   ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
                                                              END;

                                                   CaptionML=[ENU=Shortcut Dimension 1 Code;
                                                              ESP=C¢d. dim. acceso dir. 1];
                                                   CaptionClass='1,2,1' }
    { 10  ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");
                                                              END;

                                                   CaptionML=[ENU=Shortcut Dimension 2 Code;
                                                              ESP=C¢d. dim. acceso dir. 2];
                                                   CaptionClass='1,2,2' }
    { 11  ;   ;Variant Code        ;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Variant".Code WHERE (Item No.=FIELD(No.));
                                                   OnValidate=VAR
                                                                Item@1000 : Record 27;
                                                                ItemVariant@1001 : Record 5401;
                                                              BEGIN
                                                                IF "Variant Code" = '' THEN BEGIN
                                                                  IF Type = Type::Item THEN BEGIN
                                                                    Item.GET("No.");
                                                                    Description := Item.Description;
                                                                  END;
                                                                  EXIT;
                                                                END;

                                                                TESTFIELD(Type,Type::Item);
                                                                ItemVariant.GET("No.","Variant Code");
                                                                Description := ItemVariant.Description;
                                                              END;

                                                   CaptionML=[ENU=Variant Code;
                                                              ESP=C¢d. variante] }
  }
  KEYS
  {
    {    ;Standard Service Code,Line No.          ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DimDoc@1000 : Record 357;
      StdServCode@1001 : Record 5996;
      DimMgt@1002 : Codeunit 408;
      Text000@1003 : TextConst 'ENU=You cannot rename a %1.;ESP=No se puede cambiar el nombre a %1.';
      Text001@1004 : TextConst 'ENU=must not be %1;ESP=No puede ser %1.';
      Text002@1005 : TextConst 'ENU=must be positive;ESP=debe ser positivo';

    PROCEDURE EmptyLine@5() : Boolean;
    BEGIN
      EXIT(("No." = '') AND (Quantity = 0))
    END;

    PROCEDURE InsertLine@1() : Boolean;
    BEGIN
      EXIT((Type = Type::" ") OR NOT EmptyLine);
    END;

    PROCEDURE GetCurrency@2() : Code[10];
    BEGIN
      IF StdServCode.GET("Standard Service Code") THEN
        EXIT(StdServCode."Currency Code");
      EXIT('');
    END;

    PROCEDURE ShowDimensions@25();
    VAR
      DocDim@1000 : Record 357;
      DocDimensions@1001 : Form 546;
    BEGIN
      TESTFIELD("Standard Service Code");
      TESTFIELD("Line No.");
      DocDim.SETRANGE("Table ID",DATABASE::"Standard Service Line");
      DocDim.SETRANGE("Document Type",DocDim."Document Type"::" ");
      DocDim.SETRANGE("Document No.","Standard Service Code");
      DocDim.SETRANGE("Line No.","Line No.");
      DocDimensions.SETTABLEVIEW(DocDim);
      DocDimensions.RUNMODAL;
    END;

    PROCEDURE ValidateShortcutDimCode@29(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateDimValueCode(FieldNumber,ShortcutDimCode);
      IF "Line No." <> 0 THEN BEGIN
        DimMgt.SaveDocDim(
          DATABASE::"Standard Service Line",DimDoc."Document Type"::" ","Standard Service Code",
          "Line No.",FieldNumber,ShortcutDimCode);
        MODIFY;
      END ELSE
        DimMgt.SaveTempDim(FieldNumber,ShortcutDimCode);
    END;

    PROCEDURE LookupShortcutDimCode@28(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.LookupDimValueCode(FieldNumber,ShortcutDimCode);
      IF "Line No." <> 0 THEN BEGIN
        DimMgt.SaveDocDim(
          DATABASE::"Standard Service Line",DimDoc."Document Type"::" ","Standard Service Code",
          "Line No.",FieldNumber,ShortcutDimCode);
        MODIFY;
      END ELSE
        DimMgt.SaveTempDim(FieldNumber,ShortcutDimCode);
    END;

    PROCEDURE ShowShortcutDimCode@27(VAR ShortcutDimCode@1000 : ARRAY [8] OF Code[20]);
    BEGIN
      IF "Line No." <> 0 THEN
        DimMgt.ShowDocDim(
          DATABASE::"Standard Service Line",DimDoc."Document Type"::" ","Standard Service Code",
          "Line No.",ShortcutDimCode)
      ELSE
        DimMgt.ShowTempDim(ShortcutDimCode);
    END;

    PROCEDURE CreateDim@26(Type1@1000 : Integer;No1@1001 : Code[20]);
    VAR
      SourceCodeSetup@1006 : Record 242;
      ServItemLine1@1009 : Record 5901;
      TableID@1007 : ARRAY [10] OF Integer;
      No@1008 : ARRAY [10] OF Code[20];
    BEGIN
      SourceCodeSetup.GET;

      TableID[1] := Type1;
      No[1] := No1;
      "Shortcut Dimension 1 Code" := '';
      "Shortcut Dimension 2 Code" := '';

      DimMgt.GetPreviousDocDefaultDim(
        DATABASE::"Standard Service Line",DimDoc."Document Type"::" ","Standard Service Code",0,
        DimMgt.TypeToTableID5(Type),"Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");

      DimMgt.GetDefaultDim(
        TableID,No,SourceCodeSetup."Service Management",
        "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
      IF "Line No." <> 0 THEN
        DimMgt.UpdateDocDefaultDim(
          DATABASE::"Standard Service Line",DimDoc."Document Type"::" ","Standard Service Code","Line No.",
          "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
    END;

    BEGIN
    END.
  }
}
