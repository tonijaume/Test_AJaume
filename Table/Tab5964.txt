OBJECT Table 5964 Service Contract Line
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD(Description);
               GetServContractHeader;
               ServContractHeader.TESTFIELD("Customer No.");
               ServContractHeader.TESTFIELD("Contract No.");
               ServContractHeader.TESTFIELD("Starting Date");
               IF "Service Item No." <> '' THEN BEGIN
                 ServContractHeader.TESTFIELD("Service Period");
                 ServContractHeader.TESTFIELD("First Service Date");
               END;

               ServMgtSetup.GET;

               UpdateContractAnnualAmount(FALSE);

               IF ("Service Item No." = '') AND ("Response Time (Hours)" = 0) THEN
                 "Response Time (Hours)" := ServContractHeader."Response Time (Hours)";

               IF "Contract Type" = "Contract Type":: Contract THEN BEGIN
                 IF "Service Item No." <> '' THEN BEGIN
                   IF ServMgtSetup."Register Contract Changes" THEN
                     ContractChangeLog.LogContractChange(
                       "Contract No.",1,FIELDCAPTION("Service Item No."),1,'',
                       FORMAT("Service Item No."),"Service Item No.","Line No.");
                   ServLogMgt.ServItemAddToContract(Rec);
                 END ELSE
                   IF ServMgtSetup."Register Contract Changes" THEN
                     ContractChangeLog.LogContractChange(
                       "Contract No.",1,FIELDCAPTION(Description),1,'',Description,'',"Line No.");
               END;
             END;

    OnModify=BEGIN
               IF UseServContractLineAsxRec THEN BEGIN
                 xRec := ServContractLine;
                 UseServContractLineAsxRec := FALSE;
               END;

               IF ("Service Item No." = '') AND
                  ("Item No." = '') AND
                  (Description = '')
               THEN
                 ERROR(Text016,FIELDCAPTION(Description));

               ServMgtSetup.GET;
               IF ServMgtSetup."Register Contract Changes" THEN
                 IF "Contract Type" = "Contract Type"::Contract THEN
                   LogContractLineChanges(xRec);

               IF "Line Amount" <> xRec."Line Amount" THEN
                 UpdateContractAnnualAmount(FALSE);

               IF ("Service Item No." <> xRec."Service Item No.") THEN BEGIN
                 ServLogMgt.ServItemAddToContract(Rec);
                 ServLogMgt.ServItemRemovedFromContract(xRec);
               END;
             END;

    OnDelete=BEGIN
               TestStatusOpen;
               IF "Contract Type" = "Contract Type"::Contract THEN BEGIN
                 GetServContractHeader;
                 IF ServContractHeader.Status = ServContractHeader.Status::Canceled THEN
                   ERROR(Text015,ServContractHeader.Status);
                 IF (ServContractHeader.Status = ServContractHeader.Status::Signed) AND
                    (NOT "New Line") AND
                    (ServContractHeader."Automatic Credit Memos")
                 THEN BEGIN
                   TESTFIELD("Contract Expiration Date");
                   CreateCreditfromContractLines.RUN(Rec);
                 END;

                 IF (ServContractHeader.Status = ServContractHeader.Status::Signed) AND
                    (NOT "New Line") AND
                    (NOT ServContractHeader."Automatic Credit Memos")
                 THEN
                   IF (CreditMemoBaseExists) AND
                      (NOT StatusCheckSuspended)
                   THEN
                     IF NOT CONFIRM(Text022,FALSE) THEN
                       ERROR(Text023);
                 ServMgtSetup.GET;
                 IF "Service Item No." <> '' THEN BEGIN
                   IF ServMgtSetup."Register Contract Changes" THEN
                     ContractChangeLog.LogContractChange(
                       "Contract No.",1,FIELDCAPTION("Service Item No."),2,
                       FORMAT("Service Item No."),'',"Service Item No.","Line No.");
                   ServLogMgt.ServItemRemovedFromContract(Rec);
                 END ELSE
                   IF ServMgtSetup."Register Contract Changes" THEN
                     ContractChangeLog.LogContractChange(
                       "Contract No.",1,FIELDCAPTION(Description),2,Description,'','',"Line No.");

                 GetServContractHeader;
                 IF(NOT ServContractHeader."Allow Unbalanced Amounts") AND
                   (ServContractHeader.Status = ServContractHeader.Status::Signed)
                 THEN
                   ContractGainLossEntry.AddEntry(1,"Contract Type","Contract No.",-"Line Amount",'');
               END;

               ServCommentLine.SETRANGE("Table Name",ServCommentLine."Table Name"::"Service Contract");
               ServCommentLine.SETRANGE("Table Subtype","Contract Type");
               ServCommentLine.SETRANGE("No.","Contract No.");
               ServCommentLine.SETRANGE(Type,ServCommentLine.Type::General);
               ServCommentLine.SETRANGE("Table Line No.","Line No.");
               ServCommentLine.DELETEALL;

               UpdateContractAnnualAmount(TRUE);
             END;

    CaptionML=[ENU=Service Contract Line;
               ESP=L¡nea contrato servicio];
    PasteIsValid=No;
    LookupFormID=Form6075;
    DrillDownFormID=Form6075;
  }
  FIELDS
  {
    { 1   ;   ;Contract Type       ;Option        ;CaptionML=[ENU=Contract Type;
                                                              ESP=Tipo contrato];
                                                   OptionCaptionML=[ENU=Quote,Contract;
                                                                    ESP=Oferta,Contrato];
                                                   OptionString=Quote,Contract }
    { 2   ;   ;Contract No.        ;Code20        ;TableRelation="Service Contract Header"."Contract No." WHERE (Contract Type=FIELD(Contract Type));
                                                   CaptionML=[ENU=Contract No.;
                                                              ESP=N§ contrato] }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ESP=N§ l¡nea] }
    { 4   ;   ;Contract Status     ;Option        ;CaptionML=[ENU=Contract Status;
                                                              ESP=Estado contrato];
                                                   OptionCaptionML=[ENU=" ,Signed,Cancelled";
                                                                    ESP=" ,Firmado,Cancelado"];
                                                   OptionString=[ ,Signed,Cancelled] }
    { 5   ;   ;Service Item No.    ;Code20        ;TableRelation="Service Item";
                                                   OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                GetServContractHeader;
                                                                IF (ServContractHeader.Status = ServContractHeader.Status::Signed) AND
                                                                   (NOT "New Line")
                                                                THEN
                                                                  ERROR(Text013,FIELDCAPTION("Service Item No."));

                                                                IF "Service Item No." <> '' THEN BEGIN
                                                                  GetServItem;
                                                                  TESTFIELD("Customer No.");
                                                                  IF ServItem."Customer No." <> ServContractHeader."Customer No." THEN
                                                                    ERROR(Text000,ServItem.TABLECAPTION,FIELDCAPTION("Customer No."),"Customer No.");

                                                                  ServContractLine.RESET;
                                                                  ServContractLine.SETRANGE("Contract No.","Contract No.");
                                                                  ServContractLine.SETRANGE("Contract Type","Contract Type");
                                                                  ServContractLine.SETRANGE("Service Item No.","Service Item No.");
                                                                  ServContractLine.SETFILTER("Line No.",'<>%1',"Line No.");
                                                                  IF ServContractLine.FIND('-') THEN
                                                                    ERROR(Text003,ServItem.TABLECAPTION,ServContractHeader.TABLECAPTION);

                                                                  IF NOT HideDialog THEN BEGIN
                                                                    ServContractLine.RESET;
                                                                    ServContractLine.SETCURRENTKEY("Service Item No.","Contract Status");
                                                                    ServContractLine.SETRANGE("Service Item No.","Service Item No.");
                                                                    ServContractLine.SETFILTER("Contract Status",'<>%1',ServContractLine."Contract Status"::Cancelled);
                                                                    ServContractLine.SETRANGE("Contract Type",ServContractLine."Contract Type"::Contract);
                                                                    ServContractLine.SETFILTER("Contract No.",'<>%1',"Contract No.");
                                                                    IF ServContractLine.FIND('-') THEN BEGIN
                                                                      IF NOT CONFIRM(Text019,TRUE,ServItem.TABLECAPTION,"Service Item No.") THEN BEGIN
                                                                        "Service Item No." := xRec."Service Item No.";
                                                                        EXIT
                                                                      END;
                                                                    END ELSE BEGIN
                                                                      ServContractLine.RESET;
                                                                      ServContractLine.SETCURRENTKEY("Service Item No.");
                                                                      ServContractLine.SETRANGE("Service Item No.","Service Item No.");
                                                                      ServContractLine.SETRANGE("Contract Type",ServContractLine."Contract Type"::Quote);
                                                                      ServContractLine.SETFILTER("Contract No.",'<>%1',"Contract No.");
                                                                      IF ServContractLine.FIND('-') THEN
                                                                        IF NOT CONFIRM(Text019,TRUE,ServItem.TABLECAPTION,"Service Item No.")  THEN BEGIN
                                                                          "Service Item No." := xRec."Service Item No.";
                                                                          EXIT
                                                                        END;
                                                                    END;
                                                                  END;

                                                                  IF (ServItem."Ship-to Code" <> ServContractHeader."Ship-to Code") AND
                                                                     NOT HideDialog
                                                                  THEN
                                                                    IF NOT CONFIRM(
                                                                             Text001,FALSE,
                                                                             ServItem.TABLECAPTION,"Service Item No.")
                                                                    THEN BEGIN
                                                                      "Service Item No." := xRec."Service Item No.";
                                                                      EXIT;
                                                                    END;
                                                                  "Ship-to Code" := ServItem."Ship-to Code";
                                                                END;
                                                                TempServContractLine := Rec;
                                                                INIT;
                                                                "Starting Date" := TempServContractLine."Starting Date";
                                                                "Contract Expiration Date" := TempServContractLine."Contract Expiration Date";
                                                                "Credit Memo Date" := TempServContractLine."Credit Memo Date";
                                                                "Next Planned Service Date" := TempServContractLine."Next Planned Service Date";
                                                                "Service Period" := TempServContractLine."Service Period";
                                                                "Customer No." := TempServContractLine."Customer No.";
                                                                IF TempServContractLine."Service Item No." <> '' THEN
                                                                  "Ship-to Code" := TempServContractLine."Ship-to Code"
                                                                ELSE
                                                                  "Ship-to Code" := ServContractHeader."Ship-to Code";
                                                                "Contract Status" := TempServContractLine."Contract Status";
                                                                "Service Item No." := TempServContractLine."Service Item No.";
                                                                IF "Service Item No." = '' THEN
                                                                  EXIT;
                                                                Description := ServItem.Description;
                                                                "Serial No." := ServItem."Serial No.";
                                                                "Service Item Group Code" := ServItem."Service Item Group Code";
                                                                "Item No." := ServItem."Item No.";
                                                                "Variant Code" := ServItem."Variant Code";
                                                                "Unit of Measure Code" := ServItem."Unit of Measure Code";
                                                                IF (ServContractHeader."Response Time (Hours)" < ServItem."Response Time (Hours)") AND
                                                                   (ServContractHeader."Response Time (Hours)" <> 0)
                                                                THEN
                                                                  "Response Time (Hours)" := ServContractHeader."Response Time (Hours)"
                                                                ELSE
                                                                  IF ServItem."Response Time (Hours)" <> 0 THEN
                                                                    "Response Time (Hours)" := ServItem."Response Time (Hours)"
                                                                  ELSE
                                                                    "Response Time (Hours)" := ServContractHeader."Response Time (Hours)";
                                                                ServMgtSetup.GET;
                                                                VALIDATE("Line Cost",ServItem."Default Contract Cost");
                                                                VALIDATE("Line Value",ServItem."Default Contract Value");
                                                                VALIDATE("Line Discount %",ServItem."Default Contract Discount %");

                                                                IF ServContractLine.GET("Contract Type","Contract No.","Line No.") THEN BEGIN
                                                                  UseServContractLineAsxRec := TRUE;
                                                                  MODIFY(TRUE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Service Item No.;
                                                              ESP=N§ prod. servicio] }
    { 6   ;   ;Description         ;Text30        ;OnValidate=BEGIN
                                                                TestStatusOpen;
                                                              END;

                                                   CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n] }
    { 7   ;   ;Serial No.          ;Code20        ;OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                TESTFIELD("Item No.");
                                                              END;

                                                   CaptionML=[ENU=Serial No.;
                                                              ESP=N§ serie] }
    { 8   ;   ;Service Item Group Code;Code10     ;TableRelation="Service Item Group";
                                                   OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                TESTFIELD("Service Item No.");
                                                              END;

                                                   CaptionML=[ENU=Service Item Group Code;
                                                              ESP=C¢d. grupo prod. servicio] }
    { 9   ;   ;Customer No.        ;Code20        ;TableRelation=Customer;
                                                   CaptionML=[ENU=Customer No.;
                                                              ESP=N§ cliente] }
    { 10  ;   ;Ship-to Code        ;Code10        ;TableRelation="Ship-to Address".Code WHERE (Customer No.=FIELD(Customer No.));
                                                   CaptionML=[ENU=Ship-to Code;
                                                              ESP=C¢d. direcci¢n env¡o cliente];
                                                   Editable=No }
    { 11  ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                IF "Item No." <> xRec."Item No." THEN
                                                                  TESTFIELD("Service Item No.",'');

                                                                IF ("Item No." <> '') THEN BEGIN
                                                                  Item.GET("Item No.");
                                                                  Currency.InitRoundingPrecision;
                                                                  Description := Item.Description;
                                                                  "Unit of Measure Code" := Item."Sales Unit of Measure";
                                                                  GetServContractHeader;
                                                                  "Response Time (Hours)" := ServContractHeader."Response Time (Hours)";
                                                                  ServMgtSetup.GET;
                                                                  "Line Cost" :=
                                                                    ROUND(Item."Unit Cost" * ServMgtSetup."Contract Value %" / 100,
                                                                    Currency."Amount Rounding Precision");
                                                                  "Line Discount %" := 0;
                                                                  CASE ServMgtSetup."Contract Value Calc. Method" OF
                                                                    ServMgtSetup."Contract Value Calc. Method"::"Based on Unit Price":
                                                                      "Line Value" :=
                                                                        ROUND(Item."Unit Price" * ServMgtSetup."Contract Value %" / 100,
                                                                        Currency."Amount Rounding Precision");
                                                                    ServMgtSetup."Contract Value Calc. Method"::"Based on Unit Cost":
                                                                      "Line Value" :=
                                                                        ROUND(Item."Unit Cost" * ServMgtSetup."Contract Value %" / 100,
                                                                        Currency."Amount Rounding Precision");
                                                                  END;
                                                                  VALIDATE("Line Value","Line Value");
                                                                END ELSE BEGIN
                                                                  "Unit of Measure Code" := '';
                                                                  "Variant Code" := '';
                                                                  "Serial No." := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Item No.;
                                                              ESP=N§ producto] }
    { 12  ;   ;Unit of Measure Code;Code10        ;TableRelation=IF (Item No.=FILTER(<>'')) "Item Unit of Measure".Code WHERE (Item No.=FIELD(Item No.))
                                                                 ELSE "Unit of Measure";
                                                   OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                IF "Unit of Measure Code" <> xRec."Unit of Measure Code" THEN BEGIN
                                                                  TESTFIELD("Service Item No.",'');
                                                                  TESTFIELD("Item No.");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              ESP=C¢d. unidad medida] }
    { 13  ;   ;Response Time (Hours);Decimal      ;OnValidate=BEGIN
                                                                TestStatusOpen;
                                                              END;

                                                   CaptionML=[ENU=Response Time (Hours);
                                                              ESP=Tiempo respuesta (Horas)];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 14  ;   ;Last Planned Service Date;Date     ;CaptionML=[ENU=Last Planned Service Date;
                                                              ESP=élt. fecha serv. planificada];
                                                   Editable=No }
    { 15  ;   ;Next Planned Service Date;Date     ;OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                IF ("Next Planned Service Date" <> 0D) AND
                                                                   ("Next Planned Service Date" < "Starting Date")
                                                                THEN
                                                                  ERROR(Text009,FIELDCAPTION("Next Planned Service Date"),FIELDCAPTION("Starting Date"));
                                                              END;

                                                   CaptionML=[ENU=Next Planned Service Date;
                                                              ESP=Siguiente fecha serv. planif.] }
    { 16  ;   ;Last Service Date   ;Date          ;CaptionML=[ENU=Last Service Date;
                                                              ESP=élt. fecha servicio] }
    { 17  ;   ;Last Preventive Maint. Date;Date   ;CaptionML=[ENU=Last Preventive Maint. Date;
                                                              ESP=élt. fecha manten. preventivo];
                                                   Editable=No }
    { 18  ;   ;Invoiced to Date    ;Date          ;CaptionML=[ENU=Invoiced to Date;
                                                              ESP=Facturado hasta fecha];
                                                   Editable=No }
    { 19  ;   ;Credit Memo Date    ;Date          ;OnValidate=BEGIN
                                                                TestStatusOpen;

                                                                TESTFIELD(Credited,FALSE);

                                                                IF "Credit Memo Date" <> 0D THEN BEGIN
                                                                  IF "Credit Memo Date" > "Contract Expiration Date" THEN
                                                                    ERROR(
                                                                      Text008,
                                                                      FIELDCAPTION("Credit Memo Date"),FIELDCAPTION("Contract Expiration Date"));
                                                                END;

                                                                IF "Credit Memo Date" <> xRec."Credit Memo Date" THEN
                                                                  IF "Credit Memo Date" = 0D THEN
                                                                    ERROR(Text018,FIELDCAPTION("Credit Memo Date"));
                                                              END;

                                                   CaptionML=[ENU=Credit Memo Date;
                                                              ESP=Fecha abono] }
    { 20  ;   ;Contract Expiration Date;Date      ;OnValidate=BEGIN
                                                                TestStatusOpen;

                                                                TESTFIELD(Credited,FALSE);

                                                                ServContractHeader.GET("Contract Type","Contract No.");

                                                                IF (NOT ServContractHeader.Prepaid) AND
                                                                   (xRec."Contract Expiration Date" <= "Invoiced to Date") AND
                                                                   (xRec."Contract Expiration Date" <> 0D)
                                                                THEN
                                                                  IF ("Contract Expiration Date" > "Invoiced to Date") OR
                                                                     ("Contract Expiration Date" = 0D)
                                                                  THEN
                                                                    ERROR(
                                                                      Text024,
                                                                      FIELDCAPTION("Contract Expiration Date"));

                                                                IF "Contract Expiration Date" = 0D THEN BEGIN
                                                                  "Credit Memo Date" := 0D;
                                                                  EXIT;
                                                                END;

                                                                IF "Contract Expiration Date" < "Starting Date" THEN
                                                                  ERROR(
                                                                    Text009,
                                                                    FIELDCAPTION("Contract Expiration Date"),
                                                                    FIELDCAPTION("Starting Date"));

                                                                IF ServContractHeader."Expiration Date" <> 0D THEN
                                                                  IF ("Contract Expiration Date" > ServContractHeader."Expiration Date") THEN
                                                                    ERROR(
                                                                      Text017,
                                                                      FIELDCAPTION("Contract Expiration Date"),
                                                                      ServContractHeader.FIELDCAPTION("Expiration Date"));

                                                                IF "Contract Expiration Date" < "Credit Memo Date" THEN
                                                                  ERROR(
                                                                    Text009,
                                                                    FIELDCAPTION("Contract Expiration Date"),
                                                                    FIELDCAPTION("Credit Memo Date"));

                                                                IF "Credit Memo Date" = 0D THEN
                                                                  "Credit Memo Date" := "Contract Expiration Date";

                                                                IF NOT ServContractHeader."Automatic Credit Memos" THEN BEGIN
                                                                  ServLedgEntry.RESET;
                                                                  ServLedgEntry.SETCURRENTKEY(Type,"No.","Entry Type","Moved from Prepaid Acc.","Posting Date",Open);
                                                                  ServLedgEntry.SETRANGE(Type,ServLedgEntry.Type::"Service Contract");
                                                                  ServLedgEntry.SETRANGE("No.","Contract No.");
                                                                  ServLedgEntry.SETRANGE("Moved from Prepaid Acc.",FALSE);
                                                                  ServLedgEntry.SETRANGE(Open,FALSE);
                                                                  ServLedgEntry.CALCSUMS("Amount (LCY)");
                                                                  IF ServLedgEntry."Amount (LCY)" <> 0 THEN
                                                                    MESSAGE(Text011,ServLedgEntry.TABLECAPTION,TABLECAPTION,"Contract No.");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Contract Expiration Date;
                                                              ESP=Fecha fin contrato] }
    { 21  ;   ;Service Period      ;DateFormula   ;OnValidate=BEGIN
                                                                TestStatusOpen;
                                                              END;

                                                   CaptionML=[ENU=Service Period;
                                                              ESP=Periodo servicio] }
    { 22  ;   ;Line Value          ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Line Discount %");
                                                              END;

                                                   CaptionML=[ENU=Line Value;
                                                              ESP=Valor l¡nea];
                                                   BlankZero=Yes;
                                                   AutoFormatType=1 }
    { 23  ;   ;Line Discount %     ;Decimal       ;OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                Currency.InitRoundingPrecision;
                                                                "Line Value" := ROUND("Line Value",Currency."Amount Rounding Precision");
                                                                "Line Amount" :=
                                                                  ROUND("Line Value" - "Line Value" * "Line Discount %" / 100,
                                                                  Currency."Amount Rounding Precision");
                                                                "Line Discount Amount" :=
                                                                  ROUND("Line Value" - "Line Amount",Currency."Amount Rounding Precision");
                                                                Profit := ROUND("Line Amount" - "Line Cost",Currency."Amount Rounding Precision");
                                                              END;

                                                   CaptionML=[ENU=Line Discount %;
                                                              ESP=% Descuento l¡nea];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100;
                                                   BlankZero=Yes }
    { 24  ;   ;Line Amount         ;Decimal       ;OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                TESTFIELD("Line Value");
                                                                Currency.InitRoundingPrecision;
                                                                "Line Discount Amount" := ROUND("Line Value" - "Line Amount",Currency."Amount Rounding Precision");
                                                                "Line Discount %" := "Line Discount Amount"/"Line Value"*100;
                                                                Profit := ROUND("Line Amount" - "Line Cost",Currency."Amount Rounding Precision");
                                                              END;

                                                   CaptionML=[ENU=Line Amount;
                                                              ESP=Importe l¡nea];
                                                   BlankZero=Yes;
                                                   AutoFormatType=1 }
    { 28  ;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   OnValidate=BEGIN
                                                                TestStatusOpen;
                                                              END;

                                                   CaptionML=[ENU=Variant Code;
                                                              ESP=C¢d. variante] }
    { 29  ;   ;Starting Date       ;Date          ;CaptionML=[ENU=Starting Date;
                                                              ESP=Fecha inicial];
                                                   Editable=No }
    { 30  ;   ;New Line            ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[ENU=New Line;
                                                              ESP=Nueva l¡nea];
                                                   Editable=No }
    { 31  ;   ;Credited            ;Boolean       ;OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                TESTFIELD("Contract Expiration Date");
                                                              END;

                                                   CaptionML=[ENU=Credited;
                                                              ESP=Abonado] }
    { 32  ;   ;Line Cost           ;Decimal       ;OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                Currency.InitRoundingPrecision;
                                                                Profit := ROUND("Line Amount" - "Line Cost",Currency."Amount Rounding Precision");
                                                              END;

                                                   CaptionML=[ENU=Line Cost;
                                                              ESP=Coste l¡nea];
                                                   BlankZero=Yes;
                                                   AutoFormatType=1 }
    { 33  ;   ;Line Discount Amount;Decimal       ;OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                Currency.InitRoundingPrecision;
                                                                IF "Line Value" <> 0 THEN
                                                                  "Line Discount %" := "Line Discount Amount" / "Line Value" * 100
                                                                ELSE
                                                                  "Line Discount %" := 0;
                                                                "Line Amount" :=
                                                                  ROUND("Line Value" - "Line Value" * "Line Discount %" / 100,Currency."Amount Rounding Precision");
                                                                Profit := ROUND("Line Amount" - "Line Cost",Currency."Amount Rounding Precision");
                                                              END;

                                                   CaptionML=[ENU=Line Discount Amount;
                                                              ESP=Importe dto. l¡nea];
                                                   BlankZero=Yes;
                                                   AutoFormatType=1 }
    { 34  ;   ;Profit              ;Decimal       ;OnValidate=BEGIN
                                                                TestStatusOpen;
                                                                Currency.InitRoundingPrecision;
                                                                "Line Amount" := ROUND(Profit + "Line Cost",Currency."Amount Rounding Precision");
                                                                "Line Discount Amount" := ROUND("Line Value" - "Line Amount",Currency."Amount Rounding Precision");
                                                                IF "Line Value" <> 0 THEN
                                                                  "Line Discount %" := "Line Discount Amount" / "Line Value" * 100;
                                                              END;

                                                   CaptionML=[ENU=Profit;
                                                              ESP=Bf§ bruto];
                                                   BlankZero=Yes;
                                                   AutoFormatType=1 }
  }
  KEYS
  {
    {    ;Contract Type,Contract No.,Line No.     ;SumIndexFields=Line Amount,Profit;
                                                   MaintainSIFTIndex=No;
                                                   Clustered=Yes }
    {    ;Contract No.,Line No.,Contract Type      }
    {    ;Service Item No.,Contract Status         }
    {    ;Contract Type,Contract No.,Credited,New Line;
                                                   SumIndexFields=Line Amount,Profit;
                                                   MaintainSQLIndex=No;
                                                   MaintainSIFTIndex=No }
    {    ;Customer No.,Ship-to Code                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Item@1028 : Record 27;
      Cust@1013 : Record 18;
      StdTxt@1002 : Record 7;
      ServMgtSetup@1004 : Record 5911;
      ServLedgEntry@1023 : Record 5907;
      ServContractHeader@1003 : Record 5965;
      TempServContractLine@1000 : Record 5964;
      ServContractLine@1019 : Record 5964;
      ServItem@1001 : Record 5940;
      Text000@1011 : TextConst 'ENU=This %1 does not belong to %2 %3;ESP=Este %1 no pertenece a %2 %3';
      Text001@1012 : TextConst 'ENU=%1 %2 has a different ship-to code for this customer.\\Do you want to continue?;ESP=%1 %2 tiene un C¢d. direcci¢n env¡o diferente para este cliente.\\¨Desea continuar?';
      Text003@1018 : TextConst 'ENU=This %1 already exists in this service contract;ESP=Ya existe este %1 en este contrato servicio';
      Text008@1005 : TextConst 'ENU=%1 field value cannot be later than the %2 field value on the contract line.;ESP=El valor del campo %1 no puede ser posterior que el del campo %2 en la l¡nea de contrato.';
      Text009@1009 : TextConst 'ENU=The %1 cannot be less than the %2.;ESP=El %1 no puede ser menor que el %2.';
      Text011@1007 : TextConst 'ENU=%1 exists for %2 %3.\\You may need to create a credit memo.;ESP=Existe %1 para %2 %3.\\Podr¡a necesitar crear un abono.';
      ContractChangeLog@1015 : Record 5967;
      ContractGainLossEntry@1016 : Record 5969;
      ServCommentLine@1027 : Record 5906;
      ServItemMgt@1017 : Codeunit 5920;
      CreateCreditfromContractLines@1025 : Codeunit 5945;
      ServLogMgt@1030 : Codeunit 5906;
      HideDialog@1020 : Boolean;
      StatusCheckSuspended@1021 : Boolean;
      Text013@1022 : TextConst 'ENU=You cannot change the %1 field on signed service contracts.;ESP=No puede cambiar el campo %1 en contratos servicio firmados.';
      Text015@1026 : TextConst 'ENU=You cannot delete service contract lines on %1 service contracts.;ESP=No puede eliminar l¡ns. contrato servicio en %1 contratos servicio.';
      Text016@1010 : TextConst 'ENU=Service contract lines must have at least a %1 filled in.;ESP=Las l¡ns. contrato servicio deben tener al menos un %1 rellenado.';
      Text017@1029 : TextConst 'ENU=The %1 cannot be later than the %2.;ESP=El %1 no puede ser posterior que el %2.';
      Text018@1032 : TextConst 'ENU=You cannot reset %1 manually.;ESP=No puede especificar %1 manualmente.';
      Text019@1006 : TextConst 'ENU=%1 %2 already belongs to one or more service contracts/quotes.\\Do you want to continue?;ESP=%1 %2 ya pertenece a una o m s ofertas/contratos.\\¨Desea continuar?';
      Text020@1014 : TextConst 'ENU=The service period for service item %1 under contract %2 has not yet started.;ESP=El periodo servicio para el prod. servicio %1 bajo el contrato %2 todav¡a no ha comenzado.';
      Text021@1008 : TextConst 'ENU=The service period for service item %1 under contract %2 has expired.;ESP=El periodo servicio para prod. servicio %1 bajo el contrato %2 ha vencido.';
      Text022@1033 : TextConst 'ENU=If you delete this contract line while the Automatic Credit Memos check box is not selected, a credit memo will not be created.\Do you want to continue?;ESP=Si elimina esta l¡nea contrato y no est  seleccionada la casilla de verificaci¢n de Abonos autom ticos, no se crear  un abono.\¨Dese continuar?';
      Text023@1034 : TextConst 'ENU=The update has been interrupted to respect the warning.;ESP=Se ha interrumpido la actualizaci¢n para respetar la advertencia.';
      UseServContractLineAsxRec@1031 : Boolean;
      Text024@1035 : TextConst 'ENU=You cannot enter a later date in or clear the %1 field on the contract line that has been invoiced for the period containing that date.;ESP=No puede introducir una fecha posterior a la existente ni borrar la fecha en el campo %1 de una l¡nea de contrato facturada para el periodo al que corresponde la fecha.';
      Currency@1036 : Record 4;

    PROCEDURE SetupNewLine@1();
    BEGIN
      IF NOT ServContractHeader.GET("Contract Type","Contract No.") THEN
        EXIT;
      "Customer No." := ServContractHeader."Customer No.";
      "Ship-to Code" := ServContractHeader."Ship-to Code";
      "Contract Status" := ServContractHeader.Status;
      "Contract Expiration Date":= ServContractHeader."Expiration Date";
      "Credit Memo Date" := "Contract Expiration Date";
      "Service Period" := ServContractHeader."Service Period";
      IF ("Contract Type" = "Contract Type"::Contract) AND
         ("Contract Status" = "Contract Status"::Signed)
      THEN
        "Starting Date" := WORKDATE
      ELSE
        "Starting Date" := ServContractHeader."Starting Date";

      IF "Starting Date" > ServContractHeader."First Service Date" THEN
        "Next Planned Service Date" := "Starting Date"
      ELSE
        "Next Planned Service Date" := ServContractHeader."First Service Date";
    END;

    LOCAL PROCEDURE GetServItem@9();
    BEGIN
      TESTFIELD("Service Item No.");
      IF "Service Item No." <> ServItem."No." THEN
        ServItem.GET("Service Item No.");
    END;

    PROCEDURE CalculateNextServiceVisit@5();
    BEGIN
      ServMgtSetup.GET;
      IF (FORMAT("Service Period") <> '') AND
         ("Next Planned Service Date" <> 0D)
      THEN BEGIN
        GetServContractHeader;
        CASE ServMgtSetup."Next Service Calc. Method" OF
          ServMgtSetup."Next Service Calc. Method"::Planned:
            "Next Planned Service Date" := CALCDATE("Service Period","Last Planned Service Date");
          ServMgtSetup."Next Service Calc. Method"::Actual:
            "Next Planned Service Date" := CALCDATE("Service Period","Last Service Date");
        END;
      END ELSE
        "Next Planned Service Date" := 0D;
    END;

    PROCEDURE UpdateContractAnnualAmount@4(Deleting@1000 : Boolean);
    VAR
      TempAmount@1001 : Decimal;
      OldServContractHeader@1002 : Record 5965;
      ServContractLine2@1003 : Record 5964;
    BEGIN
      GetServContractHeader;
      IF NOT ServContractHeader."Allow Unbalanced Amounts" THEN BEGIN
        TempServContractLine.RESET;
        TempServContractLine.SETRANGE("Contract Type","Contract Type");
        TempServContractLine.SETRANGE("Contract No.","Contract No.");
        TempServContractLine.SETFILTER("Line No.",'<>%1',"Line No.");
        TempServContractLine.CALCSUMS("Line Amount");
        OldServContractHeader := ServContractHeader;
        IF Deleting THEN
          ServContractHeader."Annual Amount" := TempServContractLine."Line Amount"
        ELSE BEGIN
          ServContractHeader."Annual Amount" := TempServContractLine."Line Amount" + "Line Amount";
          IF NOT "New Line" THEN
            ContractGainLossEntry.AddEntry(4,"Contract Type","Contract No.", Rec."Line Amount" - xRec."Line Amount",'')
          ELSE
            IF ServContractHeader.Status = ServContractHeader.Status::Signed THEN BEGIN
              IF ServContractLine2.GET(Rec."Contract Type",Rec."Contract No.",Rec."Line No.") THEN
                ContractGainLossEntry.AddEntry(4,"Contract Type","Contract No.", "Line Amount" - ServContractLine2."Line Amount",'')
              ELSE
                ContractGainLossEntry.AddEntry(0,"Contract Type","Contract No.", "Line Amount",'')
            END;
        END;
        ServContractHeader.ValidateNextInvoicePeriod;
        ServContractHeader.SuspendStatusCheck(StatusCheckSuspended);
        ServContractHeader.MODIFY(TRUE);
        IF ServContractHeader."Contract Type" = ServContractHeader."Contract Type"::Contract THEN
          IF ServMgtSetup."Register Contract Changes" THEN
            ServContractHeader.UpdContractChangeLog(OldServContractHeader);
      END;
    END;

    PROCEDURE HideDialogBox@2(Hide@1000 : Boolean);
    BEGIN
      HideDialog := Hide;
    END;

    LOCAL PROCEDURE TestStatusOpen@33();
    BEGIN
      IF StatusCheckSuspended THEN
        EXIT;
      GetServContractHeader;
      ServContractHeader.TESTFIELD("Change Status",ServContractHeader."Change Status"::Open);
    END;

    PROCEDURE SuspendStatusCheck@39(Suspend@1000 : Boolean);
    BEGIN
      StatusCheckSuspended := Suspend;
    END;

    LOCAL PROCEDURE GetServContractHeader@3();
    BEGIN
      TESTFIELD("Contract No.");
      IF ("Contract Type" <> ServContractHeader."Contract Type") OR
         ("Contract No." <> ServContractHeader."Contract No.")
      THEN BEGIN
        ServContractHeader.GET("Contract Type","Contract No.");
      END;
    END;

    PROCEDURE ShowComments@11();
    BEGIN
      ServContractHeader.GET("Contract Type","Contract No.");
      ServContractHeader.TESTFIELD("Customer No.");
      TESTFIELD("Line No.");
      ServCommentLine.RESET;
      ServCommentLine.SETRANGE("Table Name",ServCommentLine."Table Name"::"Service Contract");
      ServCommentLine.SETRANGE("Table Subtype","Contract Type");
      ServCommentLine.SETRANGE("No.","Contract No.");
      ServCommentLine.SETRANGE(Type,ServCommentLine.Type::General);
      ServCommentLine.SETRANGE("Table Line No.","Line No.");
      FORM.RUNMODAL(FORM::"Service Comment Sheet",ServCommentLine);
    END;

    PROCEDURE ValidateServicePeriod@6(CurrentDate@1000 : Date);
    BEGIN
      IF "Starting Date" > CurrentDate THEN
        ERROR(Text020,"Service Item No.","Contract No.");
      IF "Contract Expiration Date" = 0D THEN BEGIN
        ServContractHeader.GET(ServContractHeader."Contract Type"::Contract,"Contract No.");
        IF (ServContractHeader."Expiration Date" <> 0D) AND
           (ServContractHeader."Expiration Date" <= CurrentDate)
        THEN
          ERROR(Text021,"Service Item No.","Contract No.");
      END ELSE
        IF "Contract Expiration Date" <= CurrentDate THEN
          ERROR(Text021,"Service Item No.","Contract No.");
    END;

    PROCEDURE CreditMemoBaseExists@7() : Boolean;
    VAR
      CreditAmount@1003 : Decimal;
      FirstPrepaidPostingDate@1002 : Date;
      LastIncomePostingDate@1001 : Date;
      ServContractMgt@1008 : Codeunit 5940;
    BEGIN
      IF "Line Amount" > 0 THEN BEGIN
        TESTFIELD("Contract Expiration Date");
        IF "Invoiced to Date" >= "Contract Expiration Date"  THEN BEGIN
          Currency.InitRoundingPrecision;
          IF ServContractHeader.Prepaid THEN
            FirstPrepaidPostingDate := ServContractMgt.FindFirstPrepaidTransaction("Contract No.")
          ELSE
            FirstPrepaidPostingDate := 0D;
          LastIncomePostingDate := "Invoiced to Date";
          IF FirstPrepaidPostingDate <> 0D THEN
            LastIncomePostingDate := FirstPrepaidPostingDate - 1;
          CreditAmount :=
            ROUND(
              ServContractMgt.CalcContractLineAmount("Line Amount",
              "Contract Expiration Date",LastIncomePostingDate),
              Currency."Amount Rounding Precision");
          IF FirstPrepaidPostingDate <> 0D THEN BEGIN
            IF "Contract Expiration Date" < FirstPrepaidPostingDate THEN BEGIN
              CreditAmount :=
                ROUND(
                  ServContractMgt.CalcContractLineAmount("Line Amount",
                  FirstPrepaidPostingDate,"Invoiced to Date"),
                  Currency."Amount Rounding Precision");
            END ELSE BEGIN
              CreditAmount :=
                ROUND(
                  ServContractMgt.CalcContractLineAmount("Line Amount",
                  "Contract Expiration Date","Invoiced to Date"),
                  Currency."Amount Rounding Precision");
            END;
          END;
        END;
        EXIT((CreditAmount > 0) AND (NOT Credited));
      END;

      EXIT(FALSE);
    END;

    PROCEDURE LogContractLineChanges@8(ServContractLine2@1000 : Record 5964);
    BEGIN
      IF ("Item No." <> ServContractLine2."Item No.") THEN
        ContractChangeLog.LogContractChange(
          "Contract No.",1,FIELDCAPTION("Item No."),0,
          FORMAT(ServContractLine2."Item No."),FORMAT("Item No."),
          ServContractLine2."Service Item No.", "Line No.");

      IF ("Line Value" <> ServContractLine2."Line Value") THEN
        ContractChangeLog.LogContractChange(
          "Contract No.",1,FIELDCAPTION("Line Value"),0,
          FORMAT(ServContractLine2."Line Value"),FORMAT("Line Value"),
          ServContractLine2."Service Item No.", "Line No.");

      IF ("Line Discount %" <> ServContractLine2."Line Discount %") THEN
        ContractChangeLog.LogContractChange(
          "Contract No.",1,FIELDCAPTION("Line Discount %"),0,
          FORMAT(ServContractLine2."Line Discount %"),FORMAT("Line Discount %"),
          ServContractLine2."Service Item No.", "Line No.");

      IF ("Line Amount" <> ServContractLine2."Line Amount") THEN
        ContractChangeLog.LogContractChange(
          "Contract No.",1,FIELDCAPTION("Line Amount"),0,
          FORMAT(ServContractLine2."Line Amount"),FORMAT("Line Amount"),
          ServContractLine2."Service Item No.", "Line No.");

      IF ("Contract Expiration Date" <> ServContractLine2."Contract Expiration Date") THEN
        ContractChangeLog.LogContractChange(
          "Contract No.",1,FIELDCAPTION("Contract Expiration Date"),0,
          FORMAT(ServContractLine2."Contract Expiration Date"),FORMAT("Contract Expiration Date"),
          ServContractLine2."Service Item No.", "Line No.");

      IF ("Service Item No." <> ServContractLine2."Service Item No.") THEN BEGIN
        ContractChangeLog.LogContractChange(
          "Contract No.",1,FIELDCAPTION("Service Item No."),0,
          FORMAT(ServContractLine2."Service Item No."),FORMAT("Service Item No."),
          ServContractLine2."Service Item No.", "Line No.");
      END;
    END;

    BEGIN
    END.
  }
}
