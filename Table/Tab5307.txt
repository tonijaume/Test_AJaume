OBJECT Table 5307 Outlook Synch. Option Correl.
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Outlook Value");
               TESTFIELD("Field Value");

               CheckDuplicatedRecords;
             END;

    OnModify=BEGIN
               CheckDuplicatedRecords;
             END;

    CaptionML=[ENU=Outlook Synch. Option Correl.;
               ESP=Correl. opciones sinc. Outlook];
  }
  FIELDS
  {
    { 1   ;   ;Synch. Entity Code  ;Code10        ;TableRelation="Outlook Synch. Entity".Code;
                                                   OnValidate=BEGIN
                                                                SetDefaults;
                                                              END;

                                                   CaptionML=[ENU=Synch. Entity Code;
                                                              ESP=C¢digo entidad sinc.];
                                                   NotBlank=Yes }
    { 2   ;   ;Element No.         ;Integer       ;CaptionML=[ENU=Element No.;
                                                              ESP=N§ elemento] }
    { 3   ;   ;Field Line No.      ;Integer       ;CaptionML=[ENU=Field Line No.;
                                                              ESP=N§ l¡nea campo] }
    { 4   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ESP=N§ l¡nea] }
    { 5   ;   ;Outlook Object      ;Text80        ;CaptionML=[ENU=Outlook Object;
                                                              ESP=Objeto Outlook] }
    { 6   ;   ;Outlook Property    ;Text80        ;CaptionML=[ENU=Outlook Property;
                                                              ESP=Propiedad Outlook] }
    { 7   ;   ;Outlook Value       ;Text80        ;OnValidate=VAR
                                                                IntVar@1000 : Integer;
                                                              BEGIN
                                                                OSynchField.RESET;
                                                                OSynchField.GET("Synch. Entity Code","Element No.","Field Line No.");

                                                                IF OSynchSetupMgt.CheckOEnumeration(OSynchField) THEN BEGIN
                                                                  IF "Element No." = 0 THEN
                                                                    OSynchSetupMgt.ValidateEnumerationValue(
                                                                      "Outlook Value",
                                                                      "Enumeration No.",
                                                                      OSynchField."Outlook Object",
                                                                      '',
                                                                      OSynchField."Outlook Property")
                                                                  ELSE BEGIN
                                                                    OSynchEntity.GET("Synch. Entity Code");
                                                                    OSynchSetupMgt.ValidateEnumerationValue(
                                                                      "Outlook Value",
                                                                      "Enumeration No.",
                                                                      OSynchEntity."Outlook Item",
                                                                      OSynchField."Outlook Object",
                                                                      OSynchField."Outlook Property");
                                                                  END;
                                                                END ELSE BEGIN
                                                                  IF NOT EVALUATE(IntVar,"Outlook Value") THEN
                                                                    ERROR(Text002);

                                                                  "Enumeration No." := IntVar;
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              OutlookValue@1000 : Text[80];
                                                              EnumerationNo@1001 : Integer;
                                                            BEGIN
                                                              OSynchField.RESET;
                                                              OSynchField.GET("Synch. Entity Code","Element No.","Field Line No.");
                                                              IF NOT OSynchSetupMgt.CheckOEnumeration(OSynchField) THEN
                                                                ERROR(Text003);

                                                              IF "Element No." = 0 THEN
                                                                OutlookValue := OSynchSetupMgt.ShowEnumerationsLookup("Outlook Object",'',"Outlook Property",EnumerationNo)
                                                              ELSE BEGIN
                                                                OSynchEntity.GET("Synch. Entity Code");
                                                                OutlookValue :=
                                                                  OSynchSetupMgt.ShowEnumerationsLookup(
                                                                    OSynchEntity."Outlook Item",
                                                                    "Outlook Object",
                                                                    "Outlook Property",
                                                                    EnumerationNo);
                                                              END;

                                                              IF OutlookValue = '' THEN
                                                                EXIT;

                                                              "Outlook Value" := OutlookValue;
                                                              "Enumeration No." := EnumerationNo;
                                                            END;

                                                   CaptionML=[ENU=Outlook Value;
                                                              ESP=Valor Outlook] }
    { 8   ;   ;Table No.           ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   CaptionML=[ENU=Table No.;
                                                              ESP=N§ tabla] }
    { 9   ;   ;Field No.           ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table No.));
                                                   CaptionML=[ENU=Field No.;
                                                              ESP=N§ campo] }
    { 10  ;   ;Field Value         ;Text80        ;OnLookup=VAR
                                                              LookupRecRef@1000 : RecordRef;
                                                              LookupFieldRef@1001 : FieldRef;
                                                              OptionID@1002 : Integer;
                                                            BEGIN
                                                              LookupRecRef.OPEN("Table No.",TRUE);
                                                              LookupFieldRef := LookupRecRef.FIELD("Field No.");

                                                              OptionID := OSynchSetupMgt.ShowOptionsLookup(LookupFieldRef.OPTIONCAPTION);

                                                              IF OptionID <> 0 THEN
                                                                VALIDATE("Option No.",OptionID - 1);

                                                              LookupRecRef.CLOSE;
                                                            END;

                                                   CaptionML=[ENU=Field Value;
                                                              ESP=Valor campo];
                                                   Editable=No }
    { 11  ;   ;Option No.          ;Integer       ;OnValidate=VAR
                                                                LookupRecRef@1000 : RecordRef;
                                                                LookupFieldRef@1001 : FieldRef;
                                                              BEGIN
                                                                LookupRecRef.OPEN("Table No.",TRUE);
                                                                LookupFieldRef := LookupRecRef.FIELD("Field No.");
                                                                "Field Value" := SELECTSTR("Option No." + 1,LookupFieldRef.OPTIONCAPTION);
                                                                LookupRecRef.CLOSE;
                                                              END;

                                                   CaptionML=[ENU=Option No.;
                                                              ESP=N§ opci¢n];
                                                   Editable=No }
    { 12  ;   ;Enumeration No.     ;Integer       ;CaptionML=[ENU=Enumeration No.;
                                                              ESP=N§ enumeraci¢n];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Synch. Entity Code,Element No.,Field Line No.,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      OSynchEntity@1000 : Record 5300;
      OSynchEntityElement@1001 : Record 5301;
      OSynchField@1002 : Record 5304;
      OSynchSetupMgt@1003 : Codeunit 5300;
      Text001@1006 : TextConst 'ENU=The line you are trying to create already exists.;ESP=La l¡nea que intenta crear ya existe.';
      Text002@1007 : TextConst 'ENU=This value is not valid. It must be either an integer or an enumeration element.;ESP=Este valor no es v lido. Debe ser un entero o un elemento de enumeraci¢n.';
      Text003@1008 : TextConst 'ENU=The look up window table cannot be opened because this Outlook property is not of the enumeration type.;ESP=La tabla de ventana de b£squeda no se puede abrir porque esta propiedad de Outlook no es del tipo de enumeraci¢n.';

    PROCEDURE SetDefaults@1();
    BEGIN
      IF "Element No." = 0 THEN BEGIN
        OSynchEntity.GET("Synch. Entity Code");
        "Outlook Object" := OSynchEntity."Outlook Item";
      END ELSE BEGIN
        OSynchEntityElement.GET("Synch. Entity Code","Element No.");
        "Outlook Object" := OSynchEntityElement."Outlook Collection";
      END;

      OSynchField.GET("Synch. Entity Code","Element No.","Field Line No.");
      "Outlook Property" := OSynchField."Outlook Property";
      "Field No." := OSynchField."Field No.";

      IF OSynchField."Table No." = 0 THEN
        "Table No." := OSynchField."Master Table No."
      ELSE
        "Table No." := OSynchField."Table No.";
    END;

    LOCAL PROCEDURE CheckDuplicatedRecords@2();
    VAR
      OSynchOptionCorrel@1000 : Record 5307;
    BEGIN
      OSynchOptionCorrel.RESET;
      OSynchOptionCorrel.SETRANGE("Synch. Entity Code","Synch. Entity Code");
      OSynchOptionCorrel.SETRANGE("Element No.","Element No.");
      OSynchOptionCorrel.SETRANGE("Field Line No.","Field Line No.");
      OSynchOptionCorrel.SETFILTER("Line No.",'<>%1',"Line No.");
      OSynchOptionCorrel.SETRANGE("Option No.","Option No.");
      OSynchOptionCorrel.SETRANGE("Enumeration No.","Enumeration No.");
      IF OSynchOptionCorrel.FIND('-') THEN
        ERROR(Text001);
    END;

    BEGIN
    END.
  }
}
