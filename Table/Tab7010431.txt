OBJECT Table 7010431 Cabecera Importacion TRN
{
  OBJECT-PROPERTIES
  {
    Date=14/11/14;
    Time=14:40:39;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               rCab.RESET;
               rCab.SETRANGE(Importacion, Importacion);
               rCab.ASCENDING(FALSE);
               IF rCab.FINDFIRST THEN
                 "No." := rCab."No." + 1
               ELSE
                 "No." := 1;
             END;

    OnModify=BEGIN

               // AJS 19.07.2006
               // Dejamos modificar subagencia, asi que cada vez que se modifique la cabecera obligamos a revisar los errores
               // Comprobamos que no este traspasada

               IF Traspasada THEN
                 ERROR(Text0004);

               // AJS 25.07.2006
               // Dejamos modificar la fecha

               IF Anulada THEN
                 ERROR(Text0005);

               // AJS 24.07.2006
               // Marcar la linea con errores solo si ha cambiado de agencia, subagencia, fecha o servicio

               IF (Agencia <> xRec.Agencia) OR (Subagencia <> xRec.Subagencia) OR
                  (Fecha <> xRec.Fecha) OR (Servicio <> xRec.Servicio) THEN
                 Errores := -1;
             END;

    OnDelete=VAR
               lrLin@1100244000 : Record 7010432;
               lrExtas@1100253000 : Record 7035389;
               lrVuelos@1103355000 : Record 7035391;
             BEGIN

               // Borramos sus correspondienes lineas

               lrLin.RESET;
               lrLin.SETRANGE(Importacion  , Importacion);
               lrLin.SETRANGE("No Cabecera","No.");
               IF lrLin.FINDFIRST THEN
                 lrLin.DELETEALL;

               // Borramos los extras (y suplementos)

               CLEAR(lrExtas);
               lrExtas.SETRANGE(Importacion    , Importacion);
               lrExtas.SETRANGE("No Cabecerra" , "No.");
               IF lrExtas.FINDFIRST THEN
                 lrExtas.DELETEALL;

               // AJS 24.07.2006  ## DRF 06008
               // Borramos los vuelos

               lrVuelos.RESET;
               lrVuelos.SETRANGE(Importacion   , Importacion);
               lrVuelos.SETRANGE("No. Cabecera", "No.");
               IF FINDFIRST THEN
                 lrVuelos.DELETEALL;
             END;

    CaptionML=ESP=Cabecera Importacion TRN;
    LookupFormID=Form7035290;
    DrillDownFormID=Form7035290;
  }
  FIELDS
  {
    { 1   ;   ;Importacion         ;Integer       ;TableRelation="TRF-Historico Importaciones"."No Importacion";
                                                   CaptionML=ESP=Importacion }
    { 3   ;   ;No.                 ;Integer       ;CaptionML=ESP=No. }
    { 4   ;   ;No. Parte           ;Code10        ;TableRelation=Parte."N§ Parte";
                                                   OnLookup=VAR
                                                              lrParte@1103350000 : Record 7010360;
                                                            BEGIN
                                                              IF lrParte.GET("No. Parte") THEN BEGIN
                                                                lrParte.FILTERGROUP(2);
                                                                lrParte.SETRECFILTER;
                                                                lrParte.FILTERGROUP(0);
                                                                FORM.RUNMODAL(7010400, lrParte);
                                                              END;
                                                            END;

                                                   CaptionML=ESP=No. Parte;
                                                   Description=Parte."N§ Parte" }
    { 5   ;   ;No. Parte Origen    ;Code10        ;CaptionML=ESP=No. Parte Origen }
    { 10  ;   ;Errores             ;Integer       ;InitValue=-1;
                                                   OnLookup=BEGIN
                                                              LookupErrores;
                                                            END;

                                                   CaptionML=ESP=Errores;
                                                   BlankNumbers=BlankNegAndZero;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 20  ;   ;Agencia             ;Code10        ;OnValidate=BEGIN
                                                                // AJS 22.09.2006
                                                                // Pasamos la agencia a las lineas de recogida

                                                                rLin.RESET;
                                                                rLin.SETRANGE(Importacion  , Importacion);
                                                                rLin.SETRANGE("No Cabecera", "No.");
                                                                IF rLin.FINDFIRST THEN
                                                                  rLin.MODIFYALL(Agencia, Agencia);
                                                              END;

                                                   OnLookup=BEGIN
                                                              rLin.GetLookup(2,Agencia,Agencia,Subagencia);
                                                            END;

                                                   CaptionML=ESP=Agencia }
    { 21  ;   ;Subagencia          ;Code21        ;OnValidate=BEGIN
                                                                // AJS 22.09.2006
                                                                // Pasamos la subagencia a las lineas de recogida

                                                                rLin.RESET;
                                                                rLin.SETRANGE(Importacion  , Importacion);
                                                                rLin.SETRANGE("No Cabecera", "No.");
                                                                IF rLin.FINDFIRST THEN
                                                                  rLin.MODIFYALL(Subagencia, Subagencia);
                                                              END;

                                                   OnLookup=BEGIN
                                                              rLin.GetLookup(3,Subagencia,Agencia,Subagencia);
                                                            END;

                                                   CaptionML=ESP=Subagencia }
    { 22  ;   ;Servicio            ;Code30        ;OnValidate=BEGIN
                                                                // AJS 22.09.2006
                                                                // Pasamos el Servicio a las lineas de recogida

                                                                rLin.RESET;
                                                                rLin.SETRANGE(Importacion  , Importacion);
                                                                rLin.SETRANGE("No Cabecera", "No.");
                                                                IF rLin.FINDFIRST THEN
                                                                  rLin.MODIFYALL(Servicio, Servicio);
                                                              END;

                                                   CaptionML=ESP=Servicio }
    { 23  ;   ;Fecha               ;Date          ;OnValidate=BEGIN
                                                                // AJS 25.07.2006
                                                                // Pasamos la fecha a las lineas de recogida

                                                                rLin.RESET;
                                                                rLin.SETRANGE(Importacion  , Importacion);
                                                                rLin.SETRANGE("No Cabecera", "No.");
                                                                IF rLin.FINDFIRST THEN
                                                                  rLin.MODIFYALL(Fecha, Fecha);

                                                                // Pasamos la fecha a los vuelos

                                                                rVuelo.RESET;
                                                                rVuelo.SETRANGE(Importacion   , Importacion);
                                                                rVuelo.SETRANGE("No. Cabecera", "No.");
                                                                IF rVuelo.FINDFIRST THEN BEGIN
                                                                  rVuelo.MODIFYALL(Fecha                , Fecha);
                                                                  rVuelo.MODIFYALL("Fecha llegada vuelo", Fecha);
                                                                END;
                                                              END;

                                                   CaptionML=ESP=Fecha }
    { 24  ;   ;Traspasada          ;Boolean       ;CaptionML=ESP=Traspasada }
    { 25  ;   ;Anulada             ;Boolean       ;CaptionML=ESP=Anulada }
    { 26  ;   ;Observaciones       ;Text250       ;CaptionML=ESP=Observaciones }
    { 27  ;   ;D¡a siguiente       ;Boolean       ;CaptionML=ESP=D¡a siguiente }
    { 30  ;   ;Referencia          ;Text50        ;CaptionML=ESP=Referencia;
                                                   Description=[ Se trunca a 30 en Partes (GC)] }
    { 31  ;   ;Letreros            ;Text30        ;CaptionML=ESP=Letreros }
    { 50  ;   ;Vehiculo            ;Code10        ;CaptionML=ESP=Vehiculo }
    { 51  ;   ;Conductor           ;Code10        ;CaptionML=ESP=Conductor }
    { 80  ;   ;Extras              ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Extra/Suplementos Importacion" WHERE (Importacion=FIELD(Importacion),
                                                                                                            No Cabecerra=FIELD(No.),
                                                                                                            Tipo=CONST(Extra)));
                                                   CaptionML=ESP=Extras;
                                                   BlankZero=Yes;
                                                   Description=Flowfield;
                                                   Editable=No }
    { 90  ;   ;Modificacion sin autorizar;Boolean ;OnValidate=BEGIN
                                                                IF "Modificacion sin autorizar" THEN
                                                                  ERROR(Text0001);

                                                                IF cFunBas.TestRestriccion(Text50000) THEN
                                                                  ERROR(Text0003);

                                                                IF NOT CONFIRM(Text0002) THEN
                                                                  "Modificacion sin autorizar" := TRUE;
                                                              END;

                                                   CaptionML=ESP=Modificacion sin autorizar }
    { 91  ;   ;Origen/Destino      ;Code10        ;OnLookup=BEGIN
                                                              rLin.GetLookup(5,"Origen/Destino",Agencia,Subagencia);
                                                            END;

                                                   CaptionML=ESP=Origen/Destino;
                                                   SQL Data Type=Varchar;
                                                   Description=FK Punto de recogida }
    { 100 ;   ;Privado             ;Boolean       ;CaptionML=ESP=Privado;
                                                   Description=TER }
    { 101 ;   ;Servicio shuttle    ;Boolean       ;CaptionML=ESP=Servicio shuttle;
                                                   Description=Origen de creaci¢n del parte de trabajo. }
    { 102 ;   ;Fichero XML         ;Boolean       ;CaptionML=ESP=Fichero XML }
    { 105 ;   ;Cancelacion         ;Boolean        }
  }
  KEYS
  {
    {    ;Importacion,No.                         ;Clustered=Yes }
    {    ;Importacion,Fecha,Agencia,Subagencia,Servicio,Traspasada }
    {    ;Traspasada,Anulada                       }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text50000@1103355003 : TextConst 'ESP=AUT-MOD-IMP';
      rCab@1100244000 : Record 7010431;
      rLin@1100244001 : Record 7010432;
      Text0001@1100253000 : TextConst 'ESP=No puede marcarse esta opci¢n';
      Text0002@1100253001 : TextConst 'ESP=¨Desea realemente autorizar este servicio para su traspaso?';
      rVuelo@1103355001 : Record 7035391;
      cFunBas@1100253002 : Codeunit 7010310;
      Text0003@1100253003 : TextConst 'ESP=Opci¢n Restringida';
      Text0004@1103355000 : TextConst 'ESP=Esta cabecera ya ha sido traspasada, no se puede modificar.';
      Text0005@1103355002 : TextConst 'ESP=Esta cabecera ya ha sido anulada, no se puede modificar.';

    PROCEDURE NoErrores@1100244000() : Integer;
    VAR
      lrHist@1100244000 : Record 7035334;
      lwTotal@1100244001 : Integer;
    BEGIN
      // Errores
      //
      lrHist.RESET;
      lrHist.SETCURRENTKEY("No Importacion" , NoCab,NoLin);
      lrHist.SETRANGE("No Importacion"      , Importacion);
      lrHist.SETRANGE(NoCab                 , "No.");

      lwTotal:=lrHist.COUNT;
      EXIT(lwTotal);
    END;

    PROCEDURE LookupErrores@1100244001();
    VAR
      lrHist@1100244000 : Record 7035334;
    BEGIN
      // LookupErrores
      //
      lrHist.RESET;
      lrHist.FILTERGROUP(2);
      lrHist.SETCURRENTKEY("No Importacion" , NoCab,NoLin);
      lrHist.SETRANGE("No Importacion"      , Importacion);
      lrHist.SETRANGE(NoCab                 , "No.");
      lrHist.FILTERGROUP(0);
      FORM.RUN(7035296,lrHist);
    END;

    BEGIN
    END.
  }
}
