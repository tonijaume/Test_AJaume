OBJECT Table 7010212 Precios Extra Tarifa
{
  OBJECT-PROPERTIES
  {
    Date=02/10/12;
    Time=12:00:00;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               // ----  Obtener el siguiente numero de orden  ----

               rPreExt.RESET;
               rPreExt.SETRANGE(Delegacion , Delegacion);
               rPreExt.SETRANGE("N§ Tarifa", "N§ Tarifa");
               IF rPreExt.FIND('+') THEN
                 "N§ Orden" := rPreExt."N§ Orden" + 10
               ELSE
                 "N§ Orden" := 10;

               // ----  Obtener la divisa de la tarifa de venta  ----

               rTar.RESET;
               rTar.GET(Delegacion, "N§ Tarifa");
               "Cod. Divisa" := rTar."C¢d. divisa";

               // ----  Registrar la creacion del precio en el historico de precios  ----

               rHist.Intro_Historico(Delegacion, rHist.Tipo::"Extra venta", "N§ Tarifa", "N§ Orden",
                                     Text000, '', '');
             END;

    OnDelete=BEGIN
               // ----  Registrar el borrado del precio en el historico de precios a partir del  ----
               // ----  N§ de orden podremos seguir el precio hasta su origen                    ----

               rHist.Intro_Historico(Delegacion, rHist.Tipo::"Extra venta", "N§ Tarifa", "N§ Orden",
                                     Text001, '', '');
             END;

    CaptionML=[ENU=Tariff Extra Prices;
               ESP=Precios Extra Tarifa];
    LookupFormID=Form7010286;
    DrillDownFormID=Form7010286;
  }
  FIELDS
  {
    { 1   ;   ;Delegacion          ;Code10        ;TableRelation=Delegacion.Codigo;
                                                   CaptionML=[ENU=Local Office;
                                                              ESP=Delegacion];
                                                   Description=PK, FK Delegacion;
                                                   Editable=No }
    { 3   ;   ;N§ Tarifa           ;Code10        ;TableRelation="Tarifa Venta Circuito"."N§ Tarifa" WHERE (Delegacion=FIELD(Delegacion));
                                                   CaptionML=[ENU=tariff No.;
                                                              ESP=N§ Tarifa];
                                                   Description=PK, FK Tarifa Venta;
                                                   Editable=No }
    { 5   ;   ;N§ Orden            ;Integer       ;CaptionML=[ENU=Order No.;
                                                              ESP=N§ Orden];
                                                   Description=PK;
                                                   Editable=No }
    { 7   ;   ;Cod. Circuito       ;Code10        ;TableRelation="Maestro circuitos".Circuito;
                                                   OnValidate=BEGIN
                                                                IF "Cod. Circuito" <> xRec."Cod. Circuito" THEN BEGIN
                                                                  Activo := comprueba_solape;
                                                                  rHist.Intro_Historico(Delegacion, rHist.Tipo::"Extra venta", "N§ Tarifa", "N§ Orden",
                                                                                        FIELDCAPTION("Cod. Circuito"), xRec."Cod. Circuito", "Cod. Circuito");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Circuit code;
                                                              ESP=Cod. Circuito];
                                                   Description=FK Maestro Circuitos }
    { 9   ;   ;Cod. Categoria      ;Code10        ;TableRelation="Codigo Circuito".Codigo WHERE (Tipo Codigo=CONST(Categoria));
                                                   OnValidate=BEGIN
                                                                IF "Cod. Categoria" <> xRec."Cod. Categoria" THEN BEGIN
                                                                  Activo := comprueba_solape;
                                                                  rHist.Intro_Historico(Delegacion, rHist.Tipo::"Extra venta", "N§ Tarifa", "N§ Orden",
                                                                                        FIELDCAPTION("Cod. Categoria"), xRec."Cod. Categoria", "Cod. Categoria");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Category code;
                                                              ESP=Cod. Categoria];
                                                   Description=FK Codigo Circuito }
    { 11  ;   ;Fecha desde         ;Date          ;OnValidate=BEGIN
                                                                IF "Fecha desde" <> xRec."Fecha desde" THEN BEGIN
                                                                  IF "Fecha hasta" <> 0D THEN BEGIN
                                                                    IF "Fecha desde" > "Fecha hasta" THEN
                                                                      ERROR(Text002);
                                                                  END;

                                                                  // ----  Comprobar que la fecha de inicio esta dentro de los limites de la tarifa  ----

                                                                  rTar.RESET;
                                                                  rTar.GET(Delegacion, "N§ Tarifa");
                                                                  IF ("Fecha desde" < rTar."Fecha inicio") OR ("Fecha desde" > rTar."Fecha final") THEN
                                                                    ERROR(Text003,
                                                                          rTar."Fecha inicio", rTar."Fecha final");

                                                                  Activo := comprueba_solape;
                                                                  rHist.Intro_Historico(Delegacion, rHist.Tipo::"Extra venta", "N§ Tarifa", "N§ Orden",
                                                                                        FIELDCAPTION("Fecha desde"), STRSUBSTNO('%1', xRec."Fecha desde"),
                                                                                        STRSUBSTNO('%1', "Fecha desde"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Date From;
                                                              ESP=Fecha desde];
                                                   NotBlank=Yes }
    { 13  ;   ;Fecha hasta         ;Date          ;OnValidate=BEGIN
                                                                IF "Fecha hasta" <> xRec."Fecha hasta" THEN BEGIN
                                                                  IF "Fecha desde" <> 0D THEN BEGIN
                                                                    IF "Fecha desde" > "Fecha hasta" THEN
                                                                      ERROR(Text004);
                                                                  END;

                                                                  // ----  Comprobar que la fecha de final esta dentro de los limites de la tarifa  ----

                                                                  rTar.RESET;
                                                                  rTar.GET(Delegacion, "N§ Tarifa");
                                                                  IF ("Fecha hasta" < rTar."Fecha inicio") OR ("Fecha hasta" > rTar."Fecha final") THEN
                                                                    ERROR(Text005,
                                                                          rTar."Fecha inicio", rTar."Fecha final");

                                                                  Activo := comprueba_solape;
                                                                  rHist.Intro_Historico(Delegacion, rHist.Tipo::"Extra venta", "N§ Tarifa", "N§ Orden",
                                                                                        FIELDCAPTION("Fecha hasta"), STRSUBSTNO('%1', xRec."Fecha hasta"),
                                                                                        STRSUBSTNO('%1', "Fecha hasta"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Date To;
                                                              ESP=Fecha hasta];
                                                   NotBlank=Yes }
    { 15  ;   ;Precio              ;Decimal       ;OnValidate=BEGIN
                                                                IF Precio <> xRec.Precio THEN BEGIN
                                                                  VALIDATE("Cod. Divisa");
                                                                  rHist.Intro_Historico(Delegacion, rHist.Tipo::"Extra venta", "N§ Tarifa", "N§ Orden",
                                                                                        FIELDCAPTION(Precio), STRSUBSTNO('%1', xRec.Precio),
                                                                                        STRSUBSTNO('%1', Precio));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Price;
                                                              ESP=Precio];
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Cod. Divisa" }
    { 17  ;   ;Tipo Precio         ;Option        ;OnValidate=BEGIN
                                                                IF "Tipo Precio" <> xRec."Tipo Precio" THEN
                                                                    rHist.Intro_Historico(Delegacion, rHist.Tipo::"Extra venta", "N§ Tarifa", "N§ Orden",
                                                                                        FIELDCAPTION("Tipo Precio"), STRSUBSTNO('%1', xRec."Tipo Precio"),
                                                                                        STRSUBSTNO('%1', "Tipo Precio"));
                                                              END;

                                                   CaptionML=[ENU=Price type;
                                                              ESP=Tipo Precio];
                                                   OptionCaptionML=[ENU=,Pax,Fixed;
                                                                    ESP=,Pax,Fijo];
                                                   OptionString=,Pax,Fijo }
    { 19  ;   ;Cod. Divisa         ;Code10        ;TableRelation=Currency.Code;
                                                   OnValidate=BEGIN
                                                                IF "Cod. Divisa" <> '' THEN BEGIN
                                                                  rTar.RESET;
                                                                  rTar.GET(Delegacion, "N§ Tarifa");
                                                                  IF rTar."Fecha inicio" = 0D THEN
                                                                    "Precio (DL)" := 0
                                                                  ELSE
                                                                    "Precio (DL)" := rTipoCambDivisa.ExchangeAmtFCYToLCY(rTar."Fecha inicio",
                                                                                                                    "Cod. Divisa",
                                                                                                                    Precio,
                                                                                                                    rTipoCambDivisa.ExchangeRate(rTar."Fecha inicio", "Cod. Divisa"));
                                                                END
                                                                ELSE
                                                                  "Precio (DL)" := Precio;
                                                              END;

                                                   CaptionML=[ENU=Currency Code;
                                                              ESP=Cod. divisa];
                                                   Description=FK Divisa;
                                                   Editable=No }
    { 21  ;   ;Precio (DL)         ;Decimal       ;CaptionML=[ENU=Price (LCY);
                                                              ESP=Precio (DL)];
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 23  ;   ;Activo              ;Boolean       ;CaptionML=[ENU=Active;
                                                              ESP=Activo];
                                                   Editable=No }
    { 25  ;   ;Cod. Extra          ;Code10        ;TableRelation="Codigo Circuito".Codigo WHERE (Tipo Codigo=CONST(Extra));
                                                   OnValidate=BEGIN
                                                                IF "Cod. Extra" <> xRec."Cod. Extra" THEN BEGIN
                                                                  Activo := comprueba_solape;
                                                                  rHist.Intro_Historico(Delegacion, rHist.Tipo::"Extra venta", "N§ Tarifa", "N§ Orden",
                                                                                        FIELDCAPTION("Cod. Extra"), xRec."Cod. Extra", "Cod. Extra");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Extra code;
                                                              ESP=Cod. Extra];
                                                   NotBlank=Yes;
                                                   Description=FK Codigo Circuito }
  }
  KEYS
  {
    {    ;Delegacion,N§ Tarifa,N§ Orden           ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1103355004 : TextConst 'ENU=CREATED;ESP=CREADO';
      Text001@1103355005 : TextConst 'ENU=Deleted;ESP=Borrado';
      Text002@1103355006 : TextConst 'ENU=Unable to indicate a date from after date to;ESP=No puede indicar una fecha desde posterior a la fecha hasta';
      Text003@1103355007 : TextConst 'ENU=Initial date of the price is out of the limits of the tariff, %1 - %2.;ESP=La fecha de inicio del precio esta fuera de los limites de la tarifa, %1 - %2.';
      Text004@1103355008 : TextConst 'ENU=Unable to indicate a date to previous than date from;ESP=No puede indicar una fecha hasta anterior a la fecha desde';
      Text005@1103355009 : TextConst 'ENU=Final date of the price is out of the limits of the tariff, %1-%2;ESP=La fecha de final del precio esta fuera de los limites de la tarifa, %1-%2.';
      Text006@1103355010 : TextConst 'ENU=Initial date of that price overlaps with another one entered previously.;ESP=La fecha de inicio de este precio se solapa con otro anteriormente introducido.';
      Text007@1103355011 : TextConst 'ENU=Final date of that price overlaps with another one entered previously.;ESP=La fecha de final de este precio se solapa con otro anteriormente introducido.';
      Text008@1103355012 : TextConst 'ENU=Entered price overlaps totally antoher one entered previously.;ESP=El precio introducido solapa completamente otro anteriormente introducido.';
      rTar@1103355000 : Record 7010191;
      rPreExt@1103355001 : Record 7010212;
      rHist@1103355002 : Record 7010209;
      rTipoCambDivisa@1103355003 : Record 330;

    PROCEDURE comprueba_solape@4() : Boolean;
    VAR
      lrPreExt@1103355000 : Record 7010212;
    BEGIN
      // comprueba_solape
      //
      IF ("Cod. Extra" = '') OR ("Fecha desde" = 0D) OR ("Fecha hasta" = 0D) THEN
        EXIT(FALSE);

      lrPreExt.RESET;
      lrPreExt.SETRANGE (Delegacion      , Delegacion);
      lrPreExt.SETRANGE ("N§ Tarifa"     , "N§ Tarifa");
      lrPreExt.SETFILTER("N§ Orden"      , '<>%1', "N§ Orden");
      lrPreExt.SETRANGE ("Cod. Extra"    , "Cod. Extra");
      lrPreExt.SETRANGE ("Cod. Circuito" , "Cod. Circuito");
      lrPreExt.SETRANGE ("Cod. Categoria", "Cod. Categoria");

      // ----  Comprobar si se solapa la fecha de inicio del precio actual se solapa con otro  ----
      // ----  ya introducido                                                                  ----

      lrPreExt.SETFILTER("Fecha desde"   , '<=%1', "Fecha desde");
      lrPreExt.SETFILTER("Fecha hasta"   , '>=%1', "Fecha desde");
      IF lrPreExt.FIND('-') THEN BEGIN
        MESSAGE(Text006);
        EXIT(FALSE);
      END;

      // ----  Comprobar si se solapa la fecha de final del precio actual se solapa con otro  ----
      // ----  ya introducido                                                                  ----

      lrPreExt.SETFILTER("Fecha desde"   , '<=%1', "Fecha hasta");
      lrPreExt.SETFILTER("Fecha hasta"   , '>=%1', "Fecha hasta");
      IF lrPreExt.FIND('-') THEN BEGIN
        MESSAGE(Text007);
        EXIT(FALSE);
      END;

      // ----  Comprobar si el precio introducido solapa completamente otro ya introducido  ----

      lrPreExt.SETFILTER("Fecha desde"   , '>=%1', "Fecha desde");
      lrPreExt.SETFILTER("Fecha hasta"   , '<=%1', "Fecha hasta");
      IF lrPreExt.FIND('-') THEN BEGIN
        MESSAGE(Text008);
        EXIT(FALSE);
      END;
      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}
