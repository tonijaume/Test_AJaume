OBJECT Table 5055255 Liquidity Journal Batch
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=LIQEM6.00.01;
  }
  PROPERTIES
  {
    DataCaptionFields=Name,Description;
    OnInsert=BEGIN
               LOCKTABLE;
               LiqJnlTemplate.GET("Journal Template Name");
             END;

    OnDelete=BEGIN
               LiqJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
               LiqJnlLine.SETRANGE("Journal Batch Name",Name);
               LiqJnlLine.DELETEALL;
             END;

    OnRename=BEGIN
               LiqJnlLine.SETRANGE("Journal Template Name",xRec."Journal Template Name");
               LiqJnlLine.SETRANGE("Journal Batch Name",xRec.Name);
               WHILE LiqJnlLine.FIND('-') DO
                 LiqJnlLine.RENAME("Journal Template Name",Name,LiqJnlLine."Line No.");
             END;

    CaptionML=[ENU=Liquidity Journal Batch;
               ESP=Secci¢n diario liquidez];
    LookupFormID=Form5055253;
  }
  FIELDS
  {
    { 1   ;   ;Journal Template Name;Code10       ;TableRelation="Liq. Journal Template";
                                                   CaptionML=[ENU=Journal Template Name;
                                                              ESP=Nombre libro diario];
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Code10        ;CaptionML=[ENU=Name;
                                                              ESP=Nombre];
                                                   NotBlank=Yes }
    { 3   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n] }
    { 4   ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   OnValidate=BEGIN
                                                                IF "Reason Code" <> xRec."Reason Code" THEN BEGIN
                                                                  LiqJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
                                                                  LiqJnlLine.SETRANGE("Journal Batch Name",Name);
                                                                  LiqJnlLine.MODIFYALL("Reason Code","Reason Code");
                                                                  MODIFY;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Reason Code;
                                                              ESP=C¢d. auditor¡a] }
    { 5   ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF "No. Series" <> '' THEN BEGIN
                                                                  LiqJnlTemplate.GET("Journal Template Name");
                                                                  IF LiqJnlTemplate.Recurring THEN
                                                                    ERROR(
                                                                      Text1140000,
                                                                      FIELDCAPTION("Posting No. Series"));
                                                                  IF "No. Series" = "Posting No. Series" THEN
                                                                    VALIDATE("Posting No. Series",'');
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No. Series;
                                                              ESP=Nos. serie] }
    { 6   ;   ;Posting No. Series  ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF ("Posting No. Series" = "No. Series") AND ("Posting No. Series" <> '') THEN
                                                                  FIELDERROR("Posting No. Series",STRSUBSTNO(Text1140001,"Posting No. Series"));
                                                                LiqJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
                                                                LiqJnlLine.SETRANGE("Journal Batch Name",Name);
                                                                LiqJnlLine.MODIFYALL("Posting No. Series","Posting No. Series");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Posting No. Series;
                                                              ESP=N§ serie registro] }
    { 9002;   ;Recurring           ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Liq. Journal Template".Recurring WHERE (Name=FIELD(Journal Template Name)));
                                                   CaptionML=[ENU=Recurring;
                                                              ESP=Peri¢dico];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Journal Template Name,Name              ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Name,Description                         }
  }
  CODE
  {
    VAR
      Text1140000@1140000 : TextConst 'ENU=Only the %1 field can be filled in on recurring journals.;ESP=S¢lo se debe completar el campo %1 en los diarios peri¢dicos.';
      Text1140001@1140001 : TextConst 'ENU=must not be %1;ESP=No puede ser %1.';
      LiqJnlTemplate@1140002 : Record 5055254;
      LiqJnlLine@1140003 : Record 5055256;

    PROCEDURE SetupNewBatch@1140000();
    BEGIN
      LiqJnlTemplate.GET("Journal Template Name");
      "No. Series" := LiqJnlTemplate."No. Series";
      "Posting No. Series" := LiqJnlTemplate."Posting No. Series";
      "Reason Code" := LiqJnlTemplate."Reason Code";
    END;

    BEGIN
    END.
  }
}
