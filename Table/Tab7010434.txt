OBJECT Table 7010434 Conversiones Importacion
{
  OBJECT-PROPERTIES
  {
    Date=27/11/14;
    Time=15:07:49;
    Modified=Yes;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    OnInsert=BEGIN

               rConv.RESET;
               rConv.SETRANGE(Tipo,Tipo);
               IF rConv.FINDLAST THEN
                 No := rConv.No +1
               ELSE
                 No :=1;

               TESTFIELD(Codigo);
             END;

    CaptionML=ESP=Conversiones Importacion;
    LookupFormID=Form7035295;
    DrillDownFormID=Form7035295;
  }
  FIELDS
  {
    { 1   ;   ;No                  ;Integer       ;CaptionML=ESP=No }
    { 3   ;   ;Tipo                ;Option        ;CaptionML=ESP=Tipo;
                                                   OptionCaptionML=ESP=Garaje,Codigo Servicio,Cliente,TTOO,Suplemento,Hotel,Extra,Linea;
                                                   OptionString=Garaje,Codigo Servicio,Cliente,TTOO,Suplemento,Hotel,Extra,Linea }
    { 4   ;   ;Codigo              ;Code21        ;OnValidate=VAR
                                                                lwMax@1100244000 : Integer;
                                                              BEGIN
                                                                CompDupl;
                                                              END;

                                                   CaptionML=ESP=Codigo;
                                                   NotBlank=Yes }
    { 5   ;   ;Codigo FinBus       ;Code10        ;TableRelation=IF (Tipo=CONST(Garaje)) Garaje.Garaje
                                                                 ELSE IF (Tipo=CONST(Codigo Servicio)) "Codigo servicio".Codigo
                                                                 ELSE IF (Tipo=CONST(Cliente)) Customer.No.
                                                                 ELSE IF (Tipo=CONST(TTOO)) TTOO.Codigo
                                                                 ELSE IF (Tipo=CONST(Suplemento)) "Codigo Suplemento".Codigo
                                                                 ELSE IF (Tipo=CONST(Hotel)) "Punto de recogida".Codigo
                                                                 ELSE IF (Tipo=CONST(Extra)) Extra.Codigo;
                                                   OnValidate=VAR
                                                                lrPunto@1100244001 : Record 7010315;
                                                              BEGIN
                                                              END;

                                                   OnLookup=VAR
                                                              lrCliTo@1103355001 : Record 7010313;
                                                              lrTTOO@1103355000 : Record 7010312;
                                                              lrGaraje@1103355002 : Record 7010310;
                                                              lrCodSer@1103355003 : Record 7010320;
                                                              lrCli@1103355004 : Record 18;
                                                              lrSupl@1103355005 : Record 7010318;
                                                              lrHotel@1103355006 : Record 7010315;
                                                              lrExtra@1103355007 : Record 7010321;
                                                            BEGIN

                                                              CASE Tipo OF
                                                                Tipo::Garaje : BEGIN
                                                                  IF FORM.RUNMODAL(0,lrGaraje) = ACTION::LookupOK THEN
                                                                    "Codigo FinBus" := lrGaraje.Garaje;
                                                                END;
                                                                Tipo::"Codigo Servicio": BEGIN
                                                                  IF FORM.RUNMODAL(0,lrCodSer) = ACTION::LookupOK THEN
                                                                    "Codigo FinBus" := lrCodSer.Codigo;
                                                                END;
                                                                Tipo::Cliente: BEGIN
                                                                  IF FORM.RUNMODAL(0,lrCli) = ACTION::LookupOK THEN
                                                                    "Codigo FinBus" := lrCli."No.";
                                                                END;
                                                                Tipo::TTOO: BEGIN
                                                                  IF Cliente <> '' THEN BEGIN
                                                                    CLEAR(lrCliTo);
                                                                    lrCliTo.FILTERGROUP(2);
                                                                    lrCliTo.SETRANGE(Cliente, Cliente);
                                                                    lrCliTo.FILTERGROUP(0);
                                                                    IF FORM.RUNMODAL(0,lrCliTo) = ACTION::LookupOK THEN
                                                                      "Codigo FinBus" := lrCliTo.TTOO;
                                                                  END
                                                                  ELSE BEGIN
                                                                    IF FORM.RUNMODAL(0,lrTTOO) = ACTION::LookupOK THEN
                                                                      "Codigo FinBus" := lrTTOO.Codigo;
                                                                  END;
                                                                END;
                                                                Tipo::Suplemento: BEGIN
                                                                  IF FORM.RUNMODAL(0,lrSupl) = ACTION::LookupOK THEN
                                                                    "Codigo FinBus" := lrSupl.Codigo;
                                                                END;
                                                                Tipo::Hotel: BEGIN
                                                                  IF FORM.RUNMODAL(0,lrHotel) = ACTION::LookupOK THEN
                                                                    "Codigo FinBus" := lrHotel.Codigo;
                                                                END;
                                                                Tipo::Extra: BEGIN
                                                                  IF FORM.RUNMODAL(0,lrExtra) = ACTION::LookupOK THEN
                                                                    "Codigo FinBus" := lrExtra.Codigo;
                                                                END;

                                                              END;
                                                            END;

                                                   CaptionML=ESP=Codigo FinBus }
    { 6   ;   ;Cliente             ;Code20        ;TableRelation=Customer.No.;
                                                   OnValidate=BEGIN
                                                                CompDupl;
                                                              END;

                                                   CaptionML=ESP=Cliente }
    { 7   ;   ;Touroperador        ;Code10        ;TableRelation=TTOO.Codigo;
                                                   OnValidate=BEGIN
                                                                CompDupl;
                                                              END;

                                                   CaptionML=ESP=Touroperador }
    { 10  ;   ;Nombre              ;Text50        ;CaptionML=ESP=Nombre }
  }
  KEYS
  {
    {    ;Tipo,No                                 ;Clustered=Yes }
    {    ;Codigo,Tipo,Cliente,Touroperador         }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      rConv@1100244000 : Record 7010434;
      Text0001@1100244001 : TextConst 'ESP=Ya existe el codigo %1 %2 %3';
      Text0002@1100244002 : TextConst 'ESP=El Codigo %1 no puede tener m s de %2 Caracteres';
      Text0003@1100244004 : TextConst 'ESP=No se encuentra ning£n Punto de Recojida con este c¢digo. ¨Quiere crearlo a partir de los datos de esta l¡nea?';
      Text0004@1100244003 : TextConst 'ESP=La Subzona Traslados %1 No tiene correspondencia Finbus';
      rPar@1100244005 : Record 7010311;
      cFunImp@1103355000 : Codeunit 7010402;

    PROCEDURE TextoFinBus@1100244000() : Text[100];
    VAR
      lrGaraje@1100244000 : Record 7010310;
      lrCodSer@1100244002 : Record 7010320;
      lrCliente@1100244003 : Record 18;
      lrTTOO@1100244004 : Record 7010312;
      lrSup@1100244005 : Record 7010318;
      lrPunt@1100244006 : Record 7010315;
      lrExtra@1100253000 : Record 7010321;
      lwTexto@1100244001 : Text[100];
    BEGIN
      // TextoFinBus
      // Devuelve el texto del codigo Finbus seleccionado

      CASE Tipo OF
        Tipo::Garaje: BEGIN
          lrGaraje.RESET;
          IF lrGaraje.GET("Codigo FinBus") THEN
            lwTexto := lrGaraje.Nombre;
        END;
        Tipo::"Codigo Servicio": BEGIN
          lrCodSer.RESET;
          IF lrCodSer.GET("Codigo FinBus") THEN
            lwTexto := lrCodSer.Descripcion;
        END;
        Tipo::Cliente: BEGIN
          lrCliente.RESET;
          IF lrCliente.GET("Codigo FinBus") THEN
            lwTexto := lrCliente.Name;
        END;
        Tipo::TTOO: BEGIN
          lrTTOO.RESET;
          IF lrTTOO.GET("Codigo FinBus") THEN
            lwTexto := lrTTOO.Nombre;
        END;
        Tipo::Suplemento: BEGIN
          lrSup.RESET;
          IF lrSup.GET("Codigo FinBus") THEN
            lwTexto := lrSup.Descripcion;
        END;
        Tipo::Hotel: BEGIN
          lrPunt.RESET;
          IF lrPunt.GET("Codigo FinBus") THEN
            lwTexto := lrPunt.Descripci¢n;
        END;
        Tipo::Extra: BEGIN
          CLEAR(lrExtra);
          IF lrExtra.GET("Codigo FinBus") THEN
            lwTexto :=lrExtra.Descripcion;
        END;
      END;

      EXIT(lwTexto);
    END;

    PROCEDURE CompDupl@1100244001();
    BEGIN
      // CompDupl
      // Comprueba que el codigo no se duplique

      rConv.RESET;
      rConv.SETCURRENTKEY(Codigo,Tipo,Cliente,Touroperador);
      rConv.SETRANGE(Tipo,Tipo);
      rConv.SETFILTER(Codigo,'%1',Codigo);
      rConv.SETFILTER(No,'<>%1',No);
      rConv.SETFILTER(Cliente      , '%1' , Cliente);
      rConv.SETFILTER(Touroperador , '%1' , Touroperador);
      IF rConv.FINDFIRST THEN
         ERROR(Text0001,Codigo,Cliente,Touroperador);
    END;

    PROCEDURE Max@1100244002() : Integer;
    VAR
      lwMax@1100244000 : Integer;
    BEGIN
      // Max
      // Devuelve el largo maximo seg£n el tipo
      CASE Tipo OF
        Tipo::Garaje           :lwMax:=2;
        Tipo::Cliente          :lwMax:=5;
        Tipo::TTOO             :lwMax:=10; // Es la suma de cliente y TTOO// El maximo de TTOO es 4, ponemos un '-' entre
        Tipo::"Codigo Servicio":lwMax:=5;
        Tipo::Suplemento       :lwMax:=10;
        Tipo::Extra            :lwMax:=10;
      END;

      EXIT(lwMax);
    END;

    BEGIN
    END.
  }
}
