OBJECT Table 7010091 Detalle Tarifa concepto hotel
{
  OBJECT-PROPERTIES
  {
    Date=02/10/12;
    Time=12:00:00;
    Modified=Yes;
    Version List=AIC2009,CNT;
  }
  PROPERTIES
  {
    OnInsert=VAR
               lrTar@1100253000 : Record 7009898;
             BEGIN
               //. Rellenar clave primaria
               "ID Detalle"        := NextID;

               //. Datos heredados de la tarifa

               lrTar.GET("ID Tarifa");
               Delegacion      := lrTar.Delegacion;
               "Cod. Divisa"   := lrTar."Cod. Divisa";
               "Fecha Inicial" := lrTar."Fecha Inicial";
               "Fecha Final"   := lrTar."Fecha Final";

               //. Datos de revision
               "Fecha Creacion"   := CURRENTDATETIME;
               "Usuario Creacion" := USERID;

               //. Historico de cambios
               IntroHist(Text004,'','');
             END;

    OnModify=BEGIN

               //. Comprobar si el precio se duplica
               _CheckDuplicado;

               //. Historico de cambios
               ModIntroHist;
             END;

    OnDelete=BEGIN
               //. Historico de cambios
               IntroHist(Text005,'','');
             END;

    CaptionML=[ENU=Detail Hotel concept rate;
               ESP=Detalle Tarifa concepto hotel];
  }
  FIELDS
  {
    { 1   ;   ;ID Tarifa           ;Code10        ;TableRelation="Tarifa concepto hotel"."ID Tarifa";
                                                   CaptionML=[ENU=Rate ID;
                                                              ESP=ID Tarifa];
                                                   Description=PK, "Tarifa concepto hotel"."ID Tarifa";
                                                   Editable=No }
    { 3   ;   ;ID Detalle          ;Integer       ;CaptionML=[ENU=Detail ID;
                                                              ESP=ID Detalle];
                                                   Description=PK;
                                                   Editable=No }
    { 5   ;   ;Delegacion          ;Code10        ;TableRelation=Delegacion.Codigo;
                                                   CaptionML=[ENU=Destination;
                                                              ESP=Delegacion];
                                                   Description=Delegacion.Delegacion;
                                                   Editable=No }
    { 30  ;   ;Fecha Inicial       ;Date          ;OnValidate=BEGIN
                                                                _CheckFechas;
                                                              END;

                                                   CaptionML=[ENU=Fron Date;
                                                              ESP=Fecha Inicial];
                                                   NotBlank=Yes }
    { 40  ;   ;Fecha Final         ;Date          ;OnValidate=BEGIN
                                                                _CheckFechas;
                                                              END;

                                                   CaptionML=[ENU=To Date;
                                                              ESP=Fecha Final];
                                                   NotBlank=Yes }
    { 50  ;   ;Precio Adulto       ;Decimal       ;CaptionML=[ENU=Adult Price;
                                                              ESP=Precio Adulto];
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Cod. Divisa" }
    { 60  ;   ;Precio Ni¤o         ;Decimal       ;CaptionML=[ENU=Child Price;
                                                              ESP=Precio Ni¤o];
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Cod. Divisa" }
    { 70  ;   ;Aplicar a Reservas  ;Option        ;CaptionML=[ENU=Apply to Reservations;
                                                              ESP=Aplicar a Reservas];
                                                   OptionCaptionML=[ENU=With Transfer,Without Transfer,All;
                                                                    ESP=Con Transfer,Sin Transfer,Todas];
                                                   OptionString=Con Transfer,Sin Transfer,Todas }
    { 75  ;   ;Hotel TO            ;Code20        ;TableRelation="Conversion hotel"."Hotel TO";
                                                   OnLookup=VAR
                                                              lrConvH@1100253001 : Record 7009725;
                                                              lrTar@1100253000 : Record 7009898;
                                                            BEGIN
                                                              lrTar.GET("ID Tarifa");
                                                              lrConvH.SETFILTER("Touroperador venta", '%1|%2', lrTar.Touroperador, '');
                                                              IF FORM.RUNMODAL(0, lrConvH) = ACTION::LookupOK  THEN
                                                                VALIDATE("Hotel TO", lrConvH."Hotel TO");
                                                            END;

                                                   CaptionML=[ENU=TO Hotel code;
                                                              ESP=Hotel TO];
                                                   Description=FK Conversion Hotel }
    { 130 ;   ;Fecha Creacion      ;DateTime      ;CaptionML=[ENU=Creation Date;
                                                              ESP=Fecha Creaci¢n];
                                                   Editable=No }
    { 140 ;   ;Usuario Creacion    ;Code20        ;TableRelation=User."User ID";
                                                   CaptionML=[ENU=Creation User;
                                                              ESP=Usuario Creaci¢n];
                                                   Description=User."User ID";
                                                   Editable=No }
    { 141 ;   ;Cod. Divisa         ;Code10        ;TableRelation=Currency.Code;
                                                   Description=Currency.Code }
  }
  KEYS
  {
    {    ;ID Tarifa,ID Detalle                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100253000 : TextConst 'ENU=%1 can not be later than%2;ESP=%1 No puede ser posterior a %2';
      Text002@1100253001 : TextConst 'ENU=The Field %1 Must be completed;ESP=El campo %1 Debe de estar rellenado';
      Text003@1100253002 : TextConst 'ENU=The new price overlaps with another one already existing.;ESP=El precio introducido se solapa con uno ya existente.';
      Text004@1100253003 : TextConst 'ENU=New;ESP=Nuevo';
      Text005@1100253004 : TextConst 'ENU=Deleted;ESP=Borrado';
      rPar@1100253005 : Record 7009700;
      Text006@1100253007 : TextConst 'ENU=The specified dates are outside the period of the rate.;ESP=Las fechas especificadas estan fuera de las fechas de tarifa.';
      cPar@1100253006 : Codeunit 7009700;

    LOCAL PROCEDURE _CheckFechas@1100253001();
    VAR
      lrTar@1100253000 : Record 7009898;
    BEGIN
      // _CheckFechas

      IF ("Fecha Inicial" = 0D) OR ("Fecha Final" = 0D) THEN
        EXIT;

      IF ("Fecha Inicial" > "Fecha Final") THEN
        ERROR(Text001, FIELDCAPTION("Fecha Inicial"), FIELDCAPTION("Fecha Final"));

      lrTar.GET("ID Tarifa");
      IF ("Fecha Inicial" < lrTar."Fecha Inicial") OR
         ("Fecha Final" > lrTar."Fecha Final") THEN
        ERROR(Text006);
    END;

    LOCAL PROCEDURE _CheckDuplicado@1100253000();
    VAR
      lrPre@1100253000 : Record 7010091;
    BEGIN
      // _CheckDuplicado
      // Comprueba que no existan dos precios para el mismo TTOO y fechas

      CLEAR(lrPre);
      lrPre.SETRANGE ("ID Tarifa"         , "ID Tarifa");
      lrPre.SETFILTER("ID Detalle"        , '<>%1', "ID Detalle");
      lrPre.SETFILTER("Fecha Inicial"     , '<=%1', "Fecha Final");
      lrPre.SETFILTER("Fecha Final"       , '>=%1', "Fecha Inicial");
      lrPre.SETRANGE ("Aplicar a Reservas", "Aplicar a Reservas");
      lrPre.SETRANGE ("Hotel TO"          , "Hotel TO");
      IF NOT lrPre.ISEMPTY THEN
        ERROR(Text003);
    END;

    PROCEDURE NextID@1100253005() : Integer;
    VAR
      lrPre@1100253000 : Record 7010091;
      lwID@1100253001 : Integer;
    BEGIN
      // NextID

      lwID := 0;

      CLEAR(lrPre);
      lrPre.SETRANGE("ID Tarifa", "ID Tarifa");
      IF lrPre.FINDLAST THEN
        lwID := lrPre."ID Detalle" + 1
      ELSE
        lwID := 1;

      EXIT(lwID);
    END;

    PROCEDURE IntroHist@1100253002(pwCampo@1100253000 : Text[30];pwValorAnterior@1100253001 : Text[30];pwNuevoValor@1100253002 : Text[30]);
    BEGIN
      // IntroHist

      cPar.intro_historico_precios(Delegacion, "ID Tarifa", 2, 0, "ID Detalle", pwCampo, pwNuevoValor, pwValorAnterior);
    END;

    PROCEDURE ModIntroHist@1100253006();
    VAR
      lwRec@1100253000 : RecordRef;
      lwXRec@1100253001 : RecordRef;
      lwField@1100253002 : FieldRef;
      lwXField@1100253003 : FieldRef;
      lwCnt@1100253004 : Integer;
    BEGIN
      // ModIntroHist

      lwRec.GETTABLE(Rec);
      lwXRec.GETTABLE(xRec);

      FOR lwCnt := 1 TO lwRec.FIELDCOUNT DO BEGIN
        lwField  := lwRec.FIELDINDEX(lwCnt);
        lwXField := lwXRec.FIELDINDEX(lwCnt);
        IF lwXField.VALUE <> lwField.VALUE THEN
          IntroHist(lwField.CAPTION, cPar.FieldText(lwXField), cPar.FieldText(lwField));
      END;
    END;

    PROCEDURE ShowLog@1100253003();
    VAR
      lrHist@1100253000 : Record 7009765;
    BEGIN
      // ShowLog

      CLEAR(lrHist);
      lrHist.FILTERGROUP(2);
      lrHist.SETRANGE(Delegacion     , Delegacion );
      lrHist.SETRANGE("N§ Contrato"  , "ID Tarifa");
      lrHist.SETRANGE(Tipo           , lrHist.Tipo::"Tarifa Concepto");
      lrHist.SETRANGE("Codigo Precio", 0);
      lrHist.SETRANGE("Orden Precio" , "ID Detalle");
      lrHist.FILTERGROUP(0);

      FORM.RUN(7009833, lrHist);
    END;

    BEGIN
    {
      JPT 23/04/12 CNT-CAR-12004 HOT-18
    }
    END.
  }
}
