OBJECT Table 7009995 Seguimiento Contrato
{
  OBJECT-PROPERTIES
  {
    Date=08/07/15;
    Time=[ 8:56:44];
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               // ERROR(Text0002);
             END;

    CaptionML=[ENU=Contract control;
               ESP=Seguimiento Contrato];
    LookupFormID=Form7009986;
    DrillDownFormID=Form7009986;
  }
  FIELDS
  {
    { 1   ;   ;Id                  ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[ENU=ID;
                                                              ESP=ID] }
    { 10  ;   ;Delegacion          ;Code10        ;TableRelation=Delegacion.Codigo;
                                                   CaptionML=[ENU=Local Office;
                                                              ESP=Delegacion] }
    { 11  ;   ;No Contrato         ;Code10        ;CaptionML=[ENU=Contract No.;
                                                              ESP=No Contrato] }
    { 20  ;   ;Hito                ;Option        ;CaptionML=[ENU=Milestone;
                                                              ESP=Hito];
                                                   OptionCaptionML=[ENU=Rooms,Allotments,Board rates,Room rates,Supplements,Extras,Offers;
                                                                    ESP=Habitaciones,Cupos,Precios Regimen,Precios Habitacion,Suplementos,Extras,Ofertas];
                                                   OptionString=Habitaciones,Cupos,Precios Regimen,Precios Habitacion,Suplementos,Extras,Ofertas }
    { 21  ;   ;Completado          ;Boolean       ;OnValidate=VAR
                                                                lrConv@1103355000 : Record 7009731;
                                                                lrPrecC@1103355001 : Record 7009738;
                                                                lrCupos@1103355002 : Record 7009739;
                                                              BEGIN
                                                                IF Completado THEN BEGIN
                                                                  "Usuario Confirmacion" := USERID;
                                                                  "Fecha Confirmacion"   := CURRENTDATETIME;
                                                                END
                                                                ELSE BEGIN
                                                                  IF NOT CONFIRM(Text0005) THEN
                                                                    ERROR('');

                                                                  "Usuario Retrocesion"  := USERID;
                                                                  "Fecha Retrocesion"    := CURRENTDATETIME;
                                                                END;

                                                                // Comprobaciones
                                                                IF Completado THEN BEGIN
                                                                  CASE Hito OF
                                                                    Hito::Habitaciones: BEGIN
                                                                      CLEAR(lrConv);
                                                                      lrConv.SETRANGE(Delegacion   , Delegacion);
                                                                      lrConv.SETRANGE("N§ Contrato", "No Contrato");
                                                                      lrConv.SETRANGE("Tipo contrato", lrConv."Tipo contrato"::Compra);
                                                                      IF NOT lrConv.FINDFIRST THEN
                                                                        ERROR(Text0003, Hito, "No Contrato");
                                                                    END;
                                                                    Hito::Cupos: BEGIN
                                                                      // Los cupos no son obligatorios
                                                                      // Solo se comprueba que no haya lineas sin activar
                                                                      CLEAR(lrCupos);
                                                                      lrCupos.SETRANGE(Delegacion          , Delegacion);
                                                                      lrCupos.SETRANGE("N§ Contrato compra", "No Contrato");
                                                                      lrCupos.SETRANGE(Activo, FALSE);
                                                                      IF lrCupos.FINDFIRST THEN
                                                                        ERROR(Text0004, lrCupos.TABLECAPTION , "No Contrato");
                                                                    END;
                                                                    ELSE BEGIN
                                                                      CLEAR(lrPrecC);
                                                                      lrPrecC.SETRANGE(Delegacion  ,  Delegacion);
                                                                      lrPrecC.SETRANGE("N§ Contrato", "No Contrato");
                                                                      CASE Hito OF
                                                                        Hito::"Precios Regimen"   : lrPrecC.SETRANGE("Codigo Precio" , lrPrecC."Codigo Precio"::Regimen);
                                                                        Hito::"Precios Habitacion": lrPrecC.SETRANGE("Codigo Precio" , lrPrecC."Codigo Precio"::Habitacion);
                                                                        Hito::Suplementos         : lrPrecC.SETRANGE("Codigo Precio" , lrPrecC."Codigo Precio"::Suplementos);
                                                                        Hito::Extras              : lrPrecC.SETRANGE("Codigo Precio" , lrPrecC."Codigo Precio"::Extras);
                                                                        Hito::Ofertas             : lrPrecC.SETRANGE("Codigo Precio" , lrPrecC."Codigo Precio"::Ofertas);
                                                                      END;
                                                                      // AJF 01/10/08
                                                                      // Para las ofertas pedimos confirmaci¢n en lugar de dar el error directamente.
                                                                      IF NOT lrPrecC.FINDFIRST THEN BEGIN
                                                                        // AJS 10.10.2008
                                                                        // Tambien para suplementos y extras

                                                                        IF Hito IN [Hito::Ofertas, Hito::Extras, Hito::Suplementos] THEN BEGIN
                                                                          IF NOT CONFIRM(Text0006, FALSE, Hito, "No Contrato") THEN
                                                                            ERROR(Text0003, Hito, "No Contrato");
                                                                        END
                                                                        ELSE
                                                                          ERROR(Text0003, Hito, "No Contrato");
                                                                      END;
                                                                      // JPT 18/09/08 No dejamos que haya lineas sin activar
                                                                      lrPrecC.SETRANGE(Activo, FALSE);
                                                                      IF lrPrecC.FINDFIRST THEN
                                                                        ERROR(Text0004, Hito, "No Contrato");
                                                                    END;
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Completed;
                                                              ESP=Completado] }
    { 50  ;   ;Usuario Confirmacion;Code20        ;TableRelation=User."User ID";
                                                   CaptionML=[ENU=Confirmation user;
                                                              ESP=Usuario confirmacion];
                                                   Editable=No }
    { 51  ;   ;Fecha Confirmacion  ;DateTime      ;CaptionML=[ENU=Confirmation date;
                                                              ESP=Fecha Confirmacion];
                                                   Editable=No }
    { 52  ;   ;Usuario Retrocesion ;Code20        ;TableRelation=User."User ID";
                                                   CaptionML=[ENU=Retreat User;
                                                              ESP=Usuario Retrocesion];
                                                   Editable=No }
    { 53  ;   ;Fecha Retrocesion   ;DateTime      ;CaptionML=[ENU=Retreat Date;
                                                              ESP=Fecha Retrocesion];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Id                                      ;Clustered=Yes }
    {    ;Delegacion,No Contrato,Hito             ;KeyGroups=RENUMERING }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text0001@1103355000 : TextConst 'ENU=Completed %1;ESP=Completado %1';
      Text0002@1103355001 : TextConst 'ENU=Unable to delete the registration;ESP=No es posible eliminar el registro';
      Text0003@1103355002 : TextConst 'ENU=Any %1 has been found for the contract %2;ESP=No se ha encontrado ning£n %1 para el contrato %2';
      Text0004@1103355003 : TextConst 'ENU="It has %1 non-actived lines for the contract %2 ";ESP=Tiene lineas %1 sin Activar para el contrato %2';
      Text0005@1103355004 : TextConst 'ENU=Confirm to dismark this must-see;ESP=Confirma que desea desmarcar este hito.';
      Text0006@1103355005 : TextConst 'ENU=Any %1 has been found for the contract %2. Would you like to complete the must-see;ESP=No se ha encontrado ning£n %1 para el contrato %2. ¨Desea completar el hito de todas maneras?';

    PROCEDURE Descripcion@1103355000() : Text[30];
    BEGIN
      // Descripcion

      EXIT(STRSUBSTNO(Text0001, Hito)) ;
    END;

    BEGIN
    {
      $001 AJS 08072015 Establezco la TableRelation del campo Delegacion
    }
    END.
  }
}
