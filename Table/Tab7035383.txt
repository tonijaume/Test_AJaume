OBJECT Table 7035383 Tarifa Compra Otros
{
  OBJECT-PROPERTIES
  {
    Date=16/10/09;
    Time=12:30:49;
    Version List=TRANSFER,TER;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CLEAR(rPre);
               rPre.SETRANGE(Concepto,Concepto);
               rPre.ASCENDING(FALSE);
               IF rPre.FINDFIRST THEN
                 No := rPre.No +1
               ELSE
                 No := 1;

               CompruebaSolape;
             END;

    OnModify=BEGIN
               CompruebaSolape;
             END;

    CaptionML=ESP=Tarifa Compra Otros;
  }
  FIELDS
  {
    { 1   ;   ;Concepto            ;Option        ;CaptionML=ESP=Concepto;
                                                   OptionCaptionML=ESP=Agua,Toallas,Gasoil;
                                                   OptionString=Agua,Toallas,Gasoil;
                                                   Editable=No }
    { 2   ;   ;No                  ;Integer       ;CaptionML=ESP=No;
                                                   Editable=No }
    { 11  ;   ;Fecha Desde         ;Date          ;OnValidate=BEGIN
                                                                CompruebaSolape;
                                                              END;

                                                   CaptionML=ESP=Fecha Desde }
    { 12  ;   ;Fecha Hasta         ;Date          ;OnValidate=BEGIN
                                                                CompruebaSolape;
                                                              END;

                                                   CaptionML=ESP=Fecha Hasta }
    { 15  ;   ;Proveedor           ;Code20        ;TableRelation=Vendor.No.;
                                                   OnValidate=BEGIN
                                                                CompruebaSolape;
                                                              END;

                                                   CaptionML=ESP=Proveedor }
    { 20  ;   ;Precio              ;Decimal       ;CaptionML=ESP=Precio;
                                                   DecimalPlaces=0:2 }
    { 22  ;   ;Divisa              ;Code10        ;TableRelation=Currency.Code;
                                                   CaptionML=ESP=Divisa }
    { 25  ;   ;Grupo registro IVA prod.;Code10    ;TableRelation="VAT Product Posting Group";
                                                   CaptionML=ESP=Grupo registro IVA Producto }
  }
  KEYS
  {
    {    ;Concepto,No                             ;Clustered=Yes }
    {    ;Concepto,Fecha Desde,Fecha Hasta         }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      rPre@1103350000 : Record 7035383;
      Text0001@1103350001 : TextConst 'ESP=El precio %1 de la tarifa de %2 se est  solapando con otro precio';
      Text0002@1103350002 : TextConst 'ESP=%1 No puede ser superior a %2';
      rConVDiv@1103350003 : Record 330;

    PROCEDURE CompruebaSolape@1103350000();
    BEGIN
      // CompruebaSolape

      IF ("Fecha Hasta" <> 0D) AND ("Fecha Hasta" < "Fecha Desde") THEN
        ERROR(Text0002, FIELDCAPTION("Fecha Desde"),FIELDCAPTION("Fecha Hasta"));


      CLEAR(rPre);
      rPre.SETRANGE(Concepto,Concepto);
      rPre.SETFILTER(No           ,'<>%1',No);
      rPre.SETFILTER("Fecha Desde",'<=%1',"Fecha Hasta");
      rPre.SETFILTER("Fecha Hasta",'>=%1',"Fecha Desde");
      IF Concepto = Concepto::Gasoil THEN // En la tarifa de Gasoil permitimos varios proveedores
        rPre.SETRANGE(Proveedor  , Proveedor);
      IF rPre.FINDFIRST THEN
        ERROR(Text0001,No,Concepto);
    END;

    PROCEDURE PrecioDL@1103350001() : Decimal;
    VAR
      lwFecha@1103350000 : Date;
    BEGIN
      // PrecioDL
      // Devuelve al precio DL a fecha de hoy

      lwFecha := TODAY;
      EXIT(rConVDiv.ExchangeAmtFCYToFCY(lwFecha,Divisa,'',Precio));
    END;

    BEGIN
    END.
  }
}
