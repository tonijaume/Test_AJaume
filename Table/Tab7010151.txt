OBJECT Table 7010151 Precios Compra Excursiones
{
  OBJECT-PROPERTIES
  {
    Date=30/09/14;
    Time=10:15:19;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               rPar.FINDFIRST;
               IF NOT rPar."Empresa Delegacion" THEN
                 ERROR(Text001);

               rPre.RESET;
               rPre.SETRANGE(Delegacion   , Delegacion);
               rPre.SETRANGE("N§ Contrato", "N§ Contrato");
               rPre.SETRANGE("Tipo precio", "Tipo precio");
               IF rPre.FINDLAST THEN
                 "Orden precio" := rPre."Orden precio" + 1
               ELSE
                 "Orden precio" := 1;

               IF "Tipo aplicacion" = 0 THEN
                 "Tipo aplicacion" := "Tipo aplicacion"::Pax;

               rPre.RESET;
               IF rConC.GET(Delegacion, "N§ Contrato", rConC."Tipo Contrato"::Excursion) THEN
               BEGIN
                 "Cod. divisa" := rConC."Cod. divisa";
                 //+$002
                 "Fecha desde" := rConC."Fecha inicio contrato";
                 "Fecha hasta" := rConC."Fecha final contrato";
                 //+$002
               END;
             END;

    OnModify=BEGIN
               rPar.FINDFIRST;
               IF NOT rPar."Empresa Delegacion" THEN
                 ERROR(Text002);

               IF "Fecha desde" = 0D THEN
                 MESSAGE(Text003);
               IF "Fecha hasta" = 0D THEN
                 MESSAGE(Text004);
               IF "Rango desde" = 0 THEN
                 MESSAGE(Text005);
               IF "Rango hasta" = 0 THEN
                 MESSAGE(Text006);

               CASE "Tipo precio" OF
                 "Tipo precio"::Suplemento:
                   IF "Codigo Suplemento" = '' THEN
                     MESSAGE(Text007);
                 "Tipo precio"::Oferta:
                   IF "Codigo Oferta" = '' THEN
                     MESSAGE(Text008);
                 // < $007
                 "Tipo precio"::Promocion: BEGIN
                   IF "Fecha venta desde" = 0D THEN
                     MESSAGE(Text003);
                   IF "Fecha venta hasta" = 0D THEN
                     MESSAGE(Text004);
                   IF "Codigo Oferta" = '' THEN
                     MESSAGE(Text008);
                 END;
                 // $007 >
               END;
             END;

    OnDelete=BEGIN
               rPar.FINDFIRST;
               IF NOT rPar."Empresa Delegacion" THEN
                 ERROR(Text009);

               cPar.intro_historico_precios_exc(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio",
                                                "Orden precio", Text010, Text011, '');
             END;

    OnRename=BEGIN
               rPar.FINDFIRST;
               IF NOT rPar."Empresa Delegacion" THEN
                 ERROR(Text012);
             END;

    CaptionML=[ENU=Excursions Purchase prices;
               ESP=Precios Compra Excursiones];
  }
  FIELDS
  {
    { 1   ;   ;Delegacion          ;Code10        ;TableRelation=Delegacion.Codigo;
                                                   CaptionML=[ENU=Local Office;
                                                              ESP=Delegacion];
                                                   Description=PK, FK Delegacion;
                                                   Editable=No }
    { 3   ;   ;N§ Contrato         ;Code10        ;CaptionML=[ENU=Contract No.;
                                                              ESP=N§ Contrato];
                                                   Description=PK;
                                                   Editable=No }
    { 5   ;   ;Tipo precio         ;Option        ;CaptionML=[ENU=Price Type;
                                                              ESP=Tipo precio];
                                                   OptionCaptionML=[ENU=Excursion Price,Supplement,Offer,Promotion;
                                                                    ESP=Precio Excursion,Suplemento,Oferta,Promocion];
                                                   OptionString=Precio Excursion,Suplemento,Oferta,Promocion;
                                                   Description=PK;
                                                   Editable=No }
    { 7   ;   ;Orden precio        ;Integer       ;CaptionML=[ENU=Price order;
                                                              ESP=Orden Precio];
                                                   Description=PK;
                                                   Editable=No }
    { 9   ;   ;Fecha desde         ;Date          ;OnValidate=BEGIN
                                                                IF "Fecha desde" <> xRec."Fecha desde" THEN BEGIN
                                                                  IF "Fecha hasta" <> 0D THEN BEGIN
                                                                    IF "Fecha desde" > "Fecha hasta" THEN
                                                                      ERROR(Text013);
                                                                  END;

                                                                  VALIDATE(Activo, FALSE);

                                                                  cPar.intro_historico_precios_exc(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                   FIELDCAPTION("Fecha desde"),
                                                                                                   FORMAT(xRec."Fecha desde"), FORMAT("Fecha desde"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Date From;
                                                              ESP=Fecha desde] }
    { 11  ;   ;Fecha hasta         ;Date          ;OnValidate=BEGIN
                                                                IF "Fecha hasta" <> xRec."Fecha hasta" THEN BEGIN
                                                                  IF "Fecha desde" <> 0D THEN
                                                                    IF "Fecha desde" > "Fecha hasta" THEN
                                                                      ERROR(Text013);

                                                                  VALIDATE(Activo, FALSE);

                                                                  cPar.intro_historico_precios_exc(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                   FIELDCAPTION("Fecha hasta"),
                                                                                                   FORMAT(xRec."Fecha hasta"), FORMAT("Fecha hasta"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Date To;
                                                              ESP=Fecha hasta] }
    { 14  ;   ;Precio              ;Decimal       ;OnValidate=BEGIN
                                                                IF Precio <> xRec.Precio THEN
                                                                  cPar.intro_historico_precios_exc(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                   FIELDCAPTION(Precio),
                                                                                                   FORMAT(xRec.Precio), FORMAT(Precio));

                                                                //-$006IF Porcentaje THEN BEGIN
                                                                //-$006  "Precio (DL)" := Precio;
                                                                //-$006  EXIT;
                                                                //-$006END;

                                                                //-$006IF rConC.GET(Delegacion, "N§ Contrato", rConC."Tipo Contrato"::Excursion) THEN BEGIN
                                                                //-$006  IF "Cod. divisa" <> '' THEN BEGIN
                                                                //-$006    IF rConC."Fecha firma contrato" = 0D THEN
                                                                //-$006      "Precio (DL)" := 0
                                                                //-$006    ELSE
                                                                //-$006      "Precio (DL)" := rTipoCambioDivisa.ExchangeAmtFCYToLCY(rConC."Fecha firma contrato",
                                                                //-$006                                                        "Cod. divisa",
                                                                //-$006                                                        Precio,
                                                                //-$006                                                        rTipoCambioDivisa.ExchangeRate(rConC."Fecha firma contrato",
                                                                //-$006                                                                                     "Cod. divisa"));
                                                                //-$006  END
                                                                //-$006  ELSE
                                                                //-$006    "Precio (DL)" := Precio;
                                                                //-$006END
                                                                //-$006ELSE
                                                                //-$006  "Precio (DL)" := 0;
                                                              END;

                                                   CaptionML=[ENU=Price;
                                                              ESP=Precio];
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Cod. divisa" }
    { 15  ;   ;O_Precio (DL)       ;Decimal       ;OnValidate=BEGIN
                                                                IF "O_Precio (DL)" <> xRec."O_Precio (DL)" THEN
                                                                  cPar.intro_historico_precios_exc(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                   FIELDCAPTION("O_Precio (DL)"),
                                                                                                   FORMAT(xRec."O_Precio (DL)"), FORMAT("O_Precio (DL)"));
                                                              END;

                                                   CaptionML=[ENU=Price (LCY);
                                                              ESP=Precio (DL)];
                                                   Description=$006;
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 17  ;   ;Tipo aplicacion     ;Option        ;OnValidate=BEGIN
                                                                IF "Tipo aplicacion" <> xRec."Tipo aplicacion" THEN
                                                                BEGIN
                                                                  _CheckCosteTicket; //+$003

                                                                  cPar.intro_historico_precios_exc(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                   FIELDCAPTION("Tipo aplicacion"),
                                                                                                   FORMAT(xRec."Tipo aplicacion"), FORMAT("Tipo aplicacion"));

                                                                  IF "Tipo aplicacion" = "Tipo aplicacion"::Servicio THEN
                                                                    CLEAR("Zona Hotel");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Application type;
                                                              ESP=Tipo aplicacion];
                                                   OptionCaptionML=[ENU=,Service,Pax;
                                                                    ESP=,Servicio,Pax];
                                                   OptionString=,Servicio,Pax }
    { 19  ;   ;Cod. divisa         ;Code10        ;TableRelation=Currency.Code;
                                                   OnValidate=BEGIN
                                                                IF "Cod. divisa" <> xRec."Cod. divisa" THEN
                                                                  cPar.intro_historico_precios_exc(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                   FIELDCAPTION("Cod. divisa"),
                                                                                                   FORMAT(xRec."Cod. divisa"), FORMAT("Cod. divisa"));
                                                              END;

                                                   CaptionML=[ENU=Currency code;
                                                              ESP=C¢d. divisa];
                                                   Description=FK Divisa;
                                                   Editable=No }
    { 23  ;   ;Rango desde         ;Integer       ;OnValidate=BEGIN
                                                                IF "Rango desde" <> xRec."Rango desde" THEN BEGIN
                                                                  IF "Rango hasta" <> 0 THEN
                                                                     IF "Rango desde" > "Rango hasta" THEN
                                                                       ERROR(Text014);

                                                                  VALIDATE(Activo, FALSE);

                                                                  cPar.intro_historico_precios_exc(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                   FIELDCAPTION("Rango desde"),
                                                                                                   FORMAT(xRec."Rango desde"), FORMAT("Rango desde"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Range From;
                                                              ESP=Rango desde] }
    { 25  ;   ;Rango hasta         ;Integer       ;OnValidate=BEGIN
                                                                IF "Rango hasta" <> xRec."Rango hasta" THEN BEGIN
                                                                  IF "Rango desde" <> 0 THEN
                                                                     IF "Rango desde" > "Rango hasta" THEN
                                                                       ERROR(Text014);

                                                                  VALIDATE(Activo, FALSE);

                                                                  cPar.intro_historico_precios_exc(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                   FIELDCAPTION("Rango hasta"),
                                                                                                   FORMAT(xRec."Rango hasta"), FORMAT("Rango hasta"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Range To;
                                                              ESP=Rango hasta] }
    { 35  ;   ;Activo              ;Boolean       ;OnValidate=BEGIN
                                                                Comprobar_Contrato;
                                                                Activo := Activar_Precio;
                                                              END;

                                                   CaptionML=[ENU=Active;
                                                              ESP=Activo];
                                                   Editable=No }
    { 36  ;   ;Precio Adulto       ;Decimal       ;OnValidate=BEGIN
                                                                IF "Precio Adulto" <> xRec."Precio Adulto" THEN
                                                                  cPar.intro_historico_precios_guia_o(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                      FIELDCAPTION("Precio Adulto"),
                                                                                                      FORMAT(xRec."Precio Adulto"), FORMAT("Precio Adulto"));

                                                                //-$006IF rConC.GET(Delegacion, "N§ Contrato", rConC."Tipo Contrato"::Excursion) THEN BEGIN
                                                                //-$006  IF "Cod. divisa" <> '' THEN BEGIN
                                                                //-$006    IF rConC."Fecha firma contrato" = 0D THEN
                                                                //-$006      "Precio Adulto (DL)" := 0
                                                                //-$006    ELSE
                                                                //-$006      "Precio Adulto (DL)" := rTipoCambioDivisa.ExchangeAmtFCYToLCY(rConC."Fecha firma contrato",
                                                                //-$006                                                        "Cod. divisa",
                                                                //-$006                                                        "Precio Adulto",
                                                                //-$006                                               rTipoCambioDivisa.ExchangeRate(rConC."Fecha firma contrato", "Cod. divisa"));
                                                                //-$006  END
                                                                //-$006  ELSE
                                                                //-$006    "Precio Adulto (DL)" := "Precio Adulto";
                                                                //-$006END
                                                                //-$006ELSE
                                                                //-$006  "Precio Adulto (DL)" := 0;
                                                              END;

                                                   CaptionML=[ENU=Adult price;
                                                              ESP=Precio Adulto];
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Cod. divisa" }
    { 37  ;   ;O_Precio Adulto (DL);Decimal       ;CaptionML=[ENU=Adult price (LCY);
                                                              ESP=Precio Adulto (DL)];
                                                   Description=$006;
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 38  ;   ;Precio Ni¤o         ;Decimal       ;OnValidate=BEGIN
                                                                IF "Precio Ni¤o" <> xRec."Precio Ni¤o" THEN
                                                                  cPar.intro_historico_precios_guia_o(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                      FIELDCAPTION("Precio Ni¤o"),
                                                                                                      FORMAT(xRec."Precio Ni¤o"), FORMAT("Precio Ni¤o"));

                                                                //-$006IF rConC.GET(Delegacion, "N§ Contrato", rConC."Tipo Contrato"::Excursion) THEN BEGIN
                                                                //-$006  IF "Cod. divisa" <> '' THEN BEGIN
                                                                //-$006    IF rConC."Fecha firma contrato" = 0D THEN
                                                                //-$006      "Precio Ni¤o (DL)" := 0
                                                                //-$006    ELSE
                                                                //-$006      "Precio Ni¤o (DL)" := rTipoCambioDivisa.ExchangeAmtFCYToLCY(rConC."Fecha firma contrato",
                                                                //-$006                                                        "Cod. divisa",
                                                                //-$006                                                        "Precio Ni¤o",
                                                                //-$006                                               rTipoCambioDivisa.ExchangeRate(rConC."Fecha firma contrato", "Cod. divisa"));
                                                                //-$006  END
                                                                //-$006  ELSE
                                                                //-$006    "Precio Ni¤o (DL)" := "Precio Ni¤o";
                                                                //-$006END
                                                                //-$006ELSE
                                                                //-$006  "Precio Ni¤o (DL)" := 0;
                                                              END;

                                                   CaptionML=[ENU=Children price;
                                                              ESP=Precio Ni¤o];
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Cod. divisa" }
    { 39  ;   ;O_Precio Ni¤o (DL)  ;Decimal       ;CaptionML=[ENU=Child Price (LCY);
                                                              ESP=Precio Ni¤o (DL)];
                                                   Description=$006;
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 41  ;   ;Acumula             ;Boolean       ;OnValidate=BEGIN
                                                                IF Acumula <> xRec.Acumula THEN
                                                                  cPar.intro_historico_precios_exc(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                   FIELDCAPTION(Acumula),
                                                                                                   FORMAT(xRec.Acumula), FORMAT(Acumula));
                                                              END;

                                                   CaptionML=[ENU=Accumulate;
                                                              ESP=Acumula] }
    { 43  ;   ;Porcentaje          ;Boolean       ;OnValidate=BEGIN
                                                                IF Porcentaje <> xRec.Porcentaje THEN
                                                                  cPar.intro_historico_precios_exc(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                   FIELDCAPTION(Porcentaje),
                                                                                                   FORMAT(xRec.Porcentaje), FORMAT(Porcentaje));

                                                                //-$006VALIDATE(Precio);
                                                              END;

                                                   CaptionML=[ENU=Percentage;
                                                              ESP=Porcentaje] }
    { 45  ;   ;Codigo Suplemento   ;Code10        ;TableRelation="Suplemento Compra"."Codigo Suplemento";
                                                   OnValidate=VAR
                                                                rSuplemento@1103355000 : Record 7010144;
                                                              BEGIN
                                                                IF NOT rSuplemento.GET("Codigo Suplemento") THEN
                                                                  ERROR(Text007);

                                                                IF "Codigo Suplemento" <> xRec."Codigo Suplemento" THEN BEGIN
                                                                  VALIDATE(Activo, FALSE);

                                                                  cPar.intro_historico_precios_exc(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio","Orden precio",
                                                                                                   FIELDCAPTION("Codigo Suplemento"),
                                                                                                   FORMAT(xRec."Codigo Suplemento"), FORMAT("Codigo Suplemento"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Supplement code;
                                                              ESP=Codigo Suplemento];
                                                   Description=FK Suplemento Compra }
    { 47  ;   ;Codigo Oferta       ;Code10        ;OnValidate=BEGIN
                                                                IF "Codigo Oferta" <> xRec."Codigo Oferta" THEN BEGIN
                                                                  VALIDATE(Activo, FALSE);
                                                                  IF xRec."Codigo Oferta" <> '' THEN
                                                                    cPar.intro_historico_precios_exc(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                     FIELDCAPTION("Codigo Oferta"),
                                                                                                     FORMAT(xRec."Codigo Oferta"), FORMAT("Codigo Oferta"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Offer code;
                                                              ESP=Codigo Oferta] }
    { 50  ;   ;Zona Hotel          ;Code10        ;TableRelation="Zona fisica"."Zona Fisica";
                                                   OnValidate=BEGIN
                                                                // $001
                                                                VALIDATE(Activo, FALSE);
                                                                TESTFIELD("Tipo aplicacion", "Tipo aplicacion"::Pax);

                                                                cPar.intro_historico_precios_exc(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                 FIELDCAPTION("Zona Hotel"),
                                                                                                 FORMAT(xRec."Zona Hotel"), FORMAT("Zona Hotel"));
                                                              END;

                                                   CaptionML=[ENU=Hotel Zone;
                                                              ESP=Zona Hotel];
                                                   Description=$001 }
    { 52  ;   ;Aplica sobre online ;Boolean       ;CaptionML=[ENU=Applies over online;
                                                              ESP=Aplica sobre online] }
    { 54  ;   ;Fecha venta desde   ;Date          ;OnValidate=BEGIN
                                                                IF "Fecha venta desde" <> xRec."Fecha venta desde" THEN BEGIN
                                                                  IF "Fecha venta hasta" <> 0D THEN BEGIN
                                                                    IF "Fecha venta desde" > "Fecha venta hasta" THEN
                                                                      ERROR(Text013);
                                                                  END;

                                                                  VALIDATE(Activo, FALSE);

                                                                  cPar.intro_historico_precios_exc(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                   FIELDCAPTION("Fecha venta desde"),
                                                                                                   FORMAT(xRec."Fecha venta desde"), FORMAT("Fecha venta desde"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Sales Date From;
                                                              ESP=Fecha venta desde] }
    { 56  ;   ;Fecha venta hasta   ;Date          ;OnValidate=BEGIN
                                                                IF "Fecha venta hasta" <> xRec."Fecha venta hasta" THEN BEGIN
                                                                  IF "Fecha venta desde" <> 0D THEN
                                                                    IF "Fecha venta desde" > "Fecha venta hasta" THEN
                                                                      ERROR(Text013);

                                                                  VALIDATE(Activo, FALSE);

                                                                  cPar.intro_historico_precios_exc(Delegacion, "N§ Contrato", rHisP.Tipo::Compra, "Tipo precio", "Orden precio",
                                                                                                   FIELDCAPTION("Fecha venta hasta"),
                                                                                                   FORMAT(xRec."Fecha venta hasta"), FORMAT("Fecha venta hasta"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Sales Date To;
                                                              ESP=Fecha venta hasta] }
  }
  KEYS
  {
    {    ;Delegacion,N§ Contrato,Tipo precio,Orden precio;
                                                   Clustered=Yes }
    {    ;Codigo Suplemento,Codigo Oferta,Activo,Fecha desde,Fecha hasta,Rango desde,Rango hasta;
                                                   KeyGroups=RENUMERING }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1103355007 : TextConst 'ENU=To sign up purchase prices of transfer is only available  in the local offices;ESP=Solo es posible dar de alta precios de compra de transfer en las Delegaciones.';
      Text002@1103355008 : TextConst 'ENU=To modify purchase prices of transfer is only available  in the local offices;ESP=Solo es posible modificar precios de compra de transfer en las Delegaciones.';
      Text003@1103355009 : TextConst 'ENU=Indicate a correct date from;ESP=Indique una fecha desde correcta';
      Text004@1103355010 : TextConst 'ENU=Indicate a correct to;ESP=Indique una fecha hasta correcta';
      Text005@1103355011 : TextConst 'ENU=Indicate a range from higher than 0;ESP=Indique un rango desde mayor que 0';
      Text006@1103355012 : TextConst 'ENU=Indicate a range to higher than 0;ESP=Indique un rango hasta mayor que 0';
      Text007@1103355013 : TextConst 'ENU=Indicate a correct surcharge code;ESP=Indique un codigo suplemento correcto';
      Text008@1103355014 : TextConst 'ENU=Indicate a correct offer code;ESP=Indique un codigo oferta correcto';
      Text009@1103355015 : TextConst 'ENU=To cancel purchase prices of transfer is only available  in the local offices;ESP=Solo es posible dar de baja precios de compra de transfer en las Delegaciones.';
      Text010@1103355016 : TextConst 'ENU=Deleted;ESP=Borrado';
      Text011@1103355017 : TextConst 'ENU=Register deleted;ESP=Registro Borrado';
      Text012@1103355018 : TextConst 'ENU=Only is possible to rename transfer purchase prices in the Local Offices;ESP=Solo es posible renombrar precios de compra de transfer en las Delegaciones.';
      Text013@1103355019 : TextConst 'ENU=Date from cannot be higher than date to;ESP=La fecha desde no puede ser posterior a la fecha hasta.';
      Text014@1103355020 : TextConst 'ENU=Range from cannot be higher than range to;ESP=El rango desde no puede ser mayor que el rango hasta.';
      Text015@1103355021 : TextConst 'ENU=Date from is not within the period of the contract;ESP=La fecha desde no esta entre el periodo del contrato';
      Text016@1103355022 : TextConst 'ENU=Date to is not within the period of the contract;ESP=La fecha hasta no esta entre el periodo del contrato';
      Text017@1103355023 : TextConst 'ENU=This price overlaps with another entered price;ESP=Este precio se solapa con otro precio ya introducido.';
      Text018@1103355024 : TextConst 'ENU=Contract does not exist for this register;ESP=No existe el contrato para este registro';
      rPar@1103355000 : Record 7009700;
      rPre@1103355001 : Record 7010151;
      cPar@1103355002 : Codeunit 7009700;
      rHisP@1103355003 : Record 7010156;
      rConC@1103355004 : Record 7010142;
      rTipoCambioDivisa@1103355005 : Record 330;
      Text023@1100217000 : TextConst 'ENU=You cannot indicate price per service because the excursion have cost per ticket.;ESP=No se puede indicar precio por servicio porque la excursion se costea por ticket.';

    PROCEDURE Activar_Precio@2() : Boolean;
    VAR
      rConC@1103355000 : Record 7010142;
      rPrecio@1103355001 : Record 7010151;
    BEGIN
      IF rConC.GET(Delegacion,"N§ Contrato", rConC."Tipo Contrato"::Excursion) THEN BEGIN
        IF NOT rConC.Confirmado THEN
          EXIT(FALSE);
        IF ("Fecha desde" = 0D) OR ("Fecha hasta" = 0D) THEN
          EXIT(FALSE);
        IF ("Fecha desde" < rConC."Fecha inicio contrato") OR
             ("Fecha desde" > rConC."Fecha final contrato") THEN BEGIN
          MESSAGE(Text015);
          EXIT(FALSE);
        END;

        IF ("Fecha hasta" < rConC."Fecha inicio contrato") OR
             ("Fecha hasta" > rConC."Fecha final contrato") THEN BEGIN
          MESSAGE(Text016);
          EXIT(FALSE);
        END;
      END;

      _CheckCosteTicket; //+$003

      rPrecio.RESET;
      rPrecio.SETCURRENTKEY("Codigo Suplemento", "Codigo Oferta", Activo, "Fecha desde", "Fecha hasta", "Rango desde", "Rango hasta");
      rPrecio.SETRANGE (Delegacion   , Delegacion);
      rPrecio.SETRANGE ("N§ Contrato", "N§ Contrato");
      rPrecio.SETRANGE ("Tipo precio", "Tipo precio");
      rPrecio.SETRANGE ("Zona Hotel" , "Zona Hotel"); // $001
      CASE"Tipo precio" OF
       "Tipo precio"::Suplemento:
          rPrecio.SETRANGE ("Codigo Suplemento","Codigo Suplemento");
       "Tipo precio"::Oferta:
          rPrecio.SETRANGE ("Codigo Oferta"    ,"Codigo Oferta");
       "Tipo precio"::Promocion:
          rPrecio.SETRANGE ("Codigo Oferta"    ,"Codigo Oferta"); // $007
      END;
      rPrecio.SETRANGE (Activo         , TRUE);
      rPrecio.SETFILTER("Orden precio" , '<>%1',"Orden precio");

      rPrecio.SETFILTER("Fecha desde"  , '<=%1',"Fecha desde");
      rPrecio.SETFILTER("Fecha hasta"  , '>=%1',"Fecha desde");
      IF rPrecio.FINDFIRST THEN BEGIN
        rPrecio.SETFILTER("Rango desde", '<=%1',"Rango desde");
        rPrecio.SETFILTER("Rango hasta", '>=%1',"Rango desde");
        IF rPrecio.FINDFIRST THEN BEGIN
          MESSAGE(Text017);
          EXIT(FALSE);
        END;

        rPrecio.SETFILTER("Rango desde", '<=%1',"Rango hasta");
        rPrecio.SETFILTER("Rango hasta", '>=%1',"Rango hasta");
        IF rPrecio.FINDFIRST THEN BEGIN
          MESSAGE(Text017);
          EXIT(FALSE);
        END;

        rPrecio.SETFILTER("Rango desde", '>=%1',"Rango desde");
        rPrecio.SETFILTER("Rango hasta", '<=%1',"Rango hasta");
        IF rPrecio.FINDFIRST THEN BEGIN
          MESSAGE(Text017);
          EXIT(FALSE);
        END;
      END;

      rPrecio.SETFILTER("Fecha desde"  , '<=%1',"Fecha hasta");
      rPrecio.SETFILTER("Fecha hasta"  , '>=%1',"Fecha hasta");
      IF rPrecio.FINDFIRST THEN BEGIN
        rPrecio.SETFILTER("Rango desde", '<=%1',"Rango desde");
        rPrecio.SETFILTER("Rango hasta", '>=%1',"Rango desde");
        IF rPrecio.FINDFIRST THEN BEGIN
          MESSAGE(Text017);
          EXIT(FALSE);
        END;

        rPrecio.SETFILTER("Rango desde", '<=%1',"Rango hasta");
        rPrecio.SETFILTER("Rango hasta", '>=%1',"Rango hasta");
        IF rPrecio.FINDFIRST THEN BEGIN
          MESSAGE(Text017);
          EXIT(FALSE);
        END;

        rPrecio.SETFILTER("Rango desde", '>=%1',"Rango desde");
        rPrecio.SETFILTER("Rango hasta", '<=%1',"Rango hasta");
        IF rPrecio.FINDFIRST THEN BEGIN
          MESSAGE(Text017);
          EXIT(FALSE);
        END;
      END;

      rPrecio.SETFILTER("Fecha desde"  , '>=%1',"Fecha desde");
      rPrecio.SETFILTER("Fecha hasta"  , '<=%1',"Fecha hasta");
      IF rPrecio.FINDFIRST THEN BEGIN
        rPrecio.SETFILTER("Rango desde", '<=%1',"Rango desde");
        rPrecio.SETFILTER("Rango hasta", '>=%1',"Rango desde");
        IF rPrecio.FINDFIRST THEN BEGIN
          MESSAGE(Text017);
          EXIT(FALSE);
        END;

        rPrecio.SETFILTER("Rango desde", '<=%1',"Rango hasta");
        rPrecio.SETFILTER("Rango hasta", '>=%1',"Rango hasta");
        IF rPrecio.FINDFIRST THEN BEGIN
          MESSAGE(Text017);
          EXIT(FALSE);
        END;

        rPrecio.SETFILTER("Rango desde", '>=%1',"Rango desde");
        rPrecio.SETFILTER("Rango hasta", '<=%1',"Rango hasta");
        IF rPrecio.FINDFIRST THEN BEGIN
          MESSAGE(Text017);
          EXIT(FALSE);
        END;
      END;

      CASE "Tipo precio" OF
        "Tipo precio"::"Precio Excursion":
          IF ("Fecha desde" = 0D) OR ("Fecha hasta" = 0D) OR ("Rango desde" = 0) OR ("Rango hasta" = 0) THEN
            EXIT(FALSE);
        "Tipo precio"::Suplemento:
          IF ("Codigo Suplemento" = '') OR ("Fecha desde" = 0D) OR ("Fecha hasta" = 0D) OR
             ("Rango desde" = 0) OR ("Rango hasta" = 0) THEN
            EXIT(FALSE);
        "Tipo precio"::Oferta:
          IF ("Codigo Oferta" = '') OR ("Fecha desde" = 0D) OR ("Fecha hasta" = 0D) OR
             ("Rango desde" = 0) OR ("Rango hasta" = 0) THEN
            EXIT(FALSE);
        "Tipo precio"::Promocion:
          IF ("Codigo Oferta" = '') OR ("Fecha desde" = 0D) OR ("Fecha hasta" = 0D) OR
             ("Rango desde" = 0) OR ("Rango hasta" = 0) OR
             ("Fecha venta desde" = 0D) OR ("Fecha venta hasta" = 0D) THEN
            EXIT(FALSE);
      END;

      EXIT(TRUE);
    END;

    PROCEDURE Comprobar_Contrato@1();
    VAR
      rContrato@1103355000 : Record 7010142;
    BEGIN
      IF NOT rContrato.GET(Delegacion, "N§ Contrato", rContrato."Tipo Contrato"::Excursion) THEN
        ERROR(Text018);

      IF "Fecha desde" <> 0D THEN
        IF ("Fecha desde" < rContrato."Fecha inicio contrato") OR
           ("Fecha desde" > rContrato."Fecha final contrato") THEN
          MESSAGE(Text015);

      IF "Fecha hasta" <> 0D THEN
        IF ("Fecha hasta" < rContrato."Fecha inicio contrato") OR
           ("Fecha hasta" > rContrato."Fecha final contrato") THEN
          MESSAGE(Text016);
    END;

    LOCAL PROCEDURE _CheckCosteTicket@1100217000();
    VAR
      lrExc@1100217000 : Record 7009710;
      lrContrato@1100217001 : Record 7010142;
    BEGIN
      IF ("Tipo aplicacion" = "Tipo aplicacion"::Pax) OR
         ("Tipo aplicacion" = 0) THEN
        EXIT;

      lrContrato.GET(Delegacion, "N§ Contrato", lrContrato."Tipo Contrato"::Excursion);

      lrExc.GET(lrContrato.Excursion);
      IF lrExc."Coste x ticket" THEN
        ERROR(Text023);
    END;

    BEGIN
    {
      $001 JPT 22/05/12 CNT-CAR-12004 / EXC-10 Suppliers supplement per hotel zone

      $002 AJS 31052012 Iniciamos las fechas con el periodo del contrato

      $003 AJS 11062013 Nueva funcion _CheckCosteTicket para evitar que se introduzcan precios por servicio en
                        excursiones que se calculan por ticket

      $004 AJS 16062013 Cambios en nombre de campo Tipo precio y Codigo precio

      $005 AJS 18062013 Nuevas funciones NomExcursion, PrecioAdultoExcursion y PrecioNi¤oExcursion para incluir en la consulta de
                        precios base a efectos de revision

      $006 AJS 18062013 Los campos de precio en divisa local son obsoletos

      $007 ARM 15092014 Nuevo tipo precio Promociones CNT-OC-14069

      $008 AJS 29092014 Ajuste traduccion

      $009 AJS 30092014 Traducciones
    }
    END.
  }
}
