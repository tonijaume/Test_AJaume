OBJECT Table 5913 Loaner
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    DataCaptionFields=No.,Description;
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 ServMgtSetup.GET;
                 ServMgtSetup.TESTFIELD("Loaner Nos.");
                 NoSeriesMgt.InitSeries(ServMgtSetup."Loaner Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;
             END;

    OnDelete=VAR
               ServCommentLine@1000 : Record 5906;
             BEGIN
               CALCFIELDS(Lent,"Document No.");

               IF Lent THEN
                 ERROR(Text000,TABLECAPTION,FIELDCAPTION(Lent));

               IF (NOT Blocked) THEN
                 ERROR(Text001,TABLECAPTION,FIELDCAPTION(Blocked));

               CLEAR(LoanerEntry);
               LoanerEntry.SETCURRENTKEY("Loaner No.");
               LoanerEntry.SETRANGE("Loaner No.","No.");
               LoanerEntry.DELETEALL;

               ServCommentLine.RESET;
               ServCommentLine.SETRANGE("Table Name",ServCommentLine."Table Name"::Loaner);
               ServCommentLine.SETRANGE("Table Subtype",0);
               ServCommentLine.SETRANGE("No.","No.");
               ServCommentLine.DELETEALL;
             END;

    CaptionML=[ENU=Loaner;
               ESP=Prod. prestado];
    LookupFormID=Form5923;
    DrillDownFormID=Form5923;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  ServMgtSetup.GET;
                                                                  NoSeriesMgt.TestManual(ServMgtSetup."Loaner Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              ESP=N§] }
    { 2   ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n] }
    { 4   ;   ;Description 2       ;Text30        ;CaptionML=[ENU=Description 2;
                                                              ESP=Descripci¢n 2] }
    { 5   ;   ;Unit of Measure Code;Code10        ;TableRelation=IF (Item No.=CONST(<>'')) "Item Unit of Measure".Code WHERE (Item No.=FIELD(Item No.))
                                                                 ELSE "Unit of Measure";
                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              ESP=C¢d. unidad medida] }
    { 6   ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   OnValidate=BEGIN
                                                                IF "Item No." <> '' THEN BEGIN
                                                                  Item.GET("Item No.");
                                                                  Description := Item.Description;
                                                                  "Description 2" := Item."Description 2";
                                                                END ELSE BEGIN
                                                                  Description := '';
                                                                  "Description 2" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Item No.;
                                                              ESP=N§ producto] }
    { 7   ;   ;Last Date Modified  ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              ESP=Fecha £lt. modificaci¢n];
                                                   Editable=No }
    { 8   ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Service Comment Line" WHERE (Table Name=CONST(Loaner),
                                                                                                   Table Subtype=CONST(0),
                                                                                                   No.=FIELD(No.)));
                                                   CaptionML=[ENU=Comment;
                                                              ESP=Comentario];
                                                   Editable=No }
    { 9   ;   ;Blocked             ;Boolean       ;CaptionML=[ENU=Blocked;
                                                              ESP=Bloqueado] }
    { 11  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              ESP=Nos. serie];
                                                   Editable=No }
    { 12  ;   ;Document No.        ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Loaner Entry"."Document No." WHERE (Loaner No.=FIELD(No.),
                                                                                                           Lent=CONST(Yes)));
                                                   CaptionML=[ENU=Document No.;
                                                              ESP=N§ documento];
                                                   Editable=No }
    { 13  ;   ;Lent                ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Loaner Entry" WHERE (Loaner No.=FIELD(No.),
                                                                                           Lent=CONST(Yes)));
                                                   CaptionML=[ENU=Lent;
                                                              ESP=Prestado];
                                                   Editable=No }
    { 14  ;   ;Serial No.          ;Code20        ;CaptionML=[ENU=Serial No.;
                                                              ESP=N§ serie] }
    { 15  ;   ;Document Type       ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Loaner Entry"."Document Type" WHERE (Loaner No.=FIELD(No.),
                                                                                                            Lent=CONST(Yes)));
                                                   CaptionML=[ENU=Document Type;
                                                              ESP=Tipo documento];
                                                   OptionCaptionML=[ENU=" ,Quote,Order";
                                                                    ESP=" ,Oferta,Pedido"];
                                                   OptionString=[ ,Quote,Order];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Description                             ;KeyGroups=SearchCol }
    {    ;Item No.                                ;KeyGroups=SearchCol }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;No.,Description,Item No.                 }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You cannot delete %1s that are lent.;ESP=No puede eliminar %1s que han sido prestados.';
      Text001@1001 : TextConst 'ENU=You can only delete %1s that is %2.;ESP=S¢lo puede eliminar %1s que sean %2.';
      ServMgtSetup@1002 : Record 5911;
      Item@1005 : Record 27;
      Loaner@1003 : Record 5913;
      LoanerEntry@1004 : Record 5914;
      NoSeriesMgt@1006 : Codeunit 396;

    PROCEDURE AssistEdit@2(OldLoaner@1000 : Record 5913) : Boolean;
    BEGIN
      WITH Loaner DO BEGIN
        Loaner := Rec;
        ServMgtSetup.GET;
        ServMgtSetup.TESTFIELD("Loaner Nos.");
        IF NoSeriesMgt.SelectSeries(ServMgtSetup."Loaner Nos.",OldLoaner."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("No.");
          Rec := Loaner;
          EXIT(TRUE);
        END;
      END;
    END;

    BEGIN
    END.
  }
}
