OBJECT Table 99000786 Routing Version
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "Version Code" = '' THEN BEGIN
                 RoutingHeader.GET("Routing No.");
                 RoutingHeader.TESTFIELD("Version Nos.");
                 NoSeriesMgt.InitSeries(RoutingHeader."Version Nos.",xRec."No. Series",0D,VersionCode,"No. Series");
                 IF STRLEN(VersionCode) > MAXSTRLEN("Version Code") THEN
                   ERROR(Text000,
                     FIELDCAPTION("Version Code"),
                     NoSeriesLine.FIELDCAPTION("Starting No."),
                     RoutingHeader."Version Nos.",
                     NoSeries.TABLECAPTION,
                     MAXSTRLEN("Version Code"));

                 "Version Code" := VersionCode;
               END;
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;
             END;

    OnDelete=VAR
               RtngLine@1000 : Record 99000764;
             BEGIN
               RtngLine.LOCKTABLE;
               RtngLine.SETRANGE("Routing No.","Routing No.");
               RtngLine.SETRANGE("Version Code","Version Code");
               RtngLine.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=Routing Version;
               ESP=Versi¢n ruta];
    LookupFormID=Form99000808;
    DrillDownFormID=Form99000808;
  }
  FIELDS
  {
    { 1   ;   ;Routing No.         ;Code20        ;TableRelation="Routing Header";
                                                   CaptionML=[ENU=Routing No.;
                                                              ESP=N§ ruta];
                                                   NotBlank=Yes }
    { 2   ;   ;Version Code        ;Code10        ;CaptionML=[ENU=Version Code;
                                                              ESP=C¢d. versi¢n] }
    { 3   ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n] }
    { 10  ;   ;Starting Date       ;Date          ;CaptionML=[ENU=Starting Date;
                                                              ESP=Fecha inicial] }
    { 20  ;   ;Status              ;Option        ;OnValidate=VAR
                                                                RtngHeader@1000 : Record 99000763;
                                                              BEGIN
                                                                IF (Status <> xRec.Status) AND (Status = Status::Certified) THEN BEGIN
                                                                  RtngHeader.GET("Routing No.");
                                                                  CheckRouting.Calculate(RtngHeader,"Version Code");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Status;
                                                              ESP=Estado];
                                                   OptionCaptionML=[ENU=New,Certified,Under Development,Closed;
                                                                    ESP=Nueva,Certificada,En desarrollo,Cerrada];
                                                   OptionString=New,Certified,Under Development,Closed }
    { 21  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF Status = Status::Certified THEN
                                                                  FIELDERROR(Status);
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              ESP=Tipo];
                                                   OptionCaptionML=[ENU=Serial,Parallel;
                                                                    ESP=En serie,En paralelo];
                                                   OptionString=Serial,Parallel }
    { 22  ;   ;Last Date Modified  ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              ESP=Fecha £lt. modificaci¢n];
                                                   Editable=No }
    { 50  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              ESP=Nos. serie];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Routing No.,Version Code                ;Clustered=Yes }
    {    ;Routing No.,Starting Date                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      NoSeries@1007 : Record 308;
      NoSeriesLine@1006 : Record 309;
      RoutingHeader@1000 : Record 99000763;
      RtngVersion@1001 : Record 99000786;
      CheckRouting@1002 : Codeunit 99000752;
      NoSeriesMgt@1003 : Codeunit 396;
      Text000@1008 : TextConst 'ENU=The new %1 cannot be generated by default\because the %2 for %3 %4 contains more than %5 characters.;ESP=El %1 nuevo no puede generarse de forma gen‚rica\porque el %2 para %3 %4 contiene m s de %5 caracteres.';
      VersionCode@1005 : Code[10];

    PROCEDURE AssistEdit@2(OldRoutVersion@1000 : Record 99000786) : Boolean;
    BEGIN
      WITH RtngVersion DO BEGIN
        RtngVersion := Rec;
        RoutingHeader.GET("Routing No.");
        RoutingHeader.TESTFIELD("Version Nos.");
        IF NoSeriesMgt.SelectSeries(RoutingHeader."Version Nos.",OldRoutVersion."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries(VersionCode);
          IF STRLEN(VersionCode) > MAXSTRLEN("Version Code") THEN
            ERROR(Text000,
              FIELDCAPTION("Version Code"),
              NoSeriesLine.FIELDCAPTION("Starting No."),
              RoutingHeader."Version Nos.",
              NoSeries.TABLECAPTION,
              MAXSTRLEN("Version Code"));

          "Version Code" := VersionCode;
          Rec := RtngVersion;
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE Caption@1() : Text[100];
    VAR
      RtngHeader@1000 : Record 99000763;
    BEGIN
      IF GETFILTERS = '' THEN
        EXIT('');

      IF "Routing No." = '' THEN
        EXIT('');

      RtngHeader.GET("Routing No.");
      EXIT(
        STRSUBSTNO(
          '%1 %2 %3',RtngHeader."No.",RtngHeader.Description,"Version Code"));
    END;

    BEGIN
    END.
  }
}
