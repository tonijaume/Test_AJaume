OBJECT Table 95 G/L Budget Name
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               GLBudgetEntry@1000 : Record 96;
               AnalysisViewBudgetEntry@1001 : Record 366;
             BEGIN
               GLBudgetEntry.SETCURRENTKEY("Budget Name");
               GLBudgetEntry.SETRANGE("Budget Name",Name);
               GLBudgetEntry.DELETEALL(TRUE);

               AnalysisViewBudgetEntry.SETRANGE("Budget Name",Name);
               AnalysisViewBudgetEntry.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=G/L Budget Name;
               ESP=Nombre ppto. contable];
    LookupFormID=Form121;
  }
  FIELDS
  {
    { 1   ;   ;Name                ;Code10        ;CaptionML=[ENU=Name;
                                                              ESP=Nombre];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text80        ;CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n] }
    { 3   ;   ;Blocked             ;Boolean       ;CaptionML=[ENU=Blocked;
                                                              ESP=Bloqueado] }
    { 4   ;   ;Budget Dimension 1 Code;Code20     ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                IF "Budget Dimension 1 Code" <> xRec."Budget Dimension 1 Code" THEN BEGIN
                                                                  IF Dim.CheckIfDimUsed("Budget Dimension 1 Code",9,Name,'',0) THEN
                                                                    ERROR(Text000,Dim.GetCheckDimErr);
                                                                  MODIFY;
                                                                  UpdateBudgetDim("Budget Dimension 1 Code",0);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Budget Dimension 1 Code;
                                                              ESP=C¢d. dim. presupuesto 1] }
    { 5   ;   ;Budget Dimension 2 Code;Code20     ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                IF "Budget Dimension 2 Code" <> xRec."Budget Dimension 2 Code" THEN BEGIN
                                                                  IF Dim.CheckIfDimUsed("Budget Dimension 2 Code",10,Name,'',0) THEN
                                                                    ERROR(Text000,Dim.GetCheckDimErr);
                                                                  MODIFY;
                                                                  UpdateBudgetDim("Budget Dimension 2 Code",1);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Budget Dimension 2 Code;
                                                              ESP=C¢d. dim. presupuesto 2] }
    { 6   ;   ;Budget Dimension 3 Code;Code20     ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                IF "Budget Dimension 3 Code" <> xRec."Budget Dimension 3 Code" THEN BEGIN
                                                                  IF Dim.CheckIfDimUsed("Budget Dimension 3 Code",11,Name,'',0) THEN
                                                                    ERROR(Text000,Dim.GetCheckDimErr);
                                                                  MODIFY;
                                                                  UpdateBudgetDim("Budget Dimension 3 Code",2);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Budget Dimension 3 Code;
                                                              ESP=C¢d. dim. presupuesto 3] }
    { 7   ;   ;Budget Dimension 4 Code;Code20     ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                IF "Budget Dimension 4 Code" <> xRec."Budget Dimension 4 Code" THEN BEGIN
                                                                  IF Dim.CheckIfDimUsed("Budget Dimension 4 Code",12,Name,'',0) THEN
                                                                    ERROR(Text000,Dim.GetCheckDimErr);
                                                                  MODIFY;
                                                                  UpdateBudgetDim("Budget Dimension 4 Code",3);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Budget Dimension 4 Code;
                                                              ESP=C¢d. dim. presupuesto 4] }
  }
  KEYS
  {
    {    ;Name                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1\You cannot use the same dimension twice in the same budget.;ESP=%1\No puede usar la misma dim. dos veces en el mismo ppto.';
      Text001@1001 : TextConst 'ENU=Updating budget entries @1@@@@@@@@@@@@@@@@@@;ESP=Actualizando movs. ppto. @1@@@@@@@@@@@@@@@@@@';
      Dim@1002 : Record 348;

    LOCAL PROCEDURE UpdateBudgetDim@3(NewDim@1000 : Code[20];Which@1001 : 'BudgetDim1,BudgetDim2,BudgetDim3,BudgetDim4');
    VAR
      GLBudgetEntry@1002 : Record 96;
      GLBudgetDim@1003 : Record 361;
      Window@1004 : Dialog;
      TotalCount@1005 : Integer;
      i@1006 : Integer;
    BEGIN
      WITH GLBudgetEntry DO BEGIN
        SETCURRENTKEY("Budget Name");
        SETRANGE("Budget Name",Rec.Name);
        IF NewDim = '' THEN
          GLBudgetDim.INIT;
        TotalCount := COUNTAPPROX;
        Window.OPEN(Text001);
        IF FIND('-') THEN
          REPEAT
            i := i + 1;
            IF (i MOD 1000 = 0) THEN
              Window.UPDATE(1,10000 * i DIV TotalCount);
            IF NewDim <> '' THEN
              IF NOT GLBudgetDim.GET("Entry No.",NewDim) THEN
                GLBudgetDim.INIT;
            CASE Which OF
              Which::BudgetDim1:
                IF GLBudgetDim."Dimension Value Code" <> "Budget Dimension 1 Code" THEN BEGIN
                  "Budget Dimension 1 Code" := GLBudgetDim."Dimension Value Code";
                  MODIFY;
                END;
              Which::BudgetDim2:
                IF GLBudgetDim."Dimension Value Code" <> "Budget Dimension 2 Code" THEN BEGIN
                  "Budget Dimension 2 Code" := GLBudgetDim."Dimension Value Code";
                  MODIFY;
                END;
              Which::BudgetDim3:
                IF GLBudgetDim."Dimension Value Code" <> "Budget Dimension 3 Code" THEN BEGIN
                  "Budget Dimension 3 Code" := GLBudgetDim."Dimension Value Code";
                  MODIFY;
                END;
              Which::BudgetDim4:
                IF GLBudgetDim."Dimension Value Code" <> "Budget Dimension 4 Code" THEN BEGIN
                  "Budget Dimension 4 Code" := GLBudgetDim."Dimension Value Code";
                  MODIFY;
                END;
            END;
          UNTIL NEXT = 0;
      END;
      Window.CLOSE;
    END;

    BEGIN
    END.
  }
}
