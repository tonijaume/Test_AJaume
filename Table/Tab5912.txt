OBJECT Table 5912 Service Document Log
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ServOrderLog.RESET;
               ServOrderLog.SETRANGE("Document Type","Document Type");
               ServOrderLog.SETRANGE("Document No.","Document No.");
               IF ServOrderLog.FIND('+') THEN
                 "Entry No." := ServOrderLog."Entry No." + 1
               ELSE
                 "Entry No." := 1;

               "Change Date" := TODAY;
               "Change Time" := TIME;
               "User ID" := USERID;
             END;

    CaptionML=[ENU=Service Document Log;
               ESP=Registro pedido servicio];
    LookupFormID=Form5920;
    DrillDownFormID=Form5920;
  }
  FIELDS
  {
    { 1   ;   ;Document No.        ;Code20        ;TableRelation=IF (Document Type=CONST(Quote)) "Service Header".No. WHERE (Document Type=CONST(Quote))
                                                                 ELSE IF (Document Type=CONST(Order)) "Service Header".No. WHERE (Document Type=CONST(Order))
                                                                 ELSE IF (Document Type=CONST(Invoice)) "Service Header".No. WHERE (Document Type=CONST(Invoice))
                                                                 ELSE IF (Document Type=CONST(Credit Memo)) "Service Header".No. WHERE (Document Type=CONST(Credit Memo))
                                                                 ELSE IF (Document Type=CONST(Shipment)) "Service Shipment Header"
                                                                 ELSE IF (Document Type=CONST(Posted Invoice)) "Service Invoice Header"
                                                                 ELSE IF (Document Type=CONST(Posted Credit Memo)) "Service Cr.Memo Header";
                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Document No.;
                                                              ESP=N§ documento] }
    { 2   ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              ESP=N§ mov.] }
    { 3   ;   ;Event No.           ;Integer       ;CaptionML=[ENU=Event No.;
                                                              ESP=N§ evento] }
    { 4   ;   ;Service Item Line No.;Integer      ;CaptionML=[ENU=Service Item Line No.;
                                                              ESP=N§ l¡n. prod. servicio] }
    { 5   ;   ;After               ;Text50        ;CaptionML=[ENU=After;
                                                              ESP=Despu‚s] }
    { 6   ;   ;Before              ;Text50        ;CaptionML=[ENU=Before;
                                                              ESP=Antes] }
    { 7   ;   ;Change Date         ;Date          ;CaptionML=[ENU=Change Date;
                                                              ESP=Fecha cambio] }
    { 8   ;   ;Change Time         ;Time          ;CaptionML=[ENU=Change Time;
                                                              ESP=Hora cambio] }
    { 9   ;   ;User ID             ;Code20        ;TableRelation=User;
                                                   OnLookup=VAR
                                                              LoginMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              LoginMgt.LookupUserID("User ID");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=User ID;
                                                              ESP=Id. usuario] }
    { 10  ;   ;Document Type       ;Option        ;CaptionML=[ENU=Document Type;
                                                              ESP=Tipo documento];
                                                   OptionCaptionML=[ENU=Quote,Order,Invoice,Credit Memo,Shipment,Posted Invoice,Posted Credit Memo;
                                                                    ESP=Oferta,Pedido,Factura,Abono,Env¡o,Factura registrada,Abono registrado];
                                                   OptionString=Quote,Order,Invoice,Credit Memo,Shipment,Posted Invoice,Posted Credit Memo;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Document Type,Document No.,Entry No.    ;Clustered=Yes }
    {    ;Change Date,Change Time                  }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ServOrderLog@1000 : Record 5912;

    PROCEDURE FillTempServDocLog@1(VAR ServHeader@1000 : Record 5900;VAR TempServDocLog@1001 : TEMPORARY Record 5912);
    VAR
      ServDocLog@1002 : Record 5912;
    BEGIN
      WITH ServHeader DO BEGIN
        TempServDocLog.RESET;
        TempServDocLog.DELETEALL;

        IF "No." <> '' THEN BEGIN
          ServDocLog.RESET;
          ServDocLog.SETRANGE("Document Type","Document Type");
          ServDocLog.SETRANGE("Document No.","No.");
          IF ServDocLog.FINDSET THEN
            REPEAT
              TempServDocLog := ServDocLog;
              TempServDocLog.INSERT;
            UNTIL ServDocLog.NEXT = 0;

          ServDocLog.RESET;
          ServDocLog.SETRANGE(Before,"No.");
          ServDocLog.SETFILTER("Document Type",'%1|%2|%3',
            ServDocLog."Document Type"::Shipment,ServDocLog."Document Type"::"Posted Invoice",
            ServDocLog."Document Type"::"Posted Credit Memo");

          IF ServDocLog.FINDSET THEN
            REPEAT
              TempServDocLog := ServDocLog;
              TempServDocLog.INSERT;
            UNTIL ServDocLog.NEXT = 0;
        END;

        TempServDocLog.RESET;
        TempServDocLog.SETCURRENTKEY("Change Date","Change Time");
        TempServDocLog.ASCENDING(FALSE);
      END;
    END;

    PROCEDURE ShowServDocLog@2(VAR ServHeader@1000 : Record 5900);
    VAR
      TempServDocLog@1001 : TEMPORARY Record 5912;
    BEGIN
      FillTempServDocLog(ServHeader,TempServDocLog);
      FORM.RUN(0,TempServDocLog);
    END;

    BEGIN
    END.
  }
}
