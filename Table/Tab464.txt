OBJECT Table 464 Approval Templates
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TestValidation;
             END;

    OnDelete=BEGIN
               AdditionalApprovers.SETRANGE("Approval Code","Approval Code");
               AdditionalApprovers.SETRANGE("Approval Type","Approval Type");
               AdditionalApprovers.SETRANGE("Document Type","Document Type");
               AdditionalApprovers.SETRANGE("Limit Type","Limit Type");
               AdditionalApprovers.DELETEALL;
             END;

    OnRename=BEGIN
               TestValidation;
               RenameAddApprovers(Rec,xRec);
             END;

    CaptionML=[ENU=Approval Templates;
               ESP=Plantillas aprobaci¢n];
  }
  FIELDS
  {
    { 1   ;   ;Table ID            ;Integer       ;CaptionML=[ENU=Table ID;
                                                              ESP=Id. tabla];
                                                   Editable=No }
    { 2   ;   ;Approval Code       ;Code20        ;TableRelation="Approval Code".Code;
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Enabled,FALSE);
                                                                ApprCode.GET("Approval Code");
                                                                ApprCode.TESTFIELD("Linked To Table No.");
                                                                "Table ID" := ApprCode."Linked To Table No.";
                                                              END;

                                                   CaptionML=[ENU=Approval Code;
                                                              ESP=C¢digo aprobaci¢n] }
    { 3   ;   ;Approval Type       ;Option        ;OnValidate=BEGIN
                                                                TESTFIELD(Enabled,FALSE);
                                                              END;

                                                   CaptionML=[ENU=Approval Type;
                                                              ESP=Tipo aprobaci¢n];
                                                   OptionCaptionML=[ENU=" ,Sales Pers./Purchaser,Approver";
                                                                    ESP=" ,Vendedor/Comprador,Aprobador"];
                                                   OptionString=[ ,Sales Pers./Purchaser,Approver] }
    { 4   ;   ;Document Type       ;Option        ;OnValidate=BEGIN
                                                                TESTFIELD(Enabled,FALSE);
                                                              END;

                                                   CaptionML=[ENU=Document Type;
                                                              ESP=Tipo documento];
                                                   OptionCaptionML=[ENU=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order,None;
                                                                    ESP=Oferta,Pedido,Factura,Abono,Pedido abierto,Devoluci¢n,Ninguno];
                                                   OptionString=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order,None }
    { 5   ;   ;Limit Type          ;Option        ;OnValidate=BEGIN
                                                                TESTFIELD(Enabled,FALSE);
                                                              END;

                                                   CaptionML=[ENU=Limit Type;
                                                              ESP=Tipo l¡mite];
                                                   OptionCaptionML=[ENU=Approval Limits,Credit Limits,Request Limits,No Limits;
                                                                    ESP=L¡mites aprobaci¢n,L¡mites cr‚dito,L¡mites solicitud,Sin l¡mites];
                                                   OptionString=Approval Limits,Credit Limits,Request Limits,No Limits }
    { 6   ;   ;Additional Approvers;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Additional Approvers" WHERE (Approval Code=FIELD(Approval Code),
                                                                                                   Approval Type=FIELD(Approval Type),
                                                                                                   Document Type=FIELD(Document Type),
                                                                                                   Limit Type=FIELD(Limit Type),
                                                                                                   Approver ID=FILTER(<>'')));
                                                   CaptionML=[ENU=Additional Approvers;
                                                              ESP=Aprobadores adicionales];
                                                   Editable=No }
    { 7   ;   ;Enabled             ;Boolean       ;OnValidate=VAR
                                                                Salesheader@1000 : Record 36;
                                                                PurchaseHeader@1001 : Record 38;
                                                                ApprovalEntry@1002 : Record 454;
                                                                TempApprovalTemplate@1003 : Record 464;
                                                              BEGIN
                                                                IF (Enabled = FALSE) AND (xRec.Enabled = TRUE) THEN BEGIN
                                                                  TempApprovalTemplate.SETRANGE("Approval Code","Approval Code");
                                                                  TempApprovalTemplate.SETRANGE("Document Type","Document Type");
                                                                  IF NOT TempApprovalTemplate.FINDFIRST THEN BEGIN
                                                                    CASE "Table ID" OF
                                                                      DATABASE::"Sales Header":
                                                                        BEGIN
                                                                          Salesheader.SETCURRENTKEY("Document Type",Status);
                                                                          Salesheader.SETRANGE("Document Type","Document Type");
                                                                          Salesheader.SETRANGE(Status,Salesheader.Status::"Pending Approval");
                                                                          IF Salesheader.FINDFIRST THEN BEGIN
                                                                            IF CONFIRM(Text006) THEN BEGIN
                                                                              ApprovalEntry.SETRANGE("Table ID",DATABASE::"Sales Header");
                                                                              ApprovalEntry.SETRANGE("Document Type",Rec."Document Type");
                                                                              ApprovalEntry.SETFILTER(
                                                                                Status,'%1|%2|%3',ApprovalEntry.Status::Created,ApprovalEntry.Status::Open,ApprovalEntry.Status::Approved);
                                                                              IF ApprovalEntry.FINDFIRST THEN
                                                                                ApprovalEntry.MODIFYALL(Status,ApprovalEntry.Status::Canceled);
                                                                            END;
                                                                            Salesheader.MODIFYALL(Status,Salesheader.Status::Open);
                                                                          END;
                                                                        END;
                                                                      DATABASE::"Purchase Header":
                                                                        BEGIN
                                                                          PurchaseHeader.SETCURRENTKEY("Document Type",Status);
                                                                          PurchaseHeader.SETRANGE("Document Type",Rec."Document Type");
                                                                          PurchaseHeader.SETRANGE(Status,PurchaseHeader.Status::"Pending Approval");
                                                                          IF PurchaseHeader.FINDFIRST THEN BEGIN
                                                                            IF CONFIRM(Text006) THEN BEGIN
                                                                              ApprovalEntry.SETRANGE("Table ID",DATABASE::"Purchase Header");
                                                                              ApprovalEntry.SETRANGE("Document Type",Rec."Document Type");
                                                                              ApprovalEntry.SETFILTER(
                                                                                Status,'%1|%2|%3',ApprovalEntry.Status::Created,ApprovalEntry.Status::Open,ApprovalEntry.Status::Approved);
                                                                              IF ApprovalEntry.FINDFIRST THEN
                                                                                ApprovalEntry.MODIFYALL(Status,ApprovalEntry.Status::Canceled);
                                                                            END;
                                                                            PurchaseHeader.MODIFYALL(Status,Salesheader.Status::Open);
                                                                          END;
                                                                        END;
                                                                    END;
                                                                  END;
                                                                END;

                                                                IF "Approval Type" = "Approval Type"::" " THEN BEGIN
                                                                  CALCFIELDS("Additional Approvers");
                                                                  IF NOT "Additional Approvers" AND Enabled THEN
                                                                    ERROR(STRSUBSTNO(Text005,FIELDCAPTION("Approval Type")));
                                                                END;
                                                                IF ("Approval Type" <> "Approval Type"::" ") AND ("Limit Type" = "Limit Type"::"Credit Limits") THEN BEGIN
                                                                  CALCFIELDS("Additional Approvers");
                                                                  IF NOT "Additional Approvers" AND Enabled THEN
                                                                    ERROR(STRSUBSTNO(Text007,FIELDCAPTION("Approval Type"),FORMAT("Approval Type"),
                                                                        FIELDCAPTION("Limit Type")));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Enabled;
                                                              ESP=Activado] }
  }
  KEYS
  {
    {    ;Approval Code,Approval Type,Document Type,Limit Type;
                                                   Clustered=Yes }
    {    ;Table ID,Approval Type,Enabled           }
    {    ;Approval Code,Approval Type,Enabled      }
    {    ;Enabled                                  }
    {    ;Limit Type,Document Type,Approval Type,Enabled }
    {    ;Table ID,Document Type,Enabled           }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ApprCode@1000 : Record 453;
      AdditionalApprovers@1001 : Record 465;
      Text001@1002 : TextConst 'ENU=%1 is not a valid limit type for table %2.;ESP=%1 no es un tipo l¡mite v lido para la tabla %2.';
      Text002@1003 : TextConst 'ENU=%1 is only valid for table %2.;ESP=%1 s¢lo es v lido para la tabla %2.';
      Text004@1005 : TextConst 'ENU=%1 is only valid when document type is Quote and Table ID is %2.;ESP=%1 s¢lo es v lido cuando tipo documento es Oferta y el Id. tabla es %2.';
      Text005@1006 : TextConst 'ENU=Additional Approvers must be inserted if %1 is blank.;ESP=Se deben insertar aprobadores adicionales si %1 est  vac¡o.';
      Text006@1007 : TextConst 'ENU="Do you want to cancel all outstanding approvals? ";ESP="¨Desea cancelar todas las aprobaciones pendientes? "';
      Text007@1008 : TextConst 'ENU=Additional Approvers must be inserted if %1 is %2 and %3 is Credit Limit.;ESP=Se deben insertar aprobadores adicionales si %1 es %2 y %3 es L¡mite cr‚dito.';

    PROCEDURE TestValidation@1();
    VAR
      AppSetup@1000 : Record 452;
    BEGIN
      AppSetup.GET;
      IF ("Table ID" = DATABASE::"Purchase Header") AND
         ("Limit Type" = "Limit Type"::"Credit Limits") THEN
        ERROR(STRSUBSTNO(Text001,FORMAT("Limit Type"),DATABASE::"Purchase Header"));

      IF ("Table ID" <> DATABASE::"Purchase Header") AND
         ("Limit Type" = "Limit Type"::"Request Limits") THEN
        ERROR(STRSUBSTNO(Text002,FORMAT("Limit Type"),DATABASE::"Purchase Header"))
      ELSE BEGIN
        IF ("Table ID" = DATABASE::"Purchase Header") AND
           ("Limit Type" = "Limit Type"::"Request Limits") AND
           ("Document Type" <> "Document Type"::Quote) THEN
          ERROR(STRSUBSTNO(Text004,FORMAT("Limit Type"),"Table ID"));
      END;
    END;

    PROCEDURE RenameAddApprovers@2(Template@1000 : Record 464;xTemplate@1001 : Record 464);
    VAR
      AddApprovers@1002 : Record 465;
      RenamedAddApprovers@1003 : Record 465;
    BEGIN
      AddApprovers.SETRANGE("Approval Code",xTemplate."Approval Code");
      AddApprovers.SETRANGE("Approval Type",xTemplate."Approval Type");
      AddApprovers.SETRANGE("Document Type",xTemplate."Document Type");
      AddApprovers.SETRANGE("Limit Type",xTemplate."Limit Type");
      IF AddApprovers.FIND('-') THEN BEGIN
        REPEAT
          RenamedAddApprovers := AddApprovers;
          RenamedAddApprovers."Approval Code" := Template."Approval Code";
          RenamedAddApprovers."Approval Type" := Template."Approval Type";
          RenamedAddApprovers."Document Type" := Template."Document Type";
          RenamedAddApprovers."Limit Type" := Template."Limit Type";
          AddApprovers.DELETE;
          RenamedAddApprovers.INSERT;
        UNTIL AddApprovers.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}
