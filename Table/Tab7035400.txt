OBJECT Table 7035400 Tarifa horaria x contrato
{
  OBJECT-PROPERTIES
  {
    Date=16/10/09;
    Time=12:30:50;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               Activo := ComprobarDuplicado;
             END;

    OnModify=BEGIN
               Activo := ComprobarDuplicado;
             END;

    CaptionML=ESP=Tarifa horaria x contrato;
    LookupFormID=Form7010502;
  }
  FIELDS
  {
    { 3   ;   ;Num. Contrato       ;Code20        ;TableRelation="Contrato compra"."N§ Contrato";
                                                   CaptionML=ESP=Num. Contrato;
                                                   Description="Contrato compra"."N§ Contrato" }
    { 5   ;   ;Codigo              ;Code10        ;CaptionML=ESP=Codigo }
    { 7   ;   ;Hora desde          ;Time          ;CaptionML=ESP=Hora desde }
    { 9   ;   ;Hora hasta          ;Time          ;CaptionML=ESP=Hora hasta }
    { 11  ;   ;Activo              ;Boolean       ;CaptionML=ESP=Activo;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Num. Contrato,Codigo                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1103355000 : TextConst 'ESP=La hora de inicio se solapa con una tarifa horaria ya introducida.';
      Text002@1103355001 : TextConst 'ESP=La hora de final se solapa con una tarifa horaria ya introducida.';
      Text003@1103355002 : TextConst 'ESP=Esta tarifa solapa completamente con una tarifa ya introducida.';

    PROCEDURE ComprobarDuplicado@1103355000() : Boolean;
    VAR
      lrTarHor@1103355000 : Record 7035400;
    BEGIN
      // ComprobarDuplicado;

      IF ("Hora desde" = 0T) OR
         ("Hora hasta" = 0T) THEN
        EXIT(FALSE);

      // Comprobamos que no se solape con otra tarifa horaria dentro del mismo contrato

      lrTarHor.RESET;
      lrTarHor.SETRANGE ("Num. Contrato", "Num. Contrato");
      lrTarHor.SETFILTER(Codigo         , '<>%1', Codigo);
      lrTarHor.SETRANGE (Activo         , TRUE);
      lrTarHor.SETFILTER("Hora desde"   , '<=%1', "Hora desde");
      lrTarHor.SETFILTER("Hora hasta"   , '>=%1', "Hora desde");
      IF NOT lrTarHor.ISEMPTY THEN BEGIN
        MESSAGE(Text001);
        EXIT(FALSE);
      END;

      lrTarHor.SETFILTER("Hora desde"   , '<=%1', "Hora hasta");
      lrTarHor.SETFILTER("Hora hasta"   , '>=%1', "Hora hasta");
      IF NOT lrTarHor.ISEMPTY THEN BEGIN
        MESSAGE(Text002);
        EXIT(FALSE);
      END;

      lrTarHor.SETFILTER("Hora desde"   , '>=%1', "Hora desde");
      lrTarHor.SETFILTER("Hora hasta"   , '<=%1', "Hora hasta");
      IF NOT lrTarHor.ISEMPTY THEN BEGIN
        MESSAGE(Text003);
        EXIT(FALSE);
      END;


      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}
