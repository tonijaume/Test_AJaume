OBJECT Table 7009866 Cabecera Proforma
{
  OBJECT-PROPERTIES
  {
    Date=22/07/14;
    Time=13:54:55;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               SerieProformas;

               "Num. Proforma"    := GestNoSeries.GetNextNo(Text001, TODAY, TRUE);
               "Fecha creacion"   := CURRENTDATETIME;
               "Usuario creacion" := USERID;
             END;

    OnDelete=BEGIN
               rLin.RESET;
               rLin.SETRANGE("Num. Proforma", "Num. Proforma");
               rLin.DELETEALL;
             END;

    CaptionML=[ENU=Proforma invoice Header;
               ESP=Cabecera Proforma];
  }
  FIELDS
  {
    { 1   ;   ;Num. Proforma       ;Code20        ;CaptionML=[ENU=Pro-forma invoice No.;
                                                              ESP=Num. Proforma];
                                                   Description=PK }
    { 3   ;   ;Touroperador        ;Code10        ;TableRelation=TourOperador.TourOperador;
                                                   OnValidate=BEGIN
                                                                //$002-
                                                                rTTOO.RESET;
                                                                rTTOO.GET(Touroperador);
                                                                rTTOO.TESTFIELD(Bloqueado, FALSE);
                                                                //$002+
                                                              END;

                                                   CaptionML=[ENU=TourOperator;
                                                              ESP=TourOperador];
                                                   Description=TourOperador.TourOperador }
    { 5   ;   ;Fecha llegada       ;Date          ;CaptionML=[ENU=Arrival Date;
                                                              ESP=Fecha llegada] }
    { 6   ;   ;Fecha salida        ;Date          ;CaptionML=[ENU=Check-out Date;
                                                              ESP=Fecha salida] }
    { 7   ;   ;Fecha creacion      ;DateTime      ;CaptionML=[ENU=Creation Date;
                                                              ESP=Fecha creacion];
                                                   Editable=No }
    { 9   ;   ;Usuario creacion    ;Code20        ;TableRelation=User."User ID";
                                                   CaptionML=[ENU=Creation user;
                                                              ESP=Usuario creacion];
                                                   Description=User."User ID";
                                                   Editable=No }
    { 11  ;   ;Ultima impresion    ;DateTime      ;CaptionML=[ENU=Last print;
                                                              ESP=Ultima impresion];
                                                   Editable=No }
    { 13  ;   ;Usuario impresion   ;Code20        ;TableRelation=User."User ID";
                                                   CaptionML=[ENU=User print;
                                                              ESP=Usuario impresion];
                                                   Description=User."User ID";
                                                   Editable=No }
    { 15  ;   ;Aviso x email       ;DateTime      ;CaptionML=[ENU=Warning x email;
                                                              ESP=Aviso x email];
                                                   Editable=No }
    { 17  ;   ;Usuario aviso email ;Code20        ;TableRelation=User."User ID";
                                                   CaptionML=[ENU=Warning user email;
                                                              ESP=Usuario aviso email];
                                                   Description=User."User ID";
                                                   Editable=No }
    { 19  ;   ;Fecha anulacion     ;DateTime      ;CaptionML=[ENU=Cencellation Date;
                                                              ESP=Fecha anulacion];
                                                   Editable=No }
    { 21  ;   ;Usuario anulacion   ;Code20        ;CaptionML=[ENU=Cancellation User;
                                                              ESP=Usuario anulacion];
                                                   Editable=No }
    { 23  ;   ;Nombre              ;Text250       ;CaptionML=[ENU=Name;
                                                              ESP=Nombre] }
    { 25  ;   ;Direccion           ;Text50        ;CaptionML=[ENU=Address;
                                                              ESP=Direccion] }
    { 26  ;   ;Direccion 2         ;Text50        ;CaptionML=[ENU=Address 2;
                                                              ESP=Direccion 2] }
    { 27  ;   ;C.P.                ;Code10        ;TableRelation="Post Code".Code;
                                                   CaptionML=[ENU=P.C.;
                                                              ESP=C.P.];
                                                   Description="Post Code".Code }
    { 29  ;   ;Poblacion           ;Text30        ;CaptionML=[ENU=Town;
                                                              ESP=Poblacion] }
    { 31  ;   ;Provincia           ;Text30        ;CaptionML=[ENU=Province;
                                                              ESP=Provincia] }
    { 33  ;   ;Pais                ;Code10        ;TableRelation=Country/Region.Code;
                                                   OnValidate=VAR
                                                                lrPais@1103355000 : Record 9;
                                                              BEGIN
                                                                IF Pais <> '' THEN
                                                                  IF lrPais.GET(Pais) THEN
                                                                    VALIDATE("Agencia Receptiva", lrPais."Agencia Receptiva");
                                                              END;

                                                   CaptionML=[ENU=Country;
                                                              ESP=Pais];
                                                   Description=Country.Code }
    { 35  ;   ;Importe             ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Linea Proforma".Importe WHERE (Num. Proforma=FIELD(Num. Proforma)));
                                                   CaptionML=[ENU=Amount;
                                                              ESP=Importe];
                                                   Description=Sum("Linea Proforma".Importe WHERE (Num. Proforma=FIELD(Num. Proforma)));
                                                   Editable=No }
    { 37  ;   ;Estado              ;Option        ;CaptionML=[ENU=Status;
                                                              ESP=Estado];
                                                   OptionCaptionML=[ENU=Remaining,Generated,Confirmed,Cancelled,Regenerate;
                                                                    ESP=Pendiente,Generada,Confirmada,Anulada,Regenerar];
                                                   OptionString=Pendiente,Generada,Confirmada,Anulada,Regenerar }
    { 39  ;   ;Cod. Divisa         ;Code10        ;CaptionML=[ENU=Currency Code;
                                                              ESP=Cod. divisa];
                                                   Editable=No }
    { 41  ;   ;Cliente             ;Code20        ;TableRelation=Customer.No.;
                                                   CaptionML=[ENU=Client;
                                                              ESP=Cliente];
                                                   Description=Customer.No. }
    { 42  ;   ;Fecha vencimiento   ;Date          ;CaptionML=[ENU=Expiration date;
                                                              ESP=Fecha vencimiento] }
    { 44  ;   ;Reservas incluidas  ;Integer       ;CaptionML=[ENU=Included bookings;
                                                              ESP=Reservas incluidas] }
    { 60  ;   ;Manual              ;Boolean       ;CaptionML=[ENU=Manual;
                                                              ESP=Manual] }
    { 70  ;   ;Prepagos            ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Prepago proforma".Importe WHERE (Num. Proforma=FIELD(Num. Proforma)));
                                                   OnLookup=VAR
                                                              lrPrepagos@1103355000 : Record 7009868;
                                                            BEGIN
                                                              IF NOT (Estado IN [Estado::Generada, Estado::Confirmada]) THEN
                                                                EXIT;

                                                              lrPrepagos.FILTERGROUP(2);
                                                              lrPrepagos.SETRANGE("Num. Proforma", "Num. Proforma");
                                                              lrPrepagos.FILTERGROUP(0);
                                                              FORM.RUNMODAL(0,  lrPrepagos);
                                                            END;

                                                   CaptionML=[ENU=In advance;
                                                              ESP=Prepagos];
                                                   Description=Flowfield;
                                                   Editable=No }
    { 72  ;   ;Prepagos Registrados;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Prepago proforma".Importe WHERE (Num. Proforma=FIELD(Num. Proforma),
                                                                                                     Estado=CONST(Registrado)));
                                                   OnLookup=VAR
                                                              lrPrepagos@1103355000 : Record 7009868;
                                                            BEGIN
                                                              IF NOT (Estado IN [Estado::Generada, Estado::Confirmada]) THEN
                                                                EXIT;

                                                              lrPrepagos.FILTERGROUP(2);
                                                              lrPrepagos.SETRANGE("Num. Proforma", "Num. Proforma");
                                                              lrPrepagos.FILTERGROUP(0);
                                                              FORM.RUNMODAL(0,  lrPrepagos);
                                                            END;

                                                   CaptionML=[ENU=Registered payments in advance;
                                                              ESP=Prepagos Registrados];
                                                   Description=Flowfield;
                                                   Editable=No }
    { 80  ;   ;Bono                ;Code10        ;CaptionML=[ENU=Voucher;
                                                              ESP=Bono];
                                                   Description=Indica si la proforma se gener¢ para un solo bono. }
    { 81  ;   ;LanguageID          ;Code10        ;TableRelation=Language.Code;
                                                   CaptionML=[ENU=Language;
                                                              ESP=Idioma];
                                                   Description=Indica el lenguaje en que se debe imprimir la proforma por defecto }
    { 82  ;   ;Num. cotizacion     ;Integer       ;OnValidate=VAR
                                                                lrCot@1103355000 : Record 7009871;
                                                              BEGIN
                                                                IF "Num. cotizacion" <> 0 THEN BEGIN
                                                                  lrCot.GET("Num. cotizacion");
                                                                  VALIDATE(Pais, lrCot.Pais);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Value No.;
                                                              ESP=Num. cotizacion];
                                                   Description=Vincula la proforma a una cotizaci¢n. }
    { 83  ;   ;Impresa             ;Boolean       ;CaptionML=[ENU=Printed;
                                                              ESP=Impresa] }
    { 84  ;   ;Agencia Receptiva   ;Code20        ;TableRelation=Vendor.No. WHERE (Tipo Proveedor=CONST(IA));
                                                   CaptionML=[ENU=Receptive agency;
                                                              ESP=Agencia Receptiva] }
  }
  KEYS
  {
    {    ;Num. Proforma                           ;Clustered=Yes }
    {    ;Estado                                  ;KeyGroups=RENUMERING }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1103355001 : TextConst 'ENU=PROFINCOM;ESP=PROFINCOM';
      Text002@1103355000 : TextConst 'ENU=Incoming agency Proforma invoice;ESP=Proformas agencia Incoming';
      GestNoSeries@1103355002 : Codeunit 396;
      rLin@1103355003 : Record 7009867;
      Text003@1100253000 : TextConst 'ENU=Completely;ESP=Completamente';
      Text004@1100253001 : TextConst 'ENU=Partially;ESP=Parcialmente';
      Text005@1100253002 : TextConst 'ENU=Pending;ESP=Pendiente';
      rTTOO@1000000000 : Record 7009717;

    PROCEDURE SerieProformas@1103355000();
    VAR
      NoSerie@1103355000 : Record 308;
      LinNoSerie@1103355001 : Record 309;
      LinNoSerie2@1103355003 : Record 309;
      lwFecha@1103355002 : Date;
      lwA¤o@1103355004 : Code[10];
    BEGIN
      // SerieProformas

      // El codigo de serie lo asignamos nosotros

      IF NOT NoSerie.GET(Text001) THEN BEGIN
        CLEAR(NoSerie);
        NoSerie.Code           := Text001;
        NoSerie.Description    := Text002;
        NoSerie."Default Nos." := TRUE;
        NoSerie.INSERT;
      END;

      // Crearemos una linea por cada a¤o

      lwFecha := DMY2DATE(1, 1, DATE2DMY(TODAY, 3));

      LinNoSerie.RESET;
      LinNoSerie.SETRANGE("Series Code"  , Text001);
      LinNoSerie.SETRANGE("Starting Date", lwFecha);
      IF NOT LinNoSerie.FINDFIRST THEN BEGIN
        CLEAR(LinNoSerie2);
        LinNoSerie2.SETRANGE("Series Code", NoSerie.Code);
        IF NOT LinNoSerie2.FINDLAST THEN   //: AJS 14.01.2010
          CLEAR(LinNoSerie2);

        lwA¤o := FORMAT(DATE2DMY(lwFecha, 3));
        lwA¤o := COPYSTR(lwA¤o, 3, 2);

        CLEAR(LinNoSerie);
        LinNoSerie."Series Code"      := Text001;
        LinNoSerie."Line No."         := LinNoSerie2."Line No." + 10;
        LinNoSerie."Starting Date"    := lwFecha;
        LinNoSerie."Starting No."     := 'P' + lwA¤o + '000001';
        LinNoSerie."Increment-by No." := 1;
        LinNoSerie.INSERT;
      END;
    END;

    PROCEDURE EstadoFacturacion@1100253000() : Text[30];
    VAR
      lrLin@1100253000 : Record 7009867;
      lrConcepto@1100253001 : Record 7010019;
      lwEstado@1100253002 : Integer;
    BEGIN
      // EstadoFacturacion

      //. Para cada linea buscamos en las lineas de calculo hotel si estan facturadas

      lwEstado := 0;

      lrLin.RESET;
      lrLin.SETRANGE("Num. Proforma", "Num. Proforma");
      IF lrLin.FINDSET THEN
      BEGIN
        REPEAT
          lrConcepto.RESET;
          lrConcepto.SETRANGE (TourOperador   , Touroperador);
          lrConcepto.SETRANGE (Bono           , lrLin.Bono);
          lrConcepto.SETRANGE ("N§ Reserva"   , lrLin."Num. Reserva");
          lrConcepto.SETRANGE ("Coste/Ingreso", lrConcepto."Coste/Ingreso"::Ingreso);
          lrConcepto.SETFILTER("N§ Factura"   , '<>%1', '');
          IF NOT lrConcepto.ISEMPTY THEN
          BEGIN
            lwEstado := 1;
            lrConcepto.SETRANGE("N§ Factura", '');
            IF lrConcepto.ISEMPTY THEN
              lwEstado := 2;
          END;

        //. Saldremos del bucle en la ultima linea o si la linea que hemos tratado esta parcialmente facturada
        UNTIL (lrLin.NEXT = 0) OR (lwEstado = 1);
      END;

      //. Retornar el texto que muestra el estado de la facturacion de la proforma

      CASE lwEstado OF
        0: EXIT(Text005);
        1: EXIT(Text004);
        2: EXIT(Text003);
      END;
    END;

    BEGIN
    {
      ## AJF 03/06/09

        .- A¤adido campo booleano "Impresa" para indicar que la documentaci¢n de la proforma
           se ha impreso de manera correcta.

      ## AJS 23.06.2009

        .- Cambio el caption del campo Lenguaje por Idioma

      ## AJS 14.01.2010

        .- En la funcion SerieProformas sustituyo el FINDFIST por FINDLAST a la hora de buscar
           la ultima linea de la serie.

      $001 AJS 09052012 Nueva funcion EstadoFacturacion que retorna un texto para mostrar si la proforma ha sido
                        completa o parcialmente facturada o si esta pendiente

      $002 MNC 22072014 CNT-LP-14054 No se pueden usar TTOO bloqueados
    }
    END.
  }
}
