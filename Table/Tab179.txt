OBJECT Table 179 Reversal Entry
{
  OBJECT-PROPERTIES
  {
    Date=02/10/12;
    Time=12:00:00;
    Modified=Yes;
    Version List=AIC2009,NAVW16.00.01,NAVES6.00.01;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Reversal Entry;
               ESP=Mov. de reversi¢n];
    PasteIsValid=No;
  }
  FIELDS
  {
    { 1   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ESP=N§ l¡nea] }
    { 2   ;   ;Entry Type          ;Option        ;CaptionML=[ENU=Entry Type;
                                                              ESP=Tipo movimiento];
                                                   OptionCaptionML=[ENU=" ,G/L Account,Customer,Vendor,Bank Account,Fixed Asset,Maintenance,VAT";
                                                                    ESP=" ,Cuenta,Cliente,Proveedor,Banco,Activo,Mantenimiento,IVA"];
                                                   OptionString=[ ,G/L Account,Customer,Vendor,Bank Account,Fixed Asset,Maintenance,VAT] }
    { 3   ;   ;Entry No.           ;Integer       ;TableRelation=IF (Entry Type=CONST(G/L Account)) "G/L Entry"
                                                                 ELSE IF (Entry Type=CONST(Customer)) "Cust. Ledger Entry"
                                                                 ELSE IF (Entry Type=CONST(Vendor)) "Vendor Ledger Entry"
                                                                 ELSE IF (Entry Type=CONST(Bank Account)) "Bank Account Ledger Entry"
                                                                 ELSE IF (Entry Type=CONST(Fixed Asset)) "FA Ledger Entry"
                                                                 ELSE IF (Entry Type=CONST(Maintenance)) "Maintenance Ledger Entry"
                                                                 ELSE IF (Entry Type=CONST(VAT)) "VAT Entry";
                                                   CaptionML=[ENU=Entry No.;
                                                              ESP=N§ mov.] }
    { 4   ;   ;G/L Register No.    ;Integer       ;TableRelation="G/L Register";
                                                   CaptionML=[ENU=G/L Register No.;
                                                              ESP=N§ asto. registro] }
    { 5   ;   ;Source Code         ;Code10        ;TableRelation="Source Code";
                                                   CaptionML=[ENU=Source Code;
                                                              ESP=C¢d. origen] }
    { 6   ;   ;Journal Batch Name  ;Code10        ;CaptionML=[ENU=Journal Batch Name;
                                                              ESP=Nombre secci¢n diario] }
    { 7   ;   ;Transaction No.     ;Integer       ;CaptionML=[ENU=Transaction No.;
                                                              ESP=N§ asiento] }
    { 8   ;   ;Source Type         ;Option        ;CaptionML=[ENU=Source Type;
                                                              ESP=Tipo procedencia mov.];
                                                   OptionCaptionML=[ENU=" ,Customer,Vendor,Bank Account,Fixed Asset,IC Partner";
                                                                    ESP=" ,Cliente,Proveedor,Banco,Activo,IC asociadas"];
                                                   OptionString=[ ,Customer,Vendor,Bank Account,Fixed Asset,IC Partner] }
    { 9   ;   ;Source No.          ;Code20        ;TableRelation=IF (Source Type=CONST(Customer)) Customer
                                                                 ELSE IF (Source Type=CONST(Vendor)) Vendor
                                                                 ELSE IF (Source Type=CONST(Bank Account)) "Bank Account"
                                                                 ELSE IF (Source Type=CONST(Fixed Asset)) "Fixed Asset";
                                                   CaptionML=[ENU=Source No.;
                                                              ESP=C¢d. procedencia mov.] }
    { 10  ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=[ENU=Currency Code;
                                                              ESP=C¢d. divisa] }
    { 11  ;   ;Description         ;Text80        ;CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n];
                                                   Description=$001 }
    { 12  ;   ;Amount              ;Decimal       ;CaptionML=[ENU=Amount;
                                                              ESP=Importe];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 13  ;   ;Debit Amount        ;Decimal       ;CaptionML=[ENU=Debit Amount;
                                                              ESP=Importe debe];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 14  ;   ;Credit Amount       ;Decimal       ;CaptionML=[ENU=Credit Amount;
                                                              ESP=Importe haber];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 15  ;   ;Amount (LCY)        ;Decimal       ;CaptionML=[ENU=Amount (LCY);
                                                              ESP=Importe (DL)];
                                                   AutoFormatType=1 }
    { 16  ;   ;Debit Amount (LCY)  ;Decimal       ;CaptionML=[ENU=Debit Amount (LCY);
                                                              ESP=Importe debe (DL)];
                                                   AutoFormatType=1 }
    { 17  ;   ;Credit Amount (LCY) ;Decimal       ;CaptionML=[ENU=Credit Amount (LCY);
                                                              ESP=Importe haber (DL)];
                                                   AutoFormatType=1 }
    { 18  ;   ;VAT Amount          ;Decimal       ;CaptionML=[ENU=VAT Amount;
                                                              ESP=Importe IVA];
                                                   AutoFormatType=1 }
    { 19  ;   ;Posting Date        ;Date          ;CaptionML=[ENU=Posting Date;
                                                              ESP=Fecha registro] }
    { 20  ;   ;Document Type       ;Option        ;CaptionML=[ENU=Document Type;
                                                              ESP=Tipo documento];
                                                   OptionCaptionML=[ENU=" ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund";
                                                                    ESP=" ,Pago,Factura,Abono,Docs. inter‚s,Recordatorio,Reembolso"];
                                                   OptionString=[ ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund] }
    { 21  ;   ;Document No.        ;Code20        ;CaptionML=[ENU=Document No.;
                                                              ESP=N§ documento] }
    { 22  ;   ;Account No.         ;Code20        ;CaptionML=[ENU=Account No.;
                                                              ESP=N§ cuenta] }
    { 23  ;   ;Account Name        ;Text50        ;CaptionML=[ENU=Account Name;
                                                              ESP=Nombre cuenta] }
    { 25  ;   ;Bal. Account Type   ;Option        ;CaptionML=[ENU=Bal. Account Type;
                                                              ESP=Tipo contrapartida];
                                                   OptionCaptionML=[ENU=G/L Account,Customer,Vendor,Bank Account,Fixed Asset,IC Partner;
                                                                    ESP=Cuenta,Cliente,Proveedor,Banco,Activo,Empresa vinculada asociada];
                                                   OptionString=G/L Account,Customer,Vendor,Bank Account,Fixed Asset,IC Partner }
    { 26  ;   ;Bal. Account No.    ;Code20        ;TableRelation=IF (Bal. Account Type=CONST(G/L Account)) "G/L Account"
                                                                 ELSE IF (Bal. Account Type=CONST(Customer)) Customer
                                                                 ELSE IF (Bal. Account Type=CONST(Vendor)) Vendor
                                                                 ELSE IF (Bal. Account Type=CONST(Bank Account)) "Bank Account"
                                                                 ELSE IF (Bal. Account Type=CONST(Fixed Asset)) "Fixed Asset";
                                                   CaptionML=[ENU=Bal. Account No.;
                                                              ESP=Cta. contrapartida] }
    { 27  ;   ;FA Posting Category ;Option        ;CaptionML=[ENU=FA Posting Category;
                                                              ESP=A/F Categor¡a registro];
                                                   OptionCaptionML=[ENU=" ,Disposal,Bal. Disposal";
                                                                    ESP=" ,Venta/Baja,Contrapartida venta/baja"];
                                                   OptionString=[ ,Disposal,Bal. Disposal] }
    { 28  ;   ;FA Posting Type     ;Option        ;CaptionML=[ENU=FA Posting Type;
                                                              ESP=A/F Tipo registro];
                                                   OptionCaptionML=[ENU=" ,Acquisition Cost,Depreciation,Write-Down,Appreciation,Custom 1,Custom 2,Proceeds on Disposal,Salvage Value,Gain/Loss,Book Value on Disposal";
                                                                    ESP=" ,Coste,Amortizaci¢n,Depreciaci¢n,Apreciaci¢n,Especial,Provisi¢n,Ingresos de la Venta/Baja,Valor residual,Beneficio/P‚rdida,Valor neto en venta/baja"];
                                                   OptionString=[ ,Acquisition Cost,Depreciation,Write-Down,Appreciation,Custom 1,Custom 2,Proceeds on Disposal,Salvage Value,Gain/Loss,Book Value on Disposal] }
    { 30  ;   ;Reversal Type       ;Option        ;CaptionML=[ENU=Reversal Type;
                                                              ESP=Tipo de reversi¢n];
                                                   OptionCaptionML=[ENU=Transaction,Register;
                                                                    ESP=Transacci¢n,Registro];
                                                   OptionString=Transaction,Register }
  }
  KEYS
  {
    {    ;Line No.                                ;Clustered=Yes }
    {    ;Entry Type                               }
    {    ;Document No.,Posting Date,Entry Type,Entry No. }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      GLSetup@1000 : Record 98;
      ReversalEntry@1001 : TEMPORARY Record 179;
      GLEntry@1007 : Record 17;
      CustLedgEntry@1006 : Record 21;
      VendLedgEntry@1005 : Record 25;
      BankAccLedgEntry@1004 : Record 271;
      VATEntry@1003 : Record 254;
      FALedgEntry@1002 : Record 5601;
      MaintenanceLedgEntry@1009 : Record 5625;
      GLReg@1008 : Record 45;
      Text000@1010 : TextConst 'ENU="You cannot reverse %1 No. %2 because the entry is either applied to an entry or has been changed by a batch job. ";ESP="No es posible revertir %1 n§ %2, el movimiento liquida otro movimiento o fue modificado por un trabajo por lotes. "';
      FAReg@1015 : Record 5617;
      GenJnlCheckLine@1011 : Codeunit 11;
      Text001@1012 : TextConst 'ENU=You cannot reverse %1 No. %2 because the posting date is not within the allowed posting period.;ESP=No es posible revertir %1 n§ %2, la fecha de registro no corresponde al periodo de registro aceptado.';
      AllowPostingFrom@1014 : Date;
      AllowPostingto@1013 : Date;
      Text002@1016 : TextConst 'ENU=You cannot reverse the transaction because it is out of balance.;ESP=No es posible invertir la transacci¢n, est  descuadrada.';
      Text003@1017 : TextConst 'ENU=You cannot reverse %1 No. %2 because the entry has a related check ledger entry.;ESP=No es posible revertir %1 n§ %2, existe una entrada de registro de cheque correspondiente al movimiento.';
      Text004@1018 : TextConst 'ENU=You can only reverse entries that were posted from a journal.;ESP=S¢lo puede invertir entradas registradas desde un diario.';
      Text005@1020 : TextConst 'ENU=You cannot reverse %1 No. %2 because the %3 is not within the allowed posting period.;ESP=No es posible revertir %1 n§ %2, %3 no corresponde al periodo de registro aceptado.';
      Text006@1022 : TextConst 'ENU=You cannot reverse %1 No. %2 because the entry is closed.;ESP=No es posible revertir %1 n§ %2, el movimiento ha sido cerrado.';
      Text007@1021 : TextConst 'ENU=You cannot reverse %1 No. %2 because the entry is included in a bank account reconciliation line. The bank reconciliation has not yet been posted.;ESP=No es posible revertir %1 n§ %2, el movimiento est  incluido en una l¡nea de reconciliaci¢n de cuentas que a£n no se ha registrado.';
      Text008@1019 : TextConst 'ENU=You cannot reverse the transaction because the %1 has been sold.;ESP=No es posible revertir la transacci¢n, se ha vendido el %1.';
      MaxPostingDate@1024 : Date;
      Text009@1025 : TextConst 'ENU=The transaction cannot be reversed, because the %1 has been compressed or a %2 has been deleted.;ESP=No es posible revertir la transacci¢n, se comprimi¢ el %1 o se elimin¢ un %2.';
      Text010@1023 : TextConst 'ENU=You cannot reverse %1 No. %2 because the register has already been involved in a reversal.;ESP=No es posible revertir %1 n§ %2, el registro ya se ha incluido en una reversi¢n.';
      Text011@1026 : TextConst 'ENU=You cannot reverse %1 No. %2 because the entry has already been involved in a reversal.;ESP=No es posible revertir %1 n§ %2, el movimiento ya se ha incluido en una reversi¢n.';
      PostApplied@1029 : Boolean;
      Text012@1028 : TextConst 'ENU=You cannot reverse register No. %1 because it contains customer or vendor ledger entries that have been posted and applied in the same transaction.\\You must reverse each transaction in register No. %1 separately.;ESP=No puede revertir el n§ de registro %1 porque contiene movimientos que se han registrado y aplicado en la misma transacci¢n.\\Debe revertir cada transacci¢n en el n§ de registro %1 de forma separada.';
      Text1100000@1100000 : TextConst 'ENU=You can not reverse entries that sent invoices to Cartera.;ESP=No puede revertir las entradas que hayan enviado facturas a Cartera.';

    PROCEDURE ReverseTransaction@8(TransactionNo@1000 : Integer);
    BEGIN
      InsertReversalEntry(TransactionNo,0);
      ReversalEntry.SETCURRENTKEY("Document No.","Posting Date","Entry Type","Entry No.");
      FORM.RUNMODAL(FORM::"Reverse Entries",ReversalEntry);
      ReversalEntry.DELETEALL;
    END;

    PROCEDURE ReverseRegister@9(RegisterNo@1000 : Integer);
    VAR
      GLReg@1001 : Record 45;
    BEGIN
      GLReg.GET(RegisterNo);
      IF GLReg.Reversed THEN
        ERROR(Text010,GLReg.TABLECAPTION,GLReg."No.");
      IF GLReg."Journal Batch Name" = '' THEN
        ReversalEntry.TestFieldError;

      InsertReversalEntry(RegisterNo,1);
      ReversalEntry.SETCURRENTKEY("Document No.","Posting Date","Entry Type","Entry No.");
      FORM.RUNMODAL(FORM::"Reverse Entries",ReversalEntry);
      ReversalEntry.DELETEALL;
    END;

    LOCAL PROCEDURE InsertReversalEntry@7(Number@1000 : Integer;RevType@1007 : 'Transaction,Register');
    VAR
      GLAcc@1001 : Record 15;
      Cust@1002 : Record 18;
      Vend@1003 : Record 23;
      DtldCustLedgEntry@1011 : Record 379;
      DtldVendLedgEntry@1010 : Record 380;
      BankAcc@1006 : Record 270;
      FA@1004 : Record 5600;
      TempRevertTransactionNo@1005 : TEMPORARY Record 2000000026;
      NextLineNo@1008 : Integer;
    BEGIN
      GLSetup.GET;
      ReversalEntry.DELETEALL;
      PostApplied := FALSE;
      NextLineNo := 1;
      TempRevertTransactionNo.Number := Number;
      TempRevertTransactionNo.INSERT;
      SetReverseFilter(Number,RevType);

      IF CustLedgEntry.FIND('-') THEN
        REPEAT
          CLEAR(ReversalEntry);
          IF RevType = RevType::Register THEN
            ReversalEntry."G/L Register No." := Number;
          ReversalEntry."Reversal Type" := RevType;
          ReversalEntry."Entry Type" := ReversalEntry."Entry Type"::Customer;
          ReversalEntry."Entry No." :=  CustLedgEntry."Entry No.";
          Cust.GET(CustLedgEntry."Customer No.");
          ReversalEntry."Account No." := Cust."No.";
          ReversalEntry."Account Name" := Cust.Name;
          ReversalEntry."Posting Date" := CustLedgEntry."Posting Date";
          ReversalEntry."Source Code" := CustLedgEntry."Source Code";
          ReversalEntry."Journal Batch Name" := CustLedgEntry."Journal Batch Name";
          ReversalEntry."Transaction No." := CustLedgEntry."Transaction No.";
          ReversalEntry."Currency Code" := CustLedgEntry."Currency Code";
          ReversalEntry.Description := CustLedgEntry.Description;
          CustLedgEntry.CALCFIELDS(Amount,"Debit Amount","Credit Amount",
            "Amount (LCY)","Debit Amount (LCY)","Credit Amount (LCY)");
          ReversalEntry.Amount := CustLedgEntry.Amount;
          ReversalEntry."Debit Amount" := CustLedgEntry."Debit Amount";
          ReversalEntry."Credit Amount" := CustLedgEntry."Credit Amount";
          ReversalEntry."Amount (LCY)" := CustLedgEntry."Amount (LCY)";
          ReversalEntry."Debit Amount (LCY)" := CustLedgEntry."Debit Amount (LCY)";
          ReversalEntry."Credit Amount (LCY)" := CustLedgEntry."Credit Amount (LCY)";
          ReversalEntry."Document Type" := CustLedgEntry."Document Type";
          ReversalEntry."Document No." := CustLedgEntry."Document No.";
          ReversalEntry."Bal. Account Type" := CustLedgEntry."Bal. Account Type";
          ReversalEntry."Bal. Account No." := CustLedgEntry."Bal. Account No.";
          ReversalEntry."Line No." := NextLineNo;
          NextLineNo := NextLineNo + 1;
          ReversalEntry.INSERT;

          CLEAR(DtldCustLedgEntry);
          DtldCustLedgEntry.SETCURRENTKEY("Transaction No.","Customer No.","Entry Type");
          DtldCustLedgEntry.SETRANGE(DtldCustLedgEntry."Transaction No.",CustLedgEntry."Transaction No.");
          DtldCustLedgEntry.SETRANGE(DtldCustLedgEntry."Customer No.",CustLedgEntry."Customer No.");
          DtldCustLedgEntry.SETFILTER(
            DtldCustLedgEntry."Entry Type",'<>%1',DtldCustLedgEntry."Entry Type"::"Initial Entry");
          IF DtldCustLedgEntry.FIND('-') THEN BEGIN
            IF RevType = RevType::Register THEN
              ERROR(Text012,Number);
            CLEAR(DtldCustLedgEntry);
            DtldCustLedgEntry.SETCURRENTKEY("Cust. Ledger Entry No.");
            DtldCustLedgEntry.SETRANGE("Cust. Ledger Entry No.",CustLedgEntry."Entry No.");
            DtldCustLedgEntry.SETRANGE(Unapplied,TRUE);
            IF DtldCustLedgEntry.FIND('-') THEN BEGIN
              REPEAT
                TempRevertTransactionNo.Number := DtldCustLedgEntry."Transaction No.";
                IF TempRevertTransactionNo.INSERT THEN;
              UNTIL DtldCustLedgEntry.NEXT = 0;
            END;
          END;
        UNTIL CustLedgEntry.NEXT = 0;

      IF VendLedgEntry.FIND('-') THEN
        REPEAT
          CLEAR(ReversalEntry);
          IF RevType = RevType::Register THEN
            ReversalEntry."G/L Register No." := Number;
          ReversalEntry."Reversal Type" := RevType;
          ReversalEntry."Entry Type" := ReversalEntry."Entry Type"::Vendor;
          ReversalEntry."Entry No." :=  VendLedgEntry."Entry No.";
          Vend.GET(VendLedgEntry."Vendor No.");
          ReversalEntry."Account No." := Vend."No.";
          ReversalEntry."Account Name" := Vend.Name;
          ReversalEntry."Posting Date" := VendLedgEntry."Posting Date";
          ReversalEntry."Source Code" := VendLedgEntry."Source Code";
          ReversalEntry."Journal Batch Name" := VendLedgEntry."Journal Batch Name";
          ReversalEntry."Transaction No." := VendLedgEntry."Transaction No.";
          ReversalEntry."Currency Code" := VendLedgEntry."Currency Code";
          ReversalEntry.Description := VendLedgEntry.Description;
          VendLedgEntry.CALCFIELDS(Amount,"Debit Amount","Credit Amount",
            "Amount (LCY)","Debit Amount (LCY)","Credit Amount (LCY)");
          ReversalEntry.Amount := VendLedgEntry.Amount;
          ReversalEntry."Debit Amount" := VendLedgEntry."Debit Amount";
          ReversalEntry."Credit Amount" := VendLedgEntry."Credit Amount";
          ReversalEntry."Amount (LCY)" := VendLedgEntry."Amount (LCY)";
          ReversalEntry."Debit Amount (LCY)" := VendLedgEntry."Debit Amount (LCY)";
          ReversalEntry."Credit Amount (LCY)" := VendLedgEntry."Credit Amount (LCY)";
          ReversalEntry."Document Type" := VendLedgEntry."Document Type";
          ReversalEntry."Document No." := VendLedgEntry."Document No.";
          ReversalEntry."Bal. Account Type" := VendLedgEntry."Bal. Account Type";
          ReversalEntry."Bal. Account No." := VendLedgEntry."Bal. Account No.";
          ReversalEntry."Line No." := NextLineNo;
          NextLineNo := NextLineNo + 1;
          ReversalEntry.INSERT;

          CLEAR(DtldVendLedgEntry);
          DtldVendLedgEntry.SETCURRENTKEY("Transaction No.","Vendor No.","Entry Type");
          DtldVendLedgEntry.SETRANGE(DtldVendLedgEntry."Transaction No.",VendLedgEntry."Transaction No.");
          DtldVendLedgEntry.SETRANGE(DtldVendLedgEntry."Vendor No.",VendLedgEntry."Vendor No.");
          DtldVendLedgEntry.SETFILTER(
            DtldVendLedgEntry."Entry Type",'<>%1',DtldVendLedgEntry."Entry Type"::"Initial Entry");
          IF DtldVendLedgEntry.FIND('-') THEN BEGIN
            IF RevType = RevType::Register THEN
              ERROR(Text012,Number);
            CLEAR(DtldVendLedgEntry);
            DtldVendLedgEntry.SETCURRENTKEY("Vendor Ledger Entry No.");
            DtldVendLedgEntry.SETRANGE("Vendor Ledger Entry No.",VendLedgEntry."Entry No.");
            DtldVendLedgEntry.SETRANGE(Unapplied,TRUE);
            IF DtldVendLedgEntry.FIND('-') THEN BEGIN
              REPEAT
                TempRevertTransactionNo.Number := DtldVendLedgEntry."Transaction No.";
                IF TempRevertTransactionNo.INSERT THEN;
              UNTIL DtldVendLedgEntry.NEXT = 0;
            END;
          END;
        UNTIL VendLedgEntry.NEXT = 0;

      IF BankAccLedgEntry.FIND('-') THEN
        REPEAT
          CLEAR(ReversalEntry);
          IF RevType = RevType::Register THEN
            ReversalEntry."G/L Register No." := Number;
          ReversalEntry."Reversal Type" := RevType;
          ReversalEntry."Entry Type" := ReversalEntry."Entry Type"::"Bank Account";
          ReversalEntry."Entry No." :=  BankAccLedgEntry."Entry No.";
          BankAcc.GET(BankAccLedgEntry."Bank Account No.");
          ReversalEntry."Account No." := BankAcc."No.";
          ReversalEntry."Account Name" := BankAcc.Name;
          ReversalEntry."Posting Date" := BankAccLedgEntry."Posting Date";
          ReversalEntry."Source Code" := BankAccLedgEntry."Source Code";
          ReversalEntry."Journal Batch Name" := BankAccLedgEntry."Journal Batch Name";
          ReversalEntry."Transaction No." := BankAccLedgEntry."Transaction No.";
          ReversalEntry."Currency Code" := BankAccLedgEntry."Currency Code";
          ReversalEntry.Description := BankAccLedgEntry.Description;
          ReversalEntry.Amount := BankAccLedgEntry.Amount;
          ReversalEntry."Debit Amount" := BankAccLedgEntry."Debit Amount";
          ReversalEntry."Credit Amount" := BankAccLedgEntry."Credit Amount";
          ReversalEntry."Amount (LCY)" := BankAccLedgEntry."Amount (LCY)";
          ReversalEntry."Debit Amount (LCY)" := BankAccLedgEntry."Debit Amount (LCY)";
          ReversalEntry."Credit Amount (LCY)" := BankAccLedgEntry."Credit Amount (LCY)";
          ReversalEntry."Document Type" := BankAccLedgEntry."Document Type";
          ReversalEntry."Document No." := BankAccLedgEntry."Document No.";
          ReversalEntry."Bal. Account Type" := BankAccLedgEntry."Bal. Account Type";
          ReversalEntry."Bal. Account No." := BankAccLedgEntry."Bal. Account No.";
          ReversalEntry."Line No." := NextLineNo;
          NextLineNo := NextLineNo + 1;
          ReversalEntry.INSERT;
        UNTIL BankAccLedgEntry.NEXT = 0;

      IF FALedgEntry.FIND('-') THEN
        REPEAT
          CLEAR(ReversalEntry);
          IF RevType = RevType::Register THEN
            ReversalEntry."G/L Register No." := Number;
          ReversalEntry."Reversal Type" := RevType;
          ReversalEntry."Entry Type" := ReversalEntry."Entry Type"::"Fixed Asset";
          ReversalEntry."Entry No." :=  FALedgEntry."Entry No.";
          FA.GET(FALedgEntry."FA No.");
          ReversalEntry."Account No." := FA."No.";
          ReversalEntry."Account Name" := FA.Description;
          ReversalEntry."Posting Date" := FALedgEntry."Posting Date";
          ReversalEntry."FA Posting Category" := FALedgEntry."FA Posting Category";
          ReversalEntry."FA Posting Type" := FALedgEntry."FA Posting Type" + 1;
          ReversalEntry."Source Code" := FALedgEntry."Source Code";
          ReversalEntry."Journal Batch Name" := FALedgEntry."Journal Batch Name";
          ReversalEntry."Transaction No." := FALedgEntry."Transaction No.";
          ReversalEntry.Description := FALedgEntry.Description;
          ReversalEntry."Amount (LCY)" := FALedgEntry.Amount;
          ReversalEntry."Debit Amount (LCY)" := FALedgEntry."Debit Amount";
          ReversalEntry."Credit Amount (LCY)" := FALedgEntry."Credit Amount";
          ReversalEntry."VAT Amount" := FALedgEntry."VAT Amount";
          ReversalEntry."Document Type" := FALedgEntry."Document Type";
          ReversalEntry."Document No." := FALedgEntry."Document No.";
          ReversalEntry."Bal. Account Type" := FALedgEntry."Bal. Account Type";
          ReversalEntry."Bal. Account No." := FALedgEntry."Bal. Account No.";
          IF FALedgEntry."FA Posting Type" <> FALedgEntry."FA Posting Type"::"Salvage Value" THEN BEGIN
            ReversalEntry."Line No." := NextLineNo;
            NextLineNo := NextLineNo + 1;
            ReversalEntry.INSERT;
          END;
        UNTIL FALedgEntry.NEXT = 0;

      IF MaintenanceLedgEntry.FIND('-') THEN
        REPEAT
          CLEAR(ReversalEntry);
          IF RevType = RevType::Register THEN
            ReversalEntry."G/L Register No." := Number;
          ReversalEntry."Reversal Type" := RevType;
          ReversalEntry."Entry Type" := ReversalEntry."Entry Type"::Maintenance;
          ReversalEntry."Entry No." :=  MaintenanceLedgEntry."Entry No.";
          FA.GET(MaintenanceLedgEntry."FA No.");
          ReversalEntry."Account No." := FA."No.";
          ReversalEntry."Account Name" := FA.Description;
          ReversalEntry."Posting Date" := MaintenanceLedgEntry."Posting Date";
          ReversalEntry."Source Code" := MaintenanceLedgEntry."Source Code";
          ReversalEntry."Journal Batch Name" := MaintenanceLedgEntry."Journal Batch Name";
          ReversalEntry."Transaction No." := MaintenanceLedgEntry."Transaction No.";
          ReversalEntry.Description := MaintenanceLedgEntry.Description;
          ReversalEntry."Amount (LCY)" := MaintenanceLedgEntry.Amount;
          ReversalEntry."Debit Amount (LCY)" := MaintenanceLedgEntry."Debit Amount";
          ReversalEntry."Credit Amount (LCY)" := MaintenanceLedgEntry."Credit Amount";
          ReversalEntry."VAT Amount" := MaintenanceLedgEntry."VAT Amount";
          ReversalEntry."Document Type" := MaintenanceLedgEntry."Document Type";
          ReversalEntry."Document No." := MaintenanceLedgEntry."Document No.";
          ReversalEntry."Bal. Account Type" := MaintenanceLedgEntry."Bal. Account Type";
          ReversalEntry."Bal. Account No." := MaintenanceLedgEntry."Bal. Account No.";
          ReversalEntry."Line No." := NextLineNo;
          NextLineNo := NextLineNo + 1;
          ReversalEntry.INSERT;
        UNTIL MaintenanceLedgEntry.NEXT = 0;

      TempRevertTransactionNo.FINDSET;
      REPEAT
        IF RevType = RevType::Transaction THEN
          VATEntry.SETRANGE("Transaction No.",TempRevertTransactionNo.Number);
        IF VATEntry.FINDSET THEN
          REPEAT
            CLEAR(ReversalEntry);
            IF RevType = RevType::Register THEN
              ReversalEntry."G/L Register No." := Number;
            ReversalEntry."Reversal Type" := RevType;
            ReversalEntry."Entry Type" := ReversalEntry."Entry Type"::VAT;
            ReversalEntry."Entry No." :=  VATEntry."Entry No.";
            ReversalEntry."Posting Date" := VATEntry."Posting Date";
            ReversalEntry."Source Code" := VATEntry."Source Code";
            ReversalEntry."Transaction No." := VATEntry."Transaction No.";
            ReversalEntry.Amount := VATEntry.Amount;
            ReversalEntry."Amount (LCY)" := VATEntry.Amount;
            ReversalEntry."Document Type" := VATEntry."Document Type";
            ReversalEntry."Document No." := VATEntry."Document No.";
            ReversalEntry."Line No." := NextLineNo;
            NextLineNo := NextLineNo + 1;
            ReversalEntry.INSERT;
          UNTIL VATEntry.NEXT = 0;
      UNTIL TempRevertTransactionNo.NEXT = 0;

      TempRevertTransactionNo.FINDSET;
      REPEAT
        IF RevType = RevType::Transaction THEN
          GLEntry.SETRANGE("Transaction No.",TempRevertTransactionNo.Number);
        IF GLEntry.FINDSET THEN
          REPEAT
            CLEAR(ReversalEntry);
            IF RevType = RevType::Register THEN
              ReversalEntry."G/L Register No." := Number;
            ReversalEntry."Reversal Type" := RevType;
            ReversalEntry."Entry Type" := ReversalEntry."Entry Type"::"G/L Account";
            ReversalEntry."Entry No." :=  GLEntry."Entry No.";
            IF NOT GLAcc.GET(GLEntry."G/L Account No.") THEN
              ERROR(Text009,GLEntry.TABLECAPTION,GLAcc.TABLECAPTION);
            ReversalEntry."Account No." := GLAcc."No.";
            ReversalEntry."Account Name" := GLAcc.Name;
            ReversalEntry."Posting Date" := GLEntry."Posting Date";
            ReversalEntry."Source Code" := GLEntry."Source Code";
            ReversalEntry."Journal Batch Name" := GLEntry."Journal Batch Name";
            ReversalEntry."Transaction No." := GLEntry."Transaction No.";
            ReversalEntry."Source Type" := GLEntry."Source Type";
            ReversalEntry."Source No." := GLEntry."Source No.";
            ReversalEntry.Description := GLEntry.Description;
            ReversalEntry."Amount (LCY)" := GLEntry.Amount;
            ReversalEntry."Debit Amount (LCY)" := GLEntry."Debit Amount";
            ReversalEntry."Credit Amount (LCY)" := GLEntry."Credit Amount";
            ReversalEntry."VAT Amount" := GLEntry."VAT Amount";
            ReversalEntry."Document Type" := GLEntry."Document Type";
            ReversalEntry."Document No." := GLEntry."Document No.";
            ReversalEntry."Bal. Account Type" := GLEntry."Bal. Account Type";
            ReversalEntry."Bal. Account No." := GLEntry."Bal. Account No.";
            ReversalEntry."Line No." := NextLineNo;
            NextLineNo := NextLineNo + 1;
            ReversalEntry.INSERT;
          UNTIL GLEntry.NEXT = 0;
      UNTIL TempRevertTransactionNo.NEXT = 0;

      IF ReversalEntry.FIND('-') THEN;
    END;

    PROCEDURE CheckEntries@14();
    VAR
      GLAcc@1001 : Record 15;
      DtldCustLedgEntry@1006 : Record 379;
      DtldVendLedgEntry@1007 : Record 380;
      LedgEntryDim@1008 : Record 355;
      DateComprReg@1000 : Record 87;
      BalanceCheckAmount@1012 : Decimal;
      BalanceCheckAddCurrAmount@1010 : Decimal;
    BEGIN
      DtldCustLedgEntry.LOCKTABLE;
      DtldVendLedgEntry.LOCKTABLE;
      LedgEntryDim.LOCKTABLE;
      GLEntry.LOCKTABLE;
      CustLedgEntry.LOCKTABLE;
      VendLedgEntry.LOCKTABLE;
      BankAccLedgEntry.LOCKTABLE;
      FALedgEntry.LOCKTABLE;
      MaintenanceLedgEntry.LOCKTABLE;
      VATEntry.LOCKTABLE;
      GLReg.LOCKTABLE;
      FAReg.LOCKTABLE;
      GLSetup.GET;
      MaxPostingDate := 0D;
      IF NOT GLEntry.FIND('-') THEN
        ERROR(Text009,GLEntry.TABLECAPTION,GLAcc.TABLECAPTION);
      IF GLEntry.FIND('-') THEN BEGIN
        IF GLEntry."Journal Batch Name" = '' THEN
          TestFieldError;
        REPEAT
          CheckGLAcc(GLEntry,BalanceCheckAmount,BalanceCheckAddCurrAmount);
        UNTIL GLEntry.NEXT = 0;
      END;
      IF (BalanceCheckAmount <> 0) OR (BalanceCheckAddCurrAmount <> 0) THEN
        ERROR(Text002);

      IF CustLedgEntry.FIND('-') THEN
        REPEAT
          CheckCust(CustLedgEntry);
        UNTIL CustLedgEntry.NEXT = 0;

      IF VendLedgEntry.FIND('-') THEN
        REPEAT
          CheckVend(VendLedgEntry);
        UNTIL VendLedgEntry.NEXT = 0;

      IF BankAccLedgEntry.FIND('-') THEN
        REPEAT
          CheckBankAcc(BankAccLedgEntry);
        UNTIL BankAccLedgEntry.NEXT = 0;

      IF FALedgEntry.FIND('-') THEN
        REPEAT
          CheckFA(FALedgEntry);
        UNTIL FALedgEntry.NEXT = 0;

      IF MaintenanceLedgEntry.FIND('-') THEN
        REPEAT
          CheckMaintenance(MaintenanceLedgEntry);
        UNTIL MaintenanceLedgEntry.NEXT = 0;

      IF VATEntry.FIND('-') THEN
        REPEAT
          CheckVAT(VATEntry);
        UNTIL VATEntry.NEXT = 0;
      DateComprReg.CheckMaxDateCompressed(MaxPostingDate,1);
    END;

    LOCAL PROCEDURE CheckGLAcc@13(GLEntry@1000 : Record 17;VAR BalanceCheckAmount@1002 : Decimal;VAR BalanceCheckAddCurrAmount@1001 : Decimal);
    VAR
      GLAcc@1003 : Record 15;
      Text1100100@1100000 : TextConst 'ENU=The entry cannot be reversed;ESP=No es posible revertir el movimiento';
      GLRegDoc@1100001 : Codeunit 7000001;
      CarteraDoc@1100002 : Record 7000002;
      CarteraSetup@1100010 : Record 7000016;
    BEGIN
      GLAcc.GET(GLEntry."G/L Account No.");
      CheckPostingDate(
        GLEntry."Posting Date",GLEntry.TABLECAPTION,GLEntry.FIELDCAPTION("Entry No."),GLEntry."Entry No.");
      GLAcc.TESTFIELD(Blocked,FALSE);
      GLEntry.TESTFIELD("Job No.",'');

      IF CarteraSetup.READPERMISSION THEN
        IF (GLEntry."Bill No." <> '') OR (GLRegDoc.CheckPostedDocsInPostedBGPO(GLEntry)) THEN
          ERROR(Text1100100)
        ELSE BEGIN
          CarteraDoc.SETCURRENTKEY(Type,"Document No.");
          CarteraDoc.SETRANGE("Document No.","Document No.");
          CarteraDoc.SETRANGE("Document Type",CarteraDoc."Document Type"::Invoice);
          IF CarteraDoc.FINDSET THEN
            ERROR(Text1100000);
        END;

      IF GLEntry.Reversed THEN
        AlreadyReversedEntry(GLEntry.TABLECAPTION,GLEntry."Entry No.");
      BalanceCheckAmount := BalanceCheckAmount + GLEntry.Amount;
      IF GLSetup."Additional Reporting Currency" <> '' THEN
        BalanceCheckAddCurrAmount := BalanceCheckAddCurrAmount + GLEntry."Additional-Currency Amount";
    END;

    LOCAL PROCEDURE CheckCust@16(CustLedgEntry@1001 : Record 21);
    VAR
      Cust@1000 : Record 18;
    BEGIN
      Cust.GET(CustLedgEntry."Customer No.");
      CheckPostingDate(
        CustLedgEntry."Posting Date",CustLedgEntry.TABLECAPTION,CustLedgEntry.FIELDCAPTION("Entry No."),
        CustLedgEntry."Entry No.");
      Cust.CheckBlockedCustOnJnls(Cust,CustLedgEntry."Document Type",FALSE);
      IF CustLedgEntry.Reversed THEN
        AlreadyReversedEntry(CustLedgEntry.TABLECAPTION,CustLedgEntry."Entry No.");
      CheckDtldCustLedgEntry(CustLedgEntry);
    END;

    LOCAL PROCEDURE CheckVend@17(VendLedgEntry@1001 : Record 25);
    VAR
      Vend@1000 : Record 23;
    BEGIN
      Vend.GET(VendLedgEntry."Vendor No.");
      CheckPostingDate(
        VendLedgEntry."Posting Date",VendLedgEntry.TABLECAPTION,VendLedgEntry.FIELDCAPTION("Entry No."),
        VendLedgEntry."Entry No.");
      Vend.CheckBlockedVendOnJnls(Vend,VendLedgEntry."Document Type",FALSE);
      IF VendLedgEntry.Reversed THEN
        AlreadyReversedEntry(VendLedgEntry.TABLECAPTION,VendLedgEntry."Entry No.");
      CheckDtldVendLedgEntry(VendLedgEntry);
    END;

    LOCAL PROCEDURE CheckBankAcc@18(BankAccLedgEntry@1000 : Record 271);
    VAR
      BankAcc@1001 : Record 270;
      CheckLedgEntry@1002 : Record 272;
    BEGIN
      BankAcc.GET(BankAccLedgEntry."Bank Account No.");
      CheckPostingDate(
        BankAccLedgEntry."Posting Date",BankAccLedgEntry.TABLECAPTION,BankAccLedgEntry.FIELDCAPTION("Entry No."),
        BankAccLedgEntry."Entry No.");
      BankAcc.TESTFIELD(Blocked,FALSE);
      IF BankAccLedgEntry.Reversed THEN
        AlreadyReversedEntry(BankAccLedgEntry.TABLECAPTION,BankAccLedgEntry."Entry No.");
      IF NOT BankAccLedgEntry.Open THEN
        ERROR(
          Text006,BankAccLedgEntry.TABLECAPTION,BankAccLedgEntry."Entry No.");
      IF BankAccLedgEntry."Statement No." <> '' THEN
        ERROR(
          Text007,BankAccLedgEntry.TABLECAPTION,BankAccLedgEntry."Entry No.");
      CheckLedgEntry.SETCURRENTKEY("Bank Account Ledger Entry No.");
      CheckLedgEntry.SETRANGE("Bank Account Ledger Entry No.",BankAccLedgEntry."Entry No.");
      IF CheckLedgEntry.FIND('-') THEN
        ERROR(
          Text003,BankAccLedgEntry.TABLECAPTION,BankAccLedgEntry."Entry No.");
    END;

    LOCAL PROCEDURE CheckFA@19(FALedgEntry@1000 : Record 5601);
    VAR
      FA@1001 : Record 5600;
      FADeprBook@1002 : Record 5612;
      DeprCalc@1003 : Codeunit 5616;
    BEGIN
      FA.GET(FALedgEntry."FA No.");
      CheckPostingDate(
        FALedgEntry."Posting Date",FALedgEntry.TABLECAPTION,FALedgEntry.FIELDCAPTION("Entry No."),
        FALedgEntry."Entry No.");
      CheckFAPostingDate(
        FALedgEntry."FA Posting Date",FALedgEntry.TABLECAPTION,FALedgEntry.FIELDCAPTION("Entry No."),
        FALedgEntry."Entry No.");
      FA.TESTFIELD(Blocked,FALSE);
      FA.TESTFIELD(Inactive,FALSE);
      IF FALedgEntry.Reversed THEN
        AlreadyReversedEntry(FALedgEntry.TABLECAPTION,FALedgEntry."Entry No.");
      FALedgEntry.TESTFIELD("Depreciation Book Code");
      FADeprBook.GET(FA."No.",FALedgEntry."Depreciation Book Code");
      IF FADeprBook."Disposal Date" <> 0D THEN
        ERROR(Text008,DeprCalc.FAName(FA,FALedgEntry."Depreciation Book Code"));
      FALedgEntry.TESTFIELD("G/L Entry No.");
    END;

    LOCAL PROCEDURE CheckMaintenance@20(MaintenanceLedgEntry@1000 : Record 5625);
    VAR
      FA@1001 : Record 5600;
      FADeprBook@1002 : Record 5612;
    BEGIN
      FA.GET(MaintenanceLedgEntry."FA No.");
      CheckPostingDate(
        MaintenanceLedgEntry."Posting Date",MaintenanceLedgEntry.TABLECAPTION,MaintenanceLedgEntry.FIELDCAPTION("Entry No."),
        MaintenanceLedgEntry."Entry No.");
      CheckFAPostingDate(
        MaintenanceLedgEntry."FA Posting Date",MaintenanceLedgEntry.TABLECAPTION,MaintenanceLedgEntry.FIELDCAPTION("Entry No."),
        MaintenanceLedgEntry."Entry No.");
      FA.TESTFIELD(Blocked,FALSE);
      FA.TESTFIELD(Inactive,FALSE);
      MaintenanceLedgEntry.TESTFIELD("Depreciation Book Code");
      IF MaintenanceLedgEntry.Reversed THEN
        AlreadyReversedEntry(MaintenanceLedgEntry.TABLECAPTION,MaintenanceLedgEntry."Entry No.");
      FADeprBook.GET(FA."No.",MaintenanceLedgEntry."Depreciation Book Code");
      MaintenanceLedgEntry.TESTFIELD("G/L Entry No.");
    END;

    LOCAL PROCEDURE CheckVAT@21(VATEntry@1000 : Record 254);
    BEGIN
      CheckPostingDate(
        VATEntry."Posting Date",VATEntry.TABLECAPTION,VATEntry.FIELDCAPTION("Entry No."),
        VATEntry."Entry No.");
      IF VATEntry.Closed THEN
        ERROR(
          Text006,VATEntry.TABLECAPTION,VATEntry."Entry No.");
      IF VATEntry.Reversed THEN
        AlreadyReversedEntry(VATEntry.TABLECAPTION,VATEntry."Entry No.");
    END;

    LOCAL PROCEDURE CheckDtldCustLedgEntry@2(CustLedgEntry@1000 : Record 21) : Boolean;
    VAR
      DtldCustLedgEntry@1001 : Record 379;
    BEGIN
      DtldCustLedgEntry.SETCURRENTKEY("Cust. Ledger Entry No.");
      DtldCustLedgEntry.SETRANGE("Cust. Ledger Entry No.",CustLedgEntry."Entry No.");
      IF DtldCustLedgEntry.FIND('-') THEN
        REPEAT
          IF (DtldCustLedgEntry."Entry Type" <> DtldCustLedgEntry."Entry Type"::"Initial Entry") AND
             (NOT DtldCustLedgEntry.Unapplied)
          THEN
            ERROR(
              Text000,CustLedgEntry.TABLECAPTION,CustLedgEntry."Entry No.");
        UNTIL DtldCustLedgEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE CheckDtldVendLedgEntry@28(VendLedgEntry@1000 : Record 25) : Boolean;
    VAR
      DtldVendLedgEntry@1001 : Record 380;
    BEGIN
      DtldVendLedgEntry.SETCURRENTKEY("Vendor Ledger Entry No.");
      DtldVendLedgEntry.SETRANGE("Vendor Ledger Entry No.",VendLedgEntry."Entry No.");
      IF DtldVendLedgEntry.FIND('-') THEN
        REPEAT
          IF (DtldVendLedgEntry."Entry Type" <> DtldVendLedgEntry."Entry Type"::"Initial Entry") AND
             (NOT DtldVendLedgEntry.Unapplied)
          THEN
            ERROR(
              Text000,VendLedgEntry.TABLECAPTION,VendLedgEntry."Entry No.");
        UNTIL DtldVendLedgEntry.NEXT = 0;
    END;

    PROCEDURE SetReverseFilter@1(Number@1001 : Integer;RevType@1000 : 'Transaction,Register');
    BEGIN
      IF RevType = RevType::Transaction THEN BEGIN
        GLEntry.SETCURRENTKEY("Transaction No.");
        CustLedgEntry.SETCURRENTKEY("Transaction No.");
        VendLedgEntry.SETCURRENTKEY("Transaction No.");
        BankAccLedgEntry.SETCURRENTKEY("Transaction No.");
        FALedgEntry.SETCURRENTKEY("Transaction No.");
        MaintenanceLedgEntry.SETCURRENTKEY("Transaction No.");
        VATEntry.SETCURRENTKEY("Transaction No.");
        GLEntry.SETRANGE("Transaction No.",Number);
        CustLedgEntry.SETRANGE("Transaction No.",Number);
        VendLedgEntry.SETRANGE("Transaction No.",Number);
        BankAccLedgEntry.SETRANGE("Transaction No.",Number);
        FALedgEntry.SETRANGE("Transaction No.",Number);
        MaintenanceLedgEntry.SETRANGE("Transaction No.",Number);
        VATEntry.SETRANGE("Transaction No.",Number);
      END ELSE BEGIN
        GLReg.GET(Number);
        GLEntry.SETRANGE("Entry No.",GLReg."From Entry No.",GLReg."To Entry No.");
        CustLedgEntry.SETRANGE("Entry No.",GLReg."From Entry No.",GLReg."To Entry No.");
        VendLedgEntry.SETRANGE("Entry No.",GLReg."From Entry No.",GLReg."To Entry No.");
        BankAccLedgEntry.SETRANGE("Entry No.",GLReg."From Entry No.",GLReg."To Entry No.");
        FALedgEntry.SETCURRENTKEY("G/L Entry No.");
        FALedgEntry.SETRANGE("G/L Entry No.",GLReg."From Entry No.",GLReg."To Entry No.");
        MaintenanceLedgEntry.SETCURRENTKEY("G/L Entry No.");
        MaintenanceLedgEntry.SETRANGE("G/L Entry No.",GLReg."From Entry No.",GLReg."To Entry No.");
        VATEntry.SETRANGE("Entry No.",GLReg."From VAT Entry No.",GLReg."To VAT Entry No.");
      END;
    END;

    PROCEDURE CopyFilters@15(VAR GLEntry2@1007 : Record 17;VAR CustLedgEntry2@1006 : Record 21;VAR VendLedgEntry2@1005 : Record 25;VAR BankAccLedgEntry2@1004 : Record 271;VAR VATEntry2@1003 : Record 254;VAR FALedgEntry2@1002 : Record 5601;VAR MaintenanceLedgEntry2@1001 : Record 5625);
    BEGIN
      GLEntry2.COPY(GLEntry);
      CustLedgEntry2.COPY(CustLedgEntry);
      VendLedgEntry2.COPY(VendLedgEntry);
      BankAccLedgEntry2.COPY(BankAccLedgEntry);
      VATEntry2.COPY(VATEntry);
      FALedgEntry2.COPY(FALedgEntry);
      MaintenanceLedgEntry2.COPY(MaintenanceLedgEntry);
    END;

    PROCEDURE ShowGLEntries@22();
    BEGIN
      FORM.RUN(0,GLEntry);
    END;

    PROCEDURE ShowCustLedgEntries@26();
    BEGIN
      FORM.RUN(0,CustLedgEntry);
    END;

    PROCEDURE ShowVendLedgEntries@5();
    BEGIN
      FORM.RUN(0,VendLedgEntry);
    END;

    PROCEDURE ShowBankAccLedgEntries@6();
    BEGIN
      FORM.RUN(0,BankAccLedgEntry);
    END;

    PROCEDURE ShowFALedgEntries@10();
    BEGIN
      FORM.RUN(0,FALedgEntry);
    END;

    PROCEDURE ShowMaintenanceLedgEntries@12();
    BEGIN
      FORM.RUN(0,MaintenanceLedgEntry);
    END;

    PROCEDURE ShowVATEntries@11();
    BEGIN
      FORM.RUN(0,VATEntry);
    END;

    PROCEDURE Caption@3() : Text[250];
    VAR
      GLAcc@1000 : Record 15;
      GLEntry@1002 : Record 17;
      Cust@1001 : Record 18;
      CustLedgEntry@1003 : Record 21;
      Vend@1004 : Record 23;
      VendLedgEntry@1005 : Record 25;
      BankAcc@1006 : Record 270;
      BankAccLedgEntry@1007 : Record 271;
      FA@1008 : Record 5600;
      FALedgEntry@1009 : Record 5601;
      MaintenanceLedgEntry@1011 : Record 5625;
      VATEntry@1010 : Record 254;
    BEGIN
      IF "Entry Type" = "Entry Type"::"G/L Account" THEN BEGIN
        IF GLEntry.GET("Entry No.") THEN;
        IF GLAcc.GET(GLEntry."G/L Account No.") THEN;
        EXIT(STRSUBSTNO('%1 %2 %3',GLAcc.TABLECAPTION,GLAcc."No.",GLAcc.Name));
      END;
      IF "Entry Type" = "Entry Type"::Customer THEN BEGIN
        IF CustLedgEntry.GET("Entry No.") THEN;
        IF Cust.GET(CustLedgEntry."Customer No.") THEN;
        EXIT(STRSUBSTNO('%1 %2 %3',Cust.TABLECAPTION,Cust."No.",Cust.Name));
      END;
      IF "Entry Type" = "Entry Type"::Vendor THEN BEGIN
        IF VendLedgEntry.GET("Entry No.") THEN;
        IF Vend.GET(VendLedgEntry."Vendor No.") THEN;
        EXIT(STRSUBSTNO('%1 %2 %3',Vend.TABLECAPTION,Vend."No.",Vend.Name));
      END;
      IF "Entry Type" = "Entry Type"::"Bank Account" THEN BEGIN
        IF BankAccLedgEntry.GET("Entry No.") THEN;
        IF BankAcc.GET(BankAccLedgEntry."Bank Account No.") THEN;
        EXIT(STRSUBSTNO('%1 %2 %3',BankAcc.TABLECAPTION,BankAcc."No.",BankAcc.Name));
      END;
      IF "Entry Type" = "Entry Type"::"Fixed Asset" THEN BEGIN
        IF FALedgEntry.GET("Entry No.") THEN;
        IF FA.GET(FALedgEntry."FA No.") THEN;
        EXIT(STRSUBSTNO('%1 %2 %3',FA.TABLECAPTION,FA."No.",FA.Description));
      END;
      IF "Entry Type" = "Entry Type"::Maintenance THEN BEGIN
        IF MaintenanceLedgEntry.GET("Entry No.") THEN;
        IF FA.GET(MaintenanceLedgEntry."FA No.") THEN;
        EXIT(STRSUBSTNO('%1 %2 %3',FA.TABLECAPTION,FA."No.",FA.Description));
      END;
      IF ("Entry Type" = "Entry Type"::VAT) THEN
        EXIT(STRSUBSTNO('%1',VATEntry.TABLECAPTION));
    END;

    LOCAL PROCEDURE CheckPostingDate@23(PostingDate@1000 : Date;Caption@1002 : Text[50];CaptionEntryNo@1005 : Text[50];EntryNo@1001 : Integer);
    BEGIN
      IF GenJnlCheckLine.DateNotAllowed(PostingDate) THEN
        ERROR(Text001,Caption,EntryNo);
      IF PostingDate > MaxPostingDate THEN
        MaxPostingDate := PostingDate;
    END;

    LOCAL PROCEDURE CheckFAPostingDate@24(FAPostingDate@1000 : Date;Caption@1004 : Text[50];CaptionEntryNo@1007 : Text[50];EntryNo@1003 : Integer);
    VAR
      UserSetup@1001 : Record 91;
      FASetup@1002 : Record 5603;
    BEGIN
      IF (AllowPostingFrom = 0D) AND (AllowPostingto = 0D) THEN BEGIN
        IF USERID <> '' THEN
          IF UserSetup.GET(USERID) THEN BEGIN
            AllowPostingFrom := UserSetup."Allow FA Posting From";
            AllowPostingto := UserSetup."Allow FA Posting To";
          END;
        IF (AllowPostingFrom = 0D) AND (AllowPostingto = 0D) THEN BEGIN
          FASetup.GET;
          AllowPostingFrom := FASetup."Allow FA Posting From";
          AllowPostingto := FASetup."Allow FA Posting To";
        END;
        IF AllowPostingto = 0D THEN
          AllowPostingto := 31129998D;
      END;
      IF (FAPostingDate < AllowPostingFrom) OR (FAPostingDate > AllowPostingto) THEN
        ERROR(Text005,Caption,EntryNo,FALedgEntry.FIELDCAPTION("FA Posting Date"));
      IF FAPostingDate > MaxPostingDate THEN
        MaxPostingDate := FAPostingDate;
    END;

    PROCEDURE TestFieldError@4();
    BEGIN
      ERROR(Text004);
    END;

    PROCEDURE AlreadyReversedEntry@29(Caption@1000 : Text[50];EntryNo@1001 : Integer);
    BEGIN
      ERROR(Text011,Caption,EntryNo);
    END;

    PROCEDURE VerifyReversalEntries@25(VAR ReversalEntry2@1000 : Record 179;Number@1002 : Integer;RevType@1001 : 'Transaction,Register') : Boolean;
    BEGIN
      InsertReversalEntry(Number,RevType);
      CLEAR(ReversalEntry);
      CLEAR(ReversalEntry2);
      IF ReversalEntry2.FINDSET THEN
        REPEAT
          IF ReversalEntry.NEXT = 0 THEN
            EXIT(FALSE);
          IF NOT ReversalEntry.Equal(ReversalEntry2) THEN
            EXIT(FALSE);
        UNTIL ReversalEntry2.NEXT = 0;
      EXIT(ReversalEntry.NEXT = 0);
    END;

    PROCEDURE Equal@27(ReversalEntry2@1000 : Record 179) : Boolean;
    BEGIN
      EXIT(("Entry Type" = ReversalEntry2."Entry Type") AND
        ("Entry No." = ReversalEntry2."Entry No."));
    END;

    BEGIN
    {
      $001 AJS 12802012 Ampliar el campo Description a 80 caracteres
    }
    END.
  }
}
