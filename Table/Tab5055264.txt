OBJECT Table 5055264 Liq. Account Schedule Column
{
  OBJECT-PROPERTIES
  {
    Date=13/02/07;
    Time=12:00:00;
    Version List=LIQEM5.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Liq. Account Schedule Column;
               ESP=Columna esquema ctas. liq.];
  }
  FIELDS
  {
    { 1   ;   ;Column Layout Name  ;Code10        ;TableRelation="Liq. Acc. Schedule Layout Name";
                                                   CaptionML=[ENU=Column Layout Name;
                                                              ESP=Nombre plantilla columna] }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ESP=N§ l¡nea] }
    { 3   ;   ;Column No.          ;Code10        ;CaptionML=[ENU=Column No.;
                                                              ESP=N§ columna] }
    { 4   ;   ;Column Header       ;Text30        ;CaptionML=[ENU=Column Header;
                                                              ESP=Cabecera columna] }
    { 5   ;   ;Column Type         ;Option        ;InitValue=Net Change;
                                                   CaptionML=[ENU=Column Type;
                                                              ESP=Tipo columna];
                                                   OptionCaptionML=[ENU=Formula,Net Change,Balance at Date,Beginning Balance,Year to Date,Rest of Fiscal Year,Entire Fiscal Year;
                                                                    ESP=F¢rmula,Saldo periodo,Saldo a la fecha,Saldo inicial,Hasta la fecha,Resto del ejercicio,Ejercicio completo];
                                                   OptionString=Formula,Net Change,Balance at Date,Beginning Balance,Year to Date,Rest of Fiscal Year,Entire Fiscal Year }
    { 6   ;   ;Entry Type          ;Option        ;CaptionML=[ENU=Entry Type;
                                                              ESP=Tipo movimiento];
                                                   OptionCaptionML=[ENU=Liquidity Ledger Entry;
                                                                    ESP=Movimiento liquidez];
                                                   OptionString=Liquidity Ledger Entry }
    { 7   ;   ;Amount Type         ;Option        ;CaptionML=[ENU=Amount Type;
                                                              ESP=Tipo importe];
                                                   OptionCaptionML=[ENU=Amount;
                                                                    ESP=Importe];
                                                   OptionString=Amount }
    { 8   ;   ;Formula             ;Code80        ;OnValidate=BEGIN
                                                                LiqAccSchedLine.CheckFormula(Formula);
                                                              END;

                                                   CaptionML=[ENU=Formula;
                                                              ESP=F¢rmula] }
    { 9   ;   ;Comparison Date Formula;DateFormula;OnValidate=BEGIN
                                                                IF FORMAT("Comparison Date Formula") <> '' THEN
                                                                  VALIDATE("Comparison Period Formula",'');
                                                              END;

                                                   CaptionML=[ENU=Comparison Date Formula;
                                                              ESP=F¢rmula fecha comparaci¢n] }
    { 10  ;   ;Show Opposite Sign  ;Boolean       ;CaptionML=[ENU=Show Opposite Sign;
                                                              ESP=Muestra signo opuesto] }
    { 11  ;   ;Show                ;Option        ;InitValue=Always;
                                                   CaptionML=[ENU=Show;
                                                              ESP=Mostrar];
                                                   OptionCaptionML=[ENU=Always,Never,When Positive,When Negative;
                                                                    ESP=Siempre,Nunca,Si es positivo,Si es negativo];
                                                   OptionString=Always,Never,When Positive,When Negative;
                                                   NotBlank=Yes }
    { 12  ;   ;Rounding Factor     ;Option        ;CaptionML=[ENU=Rounding Factor;
                                                              ESP=Factor redondeo];
                                                   OptionCaptionML=[ENU=None,1,1000,1000000;
                                                                    ESP=No,1,1000,1000000];
                                                   OptionString=None,1,1000,1000000 }
    { 13  ;   ;Comparison Period Formula;Code20   ;OnValidate=VAR
                                                                Steps@1140000 : Integer;
                                                                IntervalSteps@1140001 : Integer;
                                                                RangeFromInt@1140002 : Integer;
                                                                RangeToInt@1140003 : Integer;
                                                                Type@1140004 : ' ,Period,Fiscal year,Fiscal Halfyear,Fiscal Quarter';
                                                                IntervalType@1140005 : ' ,Period,Fiscal year';
                                                                RangeFromType@1140006 : 'Int,CP,LP';
                                                                RangeToType@1140007 : 'Int,CP,LP';
                                                              BEGIN
                                                                ParsePeriodFormula(
                                                                  "Comparison Period Formula",
                                                                  Steps,Type,RangeFromType,RangeToType,RangeFromInt,RangeToInt);
                                                                IF "Comparison Period Formula" <> '' THEN
                                                                  CLEAR("Comparison Date Formula");
                                                              END;

                                                   CaptionML=[ENU=Comparison Period Formula;
                                                              ESP=F¢rmula de periodo comparativo] }
    { 16  ;   ;Dimension 1 Totaling;Text80        ;ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Dimension 1 Totaling;
                                                              ESP=Total dimensi¢n 1];
                                                   CaptionClass=GetCaptionClass(5) }
    { 17  ;   ;Dimension 2 Totaling;Text80        ;ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Dimension 2 Totaling;
                                                              ESP=Total dimensi¢n 2];
                                                   CaptionClass=GetCaptionClass(6) }
    { 18  ;   ;Dimension 3 Totaling;Text80        ;ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Dimension 3 Totaling;
                                                              ESP=Total dimensi¢n 3];
                                                   CaptionClass=GetCaptionClass(7) }
    { 19  ;   ;Dimension 4 Totaling;Text80        ;ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Dimension 4 Totaling;
                                                              ESP=Total dimensi¢n 4];
                                                   CaptionClass=GetCaptionClass(8) }
  }
  KEYS
  {
    {    ;Column Layout Name,Line No.             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text1140000@1140000 : TextConst 'ENU=%1 is not a valid Period Formula;ESP=%1 no es una f¢rmula periodo v lida';
      Text1140001@1140001 : TextConst 'ENU=P;ESP=P';
      Text1140002@1140002 : TextConst 'ENU=FY;ESP=FY';
      Text1140003@1140003 : TextConst 'ENU=CP;ESP=CP';
      Text1140004@1140004 : TextConst 'ENU=LP;ESP=LP';
      Text1140006@1140006 : TextConst 'ENU=1,6,,Dimension 1 Filter;ESP=1,6,,Filtro dimensi¢n 1';
      Text1140007@1140007 : TextConst 'ENU=1,6,,Dimension 2 Filter;ESP=1,6,,Filtro dimensi¢n 2';
      Text1140008@1140008 : TextConst 'ENU=1,6,,Dimension 3 Filter;ESP=1,6,,Filtro dimensi¢n 3';
      Text1140009@1140009 : TextConst 'ENU=1,6,,Dimension 4 Filter;ESP=1,6,,Filtro dimensi¢n 4';
      Text1140010@1140010 : TextConst 'ENU=,, Totaling;ESP=,, Totales';
      Text1140011@1140011 : TextConst 'ENU=1,5,,Dimension 1 Totaling;ESP=1,5,,Total dimensi¢n 1';
      Text1140012@1140012 : TextConst 'ENU=1,5,,Dimension 2 Totaling;ESP=1,5,,Total dimensi¢n 2';
      Text1140013@1140013 : TextConst 'ENU=1,5,,Dimension 3 Totaling;ESP=1,5,,Total dimensi¢n 3';
      Text1140014@1140014 : TextConst 'ENU=1,5,,Dimension 4 Totaling;ESP=1,5,,Total dimensi¢n 4';
      Text1140015@1140015 : TextConst 'ENU=The %1 refers to %2 %3, which does not exist. The field %4 on table %5 has now been deleted.;ESP=El %1 se refiere a %2 %3, que no existe. El campo %4 en la tabla %5 ha sido borrado.';
      LiqAccSchedLine@1140005 : TEMPORARY Record 5055262;
      LiqAccScheduleLayoutName@1140016 : Record 5055263;
      LiquidityAnalysisView@1140017 : Record 5055269;
      GLSetup@1140018 : Record 98;
      HasGLSetup@1140019 : Boolean;

    PROCEDURE ParsePeriodFormula@1140000(Formula@1140000 : Code[20];VAR Steps@1140001 : Integer;VAR Type@1140002 : ' ,Period,Fiscal Year';VAR RangeFromType@1140003 : 'Int,CP,LP';VAR RangeToType@1140004 : 'Int,CP,LP';VAR RangeFromInt@1140005 : Integer;VAR RangeToInt@1140006 : Integer);
    VAR
      OriginalFormula@1140007 : Code[20];
    BEGIN
      // <PeriodFormula> ::= <signed integer> <formula> | blank
      // <signed integer> ::= <sign> <positive integer> | blank
      // <sign> ::= + | - | blank
      // <positive integer> ::= <digit 1-9> <digits>
      // <digit 1-9> ::= 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9
      // <digits> ::= 0 <digits> | <digit 1-9> <digits> | blank
      // <formula> ::= P | FY <range> | FH <range> | FQ <range>
      // <range> ::= blank | [<range2>]
      // <range2> ::= <index> .. <index> | <index>
      // <index> ::= <positive integer> | CP | LP

      OriginalFormula := Formula;
      Formula := DELCHR(Formula);

      IF NOT ParseFormula(Formula,Steps,Type) THEN
        ERROR(Text1140000,OriginalFormula);

      IF Type = Type::"Fiscal Year" THEN
        IF NOT ParseRange(Formula,RangeFromType,RangeFromInt,RangeToType,RangeToInt) THEN
          ERROR(Text1140000,OriginalFormula);

      IF Formula <> '' THEN
        ERROR(Text1140000,OriginalFormula);
    END;

    LOCAL PROCEDURE ParseFormula@1140001(VAR Formula@1140000 : Code[20];VAR Steps@1140001 : Integer;VAR Type@1140002 : ' ,Period,Fiscal year,Fiscal Halfyear,Fiscal Quarter') : Boolean;
    BEGIN
      Steps := 0;
      Type := Type::" ";

      IF Formula = '' THEN
        EXIT(TRUE);

      IF NOT ParseSignedInteger(Formula,Steps) THEN
        EXIT(FALSE);

      IF Formula = '' THEN
        EXIT(FALSE);

      IF NOT ParseType(Formula,Type) THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ParseSignedInteger@1140002(VAR Formula@1140000 : Code[20];VAR Int@1140001 : Integer) : Boolean;
    BEGIN
      Int := 0;

      CASE COPYSTR(Formula,1,1) OF
        '-':
          BEGIN
            Formula := COPYSTR(Formula,2);
            IF NOT ParseInt(Formula,Int,FALSE) THEN
              EXIT(FALSE);
            Int := -Int;
          END;
        '+':
          BEGIN
            Formula := COPYSTR(Formula,2);
            IF NOT ParseInt(Formula,Int,FALSE) THEN
              EXIT(FALSE);
          END;
        ELSE
          BEGIN
            IF NOT ParseInt(Formula,Int,TRUE) THEN
              EXIT(FALSE);
          END;
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ParseInt@1140003(VAR Formula@1140000 : Code[20];VAR Int@1140001 : Integer;AllowNotInt@1140002 : Boolean) : Boolean;
    VAR
      IntegerStr@1140003 : Code[20];
    BEGIN
      IF COPYSTR(Formula,1,1) IN ['1'..'9'] THEN
        REPEAT
          IntegerStr := IntegerStr + COPYSTR(Formula,1,1);
          Formula := COPYSTR(Formula,2);
          IF Formula = '' THEN
            EXIT(FALSE);
        UNTIL NOT (COPYSTR(Formula,1,1) IN ['0'..'9'])
      ELSE
        EXIT(AllowNotInt);
      EVALUATE(Int,IntegerStr);
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ParseType@1140004(VAR Formula@1140000 : Code[20];VAR Type@1140001 : ' ,Period,Fiscal Year') : Boolean;
    BEGIN
      CASE ReadToken(Formula) OF
        Text1140001:
          Type := Type::Period;
        Text1140002:
          Type := Type::"Fiscal Year";
        ELSE
          EXIT(FALSE);
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ParseRange@1140005(VAR Formula@1140000 : Code[20];VAR FromType@1140001 : 'Int,CP,LP';VAR FromInt@1140002 : Integer;VAR ToType@1140003 : 'Int,CP,LP';VAR ToInt@1140004 : Integer) : Boolean;
    VAR
      IntegerStr@1140005 : Code[20];
      Sign@1140006 : Integer;
    BEGIN
      FromType := FromType::CP;
      ToType := ToType::CP;

      IF Formula = '' THEN
        EXIT(TRUE);

      IF NOT ParseToken(Formula,'[') THEN
        EXIT(FALSE);

      IF NOT ParseIndex(Formula,FromType,FromInt) THEN
        EXIT(FALSE);
      IF Formula = '' THEN
        EXIT(FALSE);

      IF COPYSTR(Formula,1,1) = '.' THEN BEGIN
        IF NOT ParseToken(Formula,'..') THEN
          EXIT(FALSE);
        IF NOT ParseIndex(Formula,ToType,ToInt) THEN
          EXIT(FALSE);
      END ELSE BEGIN
        ToType := FromType;
        ToInt := FromInt;
      END;

      IF NOT ParseToken(Formula,']') THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ParseIndex@1140006(VAR Formula@1140000 : Code[20];VAR IndexType@1140001 : 'Int,CP,LP';VAR Index@1140002 : Integer) : Boolean;
    VAR
      IntegerStr@1140003 : Code[20];
      Sign@1140004 : Integer;
    BEGIN
      IF Formula = '' THEN
        EXIT(FALSE);

      IF ParseInt(Formula,Index,FALSE) THEN
        IndexType := IndexType::Int
      ELSE
        CASE ReadToken(Formula) OF
          Text1140003:
            IndexType := IndexType::CP;
          Text1140004:
            IndexType := IndexType::LP;
          ELSE
            EXIT(FALSE);
        END;

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ParseToken@1140007(VAR Formula@1140000 : Code[20];Token@1140001 : Code[20]) : Boolean;
    BEGIN
      IF COPYSTR(Formula,1,STRLEN(Token)) <> Token THEN
        EXIT(FALSE);
      Formula := COPYSTR(Formula,STRLEN(Token) + 1);
      EXIT(TRUE)
    END;

    LOCAL PROCEDURE ReadToken@1140008(VAR Formula@1140000 : Code[20]) : Code[20];
    VAR
      Token@1140001 : Code[20];
      p@1140002 : Integer;
    BEGIN
      FOR p := 1 TO STRLEN(Formula) DO BEGIN
        IF COPYSTR(Formula,p,1) IN ['[',']','.'] THEN BEGIN
          Formula := COPYSTR(Formula,STRLEN(Token) + 1);
          EXIT(Token);
        END;
        Token := Token + COPYSTR(Formula,p,1);
      END;

      Formula := '';
      EXIT(Token);
    END;

    PROCEDURE LookUpDimFilter@1140009(DimNo@1140000 : Integer;VAR Text@1140001 : Text[250]) : Boolean;
    VAR
      DimVal@1140002 : Record 349;
      DimValList@1140003 : Form 560;
    BEGIN

      GetColLayoutSetup;
      CASE DimNo OF
        1: DimVal.SETRANGE("Dimension Code",LiquidityAnalysisView."Dimension 1 Code");
        2: DimVal.SETRANGE("Dimension Code",LiquidityAnalysisView."Dimension 2 Code");
        3: DimVal.SETRANGE("Dimension Code",LiquidityAnalysisView."Dimension 3 Code");
        4: DimVal.SETRANGE("Dimension Code",LiquidityAnalysisView."Dimension 4 Code");
      END;
      DimValList.LOOKUPMODE(TRUE);
      DimValList.SETTABLEVIEW(DimVal);
      IF DimValList.RUNMODAL = ACTION::LookupOK THEN BEGIN
        DimValList.GETRECORD(DimVal);
        Text := Text + DimValList.GetSelectionFilter;
        EXIT(TRUE);
      END ELSE
        EXIT(FALSE)
    END;

    PROCEDURE GetCaptionClass@1140010(AnalysisViewDimType@1140000 : Integer) : Text[250];
    BEGIN
      GetColLayoutSetup;
      CASE AnalysisViewDimType OF
        1:
          BEGIN
            IF LiquidityAnalysisView."Dimension 1 Code" <> '' THEN
              EXIT('1,6,' + LiquidityAnalysisView."Dimension 1 Code")
            ELSE
              EXIT(Text1140006);
          END;
        2:
          BEGIN
            IF LiquidityAnalysisView."Dimension 2 Code" <> '' THEN
              EXIT('1,6,' + LiquidityAnalysisView."Dimension 2 Code")
            ELSE
              EXIT(Text1140007);
          END;
        3:
          BEGIN
            IF LiquidityAnalysisView."Dimension 3 Code" <> '' THEN
              EXIT('1,6,' + LiquidityAnalysisView."Dimension 3 Code")
            ELSE
              EXIT(Text1140008);
          END;
        4:
          BEGIN
            IF LiquidityAnalysisView."Dimension 4 Code" <> '' THEN
              EXIT('1,6,' + LiquidityAnalysisView."Dimension 4 Code")
            ELSE
              EXIT(Text1140009);
          END;
        5:
          BEGIN
            IF LiquidityAnalysisView."Dimension 1 Code" <> '' THEN
              EXIT('1,5,' + LiquidityAnalysisView."Dimension 1 Code" + Text1140010)
            ELSE
              EXIT(Text1140011);
          END;
        6:
          BEGIN
            IF LiquidityAnalysisView."Dimension 2 Code" <> '' THEN
              EXIT('1,5,' + LiquidityAnalysisView."Dimension 2 Code" + Text1140010)
            ELSE
              EXIT(Text1140012);
          END;
        7:
          BEGIN
            IF LiquidityAnalysisView."Dimension 3 Code" <> '' THEN
              EXIT('1,5,' + LiquidityAnalysisView."Dimension 3 Code" + Text1140010)
            ELSE
              EXIT(Text1140013);
          END;
        8:
          BEGIN
            IF LiquidityAnalysisView."Dimension 4 Code" <> '' THEN
              EXIT('1,5,' + LiquidityAnalysisView."Dimension 4 Code" + Text1140010)
            ELSE
              EXIT(Text1140014);
          END;
      END;
    END;

    LOCAL PROCEDURE GetColLayoutSetup@1140011();
    BEGIN
      IF "Column Layout Name" <> LiqAccScheduleLayoutName.Name THEN
        LiqAccScheduleLayoutName.GET("Column Layout Name");
      IF LiqAccScheduleLayoutName."Liq. Analysis View Name" <> '' THEN BEGIN
        IF LiqAccScheduleLayoutName."Liq. Analysis View Name" <> LiquidityAnalysisView.Code THEN
          IF NOT LiquidityAnalysisView.GET(LiqAccScheduleLayoutName."Liq. Analysis View Name") THEN BEGIN
            MESSAGE(
              Text1140015,
              LiqAccScheduleLayoutName.TABLECAPTION,LiquidityAnalysisView.TABLECAPTION,
              LiqAccScheduleLayoutName."Liq. Analysis View Name",
              LiqAccScheduleLayoutName.FIELDCAPTION("Liq. Analysis View Name"),LiqAccScheduleLayoutName.TABLECAPTION);
            LiqAccScheduleLayoutName."Liq. Analysis View Name" := '';
            LiqAccScheduleLayoutName.MODIFY;
          END;
      END;

      IF LiqAccScheduleLayoutName."Liq. Analysis View Name" = '' THEN BEGIN
        IF NOT HasGLSetup THEN BEGIN
          GLSetup.GET;
          HasGLSetup := TRUE;
        END;
        CLEAR(LiquidityAnalysisView);
        LiquidityAnalysisView."Dimension 1 Code" := GLSetup."Global Dimension 1 Code";
        LiquidityAnalysisView."Dimension 2 Code" := GLSetup."Global Dimension 2 Code";
      END;
    END;

    BEGIN
    END.
  }
}
