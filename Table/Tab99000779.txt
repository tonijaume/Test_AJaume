OBJECT Table 99000779 Production BOM Version
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    DataCaptionFields=Production BOM No.,Version Code,Description;
    OnInsert=BEGIN
               ProdBOMHeader.GET("Production BOM No.");
               IF "Version Code" = '' THEN BEGIN
                 ProdBOMHeader.TESTFIELD("Version Nos.");
                 NoSeriesMgt.InitSeries(ProdBOMHeader."Version Nos.",xRec."No. Series",0D,"Version Code","No. Series");
               END;
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;
             END;

    OnDelete=VAR
               ProdBOMLine@1000 : Record 99000772;
             BEGIN
               ProdBOMLine.SETRANGE("Production BOM No.","Production BOM No.");
               ProdBOMLine.SETRANGE("Version Code","Version Code");
               ProdBOMLine.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=Production BOM Version;
               ESP=Versi¢n L.M. producci¢n];
    LookupFormID=Form99000800;
    DrillDownFormID=Form99000800;
  }
  FIELDS
  {
    { 1   ;   ;Production BOM No.  ;Code20        ;TableRelation="Production BOM Header";
                                                   CaptionML=[ENU=Production BOM No.;
                                                              ESP=N§ L.M. producci¢n];
                                                   NotBlank=Yes }
    { 2   ;   ;Version Code        ;Code10        ;CaptionML=[ENU=Version Code;
                                                              ESP=C¢d. versi¢n] }
    { 3   ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n] }
    { 10  ;   ;Starting Date       ;Date          ;CaptionML=[ENU=Starting Date;
                                                              ESP=Fecha inicial] }
    { 21  ;   ;Unit of Measure Code;Code10        ;TableRelation="Unit of Measure";
                                                   OnValidate=BEGIN
                                                                IF (Status = Status::Certified) AND ("Unit of Measure Code" <> xRec."Unit of Measure Code") THEN
                                                                  FIELDERROR(Status);
                                                              END;

                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              ESP=C¢d. unidad medida] }
    { 22  ;   ;Last Date Modified  ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              ESP=Fecha £lt. modificaci¢n];
                                                   Editable=No }
    { 45  ;   ;Status              ;Option        ;OnValidate=VAR
                                                                ProdBOMHeader@1000 : Record 99000771;
                                                                PlanningAssignment@1001 : Record 99000850;
                                                                ProdBOMCheck@1002 : Codeunit 99000769;
                                                                BNManagement@1003 : Codeunit 8000;
                                                              BEGIN
                                                                IF (Status <> xRec.Status) AND (Status = Status::Certified) THEN BEGIN
                                                                  ProdBOMCheck.ProdBOMLineCheck("Production BOM No.","Version Code");
                                                                  TESTFIELD("Unit of Measure Code");
                                                                  ProdBOMHeader.GET("Production BOM No.");
                                                                  ProdBOMHeader."Low-Level Code" := 0;
                                                                  ProdBOMCheck.Code(ProdBOMHeader,"Version Code");
                                                                  PlanningAssignment.NewBOM("Production BOM No.");
                                                                END;

                                                                IF (Status <> xRec.Status) AND
                                                                   (Status IN [Status::Certified,Status::Closed])
                                                                THEN
                                                                  BNManagement.SendProdBOMVerToBN(Rec);

                                                                MODIFY(TRUE);
                                                                COMMIT;
                                                              END;

                                                   CaptionML=[ENU=Status;
                                                              ESP=Estado];
                                                   OptionCaptionML=[ENU=New,Certified,Under Development,Closed;
                                                                    ESP=Nueva,Certificada,En desarrollo,Cerrada];
                                                   OptionString=New,Certified,Under Development,Closed }
    { 50  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              ESP=Nos. serie];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Production BOM No.,Version Code         ;Clustered=Yes }
    {    ;Production BOM No.,Starting Date         }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ProdBOMHeader@1000 : Record 99000771;
      ProdBOMVersion@1001 : Record 99000779;
      NoSeriesMgt@1002 : Codeunit 396;

    PROCEDURE AssistEdit@2(OldProdBOMVersion@1000 : Record 99000779) : Boolean;
    BEGIN
      WITH ProdBOMVersion DO BEGIN
        ProdBOMVersion := Rec;
        ProdBOMHeader.GET("Production BOM No.");
        ProdBOMHeader.TESTFIELD("Version Nos.");
        IF NoSeriesMgt.SelectSeries(ProdBOMHeader."Version Nos.",OldProdBOMVersion."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("Version Code");
          Rec := ProdBOMVersion;
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE Caption@1() : Text[100];
    VAR
      ProdBOMHeader@1000 : Record 99000771;
    BEGIN
      IF GETFILTERS = '' THEN
        EXIT('');

      IF NOT ProdBOMHeader.GET("Production BOM No.") THEN
        EXIT('');

      EXIT(
        STRSUBSTNO('%1 %2 %3',
          "Production BOM No.",ProdBOMHeader.Description,"Version Code"));
    END;

    BEGIN
    END.
  }
}
