OBJECT Table 225 Post Code
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01,NAVES4.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Post Code;
               ESP=C¢digo postal];
    LookupFormID=Form367;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;OnValidate=VAR
                                                                PostCode@1000 : Record 225;
                                                              BEGIN
                                                                PostCode.SETCURRENTKEY("Search City");
                                                                PostCode.SETRANGE("Search City","Search City");
                                                                PostCode.SETRANGE(Code,Code);
                                                                IF PostCode.FIND('-') THEN
                                                                  ERROR(Text000,FIELDCAPTION(Code),Code);
                                                                GetCountyCode;
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              ESP=C¢digo];
                                                   NotBlank=Yes }
    { 2   ;   ;City                ;Text30        ;OnValidate=VAR
                                                                PostCode@1000 : Record 225;
                                                              BEGIN
                                                                TESTFIELD(Code);
                                                                "Search City" := City;
                                                                IF xRec."Search City" <> "Search City" THEN BEGIN
                                                                  PostCode.SETCURRENTKEY("Search City");
                                                                  PostCode.SETRANGE("Search City","Search City");
                                                                  PostCode.SETRANGE(Code,Code);
                                                                  IF PostCode.FIND('-') THEN
                                                                    ERROR(Text000,FIELDCAPTION(City),City);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=City;
                                                              ESP=Poblaci¢n] }
    { 3   ;   ;Search City         ;Code30        ;CaptionML=[ENU=Search City;
                                                              ESP=Buscar poblaci¢n] }
    { 10700;  ;County Code         ;Code10        ;TableRelation=Area;
                                                   CaptionML=[ENU=County Code;
                                                              ESP=C¢d. provincia] }
  }
  KEYS
  {
    {    ;Code,City                               ;Clustered=Yes }
    {    ;City,Code                                }
    {    ;Search City                              }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 %2 already exists.;ESP=Ya existe %1 %2.';

    PROCEDURE ValidateCity@5(VAR City@1000 : Text[30];VAR PostCode@1001 : Code[20];VAR County@1006 : Text[30]);
    VAR
      PostCodeRec@1002 : Record 225;
      PostCodeRec2@1004 : Record 225;
      SearchCity@1003 : Code[30];
    BEGIN
      IF NOT GUIALLOWED THEN
        EXIT;
      IF City <> '' THEN BEGIN
        SearchCity := City;
        PostCodeRec.SETCURRENTKEY("Search City");
        IF STRPOS(SearchCity,'*') = STRLEN(SearchCity) THEN
          PostCodeRec.SETFILTER("Search City",SearchCity)
        ELSE
          PostCodeRec.SETRANGE("Search City",SearchCity);
        IF NOT PostCodeRec.FIND('-') THEN
          EXIT;
        PostCodeRec2.COPY(PostCodeRec);
        IF PostCodeRec2.NEXT = 1 THEN
          IF FORM.RUNMODAL(FORM::"Post Codes",PostCodeRec,PostCodeRec.Code) <> ACTION::LookupOK THEN
            EXIT;
        PostCode := PostCodeRec.Code;
        City := PostCodeRec.City;
        County := PostCodeRec.CountyText;
      END;
    END;

    PROCEDURE ValidatePostCode@6(VAR City@1001 : Text[30];VAR PostCode@1000 : Code[20];VAR County@1005 : Text[30]);
    VAR
      PostCodeRec@1002 : Record 225;
      PostCodeRec2@1003 : Record 225;
    BEGIN
      IF PostCode <> '' THEN BEGIN
        IF STRPOS(PostCode,'*') = STRLEN(PostCode) THEN
          PostCodeRec.SETFILTER(Code,PostCode)
        ELSE
          PostCodeRec.SETRANGE(Code,PostCode);
        IF NOT PostCodeRec.FIND('-') THEN
          EXIT;
        PostCodeRec2.COPY(PostCodeRec);
        IF (PostCodeRec2.NEXT = 1) AND GUIALLOWED THEN
          IF FORM.RUNMODAL(FORM::"Post Codes",PostCodeRec,PostCodeRec.Code) <> ACTION::LookupOK THEN
            EXIT;
        PostCode := PostCodeRec.Code;
        City := PostCodeRec.City;
        County := PostCodeRec.CountyText;
      END;
    END;

    PROCEDURE LookUpCity@7(VAR City@1001 : Text[30];VAR PostCode@1000 : Code[20];VAR County@1005 : Text[30];ReturnValues@1002 : Boolean);
    VAR
      PostCodeRec@1003 : Record 225;
    BEGIN
      IF NOT GUIALLOWED THEN
        EXIT;
      PostCodeRec.SETCURRENTKEY(City,Code);
      PostCodeRec.Code := PostCode;
      PostCodeRec.City := City;
      IF (FORM.RUNMODAL(FORM::"Post Codes",PostCodeRec,PostCodeRec.City) = ACTION::LookupOK) AND ReturnValues THEN BEGIN
        PostCode := PostCodeRec.Code;
        City := PostCodeRec.City;
        County := PostCodeRec.CountyText;
      END;
    END;

    PROCEDURE LookUpPostCode@8(VAR City@1001 : Text[30];VAR PostCode@1000 : Code[20];VAR County@1005 : Text[30];ReturnValues@1002 : Boolean);
    VAR
      PostCodeRec@1003 : Record 225;
    BEGIN
      IF NOT GUIALLOWED THEN
        EXIT;
      PostCodeRec.SETCURRENTKEY(Code,City);
      PostCodeRec.Code := PostCode;
      PostCodeRec.City := City;
      IF (FORM.RUNMODAL(FORM::"Post Codes",PostCodeRec,PostCodeRec.Code) = ACTION::LookupOK) AND ReturnValues THEN BEGIN
        PostCode := PostCodeRec.Code;
        City := PostCodeRec.City;
        County := PostCodeRec.CountyText;
      END;
    END;

    PROCEDURE CountyText@1100002() : Text[30];
    VAR
      Area@1100000 : Record 284;
    BEGIN
      IF Area.GET("County Code") THEN
        EXIT(Area.Text)
      ELSE
        EXIT('');
    END;

    PROCEDURE GetCountyCode@1100003();
    VAR
      Area@1100000 : Record 284;
      i@1100001 : Integer;
    BEGIN
      "County Code" := '';
      FOR i := STRLEN(Code) DOWNTO 1 DO BEGIN
        Area.SETRANGE("Post Code Prefix",COPYSTR(Code,1,i));
        IF Area.FIND('-') THEN BEGIN
          "County Code" := Area.Code;
          EXIT;
        END;
      END;
    END;

    BEGIN
    END.
  }
}
