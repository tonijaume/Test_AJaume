OBJECT Table 707 BA Db. Dimension Relation
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=BA Db. Dimension Relation;
               ESP=Relaci¢n dimensi¢n BD BA];
  }
  FIELDS
  {
    { 1   ;   ;BA Db. Code         ;Code10        ;TableRelation="BA Database";
                                                   CaptionML=[ENU=BA Db. Code;
                                                              ESP=C¢digo BD de BA] }
    { 2   ;   ;BA Db. Dimension Line No.;Integer  ;CaptionML=[ENU=BA Db. Dimension Line No.;
                                                              ESP=N§ l¡nea de dimensi¢n de BD BA] }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ESP=N§ l¡nea] }
    { 4   ;   ;Relates-from Table No.;Integer     ;OnValidate=VAR
                                                                BADbDim@1001 : Record 706;
                                                                Object@1000 : Record 2000000001;
                                                              BEGIN
                                                                IF "Relates-from Table No." <> 0 THEN BEGIN
                                                                  BADbDim.GET(GETRANGEMIN("BA Db. Code"),GETRANGEMIN("BA Db. Dimension Line No."));
                                                                  BADbDim.TESTFIELD(Type,BADbDim.Type::Standard);
                                                                  BADbDim.TESTFIELD("Standard Type",BADbDim."Standard Type"::Table);
                                                                  Object.GET(Object.Type::Table,'',"Relates-from Table No.");
                                                                END;
                                                                CALCFIELDS("Relates-from Table Name");
                                                              END;

                                                   OnLookup=VAR
                                                              BADbDim@1004 : Record 706;
                                                              BADbDimRel@1003 : Record 707;
                                                              Object@1002 : Record 2000000001;
                                                              Objects@1001 : Form 358;
                                                              TableFilter@1000 : Text[1024];
                                                            BEGIN
                                                              BADbDim.GET(GETRANGEMIN("BA Db. Code"),GETRANGEMIN("BA Db. Dimension Line No."));
                                                              BADbDim.TESTFIELD(Type,BADbDim.Type::Standard);
                                                              BADbDim.TESTFIELD("Standard Type",BADbDim."Standard Type"::Table);
                                                              TableFilter := FORMAT(BADbDim."Table No.");
                                                              BADbDimRel.COPYFILTERS(Rec);
                                                              BADbDimRel.SETFILTER("Line No.",'<>%1',"Line No.");
                                                              IF BADbDimRel.FIND('-') THEN
                                                                REPEAT
                                                                  TableFilter := TableFilter + '|' + FORMAT(BADbDimRel."Relates-to Table No.");
                                                                UNTIL BADbDimRel.NEXT = 0;
                                                              IF "Relates-from Table No." <> 0 THEN BEGIN
                                                                Object.GET(Object.Type::Table,'',"Relates-from Table No.");
                                                                Objects.SETRECORD(Object);
                                                              END;
                                                              Object.SETRANGE(Type,Object.Type::Table);
                                                              Object.SETFILTER(ID,TableFilter);
                                                              Objects.SETTABLEVIEW(Object);
                                                              Objects.LOOKUPMODE := TRUE;
                                                              IF Objects.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                Objects.GETRECORD(Object);
                                                                "Relates-from Table No." := Object.ID;
                                                              END;
                                                              CALCFIELDS("Relates-from Table Name");
                                                            END;

                                                   CaptionML=[ENU=Relates-from Table No.;
                                                              ESP=Relaci¢n desde n§ de tabla] }
    { 5   ;   ;Standard Type       ;Option        ;CaptionML=[ENU=Standard Type;
                                                              ESP=Tipo est ndar];
                                                   OptionCaptionML=[ENU=Table,Numbered List;
                                                                    ESP=Tabla,Lista numerada];
                                                   OptionString=Table,Numbered List }
    { 6   ;   ;Relates-to Table No.;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   OnValidate=VAR
                                                                Object@1000 : Record 2000000001;
                                                              BEGIN
                                                                IF "Relates-to Table No." <> 0 THEN BEGIN
                                                                  TESTFIELD(Type,Type::Standard);
                                                                  TESTFIELD("Standard Type","Standard Type"::Table);
                                                                  Object.GET(Object.Type::Table,'',"Relates-to Table No.");
                                                                END;
                                                                CALCFIELDS("Relates-to Table Name");
                                                              END;

                                                   OnLookup=VAR
                                                              Object@1000 : Record 2000000001;
                                                              Objects@1001 : Form 358;
                                                            BEGIN
                                                              TESTFIELD(Type,Type::Standard);
                                                              TESTFIELD("Standard Type","Standard Type"::Table);
                                                              IF "Relates-to Table No." <> 0 THEN BEGIN
                                                                Object.GET(Object.Type::Table,'',"Relates-to Table No.");
                                                                Objects.SETRECORD(Object);
                                                              END;
                                                              Object.SETRANGE(Type,Object.Type::Table);
                                                              Objects.SETTABLEVIEW(Object);
                                                              Objects.LOOKUPMODE := TRUE;
                                                              IF Objects.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                Objects.GETRECORD(Object);
                                                                VALIDATE("Relates-to Table No.",Object.ID);
                                                              END;
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Relates-to Table No.;
                                                              ESP=Relaci¢n con n§ de tabla] }
    { 7   ;   ;Relates-to Table Name;Text50       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Relates-to Table No.)));
                                                   CaptionML=[ENU=Relates-to Table Name;
                                                              ESP=Relaci¢n con tabla] }
    { 8   ;   ;Numbered List Line No.;Integer     ;OnValidate=VAR
                                                                BADbNumList@1000 : Record 704;
                                                              BEGIN
                                                                IF "Numbered List Line No." <> 0 THEN BEGIN
                                                                  TESTFIELD(Type,Type::Standard);
                                                                  TESTFIELD("Standard Type","Standard Type"::"Numbered List");
                                                                  BADbNumList.GET("BA Db. Code","Numbered List Line No.");
                                                                END;
                                                                CALCFIELDS("Numbered List Name");
                                                              END;

                                                   OnLookup=VAR
                                                              BADbNumList@1000 : Record 704;
                                                              BADbNumLists@1001 : Form 709;
                                                            BEGIN
                                                              TESTFIELD(Type,Type::Standard);
                                                              TESTFIELD("Standard Type","Standard Type"::"Numbered List");
                                                              IF "Numbered List Line No." <> 0 THEN BEGIN
                                                                BADbNumList.GET("BA Db. Code","Numbered List Line No.");
                                                                BADbNumLists.SETRECORD(BADbNumList);
                                                              END;
                                                              BADbNumList.SETRANGE("BA Db. Code","BA Db. Code");
                                                              BADbNumLists.SETTABLEVIEW(BADbNumList);
                                                              BADbNumLists.LOOKUPMODE := TRUE;
                                                              IF BADbNumLists.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                BADbNumLists.GETRECORD(BADbNumList);
                                                                "Numbered List Line No." := BADbNumList."Line No.";
                                                              END;
                                                              CALCFIELDS("Numbered List Name");
                                                            END;

                                                   CaptionML=[ENU=Numbered List Line No.;
                                                              ESP=N§ de l¡nea en lista numerada] }
    { 9   ;   ;Numbered List Name  ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("BA Db. Numbered List".Name WHERE (BA Db. Code=FIELD(BA Db. Code),
                                                                                                         Line No.=FIELD(Numbered List Line No.)));
                                                   CaptionML=[ENU=Numbered List Name;
                                                              ESP=Nombre de la lista numerada] }
    { 10  ;   ;Relates-from Field Numbers;Text50  ;OnValidate=VAR
                                                                BAFieldSelect@1000 : Form 720;
                                                              BEGIN
                                                                TESTFIELD("Relates-from Table No.");
                                                                BAFieldSelect.SetFieldList("Relates-from Table No.","Relates-from Field Numbers");
                                                                "Relates-from Field Numbers" := BAFieldSelect.GetFieldList;
                                                              END;

                                                   OnLookup=VAR
                                                              Field@1001 : Record 2000000041;
                                                              BAFieldSelect@1000 : Form 720;
                                                            BEGIN
                                                              TESTFIELD("Relates-from Table No.");
                                                              Field.SETRANGE(TableNo,"Relates-from Table No.");
                                                              Field.SETFILTER(Type,'<>%1',Field.Type::Decimal);
                                                              BAFieldSelect.SETTABLEVIEW(Field);
                                                              BAFieldSelect.SetFieldList("Relates-from Table No.","Relates-from Field Numbers");
                                                              BAFieldSelect.LOOKUPMODE := TRUE;
                                                              IF BAFieldSelect.RUNMODAL = ACTION::LookupOK THEN
                                                                "Relates-from Field Numbers" := BAFieldSelect.GetFieldList;
                                                            END;

                                                   CaptionML=[ENU=Relates-from Field Numbers;
                                                              ESP=N£meros de campo relacionados] }
    { 11  ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              ESP=Tipo];
                                                   OptionCaptionML=[ENU=Standard,Time;
                                                                    ESP=Est ndar,Hora];
                                                   OptionString=Standard,Time }
    { 12  ;   ;Relates-from Table Name;Text50     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Relates-from Table No.)));
                                                   CaptionML=[ENU=Relates-from Table Name;
                                                              ESP=Relaci¢n desde tabla] }
  }
  KEYS
  {
    {    ;BA Db. Code,BA Db. Dimension Line No.,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    BEGIN
    END.
  }
}
