OBJECT Table 7009844 Temporal Porcentaje EBB
{
  OBJECT-PROPERTIES
  {
    Date=29/10/12;
    Time=18:15:14;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Hotel EBB Percentage;
               ESP=Porcentaje EBB Hotel];
  }
  FIELDS
  {
    { 1   ;   ;Delegacion          ;Code10        ;TableRelation=Delegacion.Codigo;
                                                   CaptionML=[ENU=Local Office;
                                                              ESP=Delegacion];
                                                   Description=PK, FK Delegacion;
                                                   Editable=No }
    { 3   ;   ;N§ Contrato         ;Code10        ;CaptionML=[ENU=Contract No.;
                                                              ESP=N§ Contrato];
                                                   Description=PK;
                                                   Editable=No }
    { 5   ;   ;Tipo Garantia       ;Option        ;CaptionML=[ENU=Guarantee type;
                                                              ESP=Tipo Garantia];
                                                   OptionCaptionML=[ENU=Amount,Room;
                                                                    ESP=Importe,Habitacion];
                                                   OptionString=Importe,Habitacion;
                                                   Description=PK;
                                                   Editable=No }
    { 7   ;   ;Num Orden           ;Integer       ;CaptionML=[ENU=Order No.;
                                                              ESP=Num Orden];
                                                   Description=PK;
                                                   Editable=No }
    { 9   ;   ;Fecha venta desde   ;Date          ;CaptionML=[ENU=Sale date from;
                                                              ESP=Fecha venta desde];
                                                   NotBlank=Yes }
    { 11  ;   ;Fecha venta hasta   ;Date          ;CaptionML=[ENU=Sale date to;
                                                              ESP=Fecha venta hasta];
                                                   NotBlank=Yes }
    { 13  ;   ;Tipo Aplicacion     ;Option        ;CaptionML=[ENU=Application type;
                                                              ESP=Tipo Aplicacion];
                                                   OptionCaptionML=[ENU=Daily,Period;
                                                                    ESP=Diaria,Periodo];
                                                   OptionString=Diaria,Periodo }
    { 15  ;   ;Habitacion desde    ;Code10        ;CaptionML=[ENU=Room from;
                                                              ESP=Habitacion desde] }
    { 17  ;   ;Habitacion hasta    ;Code10        ;CaptionML=[ENU=Room to;
                                                              ESP=Habitacion hasta] }
    { 19  ;   ;Cantidad Habitaciones;Decimal      ;CaptionML=[ENU=Room quantity;
                                                              ESP=Cantidad Habitaciones] }
    { 21  ;   ;Importe             ;Decimal       ;CaptionML=[ENU=Amount;
                                                              ESP=Importe];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 23  ;   ;Hotel               ;Code20        ;TableRelation=Hotel.Hotel;
                                                   CaptionML=[ENU=Hotel;
                                                              ESP=Hotel];
                                                   Description=FK Hotel;
                                                   Editable=No }
    { 25  ;   ;% EBB               ;Decimal       ;CaptionML=[ENU=% EBB;
                                                              ESP=% EBB] }
    { 27  ;   ;Activo              ;Boolean       ;CaptionML=[ENU=Active;
                                                              ESP=Activo];
                                                   Editable=No }
    { 28  ;   ;Replicado           ;Boolean       ;InitValue=No;
                                                   CaptionML=[ENU=Replicated;
                                                              ESP=Replicado];
                                                   Description=Campo usado por el replicador }
    { 30  ;   ;C¢d. divisa         ;Code10        ;TableRelation=Currency.Code;
                                                   CaptionML=[ENU=Currency exchange;
                                                              ESP=C¢d. divisa] }
    { 31  ;   ;Fecha llegada desde ;Date          ;CaptionML=[ENU=Arrival date from;
                                                              ESP=Fecha llegada desde] }
    { 32  ;   ;Fecha llegada hasta ;Date          ;CaptionML=[ENU=Arrival date to;
                                                              ESP=Fecha llegada hasta] }
    { 33  ;   ;Fecha pago          ;Date          ;CaptionML=[ENU=Payment date;
                                                              ESP=Fecha pago] }
    { 34  ;   ;Descripcion         ;Text200       ;CaptionML=[ENU=Description;
                                                              ESP=Descripcion] }
    { 35  ;   ;Fecha creacion      ;DateTime      ;CaptionML=[ENU=Creation Date;
                                                              ESP=Fecha creacion] }
    { 36  ;   ;Usuario creacion    ;Code20        ;TableRelation=User."User ID";
                                                   CaptionML=[ENU=Creation user;
                                                              ESP=Usuario creacion];
                                                   Description=User."User ID" }
    { 37  ;   ;Revisado            ;Boolean       ;CaptionML=[ENU=Reviewed;
                                                              ESP=Revisado] }
    { 200 ;   ;Usuario             ;Code20        ;CaptionML=[ENU=User;
                                                              ESP=Usuario];
                                                   Description=PK }
  }
  KEYS
  {
    {    ;Usuario,Delegacion,N§ Contrato,Tipo Garantia,Num Orden;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    LOCAL PROCEDURE _RellenarTemporal@1100253000() rvOK : Boolean;
    VAR
      lrPorEBB@1100253000 : Record 7009729;
    BEGIN
      // _RellenarTemporal.

      _TraerOfertasPrecio;

      rvOK := FALSE;

      CLEAR(Rec);
      SETRANGE (Usuario, USERID);
      IF NOT ISEMPTY THEN
        DELETEALL;

      CLEAR(lrPorEBB);
      lrPorEBB.SETCURRENTKEY("Fecha venta hasta", Revisado);
      lrPorEBB.SETFILTER("Fecha venta hasta", '..%1', TODAY);
      lrPorEBB.SETRANGE (Revisado, FALSE);

      IF lrPorEBB.FINDSET THEN BEGIN
        rvOK := TRUE;
        REPEAT

          CLEAR(Rec);
          TRANSFERFIELDS(lrPorEBB, FALSE);
          Usuario         := USERID;
          Delegacion      := lrPorEBB.Delegacion;
          "N§ Contrato"   := lrPorEBB."N§ Contrato";
          "Tipo Garantia" := lrPorEBB."Tipo Garantia";
          "Num Orden"     := lrPorEBB."Num Orden";
          INSERT;

        UNTIL lrPorEBB.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE _MarcarRevisados@1100253013();
    VAR
      lrPorEBB@1100253000 : Record 7009729;
    BEGIN
      // _MarcarRevisados.

      CLEAR(Rec);
      SETRANGE (Usuario, USERID);
      IF FINDSET THEN BEGIN
        REPEAT
          CLEAR(lrPorEBB);
          lrPorEBB.SETRANGE (Delegacion     , Delegacion);
          lrPorEBB.SETRANGE ("N§ Contrato"  , "N§ Contrato");
          lrPorEBB.SETRANGE ("Tipo Garantia", "Tipo Garantia");
          lrPorEBB.SETRANGE ("Num Orden"    , "Num Orden");
          IF lrPorEBB.FINDFIRST THEN BEGIN
            IF Revisado THEN BEGIN
              lrPorEBB.Revisado := Revisado;
              lrPorEBB.MODIFY;
            END;
          END;
        UNTIL NEXT = 0;
      END;
    END;

    PROCEDURE AbrirVentanaControl@1100253024();
    VAR
      lrUserSetup@1100253000 : Record 91;
    BEGIN
      // AbrirVentanaControl.

      CLEAR(lrUserSetup);
      lrUserSetup.SETRANGE("User ID", USERID);
      IF lrUserSetup.FINDFIRST THEN BEGIN
        IF lrUserSetup."Control Porcentajes EBB" THEN BEGIN
          IF _RellenarTemporal THEN BEGIN
            COMMIT;
            Rec.SETRANGE(Revisado, FALSE); //$003
            FORM.RUNMODAL(7009967, Rec);
            COMMIT;
            _MarcarRevisados;
          END;
        END;
      END;
    END;

    LOCAL PROCEDURE _TraerOfertasPrecio@1100253001();
    VAR
      lrPorEBB@1100253000 : Record 7009729;
      lrPrecios@1100253001 : Record 7009738;
    BEGIN
      // TraerOfertasPrecio

      //. Recorrer las ofertas de precios de compra cuya fecha hasta de venta no se haya superado

      lrPrecios.RESET;
      lrPrecios.SETRANGE ("Codigo Precio" , lrPrecios."Codigo Precio"::Ofertas);
      lrPrecios.SETRANGE ("Subtipo Oferta", lrPrecios."Subtipo Oferta"::EBB);
      lrPrecios.SETFILTER("Fecha hasta"   , '>=%1', TODAY);
      lrPrecios.SETRANGE (Activo          , TRUE);
      IF lrPrecios.FINDSET THEN
      BEGIN
        REPEAT
          IF NOT lrPorEBB.GET(lrPrecios.Delegacion,
                              lrPrecios."N§ Contrato",
                              lrPorEBB."Tipo Garantia"::Importe,
                              lrPrecios."Orden Precio") THEN
          BEGIN
            lrPorEBB.Delegacion            := lrPrecios.Delegacion;
            lrPorEBB."N§ Contrato"         := lrPrecios."N§ Contrato";
            lrPorEBB."Tipo Garantia"       := lrPorEBB."Tipo Garantia"::Importe;
            lrPorEBB."Num Orden"           := lrPrecios."Orden Precio";
            lrPorEBB."Fecha venta desde"   := lrPrecios."Fecha desde";
            lrPorEBB."Fecha venta hasta"   := lrPrecios."Fecha hasta";
            lrPorEBB."Fecha llegada desde" := lrPrecios."2¦ Fecha desde";
            lrPorEBB."Fecha llegada hasta" := lrPrecios."2¦ Fecha hasta";
            lrPorEBB.INSERT;
          END;
        UNTIL lrPrecios.NEXT = 0;
      END;

      COMMIT; //+$002
    END;

    BEGIN
    {
      $001 AJS 03022012 Rellenamos la tabla de porcentajes de EBB a partir de la informacion que hay en las ofertas
                        de los contratos

      $002 AJS 16022012 Agrego un commit para que no de error al crear los nuevos porcentajes EBB

      $003 AJS 03042012 Filtrar por los no revisados antes de llamar a la ventana de control

      $004 AJS 29102012 Modifico las propiedades LookupFormID y DrillDownFormID para dejarlas en blanco
    }
    END.
  }
}
