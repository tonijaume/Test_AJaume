OBJECT Table 5715 Item Substitution
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               IF Interchangeable THEN
                 IF CONFIRM(Text001 + Text002) THEN
                   DeleteInterchangeableItem(Type,"No.","Variant Code","Substitute Type","Substitute No.","Substitute Variant Code")
                 ELSE
                   IF ItemSub.GET(
                     "Substitute Type",
                     "Substitute No.",
                     "Substitute Variant Code",
                     Type,
                     "No.",
                     "Variant Code") THEN BEGIN
                   ItemSub.Interchangeable := FALSE;
                   ItemSub.MODIFY;
                   END;

               IF Condition THEN BEGIN
                 SubCondition.SETRANGE(Type,Type);
                 SubCondition.SETRANGE("No.","No.");
                 SubCondition.SETRANGE("Variant Code","Variant Code");
                 SubCondition.SETRANGE("Substitute Type","Substitute Type");
                 SubCondition.SETRANGE("Substitute No.","Substitute No.");
                 SubCondition.SETRANGE("Substitute Variant Code","Substitute Variant Code");
                 SubCondition.DELETEALL;
               END;
             END;

    CaptionML=[ENU=Item Substitution;
               ESP=Sustituci¢n producto];
    LookupFormID=Form5716;
    DrillDownFormID=Form5716;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST(Item)) Item.No.
                                                                 ELSE IF (Type=CONST(Nonstock Item)) "Nonstock Item"."Entry No.";
                                                   OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  "Variant Code" := '';
                                                                  IF Interchangeable THEN
                                                                    DeleteInterchangeableItem(
                                                                      Type,
                                                                      xRec."No.",
                                                                      "Variant Code",
                                                                      "Substitute Type",
                                                                      "Substitute No.",
                                                                      "Substitute Variant Code");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              ESP=N§];
                                                   NotBlank=Yes }
    { 2   ;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(No.));
                                                   OnValidate=BEGIN
                                                                IF "Variant Code" <> xRec."Variant Code" THEN
                                                                  RecreateSubstEntry(xRec."Variant Code","Substitute Variant Code");
                                                              END;

                                                   CaptionML=[ENU=Variant Code;
                                                              ESP=C¢d. variante] }
    { 3   ;   ;Substitute No.      ;Code20        ;TableRelation=IF (Substitute Type=CONST(Item)) Item.No.
                                                                 ELSE IF (Substitute Type=CONST(Nonstock Item)) "Nonstock Item"."Entry No.";
                                                   OnValidate=BEGIN
                                                                IF (Type = "Substitute Type") AND
                                                                   ("No." = "Substitute No.") AND
                                                                   ("Variant Code" = "Substitute Variant Code")
                                                                THEN
                                                                  ERROR(Text000);

                                                                IF "Substitute No." <> xRec."Substitute No." THEN
                                                                  IF Interchangeable THEN
                                                                    DeleteInterchangeableItem(
                                                                      Type,
                                                                      "No.",
                                                                      "Variant Code",
                                                                      "Substitute Type",
                                                                      xRec."Substitute No.",
                                                                      "Substitute Variant Code");

                                                                IF "Substitute Type" = "Substitute Type"::"Nonstock Item" THEN BEGIN
                                                                  IF NonStockItem.GET("Substitute No.") THEN
                                                                    Description := NonStockItem.Description;
                                                                END ELSE
                                                                  Description := GetItemVariantDescription;
                                                              END;

                                                   CaptionML=[ENU=Substitute No.;
                                                              ESP=N§ sustitutivo];
                                                   NotBlank=Yes }
    { 4   ;   ;Substitute Variant Code;Code10     ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Substitute No.));
                                                   OnValidate=BEGIN
                                                                IF (Type = "Substitute Type") AND
                                                                   ("No." = "Substitute No.") AND
                                                                   ("Variant Code" = "Substitute Variant Code")
                                                                THEN
                                                                  ERROR(Text000);

                                                                IF "Substitute Variant Code" <> xRec."Substitute Variant Code" THEN
                                                                  RecreateSubstEntry("Variant Code",xRec."Substitute Variant Code");

                                                                Description := GetItemVariantDescription;
                                                              END;

                                                   CaptionML=[ENU=Substitute Variant Code;
                                                              ESP=C¢d. variante sustitutivo] }
    { 5   ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n];
                                                   Editable=No }
    { 6   ;   ;Inventory           ;Decimal       ;CaptionML=[ENU=Inventory;
                                                              ESP=Existencias];
                                                   DecimalPlaces=0:5 }
    { 7   ;   ;Interchangeable     ;Boolean       ;OnValidate=BEGIN
                                                                TESTFIELD("No.");
                                                                TESTFIELD("Substitute No.");
                                                                IF NOT Interchangeable THEN
                                                                  DeleteInterchangeableItem(Type,"No.","Variant Code","Substitute Type","Substitute No.","Substitute Variant Code")
                                                                ELSE
                                                                  CreateInterchangeableItem;
                                                              END;

                                                   CaptionML=[ENU=Interchangeable;
                                                              ESP=Intercambiable] }
    { 8   ;   ;Condition           ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Substitution Condition" WHERE (Type=FIELD(Type),
                                                                                                     No.=FIELD(No.),
                                                                                                     Variant Code=FIELD(Variant Code),
                                                                                                     Substitute Type=FIELD(Substitute Type),
                                                                                                     Substitute No.=FIELD(Substitute No.),
                                                                                                     Substitute Variant Code=FIELD(Substitute Variant Code)));
                                                   CaptionML=[ENU=Condition;
                                                              ESP=Condici¢n];
                                                   Editable=No }
    { 9   ;   ;Location Filter     ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation=Location;
                                                   CaptionML=[ENU=Location Filter;
                                                              ESP=Filtro almac‚n] }
    { 100 ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF Type <> xRec.Type THEN BEGIN
                                                                  IF Interchangeable THEN
                                                                    DeleteInterchangeableItem(
                                                                      xRec.Type,
                                                                      "No.",
                                                                      "Variant Code",
                                                                      "Substitute Type",
                                                                      "Substitute No.",
                                                                      "Substitute Variant Code");
                                                                  VALIDATE("No.",'');
                                                                  VALIDATE("Substitute No.",'');
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              ESP=Tipo];
                                                   OptionCaptionML=[ENU=Item,Nonstock Item;
                                                                    ESP=Prod.,Prod. sin stock];
                                                   OptionString=Item,Nonstock Item }
    { 101 ;   ;Substitute Type     ;Option        ;OnValidate=BEGIN
                                                                IF (Type = "Substitute Type") AND
                                                                   ("No." = "Substitute No.") AND
                                                                   ("Variant Code" = "Substitute Variant Code")
                                                                THEN
                                                                  ERROR(Text000);

                                                                IF "Substitute Type" <> xRec."Substitute Type" THEN BEGIN
                                                                  IF Interchangeable THEN
                                                                    DeleteInterchangeableItem(
                                                                      Type,
                                                                      "No.",
                                                                      "Variant Code",
                                                                      xRec."Substitute Type",
                                                                      "Substitute No.",
                                                                      "Substitute Variant Code");
                                                                  Description := '';
                                                                  "Variant Code" := '';
                                                                  "Substitute No." := '';
                                                                  Interchangeable := FALSE;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Substitute Type;
                                                              ESP=Tipo sustitutivo];
                                                   OptionCaptionML=[ENU=Item,Nonstock Item;
                                                                    ESP=Prod.,Prod. sin stock];
                                                   OptionString=Item,Nonstock Item }
    { 102 ;   ;Sub. Item No.       ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Nonstock Item"."Item No." WHERE (Entry No.=FIELD(Substitute No.)));
                                                   CaptionML=[ENU=Sub. Item No.;
                                                              ESP=N§ prod. sustitutivo];
                                                   Editable=No }
    { 103 ;   ;Relations Level     ;Integer       ;CaptionML=[ENU=Relations Level;
                                                              ESP=Nivel relaciones];
                                                   Editable=No }
    { 104 ;   ;Quantity Avail. on Shpt. Date;Decimal;
                                                   CaptionML=[ENU=Quantity Avail. on Shpt. Date;
                                                              ESP=Cdad. disponible a fecha env¡o];
                                                   DecimalPlaces=0:5 }
    { 105 ;   ;Shipment Date       ;Date          ;CaptionML=[ENU=Shipment Date;
                                                              ESP=Fecha env¡o] }
  }
  KEYS
  {
    {    ;Type,No.,Variant Code,Substitute Type,Substitute No.,Substitute Variant Code;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You can not set up an item to be substituted by itself.;ESP=Un producto no puede ser sustituido por ‚l mismo.';
      Text001@1001 : TextConst 'ENU=This substitute is interchangeable. \;ESP=Este prod. sustitutivo es intercambiable. \';
      Text002@1002 : TextConst 'ENU=Do you want to delete the corresponding substitute?;ESP=¨Confirma que desea borrar el sustitutivo correspondiente?';
      Item@1005 : Record 27;
      ItemSub@1006 : Record 5715;
      SubCondition@1007 : Record 5716;
      NonStockItem@1008 : Record 5718;
      ItemVariant@1003 : Record 5401;

    PROCEDURE CreateInterchangeableItem@1();
    BEGIN
      ItemSub.Type := "Substitute Type";
      ItemSub."No." := "Substitute No.";
      ItemSub."Variant Code" := "Substitute Variant Code";
      ItemSub."Substitute Type" := Type;
      ItemSub."Substitute No." := "No.";
      ItemSub."Substitute Variant Code" := "Variant Code";
      IF Type = Type::"Nonstock Item" THEN BEGIN
        NonStockItem.GET("No.");
        ItemSub.Description := NonStockItem.Description;
      END ELSE BEGIN
        Item.GET("No.");
        ItemSub.Description := Item.Description;
      END;
      ItemSub.Interchangeable := TRUE;
      IF ItemSub.FIND THEN
        ItemSub.MODIFY
      ELSE
        ItemSub.INSERT;
    END;

    PROCEDURE DeleteInterchangeableItem@2(XType@1000 : Integer;XNo@1001 : Code[20];XVariantCode@1002 : Code[10];XSubstType@1003 : Integer;XSubstNo@1004 : Code[20];XSubstVariantCode@1005 : Code[10]);
    BEGIN
      ItemSub.Type := XSubstType;
      ItemSub."No." := XSubstNo;
      ItemSub."Variant Code" := XSubstVariantCode;
      ItemSub."Substitute Type" := XType;
      ItemSub."Substitute No." := XNo;
      ItemSub."Substitute Variant Code" := XVariantCode;
      IF ItemSub.FIND THEN BEGIN
        ItemSub.CALCFIELDS(ItemSub.Condition);
        IF ItemSub.Condition THEN BEGIN
          SubCondition.SETRANGE(Type,XType);
          SubCondition.SETRANGE("No.",XNo);
          SubCondition.SETRANGE("Variant Code",XVariantCode);
          SubCondition.SETRANGE("Substitute Type",XSubstType);
          SubCondition.SETRANGE("Substitute No.",XSubstNo);
          SubCondition.SETRANGE("Substitute Variant Code",XSubstVariantCode);
          SubCondition.DELETEALL;
        END;
        ItemSub.DELETE;
        Interchangeable := FALSE;
      END;
    END;

    PROCEDURE RecreateSubstEntry@3(XVariantCode@1000 : Code[10];XSubstVariantCode@1001 : Code[10]);
    BEGIN
      IF xRec.Interchangeable THEN
        DeleteInterchangeableItem(
          xRec.Type,
          xRec."No.",
          XVariantCode,
          xRec."Substitute Type",
          xRec."Substitute No.",
          XSubstVariantCode);
    END;

    PROCEDURE GetItemVariantDescription@4() : Text[30];
    BEGIN
      IF "Substitute Variant Code" <> '' THEN
        IF ItemVariant.GET("Substitute No.","Substitute Variant Code") THEN
          IF ItemVariant.Description <> '' THEN
            EXIT(ItemVariant.Description);

      IF Item.GET("Substitute No.") THEN
        EXIT(Item.Description);
    END;

    BEGIN
    END.
  }
}
