OBJECT Table 708 BA Db. Dimension Level
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=BA Db. Dimension Level;
               ESP=Nivel dimensi¢n BD BA];
  }
  FIELDS
  {
    { 1   ;   ;BA Db. Code         ;Code10        ;TableRelation="BA Database";
                                                   CaptionML=[ENU=BA Db. Code;
                                                              ESP=C¢digo BD de BA] }
    { 2   ;   ;BA Db. Dimension Line No.;Integer  ;CaptionML=[ENU=BA Db. Dimension Line No.;
                                                              ESP=N§ l¡nea de dimensi¢n de BD BA] }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ESP=N§ l¡nea] }
    { 4   ;   ;Standard Type       ;Option        ;CaptionML=[ENU=Standard Type;
                                                              ESP=Tipo est ndar];
                                                   OptionCaptionML=[ENU=Table,Numbered List;
                                                                    ESP=Tabla,Lista numerada];
                                                   OptionString=Table,Numbered List }
    { 5   ;   ;Table No.           ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   OnValidate=VAR
                                                                Object@1000 : Record 2000000001;
                                                              BEGIN
                                                                TESTFIELD("Standard Type","Standard Type"::Table);
                                                                IF "Table No." <> 0 THEN
                                                                  Object.GET(Object.Type::Table,'',"Table No.");
                                                                IF "Table No." <> xRec."Table No." THEN
                                                                  "Field No." := 0;
                                                                CALCFIELDS("Table Name");
                                                              END;

                                                   OnLookup=VAR
                                                              Object@1000 : Record 2000000001;
                                                              Objects@1001 : Form 358;
                                                            BEGIN
                                                              TESTFIELD("Standard Type","Standard Type"::Table);
                                                              IF "Table No." <> 0 THEN BEGIN
                                                                Object.GET(Object.Type::Table,'',"Table No.");
                                                                Objects.SETRECORD(Object);
                                                              END;
                                                              Object.SETRANGE(Type,Object.Type::Table);
                                                              Objects.SETTABLEVIEW(Object);
                                                              Objects.LOOKUPMODE := TRUE;
                                                              IF Objects.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                Objects.GETRECORD(Object);
                                                                "Table No." := Object.ID;
                                                              END;
                                                              IF "Table No." <> xRec."Table No." THEN
                                                                "Field No." := 0;
                                                              CALCFIELDS("Table Name");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Table No.;
                                                              ESP=Tabla n§] }
    { 6   ;   ;Field No.           ;Integer       ;OnValidate=VAR
                                                                Field@1000 : Record 2000000041;
                                                              BEGIN
                                                                IF "Field No." <> 0 THEN BEGIN
                                                                  TESTFIELD("Table No.");
                                                                  Field.GET("Table No.","Field No.");
                                                                END;
                                                                CALCFIELDS("Field Name");
                                                              END;

                                                   OnLookup=VAR
                                                              Field@1000 : Record 2000000041;
                                                              Fields@1001 : Form 7702;
                                                            BEGIN
                                                              TESTFIELD("Table No.");
                                                              IF "Field No." <> 0 THEN BEGIN
                                                                Field.GET("Table No.","Field No.");
                                                                Fields.SETRECORD(Field);
                                                              END;
                                                              Field.SETRANGE(TableNo,"Table No.");
                                                              Field.SETRANGE(Class,Field.Class::Normal);
                                                              Field.SETFILTER(Type,'<>%1',Field.Type::Decimal);
                                                              Fields.SETTABLEVIEW(Field);
                                                              Fields.LOOKUPMODE := TRUE;
                                                              IF Fields.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                Fields.GETRECORD(Field);
                                                                VALIDATE("Field No.",Field."No.");
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Field No.;
                                                              ESP=N§ campo] }
    { 7   ;   ;Numbered List Line No.;Integer     ;TableRelation="BA Db. Numbered List"."Line No." WHERE (BA Db. Code=FIELD(BA Db. Code));
                                                   OnValidate=BEGIN
                                                                TESTFIELD("Standard Type","Standard Type"::"Numbered List");
                                                                CALCFIELDS("Numbered List Name");
                                                              END;

                                                   OnLookup=VAR
                                                              BAEnum@1000 : Record 704;
                                                              BAEnums@1001 : Form 709;
                                                            BEGIN
                                                              TESTFIELD("Standard Type","Standard Type"::"Numbered List");
                                                              BAEnum.SETRANGE("BA Db. Code","BA Db. Code");
                                                              IF "Numbered List Line No." <> 0 THEN
                                                               IF BAEnum.GET("BA Db. Code","Numbered List Line No.") THEN;
                                                              BAEnums.SETRECORD(BAEnum);
                                                              BAEnums.SETTABLEVIEW(BAEnum);
                                                              BAEnums.LOOKUPMODE := TRUE;
                                                              IF BAEnums.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                BAEnums.GETRECORD(BAEnum);
                                                                "Numbered List Line No." := BAEnum."Line No.";
                                                              END;
                                                              CALCFIELDS("Numbered List Name");
                                                            END;

                                                   CaptionML=[ENU=Numbered List Line No.;
                                                              ESP=N§ de l¡nea en lista numerada] }
    { 8   ;   ;Table Name          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table No.)));
                                                   CaptionML=[ENU=Table Name;
                                                              ESP=Nombre tabla];
                                                   Editable=No }
    { 9   ;   ;Field Name          ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Table No.),
                                                                                                   No.=FIELD(Field No.)));
                                                   CaptionML=[ENU=Field Name;
                                                              ESP=Nombre de campo];
                                                   Editable=No }
    { 10  ;   ;Numbered List Name  ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("BA Db. Numbered List".Name WHERE (BA Db. Code=FIELD(BA Db. Code),
                                                                                                         Line No.=FIELD(Numbered List Line No.)));
                                                   CaptionML=[ENU=Numbered List Name;
                                                              ESP=Nombre de la lista numerada];
                                                   Editable=No }
    { 11  ;   ;Name                ;Text30        ;OnValidate=BEGIN
                                                                IF Name <> '' THEN BEGIN
                                                                  IF NameExists THEN
                                                                    ERROR(Text001,FIELDCAPTION(Name));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Name;
                                                              ESP=Nombre] }
    { 12  ;   ;Field Numbers       ;Text50        ;OnValidate=VAR
                                                                BAFieldSelect@1000 : Form 720;
                                                              BEGIN
                                                                TESTFIELD("Standard Type","Standard Type"::Table);
                                                                BAFieldSelect.SetFieldList("Table No.","Field Numbers");
                                                                "Field Numbers" := BAFieldSelect.GetFieldList;
                                                              END;

                                                   OnLookup=VAR
                                                              Field@1000 : Record 2000000041;
                                                              BAFieldSelect@1001 : Form 720;
                                                            BEGIN
                                                              TESTFIELD("Standard Type","Standard Type"::Table);
                                                              TESTFIELD("Table No.");
                                                              Field.SETRANGE(TableNo,"Table No.");
                                                              Field.SETFILTER(Type,'<>%1',Field.Type::Decimal);
                                                              BAFieldSelect.SETTABLEVIEW(Field);
                                                              BAFieldSelect.SetFieldList("Table No.","Field Numbers");
                                                              BAFieldSelect.LOOKUPMODE := TRUE;
                                                              IF BAFieldSelect.RUNMODAL = ACTION::LookupOK THEN
                                                                "Field Numbers" := BAFieldSelect.GetFieldList;
                                                            END;

                                                   CaptionML=[ENU=Field Numbers;
                                                              ESP=N£meros de campo] }
  }
  KEYS
  {
    {    ;BA Db. Code,BA Db. Dimension Line No.,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=%1 must be unique within a dimension.;ESP=%1 debe ser £nico dentro de una dimensi¢n.';

    PROCEDURE NameExists@1() : Boolean;
    VAR
      BADbDimLevel@1000 : Record 708;
    BEGIN
      BADbDimLevel.SETRANGE("BA Db. Code","BA Db. Code");
      BADbDimLevel.SETRANGE("BA Db. Dimension Line No.","BA Db. Dimension Line No.");
      BADbDimLevel.SETRANGE(Name,Name);
      EXIT(NOT BADbDimLevel.ISEMPTY);
    END;

    BEGIN
    END.
  }
}
