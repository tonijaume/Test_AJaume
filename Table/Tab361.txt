OBJECT Table 361 G/L Budget Dimension
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Dimension Value Code");
               CheckIfBlocked;
               UpdateDimFields("Dimension Value Code");
             END;

    OnModify=BEGIN
               CheckIfBlocked;
               UpdateDimFields("Dimension Value Code");
             END;

    OnDelete=BEGIN
               CheckIfBlocked;
               UpdateDimFields('');
             END;

    CaptionML=[ENU=G/L Budget Dimension;
               ESP=Dimensi¢n presupuesto];
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              ESP=N§ mov.] }
    { 2   ;   ;Dimension Code      ;Code20        ;TableRelation=Dimension;
                                                   CaptionML=[ENU=Dimension Code;
                                                              ESP=C¢d. dimensi¢n] }
    { 3   ;   ;Dimension Value Code;Code20        ;TableRelation="Dimension Value".Code WHERE (Dimension Code=FIELD(Dimension Code));
                                                   CaptionML=[ENU=Dimension Value Code;
                                                              ESP=C¢d. valor dimensi¢n] }
  }
  KEYS
  {
    {    ;Entry No.,Dimension Code                ;Clustered=Yes }
    {    ;Dimension Code,Dimension Value Code     ;KeyGroups=Dim(Setup) }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      GLSetup@1000 : Record 98;
      GLSetupRetrieved@1001 : Boolean;

    PROCEDURE CheckIfBlocked@1();
    VAR
      GLBudgetName@1000 : Record 95;
      GLBudgetEntry@1001 : Record 96;
    BEGIN
      IF GLBudgetEntry.GET("Entry No.") THEN
        IF GLBudgetName.GET(GLBudgetEntry."Budget Name") THEN
          GLBudgetName.TESTFIELD(Blocked,FALSE);
    END;

    LOCAL PROCEDURE UpdateDimFields@2(NewDimValue@1000 : Code[20]);
    VAR
      Modified@1001 : Boolean;
      GLBudgetName@1002 : Record 95;
      GLBudgetEntry@1003 : Record 96;
    BEGIN
      IF "Dimension Code" = '' THEN
        EXIT;
      IF NOT GLSetupRetrieved THEN BEGIN
        GLSetup.GET;
        GLSetupRetrieved := TRUE;
      END;

      IF GLBudgetEntry.GET("Entry No.") THEN BEGIN
        CASE "Dimension Code" OF
          GLSetup."Global Dimension 1 Code":
            BEGIN
              GLBudgetEntry."Global Dimension 1 Code" := NewDimValue;
              Modified := TRUE;
            END;
          GLSetup."Global Dimension 2 Code":
            BEGIN
              GLBudgetEntry."Global Dimension 2 Code" := NewDimValue;
              Modified := TRUE;
            END;
        END;
        IF GLBudgetName.GET(GLBudgetEntry."Budget Name") THEN BEGIN
          CASE "Dimension Code" OF
            GLBudgetName."Budget Dimension 1 Code":
              BEGIN
                GLBudgetEntry."Budget Dimension 1 Code" := NewDimValue;
                Modified := TRUE;
              END;
            GLBudgetName."Budget Dimension 2 Code":
              BEGIN
                GLBudgetEntry."Budget Dimension 2 Code" := NewDimValue;
                Modified := TRUE;
              END;
            GLBudgetName."Budget Dimension 3 Code":
              BEGIN
                GLBudgetEntry."Budget Dimension 3 Code" := NewDimValue;
                Modified := TRUE;
              END;
            GLBudgetName."Budget Dimension 4 Code":
              BEGIN
                GLBudgetEntry."Budget Dimension 4 Code" := NewDimValue;
                Modified := TRUE;
              END;
          END;
        END;
        IF Modified THEN BEGIN
          GLBudgetEntry."User ID" := USERID;
          GLBudgetEntry.MODIFY;
        END;
      END;
    END;

    BEGIN
    END.
  }
}
