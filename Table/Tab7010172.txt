OBJECT Table 7010172 Suplemento Compra Guia Oficial
{
  OBJECT-PROPERTIES
  {
    Date=13/05/14;
    Time=17:44:47;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               _CheckCosteTicket; //+$004

               IF rSer.GET(Delegacion, "N§ Servicio") THEN BEGIN
                 IF rSer.Estado = rSer.Estado::Anulada THEN
                   ERROR(Text000);
                 IF rSer.Estado = rSer.Estado::Confirmada THEN
                   _RecalcularServicio(FALSE); //+$005

                 //-$005  MESSAGE(Text001);
                 //-$005rSer."Pendiente Recalcular" := TRUE;
                 //-$005rSer.MODIFY;

                 Excursion := rSer.Excursion;
               END;
             END;

    OnModify=BEGIN
               IF rSer.GET(Delegacion, "N§ Servicio") THEN BEGIN
                 IF rSer.Estado = rSer.Estado::Anulada THEN
                   ERROR(Text000);
                 IF rSer.Estado = rSer.Estado::Confirmada THEN
                   _RecalcularServicio(FALSE); //+$005

                 //-$005  MESSAGE(Text001);
                 //-$005rSer."Pendiente Recalcular" := TRUE;
                 //-$005rSer.MODIFY;
               END;
             END;

    OnDelete=BEGIN
               IF rSer.GET(Delegacion, "N§ Servicio") THEN BEGIN
                 IF rSer.Estado = rSer.Estado::Anulada THEN
                   ERROR(Text000);
                 IF rSer.Estado = rSer.Estado::Confirmada THEN
                   _RecalcularServicio(TRUE); //+$005

                 //-$005  MESSAGE(Text001);
                 //-$005rSer."Pendiente Recalcular" := TRUE;
                 //-$005rSer.MODIFY;
               END;
             END;

    OnRename=BEGIN
               //+$006 <
               IF rSer.GET(Delegacion, "N§ Servicio") THEN
               BEGIN
                 IF rSer.Estado = rSer.Estado::Anulada THEN
                   ERROR(Text000);
                 IF rSer.Estado = rSer.Estado::Confirmada THEN
                   _RecalcularServicio(TRUE);
               END;
               //+$006 >
             END;

    CaptionML=[ENU=Official Guide purchase supplement;
               ESP=Suplemento Compra Guia Oficial];
  }
  FIELDS
  {
    { 1   ;   ;Delegacion          ;Code10        ;TableRelation=Delegacion.Codigo;
                                                   CaptionML=[ENU=Local Office;
                                                              ESP=Delegacion];
                                                   Description=PK, FK Delegacion;
                                                   Editable=No }
    { 3   ;   ;N§ Servicio         ;Integer       ;TableRelation="Servicio Excursion"."N§ Servicio";
                                                   CaptionML=[ENU=Service No.;
                                                              ESP=N§ Servicio];
                                                   Description=PK, FK Servicio Excursion }
    { 5   ;   ;Codigo Suplemento   ;Code10        ;TableRelation="Suplemento Guia Oficial"."Codigo Suplemento";
                                                   OnLookup=VAR
                                                              lrPrecio@1100217001 : Record 7010154;
                                                              lrSupl@1100217000 : Record 7010145;
                                                            BEGIN
                                                              //+$001

                                                              //. Limpiamos las marcas sobre la variable de codigos de suplemento
                                                              lrSupl.CLEARMARKS;

                                                              //. Buscamos el servicio para obtener la fecha de aplicacion
                                                              rSer.GET(Delegacion, "N§ Servicio");

                                                              //. Recorremos los precios de suplementos para marcar los codigos
                                                              lrPrecio.RESET;
                                                              lrPrecio.SETRANGE (Delegacion     , Delegacion);
                                                              lrPrecio.SETRANGE ("N§ Contrato"  , "Contrato Compra");
                                                              lrPrecio.SETRANGE ("Tipo precio", lrPrecio."Tipo precio"::Suplemento);
                                                              lrPrecio.SETFILTER("Fecha desde"  , '<=%1', rSer.Fecha);
                                                              lrPrecio.SETFILTER("Fecha hasta"  , '>=%1', rSer.Fecha);
                                                              IF lrPrecio.FINDSET THEN
                                                              BEGIN
                                                                REPEAT
                                                                  IF lrSupl.GET(lrPrecio."Codigo Suplemento") THEN
                                                                    lrSupl.MARK(TRUE);
                                                                UNTIL lrPrecio.NEXT = 0;
                                                              END;

                                                              lrSupl.MARKEDONLY(TRUE);
                                                              IF FORM.RUNMODAL(0, lrSupl) = ACTION::LookupOK THEN
                                                                "Codigo Suplemento" := lrSupl."Codigo Suplemento";
                                                            END;

                                                   CaptionML=[ENU=Supplement code;
                                                              ESP=Codigo Suplemento];
                                                   NotBlank=Yes;
                                                   Description=PK, FK Suplemento Guia Oficial }
    { 6   ;   ;Contrato Compra     ;Code10        ;TableRelation="Contrato Compra Excursiones"."N§ Contrato compra" WHERE (Tipo Contrato=CONST(Guia Oficial));
                                                   CaptionML=[ENU=Purchate contract;
                                                              ESP=Contrato compra];
                                                   NotBlank=Yes;
                                                   Description=PK, FK Contrato compra excursion }
    { 7   ;   ;Duracion            ;Option        ;CaptionML=[ENU=Length;
                                                              ESP=Duracion];
                                                   OptionCaptionML=[ENU=Whole day,Half day,Night,Long term;
                                                                    ESP=Dia Entero,Medio Dia,Nocturna,Larga Duracion];
                                                   OptionString=Dia Entero,Medio Dia,Nocturna,Larga Duracion }
    { 9   ;   ;Zona                ;Code10        ;TableRelation="Zona fisica"."Zona Fisica";
                                                   CaptionML=[ENU=Location;
                                                              ESP=Zona];
                                                   Description=FK Zona fisica }
    { 11  ;   ;Excursion           ;Code10        ;TableRelation="Producto agencia".Codigo WHERE (Tipo Producto=CONST(Excursion));
                                                   OnValidate=BEGIN
                                                                // $001
                                                                cFunExc.ControlOficinaExcursion(Excursion, TRUE);
                                                              END;

                                                   CaptionML=[ENU=Excursion;
                                                              ESP=Excursion];
                                                   Description=FK Producto Agencia }
  }
  KEYS
  {
    {    ;Delegacion,N§ Servicio,Codigo Suplemento,Contrato Compra,Zona;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1103355001 : TextConst 'ENU=Supplements of the cancelled excursions cannot be modified;ESP=No se pueden modificar suplementos de excursiones anuladas.';
      Text001@1103355002 : TextConst 'ENU=You just modify supplements of a confirmed excursion, remember you must calculate the service again;ESP=Acaba de modificar suplementos de una excursion confirmada, recuerde que tiene que recalcular el servicio.';
      rSer@1103355000 : Record 7010163;
      cFunExc@1100217000 : Codeunit 7010141;
      Text003@1100217001 : TextConst 'ENU=Supplements cannot be made by service if the calculation is per ticket.;ESP=No se pueden introducir suplementos por servicio si el calculo es por ticket.';

    LOCAL PROCEDURE _CheckCosteTicket@1100217007();
    VAR
      lrSer@1100217000 : Record 7010163;
      lrExc@1100217001 : Record 7009710;
    BEGIN
      lrSer.GET(Delegacion, "N§ Servicio");

      lrExc.GET(lrSer.Excursion);

      IF lrExc."Coste x ticket" THEN
        ERROR(Text003);
    END;

    LOCAL PROCEDURE _RecalcularServicio@1100217001(pwBorrado@1100217000 : Boolean);
    BEGIN
      IF NOT pwBorrado THEN
      BEGIN
        IF ("Codigo Suplemento" = '') OR
           ("Contrato Compra" = '') OR
           (Zona = '') THEN
          EXIT;
      END;
      cFunExc.Recalculo_Costes(rSer, TRUE); //+$008
    END;

    BEGIN
    {
      $001 JPT 08/08/12 CNT-OC-12004 Incluir filtro por oficina en las excursiones. Control de Oficina en Excursion

      $002 AJS 12092012 Mostrar solo los codigos de suplemento que tienen precio en el contrato

      $003 AJS 13022013 Hacer que los campos Contrato Compra y Codigo suplemento no se puedan dejar en blanco

      $004 AJS 17062013 Nueva funcion _CheckCosteTicket para controlar que no se puedan meter costes por servicio
                        en las excursiones que se calculan por ticket

      $005 AJS 17062013 Si se introduce sobre un servicio confirmado hay que recalcular

      $006 AJS 07082013 Corregido error de variable precios en el lookup de codigo suplemento

      $007 AJS 26092013 Pongo codigo en OnRename para que haga los mismo que el OnModify

      $008 AJS 13052014 Nuevo parametro en funcion de calcular servicios
    }
    END.
  }
}
