OBJECT Table 325 VAT Posting Setup
{
  OBJECT-PROPERTIES
  {
    Date=27/11/09;
    Time=12:00:00;
    Version List=NAVW16.00,NAVES4.00,PS55187;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "VAT %" = 0 THEN
                 "VAT %" := GetVATPtc;
             END;

    CaptionML=[ENU=VAT Posting Setup;
               ESP=Config. grupos registro IVA];
    LookupFormID=Form472;
    DrillDownFormID=Form472;
  }
  FIELDS
  {
    { 1   ;   ;VAT Bus. Posting Group;Code10      ;TableRelation="VAT Business Posting Group";
                                                   CaptionML=[ENU=VAT Bus. Posting Group;
                                                              ESP=Grupo registro IVA neg.] }
    { 2   ;   ;VAT Prod. Posting Group;Code10     ;TableRelation="VAT Product Posting Group";
                                                   CaptionML=[ENU=VAT Prod. Posting Group;
                                                              ESP=Grupo registro IVA prod.] }
    { 3   ;   ;VAT Calculation Type;Option        ;OnValidate=BEGIN
                                                                IF (("VAT Calculation Type" = "VAT Calculation Type"::"No Taxable VAT") AND
                                                                    ("VAT+EC %" <>0)) THEN
                                                                      ERROR(Text10700,FIELDCAPTION("VAT+EC %"),FIELDCAPTION("VAT Calculation Type"));
                                                              END;

                                                   CaptionML=[ENU=VAT Calculation Type;
                                                              ESP=Tipo c lculo IVA];
                                                   OptionCaptionML=[ENU=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax,No Taxable VAT;
                                                                    ESP=Normal,Reversi¢n,Total,Impto. venta,No sujeto];
                                                   OptionString=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax,No Taxable VAT }
    { 4   ;   ;VAT+EC %            ;Decimal       ;OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("VAT %"));
                                                                CheckVATIdentifier;
                                                              END;

                                                   CaptionML=[ENU=VAT+EC %;
                                                              ESP=IVA+CE %];
                                                   DecimalPlaces=0:6;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 5   ;   ;Unrealized VAT Type ;Option        ;OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Unrealized VAT Type"));

                                                                IF "Unrealized VAT Type" > 0 THEN BEGIN
                                                                  GLSetup.GET;
                                                                  IF NOT GLSetup."Unrealized VAT" AND NOT GLSetup."Prepayment Unrealized VAT" THEN
                                                                    GLSetup.TESTFIELD("Unrealized VAT",TRUE)
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Unrealized VAT Type;
                                                              ESP=Tipo IVA no realizado];
                                                   OptionCaptionML=[ENU=" ,Percentage,First,Last,First (Fully Paid),Last (Fully Paid)";
                                                                    ESP=" ,Porcentual,Inicial,Final,Inicial (pago completo),Final (pago completo)"];
                                                   OptionString=[ ,Percentage,First,Last,First (Fully Paid),Last (Fully Paid)] }
    { 6   ;   ;Adjust for Payment Discount;Boolean;OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Adjust for Payment Discount"));

                                                                IF "Adjust for Payment Discount" THEN BEGIN
                                                                  GLSetup.GET;
                                                                  GLSetup.TESTFIELD(
                                                                    "Payment Discount Type",GLSetup."Payment Discount Type"::"Adjust for Payment Disc.");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Adjust for Payment Discount;
                                                              ESP=Ajuste para dto. p.p.] }
    { 7   ;   ;Sales VAT Account   ;Code20        ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Sales VAT Account"));

                                                                CheckGLAcc("Sales VAT Account");
                                                              END;

                                                   CaptionML=[ENU=Sales VAT Account;
                                                              ESP=Cta. IVA por pagar] }
    { 8   ;   ;Sales VAT Unreal. Account;Code20   ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Sales VAT Unreal. Account"));

                                                                CheckGLAcc("Sales VAT Unreal. Account");
                                                              END;

                                                   CaptionML=[ENU=Sales VAT Unreal. Account;
                                                              ESP=Cta. IVA por pagar no realizado] }
    { 9   ;   ;Purchase VAT Account;Code20        ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Purchase VAT Account"));

                                                                CheckGLAcc("Purchase VAT Account");
                                                              END;

                                                   CaptionML=[ENU=Purchase VAT Account;
                                                              ESP=Cta. IVA acreditable] }
    { 10  ;   ;Purch. VAT Unreal. Account;Code20  ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Purch. VAT Unreal. Account"));

                                                                CheckGLAcc("Purch. VAT Unreal. Account");
                                                              END;

                                                   CaptionML=[ENU=Purch. VAT Unreal. Account;
                                                              ESP=Cta. IVA acreditable no realizado] }
    { 11  ;   ;Reverse Chrg. VAT Acc.;Code20      ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Reverse Chrg. VAT Acc."));

                                                                CheckGLAcc("Reverse Chrg. VAT Acc.");
                                                              END;

                                                   CaptionML=[ENU=Reverse Chrg. VAT Acc.;
                                                              ESP=Cta. reversi¢n IVA] }
    { 12  ;   ;Reverse Chrg. VAT Unreal. Acc.;Code20;
                                                   TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Reverse Chrg. VAT Unreal. Acc."));

                                                                CheckGLAcc("Reverse Chrg. VAT Unreal. Acc.");
                                                              END;

                                                   CaptionML=[ENU=Reverse Chrg. VAT Unreal. Acc.;
                                                              ESP=Cta. reversi¢n IVA no realiz.] }
    { 13  ;   ;VAT Identifier      ;Code10        ;OnValidate=BEGIN
                                                                "VAT %" := GetVATPtc;
                                                              END;

                                                   CaptionML=[ENU=VAT Identifier;
                                                              ESP=Identific. IVA] }
    { 14  ;   ;EU Service          ;Boolean       ;CaptionML=[ENU=EU Service;
                                                              ESP=Servicio UE] }
    { 10700;  ;EC %                ;Decimal       ;OnValidate=BEGIN
                                                                IF (("VAT Calculation Type" = "VAT Calculation Type"::"No Taxable VAT") AND
                                                                    ("EC %" <> 0)) THEN
                                                                      ERROR(Text1100000,FIELDCAPTION("EC %"),FIELDCAPTION("VAT Calculation Type"));
                                                                "VAT+EC %" := "VAT %" + "EC %";
                                                              END;

                                                   CaptionML=[ENU=EC %;
                                                              ESP=% RE];
                                                   DecimalPlaces=0:6;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 10701;  ;VAT %               ;Decimal       ;OnValidate=BEGIN
                                                                IF (("VAT Calculation Type" = "VAT Calculation Type"::"No Taxable VAT") AND
                                                                    ("VAT %" <> 0)) THEN
                                                                      ERROR(Text1100001,FIELDCAPTION("VAT %"),FIELDCAPTION("VAT Calculation Type"));
                                                                "VAT+EC %" := "VAT %" + "EC %";
                                                              END;

                                                   CaptionML=[ENU=VAT %;
                                                              ESP=% IVA];
                                                   DecimalPlaces=0:6;
                                                   MinValue=0;
                                                   MaxValue=100 }
  }
  KEYS
  {
    {    ;VAT Bus. Posting Group,VAT Prod. Posting Group;
                                                   Clustered=Yes }
    {    ;VAT Prod. Posting Group,VAT Bus. Posting Group }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 must be entered on the tax jurisdiction line when %2 is %3.;ESP=Se debe indicar %1 en l¡nea jurisdicci¢n impuesto cuando %2 es %3.';
      Text001@1001 : TextConst 'ENU="%1 = %2 has already been used for %3 = %4 in %5 for %6 = %7 and %8 = %9.";ESP="%1 = %2 ya ha sido utilizado por %3 = %4 en %5 para %6 = %7 y %8 = %9."';
      GLSetup@1002 : Record 98;
      Text10700@1100000 : TextConst 'ENU=The %1 for %2 No Taxable VAT must be 0.;ESP=El %1 para %2 IVA no sujeto debe ser 0.';
      Text1100000@1100001 : TextConst 'ENU=The %1 for %2 No Taxable VAT must be 0.;ESP=El %1 para %2 IVA no sujeto debe ser 0.';
      Text1100001@1100002 : TextConst 'ENU=The %1 for %2 Not Taxable VAT must be 0.;ESP=El %1 para %2 IVA no sujeto debe ser 0.';

    LOCAL PROCEDURE CheckGLAcc@2(AccNo@1000 : Code[20]);
    VAR
      GLAcc@1001 : Record 15;
    BEGIN
      IF AccNo <> '' THEN BEGIN
        GLAcc.GET(AccNo);
        GLAcc.CheckGLAcc;
      END;
    END;

    PROCEDURE TestNotSalesTax@1(FromFieldName@1000 : Text[100]);
    BEGIN
      IF "VAT Calculation Type" = "VAT Calculation Type"::"Sales Tax" THEN
        ERROR(
          Text000,
          FromFieldName,FIELDCAPTION("VAT Calculation Type"),
          "VAT Calculation Type");
    END;

    LOCAL PROCEDURE CheckVATIdentifier@14();
    VAR
      VATPostingSetup@1000 : Record 325;
    BEGIN
      VATPostingSetup.SETRANGE("VAT Bus. Posting Group","VAT Bus. Posting Group");
      VATPostingSetup.SETFILTER("VAT Prod. Posting Group",'<>%1',"VAT Prod. Posting Group");
      VATPostingSetup.SETFILTER("VAT %",'<>%1',"VAT %");
      VATPostingSetup.SETRANGE("VAT Identifier","VAT Identifier");
      IF VATPostingSetup.FIND('-') THEN
        ERROR(
        Text001,
        FIELDCAPTION("VAT Identifier"),VATPostingSetup."VAT Identifier",
        FIELDCAPTION("VAT %"),VATPostingSetup."VAT %",TABLECAPTION,
        FIELDCAPTION("VAT Bus. Posting Group"),VATPostingSetup."VAT Bus. Posting Group",
        FIELDCAPTION("VAT Prod. Posting Group"),VATPostingSetup."VAT Prod. Posting Group");
    END;

    LOCAL PROCEDURE GetVATPtc@3() : Decimal;
    VAR
      VATPostingSetup@1000 : Record 325;
    BEGIN
      VATPostingSetup.SETRANGE("VAT Bus. Posting Group","VAT Bus. Posting Group");
      VATPostingSetup.SETFILTER("VAT Prod. Posting Group",'<>%1',"VAT Prod. Posting Group");
      VATPostingSetup.SETRANGE("VAT Identifier","VAT Identifier");
      IF NOT VATPostingSetup.FIND('-') THEN
        VATPostingSetup."VAT %" := "VAT %";
      EXIT(VATPostingSetup."VAT %");
    END;

    BEGIN
    END.
  }
}
