OBJECT Table 5088 Profile Questionnaire Line
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Profile Questionnaire Code,Description;
    OnDelete=VAR
               Rating@1000 : Record 5111;
               ProfileQuestionnaireLine@1001 : Record 5088;
             BEGIN
               CALCFIELDS("No. of Contacts");
               TESTFIELD("No. of Contacts",0);

               Rating.SETRANGE("Rating Profile Quest. Code","Profile Questionnaire Code");
               Rating.SETRANGE("Rating Profile Quest. Line No.","Line No.");
               IF Rating.FIND('-') THEN
                 ERROR(Text002);

               Rating.RESET;
               Rating.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
               Rating.SETRANGE("Profile Questionnaire Line No.","Line No.");
               IF Rating.FIND('-') THEN
                 ERROR(Text003,TABLECAPTION);

               IF Type = Type::Question THEN BEGIN
                 ProfileQuestionnaireLine.GET("Profile Questionnaire Code","Line No.");
                 IF (ProfileQuestionnaireLine.NEXT <> 0) AND
                    (ProfileQuestionnaireLine.Type = ProfileQuestnLine.Type::Answer) THEN
                   ERROR(Text004,TABLECAPTION);
               END;
             END;

    CaptionML=[ENU=Profile Questionnaire Line;
               ESP=L¡n. cuestionario perfil];
    LookupFormID=Form5149;
  }
  FIELDS
  {
    { 1   ;   ;Profile Questionnaire Code;Code10  ;TableRelation="Profile Questionnaire Header";
                                                   CaptionML=[ENU=Profile Questionnaire Code;
                                                              ESP=C¢d. cuestionario perfil] }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ESP=N§ l¡nea] }
    { 3   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                CASE Type OF
                                                                  Type::Question:
                                                                    BEGIN
                                                                      CALCFIELDS("No. of Contacts");
                                                                      TESTFIELD("No. of Contacts",0);
                                                                      TESTFIELD("From Value",0);
                                                                      TESTFIELD("To Value",0);
                                                                    END;
                                                                  Type::Answer:
                                                                    BEGIN
                                                                      TESTFIELD("Multiple Answers",FALSE);
                                                                      TESTFIELD("Auto Contact Classification",FALSE);
                                                                      TESTFIELD("Customer Class. Field",0);
                                                                      TESTFIELD("Vendor Class. Field",0);
                                                                      TESTFIELD("Contact Class. Field",0);
                                                                      TESTFIELD("Starting Date Formula","0DF");
                                                                      TESTFIELD("Ending Date Formula","0DF");
                                                                      TESTFIELD("Classification Method",0);
                                                                      TESTFIELD("Sorting Method",0);
                                                                      TESTFIELD("No. of Decimals",0);
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              ESP=Tipo];
                                                   OptionCaptionML=[ENU=Question,Answer;
                                                                    ESP=Pregunta,Respuesta];
                                                   OptionString=Question,Answer }
    { 4   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n];
                                                   NotBlank=Yes }
    { 5   ;   ;Multiple Answers    ;Boolean       ;OnValidate=BEGIN
                                                                IF "Multiple Answers" THEN
                                                                  TESTFIELD(Type,Type::Question);
                                                              END;

                                                   CaptionML=[ENU=Multiple Answers;
                                                              ESP=M£ltiples respuestas] }
    { 6   ;   ;Auto Contact Classification;Boolean;OnValidate=BEGIN
                                                                IF "Auto Contact Classification" THEN
                                                                  TESTFIELD(Type,Type::Question)
                                                                ELSE BEGIN
                                                                  TESTFIELD("Customer Class. Field","Customer Class. Field"::" ");
                                                                  TESTFIELD("Vendor Class. Field","Vendor Class. Field"::" ");
                                                                  TESTFIELD("Contact Class. Field","Contact Class. Field"::" ");
                                                                  TESTFIELD("Starting Date Formula","0DF");
                                                                  TESTFIELD("Ending Date Formula","0DF");
                                                                  TESTFIELD("Classification Method","Classification Method"::" ");
                                                                  TESTFIELD("Sorting Method","Sorting Method"::" ");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Auto Contact Classification;
                                                              ESP=Clasificaci¢n contacto autom.] }
    { 7   ;   ;Customer Class. Field;Option       ;OnValidate=BEGIN
                                                                IF "Customer Class. Field" <> "Customer Class. Field"::" " THEN BEGIN
                                                                  TESTFIELD(Type,Type::Question);
                                                                  CLEAR("Vendor Class. Field");
                                                                  CLEAR("Contact Class. Field");
                                                                  IF "Classification Method" = "Classification Method"::" " THEN
                                                                    "Classification Method" := "Classification Method"::"Defined Value";
                                                                END ELSE
                                                                  ResetFields;
                                                              END;

                                                   CaptionML=[ENU=Customer Class. Field;
                                                              ESP=Campo clasific. cliente];
                                                   OptionCaptionML=[ENU=" ,Sales (LCY),Profit (LCY),Sales Frequency (Invoices/Year),Avg. Invoice Amount (LCY),Discount (%),Avg. Overdue (Day)";
                                                                    ESP=" ,Ventas (DL),Ganancia (DL),Frecuencia vta. (Facturas/A¤o),Imp. fact. promedio (DL),Descuento (%),Vencido promedio (D¡a)"];
                                                   OptionString=[ ,Sales (LCY),Profit (LCY),Sales Frequency (Invoices/Year),Avg. Invoice Amount (LCY),Discount (%),Avg. Overdue (Day)] }
    { 8   ;   ;Vendor Class. Field ;Option        ;OnValidate=BEGIN
                                                                IF "Vendor Class. Field" <> "Vendor Class. Field"::" " THEN BEGIN
                                                                  TESTFIELD(Type,Type::Question);
                                                                  CLEAR("Customer Class. Field");
                                                                  CLEAR("Contact Class. Field");
                                                                  IF "Classification Method" = "Classification Method"::" " THEN
                                                                    "Classification Method" := "Classification Method"::"Defined Value";
                                                                END ELSE
                                                                  ResetFields;
                                                              END;

                                                   CaptionML=[ENU=Vendor Class. Field;
                                                              ESP=Campo clasific. proveedor];
                                                   OptionCaptionML=[ENU=" ,Purchase (LCY),Purchase Frequency (Invoices/Year),Avg. Ticket Size (LCY),Discount (%),Avg. Overdue (Day)";
                                                                    ESP=" ,Compras (DL),Frecuencia compra (Facturas/A¤o),Imp. fact. promedio (DL),Descuento (%),Vencido promedio (D¡a)"];
                                                   OptionString=[ ,Purchase (LCY),Purchase Frequency (Invoices/Year),Avg. Ticket Size (LCY),Discount (%),Avg. Overdue (Day)] }
    { 9   ;   ;Contact Class. Field;Option        ;OnValidate=VAR
                                                                Rating@1000 : Record 5111;
                                                              BEGIN
                                                                IF xRec."Contact Class. Field" = "Contact Class. Field"::Rating THEN BEGIN
                                                                  Rating.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
                                                                  Rating.SETRANGE("Profile Questionnaire Line No.","Line No.");
                                                                  IF Rating.FIND('-') THEN
                                                                    IF CONFIRM(Text000,FALSE) THEN
                                                                      Rating.DELETEALL
                                                                    ELSE
                                                                      ERROR(Text001,FIELDCAPTION("Contact Class. Field"));
                                                                END;

                                                                IF "Contact Class. Field" <> "Contact Class. Field"::" " THEN BEGIN
                                                                  TESTFIELD(Type,Type::Question);
                                                                  CLEAR("Customer Class. Field");
                                                                  CLEAR("Vendor Class. Field");
                                                                  IF ("Classification Method" = "Classification Method"::" ") OR
                                                                     ("Contact Class. Field" = "Contact Class. Field"::Rating)
                                                                  THEN BEGIN
                                                                    "Classification Method" := "Classification Method"::"Defined Value";
                                                                    "Sorting Method" := "Sorting Method"::" ";
                                                                  END;
                                                                  IF "Contact Class. Field" = "Contact Class. Field"::Rating THEN BEGIN
                                                                    CLEAR("Starting Date Formula");
                                                                    CLEAR("Ending Date Formula");
                                                                  END;
                                                                END ELSE
                                                                  ResetFields;
                                                              END;

                                                   CaptionML=[ENU=Contact Class. Field;
                                                              ESP=Campo clasific. contacto];
                                                   OptionCaptionML=[ENU=" ,Interaction Quantity,Interaction Frequency (No./Year),Avg. Interaction Cost (LCY),Avg. Interaction Duration (Min.),Opportunity Won (%),Rating";
                                                                    ESP=" ,Cdad. interacci¢n,Frecuencia interacci¢n (N§/A¤o),Coste interac. promedio (DL),Durac. inter. promedio (Min.),(%) oportunidad ganada,Clasificaci¢n"];
                                                   OptionString=[ ,Interaction Quantity,Interaction Frequency (No./Year),Avg. Interaction Cost (LCY),Avg. Interaction Duration (Min.),Opportunity Won (%),Rating] }
    { 10  ;   ;Starting Date Formula;DateFormula  ;OnValidate=BEGIN
                                                                IF FORMAT("Starting Date Formula") <> '' THEN
                                                                  TESTFIELD(Type,Type::Question);
                                                              END;

                                                   CaptionML=[ENU=Starting Date Formula;
                                                              ESP=F¢rmula fecha inicio] }
    { 11  ;   ;Ending Date Formula ;DateFormula   ;OnValidate=BEGIN
                                                                IF FORMAT("Ending Date Formula") <> '' THEN
                                                                  TESTFIELD(Type,Type::Question);
                                                              END;

                                                   CaptionML=[ENU=Ending Date Formula;
                                                              ESP=F¢rmula fecha fin] }
    { 12  ;   ;Classification Method;Option       ;OnValidate=BEGIN
                                                                IF "Classification Method" <> "Classification Method"::" " THEN BEGIN
                                                                  TESTFIELD(Type,Type::Question);
                                                                  IF "Classification Method" <> "Classification Method"::"Defined Value" THEN
                                                                    "Sorting Method" := ProfileQuestnLine."Sorting Method"::Descending
                                                                  ELSE
                                                                    "Sorting Method" := ProfileQuestnLine."Sorting Method"::" ";
                                                                END ELSE
                                                                  "Sorting Method" := ProfileQuestnLine."Sorting Method"::" ";
                                                              END;

                                                   CaptionML=[ENU=Classification Method;
                                                              ESP=M‚todo clasificaci¢n];
                                                   OptionCaptionML=[ENU=" ,Defined Value,Percentage of Value,Percentage of Contacts";
                                                                    ESP=" ,Valor definido,Porcentaje de valor,Porcentaje de contactos"];
                                                   OptionString=[ ,Defined Value,Percentage of Value,Percentage of Contacts] }
    { 13  ;   ;Sorting Method      ;Option        ;OnValidate=BEGIN
                                                                IF "Sorting Method" <> "Sorting Method"::" " THEN
                                                                  TESTFIELD(Type,Type::Question);
                                                              END;

                                                   CaptionML=[ENU=Sorting Method;
                                                              ESP=M‚todo ordenaci¢n];
                                                   OptionCaptionML=[ENU=" ,Descending,Ascending";
                                                                    ESP=" ,Descendente,Ascendente"];
                                                   OptionString=[ ,Descending,Ascending] }
    { 14  ;   ;From Value          ;Decimal       ;OnValidate=BEGIN
                                                                IF "From Value" <> 0 THEN
                                                                  TESTFIELD(Type,Type::Answer);
                                                              END;

                                                   CaptionML=[ENU=From Value;
                                                              ESP=Desde valor];
                                                   DecimalPlaces=0:;
                                                   BlankZero=Yes }
    { 15  ;   ;To Value            ;Decimal       ;OnValidate=BEGIN
                                                                IF "To Value" <> 0 THEN
                                                                  TESTFIELD(Type,Type::Answer);
                                                              END;

                                                   CaptionML=[ENU=To Value;
                                                              ESP=Hasta valor];
                                                   DecimalPlaces=0:;
                                                   BlankZero=Yes }
    { 16  ;   ;No. of Contacts     ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Contact Profile Answer" WHERE (Profile Questionnaire Code=FIELD(Profile Questionnaire Code),
                                                                                                     Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=No. of Contacts;
                                                              ESP=N§ de contactos];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 17  ;   ;Priority            ;Option        ;InitValue=Normal;
                                                   OnValidate=VAR
                                                                ContProfileAnswer@1000 : Record 5089;
                                                              BEGIN
                                                                TESTFIELD(Type,Type::Answer);
                                                                ContProfileAnswer.SETCURRENTKEY("Profile Questionnaire Code","Line No.");
                                                                ContProfileAnswer.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
                                                                ContProfileAnswer.SETRANGE("Line No.","Line No.");
                                                                ContProfileAnswer.MODIFYALL("Answer Priority",Priority);
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Priority;
                                                              ESP=Prioridad];
                                                   OptionCaptionML=[ENU=Very Low (Hidden),Low,Normal,High,Very High;
                                                                    ESP=Muy baja (Oculta),Baja,Normal,Alta,Muy alta];
                                                   OptionString=Very Low (Hidden),Low,Normal,High,Very High }
    { 18  ;   ;No. of Decimals     ;Integer       ;OnValidate=BEGIN
                                                                IF "No. of Decimals" <> 0 THEN
                                                                  TESTFIELD(Type,Type::Question);
                                                              END;

                                                   CaptionML=[ENU=No. of Decimals;
                                                              ESP=N§ de decimales];
                                                   MinValue=-25;
                                                   MaxValue=25 }
    { 19  ;   ;Min. % Questions Answered;Decimal  ;OnValidate=BEGIN
                                                                IF "Min. % Questions Answered" <> 0 THEN BEGIN
                                                                  TESTFIELD(Type,Type::Question);
                                                                  TESTFIELD("Contact Class. Field","Contact Class. Field"::Rating);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Min. % Questions Answered;
                                                              ESP=% M¡n. preguntas respondidas];
                                                   DecimalPlaces=0:0;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 9501;   ;Wizard Step         ;Option        ;CaptionML=[ENU=Wizard Step;
                                                              ESP=Paso asistente];
                                                   OptionCaptionML=[ENU=" ,1,2,3,4,5,6";
                                                                    ESP=" ,1,2,3,4,5,6"];
                                                   OptionString=[ ,1,2,3,4,5,6];
                                                   Editable=No }
    { 9502;   ;Interval Option     ;Option        ;CaptionML=[ENU=Interval Option;
                                                              ESP=Opci¢n intervalo];
                                                   OptionCaptionML=[ENU=Minimum,Maximum,Interval;
                                                                    ESP=M¡nimo,M ximo,Intervalo];
                                                   OptionString=Minimum,Maximum,Interval }
    { 9503;   ;Answer Option       ;Option        ;CaptionML=[ENU=Answer Option;
                                                              ESP=Opci¢n respuesta];
                                                   OptionCaptionML=[ENU=HighLow,ABC,Custom;
                                                                    ESP=AltaBaja,ABC,Personalizada];
                                                   OptionString=HighLow,ABC,Custom }
    { 9504;   ;Answer Description  ;Text50        ;CaptionML=[ENU=Answer Description;
                                                              ESP=Descripci¢n respuesta] }
    { 9505;   ;Wizard From Value   ;Decimal       ;OnValidate=BEGIN
                                                                 IF "From Value" <> 0 THEN
                                                                   TESTFIELD(Type,Type::Answer);
                                                              END;

                                                   CaptionML=[ENU=Wizard From Value;
                                                              ESP=Asistente desde valor];
                                                   DecimalPlaces=0:;
                                                   BlankZero=Yes }
    { 9506;   ;Wizard To Value     ;Decimal       ;OnValidate=BEGIN
                                                                 IF "To Value" <> 0 THEN
                                                                   TESTFIELD(Type,Type::Answer);
                                                              END;

                                                   CaptionML=[ENU=Wizard To Value;
                                                              ESP=Asistente hasta valor];
                                                   DecimalPlaces=0:;
                                                   BlankZero=Yes }
    { 9707;   ;Wizard From Line No.;Integer       ;OnValidate=BEGIN
                                                                 IF "To Value" <> 0 THEN
                                                                   TESTFIELD(Type,Type::Answer);
                                                              END;

                                                   CaptionML=[ENU=Wizard From Line No.;
                                                              ESP=Asistente desde n§ l¡nea];
                                                   BlankZero=Yes }
  }
  KEYS
  {
    {    ;Profile Questionnaire Code,Line No.     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ProfileQuestnLine@1000 : Record 5088;
      TempProfileLineAnswer@1007 : TEMPORARY Record 5088;
      "0DF"@1001 : DateFormula;
      Text000@1002 : TextConst 'ENU=Do you want to delete the rating values?;ESP=¨Confirma que desea eliminar los valores de clasificaci¢n?';
      Text001@1003 : TextConst 'ENU=%1 cannot be changed until the rating value is deleted.;ESP=No se puede cambiar %1 hasta que se haya eliminado el valor de clasificaci¢n.';
      Text002@1004 : TextConst 'ENU=You cannot delete this line because one or more questions are depending on it.;ESP=No puede eliminar esta l¡nea porque una o m s preguntas dependen de ella.';
      Text003@1005 : TextConst 'ENU=You cannot delete this line because one or more rating values exists.;ESP=No puede eliminar esta l¡nea porque existen uno o m s valores clasificaci¢n.';
      Text004@1006 : TextConst 'ENU=You cannot delete this question while answers exists.;ESP=No puede eliminar esta pregunta porque existen respuestas.';
      Text005@1011 : TextConst 'ENU=Please select for which questionnaire this rating should be created.;ESP=Seleccione el cuestionario para el que debe crearse esta clasificaci¢n.';
      Text006@1010 : TextConst 'ENU=Please describe the rating.;ESP=Describa la clasificaci¢n.';
      Text007@1009 : TextConst 'ENU=Please create one or more different answers.;ESP=Cree una o m s respuestas diferentes.';
      Text008@1008 : TextConst 'ENU=Please enter which range of points this answer should require.;ESP=Introduzca el rango de puntos que necesitar¡a esta respuesta.';
      Text009@1012 : TextConst 'ENU=High;ESP=Alto';
      Text010@1013 : TextConst 'ENU=Low;ESP=Bajo';

    PROCEDURE MoveUp@1();
    VAR
      UpperProfileQuestnLine@1000 : Record 5088;
      LineNo@1001 : Integer;
      UpperRecLineNo@1002 : Integer;
    BEGIN
      TESTFIELD(Type,Type::Answer);
      UpperProfileQuestnLine.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
      LineNo := "Line No.";
      UpperProfileQuestnLine.GET("Profile Questionnaire Code","Line No.");

      IF UpperProfileQuestnLine.FIND('<') AND
         (UpperProfileQuestnLine.Type = UpperProfileQuestnLine.Type::Answer)
      THEN BEGIN
        UpperRecLineNo := UpperProfileQuestnLine."Line No.";
        RENAME("Profile Questionnaire Code",-1);
        UpperProfileQuestnLine.RENAME("Profile Questionnaire Code",LineNo);
        RENAME("Profile Questionnaire Code",UpperRecLineNo);
      END;
    END;

    PROCEDURE MoveDown@2();
    VAR
      LowerProfileQuestnLine@1000 : Record 5088;
      LineNo@1001 : Integer;
      LowerRecLineNo@1002 : Integer;
    BEGIN
      TESTFIELD(Type,Type::Answer);
      LowerProfileQuestnLine.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
      LineNo := "Line No.";
      LowerProfileQuestnLine.GET("Profile Questionnaire Code","Line No.");

      IF LowerProfileQuestnLine.FIND('>') AND
         (LowerProfileQuestnLine.Type = LowerProfileQuestnLine.Type::Answer)
      THEN BEGIN
        LowerRecLineNo := LowerProfileQuestnLine."Line No.";
        RENAME("Profile Questionnaire Code",-1);
        LowerProfileQuestnLine.RENAME("Profile Questionnaire Code",LineNo);
        RENAME("Profile Questionnaire Code",LowerRecLineNo);
      END;
    END;

    PROCEDURE Question@4() : Text[50];
    BEGIN
      ProfileQuestnLine.RESET;
      ProfileQuestnLine.SETRANGE("Profile Questionnaire Code",Rec."Profile Questionnaire Code");
      ProfileQuestnLine.SETFILTER("Line No.",'<%1',Rec."Line No.");
      ProfileQuestnLine.SETRANGE(Type,Type::Question);
      IF ProfileQuestnLine.FIND('+') THEN
        EXIT(ProfileQuestnLine.Description);
    END;

    PROCEDURE FindQuestionLine@3() QuestnLineNo@1003 : Integer;
    VAR
      ProfileQuestnLine@1002 : Record 5088;
    BEGIN
      ProfileQuestnLine.RESET;
      ProfileQuestnLine.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
      ProfileQuestnLine.SETFILTER("Line No.",'<%1',"Line No.");
      ProfileQuestnLine.SETRANGE(Type,Type::Question);
      IF ProfileQuestnLine.FIND('+') THEN
        EXIT(ProfileQuestnLine."Line No.");
    END;

    PROCEDURE ResetFields@5();
    BEGIN
      CLEAR("Starting Date Formula");
      CLEAR("Ending Date Formula");
      "Classification Method" := "Classification Method"::" ";
      "Sorting Method" := "Sorting Method"::" ";
      "No. of Decimals" := 0;
      "Min. % Questions Answered" := 0;
    END;

    PROCEDURE CreateRatingFromProfQuestnLine@6(VAR ProfileQuestnLine@1001 : Record 5088);
    VAR
      TempProfileQuestionnaireLine@1000 : TEMPORARY Record 5088;
    BEGIN
      INIT;
      "Profile Questionnaire Code" := ProfileQuestnLine."Profile Questionnaire Code";
      StartWizard;
    END;

    PROCEDURE StartWizard@16();
    BEGIN
      "Wizard Step" := "Wizard Step"::"1";
      VALIDATE("Auto Contact Classification",TRUE);
      VALIDATE("Contact Class. Field","Contact Class. Field"::Rating);
      INSERT;

      ValidateAnswerOption;
      ValidateIntervalOption;

      FORM.RUNMODAL(FORM::"Create Rating",Rec);
    END;

    PROCEDURE CheckStatus@9();
    BEGIN
      CASE "Wizard Step" OF
        "Wizard Step"::"1":
          BEGIN
            IF "Profile Questionnaire Code" = '' THEN
              ERROR(Text005);
            IF Description = '' THEN
              ERROR(Text006);
          END;
        "Wizard Step"::"2":
          BEGIN
            IF TempProfileLineAnswer.COUNT = 0 THEN
              ERROR(Text007);
          END;
        "Wizard Step"::"3":
          IF ("Wizard From Value" = 0) AND ("Wizard To Value" = 0) THEN
            ERROR(Text008);
      END;
    END;

    PROCEDURE PerformNextWizardStatus@8();
    BEGIN
      CASE "Wizard Step" OF
        "Wizard Step"::"1":
          "Wizard Step" := "Wizard Step" + 1;
        "Wizard Step"::"2":
          BEGIN
            "Wizard From Line No." := 0;
            "Wizard Step" := "Wizard Step" + 1;
            TempProfileLineAnswer.SETRANGE("Line No.");
            TempProfileLineAnswer.FIND('-');
            SetIntervalOption;
          END;
        "Wizard Step"::"3":
          BEGIN
            TempProfileLineAnswer.SETFILTER("Line No.",'%1..',"Wizard From Line No.");
            TempProfileLineAnswer.FIND('-');
            TempProfileLineAnswer."From Value" := "Wizard From Value";
            TempProfileLineAnswer."To Value" := "Wizard To Value";
            TempProfileLineAnswer.MODIFY;
            IF TempProfileLineAnswer.NEXT <> 0 THEN BEGIN
              TempProfileLineAnswer.SETRANGE("Line No.",TempProfileLineAnswer."Line No.");
              "Wizard From Line No." := TempProfileLineAnswer."Line No.";
              "Wizard From Value" := TempProfileLineAnswer."From Value";
              "Wizard To Value" := TempProfileLineAnswer."To Value";
              SetIntervalOption;
            END ELSE BEGIN
              TempProfileLineAnswer.SETRANGE("Line No.");
              TempProfileLineAnswer.FIND('-');
              "Wizard Step" := "Wizard Step" + 1;
            END;
          END;
      END;
    END;

    PROCEDURE PerformPrevWizardStatus@17();
    BEGIN
      CASE "Wizard Step" OF
        "Wizard Step"::"3":
          BEGIN
            TempProfileLineAnswer.SETFILTER("Line No.",'..%1',"Wizard From Line No.");
            IF TempProfileLineAnswer.FIND('+') THEN BEGIN
              TempProfileLineAnswer."From Value" := "Wizard From Value";
              TempProfileLineAnswer."To Value" := "Wizard To Value";
              TempProfileLineAnswer.MODIFY;
            END;
            IF TempProfileLineAnswer.NEXT(-1) <> 0 THEN BEGIN
              "Wizard From Line No." := TempProfileLineAnswer."Line No.";
              "Wizard From Value" := TempProfileLineAnswer."From Value";
              "Wizard To Value" := TempProfileLineAnswer."To Value";
              SetIntervalOption
            END ELSE BEGIN
              TempProfileLineAnswer.SETRANGE("Line No.");
              TempProfileLineAnswer.FIND('-');
              "Wizard Step" := "Wizard Step" - 1;
            END;
          END;
        ELSE
          "Wizard Step" := "Wizard Step" - 1;
      END;
    END;

    PROCEDURE FinishWizard@7() : Boolean;
    VAR
      ProfileQuestionnaireLine@1003 : Record 5088;
      NextLineNo@1002 : Integer;
      QuestionLineNo@1001 : Integer;
      ProfileManagement@1000 : Codeunit 5059;
    BEGIN
      // RENAME(ProfileQuestionnaireCode);

      ProfileQuestionnaireLine.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
      IF ProfileQuestionnaireLine.FIND('+') THEN
        QuestionLineNo := ProfileQuestionnaireLine."Line No." + 10000
      ELSE
        QuestionLineNo := 10000;

      ProfileQuestionnaireLine := Rec;
      ProfileQuestionnaireLine."Line No." := QuestionLineNo;
      ProfileQuestionnaireLine.INSERT(TRUE);

      NextLineNo := QuestionLineNo;
      IF TempProfileLineAnswer.FIND('-') THEN
        REPEAT
          NextLineNo := NextLineNo + 10000;
          ProfileQuestionnaireLine := TempProfileLineAnswer;
          ProfileQuestionnaireLine."Profile Questionnaire Code" := "Profile Questionnaire Code";
          ProfileQuestionnaireLine."Line No." := NextLineNo;
          ProfileQuestionnaireLine.INSERT(TRUE);
        UNTIL TempProfileLineAnswer.NEXT = 0;

      COMMIT;

      ProfileQuestionnaireLine.GET("Profile Questionnaire Code",QuestionLineNo);
      ProfileManagement.ShowAnswerPoints(ProfileQuestionnaireLine);
    END;

    PROCEDURE SetIntervalOption@10();
    BEGIN
      CASE TRUE OF
        (TempProfileLineAnswer."From Value" = 0) AND (TempProfileLineAnswer."To Value" <> 0):
          "Interval Option" := "Interval Option"::Maximum;
        (TempProfileLineAnswer."From Value" <> 0) AND (TempProfileLineAnswer."To Value" = 0):
          "Interval Option" := "Interval Option"::Minimum
        ELSE
          "Interval Option" := "Interval Option"::Interval
      END;

      ValidateIntervalOption;
    END;

    PROCEDURE ValidateIntervalOption@14();
    BEGIN
      TempProfileLineAnswer.SETFILTER("Line No.",'%1..',"Wizard From Line No.");
      TempProfileLineAnswer.FIND('-');
      IF "Interval Option" = "Interval Option"::Minimum THEN
        TempProfileLineAnswer."To Value" := 0;
      IF "Interval Option" = "Interval Option"::Maximum THEN
        TempProfileLineAnswer."From Value" := 0;
      TempProfileLineAnswer.MODIFY;
    END;

    PROCEDURE ValidateAnswerOption@13();
    BEGIN
      IF "Answer Option" = "Answer Option"::Custom THEN
        EXIT;

      TempProfileLineAnswer.DELETEALL;

      CASE "Answer Option" OF
        "Answer Option"::HighLow:
          BEGIN
            CreateAnswer(Text009);
            CreateAnswer(Text010);
          END;
        "Answer Option"::ABC:
          BEGIN
            CreateAnswer('A');
            CreateAnswer('B');
            CreateAnswer('C');
          END;
      END;
    END;

    PROCEDURE CreateAnswer@15(AnswerDescription@1000 : Text[50]);
    BEGIN
      TempProfileLineAnswer.INIT;
      TempProfileLineAnswer."Line No." := (TempProfileLineAnswer.COUNT + 1) * 10000;
      TempProfileLineAnswer.Type := TempProfileLineAnswer.Type::Answer;
      TempProfileLineAnswer.Description := AnswerDescription;
      TempProfileLineAnswer.INSERT;
    END;

    PROCEDURE NoOfProfileAnswers@11() : Decimal;
    BEGIN
      EXIT(TempProfileLineAnswer.COUNT);
    END;

    PROCEDURE ShowAnswers@12();
    VAR
      TempProfileLineAnswer2@1000 : TEMPORARY Record 5088;
    BEGIN
      IF "Answer Option" <> "Answer Option"::Custom THEN
        IF TempProfileLineAnswer.FIND('-') THEN
          REPEAT
            TempProfileLineAnswer2 := TempProfileLineAnswer;
            TempProfileLineAnswer2.INSERT;
          UNTIL TempProfileLineAnswer.NEXT = 0;

      FORM.RUNMODAL(FORM::"Rating Answers",TempProfileLineAnswer);

      IF "Answer Option" <> "Answer Option"::Custom THEN
        IF TempProfileLineAnswer.COUNT <> TempProfileLineAnswer2.COUNT THEN
          "Answer Option" := "Answer Option"::Custom
        ELSE BEGIN
          IF TempProfileLineAnswer.FIND('-') THEN
            REPEAT
              IF NOT TempProfileLineAnswer2.GET(
                TempProfileLineAnswer."Profile Questionnaire Code",TempProfileLineAnswer."Line No.")
              THEN
                "Answer Option" := "Answer Option"::Custom
              ELSE
                IF TempProfileLineAnswer.Description <> TempProfileLineAnswer2.Description THEN
                  "Answer Option" := "Answer Option"::Custom
            UNTIL (TempProfileLineAnswer.NEXT = 0) OR ("Answer Option" = "Answer Option"::Custom);
        END;
    END;

    PROCEDURE GetProfileLineAnswerDesc@18() : Text[100];
    BEGIN
      TempProfileLineAnswer.SETFILTER("Line No.",'%1..',"Wizard From Line No.");
      TempProfileLineAnswer.FIND('-');
      EXIT(TempProfileLineAnswer.Description);
    END;

    PROCEDURE GetAnswers@19(VAR ProfileLineAnswer@1000 : Record 5088);
    BEGIN
      TempProfileLineAnswer.RESET;
      ProfileLineAnswer.RESET;
      ProfileLineAnswer.DELETEALL;
      IF TempProfileLineAnswer.FIND('-') THEN
        REPEAT
          ProfileLineAnswer.INIT;
          ProfileLineAnswer := TempProfileLineAnswer;
          ProfileLineAnswer.INSERT;
        UNTIL TempProfileLineAnswer.NEXT = 0;
    END;

    BEGIN
    END.
  }
}
