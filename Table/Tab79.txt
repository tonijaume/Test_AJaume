OBJECT Table 79 Company Information
{
  OBJECT-PROPERTIES
  {
    Date=21/11/14;
    Time=12:00:00;
    Modified=Yes;
    Version List=AIC2009,TRF2009,NAVW16.00.10,NAVES4.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Company Information;
               ESP=Informaci¢n empresa];
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Code10        ;CaptionML=[ENU=Primary Key;
                                                              ESP=Clave primaria] }
    { 2   ;   ;Name                ;Text50        ;OnValidate=BEGIN
                                                                "Payment Days Code" := COPYSTR(Name,1,10);
                                                                "Non-Paymt. Periods Code" := COPYSTR(Name,1,10);
                                                              END;

                                                   CaptionML=[ENU=Name;
                                                              ESP=Nombre] }
    { 3   ;   ;Name 2              ;Text50        ;CaptionML=[ENU=Name 2;
                                                              ESP=Nombre 2] }
    { 4   ;   ;Address             ;Text50        ;CaptionML=[ENU=Address;
                                                              ESP=Direcci¢n] }
    { 5   ;   ;Address 2           ;Text50        ;CaptionML=[ENU=Address 2;
                                                              ESP=Direcci¢n 2] }
    { 6   ;   ;City                ;Text30        ;OnValidate=BEGIN
                                                                PostCode.ValidateCity(City,"Post Code",County);
                                                              END;

                                                   OnLookup=BEGIN
                                                              PostCode.LookUpCity(City,"Post Code",County,TRUE);
                                                            END;

                                                   CaptionML=[ENU=City;
                                                              ESP=Poblaci¢n] }
    { 7   ;   ;Phone No.           ;Text30        ;ExtendedDatatype=Phone No.;
                                                   CaptionML=[ENU=Phone No.;
                                                              ESP=Nß telÇfono] }
    { 8   ;   ;Phone No. 2         ;Text30        ;ExtendedDatatype=Phone No.;
                                                   CaptionML=[ENU=Phone No. 2;
                                                              ESP=Nß telÇfono 2] }
    { 9   ;   ;Telex No.           ;Text30        ;CaptionML=[ENU=Telex No.;
                                                              ESP=Nß tÇlex] }
    { 10  ;   ;Fax No.             ;Text30        ;CaptionML=[ENU=Fax No.;
                                                              ESP=Nß fax] }
    { 11  ;   ;Giro No.            ;Text20        ;CaptionML=[ENU=Giro No.;
                                                              ESP=Nß giro postal] }
    { 12  ;   ;Bank Name           ;Text50        ;CaptionML=[ENU=Bank Name;
                                                              ESP=Nombre banco] }
    { 13  ;   ;Bank Branch No.     ;Text20        ;CaptionML=[ENU=Bank Branch No.;
                                                              ESP=C¢d. sucursal banco] }
    { 14  ;   ;Bank Account No.    ;Text30        ;OnValidate=BEGIN
                                                                TESTFIELD("CCC Bank Account No.",'');
                                                              END;

                                                   CaptionML=[ENU=Bank Account No.;
                                                              ESP=C¢d. cuenta banco] }
    { 15  ;   ;Payment Routing No. ;Text20        ;CaptionML=[ENU=Payment Routing No.;
                                                              ESP=C¢d. forma pago base] }
    { 17  ;   ;Customs Permit No.  ;Text10        ;CaptionML=[ENU=Customs Permit No.;
                                                              ESP=Nß licencia aduana] }
    { 18  ;   ;Customs Permit Date ;Date          ;CaptionML=[ENU=Customs Permit Date;
                                                              ESP=Fecha licencia aduana] }
    { 19  ;   ;VAT Registration No.;Text20        ;OnValidate=VAR
                                                                VATRegNoFormat@1000 : Record 381;
                                                              BEGIN
                                                                VATRegNoFormat.Test("VAT Registration No.","Country/Region Code",'',DATABASE::"Company Information");
                                                              END;

                                                   CaptionML=[ENU=VAT Registration No.;
                                                              ESP=CIF/NIF] }
    { 20  ;   ;Registration No.    ;Text20        ;CaptionML=[ENU=Registration No.;
                                                              ESP=Nß reg. mercantil] }
    { 21  ;   ;Telex Answer Back   ;Text20        ;CaptionML=[ENU=Telex Answer Back;
                                                              ESP=Nß tÇlex respuesta] }
    { 22  ;   ;Ship-to Name        ;Text50        ;CaptionML=[ENU=Ship-to Name;
                                                              ESP=Env°o a-Nombre] }
    { 23  ;   ;Ship-to Name 2      ;Text50        ;CaptionML=[ENU=Ship-to Name 2;
                                                              ESP=Env°o a-Nombre 2] }
    { 24  ;   ;Ship-to Address     ;Text50        ;CaptionML=[ENU=Ship-to Address;
                                                              ESP=Env°o a-Direcci¢n] }
    { 25  ;   ;Ship-to Address 2   ;Text50        ;CaptionML=[ENU=Ship-to Address 2;
                                                              ESP=Env°o a-Direcci¢n 2] }
    { 26  ;   ;Ship-to City        ;Text30        ;OnValidate=BEGIN
                                                                PostCode.ValidateCity("Ship-to City","Ship-to Post Code","Ship-to County");
                                                              END;

                                                   OnLookup=BEGIN
                                                              PostCode.LookUpCity("Ship-to City","Ship-to Post Code","Ship-to County",TRUE);
                                                            END;

                                                   CaptionML=[ENU=Ship-to City;
                                                              ESP=Env°o a-Poblaci¢n] }
    { 27  ;   ;Ship-to Contact     ;Text50        ;CaptionML=[ENU=Ship-to Contact;
                                                              ESP=Env°o a-Atenci¢n] }
    { 28  ;   ;Location Code       ;Code10        ;TableRelation=Location WHERE (Use As In-Transit=CONST(No));
                                                   CaptionML=[ENU=Location Code;
                                                              ESP=C¢d. almacÇn] }
    { 29  ;   ;Picture             ;BLOB          ;CaptionML=[ENU=Picture;
                                                              ESP=Imagen];
                                                   SubType=Bitmap }
    { 30  ;   ;Post Code           ;Code20        ;TableRelation="Post Code";
                                                   OnValidate=BEGIN
                                                                PostCode.ValidatePostCode(City,"Post Code",County);
                                                              END;

                                                   OnLookup=BEGIN
                                                              PostCode.LookUpPostCode(City,"Post Code",County,TRUE);
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Post Code;
                                                              ESP=C.P.] }
    { 31  ;   ;County              ;Text30        ;CaptionML=[ENU=County;
                                                              ESP=Provincia] }
    { 32  ;   ;Ship-to Post Code   ;Code20        ;TableRelation="Post Code";
                                                   OnValidate=BEGIN
                                                                PostCode.ValidatePostCode("Ship-to City","Ship-to Post Code","Ship-to County");
                                                              END;

                                                   OnLookup=BEGIN
                                                              PostCode.LookUpPostCode("Ship-to City","Ship-to Post Code","Ship-to County",TRUE);
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Ship-to Post Code;
                                                              ESP=Env°o a-C.P.] }
    { 33  ;   ;Ship-to County      ;Text30        ;CaptionML=[ENU=Ship-to County;
                                                              ESP=Env°o a-Provincia] }
    { 34  ;   ;E-Mail              ;Text80        ;ExtendedDatatype=E-Mail;
                                                   CaptionML=[ENU=E-Mail;
                                                              ESP=Correo electr¢nico] }
    { 35  ;   ;Home Page           ;Text80        ;ExtendedDatatype=URL;
                                                   CaptionML=[ENU=Home Page;
                                                              ESP=P†gina Web] }
    { 36  ;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   CaptionML=[ENU=Country/Region Code;
                                                              ESP=C¢d. pa°s/regi¢n] }
    { 37  ;   ;Ship-to Country/Region Code;Code10 ;TableRelation=Country/Region;
                                                   CaptionML=[ENU=Ship-to Country/Region Code;
                                                              ESP=Env°o a-C¢d. pa°s/regi¢n] }
    { 38  ;   ;IBAN                ;Code50        ;OnValidate=BEGIN
                                                                CheckIBAN(IBAN);
                                                              END;

                                                   CaptionML=[ENU=IBAN;
                                                              ESP=IBAN] }
    { 39  ;   ;SWIFT Code          ;Code20        ;CaptionML=[ENU=SWIFT Code;
                                                              ESP=C¢digo SWIFT] }
    { 40  ;   ;Industrial Classification;Text30   ;CaptionML=[ENU=Industrial Classification;
                                                              ESP=C¢d. CNAE] }
    { 41  ;   ;IC Partner Code     ;Code20        ;CaptionML=[ENU=IC Partner Code;
                                                              ESP=C¢digo socio IC] }
    { 42  ;   ;IC Inbox Type       ;Option        ;OnValidate=BEGIN
                                                                IF "IC Inbox Type" = "IC Inbox Type"::Database THEN
                                                                  "IC Inbox Details" := '';
                                                              END;

                                                   CaptionML=[ENU=IC Inbox Type;
                                                              ESP=Tipo buz¢n IC];
                                                   OptionCaptionML=[ENU=File Location,Database;
                                                                    ESP=Ubicaci¢n del archivo,Base de datos];
                                                   OptionString=File Location,Database }
    { 43  ;   ;IC Inbox Details    ;Text250       ;OnLookup=VAR
                                                              CmDlgMgt@1001 : Codeunit 412;
                                                              i@1000 : Integer;
                                                              FileName@1002 : Text[1024];
                                                            BEGIN
                                                              TESTFIELD("IC Partner Code");
                                                              CASE "IC Inbox Type" OF
                                                                "IC Inbox Type"::"File Location":
                                                                  BEGIN
                                                                    IF "IC Inbox Details" = '' THEN BEGIN
                                                                      FileName := STRSUBSTNO('%1.xml',"IC Partner Code");
                                                                      "IC Inbox Details" := CmDlgMgt.OpenFile(Text001,FileName,4,'*.xml|*.xml',1)
                                                                    END ELSE BEGIN
                                                                      FileName := "IC Inbox Details" + STRSUBSTNO('\%1.xml',"IC Partner Code");
                                                                      "IC Inbox Details" := CmDlgMgt.OpenFile(Text001,FileName,4,'*.xml|*.xml',1);
                                                                    END;
                                                                    IF STRLEN("IC Inbox Details") > 4 THEN BEGIN
                                                                      i := STRLEN("IC Inbox Details") - 4;
                                                                      WHILE (i > 1) AND ("IC Inbox Details"[i] <> '\') DO
                                                                        i := i - 1;
                                                                      IF i > 1 THEN
                                                                        "IC Inbox Details" := COPYSTR("IC Inbox Details",1,i - 1);
                                                                    END;
                                                                  END;
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=IC Inbox Details;
                                                              ESP=Detalles buz¢n IC] }
    { 44  ;   ;Abbreviated Name    ;Text4         ;CaptionML=[ENU=Abbreviated Name;
                                                              ESP=Nombre abreviado] }
    { 45  ;   ;Show Abbreviated Name;Boolean      ;CaptionML=[ENU=Show Abbreviated Name;
                                                              ESP=Mostrar nombre abreviado] }
    { 46  ;   ;System Indicator    ;Option        ;CaptionML=[ENU=System Indicator;
                                                              ESP=Indicador del sistema];
                                                   OptionCaptionML=[ENU=None,Custom Text,Company Information,Company,Database,Company+Database;
                                                                    ESP=Ninguno,Texto personalizado,Informaci¢n de la empresa,Empresa,Base de datos,Empresa+base de datos];
                                                   OptionString=None,Custom Text,Company Information,Company,Database,Company+Database }
    { 47  ;   ;Custom System Indicator Text;Text250;
                                                   CaptionML=[ENU=Custom System Indicator Text;
                                                              ESP=Texto de indicador del sistema personalizado] }
    { 48  ;   ;System Indicator Style;Option      ;CaptionML=[ENU=System Indicator Style;
                                                              ESP=Estilo de indicador del sistema];
                                                   OptionCaptionML=[ENU=Standard,Accent1,Accent2,Accent3,Accent4,Accent5,Accent6,Accent7,Accent8,Accent9;
                                                                    ESP=Est†ndar,ênfasis1,ênfasis2,ênfasis3,ênfasis4,ênfasis5,ênfasis6,ênfasis7,ênfasis8,ênfasis9];
                                                   OptionString=Standard,Accent1,Accent2,Accent3,Accent4,Accent5,Accent6,Accent7,Accent8,Accent9 }
    { 5700;   ;Responsibility Center;Code10       ;TableRelation="Responsibility Center".Code;
                                                   ValidateTableRelation=Yes;
                                                   CaptionML=[ENU=Responsibility Center;
                                                              ESP=Centro responsabilidad] }
    { 5791;   ;Check-Avail. Period Calc.;DateFormula;
                                                   CaptionML=[ENU=Check-Avail. Period Calc.;
                                                              ESP=Periodo c†lculo disponib.] }
    { 5792;   ;Check-Avail. Time Bucket;Option    ;CaptionML=[ENU=Check-Avail. Time Bucket;
                                                              ESP=Longitud periodo calc. disp.];
                                                   OptionCaptionML=[ENU=Day,Week,Month,Quarter,Year;
                                                                    ESP=D°a,Semana,Mes,Trimestre,A§o];
                                                   OptionString=Day,Week,Month,Quarter,Year }
    { 7600;   ;Base Calendar Code  ;Code10        ;TableRelation="Base Calendar";
                                                   CaptionML=[ENU=Base Calendar Code;
                                                              ESP=C¢digo calendario base] }
    { 7601;   ;Cal. Convergence Time Frame;DateFormula;
                                                   InitValue=1Y;
                                                   CaptionML=[ENU=Cal. Convergence Time Frame;
                                                              ESP=Calc. convergencia de periodo] }
    { 10701;  ;CNAE Description    ;Text80        ;CaptionML=[ENU=CNAE Description;
                                                              ESP=Descripci¢n CNAE] }
    { 10702;  ;Payment Days Code   ;Code10        ;CaptionML=[ENU=Payment Days Code;
                                                              ESP=C¢d. d°as pago] }
    { 10703;  ;Non-Paymt. Periods Code;Code10     ;CaptionML=[ENU=Non-Paymt. Periods Code;
                                                              ESP=C¢d. periodo no pago] }
    { 10704;  ;CCC Bank No.        ;Text4         ;OnValidate=BEGIN
                                                                "CCC Bank No." := PrePadString("CCC Bank No.",MAXSTRLEN("CCC Bank No."));
                                                                BuildCCC;
                                                              END;

                                                   CaptionML=[ENU=CCC Bank No.;
                                                              ESP=CCC C¢d. banco];
                                                   Numeric=Yes }
    { 10705;  ;CCC Bank Branch No. ;Text4         ;OnValidate=BEGIN
                                                                "CCC Bank Branch No." := PrePadString("CCC Bank Branch No.",MAXSTRLEN("CCC Bank Branch No."));
                                                                BuildCCC;
                                                              END;

                                                   CaptionML=[ENU=CCC Bank Branch No.;
                                                              ESP=CCC C¢d. oficina];
                                                   Numeric=Yes }
    { 10706;  ;CCC Control Digits  ;Text2         ;OnValidate=BEGIN
                                                                "CCC Control Digits" := PrePadString("CCC Control Digits",MAXSTRLEN("CCC Control Digits"));
                                                                BuildCCC;
                                                              END;

                                                   CaptionML=[ENU=CCC Control Digits;
                                                              ESP=CCC D°gito control];
                                                   Numeric=Yes }
    { 10707;  ;CCC Bank Account No.;Text10        ;OnValidate=BEGIN
                                                                "CCC Bank Account No." := PrePadString("CCC Bank Account No.",MAXSTRLEN("CCC Bank Account No."));
                                                                BuildCCC;
                                                              END;

                                                   CaptionML=[ENU=CCC Bank Account No.;
                                                              ESP=CCC Nß cuenta];
                                                   Numeric=Yes }
    { 10708;  ;CCC No.             ;Text20        ;OnValidate=BEGIN
                                                                "CCC Bank No." := COPYSTR("CCC No.",1,4);
                                                                "CCC Bank Branch No." := COPYSTR("CCC No.",5,4);
                                                                "CCC Control Digits" := COPYSTR("CCC No.",9,2);
                                                                "CCC Bank Account No." := COPYSTR("CCC No.",11,23);
                                                              END;

                                                   CaptionML=[ENU=CCC No.;
                                                              ESP=Nß CCC];
                                                   Numeric=Yes }
    { 50000;  ;OLD_Nß Fax 2        ;Text20        ;CaptionML=ESP=OLD_Nß Fax 2;
                                                   Description=$001 }
    { 7010310;;Cliente Exportacion ;Code20         }
  }
  KEYS
  {
    {    ;Primary Key                             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PostCode@1000 : Record 225;
      Text000@1001 : TextConst 'ENU=The number you entered is not a valid IBAN.;ESP=El n£mero que ha introducido no es un IBAN v†lido.';
      Text001@1002 : TextConst 'ENU=File Location for IC files;ESP=Ubicaci¢n de los archivos IC';
      Text002@1003 : TextConst 'ENU=Before you can use Online Map, you must fill in the Online Map Setup window.\See Setting Up Online Map in Help.;ESP=Para poder usar Online Map, primero debe rellenar la ventana Configuraci¢n Online Map.\Consulte Configuraci¢n de Online Map en la Ayuda.';

    PROCEDURE CheckIBAN@1(IBANCode@1000 : Code[100]);
    VAR
      Modulus97@1001 : Integer;
      I@1002 : Integer;
    BEGIN
      IF IBANCode = '' THEN
        EXIT;
      IBANCode := DELCHR(IBANCode);
      Modulus97 := 97;
      IF (STRLEN(IBANCode) <= 5) OR (STRLEN(IBANCode) > 34) THEN
        IBANError;
      ConvertIBAN(IBANCode);
      WHILE STRLEN(IBANCode) > 6 DO
        IBANCode := CalcModulus(COPYSTR(IBANCode,1,6),Modulus97) + COPYSTR(IBANCode,7);
      EVALUATE(I,IBANCode);
      IF (I MOD Modulus97) <> 1 THEN
        IBANError;
    END;

    LOCAL PROCEDURE ConvertIBAN@4(VAR IBANCode@1000 : Code[100]);
    VAR
      I@1002 : Integer;
    BEGIN
      IBANCode := COPYSTR(IBANCode,5) + COPYSTR(IBANCode,1,4);
      I := 0;
      WHILE I < STRLEN(IBANCode) DO BEGIN
        I := I + 1;
        IF ConvertLetter(IBANCode,COPYSTR(IBANCode,I,1),I) THEN
          I := 0;
      END;
    END;

    LOCAL PROCEDURE CalcModulus@3(Number@1000 : Code[10];Modulus97@1001 : Integer) : Code[10];
    VAR
      I@1002 : Integer;
    BEGIN
      EVALUATE(I,Number);
      I := I MOD Modulus97;
      IF I = 0 THEN
        EXIT('');
      EXIT(FORMAT(I));
    END;

    LOCAL PROCEDURE ConvertLetter@5(VAR IBANCode@1000 : Code[100];Letter@1001 : Code[1];LetterPlace@1002 : Integer) : Boolean;
    VAR
      Letter2@1003 : Code[2];
    BEGIN
      IF (Letter >= 'A') AND (Letter <= 'Z') THEN BEGIN
        CASE Letter OF
          'A': Letter2 := '10';
          'B': Letter2 := '11';
          'C': Letter2 := '12';
          'D': Letter2 := '13';
          'E': Letter2 := '14';
          'F': Letter2 := '15';
          'G': Letter2 := '16';
          'H': Letter2 := '17';
          'I': Letter2 := '18';
          'J': Letter2 := '19';
          'K': Letter2 := '20';
          'L': Letter2 := '21';
          'M': Letter2 := '22';
          'N': Letter2 := '23';
          'O': Letter2 := '24';
          'P': Letter2 := '25';
          'Q': Letter2 := '26';
          'R': Letter2 := '27';
          'S': Letter2 := '28';
          'T': Letter2 := '29';
          'U': Letter2 := '30';
          'V': Letter2 := '31';
          'W': Letter2 := '32';
          'X': Letter2 := '33';
          'Y': Letter2 := '34';
          'Z': Letter2 := '35';
        END;
        IF LetterPlace = 1 THEN
          IBANCode := Letter2 + COPYSTR(IBANCode,2)
        ELSE BEGIN
          IF LetterPlace = STRLEN(IBANCode) THEN
            IBANCode := COPYSTR(IBANCode,1,LetterPlace - 1) + Letter2
          ELSE
            IBANCode :=
              COPYSTR(IBANCode,1,LetterPlace - 1) + Letter2 + COPYSTR(IBANCode,LetterPlace + 1);
        END;
        EXIT(TRUE);
      END;
      IF (Letter >= '0') AND (Letter <= '9') THEN
        EXIT(FALSE);

      IBANError;
    END;

    LOCAL PROCEDURE IBANError@12();
    BEGIN
      ERROR(Text000);
    END;

    PROCEDURE DisplayMap@7();
    VAR
      MapPoint@1001 : Record 800;
      MapMgt@1000 : Codeunit 802;
    BEGIN
      IF MapPoint.FIND('-') THEN
        MapMgt.MakeSelection(DATABASE::"Company Information",GETPOSITION)
      ELSE MESSAGE(Text002);
    END;

    PROCEDURE GetSystemIndicator@8(VAR Text@1000 : Text[250];VAR Style@1001 : 'Standard,Accent1,Accent2,Accent3,Accent4,Accent5,Accent6,Accent7,Accent8,Accent9');
    BEGIN
      Style := "System Indicator Style";
      CASE "System Indicator" OF
        "System Indicator"::None:
          Text := '';
        "System Indicator"::"Custom Text":
          Text := "Custom System Indicator Text";
        "System Indicator"::"Company Information":
          Text := Name;
        "System Indicator"::Company:
          Text := COMPANYNAME;
        "System Indicator"::Database:
          Text := GetDatabaseIndicatorText(FALSE);
        "System Indicator"::"Company+Database":
          Text := GetDatabaseIndicatorText(TRUE);
      END
    END;

    LOCAL PROCEDURE GetDatabaseIndicatorText@9(IncludeCompany@1003 : Boolean) : Text[250];
    VAR
      Session@1000 : Record 2000000009;
      Text@1002 : Text[1024];
    BEGIN
      Session.SETRANGE("My Session",TRUE);
      Session.FINDFIRST;
      Text := Session."Database Name" + ' - ' + Session."Host Name";
      IF IncludeCompany THEN
        Text := COMPANYNAME + ' - ' + Text;
      IF STRLEN(Text) > 250 THEN
        EXIT(COPYSTR(Text,1,247) + '...');
      EXIT(Text)
    END;

    PROCEDURE BuildCCC@1100000();
    BEGIN
      "CCC No." := "CCC Bank No." + "CCC Bank Branch No." + "CCC Control Digits" + "CCC Bank Account No.";
      IF "CCC No." <> '' THEN
        TESTFIELD("Bank Account No.",'');
    END;

    PROCEDURE PrePadString@1100001(InString@1100000 : Text[250];MaxLen@1100001 : Integer) : Text[250];
    BEGIN
      EXIT(PADSTR('', MaxLen - STRLEN(InString),'0') + InString);
    END;

    BEGIN
    {
      // INCOMING

      $001 Se ha agregado un nuevo campo

          50000 Nß Fax 2 Text20

      $002 AJS 21112014 Marco el campo 50000 como obsoleto

      $003 AJS 21112014 Integracion Atennea Transfer, nuevo campo "Cliente Exportacion"
    }
    END.
  }
}
