OBJECT Table 7010214 Persona x transporte soporte
{
  OBJECT-PROPERTIES
  {
    Date=28/10/14;
    Time=16:43:04;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    OnInsert=VAR
               lrPerTS@1100253000 : Record 7010214;
             BEGIN
               "Id Detalle" := 1;
               CLEAR(lrPerTS);
               lrPerTS.SETRANGE(Delegacion   , Delegacion);
               lrPerTS.SETRANGE("N§ Servicio", "N§ Servicio");
               lrPerTS.SETRANGE("Id Soporte" , "Id Soporte");
               IF lrPerTS.FINDLAST THEN
                 "Id Detalle" += lrPerTS."Id Detalle";

               "Usuario Creacion" := USERID;
               "Fecha Creacion"   := CURRENTDATETIME;

               CompServ;

               InsHistorico(0);
             END;

    OnModify=BEGIN

               CompServ;
             END;

    OnDelete=BEGIN

               CompServ;

               InsHistorico(2);
             END;

    LookupFormID=Form7010211;
    DrillDownFormID=Form7010211;
  }
  FIELDS
  {
    { 1   ;   ;Delegacion          ;Code10        ;TableRelation=Delegacion.Codigo;
                                                   CaptionML=[ENU=Delegation;
                                                              ESP=Delegaci¢n];
                                                   NotBlank=Yes }
    { 10  ;   ;N§ Servicio         ;Integer       ;TableRelation="Servicio Excursion"."N§ Servicio" WHERE (Delegacion=FIELD(Delegacion));
                                                   CaptionML=[ENU=Service ID;
                                                              ESP=N§ Servicio];
                                                   NotBlank=Yes }
    { 20  ;   ;Id Soporte          ;Integer       ;TableRelation="Transporte Excursion - Soporte"."ID Soporte" WHERE (Delegacion=FIELD(Delegacion),
                                                                                                                      N§ Servicio=FIELD(N§ Servicio));
                                                   CaptionML=[ENU=Support ID;
                                                              ESP=ID Soporte];
                                                   NotBlank=Yes }
    { 30  ;   ;Id Detalle          ;Integer       ;CaptionML=[ENU=Detail ID;
                                                              ESP=ID Detalle];
                                                   Editable=No }
    { 40  ;   ;N§ Reserva          ;Integer       ;TableRelation="Reserva Excursion"."N§ Reserva" WHERE (Delegacion=FIELD(Delegacion),
                                                                                                         N§ Servicio=FIELD(N§ Servicio));
                                                   OnValidate=VAR
                                                                lrPers@1100253001 : Record 7010214;
                                                                lrSop@1100253000 : ARRAY [2] OF Record 7010206;
                                                              BEGIN
                                                                // Una reserva solo puede estar una vez por servicio y direcci¢n
                                                                IF "N§ Reserva" <> 0 THEN BEGIN
                                                                  CLEAR(lrSop);
                                                                  CLEAR(lrPers);
                                                                  lrPers.SETRANGE(Delegacion    , Delegacion);
                                                                  lrPers.SETRANGE("N§ Servicio" , "N§ Servicio");
                                                                  lrPers.SETRANGE("N§ Reserva"  , "N§ Reserva");
                                                                  IF lrPers.FINDSET THEN BEGIN
                                                                    lrSop[1].GET(Delegacion, "N§ Servicio", "Id Soporte");
                                                                    REPEAT
                                                                      IF lrPers.GETPOSITION <> GETPOSITION THEN BEGIN // Que no sea el mismo registro
                                                                        lrSop[2].GET(lrPers.Delegacion, lrPers."N§ Servicio", lrPers."Id Soporte");
                                                                        IF lrSop[1].Direccion = lrSop[2].Direccion THEN
                                                                          ERROR(Text003, "N§ Reserva", lrSop[1].Direccion, "N§ Servicio");
                                                                      END;
                                                                    UNTIL lrPers.NEXT=0;
                                                                  END;
                                                                END;

                                                                GetInfoResv; // JPT 21/06/12 Se inserta la informaci¢n de la reserva
                                                              END;

                                                   CaptionML=ESP=N§ Reserva }
    { 50  ;   ;Usuario Creacion    ;Code20        ;TableRelation=User."User ID";
                                                   CaptionML=[ENU=Creation User;
                                                              ESP=Usuario Creaci¢n];
                                                   Editable=No }
    { 60  ;   ;Fecha Creacion      ;DateTime      ;CaptionML=[ENU=Creation Time;
                                                              ESP=Fecha Creaci¢n];
                                                   Editable=No }
    { 70  ;   ;N§ Ticket           ;Code10        ;OnValidate=VAR
                                                                lrRsv@1100217000 : Record 7010162;
                                                              BEGIN
                                                                lrRsv.RESET;
                                                                lrRsv.SETCURRENTKEY("N§ Ticket");
                                                                lrRsv.SETRANGE("N§ Servicio", "N§ Servicio");
                                                                lrRsv.SETRANGE("N§ Ticket"  , "N§ Ticket");
                                                                IF lrRsv.FINDFIRST THEN
                                                                  VALIDATE("N§ Reserva", lrRsv."N§ Reserva");
                                                              END;

                                                   CaptionML=[ENU=Ticket Num.;
                                                              ESP=N§ Ticket];
                                                   NotBlank=Yes }
    { 80  ;   ;Nombre Cliente      ;Text50        ;CaptionML=[ENU=Client Name;
                                                              ESP=Nombre Cliente];
                                                   Editable=No }
    { 90  ;   ;Hotel               ;Code20        ;TableRelation=Hotel.Hotel;
                                                   CaptionML=[ENU=hotel;
                                                              ESP=Hotel];
                                                   Editable=No }
    { 100 ;   ;Adultos             ;Decimal       ;CaptionML=[ENU=Adults;
                                                              ESP=Adultos];
                                                   DecimalPlaces=0:2;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 110 ;   ;Ni¤os               ;Decimal       ;CaptionML=[ENU=Childs;
                                                              ESP=Ni¤os];
                                                   DecimalPlaces=0:2;
                                                   BlankZero=Yes;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Delegacion,N§ Servicio,Id Soporte,Id Detalle;
                                                   Clustered=Yes }
    {    ;Delegacion,N§ Reserva                    }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100253000 : TextConst 'ENU=Service %1 is in %2  Status;ESP=El servicio %1 est  en estado %2';
      Text002@1100253002 : TextConst 'ESP=Persona Transp. Sop.';
      Text003@1100253003 : TextConst 'ENU=Reservation %1 for %2 of service %3 already exists;ESP=Ya existe la reserva %1 para %2 del servicio %3';

    PROCEDURE CompServ@1100253000();
    VAR
      lrServ@1100253000 : Record 7010163;
    BEGIN
      // CompServ
      // Comprobamos que el servicio no est‚ Confirmado o Anulado
      // Adem s se marca como Pendiente de Recalcular

      CLEAR(lrServ);
      IF lrServ.GET(Delegacion, "N§ Servicio") THEN BEGIN
        IF lrServ.Estado IN [lrServ.Estado::Confirmada, lrServ.Estado::Anulada] THEN
          ERROR(Text001, lrServ."N§ Servicio", lrServ.Estado);

        IF NOT lrServ."Pendiente Recalcular" THEN BEGIN
          lrServ."Pendiente Recalcular" := TRUE;
          lrServ.MODIFY;
        END;
      END;
    END;

    PROCEDURE InsHistorico@1100253006(pwModo@1100253001 : 'Inserta,Modifica,Elimina');
    VAR
      lrHis@1100253000 : Record 7010255;
      lrResv@1100253002 : Record 7010162;
    BEGIN
      // InsHistorico

      IF lrResv.GET(Delegacion, "N§ Reserva") THEN
        lrHis.NewRecord(Delegacion, lrResv."N§ Reserva", lrHis."Tipo Registro"::Reserva,
                        STRSUBSTNO('%1 %2', pwModo, Text002), '', STRSUBSTNO('%1-%2', "Id Soporte", "Id Detalle"));
    END;

    PROCEDURE ResvInfo@1100253001(pwResvExc@1100253000 : Record 7010162);
    BEGIN
      // ResvInfo
      // JPT 21/06/12 Se inserta la informaci¢n de la reserva

      "N§ Ticket"      := pwResvExc."N§ Ticket";
      "Nombre Cliente" := pwResvExc."Nombre Cliente";
      Hotel            := pwResvExc."Hotel Cliente";
      Adultos          := pwResvExc.Adultos;
      Ni¤os            := pwResvExc.Ni¤os;
    END;

    PROCEDURE GetInfoResv@1100253002();
    VAR
      lrRes@1100253000 : Record 7010162;
    BEGIN
      // GetInfoResv

      IF "N§ Reserva" = 0 THEN
        EXIT;

      CLEAR(lrRes);
      IF lrRes.GET(Delegacion, "N§ Reserva") THEN
        ResvInfo(lrRes);
    END;

    BEGIN
    {
      JPT 16/05/12 CNT-CAR-12004 / EXC-08 Transport support service

      $001 AJS 30112012 Rellenar el ticket para obtener los datos de la reserva

      $002 AJS 12062013 Hemos creado una nueva tabla de historico para las reservas de excursiones

      $003 AJS 28102014 Amplio el campo Nombre cliente a Text50
    }
    END.
  }
}
