OBJECT Table 82 Item Journal Template
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               VALIDATE("Form ID");
             END;

    OnDelete=BEGIN
               ItemJnlLine.SETRANGE("Journal Template Name",Name);
               ItemJnlLine.DELETEALL(TRUE);
               ItemJnlBatch.SETRANGE("Journal Template Name",Name);
               ItemJnlBatch.DELETEALL;
             END;

    OnRename=BEGIN
               ReservEngineMgt.RenamePointer(DATABASE::"Item Journal Line",
                 0,xRec.Name,'',0,0,
                 0,Name,'',0,0);
             END;

    CaptionML=[ENU=Item Journal Template;
               ESP=Libro del diario producto];
    LookupFormID=Form261;
  }
  FIELDS
  {
    { 1   ;   ;Name                ;Code10        ;CaptionML=[ENU=Name;
                                                              ESP=Nombre];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text80        ;CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n] }
    { 5   ;   ;Test Report ID      ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Report));
                                                   CaptionML=[ENU=Test Report ID;
                                                              ESP=N§ informe test] }
    { 6   ;   ;Form ID             ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Form));
                                                   OnValidate=BEGIN
                                                                IF "Form ID" = 0 THEN
                                                                  VALIDATE(Type);
                                                              END;

                                                   CaptionML=[ENU=Form ID;
                                                              ESP=Id. formulario] }
    { 7   ;   ;Posting Report ID   ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Report));
                                                   CaptionML=[ENU=Posting Report ID;
                                                              ESP=N§ informe para registro] }
    { 8   ;   ;Force Posting Report;Boolean       ;CaptionML=[ENU=Force Posting Report;
                                                              ESP=Forzar informe para registro] }
    { 9   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                "Test Report ID" := REPORT::"Inventory Posting - Test";
                                                                "Posting Report ID" := REPORT::"Item Register - Quantity";
                                                                "Whse. Register Report ID" := REPORT::"Warehouse Register - Quantity";
                                                                SourceCodeSetup.GET;
                                                                CASE Type OF
                                                                  Type::Item:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Item Journal";
                                                                      "Form ID" :=  FORM::"Item Journal";
                                                                    END;
                                                                  Type::Transfer:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Item Reclass. Journal";
                                                                      "Form ID" := FORM::"Item Reclass. Journal";
                                                                    END;
                                                                  Type::"Phys. Inventory":
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Phys. Inventory Journal";
                                                                      "Form ID" := FORM::"Phys. Inventory Journal";
                                                                    END;
                                                                  Type::Revaluation:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Revaluation Journal";
                                                                      "Form ID" := FORM::"Revaluation Journal";
                                                                      "Test Report ID" := REPORT::"Revaluation Posting - Test";
                                                                      "Posting Report ID" := REPORT::"Item Register - Value";
                                                                    END;
                                                                  Type::Consumption:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Consumption Journal";
                                                                      "Form ID" := FORM::"Consumption Journal";
                                                                    END;
                                                                  Type::Output:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Output Journal";
                                                                      "Form ID" := FORM::"Output Journal";
                                                                    END;
                                                                  Type::Capacity:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Capacity Journal";
                                                                      "Form ID" := FORM::"Capacity Journal";
                                                                    END;
                                                                  Type::"Prod.Order":
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Production Journal";
                                                                      "Form ID" := FORM::"Production Journal";
                                                                    END;
                                                                END;
                                                                IF Recurring THEN
                                                                  CASE Type OF
                                                                    Type::Item:
                                                                      "Form ID" :=  FORM::"Recurring Item Jnl.";
                                                                    Type::Consumption:
                                                                      "Form ID" := FORM::"Recurring Consumption Journal";
                                                                    Type::Output:
                                                                      "Form ID" := FORM::"Recurring Output Journal";
                                                                    Type::Capacity:
                                                                      "Form ID" := FORM::"Recurring Capacity Journal";
                                                                  END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              ESP=Tipo];
                                                   OptionCaptionML=[ENU=Item,Transfer,Phys. Inventory,Revaluation,Consumption,Output,Capacity,Prod.Order;
                                                                    ESP=Producto,Transferencia,Invent. f¡s.,Revalorizaci¢n,Consumo,Salida,Capacidad,Orden prod.];
                                                   OptionString=Item,Transfer,Phys. Inventory,Revaluation,Consumption,Output,Capacity,Prod.Order }
    { 10  ;   ;Source Code         ;Code10        ;TableRelation="Source Code";
                                                   OnValidate=BEGIN
                                                                ItemJnlLine.SETRANGE("Journal Template Name",Name);
                                                                ItemJnlLine.MODIFYALL("Source Code","Source Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Source Code;
                                                              ESP=C¢d. origen] }
    { 11  ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   CaptionML=[ENU=Reason Code;
                                                              ESP=C¢d. auditor¡a] }
    { 12  ;   ;Recurring           ;Boolean       ;OnValidate=BEGIN
                                                                VALIDATE(Type);
                                                                IF Recurring THEN
                                                                  TESTFIELD("No. Series",'');
                                                              END;

                                                   CaptionML=[ENU=Recurring;
                                                              ESP=Peri¢dico] }
    { 15  ;   ;Test Report Name    ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Report),
                                                                                                                Object ID=FIELD(Test Report ID)));
                                                   CaptionML=[ENU=Test Report Name;
                                                              ESP=Nombre informe test];
                                                   Editable=No }
    { 16  ;   ;Form Name           ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Form),
                                                                                                                Object ID=FIELD(Form ID)));
                                                   CaptionML=[ENU=Form Name;
                                                              ESP=Nombre formulario];
                                                   Editable=No }
    { 17  ;   ;Posting Report Name ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Report),
                                                                                                                Object ID=FIELD(Posting Report ID)));
                                                   CaptionML=[ENU=Posting Report Name;
                                                              ESP=Nombre informe para registro];
                                                   Editable=No }
    { 19  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF "No. Series" <> '' THEN BEGIN
                                                                  IF Recurring THEN
                                                                    ERROR(
                                                                      Text000,
                                                                      FIELDCAPTION("Posting No. Series"));
                                                                  IF "No. Series" = "Posting No. Series" THEN
                                                                    "Posting No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No. Series;
                                                              ESP=Nos. serie] }
    { 20  ;   ;Posting No. Series  ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF ("Posting No. Series" = "No. Series") AND ("Posting No. Series" <> '') THEN
                                                                  FIELDERROR("Posting No. Series",STRSUBSTNO(Text001,"Posting No. Series"));
                                                              END;

                                                   CaptionML=[ENU=Posting No. Series;
                                                              ESP=N§ serie registro] }
    { 21  ;   ;Whse. Register Report ID;Integer   ;TableRelation=Object.ID WHERE (Type=CONST(Report));
                                                   CaptionML=[ENU=Whse. Register Report ID;
                                                              ESP=Id. informe movs. alm.] }
    { 22  ;   ;Whse. Register Report Name;Text80  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Report),
                                                                                                                Object ID=FIELD(Whse. Register Report ID)));
                                                   CaptionML=[ENU=Whse. Register Report Name;
                                                              ESP=Nombre informe movs. alm.];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Name                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Name,Description,Type                    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Only the %1 field can be filled in on recurring journals.;ESP=S¢lo se debe completar el campo %1 en los diarios peri¢dicos.';
      Text001@1001 : TextConst 'ENU=must not be %1;ESP=No puede ser %1.';
      ItemJnlBatch@1002 : Record 233;
      ItemJnlLine@1003 : Record 83;
      SourceCodeSetup@1004 : Record 242;
      ReservEngineMgt@1005 : Codeunit 99000831;

    BEGIN
    END.
  }
}
