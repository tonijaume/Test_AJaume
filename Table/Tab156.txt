OBJECT Table 156 Resource
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01,NAVES6.00;
  }
  PROPERTIES
  {
    DataCaptionFields=No.,Name;
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 ResSetup.GET;
                 ResSetup.TESTFIELD("Resource Nos.");
                 NoSeriesMgt.InitSeries(ResSetup."Resource Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;

               IF GETFILTER("Resource Group No.") <> '' THEN
                 IF GETRANGEMIN("Resource Group No.") = GETRANGEMAX("Resource Group No.") THEN
                   VALIDATE("Resource Group No.",GETRANGEMIN("Resource Group No."));

               DimMgt.UpdateDefaultDim(
                 DATABASE::Resource,"No.",
                 "Global Dimension 1 Code","Global Dimension 2 Code");
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;
             END;

    OnDelete=BEGIN
               MoveEntries.MoveResEntries(Rec);

               ResCapacityEntry.SETCURRENTKEY("Resource No.");
               ResCapacityEntry.SETRANGE("Resource No.","No.");
               ResCapacityEntry.DELETEALL;

               ResCost.SETRANGE(Type,ResCost.Type::Resource);
               ResCost.SETRANGE(Code,"No.");
               ResCost.DELETEALL;

               ResPrice.SETRANGE(Type,ResPrice.Type::Resource);
               ResPrice.SETRANGE(Code,"No.");
               ResPrice.DELETEALL;

               CommentLine.SETRANGE("Table Name",CommentLine."Table Name"::Resource);
               CommentLine.SETRANGE("No.","No.");
               CommentLine.DELETEALL;

               ExtTextHeader.SETRANGE("Table Name",ExtTextHeader."Table Name"::Resource);
               ExtTextHeader.SETRANGE("No.","No.");
               ExtTextHeader.DELETEALL(TRUE);

               ResSkill.RESET;
               ResSkill.SETRANGE(Type,ResSkill.Type::Resource);
               ResSkill.SETRANGE("No.","No.");
               ResSkill.DELETEALL;

               ResLoc.RESET;
               ResLoc.SETCURRENTKEY("Resource No.","Starting Date");
               ResLoc.SETRANGE("Resource No.","No.");
               ResLoc.DELETEALL;

               ResServZone.RESET;
               ResServZone.SETRANGE("Resource No.","No.");
               ResServZone.DELETEALL;

               ResUnitMeasure.RESET;
               ResUnitMeasure.SETRANGE("Resource No.","No.");
               ResUnitMeasure.DELETEALL;

               SalesOrderLine.SETCURRENTKEY(Type,"No.");
               SalesOrderLine.SETFILTER("Document Type",'%1|%2',
                 SalesOrderLine."Document Type"::Order,
                 SalesOrderLine."Document Type"::"Return Order");
               SalesOrderLine.SETRANGE(Type,SalesOrderLine.Type::Resource);
               SalesOrderLine.SETRANGE("No.","No.");
               IF SalesOrderLine.FIND('-') THEN BEGIN
                 IF SalesOrderLine."Document Type" = SalesOrderLine."Document Type"::Order THEN
                   ERROR(Text000,TABLECAPTION,"No.");
                 IF SalesOrderLine."Document Type" = SalesOrderLine."Document Type"::"Return Order" THEN
                   ERROR(Text003,TABLECAPTION,"No.");
               END;

               DimMgt.DeleteDefaultDim(DATABASE::Resource,"No.");
             END;

    OnRename=BEGIN
               "Last Date Modified" := TODAY;
             END;

    CaptionML=[ENU=Resource;
               ESP=Recurso];
    LookupFormID=Form77;
    DrillDownFormID=Form77;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;AltSearchField=Search Name;
                                                   OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  ResSetup.GET;
                                                                  NoSeriesMgt.TestManual(ResSetup."Resource Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              ESP=N§] }
    { 2   ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              ESP=Tipo];
                                                   OptionCaptionML=[ENU=Person,Machine;
                                                                    ESP=Persona,M quina];
                                                   OptionString=Person,Machine }
    { 3   ;   ;Name                ;Text50        ;OnValidate=BEGIN
                                                                IF ("Search Name" = UPPERCASE(xRec.Name)) OR ("Search Name" = '') THEN
                                                                  "Search Name" := Name;
                                                              END;

                                                   CaptionML=[ENU=Name;
                                                              ESP=Nombre] }
    { 4   ;   ;Search Name         ;Code50        ;CaptionML=[ENU=Search Name;
                                                              ESP=Alias] }
    { 5   ;   ;Name 2              ;Text50        ;CaptionML=[ENU=Name 2;
                                                              ESP=Nombre 2] }
    { 6   ;   ;Address             ;Text50        ;CaptionML=[ENU=Address;
                                                              ESP=Direcci¢n] }
    { 7   ;   ;Address 2           ;Text50        ;CaptionML=[ENU=Address 2;
                                                              ESP=Direcci¢n 2] }
    { 8   ;   ;City                ;Text30        ;OnValidate=BEGIN
                                                                PostCode.ValidateCity(City,"Post Code",County);
                                                              END;

                                                   OnLookup=BEGIN
                                                              PostCode.LookUpCity(City,"Post Code",County,TRUE);
                                                            END;

                                                   CaptionML=[ENU=City;
                                                              ESP=Poblaci¢n] }
    { 9   ;   ;Social Security No. ;Text30        ;CaptionML=[ENU=Social Security No.;
                                                              ESP=N§ seguridad social] }
    { 10  ;   ;Job Title           ;Text30        ;CaptionML=[ENU=Job Title;
                                                              ESP=Cargo] }
    { 11  ;   ;Education           ;Text30        ;CaptionML=[ENU=Education;
                                                              ESP=Profesi¢n] }
    { 12  ;   ;Contract Class      ;Text30        ;CaptionML=[ENU=Contract Class;
                                                              ESP=Convenio laboral] }
    { 13  ;   ;Employment Date     ;Date          ;CaptionML=[ENU=Employment Date;
                                                              ESP=Fecha contrataci¢n] }
    { 14  ;   ;Resource Group No.  ;Code20        ;TableRelation="Resource Group";
                                                   OnValidate=BEGIN
                                                                IF "Resource Group No." = xRec."Resource Group No." THEN
                                                                  EXIT;

                                                                IF xRec."Resource Group No." <> '' THEN BEGIN
                                                                  IF NOT
                                                                     CONFIRM(
                                                                       Text001,FALSE,
                                                                       FIELDCAPTION("Resource Group No."))
                                                                  THEN BEGIN
                                                                    "Resource Group No." := xRec."Resource Group No.";
                                                                    EXIT;
                                                                  END;
                                                                END;

                                                                IF xRec.GETFILTER("Resource Group No.") <> '' THEN
                                                                  SETFILTER("Resource Group No.","Resource Group No.");


                                                                // Resource Capacity Entries
                                                                ResCapacityEntry.SETCURRENTKEY("Resource No.");
                                                                ResCapacityEntry.SETRANGE("Resource No.","No.");
                                                                ResCapacityEntry.MODIFYALL("Resource Group No.","Resource Group No.");

                                                                PlanningLine.SETCURRENTKEY(Type,"No.");
                                                                PlanningLine.SETRANGE(Type,PlanningLine.Type::Resource);
                                                                PlanningLine.SETRANGE("No.","No.");
                                                                PlanningLine.SETRANGE("Schedule Line",TRUE);
                                                                PlanningLine.MODIFYALL("Resource Group No.","Resource Group No.");
                                                              END;

                                                   CaptionML=[ENU=Resource Group No.;
                                                              ESP=N§ fam. recurso] }
    { 16  ;   ;Global Dimension 1 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Global Dimension 1 Code");
                                                              END;

                                                   CaptionML=[ENU=Global Dimension 1 Code;
                                                              ESP=C¢d. dimensi¢n global 1];
                                                   CaptionClass='1,1,1' }
    { 17  ;   ;Global Dimension 2 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Global Dimension 2 Code");
                                                              END;

                                                   CaptionML=[ENU=Global Dimension 2 Code;
                                                              ESP=C¢d. dimensi¢n global 2];
                                                   CaptionClass='1,1,2' }
    { 18  ;   ;Base Unit of Measure;Code10        ;TableRelation="Resource Unit of Measure".Code WHERE (Resource No.=FIELD(No.));
                                                   OnValidate=VAR
                                                                ResUnitOfMeasure@1000 : Record 205;
                                                                ResLedgEnty@1001 : Record 203;
                                                              BEGIN
                                                                IF "Base Unit of Measure" <> xRec."Base Unit of Measure" THEN BEGIN
                                                                  ResLedgEnty.SETCURRENTKEY("Resource No.");
                                                                  ResLedgEnty.SETRANGE("Resource No.","No.");
                                                                  IF ResLedgEnty.FIND('-') THEN
                                                                    ERROR(Text002,FIELDCAPTION("Base Unit of Measure"));
                                                                END;

                                                                ResUnitOfMeasure.GET("No.","Base Unit of Measure");
                                                                ResUnitOfMeasure.TESTFIELD("Qty. per Unit of Measure",1);
                                                                ResUnitOfMeasure.TESTFIELD("Related to Base Unit of Meas.");
                                                              END;

                                                   CaptionML=[ENU=Base Unit of Measure;
                                                              ESP=Unidad medida base] }
    { 19  ;   ;Direct Unit Cost    ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Indirect Cost %");
                                                              END;

                                                   CaptionML=[ENU=Direct Unit Cost;
                                                              ESP=Coste unit. directo];
                                                   MinValue=0;
                                                   AutoFormatType=2 }
    { 20  ;   ;Indirect Cost %     ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Unit Cost",ROUND("Direct Unit Cost" * (1 + "Indirect Cost %" / 100)));
                                                              END;

                                                   CaptionML=[ENU=Indirect Cost %;
                                                              ESP=% Coste indirecto];
                                                   DecimalPlaces=2:2 }
    { 21  ;   ;Unit Cost           ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Price/Profit Calculation");
                                                              END;

                                                   CaptionML=[ENU=Unit Cost;
                                                              ESP=Coste unitario];
                                                   MinValue=0;
                                                   AutoFormatType=2 }
    { 22  ;   ;Profit %            ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Price/Profit Calculation");
                                                              END;

                                                   CaptionML=[ENU=Profit %;
                                                              ESP=% Bf§ bruto];
                                                   DecimalPlaces=0:5 }
    { 23  ;   ;Price/Profit Calculation;Option    ;OnValidate=BEGIN
                                                                CASE "Price/Profit Calculation" OF
                                                                  "Price/Profit Calculation"::"Profit=Price-Cost":
                                                                    IF "Unit Price" <> 0 THEN
                                                                      "Profit %" := ROUND(100 * (1 - "Unit Cost" / "Unit Price"),0.00001)
                                                                    ELSE
                                                                      "Profit %" := 0;
                                                                  "Price/Profit Calculation"::"Price=Cost+Profit":
                                                                    IF "Profit %" < 100 THEN
                                                                      "Unit Price" := ROUND("Unit Cost" / (1 - "Profit %" / 100),0.00001);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Price/Profit Calculation;
                                                              ESP=C lculo precio/bf§ bruto];
                                                   OptionCaptionML=[ENU="Profit=Price-Cost,Price=Cost+Profit,No Relationship";
                                                                    ESP="Beneficio=Precio-Coste,Precio=Coste+Beneficio,Sin relaci¢n"];
                                                   OptionString=Profit=Price-Cost,Price=Cost+Profit,No Relationship }
    { 24  ;   ;Unit Price          ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Price/Profit Calculation");
                                                              END;

                                                   CaptionML=[ENU=Unit Price;
                                                              ESP=Precio venta];
                                                   MinValue=0;
                                                   AutoFormatType=2 }
    { 25  ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor;
                                                   CaptionML=[ENU=Vendor No.;
                                                              ESP=N§ proveedor] }
    { 26  ;   ;Last Date Modified  ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              ESP=Fecha £lt. modificaci¢n];
                                                   Editable=No }
    { 27  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Comment Line" WHERE (Table Name=CONST(Resource),
                                                                                           No.=FIELD(No.)));
                                                   CaptionML=[ENU=Comment;
                                                              ESP=Comentario];
                                                   Editable=No }
    { 38  ;   ;Blocked             ;Boolean       ;CaptionML=[ENU=Blocked;
                                                              ESP=Bloqueado] }
    { 39  ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Date Filter;
                                                              ESP=Filtro fecha] }
    { 40  ;   ;Unit of Measure Filter;Code10      ;FieldClass=FlowFilter;
                                                   TableRelation="Unit of Measure";
                                                   CaptionML=[ENU=Unit of Measure Filter;
                                                              ESP=Filtro unidad medida] }
    { 41  ;   ;Capacity            ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Res. Capacity Entry".Capacity WHERE (Resource No.=FIELD(No.),
                                                                                                         Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Capacity;
                                                              ESP=Capacidad];
                                                   DecimalPlaces=0:5 }
    { 42  ;   ;Qty. on Order (Job) ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Job Planning Line"."Quantity (Base)" WHERE (Status=CONST(Order),
                                                                                                                Schedule Line=CONST(Yes),
                                                                                                                Type=CONST(Resource),
                                                                                                                No.=FIELD(No.),
                                                                                                                Planning Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Qty. on Order (Job);
                                                              ESP=Cdad. pedida en proyectos];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 43  ;   ;Qty. Quoted (Job)   ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Job Planning Line"."Quantity (Base)" WHERE (Status=CONST(Quote),
                                                                                                                Schedule Line=CONST(Yes),
                                                                                                                Type=CONST(Resource),
                                                                                                                No.=FIELD(No.),
                                                                                                                Planning Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Qty. Quoted (Job);
                                                              ESP=Cdad. ofertada en proyectos];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 44  ;   ;Usage (Qty.)        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Res. Ledger Entry"."Quantity (Base)" WHERE (Entry Type=CONST(Usage),
                                                                                                                Chargeable=FIELD(Chargeable Filter),
                                                                                                                Unit of Measure Code=FIELD(Unit of Measure Filter),
                                                                                                                Resource No.=FIELD(No.),
                                                                                                                Posting Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Usage (Qty.);
                                                              ESP=Consumo (cdad.)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 45  ;   ;Usage (Cost)        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Res. Ledger Entry"."Total Cost" WHERE (Entry Type=CONST(Usage),
                                                                                                           Chargeable=FIELD(Chargeable Filter),
                                                                                                           Unit of Measure Code=FIELD(Unit of Measure Filter),
                                                                                                           Resource No.=FIELD(No.),
                                                                                                           Posting Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Usage (Cost);
                                                              ESP=Consumo (p. coste)];
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 46  ;   ;Usage (Price)       ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Res. Ledger Entry"."Total Price" WHERE (Entry Type=CONST(Usage),
                                                                                                            Chargeable=FIELD(Chargeable Filter),
                                                                                                            Unit of Measure Code=FIELD(Unit of Measure Filter),
                                                                                                            Resource No.=FIELD(No.),
                                                                                                            Posting Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Usage (Price);
                                                              ESP=Imp. consumido a p. venta];
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 47  ;   ;Sales (Qty.)        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("Res. Ledger Entry"."Quantity (Base)" WHERE (Entry Type=CONST(Sale),
                                                                                                                 Unit of Measure Code=FIELD(Unit of Measure Filter),
                                                                                                                 Resource No.=FIELD(No.),
                                                                                                                 Posting Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Sales (Qty.);
                                                              ESP=Ventas (cdad.)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 48  ;   ;Sales (Cost)        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("Res. Ledger Entry"."Total Cost" WHERE (Entry Type=CONST(Sale),
                                                                                                            Unit of Measure Code=FIELD(Unit of Measure Filter),
                                                                                                            Resource No.=FIELD(No.),
                                                                                                            Posting Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Sales (Cost);
                                                              ESP=Imp. coste de ventas];
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 49  ;   ;Sales (Price)       ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("Res. Ledger Entry"."Total Price" WHERE (Entry Type=CONST(Sale),
                                                                                                             Unit of Measure Code=FIELD(Unit of Measure Filter),
                                                                                                             Resource No.=FIELD(No.),
                                                                                                             Posting Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Sales (Price);
                                                              ESP=Imp. ventas];
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 50  ;   ;Chargeable Filter   ;Boolean       ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Chargeable Filter;
                                                              ESP=Filtro facturable] }
    { 51  ;   ;Gen. Prod. Posting Group;Code10    ;TableRelation="Gen. Product Posting Group";
                                                   OnValidate=BEGIN
                                                                IF xRec."Gen. Prod. Posting Group" <> "Gen. Prod. Posting Group" THEN
                                                                  IF GenProdPostingGrp.ValidateVatProdPostingGroup(GenProdPostingGrp,"Gen. Prod. Posting Group") THEN
                                                                    VALIDATE("VAT Prod. Posting Group",GenProdPostingGrp."Def. VAT Prod. Posting Group");
                                                              END;

                                                   CaptionML=[ENU=Gen. Prod. Posting Group;
                                                              ESP=Grupo contable producto] }
    { 52  ;   ;Picture             ;BLOB          ;CaptionML=[ENU=Picture;
                                                              ESP=Imagen];
                                                   SubType=Bitmap }
    { 53  ;   ;Post Code           ;Code20        ;TableRelation="Post Code";
                                                   OnValidate=BEGIN
                                                                PostCode.ValidatePostCode(City,"Post Code",County);
                                                              END;

                                                   OnLookup=BEGIN
                                                              PostCode.LookUpPostCode(City,"Post Code",County,TRUE);
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Post Code;
                                                              ESP=C.P.] }
    { 54  ;   ;County              ;Text30        ;CaptionML=[ENU=County;
                                                              ESP=Provincia] }
    { 55  ;   ;Automatic Ext. Texts;Boolean       ;CaptionML=[ENU=Automatic Ext. Texts;
                                                              ESP=Texto adicional autom tico] }
    { 56  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              ESP=Nos. serie];
                                                   Editable=No }
    { 57  ;   ;Tax Group Code      ;Code10        ;TableRelation="Tax Group";
                                                   CaptionML=[ENU=Tax Group Code;
                                                              ESP=C¢d. grupo impuesto] }
    { 58  ;   ;VAT Prod. Posting Group;Code10     ;TableRelation="VAT Product Posting Group";
                                                   CaptionML=[ENU=VAT Prod. Posting Group;
                                                              ESP=Grupo registro IVA prod.] }
    { 59  ;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   CaptionML=[ENU=Country/Region Code;
                                                              ESP=C¢d. pa¡s/regi¢n] }
    { 60  ;   ;IC Partner Purch. G/L Acc. No.;Code20;
                                                   TableRelation="IC G/L Account";
                                                   CaptionML=[ENU=IC Partner Purch. G/L Acc. No.;
                                                              ESP=N§ cuenta compra IC asociada] }
    { 5900;   ;Qty. on Service Order;Decimal      ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Service Order Allocation"."Allocated Hours" WHERE (Posted=CONST(No),
                                                                                                                       Resource No.=FIELD(No.),
                                                                                                                       Allocation Date=FIELD(Date Filter),
                                                                                                                       Status=CONST(Active)));
                                                   CaptionML=[ENU=Qty. on Service Order;
                                                              ESP=Cdad. en ped. servicio];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 5901;   ;Service Zone Filter ;Code10        ;TableRelation="Service Zone";
                                                   CaptionML=[ENU=Service Zone Filter;
                                                              ESP=Filtro zona servicio] }
    { 5902;   ;In Customer Zone    ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Resource Service Zone" WHERE (Resource No.=FIELD(No.),
                                                                                                    Service Zone Code=FIELD(Service Zone Filter)));
                                                   CaptionML=[ENU=In Customer Zone;
                                                              ESP=En zona cliente];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Search Name                              }
    {    ;Gen. Prod. Posting Group                 }
    {    ;Name                                    ;KeyGroups=SearchCol }
    {    ;Type                                    ;KeyGroups=SearchCol }
    {    ;Base Unit of Measure                    ;KeyGroups=SearchCol }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;No.,Name,Type,Base Unit of Measure       }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You cannot delete %1 %2 because there are one or more outstanding Sales Orders that include this resource.;ESP=No puede borrar %1 %2 porque hay uno o m s pedidos de ventas pendientes que incluyen este recurso';
      Text001@1001 : TextConst 'ENU=Do you want to change %1?;ESP=¨Confirma que desea cambiar %1?';
      ResSetup@1002 : Record 314;
      Res@1003 : Record 156;
      ResCapacityEntry@1004 : Record 160;
      CommentLine@1006 : Record 97;
      ResCost@1007 : Record 202;
      ResPrice@1008 : Record 201;
      SalesOrderLine@1009 : Record 37;
      ExtTextHeader@1010 : Record 279;
      PostCode@1011 : Record 225;
      GenProdPostingGrp@1012 : Record 251;
      ResSkill@1016 : Record 5956;
      ResLoc@1017 : Record 5952;
      ResServZone@1018 : Record 5958;
      ResUnitMeasure@1020 : Record 205;
      PlanningLine@1005 : Record 1003;
      NoSeriesMgt@1013 : Codeunit 396;
      MoveEntries@1014 : Codeunit 361;
      DimMgt@1015 : Codeunit 408;
      Text002@1019 : TextConst 'ENU=You cannot change %1 because there are ledger entries for this resource.;ESP=No puede cambiar el %1 por que hay movs. contables para este recurso.';
      Text003@1021 : TextConst 'ENU=You cannot delete %1 %2 because there are one or more outstanding Sales Return Orders that include this resource.;ESP=No puede borrar %1 %2 porque hay uno o m s pedidos de devoluci¢n de venta pendientes que incluyen este recurso.';
      Text004@1022 : TextConst 'ENU=Before you can use Online Map, you must fill in the Online Map Setup window.\See Setting Up Online Map in Help.;ESP=Para poder usar Online Map, primero debe rellenar la ventana Configuraci¢n Online Map.\Consulte Configuraci¢n de Online Map en la Ayuda.';

    PROCEDURE AssistEdit@2(OldRes@1000 : Record 156) : Boolean;
    BEGIN
      WITH Res DO BEGIN
        Res := Rec;
        ResSetup.GET;
        ResSetup.TESTFIELD("Resource Nos.");
        IF NoSeriesMgt.SelectSeries(ResSetup."Resource Nos.",OldRes."No. Series","No. Series") THEN BEGIN
          ResSetup.GET;
          ResSetup.TESTFIELD("Resource Nos.");
          NoSeriesMgt.SetSeries("No.");
          Rec := Res;
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE ValidateShortcutDimCode@29(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateDimValueCode(FieldNumber,ShortcutDimCode);
      DimMgt.SaveDefaultDim(DATABASE::Resource,"No.",FieldNumber,ShortcutDimCode);
      MODIFY;
    END;

    PROCEDURE DisplayMap@7();
    VAR
      MapPoint@1001 : Record 800;
      MapMgt@1000 : Codeunit 802;
    BEGIN
      IF MapPoint.FIND('-') THEN
        MapMgt.MakeSelection(DATABASE::Resource,GETPOSITION)
      ELSE
        MESSAGE(Text004);
    END;

    BEGIN
    END.
  }
}
