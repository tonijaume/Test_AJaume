OBJECT Table 7035369 Servicios no asignables
{
  OBJECT-PROPERTIES
  {
    Date=16/10/09;
    Time=12:30:49;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    OnInsert=VAR
               rSNA@1100244000 : Record 7035369;
             BEGIN
               IF ("Codigo Servicio"='') OR ("Pax desde" = 0) OR ("Pax Hasta" = 0) THEN
                 ERROR(Text002);

               IF "Pax desde" > "Pax Hasta" THEN
                 ERROR(Text000);

               ID := 1;
               rSNA.RESET;
               IF rSNA.FINDLAST THEN
                 ID := rSNA.ID + 1;

               ValidarSolape;
             END;

    OnModify=BEGIN
               IF "Pax desde" > "Pax Hasta" THEN
                 ERROR(Text000);

               ValidarSolape;
             END;

    CaptionML=ESP=Servicios no asignables;
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;Integer       ;CaptionML=ESP=ID;
                                                   NotBlank=No;
                                                   Editable=No }
    { 2   ;   ;Codigo Servicio     ;Code10        ;TableRelation="Codigo servicio".Codigo;
                                                   CaptionML=ESP=Codigo Servicio;
                                                   NotBlank=Yes }
    { 3   ;   ;Pax desde           ;Integer       ;CaptionML=ESP=Pax desde;
                                                   MinValue=1;
                                                   NotBlank=Yes }
    { 4   ;   ;Pax Hasta           ;Integer       ;CaptionML=ESP=Pax Hasta;
                                                   MinValue=1;
                                                   NotBlank=Yes }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
    {    ;Codigo Servicio,Pax desde                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100244002 : TextConst 'ESP=Plazas Desde no puede tener un valor mayor que Plazas Hasta';
      Text001@1100244001 : TextConst 'ESP=Hay solape de rangos con otro registro';
      Text002@1100244000 : TextConst 'ESP=Hay que introducir el c¢digo de servicio el rango de paxes';

    PROCEDURE ValidarSolape@1100244000();
    VAR
      rSNA@1100244001 : Record 7035369;
      cModulo@1100244002 : Codeunit 7010313;
    BEGIN
      rSNA.RESET;
      rSNA.SETRANGE("Codigo Servicio","Codigo Servicio");
      IF rSNA.FINDSET THEN
        REPEAT
          //Vigilaremos de no validar justamente el registro que estamos modificando.
          IF ID <> rSNA.ID THEN
            //Revisaremos si los periodos intersectan.
            IF InterseccionRangos(rSNA."Pax desde",rSNA."Pax Hasta",
                                  "Pax desde","Pax Hasta") THEN

              ERROR(Text001);
        UNTIL rSNA.NEXT=0;
    END;

    PROCEDURE InterseccionRangos@1100244001(a1@1100244000 : Integer;a2@1100244001 : Integer;b1@1100244002 : Integer;b2@1100244003 : Integer) : Boolean;
    BEGIN
      //InterseccionRangos().
      // Condicion para detectar la interseccion.
      //    a1--------a2                   a1---------a2
      //         b1--------b2        b1--------b2

      IF ( (a1 >= b1) AND (a1 <= b2) ) OR ( (a2 >= b1) AND (a2 <= b2) ) OR
         ( (b1 >= a1) AND (b1 <= a2) ) OR ( (b2 >= a1) AND (b2 <= a2) ) THEN
        EXIT(TRUE);

      EXIT(FALSE);
    END;

    BEGIN
    END.
  }
}
