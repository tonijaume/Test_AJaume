OBJECT Table 7010193 Hotel TO-Circuito
{
  OBJECT-PROPERTIES
  {
    Date=06/03/14;
    Time=22:32:54;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    OnInsert=VAR
               rHot@1103355000 : Record 7010193;
             BEGIN
               rHot.RESET;
               rHot.SETRANGE(Delegacion , Delegacion);
               rHot.SETRANGE("N§ Tarifa", "N§ Tarifa");
               IF rHot.FINDLAST THEN
                 Orden := rHot.Orden + 1
               ELSE
                 Orden := 1;
             END;

    OnModify=BEGIN
               Activo := Activar_Hotel;
             END;

    CaptionML=[ENU=Roundtrip-TO Hotel;
               ESP=Hotel TO-Circuito];
    LookupFormID=Form7010242;
    DrillDownFormID=Form7010242;
  }
  FIELDS
  {
    { 1   ;   ;Delegacion          ;Code10        ;TableRelation=Delegacion.Codigo;
                                                   CaptionML=[ENU=Local Office;
                                                              ESP=Delegacion];
                                                   NotBlank=Yes;
                                                   Description=PK, FK Delegacion;
                                                   Editable=No }
    { 3   ;   ;N§ Tarifa           ;Code10        ;CaptionML=[ENU=Tariff No.;
                                                              ESP=N§ Tarifa];
                                                   NotBlank=Yes;
                                                   Description=PK;
                                                   Editable=No }
    { 5   ;   ;Orden               ;Integer       ;CaptionML=[ENU=Order;
                                                              ESP=Orden];
                                                   Description=PK;
                                                   Editable=No }
    { 7   ;   ;Circuito            ;Code10        ;TableRelation="Maestro circuitos".Circuito;
                                                   CaptionML=[ENU=Circuit;
                                                              ESP=Circuito];
                                                   Description=FK Maestro Circuito }
    { 9   ;   ;Fecha desde         ;Date          ;CaptionML=[ENU=Date From;
                                                              ESP=Fecha desde] }
    { 10  ;   ;Fecha hasta         ;Date          ;CaptionML=[ENU=Date To;
                                                              ESP=Fecha hasta] }
    { 13  ;   ;Hotel               ;Code20        ;TableRelation=Hotel.Hotel;
                                                   CaptionML=[ENU=Hotel;
                                                              ESP=Hotel];
                                                   Description=FK Hotel }
    { 15  ;   ;Dia inicio          ;Integer       ;CaptionML=[ENU=Starting day;
                                                              ESP=Dia inicio] }
    { 17  ;   ;Estancia            ;Integer       ;CaptionML=[ENU=Stay;
                                                              ESP=Estancia] }
    { 19  ;   ;Dias PC             ;Integer       ;OnValidate=BEGIN
                                                                IF "Dias PC" > Estancia THEN
                                                                  ERROR(Text000);
                                                              END;

                                                   CaptionML=[ENU=PC Days;
                                                              ESP=Dias PC] }
    { 21  ;   ;Activo              ;Boolean       ;CaptionML=[ENU=Active;
                                                              ESP=Activo];
                                                   Editable=No }
    { 23  ;   ;C¢d. Categoria      ;Code10        ;TableRelation="Codigo Circuito".Codigo WHERE (Tipo Codigo=CONST(Categoria));
                                                   CaptionML=[ENU=Category code;
                                                              ESP=C¢d. Categoria];
                                                   Description=FK Codigo Circuito }
  }
  KEYS
  {
    {    ;Delegacion,N§ Tarifa,Orden              ;Clustered=Yes }
    {    ;Dia inicio                              ;KeyGroups=RENUMERING }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1103355000 : TextConst 'ENU=Unable to be more days on full board than stay days;ESP=No puede haber m s d¡as a pensi¢n completa que dias estancia';
      Text001@1103355001 : TextConst 'ENU=This hotel overlaps with another one already entered;ESP=Este hotel se solapa con otro ya introducido.';
      Text002@1103355002 : TextConst 'ENU=Unable to activate the prices if the tariff is not confirmed;ESP=No se pueden activar los precios si la tarifa no est  confirmada';
      Text003@1103355003 : TextConst 'ENU=Date from cannot be lower than the initial date of the tariff;ESP=La fecha desde no puede ser inferior a la fecha de inicio de la tarifa';
      Text004@1103355004 : TextConst 'ENU=Date to cannot be higher than final date of the tariff;ESP=La fecha hasta no puede ser superior a la fecha final de la tarifa';
      Text005@1103355005 : TextConst 'ENU=Associated hotels to circuit sale contract no. %1, circuit %2 of %3 and category %4 does not exist.;ESP=No existen hoteles asociados al contrato de venta circuito n§ %1, circuito %2 de %3 y categor¡a %4';

    PROCEDURE Activar_Hotel@1() : Boolean;
    VAR
      rHotTO@1103355000 : Record 7010193;
    BEGIN
      // activar_Precio
      //
      IF ("Fecha desde" = 0D) OR ("Fecha hasta" = 0D) THEN
        EXIT(FALSE);

      IF NOT Comprobar_Tarifa THEN
        EXIT(FALSE);

      rHotTO.RESET;
      rHotTO.SETRANGE (Delegacion      , Delegacion);
      rHotTO.SETRANGE ("N§ Tarifa"     , "N§ Tarifa");
      rHotTO.SETFILTER(Orden           , '<>%1', Orden);
      rHotTO.SETFILTER(Circuito        , Circuito);
      rHotTO.SETRANGE ("C¢d. Categoria", "C¢d. Categoria");
      rHotTO.SETFILTER("Dia inicio"    , '<%1', "Dia inicio" + Estancia);
      rHotTO.SETRANGE (Activo       , TRUE);

      rHotTO.SETFILTER("Fecha desde", '<=%1', "Fecha desde");
      rHotTO.SETFILTER("Fecha hasta", '>=%1', "Fecha desde");
      IF rHotTO.FIND('-') THEN BEGIN
      //  rHotTO.setfilter("Dia inicio"    , '>=%1&<%1', "dia inicio", "dia inicio" + estancia);
        REPEAT
          IF ((rHotTO."Dia inicio" + rHotTO.Estancia) > "Dia inicio") THEN BEGIN
            MESSAGE(Text001);
            EXIT(FALSE);
          END;
        UNTIL rHotTO.NEXT = 0;
      END;

      rHotTO.SETFILTER("Fecha desde", '<=%1', "Fecha hasta");
      rHotTO.SETFILTER("Fecha hasta", '>=%1', "Fecha hasta");
      IF rHotTO.FIND('-') THEN BEGIN
        REPEAT
          IF ((rHotTO."Dia inicio" + rHotTO.Estancia) > "Dia inicio") THEN BEGIN
            MESSAGE(Text001);
            EXIT(FALSE);
          END;
        UNTIL rHotTO.NEXT = 0;
      END;

      rHotTO.SETFILTER("Fecha desde", '>=%1', "Fecha desde");
      rHotTO.SETFILTER("Fecha hasta", '<=%1', "Fecha hasta");
      IF rHotTO.FIND('-') THEN BEGIN
        REPEAT
          IF ((rHotTO."Dia inicio" + rHotTO.Estancia) > "Dia inicio") THEN BEGIN
            MESSAGE(Text001);
            EXIT(FALSE);
          END;
        UNTIL rHotTO.NEXT = 0;
      END;

      EXIT(TRUE);
    END;

    PROCEDURE Comprobar_Tarifa@2() : Boolean;
    VAR
      rTar@1103355000 : Record 7010191;
    BEGIN
      IF NOT rTar.GET(Delegacion, "N§ Tarifa") THEN
        EXIT(FALSE);

      IF NOT rTar.Confirmado THEN BEGIN
        MESSAGE(Text002);
        EXIT(FALSE);
      END;

      IF "Fecha desde" < rTar."Fecha inicio" THEN BEGIN
        MESSAGE(Text003);
        EXIT(FALSE);
      END;

      IF "Fecha hasta" > rTar."Fecha final" THEN BEGIN
        MESSAGE(Text004);
        EXIT(FALSE);
      END;

      EXIT(TRUE);
    END;

    PROCEDURE Buscar_Hoteles_Tarifa@3(par_Delegacion@1103355000 : Code[10];par_Tarifa@1103355001 : Code[10];par_Circuito@1103355002 : Code[10];par_Categoria@1103355003 : Code[10];par_Fecha@1103355004 : Date;par_Mostrar_Error@1103355005 : Boolean) : Boolean;
    BEGIN
      // Buscar_Hoteles
      //
      RESET;
      SETRANGE (Delegacion      , par_Delegacion);
      SETRANGE ("N§ Tarifa"     , par_Tarifa);
      SETRANGE (Circuito        , par_Circuito);
      SETRANGE ("C¢d. Categoria", par_Categoria);
      SETFILTER("Fecha desde"   , '<=%1', par_Fecha);
      SETFILTER("Fecha hasta"   , '>=%1', par_Fecha);
      SETRANGE (Activo          , TRUE);
      SETCURRENTKEY("Dia inicio");
      IF NOT FIND('-') THEN BEGIN
        SETRANGE (Circuito      , '');
        IF NOT FIND('-') THEN BEGIN
          SETRANGE ("C¢d. Categoria", '');
          SETRANGE (Circuito        , par_Circuito);
          IF NOT FIND('-') THEN BEGIN
            SETRANGE (Circuito      , '');
            IF NOT FIND('-') THEN BEGIN
              IF par_Mostrar_Error THEN
                ERROR(Text005,
                      par_Tarifa, par_Circuito, par_Fecha, par_Categoria);
              EXIT(FALSE);
            END;
          END;
        END;
      END;

      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}
