OBJECT Table 5405 Production Order
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    DataCaptionFields=No.,Description;
    OnInsert=BEGIN
               MfgSetup.GET;
               IF "No." = '' THEN BEGIN
                 TestNoSeries;
                 NoSeriesMgt.InitSeries(GetNoSeriesCode,xRec."No. Series","Due Date","No.","No. Series");
               END;

               IF Status = Status::Released THEN
                 IF ProdOrder.GET(Status::Finished,"No.") THEN
                   ERROR(Text007,Status,TABLECAPTION,ProdOrder."No.",ProdOrder.Status);
               InitRecord;

               "Starting Time" := MfgSetup."Normal Starting Time";
               "Ending Time" := MfgSetup."Normal Ending Time";
               "Creation Date" := TODAY;
               UpdateDatetime;
               DimMgt.InsertProdDocDim(
                 DATABASE::"Production Order",Status,"No.",0,0,
                 "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;
               IF Status = Status::Finished THEN
                 ERROR(Text006);
             END;

    OnDelete=BEGIN
               IF Status IN [Status::Released,Status::Finished] THEN BEGIN
                 ItemLedgEntry.SETCURRENTKEY("Prod. Order No.");
                 ItemLedgEntry.SETRANGE("Prod. Order No.","No.");
                 IF ItemLedgEntry.FIND('-') THEN
                   ERROR(
                     Text000,
                     Status,TABLECAPTION,"No.",ItemLedgEntry.TABLECAPTION);

                 CapLedgEntry.SETCURRENTKEY("Prod. Order No.");
                 CapLedgEntry.SETRANGE("Prod. Order No.","No.");
                 IF CapLedgEntry.FIND('-') THEN
                   ERROR(
                     Text000,
                     Status,TABLECAPTION,"No.",CapLedgEntry.TABLECAPTION);
               END;

               IF Status = Status::Released THEN BEGIN
                 PurchLine.SETCURRENTKEY(
                   "Document Type",Type,"Prod. Order No.","Prod. Order Line No.","Routing No.","Operation No.");
                 PurchLine.SETRANGE("Document Type",PurchLine."Document Type"::Order);
                 PurchLine.SETRANGE(Type,PurchLine.Type::Item);
                 PurchLine.SETRANGE("Prod. Order No.","No.");
                 IF PurchLine.FIND('-') THEN
                   ERROR(
                     Text000,
                     Status,TABLECAPTION,"No.",PurchLine.TABLECAPTION);
               END;

               IF Status = Status::Finished THEN
                 DeleteFnshdProdOrderRelations
               ELSE
                 DeleteRelations;
               DimMgt.DeleteProdDocDim(DATABASE::"Production Order",Status,"No.",0,0);
             END;

    OnRename=BEGIN
               ERROR(Text001,TABLECAPTION);
             END;

    CaptionML=[ENU=Production Order;
               ESP=Orden producci¢n];
    LookupFormID=Form99000815;
    DrillDownFormID=Form99000815;
  }
  FIELDS
  {
    { 1   ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              ESP=Estado];
                                                   OptionCaptionML=[ENU=Simulated,Planned,Firm Planned,Released,Finished;
                                                                    ESP=Simulada,Planificada,Planif. en firme,Lanzada,Terminada];
                                                   OptionString=Simulated,Planned,Firm Planned,Released,Finished }
    { 2   ;   ;No.                 ;Code20        ;TableRelation="Production Order".No. WHERE (Status=FIELD(Status));
                                                   AltSearchField=Search Description;
                                                   OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  MfgSetup.GET;
                                                                  NoSeriesMgt.TestManual(GetNoSeriesCode);
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=No.;
                                                              ESP=N§] }
    { 3   ;   ;Description         ;Text50        ;OnValidate=BEGIN
                                                                "Search Description" := Description;
                                                              END;

                                                   CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n] }
    { 4   ;   ;Search Description  ;Code50        ;CaptionML=[ENU=Search Description;
                                                              ESP=Descripci¢n alias] }
    { 5   ;   ;Description 2       ;Text50        ;CaptionML=[ENU=Description 2;
                                                              ESP=Descripci¢n 2] }
    { 6   ;   ;Creation Date       ;Date          ;CaptionML=[ENU=Creation Date;
                                                              ESP=Fecha creaci¢n];
                                                   Editable=No }
    { 7   ;   ;Last Date Modified  ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              ESP=Fecha £lt. modificaci¢n];
                                                   Editable=No }
    { 9   ;   ;Source Type         ;Option        ;OnValidate=BEGIN
                                                                IF "Source Type" <> xRec."Source Type" THEN
                                                                  CheckProdOrderStatus(FIELDCAPTION("Source Type"));
                                                              END;

                                                   CaptionML=[ENU=Source Type;
                                                              ESP=Tipo procedencia mov.];
                                                   OptionCaptionML=[ENU=Item,Family,Sales Header;
                                                                    ESP=Producto,Familia,Cab. venta];
                                                   OptionString=Item,Family,Sales Header }
    { 10  ;   ;Source No.          ;Code20        ;TableRelation=IF (Source Type=CONST(Item)) Item
                                                                 ELSE IF (Source Type=CONST(Family)) Family
                                                                 ELSE IF (Status=CONST(Simulated),
                                                                          Source Type=CONST(Sales Header)) "Sales Header".No. WHERE (Document Type=CONST(Quote))
                                                                          ELSE IF (Status=FILTER(Planned..),
                                                                                   Source Type=CONST(Sales Header)) "Sales Header".No. WHERE (Document Type=CONST(Order));
                                                   OnValidate=VAR
                                                                Item@1000 : Record 27;
                                                                Family@1001 : Record 99000773;
                                                                SalesHeader@1002 : Record 36;
                                                              BEGIN
                                                                IF "Source No." <> xRec."Source No." THEN
                                                                  CheckProdOrderStatus(FIELDCAPTION("Source No."));

                                                                IF "Source No." = '' THEN
                                                                  EXIT;

                                                                CASE "Source Type" OF
                                                                  "Source Type"::Item:
                                                                    BEGIN
                                                                      Item.GET("Source No.");
                                                                      Item.TESTFIELD(Blocked,FALSE);
                                                                      Description := Item.Description;
                                                                      "Description 2" := Item."Description 2";
                                                                      "Routing No." := Item."Routing No.";
                                                                      "Inventory Posting Group" := Item."Inventory Posting Group";
                                                                      "Gen. Prod. Posting Group" := Item."Gen. Prod. Posting Group";
                                                                      "Unit Cost" := Item."Unit Cost";
                                                                      CreateDim(DATABASE::Item,"Source No.");
                                                                    END;
                                                                  "Source Type"::Family:
                                                                    BEGIN
                                                                      Family.GET("Source No.");
                                                                      Description := Family.Description;
                                                                      "Description 2" := Family."Description 2";
                                                                      "Routing No." := Family."Routing No.";
                                                                      "Inventory Posting Group" := '';
                                                                      "Gen. Prod. Posting Group" := '';
                                                                      "Unit Cost" := 0;
                                                                    END;
                                                                  "Source Type"::"Sales Header":
                                                                    BEGIN
                                                                      IF Status = Status::Simulated THEN
                                                                        SalesHeader.GET(SalesHeader."Document Type"::Quote,"Source No.")
                                                                      ELSE
                                                                        SalesHeader.GET(SalesHeader."Document Type"::Order,"Source No.");
                                                                      Description := SalesHeader."Ship-to Name";
                                                                      "Description 2" := SalesHeader."Ship-to Name 2";
                                                                      "Routing No." := '';
                                                                      "Inventory Posting Group" := '';
                                                                      "Gen. Prod. Posting Group" := '';
                                                                      "Gen. Bus. Posting Group" := SalesHeader."Gen. Bus. Posting Group";
                                                                      "Unit Cost" := 0;
                                                                      "Location Code" := SalesHeader."Location Code";
                                                                      "Due Date" := SalesHeader."Shipment Date";
                                                                      "Ending Date" := SalesHeader."Shipment Date";
                                                                      CreateDocDim(DATABASE::"Sales Header",SalesHeader."Document Type","Source No.",0);
                                                                    END;
                                                                END;
                                                                VALIDATE(Description);
                                                                InitRecord;
                                                                UpdateDatetime;
                                                              END;

                                                   CaptionML=[ENU=Source No.;
                                                              ESP=C¢d. procedencia mov.] }
    { 11  ;   ;Routing No.         ;Code20        ;TableRelation="Routing Header";
                                                   CaptionML=[ENU=Routing No.;
                                                              ESP=N§ ruta] }
    { 15  ;   ;Inventory Posting Group;Code10     ;TableRelation="Inventory Posting Group";
                                                   CaptionML=[ENU=Inventory Posting Group;
                                                              ESP=Grupo contable existencias] }
    { 16  ;   ;Gen. Prod. Posting Group;Code10    ;TableRelation="Gen. Product Posting Group";
                                                   CaptionML=[ENU=Gen. Prod. Posting Group;
                                                              ESP=Grupo contable producto] }
    { 17  ;   ;Gen. Bus. Posting Group;Code10     ;TableRelation="Gen. Business Posting Group";
                                                   CaptionML=[ENU=Gen. Bus. Posting Group;
                                                              ESP=Grupo contable negocio] }
    { 19  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Prod. Order Comment Line" WHERE (Status=FIELD(Status),
                                                                                                       Prod. Order No.=FIELD(No.)));
                                                   CaptionML=[ENU=Comment;
                                                              ESP=Comentario];
                                                   Editable=No }
    { 20  ;   ;Starting Time       ;Time          ;OnValidate=BEGIN
                                                                ProdOrderLine.SETCURRENTKEY(Status,"Prod. Order No.","Planning Level Code");
                                                                ProdOrderLine.ASCENDING(FALSE);
                                                                ProdOrderLine.SETRANGE(Status,Status);
                                                                ProdOrderLine.SETRANGE("Prod. Order No.","No.");
                                                                ProdOrderLine.SETFILTER("Item No.",'<>%1','');
                                                                ProdOrderLine.SETFILTER("Planning Level Code",'>%1',0);
                                                                IF ProdOrderLine.FIND('-') THEN BEGIN
                                                                  "Starting Date-Time" := CREATEDATETIME("Starting Date","Starting Time");
                                                                  MODIFY;
                                                                  MultiLevelMessage;
                                                                  EXIT;
                                                                END ELSE BEGIN
                                                                  "Due Date" := 0D;
                                                                  ProdOrderLine.SETRANGE("Planning Level Code");
                                                                  IF ProdOrderLine.FIND('-') THEN
                                                                    REPEAT
                                                                      ProdOrderLine."Starting Time" := "Starting Time";
                                                                      ProdOrderLine."Starting Date" := "Starting Date";
                                                                      ProdOrderLine.MODIFY;
                                                                      CalcProdOrder.SetParameter(TRUE);
                                                                      CalcProdOrder.Recalculate(ProdOrderLine,0);
                                                                      IF ProdOrderLine."Planning Level Code" > 0 THEN
                                                                        ProdOrderLine."Due Date" := ProdOrderLine."Ending Date"
                                                                      ELSE
                                                                        ProdOrderLine."Due Date" :=
                                                                          LeadTimeMgt.PlannedDueDate(
                                                                            ProdOrderLine."Item No.",
                                                                            ProdOrderLine."Location Code",
                                                                            ProdOrderLine."Variant Code",
                                                                            ProdOrderLine."Ending Date",
                                                                            '',
                                                                            2);

                                                                      IF "Due Date" = 0D THEN
                                                                        "Due Date" := ProdOrderLine."Due Date";
                                                                      "Starting Date-Time" := CREATEDATETIME("Starting Date","Starting Time");
                                                                      ProdOrderLine.MODIFY(TRUE);
                                                                      ProdOrderLine.CheckEndingDate(CurrFieldNo <> 0);
                                                                    UNTIL ProdOrderLine.NEXT = 0
                                                                  ELSE BEGIN
                                                                    "Ending Date" := "Starting Date";
                                                                    "Ending Time" := "Starting Time";
                                                                  END;
                                                                  AdjustStartEndingDate;
                                                                  MODIFY;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Starting Time;
                                                              ESP=Hora inicial] }
    { 21  ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                VALIDATE("Starting Time");
                                                              END;

                                                   CaptionML=[ENU=Starting Date;
                                                              ESP=Fecha inicial] }
    { 22  ;   ;Ending Time         ;Time          ;OnValidate=BEGIN
                                                                ProdOrderLine.SETCURRENTKEY(Status,"Prod. Order No.","Planning Level Code");
                                                                ProdOrderLine.ASCENDING(TRUE);
                                                                ProdOrderLine.SETRANGE(Status,Status);
                                                                ProdOrderLine.SETRANGE("Prod. Order No.","No.");
                                                                ProdOrderLine.SETFILTER("Item No.",'<>%1','');
                                                                ProdOrderLine.SETFILTER("Planning Level Code",'>%1',0);
                                                                IF ProdOrderLine.FIND('-') THEN BEGIN
                                                                  "Ending Date-Time" := CREATEDATETIME("Ending Date","Ending Time");
                                                                  MODIFY;
                                                                  MultiLevelMessage;
                                                                  EXIT;
                                                                END ELSE BEGIN
                                                                  "Due Date" := 0D;
                                                                  ProdOrderLine.SETRANGE("Planning Level Code");
                                                                  IF ProdOrderLine.FIND('-') THEN
                                                                    REPEAT
                                                                      ProdOrderLine."Ending Time" := "Ending Time";
                                                                      ProdOrderLine."Ending Date" := "Ending Date";
                                                                      ProdOrderLine.MODIFY;
                                                                      CalcProdOrder.SetParameter(TRUE);
                                                                      CalcProdOrder.Recalculate(ProdOrderLine,1);
                                                                      IF ProdOrderLine."Planning Level Code" > 0 THEN
                                                                        ProdOrderLine."Due Date" := ProdOrderLine."Ending Date"
                                                                      ELSE
                                                                        ProdOrderLine."Due Date" :=
                                                                          LeadTimeMgt.PlannedDueDate(
                                                                            ProdOrderLine."Item No.",
                                                                            ProdOrderLine."Location Code",
                                                                            ProdOrderLine."Variant Code",
                                                                            ProdOrderLine."Ending Date",
                                                                            '',
                                                                            2);
                                                                      IF "Due Date" = 0D THEN
                                                                        "Due Date" := ProdOrderLine."Due Date";
                                                                      "Ending Date-Time" := CREATEDATETIME("Ending Date","Ending Time");
                                                                      ProdOrderLine.MODIFY(TRUE);
                                                                      ProdOrderLine.CheckEndingDate(CurrFieldNo <> 0);
                                                                    UNTIL ProdOrderLine.NEXT = 0
                                                                  ELSE BEGIN
                                                                    "Starting Date" := "Ending Date";
                                                                    "Starting Time" := "Ending Time";
                                                                  END;
                                                                  AdjustStartEndingDate;
                                                                  MODIFY;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Ending Time;
                                                              ESP=Hora final] }
    { 23  ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                VALIDATE("Ending Time");
                                                              END;

                                                   CaptionML=[ENU=Ending Date;
                                                              ESP=Fecha final] }
    { 24  ;   ;Due Date            ;Date          ;OnValidate=BEGIN
                                                                IF "Due Date" = 0D THEN
                                                                  EXIT;
                                                                IF (CurrFieldNo = FIELDNO("Due Date")) OR
                                                                   (CurrFieldNo = FIELDNO("Location Code")) OR
                                                                   UpdateEndDate
                                                                THEN BEGIN
                                                                  ProdOrderLine.SETCURRENTKEY(Status,"Prod. Order No.","Planning Level Code");
                                                                  ProdOrderLine.ASCENDING(TRUE);
                                                                  ProdOrderLine.SETRANGE(Status,Status);
                                                                  ProdOrderLine.SETRANGE("Prod. Order No.","No.");
                                                                  ProdOrderLine.SETFILTER("Item No.",'<>%1','');
                                                                  ProdOrderLine.SETFILTER("Planning Level Code",'>%1',0);
                                                                  IF NOT ProdOrderLine.ISEMPTY THEN BEGIN
                                                                    ProdOrderLine.SETRANGE("Planning Level Code",0);
                                                                    IF "Source Type" = "Source Type"::Family THEN BEGIN
                                                                      UpdateEndingDate(ProdOrderLine);
                                                                    END ELSE BEGIN
                                                                      IF ProdOrderLine.FIND('-') THEN
                                                                        "Ending Date" :=
                                                                          LeadTimeMgt.PlannedEndingDate(ProdOrderLine."Item No.","Location Code",'',"Due Date",'',2)
                                                                      ELSE
                                                                        "Ending Date" := "Due Date";
                                                                      "Ending Date-Time" := CREATEDATETIME("Ending Date","Ending Time");
                                                                      MultiLevelMessage;
                                                                      EXIT;
                                                                    END;
                                                                  END ELSE BEGIN
                                                                    ProdOrderLine.SETRANGE("Planning Level Code");
                                                                    IF NOT ProdOrderLine.ISEMPTY THEN
                                                                      UpdateEndingDate(ProdOrderLine)
                                                                    ELSE BEGIN
                                                                      IF "Source Type" = "Source Type"::Item THEN
                                                                        "Ending Date" :=
                                                                          LeadTimeMgt.PlannedEndingDate(
                                                                            "Source No.",
                                                                            "Location Code",
                                                                            '',
                                                                            "Due Date",
                                                                            '',
                                                                            2)
                                                                      ELSE
                                                                        "Ending Date" := "Due Date";
                                                                      "Starting Date" := "Ending Date";
                                                                      "Starting Date-Time" := CREATEDATETIME("Starting Date","Starting Time");
                                                                      "Ending Date-Time" := CREATEDATETIME("Ending Date","Ending Time");
                                                                    END;
                                                                    AdjustStartEndingDate;
                                                                    MODIFY;
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Due Date;
                                                              ESP=Fecha vencimiento] }
    { 25  ;   ;Finished Date       ;Date          ;CaptionML=[ENU=Finished Date;
                                                              ESP=Fecha acabado];
                                                   Editable=No }
    { 28  ;   ;Blocked             ;Boolean       ;CaptionML=[ENU=Blocked;
                                                              ESP=Bloqueado] }
    { 30  ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
                                                              END;

                                                   CaptionML=[ENU=Shortcut Dimension 1 Code;
                                                              ESP=C¢d. dim. acceso dir. 1];
                                                   CaptionClass='1,2,1' }
    { 31  ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");
                                                              END;

                                                   CaptionML=[ENU=Shortcut Dimension 2 Code;
                                                              ESP=C¢d. dim. acceso dir. 2];
                                                   CaptionClass='1,2,2' }
    { 32  ;   ;Location Code       ;Code10        ;TableRelation=Location WHERE (Use As In-Transit=CONST(No));
                                                   OnValidate=BEGIN
                                                                GetDefaultBin;

                                                                VALIDATE("Due Date"); // Scheduling consider Calendar assigned to Location
                                                              END;

                                                   CaptionML=[ENU=Location Code;
                                                              ESP=C¢d. almac‚n] }
    { 33  ;   ;Bin Code            ;Code20        ;TableRelation=IF (Source Type=CONST(Item)) Bin.Code WHERE (Location Code=FIELD(Location Code),
                                                                                                              Item Filter=FIELD(Source No.))
                                                                                                              ELSE IF (Source Type=FILTER(<>Item)) Bin.Code WHERE (Location Code=FIELD(Location Code));
                                                   CaptionML=[ENU=Bin Code;
                                                              ESP=C¢d. ubicaci¢n] }
    { 34  ;   ;Replan Ref. No.     ;Code20        ;CaptionML=[ENU=Replan Ref. No.;
                                                              ESP=Replanificar n§ ref.];
                                                   Editable=No }
    { 35  ;   ;Replan Ref. Status  ;Option        ;CaptionML=[ENU=Replan Ref. Status;
                                                              ESP=Replanificar estado ref.];
                                                   OptionCaptionML=[ENU=Simulated,Planned,Firm Planned,Released,Finished;
                                                                    ESP=Simulada,Planificada,Planif. en firme,Lanzada,Terminada];
                                                   OptionString=Simulated,Planned,Firm Planned,Released,Finished;
                                                   Editable=No }
    { 38  ;   ;Low-Level Code      ;Integer       ;CaptionML=[ENU=Low-Level Code;
                                                              ESP=C¢d. nivel m s bajo];
                                                   Editable=No }
    { 40  ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                IF "Source Type" = "Source Type"::Item THEN
                                                                  "Cost Amount" := ROUND(Quantity * "Unit Cost")
                                                                ELSE
                                                                  "Cost Amount" := 0;
                                                              END;

                                                   CaptionML=[ENU=Quantity;
                                                              ESP=Cantidad];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 41  ;   ;Unit Cost           ;Decimal       ;CaptionML=[ENU=Unit Cost;
                                                              ESP=Coste unitario];
                                                   DecimalPlaces=2:5 }
    { 42  ;   ;Cost Amount         ;Decimal       ;CaptionML=[ENU=Cost Amount;
                                                              ESP=Importe coste];
                                                   DecimalPlaces=2:2 }
    { 47  ;   ;Work Center Filter  ;Code20        ;FieldClass=FlowFilter;
                                                   TableRelation="Work Center";
                                                   CaptionML=[ENU=Work Center Filter;
                                                              ESP=Filtro centro trab.] }
    { 48  ;   ;Capacity Type Filter;Option        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Capacity Type Filter;
                                                              ESP=Filtro tipo capacidad];
                                                   OptionCaptionML=[ENU=Work Center,Machine Center;
                                                                    ESP=Centro trabajo,Centro m quina];
                                                   OptionString=Work Center,Machine Center }
    { 49  ;   ;Capacity No. Filter ;Code20        ;FieldClass=FlowFilter;
                                                   TableRelation=IF (Capacity Type Filter=CONST(Work Center)) "Machine Center"
                                                                 ELSE IF (Capacity Type Filter=CONST(Machine Center)) "Work Center";
                                                   CaptionML=[ENU=Capacity No. Filter;
                                                              ESP=Filtro n§ capacidad] }
    { 50  ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Date Filter;
                                                              ESP=Filtro fecha] }
    { 51  ;   ;Expected Operation Cost Amt.;Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("Prod. Order Routing Line"."Expected Operation Cost Amt." WHERE (Status=FIELD(Status),
                                                                                                                                    Prod. Order No.=FIELD(No.)));
                                                   CaptionML=[ENU=Expected Operation Cost Amt.;
                                                              ESP=Coste operaci¢n previsto];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 52  ;   ;Expected Component Cost Amt.;Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("Prod. Order Component"."Cost Amount" WHERE (Status=FIELD(Status),
                                                                                                                Prod. Order No.=FIELD(No.),
                                                                                                                Due Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Expected Component Cost Amt.;
                                                              ESP=Coste componente previsto];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 55  ;   ;Actual Time Used    ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Capacity Ledger Entry".Quantity WHERE (Prod. Order No.=FIELD(No.),
                                                                                                           Type=FIELD(Capacity Type Filter),
                                                                                                           No.=FIELD(Capacity No. Filter),
                                                                                                           Posting Date=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Actual Time Used;
                                                              ESP=Tiempo consumido real];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 56  ;   ;Allocated Capacity Need;Decimal    ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Prod. Order Capacity Need"."Needed Time" WHERE (Status=FIELD(Status),
                                                                                                                    Prod. Order No.=FIELD(No.),
                                                                                                                    Type=FIELD(Capacity Type Filter),
                                                                                                                    No.=FIELD(Capacity No. Filter),
                                                                                                                    Work Center No.=FIELD(Work Center Filter),
                                                                                                                    Date=FIELD(Date Filter),
                                                                                                                    Requested Only=CONST(No)));
                                                   CaptionML=[ENU=Allocated Capacity Need;
                                                              ESP=Nec. capacidad asignada];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 57  ;   ;Expected Capacity Need;Decimal     ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Prod. Order Capacity Need"."Needed Time" WHERE (Status=FIELD(Status),
                                                                                                                    Prod. Order No.=FIELD(No.),
                                                                                                                    Type=FIELD(Capacity Type Filter),
                                                                                                                    No.=FIELD(Capacity No. Filter),
                                                                                                                    Work Center No.=FIELD(Work Center Filter),
                                                                                                                    Date=FIELD(Date Filter),
                                                                                                                    Requested Only=CONST(No)));
                                                   CaptionML=[ENU=Expected Capacity Need;
                                                              ESP=Nec. capacidad prevista];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 80  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              ESP=Nos. serie];
                                                   Editable=No }
    { 82  ;   ;Planned Order No.   ;Code20        ;CaptionML=[ENU=Planned Order No.;
                                                              ESP=N§ orden planif.] }
    { 83  ;   ;Firm Planned Order No.;Code20      ;CaptionML=[ENU=Firm Planned Order No.;
                                                              ESP=N§ orden planif. en firme] }
    { 85  ;   ;Simulated Order No. ;Code20        ;CaptionML=[ENU=Simulated Order No.;
                                                              ESP=N§ orden simulada] }
    { 92  ;   ;Expected Material Ovhd. Cost;Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("Prod. Order Component"."Overhead Amount" WHERE (Status=FIELD(Status),
                                                                                                                    Prod. Order No.=FIELD(No.)));
                                                   CaptionML=[ENU=Expected Material Ovhd. Cost;
                                                              ESP=Costes gen. material previstos];
                                                   DecimalPlaces=2:2;
                                                   Editable=No }
    { 94  ;   ;Expected Capacity Ovhd. Cost;Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("Prod. Order Routing Line"."Expected Capacity Ovhd. Cost" WHERE (Status=FIELD(Status),
                                                                                                                                    Prod. Order No.=FIELD(No.)));
                                                   CaptionML=[ENU=Expected Capacity Ovhd. Cost;
                                                              ESP=Costes gen. capacidad prev.];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 98  ;   ;Starting Date-Time  ;DateTime      ;OnValidate=BEGIN
                                                                "Starting Date" := DT2DATE("Starting Date-Time");
                                                                "Starting Time" := DT2TIME("Starting Date-Time");
                                                                VALIDATE("Starting Time");
                                                              END;

                                                   CaptionML=[ENU=Starting Date-Time;
                                                              ESP=Fecha-hora inicial] }
    { 99  ;   ;Ending Date-Time    ;DateTime      ;OnValidate=BEGIN
                                                                "Ending Date" := DT2DATE("Ending Date-Time");
                                                                "Ending Time" := DT2TIME("Ending Date-Time");
                                                                VALIDATE("Ending Time");
                                                              END;

                                                   CaptionML=[ENU=Ending Date-Time;
                                                              ESP=Fecha-hora final] }
    { 7300;   ;Completely Picked   ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Min("Prod. Order Component"."Completely Picked" WHERE (Status=FIELD(Status),
                                                                                                                      Prod. Order No.=FIELD(No.)));
                                                   CaptionML=[ENU=Completely Picked;
                                                              ESP=Picking completo] }
    { 9000;   ;Assigned User ID    ;Code20        ;TableRelation="User Setup";
                                                   CaptionML=[ENU=Assigned User ID;
                                                              ESP=Id. usuario asignado] }
  }
  KEYS
  {
    {    ;Status,No.                              ;Clustered=Yes }
    {    ;No.,Status                               }
    {    ;Search Description                       }
    {    ;Low-Level Code,Replan Ref. No.,Replan Ref. Status }
    { No ;Source Type,Source No.                   }
    {    ;Description                             ;KeyGroups=SearchProd }
    {    ;Source No.                              ;KeyGroups=SearchProd }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;No.,Description,Source No.,Source Type   }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You cannot delete %1 %2 %3 because there exists at least one %4 associated with it.;ESP=No puede borrar %1 %2 %3 porque existe al menos un %4 asociado a ‚l.';
      Text001@1001 : TextConst 'ENU=You cannot rename a %1.;ESP=No se puede cambiar el nombre a %1.';
      Text002@1002 : TextConst 'ENU=You can not change %1 on %2 %3 %4, because there exists at least one %5 associated with it.;ESP=No puede cambiar %1 en %2 %3 %4, porque existe al menos un %5 asociado a ‚l.';
      Text003@1003 : TextConst 'ENU=The production order contains lines connected in a multi-level structure and the production order lines have not been automatically rescheduled.\;ESP=El ped. producci¢n contiene l¡ns. conectadas con una estructura multinivel y las l¡ns. pedido producci¢n no se han replanificado autom ticamente.\';
      Text005@1005 : TextConst 'ENU=Use Refresh if you want to reschedule the lines.;ESP=Utilice Actualizar si quiere reprogramar las l¡neas.';
      MfgSetup@1006 : Record 99000765;
      ProdOrder@1007 : Record 5405;
      ProdOrderLine@1008 : Record 5406;
      CapLedgEntry@1009 : Record 5832;
      ItemLedgEntry@1015 : Record 32;
      Location@1004 : Record 14;
      PurchLine@1021 : Record 39;
      NoSeriesMgt@1010 : Codeunit 396;
      CalcProdOrder@1011 : Codeunit 99000773;
      LeadTimeMgt@1013 : Codeunit 5404;
      DimMgt@1014 : Codeunit 408;
      Text006@1016 : TextConst 'ENU=A Finished Production Order cannot be modified.;ESP=Una O.P. Terminada no puede modificarse.';
      Text007@1017 : TextConst 'ENU=%1 %2 %3 cannot be created, because a %4 %2 %3 already exists.;ESP=%1 %2 %3 no se puede crear, porque ya existe un %4 %2 %3.';
      ItemTrackingMgt@1020 : Codeunit 6500;
      HideValidationDialog@1018 : Boolean;
      Text008@1019 : TextConst 'ENU=Nothing to handle.;ESP=Nada a manipular.';
      Text009@1022 : TextConst 'ENU=The %1 %2 %3 has item tracking. Do you want to delete it anyway?;ESP=%1 %2 %3 tiene seguimiento de productos. ¨Desea eliminarlo de todas maneras?';
      UpdateEndDate@1012 : Boolean;

    PROCEDURE InitRecord@10();
    BEGIN
      IF "Due Date" = 0D THEN
        VALIDATE("Due Date",WORKDATE);
      IF ("Source Type" = "Source Type"::Item) AND ("Source No." <> '') THEN
        "Ending Date" :=
          LeadTimeMgt.PlannedEndingDate(
            "Source No.",
            "Location Code",
            '',
            "Due Date",
            '',
            2)
      ELSE
        "Ending Date" := "Due Date";
      "Starting Date" := "Ending Date";
      "Starting Date-Time" := CREATEDATETIME("Starting Date","Starting Time");
      "Ending Date-Time" := CREATEDATETIME("Ending Date","Ending Time");
    END;

    PROCEDURE TestNoSeries@5();
    BEGIN
      MfgSetup.GET;

      CASE Status OF
        Status::Simulated:
          MfgSetup.TESTFIELD("Simulated Order Nos.");
        Status::Planned:
          MfgSetup.TESTFIELD("Planned Order Nos.");
        Status::"Firm Planned":
          MfgSetup.TESTFIELD("Firm Planned Order Nos.");
        Status::Released:
          MfgSetup.TESTFIELD("Released Order Nos.");
      END;
    END;

    PROCEDURE AssistEdit@7(OldProdOrder@1000 : Record 5405) : Boolean;
    BEGIN
      WITH ProdOrder DO BEGIN
        ProdOrder := Rec;
        MfgSetup.GET;
        TestNoSeries;
        IF NoSeriesMgt.SelectSeries(GetNoSeriesCode,OldProdOrder."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("No.");
          Rec := ProdOrder;
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE GetNoSeriesCode@6() : Code[10];
    BEGIN
      MfgSetup.GET;

      CASE Status OF
        Status::Simulated:
          EXIT(MfgSetup."Simulated Order Nos.");
        Status::Planned:
          EXIT(MfgSetup."Planned Order Nos.");
        Status::"Firm Planned":
          EXIT(MfgSetup."Firm Planned Order Nos.");
        Status::Released:
          EXIT(MfgSetup."Released Order Nos.");
      END;
    END;

    PROCEDURE CheckProdOrderStatus@3(Name@1000 : Text[80]);
    BEGIN
      IF Status <> Status::Released THEN
        EXIT;

      IF Status IN [Status::Released,Status::Finished] THEN BEGIN
        ItemLedgEntry.SETCURRENTKEY("Prod. Order No.");
        ItemLedgEntry.SETRANGE("Prod. Order No.","No.");
        IF ItemLedgEntry.FIND('-') THEN
          ERROR(
            Text002,
            Name,Status,TABLECAPTION,"No.",ItemLedgEntry.TABLECAPTION);

        CapLedgEntry.SETCURRENTKEY("Prod. Order No.");
        CapLedgEntry.SETRANGE("Prod. Order No.","No.");
        IF CapLedgEntry.FIND('-') THEN
          ERROR(
            Text002,
            Name,Status,TABLECAPTION,"No.",CapLedgEntry.TABLECAPTION);
      END;
    END;

    PROCEDURE DeleteRelations@2();
    VAR
      ProdOrderComment@1000 : Record 5414;
      WhseRequest@1001 : Record 7325;
    BEGIN
      ProdOrderComment.SETRANGE(Status,Status);
      ProdOrderComment.SETRANGE("Prod. Order No.","No.");
      ProdOrderComment.DELETEALL;

      HandleItemTrackingDeletion;

      ProdOrderLine.LOCKTABLE;
      ProdOrderLine.SETRANGE(Status,Status);
      ProdOrderLine.SETRANGE("Prod. Order No.","No.");
      ProdOrderLine.DELETEALL(TRUE);

      WhseRequest.SETRANGE("Document Type",WhseRequest."Document Type"::Production);
      WhseRequest.SETRANGE("Production Subtype",Status);
      WhseRequest.SETRANGE("Document No.","No.");
      WhseRequest.DELETEALL(TRUE);
      ItemTrackingMgt.DeleteWhseItemTrkgLines(
        DATABASE::"Prod. Order Component",Status,"No.",'',0,0,'',FALSE);
    END;

    PROCEDURE DeleteFnshdProdOrderRelations@12();
    VAR
      FnshdProdOrderRtngLine@1001 : Record 5409;
      FnshdProdOrderLine@1003 : Record 5406;
      FnshdProdOrderComp@1004 : Record 5407;
      FnshdProdOrderRtngTool@1005 : Record 5411;
      FnshdProdOrderRtngPers@1006 : Record 5412;
      FnshdProdOrderRtngQltyMeas@1007 : Record 5413;
      FnshdProdOrderComment@1008 : Record 5414;
      FnshdProdOrderRtngCmt@1009 : Record 5415;
      FnshdProdOrderBOMComment@1010 : Record 5416;
      FnshdProdDocDim@1000 : Record 358;
    BEGIN
      FnshdProdOrderRtngLine.SETRANGE(Status,Status);
      FnshdProdOrderRtngLine.SETRANGE("Prod. Order No.","No.");
      FnshdProdOrderRtngLine.DELETEALL;

      FnshdProdOrderLine.SETRANGE(Status,Status);
      FnshdProdOrderLine.SETRANGE("Prod. Order No.","No.");
      FnshdProdOrderLine.DELETEALL;

      FnshdProdOrderComp.SETRANGE(Status,Status);
      FnshdProdOrderComp.SETRANGE("Prod. Order No.","No.");
      FnshdProdOrderComp.DELETEALL;

      FnshdProdOrderRtngTool.SETRANGE(Status,Status);
      FnshdProdOrderRtngTool.SETRANGE("Prod. Order No.","No.");
      FnshdProdOrderRtngTool.DELETEALL;

      FnshdProdOrderRtngPers.SETRANGE(Status,Status);
      FnshdProdOrderRtngPers.SETRANGE("Prod. Order No.","No.");
      FnshdProdOrderRtngPers.DELETEALL;

      FnshdProdOrderRtngQltyMeas.SETRANGE(Status,Status);
      FnshdProdOrderRtngQltyMeas.SETRANGE("Prod. Order No.","No.");
      FnshdProdOrderRtngQltyMeas.DELETEALL;

      FnshdProdOrderComment.SETRANGE(Status,Status);
      FnshdProdOrderComment.SETRANGE("Prod. Order No.","No.");
      FnshdProdOrderComment.DELETEALL;

      FnshdProdOrderRtngCmt.SETRANGE(Status,Status);
      FnshdProdOrderRtngCmt.SETRANGE("Prod. Order No.","No.");
      FnshdProdOrderRtngCmt.DELETEALL;

      FnshdProdOrderBOMComment.SETRANGE(Status,Status);
      FnshdProdOrderBOMComment.SETRANGE("Prod. Order No.","No.");
      FnshdProdOrderBOMComment.DELETEALL;

      FnshdProdDocDim.SETRANGE("Document Status",Status);
      FnshdProdDocDim.SETRANGE("Document No.","No.");
      FnshdProdDocDim.DELETEALL;
    END;

    PROCEDURE HandleItemTrackingDeletion@36();
    VAR
      ReservEntry@1000 : Record 337;
      ReservEntry2@1002 : Record 337;
      Confirmed@1001 : Boolean;
    BEGIN
      WITH ReservEntry DO BEGIN
        ProdOrder := Rec;
        RESET;
        SETCURRENTKEY(
          "Source ID","Source Ref. No.","Source Type","Source Subtype",
          "Source Batch Name","Source Prod. Order Line","Reservation Status");
        SETFILTER("Source Type",'%1|%2',DATABASE::"Prod. Order Line",DATABASE::"Prod. Order Component");
        SETRANGE("Source Subtype",ProdOrder.Status);
        SETRANGE("Source ID",ProdOrder."No.");
        SETRANGE("Source Batch Name",'');
        SETFILTER("Item Tracking",'> %1',"Item Tracking"::None);
        IF ISEMPTY THEN
          EXIT;

        IF HideValidationDialog OR NOT GUIALLOWED THEN
          Confirmed := TRUE
        ELSE
          Confirmed := CONFIRM(Text009,FALSE,ProdOrder.Status,ProdOrder.TABLECAPTION,ProdOrder."No.");

        IF NOT Confirmed THEN
          ERROR('');

        IF FINDSET THEN
          REPEAT
            ReservEntry2 := ReservEntry;
            ReservEntry2.ClearItemTrackingFields;
            ReservEntry2.MODIFY;
          UNTIL NEXT = 0;
      END;
    END;

    PROCEDURE AdjustStartEndingDate@1();
    BEGIN
      ProdOrderLine.RESET;
      ProdOrderLine.SETRANGE(Status,Status);
      ProdOrderLine.SETRANGE("Prod. Order No.","No.");

      IF NOT ProdOrderLine.FIND('-') THEN
        EXIT;

      "Starting Date" := 31129999D;
      "Starting Time" := 235959T;
      "Ending Date" := 0D;
      "Ending Time" := 000000T;

      REPEAT
        IF (ProdOrderLine."Starting Date" < "Starting Date") OR
           ((ProdOrderLine."Starting Date" = "Starting Date") AND
            (ProdOrderLine."Starting Time" < "Starting Time"))
        THEN BEGIN
          "Starting Time" := ProdOrderLine."Starting Time";
          "Starting Date" := ProdOrderLine."Starting Date";
        END;
        IF (ProdOrderLine."Ending Date" > "Ending Date") OR
           ((ProdOrderLine."Ending Date" = "Ending Date") AND
            (ProdOrderLine."Ending Time" > "Ending Time"))
        THEN BEGIN
          "Ending Time" := ProdOrderLine."Ending Time";
          "Ending Date" := ProdOrderLine."Ending Date";
        END;
      UNTIL ProdOrderLine.NEXT = 0;
      UpdateDatetime;
    END;

    PROCEDURE MultiLevelMessage@4();
    BEGIN
      MESSAGE(
        Text003 +
        Text005);
    END;

    PROCEDURE UpdateDatetime@11();
    BEGIN
      "Starting Date-Time" := CREATEDATETIME("Starting Date","Starting Time");
      "Ending Date-Time" := CREATEDATETIME("Ending Date","Ending Time");
    END;

    PROCEDURE CreateDim@8(Type1@1000 : Integer;No1@1001 : Code[20]);
    VAR
      TableID@1002 : ARRAY [10] OF Integer;
      No@1003 : ARRAY [10] OF Code[20];
    BEGIN
      TableID[1] := Type1;
      No[1] := No1;
      "Shortcut Dimension 1 Code" := '';
      "Shortcut Dimension 2 Code" := '';
      DimMgt.GetDefaultDim(
        TableID,No,'',
        "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
      IF "No." <> '' THEN
        DimMgt.UpdateProdDocDefaultDim(
          DATABASE::"Production Order",
          Status,"No.",0,0,
          "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
    END;

    PROCEDURE CreateDocDim@14(TableID@1000 : Integer;DocType@1001 : Option;DocNo@1002 : Code[20];LineNo@1003 : Integer);
    BEGIN
      "Shortcut Dimension 1 Code" := '';
      "Shortcut Dimension 2 Code" := '';
      DimMgt.GetDocDim(
        TableID,DocType,DocNo,LineNo,'',
        "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
      IF "No." <> '' THEN
        DimMgt.UpdateProdDocDefaultDim(
          DATABASE::"Production Order",
          Status,"No.",0,0,
          "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
    END;

    PROCEDURE ValidateShortcutDimCode@13(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateDimValueCode(FieldNumber,ShortcutDimCode);
      IF "No." <> '' THEN BEGIN
        DimMgt.SaveProdDocDim(
          DATABASE::"Production Order",Status,"No.",
          0,0,FieldNumber,ShortcutDimCode);
        MODIFY;
      END ELSE
        DimMgt.SaveTempDim(FieldNumber,ShortcutDimCode);
    END;

    PROCEDURE Navigate@9();
    VAR
      NavigateForm@1000 : Form 344;
    BEGIN
      NavigateForm.SetDoc("Due Date","No.");
      NavigateForm.RUN;
    END;

    PROCEDURE CreatePick@7300(ProdOrderHeader@1000 : Record 5405);
    VAR
      ProdOrderCompLine@1001 : Record 5407;
      CreatePickFromWhseSource@1002 : Report 7305;
    BEGIN
      ProdOrderHeader.TESTFIELD(Status,ProdOrderHeader.Status::Released);
      ProdOrderHeader.CALCFIELDS("Completely Picked");
      IF ProdOrderHeader."Completely Picked" THEN
        ERROR(Text008);

      ProdOrderCompLine.SETRANGE(Status,ProdOrderHeader.Status);
      ProdOrderCompLine.SETRANGE("Prod. Order No.",ProdOrderHeader."No.");
      ProdOrderCompLine.SETFILTER(
        "Flushing Method",'%1|%2|%3',
        ProdOrderCompLine."Flushing Method"::Manual,
        ProdOrderCompLine."Flushing Method"::"Pick + Forward",
        ProdOrderCompLine."Flushing Method"::"Pick + Backward");
      ProdOrderCompLine.SETRANGE("Planning Level Code",0);
      ProdOrderCompLine.SETFILTER("Expected Quantity",'>0');
      IF ProdOrderCompLine.FIND('-') THEN BEGIN
        CreatePickFromWhseSource.SetProdOrder(ProdOrderHeader);
        CreatePickFromWhseSource.SetHideValidationDialog(HideValidationDialog);
        CreatePickFromWhseSource.USEREQUESTFORM(NOT HideValidationDialog);
        CreatePickFromWhseSource.RUNMODAL;
        CreatePickFromWhseSource.GetResultMessage(2);
        CLEAR(CreatePickFromWhseSource);
      END ELSE
        IF NOT HideValidationDialog THEN
          MESSAGE(Text008);
    END;

    PROCEDURE SetHideValidationDialog@7301(NewHideValidationDialog@1000 : Boolean);
    BEGIN
      HideValidationDialog := NewHideValidationDialog;
    END;

    PROCEDURE CreateInvtPutAwayPick@29();
    VAR
      WhseRequest@1000 : Record 5765;
    BEGIN
      TESTFIELD(Status,Status::Released);

      WhseRequest.RESET;
      WhseRequest.SETCURRENTKEY("Source Document","Source No.");
      WhseRequest.SETFILTER(
        "Source Document",'%1|%2',
        WhseRequest."Source Document"::"Prod. Consumption",
        WhseRequest."Source Document"::"Prod. Output");
      WhseRequest.SETRANGE("Source No.","No.");
      REPORT.RUNMODAL(REPORT::"Create Invt. Put-away / Pick",TRUE,FALSE,WhseRequest);
    END;

    LOCAL PROCEDURE GetDefaultBin@50();
    VAR
      WMSManagement@1000 : Codeunit 7302;
    BEGIN
      "Bin Code" := '';
      IF "Source Type" <> "Source Type"::Item THEN
        EXIT;

      IF ("Location Code" <> '') AND ("Source No." <> '') THEN BEGIN
        GetLocation("Location Code");
        IF Location."Bin Mandatory" AND NOT Location."Directed Put-away and Pick" THEN
          WMSManagement.GetDefaultBin("Source No.",'',"Location Code","Bin Code");
      END;
    END;

    LOCAL PROCEDURE GetLocation@15(LocationCode@1000 : Code[10]);
    BEGIN
      IF Location.Code <> LocationCode THEN
        Location.GET(LocationCode);
    END;

    PROCEDURE SetUpdateEndDate@16();
    BEGIN
      UpdateEndDate := TRUE;
    END;

    LOCAL PROCEDURE UpdateEndingDate@17(VAR ProdOrderLine@1000 : Record 5406);
    BEGIN
      IF ProdOrderLine.FINDSET(TRUE) THEN
        REPEAT
          ProdOrderLine."Due Date" := "Due Date";
          ProdOrderLine.MODIFY;
          CalcProdOrder.SetParameter(TRUE);
          ProdOrderLine."Ending Date" :=
            LeadTimeMgt.PlannedEndingDate(
              ProdOrderLine."Item No.",
              ProdOrderLine."Location Code",
              ProdOrderLine."Variant Code",
              ProdOrderLine."Due Date",
              '',
              2);
          CalcProdOrder.Recalculate(ProdOrderLine,1);
          "Starting Date-Time" := CREATEDATETIME("Starting Date","Starting Time");
          "Ending Date-Time" := CREATEDATETIME("Ending Date","Ending Time");
          ProdOrderLine.MODIFY(TRUE);
          ProdOrderLine.CheckEndingDate(CurrFieldNo <> 0);
        UNTIL ProdOrderLine.NEXT = 0
    END;

    BEGIN
    END.
  }
}
