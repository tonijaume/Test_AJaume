OBJECT Table 7010254 Calendario excursion
{
  OBJECT-PROPERTIES
  {
    Date=25/06/15;
    Time=18:10:43;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               _CheckSolape;
             END;

    OnModify=BEGIN
               _CheckSolape;
             END;

    OnDelete=BEGIN
               _DeleteDetails; //+$005
             END;

    OnRename=BEGIN
               _CheckSolape;
             END;

    CaptionML=[ENU=Excursion schedule;
               ESP=Calendario excursion];
    LookupFormID=Form7010224;
  }
  FIELDS
  {
    { 1   ;   ;Excursion           ;Code10        ;CaptionML=[ENU=Excursion;
                                                              ESP=Excursion];
                                                   NotBlank=Yes }
    { 3   ;   ;Fecha inicio        ;Date          ;OnValidate=BEGIN
                                                                _CheckDates;
                                                              END;

                                                   CaptionML=[ENU=From date;
                                                              ESP=Fecha inicio];
                                                   NotBlank=Yes }
    { 5   ;   ;Fecha final         ;Date          ;OnValidate=BEGIN
                                                                _CheckDates;
                                                              END;

                                                   CaptionML=[ENU=To Date;
                                                              ESP=Fecha final];
                                                   NotBlank=Yes }
    { 7   ;   ;Lunes               ;Boolean       ;CaptionML=[ENU=Monday;
                                                              ESP=Lunes] }
    { 9   ;   ;Martes              ;Boolean       ;CaptionML=[ENU=Tuesday;
                                                              ESP=Martes] }
    { 11  ;   ;Miercoles           ;Boolean       ;CaptionML=[ENU=Wednesday;
                                                              ESP=Miercoles] }
    { 13  ;   ;Jueves              ;Boolean       ;CaptionML=[ENU=Thursday;
                                                              ESP=Jueves] }
    { 15  ;   ;Viernes             ;Boolean       ;CaptionML=[ENU=Friday;
                                                              ESP=Viernes] }
    { 17  ;   ;Sabado              ;Boolean       ;CaptionML=[ENU=Saturday;
                                                              ESP=Sabado] }
    { 19  ;   ;Domingo             ;Boolean       ;CaptionML=[ENU=Sunday;
                                                              ESP=Domingo] }
    { 21  ;   ;Hora inicio         ;Time          ;CaptionML=[ENU=Starting time;
                                                              ESP=Hora inicio] }
    { 23  ;   ;Turno               ;Integer       ;InitValue=1;
                                                   CaptionML=[ENU=Turn;
                                                              ESP=Turno];
                                                   MinValue=0;
                                                   Description=$001 }
    { 25  ;   ;Tiene idiomas       ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Idiomas x excursion" WHERE (Excursion=FIELD(Excursion),
                                                                                                  Fecha inicio=FIELD(Fecha inicio),
                                                                                                  Fecha final=FIELD(Fecha final),
                                                                                                  Turno=FIELD(Turno)));
                                                   CaptionML=[ENU=Languages;
                                                              ESP=Tiene idiomas];
                                                   Description=$004 }
    { 27  ;   ;Tiene cupos         ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Cupo excursion" WHERE (Excursion=FIELD(Excursion),
                                                                                             Turno=FIELD(Turno)));
                                                   CaptionML=[ENU=Allotments;
                                                              ESP=Tiene cupos];
                                                   Description=$004 }
    { 29  ;   ;Tiene recogidas     ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Recogidas excursion x hotel" WHERE (Excursion=FIELD(Excursion),
                                                                                                          Turno=FIELD(Turno)));
                                                   CaptionML=[ENU=Pick up;
                                                              ESP=Tiene recogidas];
                                                   Description=$004 }
  }
  KEYS
  {
    {    ;Excursion,Fecha inicio,Fecha final,Turno;Clustered=Yes }
    {    ;Excursion,Turno                          }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100217000 : TextConst 'ENU=The period of dates is incorrect.;ESP=El periodo de fechas especificado es incorrecto.';
      Text002@1100217001 : TextConst 'ENU="This calendar overlaps with another one. ";ESP=Este calendario se solapa con otro ya existente.';

    LOCAL PROCEDURE _CheckDates@1100217000();
    BEGIN
      // _CheckDates

      IF ("Fecha inicio" = 0D) OR
         ("Fecha final" = 0D) THEN
        EXIT;

      IF "Fecha inicio" > "Fecha final" THEN
        ERROR(Text001);
    END;

    LOCAL PROCEDURE _CheckSolape@1100217001();
    VAR
      lrCalendar@1100217000 : Record 7010254;
    BEGIN
      // _CheckSolape

      IF ("Fecha inicio" = 0D) OR
         ("Fecha final" = 0D) THEN
        EXIT;

      lrCalendar.RESET;
      lrCalendar.SETRANGE (Excursion     , Excursion);
      lrCalendar.SETRANGE (Turno         , Turno);
      lrCalendar.SETFILTER("Fecha inicio", '<=%1', "Fecha final");
      lrCalendar.SETFILTER("Fecha final" , '>=%1', "Fecha inicio");
      IF lrCalendar.FINDFIRST THEN
      BEGIN
        REPEAT
          IF (lrCalendar."Fecha inicio" <> xRec."Fecha inicio") OR
             (lrCalendar."Fecha final" <> xRec."Fecha final") THEN
          ERROR(Text002);
        UNTIL lrCalendar.NEXT = 0;
      END;
    END;

    PROCEDURE GetNumTurnos@1000000000(vExc@1000000001 : Code[10];vFec@1000000002 : Date) vCont : Integer;
    VAR
      lrCalendar@1000000000 : Record 7010254;
      vTurno@1000000003 : Integer;
    BEGIN
      // $001

      vCont := 0;
      lrCalendar.RESET;
      lrCalendar.SETCURRENTKEY(lrCalendar.Excursion,lrCalendar.Turno);
      lrCalendar.SETRANGE (Excursion     , vExc);
      lrCalendar.SETFILTER("Fecha inicio", '<=%1', vFec);
      lrCalendar.SETFILTER("Fecha final" , '>=%1', vFec);
      CASE DATE2DWY(vFec, 1) OF
        1: lrCalendar.SETRANGE(lrCalendar.Lunes, TRUE);
        2: lrCalendar.SETRANGE(lrCalendar.Martes, TRUE);
        3: lrCalendar.SETRANGE(lrCalendar.Miercoles, TRUE);
        4: lrCalendar.SETRANGE(lrCalendar.Jueves, TRUE);
        5: lrCalendar.SETRANGE(lrCalendar.Viernes, TRUE);
        6: lrCalendar.SETRANGE(lrCalendar.Sabado, TRUE);
        7: lrCalendar.SETRANGE(lrCalendar.Domingo, TRUE);
      END;
      IF lrCalendar.FINDFIRST THEN BEGIN
        vTurno := 0;
        REPEAT
          IF vTurno <> lrCalendar.Turno THEN BEGIN
            vTurno := lrCalendar.Turno;
            vCont += 1;
          END;
        UNTIL lrCalendar.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE _DeleteDetails@1000000001();
    VAR
      lrCupo@1000000000 : Record 7010265;
      lrRecogida@1000000001 : Record 7010184;
      lrIdioma@1000000002 : Record 7010002;
    BEGIN
      // _DeleteDetails

      lrCupo.RESET;
      lrCupo.SETRANGE(Excursion, Excursion);
      lrCupo.SETRANGE(Turno    , Turno);
      IF lrCupo.FINDSET(TRUE) THEN
        lrCupo.DELETEALL;

      lrRecogida.RESET;
      lrRecogida.SETRANGE(Excursion, Excursion);
      lrRecogida.SETRANGE(Turno    , Turno);
      IF lrRecogida.FINDSET(TRUE) THEN
        lrRecogida.DELETEALL;

      lrIdioma.RESET;
      lrIdioma.SETRANGE(Excursion, Excursion);
      lrIdioma.SETRANGE(Turno    , Turno);
      IF lrIdioma.FINDSET(TRUE) THEN
        lrIdioma.DELETEALL;
    END;

    BEGIN
    {
      $001 ARM 25/08/2014 Nuevo campo turno CNT-CAR-14073

      $002 AJS 29090214 Traducciones

      $003 AJS 30092014 Evitar errores en la comprobacion de solapamientos

      $004 ARM 20112014 CNT-OC-14105 Eliminado el campo Hora inicio de clave primaria y a¤adido campo Tiene idiomas, cupos y recogidas

      $005 AJS 25062015 Al borrar un turno hay que borrar todas las tablas relacionadas
    }
    END.
  }
}
