OBJECT Table 7010398 Procesos depuracion
{
  OBJECT-PROPERTIES
  {
    Date=16/10/09;
    Time=12:30:44;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    CaptionML=ESP=Procesos depuracion;
  }
  FIELDS
  {
    { 1   ;   ;Id                  ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=ESP=Id }
    { 10  ;   ;Tipo                ;Option        ;CaptionML=ESP=Tipo;
                                                   OptionCaptionML=ESP=,BackupParte;
                                                   OptionString=,BackupParte;
                                                   BlankZero=Yes }
    { 20  ;   ;Tablas Depuradas    ;Text30        ;CaptionML=ESP=Tablas Depuradas }
    { 30  ;   ;Usuario Ejecucion   ;Code20        ;CaptionML=ESP=Usuario Ejecucion;
                                                   Editable=No }
    { 31  ;   ;Fecha Ejecucion     ;DateTime      ;CaptionML=ESP=Fecha Ejecucion;
                                                   Editable=No }
    { 32  ;   ;Fecha Finalizacion  ;DateTime      ;CaptionML=ESP=Fecha Finalizacion;
                                                   Editable=No }
    { 50  ;   ;Cantidad            ;Integer       ;CaptionML=ESP=Cantidad }
  }
  KEYS
  {
    {    ;Id                                      ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text50000@1103355003 : TextConst 'ESP=<-2Y>';
      Text0001@1103355000 : TextConst 'ESP=Se va a proceder a eliminar definitivamente todos los backups de Partes cuya fecha de servicio sea anterior a  %1.\¨Desea continuar?';
      wVentana@1103355001 : Dialog;
      Text0002@1103355002 : TextConst 'ESP=No se ha encontrado ning£n registro';

    PROCEDURE DepurarBackupPartes@1103355000();
    VAR
      lwFecha@1103355000 : Date;
      lrBKPartes@1103355001 : Record 7035401;
      lrBKPartes2@1103355002 : Record 7035401;
      lrProc@1103355007 : Record 7010398;
      lwTotal@1103355003 : Integer;
      lwCont@1103355004 : Integer;
      lwDT1@1103355005 : DateTime;
      lwDT2@1103355006 : DateTime;
    BEGIN
      // DepurarBackupPartes

      // depuramos todos los backups de partes con una antiguedad superior a los dos a¤os
      lwFecha := CALCDATE(Text50000, TODAY);

      IF NOT CONFIRM(Text0001, FALSE, lwFecha) THEN
        EXIT;

      lwDT1 := CURRENTDATETIME;
      CLEAR(lrBKPartes);
      lrBKPartes.SETCURRENTKEY(Fecha);
      lrBKPartes.SETFILTER(Fecha, '<%1', lwFecha);
      IF lrBKPartes.FINDSET THEN BEGIN
        lwTotal := lrBKPartes.COUNT;
        CLEAR(lwCont);
        wVentana.OPEN('@1@@@@@@@@@');
        REPEAT
          lrBKPartes2 := lrBKPartes;
          lrBKPartes2.DELETE(TRUE);
          lwCont +=1;
          wVentana.UPDATE(1, ROUND(lwCont/lwTotal*10000,1));
        UNTIL lrBKPartes.NEXT=0;
        wVentana.CLOSE;
      END
      ELSE
       ERROR(Text0002);

      lwDT2 := CURRENTDATETIME;

      // Grabamos el registro del proceso
      CLEAR(lrProc);
      lrProc.Tipo := lrProc.Tipo::BackupParte;
      lrProc."Tablas Depuradas" := lrBKPartes.TABLECAPTION;
      lrProc."Usuario Ejecucion" := USERID;
      lrProc."Fecha Ejecucion"   := lwDT1;
      lrProc."Fecha Finalizacion":= lwDT2;
      lrProc.Cantidad            := lwTotal;
      lrProc.INSERT;
    END;

    BEGIN
    END.
  }
}
