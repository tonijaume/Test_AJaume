OBJECT Table 7010013 Bono
{
  OBJECT-PROPERTIES
  {
    Date=28/10/14;
    Time=15:38:38;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               rPar.FINDFIRST;

               IF "Creado por" = "Creado por"::Regularizacion THEN BEGIN
                 "Tipo importe" := "Tipo importe"::Manual;
                 graba_coste(Rec);
               END;
               VALIDATE(Replicado);
             END;

    OnModify=VAR
               "rC/I"@1103355000 : Record 7010018;
             BEGIN
               rPar.FINDFIRST;

               IF "Creado por" = "Creado por"::Regularizacion THEN BEGIN
                 "rC/I".RESET;
                 "rC/I".SETRANGE(Delegacion  , Delegacion);
                 "rC/I".SETRANGE(TourOperador, TourOperador);
                 "rC/I".SETRANGE(Bono        , Bono);
                 "rC/I".SETRANGE("N§ Reserva", "N§ Reserva");
                 "rC/I".SETRANGE("N§ Secuencia", "N§ Secuencia");
                 IF "rC/I".FINDFIRST THEN BEGIN
                   IF "rC/I"."N§ cierre central" <> 0 THEN
                     Rec := xRec;
                   "rC/I".DELETE;
                 END;
                 graba_coste(Rec);
               END;

               VALIDATE(Replicado);
             END;

    OnDelete=VAR
               lrCI@1103355000 : Record 7010018;
               lrConc@1103355001 : Record 7010019;
               lrDia@1103355002 : Record 7010020;
             BEGIN
               rPar.FINDFIRST;

               // Borrar los costes/ingresos del bono

               IF "N§ Factura" <> '' THEN
                 ERROR(Text006);

               lrCI.SETRANGE(Delegacion    , Delegacion);
               lrCI.SETRANGE(TourOperador  , TourOperador);
               lrCI.SETRANGE(Bono          , Bono);
               lrCI.SETRANGE("N§ Reserva"  , "N§ Reserva");
               lrCI.SETRANGE("N§ Secuencia", "N§ Secuencia");
               IF lrCI.FIND('-') THEN
                 lrCI.DELETEALL;

               lrConc.SETRANGE(Delegacion    , Delegacion);
               lrConc.SETRANGE(TourOperador  , TourOperador);
               lrConc.SETRANGE(Bono          , Bono);
               lrConc.SETRANGE("N§ Reserva"  , "N§ Reserva");
               lrConc.SETRANGE("N§ Secuencia", "N§ Secuencia");
               IF lrConc.FIND('-') THEN
                 lrConc.DELETEALL;

               lrDia.SETRANGE(Delegacion    , Delegacion);
               lrDia.SETRANGE(TourOperador  , TourOperador);
               lrDia.SETRANGE(Bono          , Bono);
               lrDia.SETRANGE("N§ Reserva"  , "N§ Reserva");
               lrDia.SETRANGE("N§ Secuencia", "N§ Secuencia");
               IF lrDia.FIND('-') THEN
                 lrDia.DELETEALL;
             END;

    OnRename=BEGIN
               rPar.FINDFIRST;
               IF NOT rPar."Empresa Central" THEN
                 ERROR(Text007);

               VALIDATE(Replicado);
             END;

    CaptionML=[ENU=Voucher;
               ESP=Bono];
    LookupFormID=Form7009859;
    DrillDownFormID=Form7009859;
  }
  FIELDS
  {
    { 1   ;   ;Delegacion          ;Code10        ;TableRelation=Delegacion.Codigo;
                                                   CaptionML=[ENU=Local Office;
                                                              ESP=Delegacion];
                                                   Description=PK, FK Delegacion }
    { 3   ;   ;TourOperador        ;Code10        ;TableRelation=TourOperador.TourOperador;
                                                   OnValidate=BEGIN
                                                                IF "TourOperador Compra" = '' THEN
                                                                  "TourOperador Compra" := TourOperador;
                                                              END;

                                                   CaptionML=[ENU=TourOperator;
                                                              ESP=TourOperador];
                                                   Description=PK, FK TourOperador }
    { 5   ;   ;Bono                ;Code10        ;CaptionML=[ENU=Voucher;
                                                              ESP=Bono];
                                                   Description=PK }
    { 7   ;   ;N§ Interno          ;Integer       ;CaptionML=[ENU=Internal No.;
                                                              ESP=N§ Interno] }
    { 9   ;   ;N§ Periodo          ;Integer       ;CaptionML=[ENU=Period No.;
                                                              ESP=N§ Periodo] }
    { 11  ;   ;N§ Secuencia        ;Code10        ;CaptionML=[ENU=Sequence No.;
                                                              ESP=N§ Secuencia];
                                                   Description=PK }
    { 13  ;   ;Nombre Responsable  ;Text50        ;CaptionML=[ENU=Responsible name;
                                                              ESP=Nombre Responsable] }
    { 15  ;   ;Fecha llegada       ;Date          ;CaptionML=[ENU=Arrival Date;
                                                              ESP=Fecha llegada] }
    { 17  ;   ;Importe coste       ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("C/I x Bono".Coste WHERE (Delegacion=FIELD(Delegacion),
                                                                                             TourOperador=FIELD(TourOperador),
                                                                                             Bono=FIELD(Bono),
                                                                                             N§ Secuencia=FIELD(N§ Secuencia),
                                                                                             N§ Reserva=FIELD(N§ Reserva)));
                                                   CaptionML=[ENU=Cost Amount;
                                                              ESP=Importe coste];
                                                   Description=Campo calculado sobre C/I x bono;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 19  ;   ;Importe ingreso     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("C/I x Bono".Ingreso WHERE (Delegacion=FIELD(Delegacion),
                                                                                               TourOperador=FIELD(TourOperador),
                                                                                               Bono=FIELD(Bono),
                                                                                               N§ Secuencia=FIELD(N§ Secuencia),
                                                                                               N§ Reserva=FIELD(N§ Reserva)));
                                                   CaptionML=[ENU=Incoming amount;
                                                              ESP=Importe Ingreso];
                                                   Description=Campo calculado sobre C/I x bono;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 21  ;   ;Coste final         ;Decimal       ;CaptionML=[ENU=Final cost;
                                                              ESP=Coste final];
                                                   Description=Soporte para el coste;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 23  ;   ;Tipo importe        ;Option        ;CaptionML=[ENU=Amount type;
                                                              ESP=Tipo importe];
                                                   OptionCaptionML=[ENU=Automatic,Manual;
                                                                    ESP=Automatico,Manual];
                                                   OptionString=Automatico,Manual;
                                                   Editable=No }
    { 25  ;   ;Importe calculado Blank;Decimal    ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Datos Blank".Importe WHERE (TourOperador=FIELD(TourOperador),
                                                                                                Bono=FIELD(Bono)));
                                                   CaptionML=[ENU=Calculated Blank amount;
                                                              ESP=Importe calculado Blank];
                                                   Description=Campo calculado sobre Datos Blank;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 27  ;   ;Ultimo importe Blank;Decimal       ;CaptionML=[ENU=Last Blank amount;
                                                              ESP=Ultimo importe Blank];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 29  ;   ;Tipo conciliacion   ;Option        ;CaptionML=[ENU=Conciliation Type;
                                                              ESP=Tipo conciliacion];
                                                   OptionCaptionML=[ENU=No concil,No Blank,No Voucher,Equals,Major Blank,Minor Blank,Agency 0,Blank 0,Direct payment,Now concil,Different inf.,Now claim,Extend;
                                                                    ESP=No concil,No Blank,No Bono,Iguales,Blank mayor,Blank menor,Agencia 0,Blank 0,Pago Directo,Ya concil,Inf. distinta,Ya reclam,Prolon];
                                                   OptionString=No concil,No Blank,No Bono,Iguales,Blank mayor,Blank menor,Agencia 0,Blank 0,Pago Directo,Ya concil,Inf. distinta,Ya reclam,Prolon;
                                                   Editable=No }
    { 31  ;   ;Modificado tras conciliar;Boolean  ;CaptionML=[ENU=Modified after conciliate;
                                                              ESP=Modificado tras conciliar] }
    { 33  ;   ;N§ Informe Blank    ;Code10        ;CaptionML=[ENU=Blank report No.;
                                                              ESP=N§ Informe Blank] }
    { 35  ;   ;Precios no coinciden;Boolean       ;OnValidate=BEGIN
                                                                VALIDATE(Reclamada);
                                                              END;

                                                   CaptionML=[ENU=Rates do not coincide;
                                                              ESP=Precios no coinciden] }
    { 37  ;   ;No aplica ofertas   ;Boolean       ;OnValidate=BEGIN
                                                                VALIDATE(Reclamada);
                                                              END;

                                                   CaptionML=[ENU=Do not apply offers;
                                                              ESP=No aplica ofertas] }
    { 39  ;   ;Falta de dtos extras;Boolean       ;OnValidate=BEGIN
                                                                VALIDATE(Reclamada);
                                                              END;

                                                   CaptionML=[ENU=Missing Extras disc.;
                                                              ESP=Falta de dtos extras] }
    { 41  ;   ;Falta bono          ;Boolean       ;OnValidate=BEGIN
                                                                VALIDATE(Reclamada);
                                                              END;

                                                   CaptionML=[ENU=Missing Voucher;
                                                              ESP=Falta bono] }
    { 43  ;   ;Otros               ;Boolean       ;OnValidate=BEGIN
                                                                VALIDATE(Reclamada);
                                                              END;

                                                   CaptionML=[ENU=Others;
                                                              ESP=Otros] }
    { 45  ;   ;Reclamada           ;Boolean       ;OnValidate=BEGIN
                                                                IF ("Precios no coinciden") OR ("No aplica ofertas") OR ("Falta de dtos extras") OR
                                                                   ("Falta bono") OR (Otros) THEN BEGIN
                                                                  Reclamada := TRUE;
                                                                END
                                                                ELSE
                                                                  Reclamada := FALSE;
                                                              END;

                                                   CaptionML=[ENU=Claimed;
                                                              ESP=Reclamada];
                                                   Editable=No }
    { 47  ;   ;Observaciones       ;Text250       ;CaptionML=[ENU=Comments;
                                                              ESP=Observaciones] }
    { 49  ;   ;Importe validacion  ;Decimal       ;OnValidate=BEGIN
                                                                CALCFIELDS("Importe ya validado");
                                                                IF "Importe validacion" > ("Coste final" - "Importe ya validado") THEN
                                                                  ERROR(Text008);
                                                              END;

                                                   CaptionML=[ENU=Validation amount;
                                                              ESP=Importe validacion];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 51  ;   ;Provision generada  ;Decimal       ;CaptionML=[ENU=Generated provision;
                                                              ESP=Provision generada];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 53  ;   ;Hotel Reservado     ;Code20        ;TableRelation=Hotel.Hotel;
                                                   CaptionML=[ENU=Booked hotel;
                                                              ESP=Hotel Reservado];
                                                   Description=FK Hotel }
    { 55  ;   ;Recien importado    ;Boolean       ;CaptionML=[ENU=Just imported;
                                                              ESP=Recien importado] }
    { 57  ;   ;Creado por          ;Option        ;CaptionML=[ENU=Created by;
                                                              ESP=Creado por];
                                                   OptionCaptionML=[ENU=Local office,Blank,Regularization;
                                                                    ESP=Delegacion,Blank,Regularizacion];
                                                   OptionString=Delegacion,Blank,Regularizacion;
                                                   Editable=No }
    { 59  ;   ;C¢d. divisa         ;Code10        ;TableRelation=Currency.Code;
                                                   CaptionML=[ENU=Currency code;
                                                              ESP=C¢d. divisa] }
    { 61  ;   ;Fecha salida        ;Date          ;CaptionML=[ENU=Check-out Date;
                                                              ESP=Fecha salida] }
    { 63  ;   ;Validado            ;Boolean       ;CaptionML=[ENU=Validated;
                                                              ESP=Validado];
                                                   Editable=No }
    { 65  ;   ;Importe ya validado ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Validacion bono"."Importe validacion" WHERE (Delegacion=FIELD(Delegacion),
                                                                                                                 TourOperador=FIELD(TourOperador),
                                                                                                                 Bono=FIELD(Bono),
                                                                                                                 N§ Secuencia=FIELD(N§ Secuencia),
                                                                                                                 N§ Reserva=FIELD(N§ Reserva),
                                                                                                                 N§ Factura=FIELD(Filtro Factura)));
                                                   CaptionML=[ENU=Amount already validated;
                                                              ESP=Importe ya validado];
                                                   Description=Campo calculado;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 67  ;   ;Imp. conciliar BLANK;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum(Bono."Conciliacion BLANK" WHERE (Delegacion=FIELD(Delegacion),
                                                                                                    TourOperador=FIELD(TourOperador),
                                                                                                    Bono=FIELD(Bono),
                                                                                                    N§ Secuencia=FIELD(N§ Secuencia),
                                                                                                    Fecha llegada=FIELD(Rango fechas)));
                                                   CaptionML=[ENU=Blank amount conciliate;
                                                              ESP=Imp. conciliar BLANK];
                                                   Description=Campo calculado sobre Bono;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 69  ;   ;N§ Contrato         ;Code10        ;TableRelation="Contrato compra hotel"."N§ Contrato compra" WHERE (Delegacion=FIELD(Delegacion),
                                                                                                                     Hotel=FIELD(Hotel));
                                                   OnValidate=BEGIN
                                                                rConC.RESET;
                                                                IF rConC.GET(Delegacion, "N§ Contrato") THEN BEGIN
                                                                  IF Hotel <> rConC.Hotel THEN
                                                                    ERROR(Text009,
                                                                          Hotel, rConC.Hotel);
                                                                  IF TourOperador <> rConC.TourOperador THEN
                                                                    ERROR(Text010,
                                                                          TourOperador, rConC.TourOperador);
                                                                END
                                                                ELSE BEGIN
                                                                  ERROR(Text011, Delegacion);
                                                                END;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Contract No.;
                                                              ESP=N§ Contrato];
                                                   Description=FK Contrato compra hotel }
    { 71  ;   ;Bote directo        ;Boolean       ;CaptionML=[ENU=Direct pot;
                                                              ESP=Bote directo];
                                                   Editable=No }
    { 73  ;   ;Bono garantia       ;Boolean       ;CaptionML=[ENU=Guarantee voucher;
                                                              ESP=Bono garantia] }
    { 75  ;   ;Conciliacion BLANK  ;Decimal       ;CaptionML=[ENU=BLANK Conciliation;
                                                              ESP=Conciliacion BLANK];
                                                   Description=Campo BackUp en el momento de conciliar.;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 77  ;   ;Rango fechas        ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Dates Range;
                                                              ESP=Rango Fechas] }
    { 79  ;   ;Importe en FPR      ;Decimal       ;CaptionML=[ENU=Amount on Invoices to be received;
                                                              ESP=Importe en FPR];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 81  ;   ;N§ Reserva          ;Integer       ;CaptionML=[ENU=Reservation No.;
                                                              ESP=N§ Reserva];
                                                   Description=PK }
    { 83  ;   ;Usuario             ;Code20        ;CaptionML=[ENU=User;
                                                              ESP=Usuario];
                                                   Editable=No }
    { 85  ;   ;Pendiente Cierre    ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[ENU=Remaining closure;
                                                              ESP=Pendiente Cierre] }
    { 87  ;   ;Hotel               ;Code20        ;TableRelation=Hotel.Hotel;
                                                   OnValidate=BEGIN
                                                                IF "Creado por" = "Creado por"::Regularizacion THEN
                                                                  "Hotel Reservado" := Hotel;
                                                              END;

                                                   CaptionML=[ENU=Hotel;
                                                              ESP=Hotel];
                                                   Description=FK Hotel }
    { 89  ;   ;N§ Prefactura       ;Code10        ;CaptionML=[ENU=Pro-forma invoice No.;
                                                              ESP=N§ Prefactura] }
    { 91  ;   ;N§ Factura          ;Code20        ;CaptionML=[ENU=Invoice No.;
                                                              ESP=N§ Factura] }
    { 93  ;   ;Fecha ultima factura;Date          ;CaptionML=[ENU=Date of last invoice;
                                                              ESP=Fecha ultima factura] }
    { 95  ;   ;Facturable          ;Boolean       ;CaptionML=[ENU=Billable;
                                                              ESP=Facturable] }
    { 97  ;   ;TourOperador Compra ;Code10        ;TableRelation=TourOperador.TourOperador;
                                                   CaptionML=[ENU=TourOperador Compra;
                                                              ESP=TourOperador Compra];
                                                   Description=FK TourOperador }
    { 99  ;   ;FPR Factura         ;Decimal       ;CaptionML=[ENU=Invoices to be received;
                                                              ESP=FPR Factura];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 101 ;   ;N§ HPL              ;Code10        ;CaptionML=[ENU=HPL No.;
                                                              ESP=N§ HPL] }
    { 103 ;   ;Ingreso Final       ;Decimal       ;CaptionML=[ENU=Final income;
                                                              ESP=Ingreso Final];
                                                   Description=Soporte para el ingreso;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 105 ;   ;Coste Garantizado   ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("C/I x Bono-Dia".Importe WHERE (Delegacion=FIELD(Delegacion),
                                                                                                    TourOperador=FIELD(TourOperador),
                                                                                                    Bono=FIELD(Bono),
                                                                                                    N§ Reserva=FIELD(N§ Reserva),
                                                                                                    N§ Secuencia=FIELD(N§ Secuencia),
                                                                                                    Coste/Ingreso=CONST(Coste)));
                                                   CaptionML=[ENU=Guaranteed cost;
                                                              ESP=Coste Garantizado];
                                                   Description=Calculado sobre C/I x bono-dia;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 107 ;   ;Ingreso extras      ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("C/I x Bono-Concepto".Importe WHERE (Delegacion=FIELD(Delegacion),
                                                                                                        TourOperador=FIELD(TourOperador),
                                                                                                        Bono=FIELD(Bono),
                                                                                                        N§ Reserva=FIELD(N§ Reserva),
                                                                                                        N§ Secuencia=FIELD(N§ Secuencia),
                                                                                                        Concepto=FILTER(EXTRAFIJOV|EXTRAVARIV)));
                                                   CaptionML=[ENU=Extras income;
                                                              ESP=Ingreso extras];
                                                   Description=Calculado sobre C/I x bono-Concepto;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 109 ;   ;Coste extras        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("C/I x Bono-Concepto".Importe WHERE (Delegacion=FIELD(Delegacion),
                                                                                                         TourOperador=FIELD(TourOperador),
                                                                                                         Bono=FIELD(Bono),
                                                                                                         N§ Reserva=FIELD(N§ Reserva),
                                                                                                         N§ Secuencia=FIELD(N§ Secuencia),
                                                                                                         Concepto=FILTER(EXTRAFIJOS|EXTRAVARIA)));
                                                   CaptionML=[ENU=Extras cost;
                                                              ESP=Coste extras];
                                                   Description=Calculado sobre C/I x bono-Concepto;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 110 ;   ;Comentario conciliacion;Code10     ;TableRelation="Codigo Diferencias Blank";
                                                   CaptionML=[ENU=Conciliation Comment;
                                                              ESP=Comentario conciliacion];
                                                   Description=FK Codigo Diferencias Blank }
    { 111 ;   ;Validacion HP especial;Boolean     ;CaptionML=[ENU=Special HP validation;
                                                              ESP=Validacion HP especial];
                                                   Description=Marcar los bonos que se validan a traves de Hotel Payment, separadamente del bono normal }
    { 150 ;   ;Filtro Factura      ;Code20        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Filter invoice;
                                                              ESP=Filtro Factura];
                                                   Description=Flowfilter }
    { 160 ;   ;Validacion Autorizada;Boolean      ;CaptionML=[ENU=Authorized validation;
                                                              ESP=Validacion Autorizada] }
    { 170 ;   ;Facturado           ;Boolean       ;CaptionML=[ENU=Invoiced;
                                                              ESP=Facturado];
                                                   Description=$004 }
    { 180 ;   ;Ingreso sin facturar;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("C/I x Bono-Concepto".Importe WHERE (Delegacion=FIELD(Delegacion),
                                                                                                        TourOperador=FIELD(TourOperador),
                                                                                                        Bono=FIELD(Bono),
                                                                                                        N§ Reserva=FIELD(N§ Reserva),
                                                                                                        N§ Secuencia=FIELD(N§ Secuencia),
                                                                                                        Coste/Ingreso=CONST(Ingreso),
                                                                                                        N§ Factura=CONST()));
                                                   CaptionML=[ENU=Income w/o invoice;
                                                              ESP=Ingreso sin facturar];
                                                   Description=$005, Sum("C/I x Bono-Concepto".Importe WHERE (Delegacion=FIELD(Delegacion),TourOperador=FIELD(TourOperador),Bono=FIELD(Bono),N§ Reserva=FIELD(N§ Reserva),N§ Secuencia=FIELD(N§ Secuencia),Coste/Ingreso=CONST(Ingreso),N§ Factura=CONST()));
                                                   Editable=No }
    { 50000;  ;Replicado           ;Boolean       ;OnValidate=VAR
                                                                Bono@1103355000 : Record 7010013;
                                                                Res@1103355001 : Boolean;
                                                              BEGIN
                                                                Replicado:= FALSE;
                                                              END;

                                                   CaptionML=[ENU=Replicated;
                                                              ESP=Replicado];
                                                   Description=Campo usado por el replicador }
  }
  KEYS
  {
    {    ;Delegacion,TourOperador,Bono,N§ Reserva,N§ Secuencia;
                                                   SumIndexFields=Coste final,Conciliacion BLANK;
                                                   KeyGroups=RENUMERING;
                                                   Clustered=Yes }
    {    ;N§ Secuencia,Fecha llegada              ;KeyGroups=RENUMERING }
    { No ;Recien importado                         }
    {    ;Modificado tras conciliar,Fecha llegada,Delegacion;
                                                   KeyGroups=RENUMERING }
    {    ;Delegacion,Tipo conciliacion,Fecha llegada;
                                                   KeyGroups=RENUMERING }
    {    ;Delegacion,Tipo conciliacion,Fecha salida;
                                                   KeyGroups=RENUMERING }
    {    ;Delegacion,Fecha llegada                ;KeyGroups=RENUMERING }
    {    ;Hotel Reservado                         ;KeyGroups=RENUMERING }
    {    ;Delegacion,Hotel,Fecha llegada          ;KeyGroups=RENUMERING }
    {    ;N§ Informe Blank                        ;KeyGroups=RENUMERING }
    {    ;Delegacion,TourOperador,Hotel,Fecha llegada,Validado;
                                                   SumIndexFields=Coste final,Conciliacion BLANK,Ingreso Final;
                                                   KeyGroups=RENUMERING }
    {    ;Delegacion,TourOperador,Hotel Reservado,Fecha llegada,Bono,N§ HPL;
                                                   SumIndexFields=Conciliacion BLANK;
                                                   KeyGroups=RENUMERING }
    { No ;Creado por,Tipo importe                  }
    {    ;Usuario                                 ;KeyGroups=RENUMERING }
    {    ;Pendiente Cierre                        ;KeyGroups=RENUMERING }
    {    ;Hotel,Delegacion,Fecha llegada          ;KeyGroups=RENUMERING }
    {    ;Delegacion,N§ Prefactura,TourOperador,Fecha llegada,Facturable;
                                                   KeyGroups=RENUMERING }
    {    ;TourOperador,Delegacion,N§ Secuencia,Fecha llegada,Validado;
                                                   KeyGroups=RENUMERING }
    {    ;Delegacion,N§ Prefactura,TourOperador,Hotel,Fecha llegada,Facturable;
                                                   KeyGroups=RENUMERING }
    {    ;Tipo conciliacion,N§ HPL                ;KeyGroups=RENUMERING }
    {    ;Replicado                               ;KeyGroups=RENUMERING }
    {    ;N§ Interno                              ;KeyGroups=RENUMERING }
    {    ;Delegacion,N§ Factura,TourOperador,Fecha llegada,Facturable;
                                                   KeyGroups=RENUMERING }
    {    ;Facturable,Facturado,N§ Prefactura,TourOperador,Hotel,Fecha llegada }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1103355003 : TextConst 'ENU=Agency parameters does not exist;ESP=No existe el fichero de Parametros de la Agencia.';
      Text001@1103355004 : TextConst 'ENU=Vouchers only can be registered in central;ESP=Solo es posible dar de alta bonos en central.';
      Text002@1103355005 : TextConst 'ENU=Agency parameters does not exist;ESP=No existe el fichero de Parametros de la Agencia.';
      Text003@1103355006 : TextConst 'ENU=Vouchers only can be modified in central;ESP=Solo es posible modificar bonos en central.';
      Text004@1103355007 : TextConst 'ENU=Vouchers only can be cancelled in central;ESP=Solo es posible dar de baja bonos en central.';
      Text005@1103355008 : TextConst 'ENU=Adjustment vouchers cannot be deleted;ESP=No se pueden borrar los bonos de regularizacion.';
      Text006@1103355009 : TextConst 'ENU=Invoiced voucher, cannot be deleted;ESP=El bono est  facturado, no se puede borrar';
      Text007@1103355010 : TextConst 'ENU=Vouchers only can be renamed in central;ESP=Solo es posible renombrar bonos en central.';
      Text008@1103355011 : TextConst 'ENU=Unable to indicate a validation amount higher than the final amount;ESP=No puede indicar un importe validacion superior al importe final.';
      Text009@1103355012 : TextConst 'ENU=Indicated Hotel (%1) in voucher does not match with the one in the contract (%2);ESP=El hotel (%1) indicado en el bono no coincide con el del contrato (%2).';
      Text010@1103355013 : TextConst 'ENU=Indicated Touroperator (%1) in voucher does not match with the one in the contract (%2);ESP=El TourOperador (%1) indicado en el bono no coincide con el del contrato (%2).';
      Text011@1103355014 : TextConst 'ENU=This contract does not exist for local office %1.;ESP=Este contrato no existe para la delegacion %1.';
      rDat@1100217004 : Record 7009768;
      rPar@1103355000 : Record 7009700;
      rConC@1103355001 : Record 7009723;
      rFra@1103355002 : Record 7010014;
      Text013@1100217000 : TextConst 'ENU=You must specify the invoice report in Agency Parameters;ESP=Debe especificar la factura venta hotel en par metros agencia';
      Text014@1100217003 : TextConst 'ENU=You must specify the posted invoice report in Agency Parameters;ESP=Debe especificar la factura venta hotel registrada en par metros agencia';
      Text015@1100217001 : TextConst 'ENU=You must specify the credit memo report in Agency Parameters;ESP=Debe especificar el abono venta hotel registrado en par metros agencia';

    PROCEDURE graba_coste@6(bono@1103355000 : Record 7010013);
    VAR
      rCI@1103355001 : Record 7010018;
    BEGIN
      CLEAR(rCI);
      rCI.Delegacion           := bono.Delegacion;
      rCI.TourOperador         := bono.TourOperador;
      rCI.Bono                 := bono.Bono;
      rCI."N§ Reserva"         := bono."N§ Reserva";
      rCI."N§ Secuencia"       := bono."N§ Secuencia";
      rCI.Coste                := bono."Coste final";
      rCI."Nombre responsable" := bono."Nombre Responsable";
      rCI."Contrato compra"    := bono."N§ Contrato";
      rCI."Fecha llegada"      := bono."Fecha llegada";
      rCI."Hotel Pago"         := bono.Hotel;
      rCI.INSERT;
    END;

    PROCEDURE CompruebaBonoFact@1100253012() : Boolean;
    VAR
      lrCntps@1100253001 : Record 7010019;
    BEGIN
      // CompruebaBonoFact
      // Devuelve true si todas las lineas de conceptos del bono est n facturadas

      CLEAR(lrCntps);
      lrCntps.SETRANGE(Delegacion      , Delegacion);
      lrCntps.SETRANGE(TourOperador    , TourOperador);
      lrCntps.SETRANGE(Bono            , Bono);
      lrCntps.SETRANGE("N§ Reserva"    , "N§ Reserva");
      lrCntps.SETRANGE("N§ Secuencia"  , "N§ Secuencia");
      lrCntps.SETRANGE("N§ Factura"   , '');
      lrCntps.SETRANGE("Coste/Ingreso" , lrCntps."Coste/Ingreso"::Ingreso);
      EXIT (NOT lrCntps.FINDFIRST);
    END;

    PROCEDURE ImprimirFactStd@1100217000();
    VAR
      rCabVta@1100217001 : Record 36;
      rCIbonoCnp@1100217000 : Record 7010019;
    BEGIN
      // ImprimirFactStd

      IF "N§ Factura" = '' THEN
        EXIT;

      rPar.FINDFIRST;
      IF rPar."Factura Venta Hotel" = 0 THEN
        ERROR(Text013);

      //IF rPar."Factura Venta Hotel registrada" = 0 THEN
      //  ERROR(Text014);
      //IF rPar."Abono Venta Hotel registrado" = 0 THEN
      //  ERROR(Text015);

      rCIbonoCnp.RESET;
      rCIbonoCnp.SETCURRENTKEY("N§ Prefactura",
                               "Coste/Ingreso");//,Fecha llegada,TourOperador,N§ Reserva,Concepto
      rCIbonoCnp.SETRANGE("N§ Prefactura", "N§ Prefactura");
      rCIbonoCnp.SETRANGE("Coste/Ingreso", rCIbonoCnp."Coste/Ingreso"::Ingreso);
      rCIbonoCnp.CALCSUMS(Importe);
      IF rCIbonoCnp.Importe < 0 THEN
        Buscar_FraStd(rCabVta."Document Type"::"Credit Memo", "N§ Factura")
      ELSE
        Buscar_FraStd(rCabVta."Document Type"::Invoice, "N§ Factura")
    END;

    PROCEDURE Buscar_FraStd@2(TipoDoc@1103355000 : 'Oferta,Pedido,Factura,Abono,Pedido abierto';nFac@1103355001 : Code[10]) : Boolean;
    VAR
      rCabVta@1103355002 : Record 36;
      rHisVta@1103355003 : Record 112;
      rHisVtaAbo@1103355004 : Record 114;
    BEGIN
      // Buscar_FraStd

      rCabVta.SETRANGE("Document Type", TipoDoc);
      rCabVta.SETRANGE("No."            , nFac);
      IF NOT rCabVta.GET(TipoDoc, nFac) THEN BEGIN
        CASE TipoDoc OF
          TipoDoc::Factura:
          BEGIN
            rHisVta.SETCURRENTKEY("Pre-Assigned No.");
            rHisVta.SETRANGE("Pre-Assigned No.", nFac);
            IF NOT rHisVta.FIND('-') THEN
              EXIT(FALSE);
            REPORT.RUNMODAL(rPar."Factura Venta Hotel registrada", TRUE, FALSE, rHisVta);
          END;
          TipoDoc::Abono:
          BEGIN
            rHisVtaAbo.SETCURRENTKEY("Pre-Assigned No.");
            rHisVtaAbo.SETRANGE("Pre-Assigned No.", nFac);
            IF NOT rHisVtaAbo.FIND('-') THEN
              EXIT(FALSE);
            REPORT.RUNMODAL(rPar."Abono Venta Hotel registrado", TRUE, FALSE, rHisVtaAbo);
          END;
        END;
      END
      ELSE BEGIN
        REPORT.RUNMODAL(rPar."Factura Venta Hotel", TRUE, FALSE, rCabVta);
      END;
      rCabVta.RESET;
      rHisVta.RESET;
      EXIT(TRUE);
    END;

    PROCEDURE ImprimirFact@1100217013();
    VAR
      lrCab@1100217000 : Record 7009908;
    BEGIN
      // ImprimirFact

      IF "N§ Factura" = '' THEN
        EXIT;

      // < JPT 08/10/12 De momento GOC imprime las facturas a traves de las tablas del estandard
      rDat.FINDFIRST;
      IF rDat."Empresa Real" = rDat."Empresa Real"::GOC THEN BEGIN
        ImprimirFactStd;
        EXIT;
      END;
      // JPT 08/10/12 >

      rPar.FINDFIRST;
      IF rPar."Factura Venta Hotel" = 0 THEN
        ERROR(Text013);

      CLEAR(lrCab);
      lrCab.SETRANGE("No.", "N§ Factura");
      IF lrCab.FINDFIRST THEN
        REPORT.RUNMODAL(rPar."Factura Venta Hotel", TRUE, FALSE, lrCab);
    END;

    BEGIN
    {
      $001 AJS 01112011 Modifico la clave Delegacion,N§ Factura,TourOperador,Fecha llegada para incluir el campo Facturable
                        al final, mejorar el rendimiento de la ventana de facturacion

      $002 AJS 24012012 Ampliamos el campo N§ Factura a 20 caracteres para cumplir con el estandar
      $003 AJS 02022012 Modifico la clave Touroperador,Delegacion,Hotel,Fecha llegada,Validad para que el campo Delegacion
                        sea el primero

      $004 JPT 23/04/12 CNT-CAR-12004 HOT-18

      $005 AJS 31082012 Nuevo campo [Ingreso sin facturar] para ver claramente en la ventana de facturacion si hay parte del
                        ingreso de una reserva que aun no esta facturada, por diferencias entre cierres, por ejemplo.

      $006 AJS 28102014 Amplio el campo Nombre responsable a Text50
    }
    END.
  }
}
