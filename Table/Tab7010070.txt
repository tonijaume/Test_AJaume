OBJECT Table 7010070 Conciliacion bono
{
  OBJECT-PROPERTIES
  {
    Date=02/12/13;
    Time=[ 4:52:16];
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               rPar.FINDFIRST;
               IF NOT rPar."Empresa Hotel Payment" THEN
                 ERROR(Text001);
             END;

    OnModify=BEGIN
               rPar.FINDFIRST;
               IF NOT rPar."Empresa Hotel Payment" THEN
                 ERROR(Text002);
             END;

    OnDelete=BEGIN
               rPar.FINDFIRST;
               IF NOT rPar."Empresa Hotel Payment" THEN
                 ERROR(Text003);
             END;

    OnRename=BEGIN
               rPar.FINDFIRST;
               IF NOT rPar."Empresa Hotel Payment" THEN
                 ERROR(Text004);
             END;

    CaptionML=[ENU=Voucher conciliation;
               ESP=Conciliacion bono];
    LookupFormID=Form7010074;
    DrillDownFormID=Form7010074;
  }
  FIELDS
  {
    { 1   ;   ;Delegacion          ;Code10        ;TableRelation=Delegacion.Codigo;
                                                   CaptionML=[ENU=Local Office;
                                                              ESP=Delegacion];
                                                   Description=PK, FK Delegacion;
                                                   Editable=No }
    { 3   ;   ;TourOperador        ;Code10        ;TableRelation=TourOperador.TourOperador;
                                                   CaptionML=[ENU=TourOperator;
                                                              ESP=TourOperador];
                                                   Description=PK, FK TourOperador;
                                                   Editable=No }
    { 5   ;   ;Bono                ;Code10        ;CaptionML=[ENU=Voucher;
                                                              ESP=Bono];
                                                   Description=PK;
                                                   Editable=No }
    { 7   ;   ;N§ Reserva          ;Integer       ;CaptionML=[ENU=Reservation No.;
                                                              ESP=N§ Reserva];
                                                   Description=PK;
                                                   Editable=No }
    { 9   ;   ;N§ Secuencia        ;Code10        ;CaptionML=[ENU=Sequence No.;
                                                              ESP=N§ Secuencia];
                                                   Description=PK;
                                                   Editable=No }
    { 11  ;   ;Hotel reservado agencia;Code20     ;TableRelation=Hotel.Hotel;
                                                   CaptionML=[ENU=Booked Hotel Agency;
                                                              ESP=Hotel reservado agencia];
                                                   Description=PK, FK Hotel;
                                                   Editable=No }
    { 13  ;   ;Fecha llegada agencia;Date         ;CaptionML=[ENU=Agency arrival date;
                                                              ESP=Fecha llegada agencia];
                                                   Description=PK;
                                                   Editable=No }
    { 15  ;   ;N§ Conciliacion     ;Integer       ;CaptionML=[ENU=Reconciliation No.;
                                                              ESP=N§ Conciliacion];
                                                   Description=PK;
                                                   Editable=No }
    { 17  ;   ;Fecha conciliacion  ;Date          ;CaptionML=[ENU=Reconciled date;
                                                              ESP=Fecha conciliacion];
                                                   Editable=No }
    { 19  ;   ;Importe TTOO        ;Decimal       ;OnValidate=BEGIN
                                                                IF "Importe TTOO" <> xRec."Importe TTOO" THEN BEGIN

                                                                  IF NOT CONFIRM(Text005) THEN BEGIN
                                                                    "Importe TTOO" := xRec."Importe TTOO";
                                                                    EXIT;
                                                                  END
                                                                  ELSE BEGIN
                                                                    "Diferencia TTOO-Agencia" := "Importe TTOO" - "Importe agencia";
                                                                    "Importe TTOO editado"         := TRUE;

                                                                    IF "Importe TTOO" = "Importe agencia" THEN
                                                                      "Tipo conciliacion" := "Tipo conciliacion"::Iguales
                                                                    ELSE BEGIN
                                                                      IF "Importe TTOO" > "Importe agencia" THEN
                                                                        "Tipo conciliacion" := "Tipo conciliacion"::"Blank mayor"
                                                                      ELSE
                                                                        "Tipo conciliacion" := "Tipo conciliacion"::"Blank menor";
                                                                    END;
                                                                  END;

                                                                END;
                                                              END;

                                                   CaptionML=[ENU=TTOO Amount;
                                                              ESP=Importe TTOO];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 21  ;   ;Tipo conciliacion   ;Option        ;CaptionML=[ENU=Conciliation Type;
                                                              ESP=Tipo conciliacion];
                                                   OptionCaptionML=[ENU=No concil,No Blank,No Voucher,Equals,Major Blank,Minor Blank,Agency 0,Blank 0,Direct payment,Now concil,Different inf.,Now claim,Extend;
                                                                    ESP=No concil,No Blank,No Bono,Iguales,Blank mayor,Blank menor,Agencia 0,Blank 0,Pago Directo,Ya concil,Inf. distinta,Ya reclam,Prolon];
                                                   OptionString=No concil,No Blank,No Bono,Iguales,Blank mayor,Blank menor,Agencia 0,Blank 0,Pago Directo,Ya concil,Inf. distinta,Ya reclam,Prolon;
                                                   Editable=No }
    { 23  ;   ;Informe BLANK       ;Code10        ;CaptionML=[ENU="Blank report ";
                                                              ESP=Informe BLANK];
                                                   Editable=No }
    { 25  ;   ;Codigo diferencia   ;Code10        ;TableRelation="Codigo Diferencias Blank".Codigo;
                                                   OnValidate=BEGIN
                                                                IF rDif.GET("Codigo diferencia") THEN
                                                                  "Descripcion diferencia" := rDif.Descripcion;
                                                              END;

                                                   CaptionML=[ENU=Difference code;
                                                              ESP=Codigo diferencia];
                                                   Description=FK Diferencia BLANK;
                                                   Editable=No }
    { 27  ;   ;N§ HPL              ;Code10        ;CaptionML=[ENU=HPL No.;
                                                              ESP=N§ HPL];
                                                   Editable=No }
    { 29  ;   ;Diferencia TTOO-Agencia;Decimal    ;CaptionML=[ENU=TTOO-Agency difference;
                                                              ESP=Diferencia TTOO-Agencia];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 31  ;   ;Descripcion diferencia;Text60      ;CaptionML=[ENU=Difference description;
                                                              ESP=Descripcion diferencia] }
    { 33  ;   ;Importe agencia     ;Decimal       ;CaptionML=[ENU=Agency amount;
                                                              ESP=Importe agencia];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="C¢d. divisa" }
    { 35  ;   ;Hotel TO agencia    ;Code20        ;CaptionML=[ENU=Hotel TO Agency;
                                                              ESP=Hotel TO agencia];
                                                   Editable=No }
    { 37  ;   ;Hotel Pago          ;Code20        ;TableRelation=Hotel.Hotel;
                                                   CaptionML=[ENU=Payment hotel;
                                                              ESP=Hotel Pago];
                                                   Editable=No }
    { 39  ;   ;Tipo Garantias      ;Option        ;CaptionML=[ENU=Guarantees type;
                                                              ESP=Tipo garantias];
                                                   OptionCaptionML=[ENU=Without Guarantees,Partial,No value;
                                                                    ESP=Sin Garantias,Total,Parcial,Sin valor];
                                                   OptionString=Sin Garantias,Total,Parcial,Sin valor;
                                                   Editable=No }
    { 41  ;   ;Habitacion TourOperador;Code10     ;CaptionML=[ENU=Touroperator Room;
                                                              ESP=Habitacion TourOperador] }
    { 43  ;   ;Regimen TourOperador;Code10        ;CaptionML=[ENU=Touroperator Board;
                                                              ESP=Regimen TourOperador] }
    { 45  ;   ;Dias estancia       ;Integer       ;CaptionML=[ENU=Stay days;
                                                              ESP=Dias Estancia] }
    { 47  ;   ;Fecha venta         ;Date          ;CaptionML=[ENU=Sale date;
                                                              ESP=Fecha venta] }
    { 49  ;   ;Hotel Reservado BLANK;Code20       ;CaptionML=[ENU=Booked Hotel BLANK;
                                                              ESP=Hotel Reservado BLANK] }
    { 51  ;   ;Hotel TO BLANK      ;Code20        ;CaptionML=[ENU=Hotel TO BLANK;
                                                              ESP=Hotel TO BLANK] }
    { 53  ;   ;Fecha llegada BLANK ;Date          ;CaptionML=[ENU=BLANK arrival date;
                                                              ESP=Fecha llegada BLANK] }
    { 55  ;   ;No Analisis         ;Boolean       ;CaptionML=[ENU=No Analysis;
                                                              ESP=No Analisis] }
    { 57  ;   ;C¢d. divisa         ;Code10        ;TableRelation=Currency;
                                                   CaptionML=[ENU=Currency exchange;
                                                              ESP=C¢d. divisa];
                                                   Description=FK Divisa }
    { 59  ;   ;Usuario conciliacion;Code20        ;CaptionML=[ENU=Conciliation user;
                                                              ESP=Usuario conciliacion] }
    { 60  ;   ;Touroperador Venta  ;Code10        ;TableRelation=TourOperador.TourOperador;
                                                   CaptionML=[ENU=Sale touroperator;
                                                              ESP=Touroperador venta];
                                                   Editable=No }
    { 61  ;   ;Importe TTOO editado;Boolean       ;CaptionML=[ENU=Edited TTOO Amount;
                                                              ESP=Importe TTOO editado] }
    { 80  ;   ;Conciliaci¢n definitiva;Boolean    ;OnValidate=VAR
                                                                lrBono@1103355000 : Record 7010013;
                                                              BEGIN
                                                                IF NOT "Conciliaci¢n definitiva" THEN
                                                                  ERROR(Text006, FIELDCAPTION("Conciliaci¢n definitiva"));

                                                                IF "Conciliaci¢n definitiva" THEN
                                                                  IF NOT CONFIRM(Text007) THEN BEGIN
                                                                    "Conciliaci¢n definitiva" := FALSE;
                                                                    EXIT;
                                                                  END;

                                                                CLEAR(lrBono);
                                                                lrBono.SETRANGE(Delegacion              , Delegacion);
                                                                lrBono.SETRANGE("TourOperador Compra"   , TourOperador);
                                                                lrBono.SETRANGE(Bono                    , Bono);
                                                                lrBono.MODIFYALL("Validacion Autorizada", "Conciliaci¢n definitiva");
                                                              END;

                                                   CaptionML=[ENU=Final Conciliation;
                                                              ESP=Conciliaci¢n definitiva];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Delegacion,TourOperador,Bono,N§ Reserva,N§ Secuencia,Hotel reservado agencia,Fecha llegada agencia,N§ Conciliacion;
                                                   SumIndexFields=Importe TTOO,Importe agencia,Diferencia TTOO-Agencia;
                                                   KeyGroups=RENUMERING;
                                                   Clustered=Yes }
    {    ;Hotel reservado agencia,Tipo conciliacion;
                                                   KeyGroups=RENUMERING }
    {    ;Informe BLANK                           ;KeyGroups=RENUMERING }
    {    ;N§ HPL,Hotel TO BLANK,Fecha llegada BLANK;
                                                   KeyGroups=RENUMERING }
    {    ;N§ HPL,Hotel TO agencia,Fecha llegada agencia;
                                                   KeyGroups=RENUMERING }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1103355003 : TextConst 'ENU=Only is available to register Conciliations in the Central;ESP=Solo es posible dar de alta Conciliaciones en la Central.';
      Text002@1103355004 : TextConst 'ENU=Only is available to modify Conciliations in the Central;ESP=Solo es posible modificar Conciliaciones en la Central.';
      Text003@1103355005 : TextConst 'ENU=Only is available to cancel Conciliations in the Central;ESP=Solo es posible dar de baja Conciliaciones en la Central.';
      Text004@1103355006 : TextConst 'ENU=Only is available to rename Conciliations in the Central;ESP=Solo es posible renombrar Conciliaciones en la Central.';
      rPar@1103355000 : Record 7009700;
      rDif@1103355001 : Record 7009706;
      Text005@1103355007 : TextConst 'ENU=Would you like to modify manually the touroperator amount of this conciliation;ESP=Desea modificar manualmente el importe Touroperador de esta conciliacion';
      Text006@1103355008 : TextConst 'ENU=Field %1 cannot be unmarked;ESP=El campo %1 No puede desmarcarse';
      Text007@1103355009 : TextConst 'ENU=Would you like to mark the field Definitive Conciliation?;ESP=¨Desea realmente marcar el campo Conciliacion Definitiva?';
      wVentana@1103355010 : Dialog;
      Text008@1103355011 : TextConst 'ENU=Anything has been selected;ESP=No se ha seleccionado nada';
      Text009@1103355012 : TextConst 'ENU=Would you like to mark the field Definitive Conciliation in the %1 selected lines?;ESP=¨Desea realmente marcar el campo Conciliacion Definitiva en las %1 lineas seleccionadas?';

    PROCEDURE nom_delegacion@1() : Text[30];
    VAR
      rDel@1103355000 : Record 7009718;
    BEGIN
      IF NOT rDel.GET(Delegacion) THEN
        EXIT('')
      ELSE
        EXIT(rDel.Nombre);
    END;

    PROCEDURE nom_touroperador@2() : Text[30];
    VAR
      rTO@1103355000 : Record 7009717;
    BEGIN
      IF NOT rTO.GET(TourOperador) THEN
        EXIT('')
      ELSE
        EXIT(rTO.Nombre);
    END;

    PROCEDURE nom_hotel@3() : Text[30];
    VAR
      rHot@1103355000 : Record 7009724;
    BEGIN
      IF NOT rHot.GET("Hotel reservado agencia") THEN
        EXIT('')
      ELSE
        EXIT(rHot.Nombre);
    END;

    PROCEDURE MarcaConciliacionDef@1103355000(VAR prConc@1103355001 : Record 7010070);
    VAR
      lrBono@1103355000 : Record 7010013;
      lwTotal@1103355002 : Integer;
      lwCont@1103355003 : Integer;
      lwTexto@1103355004 : Text[1024];
    BEGIN
      // MarcaConciliacionDef

      // La tabla prConc tiene que venir previamente filtrada

      lwTotal := prConc.COUNT;
      CLEAR(lwCont);
      CASE lwTotal OF
        0 : ERROR(Text008);
        1 : lwTexto := Text007
        ELSE lwTexto := STRSUBSTNO(Text009, lwTotal);
      END;

      IF NOT CONFIRM(lwTexto) THEN
        EXIT;

      IF prConc.FINDSET THEN BEGIN
        wVentana.OPEN('@1@@@@@@@@@');
        REPEAT
          // IF NOT prConc."Conciliaci¢n definitiva" THEN BEGIN
            CLEAR(lrBono);
            lrBono.SETRANGE(Delegacion              , Delegacion);
            lrBono.SETRANGE("TourOperador Compra"   , TourOperador);
            lrBono.SETRANGE(Bono                    , Bono);
            lrBono.MODIFYALL("Validacion Autorizada", TRUE);
            prConc."Conciliaci¢n definitiva" := TRUE;
            prConc.MODIFY;
          // END;
          lwCont +=1;
          wVentana.UPDATE(1, ROUND(lwCont/lwTotal*10000,1));
        UNTIL prConc.NEXT=0;
        wVentana.CLOSE;
      END
      ELSE
        ERROR(Text008);
    END;

    BEGIN
    {
      $001 AJS 02122013 Sustituyo el campo Central por "BD Hotel Payment"
    }
    END.
  }
}
