OBJECT Table 363 Analysis View
{
  OBJECT-PROPERTIES
  {
    Date=02/10/12;
    Time=12:00:00;
    Modified=Yes;
    Version List=AIC2009,NAVW16.00,NAVES6.00;
  }
  PROPERTIES
  {
    Permissions=TableData 365=rimd,
                TableData 366=rimd;
    DataCaptionFields=Code,Name;
    OnDelete=VAR
               AnalysisViewFilter@1000 : Record 364;
             BEGIN
               AnalysisViewReset;
               AnalysisViewFilter.SETRANGE("Analysis View Code",Code);
               AnalysisViewFilter.DELETEALL;
             END;

    CaptionML=[ENU=Analysis View;
               ESP=Vista de an lisis];
    LookupFormID=Form556;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              ESP=C¢digo];
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Text50        ;CaptionML=[ENU=Name;
                                                              ESP=Nombre] }
    { 4   ;   ;Last Entry No.      ;Integer       ;CaptionML=[ENU=Last Entry No.;
                                                              ESP=N§ £lt. mov.] }
    { 5   ;   ;Last Budget Entry No.;Integer      ;CaptionML=[ENU=Last Budget Entry No.;
                                                              ESP=N§ £lt. mov. ppto.] }
    { 6   ;   ;Last Date Updated   ;Date          ;CaptionML=[ENU=Last Date Updated;
                                                              ESP=élt. fecha actualiz.] }
    { 7   ;   ;Update on Posting   ;Boolean       ;CaptionML=[ENU=Update on Posting;
                                                              ESP=Actualizar al registrar] }
    { 8   ;   ;Blocked             ;Boolean       ;OnValidate=BEGIN
                                                                IF (Blocked = FALSE) AND ("Refresh When Unblocked" = TRUE)
                                                                THEN BEGIN
                                                                  ValidateDelete(FIELDCAPTION(Blocked));
                                                                  AnalysisViewReset;
                                                                  "Refresh When Unblocked" := FALSE;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Blocked;
                                                              ESP=Bloqueado] }
    { 9   ;   ;G/L Account Filter  ;Text250       ;TableRelation="G/L Account";
                                                   OnValidate=VAR
                                                                AnalysisViewEntry@1000 : Record 365;
                                                                AnalysisViewBudgetEntry@1001 : Record 366;
                                                                GLAcc@1002 : Record 15;
                                                              BEGIN
                                                                TESTFIELD(Blocked,FALSE);
                                                                IF ("Last Entry No." <> 0) AND (xRec."G/L Account Filter" = '') AND ("G/L Account Filter" <> '')
                                                                THEN BEGIN
                                                                  ValidateModify(FIELDCAPTION("G/L Account Filter"));
                                                                  GLAcc.SETFILTER("No.","G/L Account Filter");
                                                                  IF GLAcc.FIND('-') THEN
                                                                    REPEAT
                                                                      GLAcc.MARK := TRUE;
                                                                    UNTIL GLAcc.NEXT = 0;
                                                                  GLAcc.SETRANGE("No.");
                                                                  IF GLAcc.FIND('-') THEN
                                                                    REPEAT
                                                                      IF NOT GLAcc.MARK THEN BEGIN
                                                                        AnalysisViewEntry.SETRANGE("Analysis View Code",Code);
                                                                        AnalysisViewEntry.SETRANGE("G/L Account No.",GLAcc."No.");
                                                                        AnalysisViewEntry.DELETEALL;
                                                                        AnalysisViewBudgetEntry.SETRANGE("Analysis View Code",Code);
                                                                        AnalysisViewBudgetEntry.SETRANGE("G/L Account No.",GLAcc."No.");
                                                                        AnalysisViewBudgetEntry.DELETEALL;
                                                                      END;
                                                                    UNTIL GLAcc.NEXT = 0;
                                                                END;
                                                                IF ("Last Entry No." <> 0) AND ("G/L Account Filter" <> xRec."G/L Account Filter") AND (xRec."G/L Account Filter" <> '')
                                                                THEN  BEGIN
                                                                  ValidateDelete(FIELDCAPTION("G/L Account Filter"));
                                                                  AnalysisViewReset;
                                                                END;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=G/L Account Filter;
                                                              ESP=Filtro cuenta] }
    { 10  ;   ;Business Unit Filter;Code250       ;TableRelation="Business Unit";
                                                   OnValidate=VAR
                                                                BusUnit@1000 : Record 220;
                                                                AnalysisViewEntry@1001 : Record 365;
                                                                AnalysisViewBudgetEntry@1002 : Record 366;
                                                                TempBusUnit@1003 : TEMPORARY Record 220;
                                                              BEGIN
                                                                TESTFIELD(Blocked,FALSE);
                                                                IF ("Last Entry No." <> 0) AND (xRec."Business Unit Filter" = '') AND
                                                                   ("Business Unit Filter" <> xRec."Business Unit Filter")
                                                                THEN BEGIN
                                                                  ValidateModify(FIELDCAPTION("Business Unit Filter"));
                                                                  IF BusUnit.FIND('-') THEN
                                                                    REPEAT
                                                                      TempBusUnit := BusUnit;
                                                                      TempBusUnit.INSERT;
                                                                    UNTIL BusUnit.NEXT = 0;
                                                                  TempBusUnit.INIT;
                                                                  TempBusUnit.Code := '';
                                                                  TempBusUnit.INSERT;
                                                                  TempBusUnit.SETFILTER(Code,"Business Unit Filter");
                                                                  TempBusUnit.DELETEALL;
                                                                  TempBusUnit.SETRANGE(Code);
                                                                  IF TempBusUnit.FIND('-') THEN
                                                                    REPEAT
                                                                      AnalysisViewEntry.SETRANGE("Analysis View Code",Code);
                                                                      AnalysisViewEntry.SETRANGE("Business Unit Code",TempBusUnit.Code);
                                                                      AnalysisViewEntry.DELETEALL;
                                                                      AnalysisViewBudgetEntry.SETRANGE("Analysis View Code",Code);
                                                                      AnalysisViewBudgetEntry.SETRANGE("Business Unit Code",TempBusUnit.Code);
                                                                      AnalysisViewBudgetEntry.DELETEALL;
                                                                    UNTIL TempBusUnit.NEXT = 0
                                                                END;
                                                                IF ("Last Entry No." <> 0) AND (xRec."Business Unit Filter" <>'') AND
                                                                   ("Business Unit Filter" <> xRec."Business Unit Filter")
                                                                THEN BEGIN
                                                                  ValidateDelete(FIELDCAPTION("Business Unit Filter"));
                                                                  AnalysisViewReset;
                                                                END;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Business Unit Filter;
                                                              ESP=Filtro empresa] }
    { 11  ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                TESTFIELD(Blocked,FALSE);
                                                                IF ("Last Entry No." <> 0) AND ("Starting Date" <> xRec."Starting Date") THEN BEGIN
                                                                  ValidateDelete(FIELDCAPTION("Starting Date"));
                                                                  AnalysisViewReset;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Starting Date;
                                                              ESP=Fecha inicial] }
    { 12  ;   ;Date Compression    ;Option        ;InitValue=Day;
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Blocked,FALSE);
                                                                IF ("Last Entry No." <> 0) AND ("Date Compression" <> xRec."Date Compression") THEN BEGIN
                                                                  ValidateDelete(FIELDCAPTION("Date Compression"));
                                                                  AnalysisViewReset;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Date Compression;
                                                              ESP=Compresi¢n por fechas];
                                                   OptionCaptionML=[ENU=None,Day,Week,Month,Quarter,Year,Period;
                                                                    ESP=Ninguno,D¡a,Semana,Mes,Trimestre,A¤o,Periodo];
                                                   OptionString=None,Day,Week,Month,Quarter,Year,Period }
    { 13  ;   ;Dimension 1 Code    ;Code20        ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Blocked,FALSE);
                                                                IF Dim.CheckIfDimUsed("Dimension 1 Code",13,'',Code,0) THEN
                                                                  ERROR(Text000,Dim.GetCheckDimErr);
                                                                ModifyDim(FIELDCAPTION("Dimension 1 Code"),"Dimension 1 Code",xRec."Dimension 1 Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Dimension 1 Code;
                                                              ESP=C¢d. dimensi¢n 1] }
    { 14  ;   ;Dimension 2 Code    ;Code20        ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Blocked,FALSE);
                                                                IF Dim.CheckIfDimUsed("Dimension 2 Code",14,'',Code,0) THEN
                                                                  ERROR(Text000,Dim.GetCheckDimErr);
                                                                ModifyDim(FIELDCAPTION("Dimension 2 Code"),"Dimension 2 Code",xRec."Dimension 2 Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Dimension 2 Code;
                                                              ESP=C¢d. dimensi¢n 2] }
    { 15  ;   ;Dimension 3 Code    ;Code20        ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Blocked,FALSE);
                                                                IF Dim.CheckIfDimUsed("Dimension 3 Code",15,'',Code,0) THEN
                                                                  ERROR(Text000,Dim.GetCheckDimErr);
                                                                ModifyDim(FIELDCAPTION("Dimension 3 Code"),"Dimension 3 Code",xRec."Dimension 3 Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Dimension 3 Code;
                                                              ESP=C¢d. dimensi¢n 3] }
    { 16  ;   ;Dimension 4 Code    ;Code20        ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Blocked,FALSE);
                                                                IF Dim.CheckIfDimUsed("Dimension 4 Code",16,'',Code,0) THEN
                                                                  ERROR(Text000,Dim.GetCheckDimErr);
                                                                ModifyDim(FIELDCAPTION("Dimension 4 Code"),"Dimension 4 Code",xRec."Dimension 4 Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Dimension 4 Code;
                                                              ESP=C¢d. dimensi¢n 4] }
    { 17  ;   ;Include Budgets     ;Boolean       ;OnValidate=BEGIN
                                                                TESTFIELD(Blocked,FALSE);
                                                                IF ("Last Entry No." <> 0) AND (xRec."Include Budgets" = TRUE) AND ("Include Budgets" = FALSE)
                                                                THEN BEGIN
                                                                  ValidateDelete(FIELDCAPTION("Include Budgets"));
                                                                  AnalysisviewBudgetReset;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Include Budgets;
                                                              ESP=Incluir pptos.] }
    { 18  ;   ;Refresh When Unblocked;Boolean     ;CaptionML=[ENU=Refresh When Unblocked;
                                                              ESP=Refrescar cdo. bloqueado] }
    { 10700;  ;Historic G/L Account Filter;Text250;TableRelation="Historic G/L Account";
                                                   OnValidate=VAR
                                                                AnalysisViewEntry@1100000 : Record 365;
                                                                AnalysisViewBudgetEntry@1100001 : Record 366;
                                                                HistoricGLAcc@1100002 : Record 10721;
                                                              BEGIN
                                                                TESTFIELD(Blocked,FALSE);
                                                                IF ("Last Entry No." <> 0) AND (xRec."Historic G/L Account Filter" = '') AND ("Historic G/L Account Filter" <> '')
                                                                THEN BEGIN
                                                                  ValidateModify(FIELDCAPTION("Historic G/L Account Filter"));
                                                                  HistoricGLAcc.SETFILTER("No.","Historic G/L Account Filter");
                                                                  IF HistoricGLAcc.FIND('-') THEN
                                                                    REPEAT
                                                                      HistoricGLAcc.MARK := TRUE;
                                                                    UNTIL HistoricGLAcc.NEXT = 0;
                                                                  HistoricGLAcc.SETRANGE("No.");
                                                                  IF HistoricGLAcc.FIND('-') THEN
                                                                    REPEAT
                                                                      IF NOT HistoricGLAcc.MARK THEN BEGIN
                                                                        AnalysisViewEntry.SETRANGE("Analysis View Code",Code);
                                                                        AnalysisViewEntry.SETRANGE("Old G/L Account No.",HistoricGLAcc."No.");
                                                                        AnalysisViewEntry.DELETEALL;
                                                                        AnalysisViewBudgetEntry.SETRANGE("Analysis View Code",Code);
                                                                        AnalysisViewBudgetEntry.SETRANGE("Old G/L Account No.",HistoricGLAcc."No.");
                                                                        AnalysisViewBudgetEntry.DELETEALL;
                                                                      END;
                                                                    UNTIL HistoricGLAcc.NEXT = 0;
                                                                END;
                                                                IF ("Last Entry No." <> 0) AND ("Historic G/L Account Filter" <> xRec."Historic G/L Account Filter") AND
                                                                 (xRec."Historic G/L Account Filter" <> '')
                                                                THEN  BEGIN
                                                                  ValidateDelete(FIELDCAPTION("Historic G/L Account Filter"));
                                                                  AnalysisViewReset;
                                                                END;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Historic G/L Account Filter;
                                                              ESP=Filtro cuenta de hist¢rico] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1\You cannot use the same dimension twice in the same analysis view.;ESP=%1\No puede usar la misma dim. dos veces en la misma vista an lisis.';
      Text001@1001 : TextConst 'ENU=The dimension %1 is used in the analysis view %2 %3.;ESP=La dimensi¢n %1 se usa en la vista an lisis %2 %3.';
      Text002@1002 : TextConst 'ENU=" You must therefore retain the dimension to keep consistency between the analysis view and the G/L entries.";ESP=" Debe conservar la dimensi¢n para mantener la consistencia entre el an lisis y los movs. de contabilidad."';
      Text004@1004 : TextConst 'ENU=All analysis views must be updated with the latest G/L entries and G/L budget entries.;ESP=Todas vistas an lisis deben ser actualizadas con los £ltimos movs. de contabilidad y presupuestos.';
      Text005@1005 : TextConst 'ENU=" Both blocked and unblocked analysis views must be updated.";ESP=" Deben actualizarse las vistas an lisis bloqueadas y sin bloquear."';
      Text007@1007 : TextConst 'ENU=" Note, you must remove the checkmark in the blocked field before updating the blocked analysis views.\";ESP=" Nota, debe borrar la marca en el campo bloqueado antes de actualizar las vistas an lisis bloqueadas.\"';
      Text008@1008 : TextConst 'ENU=Currently, %1 analysis views are not updated.;ESP=Vistas an lisis %1 no est n actualizadas.';
      Text009@1009 : TextConst 'ENU=" Do you wish to update these analysis views?";ESP=" ¨Confirma que desea actualizar estas vistas an lisis?"';
      Text010@1010 : TextConst 'ENU=All analysis views must be updated with the latest G/L entries.;ESP=Todas vistas an lisis deben ser actualizadas con los £ltimos movs.';
      Text011@1011 : TextConst 'ENU=If you change the contents of the %1 field, the analysis view entries will be deleted.;ESP=Si cambia los contenidos del campo %1, se borrar n los movs. vista an lisis.';
      Text012@1012 : TextConst 'ENU=\You will have to update again.\\Do you want to enter a new value in the %1 field?;ESP=\Tendr  que actualizar de nuevo.\\¨Quiere introducir un valor nuevo en el campo %1?';
      Text013@1013 : TextConst 'ENU=The update has been interrupted in response to the warning.;ESP=La actualizaci¢n ha sido interrumpida como repuesta a la advertencia.';
      Text014@1014 : TextConst 'ENU=If you change the contents of the %1 field, the analysis view entries will be changed as well.\\;ESP=Si cambia los contenidos del campo %1, cambiar n tambi‚n los movs. vista an lisis.\\';
      Text015@1015 : TextConst 'ENU=Do you want to enter a new value in the %1 field?;ESP=¨Confirma que desea introducir un valor nuevo en el campo %1?';
      AnalysisViewEntry@1016 : Record 365;
      NewAnalysisViewEntry@1017 : Record 365;
      AnalysisViewBudgetEntry@1018 : Record 366;
      NewAnalysisViewBudgetEntry@1019 : Record 366;
      Dim@1020 : Record 348;

    PROCEDURE ModifyDim@2(DimFieldName@1000 : Text[100];DimValue@1001 : Code[20];xDimValue@1002 : Code[20]);
    BEGIN
      IF ("Last Entry No." <> 0) AND (DimValue <> xDimValue) THEN BEGIN
        IF DimValue <> '' THEN BEGIN
          ValidateDelete(DimFieldName);
          AnalysisViewReset;
        END;
        IF DimValue = '' THEN BEGIN
          ValidateModify(DimFieldName);
          CASE DimFieldName OF
            FIELDCAPTION("Dimension 1 Code"):
              BEGIN
                AnalysisViewEntry.SETFILTER("Dimension 1 Value Code",'<>%1','');
                AnalysisViewBudgetEntry.SETFILTER("Dimension 1 Value Code",'<>%1','');
              END;
            FIELDCAPTION("Dimension 2 Code"):
              BEGIN
                AnalysisViewEntry.SETFILTER("Dimension 2 Value Code",'<>%1','');
                AnalysisViewBudgetEntry.SETFILTER("Dimension 2 Value Code",'<>%1','');
              END;
            FIELDCAPTION("Dimension 3 Code"):
              BEGIN
                AnalysisViewEntry.SETFILTER("Dimension 3 Value Code",'<>%1','');
                AnalysisViewBudgetEntry.SETFILTER("Dimension 3 Value Code",'<>%1','');
              END;
            FIELDCAPTION("Dimension 4 Code"):
              BEGIN
                AnalysisViewEntry.SETFILTER("Dimension 4 Value Code",'<>%1','');
                AnalysisViewBudgetEntry.SETFILTER("Dimension 4 Value Code",'<>%1','');
              END;
          END;
          AnalysisViewEntry.SETRANGE("Analysis View Code",Code);
          AnalysisViewBudgetEntry.SETRANGE("Analysis View Code",Code);
          IF AnalysisViewEntry.FIND('-') THEN
            REPEAT
              AnalysisViewEntry.DELETE;
              NewAnalysisViewEntry := AnalysisViewEntry;
              CASE DimFieldName OF
                FIELDCAPTION("Dimension 1 Code"):
                  NewAnalysisViewEntry."Dimension 1 Value Code" := '';
                FIELDCAPTION("Dimension 2 Code"):
                  NewAnalysisViewEntry."Dimension 2 Value Code" := '';
                FIELDCAPTION("Dimension 3 Code"):
                  NewAnalysisViewEntry."Dimension 3 Value Code" := '';
                FIELDCAPTION("Dimension 4 Code"):
                  NewAnalysisViewEntry."Dimension 4 Value Code" := '';
              END;
              InsertAnalysisViewEntry;
             UNTIL AnalysisViewEntry.NEXT = 0;
          IF AnalysisViewBudgetEntry.FIND('-') THEN
            REPEAT
              AnalysisViewBudgetEntry.DELETE;
              NewAnalysisViewBudgetEntry := AnalysisViewBudgetEntry;
              CASE DimFieldName OF
                FIELDCAPTION("Dimension 1 Code"):
                  NewAnalysisViewBudgetEntry."Dimension 1 Value Code" := '';
                FIELDCAPTION("Dimension 2 Code"):
                  NewAnalysisViewBudgetEntry."Dimension 2 Value Code" := '';
                FIELDCAPTION("Dimension 3 Code"):
                  NewAnalysisViewBudgetEntry."Dimension 3 Value Code" := '';
                FIELDCAPTION("Dimension 4 Code"):
                  NewAnalysisViewBudgetEntry."Dimension 4 Value Code" := '';
              END;
              InsertAnalysisViewBudgetEntry;
            UNTIL AnalysisViewBudgetEntry.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE InsertAnalysisViewEntry@1();
    BEGIN
      IF NOT NewAnalysisViewEntry.INSERT THEN BEGIN
        NewAnalysisViewEntry.FIND;
        NewAnalysisViewEntry.Amount := NewAnalysisViewEntry.Amount + AnalysisViewEntry.Amount;
        NewAnalysisViewEntry."Debit Amount" :=
          NewAnalysisViewEntry."Debit Amount" + AnalysisViewEntry."Debit Amount";
        NewAnalysisViewEntry."Credit Amount" :=
          NewAnalysisViewEntry."Credit Amount" + AnalysisViewEntry."Credit Amount";
        NewAnalysisViewEntry."Add.-Curr. Debit Amount" :=
          NewAnalysisViewEntry."Add.-Curr. Debit Amount" + AnalysisViewEntry."Add.-Curr. Debit Amount";
        NewAnalysisViewEntry."Add.-Curr. Credit Amount" :=
          NewAnalysisViewEntry."Add.-Curr. Credit Amount" + AnalysisViewEntry."Add.-Curr. Credit Amount";
        NewAnalysisViewEntry.MODIFY;
      END;
    END;

    LOCAL PROCEDURE InsertAnalysisViewBudgetEntry@6();
    BEGIN
      IF NOT NewAnalysisViewBudgetEntry.INSERT THEN BEGIN
        NewAnalysisViewBudgetEntry.FIND;
        NewAnalysisViewBudgetEntry.Amount := NewAnalysisViewBudgetEntry.Amount + AnalysisViewBudgetEntry.Amount;
        NewAnalysisViewBudgetEntry.MODIFY;
      END;
    END;

    PROCEDURE AnalysisViewReset@5();
    VAR
      AnalysisViewEntry@1000 : Record 365;
    BEGIN
      AnalysisViewEntry.SETRANGE("Analysis View Code",Code);
      AnalysisViewEntry.DELETEALL;
      "Last Entry No." := 0;
      "Last Date Updated" := 0D;
      AnalysisviewBudgetReset;
    END;

    PROCEDURE CheckDimensionsAreRetained@3(ObjectType@1000 : Integer;ObjectID@1001 : Integer;OnlyIfIncludeBudgets@1002 : Boolean);
    BEGIN
      RESET;
      IF OnlyIfIncludeBudgets THEN
        SETRANGE("Include Budgets",TRUE);
      IF FIND('-') THEN
        REPEAT
          CheckDimIsRetained(ObjectType,ObjectID,"Dimension 1 Code",Code,Name);
          CheckDimIsRetained(ObjectType,ObjectID,"Dimension 2 Code",Code,Name);
          CheckDimIsRetained(ObjectType,ObjectID,"Dimension 3 Code",Code,Name);
          CheckDimIsRetained(ObjectType,ObjectID,"Dimension 4 Code",Code,Name);
        UNTIL NEXT = 0;
    END;

    LOCAL PROCEDURE CheckDimIsRetained@8(ObjectType@1000 : Integer;ObjectID@1001 : Integer;DimCode@1002 : Code[20];AnalysisViewCode@1003 : Code[10];AnalysisViewName@1004 : Text[50]);
    VAR
      SelectedDim@1005 : Record 369;
    BEGIN
      IF DimCode <> '' THEN
        IF NOT SelectedDim.GET(USERID,ObjectType,ObjectID,'',DimCode) THEN
          ERROR(
            Text001 +
            Text002,
            DimCode,AnalysisViewCode,AnalysisViewName);
    END;

    PROCEDURE CheckViewsAreUpdated@4();
    VAR
      GLEntry@1000 : Record 17;
      GLBudgetEntry@1001 : Record 96;
      UpdateAnalysisView@1002 : Codeunit 410;
      NoNotUpdated@1003 : Integer;
    BEGIN
      IF GLEntry.FIND('+') OR GLBudgetEntry.FIND('+') THEN BEGIN
        NoNotUpdated := 0;
        RESET;
        IF FIND('-') THEN
          REPEAT
            IF ("Last Entry No." < GLEntry."Entry No.") OR
               "Include Budgets" AND ("Last Budget Entry No." < GLBudgetEntry."Entry No.")
            THEN
              NoNotUpdated := NoNotUpdated + 1;
          UNTIL NEXT = 0;
        IF NoNotUpdated > 0 THEN
          IF CONFIRM(
              Text004 +
              Text005 +
              Text007 +
              Text008 +
              Text009,TRUE,NoNotUpdated)
          THEN BEGIN
            IF FIND('-') THEN
              REPEAT
                IF Blocked THEN BEGIN
                  "Refresh When Unblocked" := TRUE;
                  "Last Budget Entry No." := 0;
                  MODIFY;
                END ELSE
                  UpdateAnalysisView.Update(Rec,2,TRUE);
              UNTIL NEXT = 0;
          END ELSE
            ERROR(Text010);
      END;
    END;

    PROCEDURE UpdateLastEntryNo@7();
    VAR
      GLEntry@1000 : Record 17;
    BEGIN
      IF GLEntry.FIND('+') THEN BEGIN
        SETRANGE(Blocked,FALSE);
        IF FIND('-') THEN
          REPEAT
            "Last Entry No." := GLEntry."Entry No.";
            MODIFY;
          UNTIL NEXT = 0;
        SETRANGE(Blocked);
      END;
    END;

    PROCEDURE ValidateDelete@10(FieldName@1000 : Text[100]);
    VAR
      Question@1001 : Text[250];
    BEGIN
      Question := STRSUBSTNO(
        Text011 +
        Text012,FieldName);
      IF NOT DIALOG.CONFIRM(Question, TRUE) THEN
        ERROR(Text013);
    END;

    PROCEDURE AnalysisviewBudgetReset@9();
    VAR
      AnalysisViewBudgetEntry@1000 : Record 366;
    BEGIN
      AnalysisViewBudgetEntry.SETRANGE("Analysis View Code",Code);
      AnalysisViewBudgetEntry.DELETEALL;
      "Last Budget Entry No." := 0;
    END;

    PROCEDURE ValidateModify@11(FieldName@1000 : Text[100]);
    VAR
      Question@1001 : Text[250];
    BEGIN
      Question := STRSUBSTNO(
        Text014 +
        Text015,FieldName);
      IF NOT DIALOG.CONFIRM(Question, TRUE) THEN
        ERROR(Text013);
    END;

    PROCEDURE CopyAnalysisViewFilters@12(ObjectType@1000 : Integer;ObjectID@1001 : Integer;AnalysisViewCode@1002 : Code[10]);
    VAR
      SelectedDim@1003 : Record 369;
      GLAcc@1004 : Record 15;
      BusUnit@1005 : Record 220;
    BEGIN
      IF GET(AnalysisViewCode) THEN BEGIN
        IF "G/L Account Filter" <> '' THEN BEGIN
          IF SelectedDim.GET(
            USERID,ObjectType,ObjectID,AnalysisViewCode,GLAcc.TABLECAPTION)
          THEN BEGIN
            IF SelectedDim."Dimension Value Filter" = '' THEN BEGIN
              SelectedDim."Dimension Value Filter" := "G/L Account Filter";
              SelectedDim.MODIFY;
            END;
          END ELSE BEGIN
            SelectedDim.INIT;
            SelectedDim."User ID" := USERID;
            SelectedDim."Object Type" := ObjectType;
            SelectedDim."Object ID" := ObjectID;
            SelectedDim."Analysis View Code" := AnalysisViewCode;
            SelectedDim."Dimension Code" := GLAcc.TABLECAPTION;
            SelectedDim."Dimension Value Filter" := "G/L Account Filter";
            SelectedDim.INSERT;
          END;
        END;
        IF "Business Unit Filter" <> '' THEN BEGIN
          IF SelectedDim.GET(
            USERID,ObjectType,ObjectID,AnalysisViewCode,BusUnit.TABLECAPTION)
          THEN BEGIN
            IF SelectedDim."Dimension Value Filter" = '' THEN BEGIN
              SelectedDim."Dimension Value Filter" := "Business Unit Filter";
              SelectedDim.MODIFY;
            END;
          END ELSE BEGIN
            SelectedDim.INIT;
            SelectedDim."User ID" := USERID;
            SelectedDim."Object Type" := ObjectType;
            SelectedDim."Object ID" := ObjectID;
            SelectedDim."Analysis View Code" := AnalysisViewCode;
            SelectedDim."Dimension Code" := BusUnit.TABLECAPTION;
            SelectedDim."Dimension Value Filter" := "Business Unit Filter";
            SelectedDim.INSERT;
          END;
        END;
      END;
    END;

    BEGIN
    END.
  }
}
