OBJECT Table 7010140 Prestaciones
{
  OBJECT-PROPERTIES
  {
    Date=13/05/14;
    Time=17:47:38;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    OnInsert=VAR
               rPrestacion@1103355000 : Record 7010140;
             BEGIN
               rPrestacion.RESET;
               rPrestacion.SETRANGE("Codigo producto", "Codigo producto");
               IF rPrestacion.FINDLAST THEN
                 Orden := rPrestacion.Orden + 10000
               ELSE
                 Orden := 10000;

               //+$001
               _CheckCommissionProvider;

               //+$002
               cFuncExc.ControlOficinaExcursion("Codigo producto", TRUE);
             END;

    OnModify=VAR
               rPrest_Cont@1103355000 : Record 7010141;
             BEGIN
               //+$001
               _CheckCommissionProvider;
             END;

    OnDelete=VAR
               rPrest_Cont@1103355000 : Record 7010141;
             BEGIN
               rPrest_Cont.RESET;
               rPrest_Cont.SETRANGE("Codigo Producto", "Codigo producto");
               rPrest_Cont.SETRANGE(Orden, Orden);
               IF rPrest_Cont.FINDSET(TRUE) THEN
                 rPrest_Cont.DELETEALL;
             END;

    CaptionML=[ENU=Features;
               ESP=Prestaciones];
    LookupFormID=Form7010142;
  }
  FIELDS
  {
    { 1   ;   ;Codigo producto     ;Code10        ;TableRelation="Producto agencia".Codigo WHERE (Tipo Producto=CONST(Excursion));
                                                   OnValidate=VAR
                                                                rExcursion@1103355000 : Record 7009710;
                                                              BEGIN
                                                              END;

                                                   CaptionML=[ENU=Product code;
                                                              ESP=Codigo producto];
                                                   Description=PK, FK Producto agencia;
                                                   Editable=No }
    { 3   ;   ;Orden               ;Integer       ;CaptionML=[ENU=Order;
                                                              ESP=Orden];
                                                   Description=PK;
                                                   Editable=No }
    { 5   ;   ;Tipo Contrato       ;Option        ;OnValidate=VAR
                                                                rPrest_Cont@1100217000 : Record 7010141;
                                                              BEGIN
                                                                IF "Tipo Contrato" <> xRec."Tipo Contrato" THEN
                                                                BEGIN
                                                                  rPrest_Cont.RESET;
                                                                  rPrest_Cont.SETRANGE("Codigo Producto", "Codigo producto");
                                                                  rPrest_Cont.SETRANGE(Orden, Orden);
                                                                  IF rPrest_Cont.FINDSET(TRUE) THEN
                                                                    rPrest_Cont.DELETEALL;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Contract type;
                                                              ESP=Tipo contrato];
                                                   OptionCaptionML=[ENU=Excursion,Carrier,,Offical Guide;
                                                                    ESP=Excursion,Transportista,,Guia Oficial];
                                                   OptionString=Excursion,Transportista,,Guia Oficial }
    { 7   ;   ;Descripcion         ;Text30        ;CaptionML=[ENU=Description;
                                                              ESP=Descripcion] }
    { 9   ;   ;Obligatorio         ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[ENU=Compulsory;
                                                              ESP=Obligatorio] }
    { 20  ;   ;Pago Contado        ;Boolean       ;CaptionML=[ENU=Cash Payment;
                                                              ESP=Pago Contado] }
  }
  KEYS
  {
    {    ;Codigo producto,Orden                   ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100253001 : TextConst 'ENU=Only excursion service providers can be used in the trading commission excursions.;ESP=Solo se pueden utilizar proveedores de servicio en las excursiones de comision mercantil.';
      Text002@1100253000 : TextConst 'ENU=Only one provider can be used in the trading commission excursions.;ESP=Solo puede indicar un proveedor en las excursiones de comision mercantil.';
      cFuncExc@1100217000 : Codeunit 7010141;
      Text003@1100217001 : TextConst 'ENU=More than one;ESP=M s de uno';

    LOCAL PROCEDURE _CheckCommissionProvider@1100253000();
    VAR
      lrExcursion@1100253000 : Record 7009710;
      lrPrestacion@1100253002 : Record 7010140;
    BEGIN
      // _CheckCommissionProvider

      lrExcursion.RESET;
      lrExcursion.GET("Codigo producto");
      IF NOT lrExcursion."Proveedor comision mercantil" THEN
        EXIT;

      IF "Tipo Contrato" <> "Tipo Contrato"::Excursion THEN
        ERROR(Text001);

      lrPrestacion.RESET;
      lrPrestacion.SETRANGE ("Codigo producto", "Codigo producto");
      lrPrestacion.SETFILTER(Orden            , '<>%1', Orden);
      IF lrPrestacion.FINDFIRST THEN
        ERROR(Text002);
    END;

    PROCEDURE VendorName@1100217000() : Text[50];
    VAR
      lrContratoPrest@1100217000 : Record 7010141;
      lrContrato@1100217002 : Record 7010142;
      lrVendor@1100217001 : Record 23;
      lrPar@1100217003 : Record 7009700;
    BEGIN
      // VendorName

      lrPar.FINDFIRST;

      lrContratoPrest.RESET;
      lrContratoPrest.SETRANGE ("Codigo Producto", "Codigo producto");
      lrContratoPrest.SETRANGE (Orden            , Orden);
      lrContratoPrest.SETRANGE ("Tipo Contrato"  , "Tipo Contrato");
      lrContratoPrest.SETFILTER(Contrato         , '<>%1', '');
      IF lrContratoPrest.FINDFIRST THEN
      BEGIN
        IF lrContratoPrest.COUNT = 1 THEN
        BEGIN
          IF lrContrato.GET(lrPar.Delegacion, lrContratoPrest.Contrato, lrContratoPrest."Tipo Contrato") THEN
          BEGIN
            IF lrVendor.GET(lrContrato.Proveedor) THEN
              EXIT(lrVendor.Name);
          END;
        END
        ELSE
          EXIT(Text003);
      END;
    END;

    BEGIN
    {
      $001 AJS 30052012 CNT-CAR-12004, EXC-11 Trading commission providers

      $002 JPT 08/08/12 CNT-OC-12004 Incluir filtro por oficina en las excursiones.Control de Oficina en Excursion

      $003 AJS 07012013 Muevo el codigo que comprueba oficina al OnInsert

      $004 AJS 28082013 Elimino las opciones Guia venta y Touroperador del campo Tipo contrato

      $005 AJS 13052014 Nueva funcion VendorName para mostrar en el formulario de definicion de la excursion
    }
    END.
  }
}
