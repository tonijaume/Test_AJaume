OBJECT Table 7010337 Dietas en Tipos Acuerdo
{
  OBJECT-PROPERTIES
  {
    Date=16/10/09;
    Time=12:30:41;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
                   ValidarCampos(TRUE);
             END;

    OnModify=BEGIN
                   ValidarCampos(FALSE);
             END;

    OnRename=BEGIN
                   ValidarCampos(FALSE);
             END;

    CaptionML=ESP=Dietas en Tipos Acuerdo;
  }
  FIELDS
  {
    { 1   ;   ;Tipo Acuerdo        ;Code10        ;TableRelation="Tipo Acuerdo Conductor"."Tipo Acuerdo";
                                                   CaptionML=ESP=Tipo Acuerdo;
                                                   NotBlank=Yes }
    { 3   ;   ;Fecha Inicio Validez;Date          ;CaptionML=ESP=Fecha Inicio Validez;
                                                   NotBlank=Yes }
    { 4   ;   ;Fecha Fin Validez   ;Date          ;CaptionML=ESP=Fecha Fin Validez }
    { 5   ;   ;Hora Inicio         ;Time          ;CaptionML=ESP=Hora Inicio;
                                                   NotBlank=Yes }
    { 6   ;   ;Hora Fin            ;Time          ;CaptionML=ESP=Hora Fin;
                                                   NotBlank=Yes }
  }
  KEYS
  {
    {    ;Tipo Acuerdo,Fecha Inicio Validez,Fecha Fin Validez,Hora Inicio,Hora Fin;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE ValidarCampos@1(Insercion@1100244000 : Boolean);
    VAR
      rFranja@1100244001 : Record 7010337;
      cModulo@1100244002 : Codeunit 7010313;
      ERROR_SOLAPE@1100244003 : TextConst 'ESP=Se ha detectado solapamiento horario';
      vFecA1@1100244004 : Date;
      vFecA2@1100244005 : Date;
      vFecB1@1100244006 : Date;
      vFecB2@1100244007 : Date;
      FECHAS_INCOMPATIBLES@1100244008 : TextConst 'ESP=La fecha final no puede ser menor que la fecha de inicio';
    BEGIN
      IF ("Fecha Fin Validez" <> 0D) AND ("Fecha Fin Validez" < "Fecha Inicio Validez") THEN
        ERROR(FECHAS_INCOMPATIBLES);

      //Validaremos que no haya solapamiento de fechas.
      rFranja.RESET;
      rFranja.SETCURRENTKEY("Tipo Acuerdo","Fecha Inicio Validez");
      rFranja.SETRANGE("Tipo Acuerdo","Tipo Acuerdo");
      IF rFranja.FINDSET THEN
        REPEAT
        //No haremos la comprobaci¢n si se trata del mismo contrato que estamos modificando.
          IF Insercion OR
             (rFranja."Fecha Inicio Validez" <> xRec."Fecha Inicio Validez") OR
             (rFranja."Fecha Fin Validez"    <> xRec."Fecha Fin Validez") OR
             (rFranja."Hora Inicio"          <> xRec."Hora Inicio") OR
             (rFranja."Hora Fin"             <> xRec."Hora Fin") THEN BEGIN

            //Para hacer la validaci¢n de horas miraremos primero si hay intersecci¢n de fechas.
            IF cModulo.InterseccionFechas(Rec."Fecha Inicio Validez",Rec."Fecha Fin Validez",
                                         rFranja."Fecha Inicio Validez",rFranja."Fecha Fin Validez") THEN BEGIN

              //Y en caso de la haya, comprobaremos que no haya solape de horas.
              vFecA1:= Rec."Fecha Inicio Validez";
              IF Rec."Hora Inicio" <= Rec."Hora Fin" THEN
                vFecA2:=vFecA1
              ELSE
                vFecA2:=vFecA1+1;

              vFecB1:= vFecA1;
              IF rFranja."Hora Inicio" <= rFranja."Hora Fin" THEN
                vFecB2:=vFecB1
              ELSE
                vFecB2:=vFecB1+1;

              IF cModulo.InterseccionHoras(vFecA1,vFecA2,vFecB1,vFecB2,Rec."Hora Inicio",
                                           Rec."Hora Fin",
                                           rFranja."Hora Inicio", rFranja."Hora Fin") THEN
                ERROR(ERROR_SOLAPE);

            END;

          END;

        UNTIL rFranja.NEXT=0;
    END;

    BEGIN
    END.
  }
}
