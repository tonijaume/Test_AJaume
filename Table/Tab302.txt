OBJECT Table 302 Finance Charge Memo Header
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00,NAVES4.00;
  }
  PROPERTIES
  {
    DataCaptionFields=No.,Name;
    OnInsert=VAR
               DocDim@1000 : TEMPORARY Record 357;
             BEGIN
               SalesSetup.GET;
               IF "No." = '' THEN BEGIN
                 SalesSetup.TESTFIELD("Fin. Chrg. Memo Nos.");
                 SalesSetup.TESTFIELD("Issued Fin. Chrg. M. Nos.");
                 NoSeriesMgt.InitSeries(
                   SalesSetup."Fin. Chrg. Memo Nos.",xRec."No. Series","Posting Date",
                   "No.","No. Series");
               END;
               "Posting Description" := STRSUBSTNO(Text000,"No.");
               IF ("No. Series" <> '') AND
                  (SalesSetup."Fin. Chrg. Memo Nos." = SalesSetup."Issued Fin. Chrg. M. Nos.")
               THEN
                 "Issuing No. Series" := "No. Series"
               ELSE
                 NoSeriesMgt.SetDefaultSeries("Issuing No. Series",SalesSetup."Issued Fin. Chrg. M. Nos.");
               IF "Posting Date" = 0D THEN
                 "Posting Date" := WORKDATE;
               "Document Date" := WORKDATE;
               "Due Date" := WORKDATE;
               IF GETFILTER("Customer No.") <> '' THEN
                 IF GETRANGEMIN("Customer No.") = GETRANGEMAX("Customer No.") THEN
                   VALIDATE("Customer No.",GETRANGEMIN("Customer No."));

               DimMgt.InsertDocDim(
                 DATABASE::"Finance Charge Memo Header",DocDim."Document Type"::" ","No.",0,
                 "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
             END;

    OnDelete=VAR
               DocDim@1000 : Record 357;
             BEGIN
               FinChrgMemoIssue.DeleteHeader(Rec,IssuedFinChrgMemoHdr);

               FinChrgMemoLine.SETRANGE("Finance Charge Memo No.","No.");
               FinChrgMemoLine.DELETEALL;

               FinChrgMemoCommentLine.SETRANGE(Type,FinChrgMemoCommentLine.Type::"Finance Charge Memo");
               FinChrgMemoCommentLine.SETRANGE("No.","No.");
               FinChrgMemoCommentLine.DELETEALL;

               DimMgt.DeleteDocDim(DATABASE::"Finance Charge Memo Header",DocDim."Document Type"::" ","No.",0);

               IF IssuedFinChrgMemoHdr."No." <> '' THEN BEGIN
                 DELETE;
                 COMMIT;
                 IF CONFIRM(
                      Text001,TRUE,
                      IssuedFinChrgMemoHdr."No.")
                 THEN BEGIN
                   IssuedFinChrgMemoHdr.SETRECFILTER;
                   IssuedFinChrgMemoHdr.PrintRecords(TRUE)
                 END;
               END;
             END;

    CaptionML=[ENU=Finance Charge Memo Header;
               ESP=Cab. doc. inter‚s];
    LookupFormID=Form448;
    DrillDownFormID=Form448;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  SalesSetup.GET;
                                                                  NoSeriesMgt.TestManual(SalesSetup."Fin. Chrg. Memo Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                                "Posting Description" := STRSUBSTNO(Text000,"No.");
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              ESP=N§] }
    { 2   ;   ;Customer No.        ;Code20        ;TableRelation=Customer;
                                                   OnValidate=BEGIN
                                                                IF CurrFieldNo = FIELDNO("Customer No.") THEN
                                                                  IF Undo THEN BEGIN
                                                                    "Customer No." := xRec."Customer No.";
                                                                    EXIT;
                                                                  END;
                                                                IF "Customer No." = '' THEN
                                                                  EXIT;
                                                                Cust.GET("Customer No.");
                                                                Name := Cust.Name;
                                                                "Name 2" := Cust."Name 2";
                                                                Address := Cust.Address;
                                                                "Address 2" := Cust."Address 2";
                                                                "Post Code" := Cust."Post Code";
                                                                City := Cust.City;
                                                                County := Cust.County;
                                                                Contact := Cust.Contact;
                                                                "Country/Region Code" := Cust."Country/Region Code";
                                                                "Language Code" := Cust."Language Code";
                                                                "Currency Code" := Cust."Currency Code";
                                                                "Shortcut Dimension 1 Code" := Cust."Global Dimension 1 Code";
                                                                "Shortcut Dimension 2 Code" := Cust."Global Dimension 2 Code";
                                                                "VAT Registration No." := Cust."VAT Registration No.";
                                                                Cust.TESTFIELD("Customer Posting Group");
                                                                "Customer Posting Group" := Cust."Customer Posting Group";
                                                                "Gen. Bus. Posting Group" := Cust."Gen. Bus. Posting Group";
                                                                "VAT Bus. Posting Group" := Cust."VAT Bus. Posting Group";
                                                                "Tax Area Code" := Cust."Tax Area Code";
                                                                "Tax Liable" := Cust."Tax Liable";
                                                                VALIDATE("Fin. Charge Terms Code",Cust."Fin. Charge Terms Code");

                                                                CreateDim(DATABASE::Customer,"Customer No.");
                                                              END;

                                                   CaptionML=[ENU=Customer No.;
                                                              ESP=N§ cliente] }
    { 3   ;   ;Name                ;Text50        ;CaptionML=[ENU=Name;
                                                              ESP=Nombre] }
    { 4   ;   ;Name 2              ;Text50        ;CaptionML=[ENU=Name 2;
                                                              ESP=Nombre 2] }
    { 5   ;   ;Address             ;Text50        ;CaptionML=[ENU=Address;
                                                              ESP=Direcci¢n] }
    { 6   ;   ;Address 2           ;Text50        ;CaptionML=[ENU=Address 2;
                                                              ESP=Direcci¢n 2] }
    { 7   ;   ;Post Code           ;Code20        ;TableRelation="Post Code";
                                                   OnValidate=BEGIN
                                                                PostCode.ValidatePostCode(City,"Post Code",County);
                                                              END;

                                                   OnLookup=BEGIN
                                                              PostCode.LookUpPostCode(City,"Post Code",County,TRUE);
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Post Code;
                                                              ESP=C.P.] }
    { 8   ;   ;City                ;Text30        ;OnValidate=BEGIN
                                                                PostCode.ValidateCity(City,"Post Code",County);
                                                              END;

                                                   OnLookup=BEGIN
                                                              PostCode.LookUpCity(City,"Post Code",County,TRUE);
                                                            END;

                                                   CaptionML=[ENU=City;
                                                              ESP=Poblaci¢n] }
    { 9   ;   ;County              ;Text30        ;CaptionML=[ENU=County;
                                                              ESP=Provincia] }
    { 10  ;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   CaptionML=[ENU=Country/Region Code;
                                                              ESP=C¢d. pa¡s/regi¢n] }
    { 11  ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   CaptionML=[ENU=Language Code;
                                                              ESP=C¢d. idioma] }
    { 12  ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   OnValidate=BEGIN
                                                                IF CurrFieldNo = FIELDNO("Currency Code") THEN
                                                                  IF Undo THEN BEGIN
                                                                    "Currency Code" := xRec."Currency Code";
                                                                    EXIT;
                                                                  END;
                                                              END;

                                                   CaptionML=[ENU=Currency Code;
                                                              ESP=C¢d. divisa] }
    { 13  ;   ;Contact             ;Text50        ;CaptionML=[ENU=Contact;
                                                              ESP=Contacto] }
    { 14  ;   ;Your Reference      ;Text30        ;CaptionML=[ENU=Your Reference;
                                                              ESP=Su/Ntra. ref.] }
    { 15  ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
                                                              END;

                                                   CaptionML=[ENU=Shortcut Dimension 1 Code;
                                                              ESP=C¢d. dim. acceso dir. 1];
                                                   CaptionClass='1,2,1' }
    { 16  ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");
                                                              END;

                                                   CaptionML=[ENU=Shortcut Dimension 2 Code;
                                                              ESP=C¢d. dim. acceso dir. 2];
                                                   CaptionClass='1,2,2' }
    { 17  ;   ;Customer Posting Group;Code10      ;TableRelation="Customer Posting Group";
                                                   CaptionML=[ENU=Customer Posting Group;
                                                              ESP=Grupo contable cliente];
                                                   Editable=No }
    { 18  ;   ;Gen. Bus. Posting Group;Code10     ;TableRelation="Gen. Business Posting Group";
                                                   OnValidate=BEGIN
                                                                IF xRec."Gen. Bus. Posting Group" <> "Gen. Bus. Posting Group" THEN
                                                                  IF GenBusPostingGrp.ValidateVatBusPostingGroup(GenBusPostingGrp,"Gen. Bus. Posting Group") THEN
                                                                    VALIDATE("VAT Bus. Posting Group",GenBusPostingGrp."Def. VAT Bus. Posting Group");
                                                              END;

                                                   CaptionML=[ENU=Gen. Bus. Posting Group;
                                                              ESP=Grupo contable negocio];
                                                   Editable=No }
    { 19  ;   ;VAT Registration No.;Text20        ;CaptionML=[ENU=VAT Registration No.;
                                                              ESP=CIF/NIF] }
    { 20  ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   CaptionML=[ENU=Reason Code;
                                                              ESP=C¢d. auditor¡a] }
    { 21  ;   ;Posting Date        ;Date          ;CaptionML=[ENU=Posting Date;
                                                              ESP=Fecha registro] }
    { 22  ;   ;Document Date       ;Date          ;OnValidate=BEGIN
                                                                IF CurrFieldNo = FIELDNO("Document Date") THEN
                                                                  IF Undo THEN BEGIN
                                                                    "Document Date" := xRec."Document Date";
                                                                     EXIT;
                                                                  END;
                                                                VALIDATE("Fin. Charge Terms Code");
                                                              END;

                                                   CaptionML=[ENU=Document Date;
                                                              ESP=Fecha emisi¢n documento] }
    { 23  ;   ;Due Date            ;Date          ;CaptionML=[ENU=Due Date;
                                                              ESP=Fecha vencimiento] }
    { 25  ;   ;Fin. Charge Terms Code;Code10      ;TableRelation="Finance Charge Terms";
                                                   OnValidate=BEGIN
                                                                IF CurrFieldNo = FIELDNO("Fin. Charge Terms Code") THEN
                                                                  IF Undo THEN BEGIN
                                                                    "Fin. Charge Terms Code" := xRec."Fin. Charge Terms Code";
                                                                    EXIT;
                                                                  END;
                                                                IF "Fin. Charge Terms Code" <> '' THEN BEGIN
                                                                  FinChrgTerms.GET("Fin. Charge Terms Code");
                                                                  "Post Interest" := FinChrgTerms."Post Interest";
                                                                  "Post Additional Fee" := FinChrgTerms."Post Additional Fee";
                                                                  IF "Document Date" <> 0D THEN
                                                                    "Due Date" := CALCDATE(FinChrgTerms."Due Date Calculation","Document Date");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Fin. Charge Terms Code;
                                                              ESP=C¢d. inter‚s] }
    { 26  ;   ;Post Interest       ;Boolean       ;CaptionML=[ENU=Post Interest;
                                                              ESP=Registrar inter‚s] }
    { 27  ;   ;Post Additional Fee ;Boolean       ;CaptionML=[ENU=Post Additional Fee;
                                                              ESP=Registrar recargo fijo] }
    { 29  ;   ;Posting Description ;Text50        ;CaptionML=[ENU=Posting Description;
                                                              ESP=Texto de registro] }
    { 30  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Fin. Charge Comment Line" WHERE (Type=CONST(Finance Charge Memo),
                                                                                                       No.=FIELD(No.)));
                                                   CaptionML=[ENU=Comment;
                                                              ESP=Comentario];
                                                   Editable=No }
    { 31  ;   ;Remaining Amount    ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Finance Charge Memo Line"."Remaining Amount" WHERE (Finance Charge Memo No.=FIELD(No.)));
                                                   CaptionML=[ENU=Remaining Amount;
                                                              ESP=Importe pendiente];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 32  ;   ;Interest Amount     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Finance Charge Memo Line".Amount WHERE (Finance Charge Memo No.=FIELD(No.),
                                                                                                            Type=CONST(Customer Ledger Entry)));
                                                   CaptionML=[ENU=Interest Amount;
                                                              ESP=Importe inter‚s];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 33  ;   ;Additional Fee      ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Finance Charge Memo Line".Amount WHERE (Finance Charge Memo No.=FIELD(No.),
                                                                                                            Type=CONST(G/L Account)));
                                                   CaptionML=[ENU=Additional Fee;
                                                              ESP=Recargo fijo];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 34  ;   ;VAT Amount          ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Finance Charge Memo Line"."VAT Amount" WHERE (Finance Charge Memo No.=FIELD(No.)));
                                                   CaptionML=[ENU=VAT Amount;
                                                              ESP=Importe IVA];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 37  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              ESP=Nos. serie];
                                                   Editable=No }
    { 38  ;   ;Issuing No. Series  ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF "Issuing No. Series" <> '' THEN BEGIN
                                                                  SalesSetup.GET;
                                                                  SalesSetup.TESTFIELD("Fin. Chrg. Memo Nos.");
                                                                  SalesSetup.TESTFIELD("Issued Fin. Chrg. M. Nos.");
                                                                  NoSeriesMgt.TestSeries(SalesSetup."Issued Fin. Chrg. M. Nos.","Issuing No. Series");
                                                                END;
                                                                TESTFIELD("Issuing No.",'');
                                                              END;

                                                   OnLookup=BEGIN
                                                              WITH FinChrgMemoHeader DO BEGIN
                                                                FinChrgMemoHeader := Rec;
                                                                SalesSetup.GET;
                                                                SalesSetup.TESTFIELD("Fin. Chrg. Memo Nos.");
                                                                SalesSetup.TESTFIELD("Issued Fin. Chrg. M. Nos.");
                                                                IF NoSeriesMgt.LookupSeries(SalesSetup."Issued Fin. Chrg. M. Nos.","Issuing No. Series") THEN
                                                                  VALIDATE("Issuing No. Series");
                                                                  Rec := FinChrgMemoHeader;
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Issuing No. Series;
                                                              ESP=N§ serie emisi¢n] }
    { 39  ;   ;Issuing No.         ;Code20        ;CaptionML=[ENU=Issuing No.;
                                                              ESP=N§ emisi¢n] }
    { 41  ;   ;Tax Area Code       ;Code20        ;TableRelation="Tax Area";
                                                   CaptionML=[ENU=Tax Area Code;
                                                              ESP=C¢d.  rea impuesto] }
    { 42  ;   ;Tax Liable          ;Boolean       ;CaptionML=[ENU=Tax Liable;
                                                              ESP=Sujeto a impuesto] }
    { 43  ;   ;VAT Bus. Posting Group;Code10      ;TableRelation="VAT Business Posting Group";
                                                   CaptionML=[ENU=VAT Bus. Posting Group;
                                                              ESP=Grupo registro IVA neg.] }
    { 9000;   ;Assigned User ID    ;Code20        ;TableRelation="User Setup";
                                                   CaptionML=[ENU=Assigned User ID;
                                                              ESP=Id. usuario asignado] }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Customer No.,Currency Code               }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;No.,Customer No.,Name,Due Date           }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Finance Charge Memo %1;ESP=Doc. inter‚s %1';
      Text001@1001 : TextConst 'ENU=Do you want to print finance charge memo %1?;ESP=¨Confirma que desea imprimir el documento de inter‚s %1?';
      Text002@1002 : TextConst 'ENU=This change will cause the existing lines to be deleted for this finance charge memo.\\;ESP=Este cambio provocar  que se borren las l¡neas existentes para este doc. de inter‚s.\\';
      Text003@1003 : TextConst 'ENU=Do you want to continue?;ESP=¨Confirma que desea continuar?';
      Text004@1004 : TextConst 'ENU=There is not enough space to insert the text.;ESP=No hay espacio suficiente para a¤adir el texto.';
      Text005@1005 : TextConst 'ENU=Deleting this document will cause a gap in the number series for finance charge memos.;ESP=Si borra el documento, se provocar  una discontinuidad en la numeraci¢n de la serie de doc. de inter‚s.';
      Text006@1006 : TextConst 'ENU=An empty finance charge memo %1 will be created to fill this gap in the number series.\\;ESP=Se crear  un doc. de inter‚s en blanco %1 para completar la misma.\\';
      SalesSetup@1007 : Record 311;
      CustPostingGr@1008 : Record 92;
      FinChrgTerms@1009 : Record 5;
      CurrForFinChrgTerms@1010 : Record 328;
      FinChrgText@1011 : Record 301;
      FinChrgMemoHeader@1012 : Record 302;
      FinChrgMemoLine@1013 : Record 303;
      FinChrgMemoCommentLine@1014 : Record 306;
      Cust@1015 : Record 18;
      PostCode@1016 : Record 225;
      IssuedFinChrgMemoHdr@1017 : Record 304;
      GenBusPostingGrp@1018 : Record 250;
      CurrExchRate@1019 : Record 330;
      ApplicationManagement@1028 : Codeunit 1;
      NoSeriesMgt@1020 : Codeunit 396;
      TransferExtendedText@1021 : Codeunit 378;
      FinChrgMemoIssue@1022 : Codeunit 395;
      DimMgt@1023 : Codeunit 408;
      NextLineNo@1024 : Integer;
      LineSpacing@1025 : Integer;
      FinChrgMemoTotal@1026 : Decimal;
      OK@1027 : Boolean;
      Currency@1029 : Record 4;

    PROCEDURE AssistEdit@5(OldFinChrgMemoHeader@1000 : Record 302) : Boolean;
    BEGIN
      WITH FinChrgMemoHeader DO BEGIN
        FinChrgMemoHeader := Rec;
        SalesSetup.GET;
        SalesSetup.TESTFIELD("Fin. Chrg. Memo Nos.");
        SalesSetup.TESTFIELD("Issued Fin. Chrg. M. Nos.");
        IF NoSeriesMgt.SelectSeries(SalesSetup."Fin. Chrg. Memo Nos.",OldFinChrgMemoHeader."No. Series","No. Series") THEN BEGIN
          SalesSetup.GET;
          SalesSetup.TESTFIELD("Fin. Chrg. Memo Nos.");
          SalesSetup.TESTFIELD("Issued Fin. Chrg. M. Nos.");
          NoSeriesMgt.SetSeries("No.");
          Rec := FinChrgMemoHeader;
          EXIT(TRUE);
        END;
      END;
    END;

    LOCAL PROCEDURE Undo@6() : Boolean;
    BEGIN
      FinChrgMemoLine.SETRANGE("Finance Charge Memo No.","No.");
      IF FinChrgMemoLine.FIND('-') THEN BEGIN
        COMMIT;
        IF NOT
           CONFIRM(
             Text002 +
             Text003,
             FALSE)
        THEN
          EXIT(TRUE);
        FinChrgMemoLine.DELETEALL;
        MODIFY;
      END;
    END;

    PROCEDURE InsertLines@3();
    BEGIN
      TESTFIELD("Fin. Charge Terms Code");
      FinChrgTerms.GET("Fin. Charge Terms Code");
      IF FinChrgTerms."Additional Fee (LCY)" <> 0 THEN BEGIN
        FinChrgMemoLine.RESET;
        FinChrgMemoLine.SETRANGE("Finance Charge Memo No.","No.");
        FinChrgMemoLine."Finance Charge Memo No." := "No.";
        IF FinChrgMemoLine.FIND('+') THEN
          NextLineNo := FinChrgMemoLine."Line No."
         ELSE
          NextLineNo := 0;
        IF (FinChrgMemoLine.Type <> FinChrgMemoLine.Type::" ") OR
           (FinChrgMemoLine.Description <> '')
        THEN BEGIN
          LineSpacing := 10000;
          InsertBlankLine(FinChrgMemoLine."Line Type"::"Finance Charge Memo Line");
        END;
        NextLineNo := NextLineNo + 10000;
        FinChrgMemoLine.INIT;
        FinChrgMemoLine."Line No." := NextLineNo;
        FinChrgMemoLine.Type := FinChrgMemoLine.Type::"G/L Account";
        TESTFIELD("Customer Posting Group");
        CustPostingGr.GET("Customer Posting Group");
        CustPostingGr.TESTFIELD("Additional Fee Account");
        FinChrgMemoLine.VALIDATE("No.",CustPostingGr."Additional Fee Account");
        FinChrgMemoLine.Description := CurrForFinChrgTerms.FIELDCAPTION("Additional Fee");
        IF "Currency Code" = '' THEN
          FinChrgMemoLine.VALIDATE(Amount,FinChrgTerms."Additional Fee (LCY)")
        ELSE BEGIN
          IF NOT CurrForFinChrgTerms.GET("Fin. Charge Terms Code","Currency Code") THEN
            CurrForFinChrgTerms."Additional Fee" :=
              CurrExchRate.ExchangeAmtLCYToFCY(
                "Posting Date","Currency Code",
                FinChrgTerms."Additional Fee (LCY)",CurrExchRate.ExchangeRate(
                  "Posting Date", "Currency Code"));
          FinChrgMemoLine.VALIDATE(Amount,CurrForFinChrgTerms."Additional Fee");
        END;
        FinChrgMemoLine.INSERT;
        IF TransferExtendedText.FinChrgMemoCheckIfAnyExtText(FinChrgMemoLine,FALSE) THEN
          TransferExtendedText.InsertFinChrgMemoExtText(FinChrgMemoLine);
      END;
      FinChrgMemoLine."Line No." := FinChrgMemoLine."Line No." + 10000;
      FinanceChargeRounding(Rec);
      InsertBeginTexts(Rec);
      InsertEndTexts(Rec);
      MODIFY;
    END;

    PROCEDURE UpdateLines@8(FinChrgMemoHeader2@1000 : Record 302);
    BEGIN
      FinChrgMemoLine.RESET;
      FinChrgMemoLine.SETRANGE("Finance Charge Memo No.",FinChrgMemoHeader2."No.");
      OK := FinChrgMemoLine.FIND('-');
      WHILE OK DO BEGIN
        OK :=
          (FinChrgMemoLine.Type = FinChrgMemoLine.Type::" ") AND
          (FinChrgMemoLine."Attached to Line No." = 0);
        IF OK THEN BEGIN
          FinChrgMemoLine.DELETE(TRUE);
          OK := FinChrgMemoLine.NEXT <> 0;
        END;
      END;
      OK := FinChrgMemoLine.FIND('+');
      WHILE OK DO BEGIN
        OK :=
          (FinChrgMemoLine.Type = FinChrgMemoLine.Type::" ") AND
          (FinChrgMemoLine."Attached to Line No." = 0);
        IF OK THEN BEGIN
          FinChrgMemoLine.DELETE(TRUE);
          OK := FinChrgMemoLine.NEXT(-1) <> 0;
        END;
      END;
      FinChrgMemoLine.SETRANGE("Line Type",FinChrgMemoLine."Line Type"::Rounding);
      IF FinChrgMemoLine.FINDFIRST THEN
        FinChrgMemoLine.DELETE(TRUE);

      FinChrgMemoLine.SETRANGE("Line Type");
      FinChrgMemoLine.FINDLAST;
      FinChrgMemoLine."Line No." := FinChrgMemoLine."Line No." + 10000;
      FinanceChargeRounding(FinChrgMemoHeader2);

      InsertBeginTexts(FinChrgMemoHeader2);
      InsertEndTexts(FinChrgMemoHeader2);
    END;

    LOCAL PROCEDURE InsertBeginTexts@11(FinChrgMemoHeader2@1000 : Record 302);
    BEGIN
      FinChrgText.RESET;
      FinChrgText.SETRANGE("Fin. Charge Terms Code",FinChrgMemoHeader2."Fin. Charge Terms Code");
      FinChrgText.SETRANGE(Position,FinChrgText.Position::Beginning);

      FinChrgMemoLine.RESET;
      FinChrgMemoLine.SETRANGE("Finance Charge Memo No.",FinChrgMemoHeader2."No.");
      FinChrgMemoLine."Finance Charge Memo No." := FinChrgMemoHeader2."No.";
      IF FinChrgMemoLine.FIND('-') THEN BEGIN
        LineSpacing := FinChrgMemoLine."Line No." DIV (FinChrgText.COUNT + 2);
        IF LineSpacing = 0 THEN
          ERROR(Text004);
      END ELSE
        LineSpacing := 10000;
      NextLineNo := 0;
      InsertTextLines(FinChrgMemoHeader2);
    END;

    LOCAL PROCEDURE InsertEndTexts@12(FinChrgMemoHeader2@1000 : Record 302);
    BEGIN
      FinChrgText.RESET;
      FinChrgText.SETRANGE("Fin. Charge Terms Code",FinChrgMemoHeader2."Fin. Charge Terms Code");
      FinChrgText.SETRANGE(Position,FinChrgText.Position::Ending);

      FinChrgMemoLine.RESET;
      FinChrgMemoLine.SETRANGE("Finance Charge Memo No.",FinChrgMemoHeader2."No.");
      FinChrgMemoLine."Finance Charge Memo No." := FinChrgMemoHeader2."No.";
      IF FinChrgMemoLine.FIND('+') THEN
        NextLineNo := FinChrgMemoLine."Line No."
      ELSE BEGIN
        FinChrgMemoLine.SETFILTER("Line Type",'%1|%2',
                                  FinChrgMemoLine."Line Type"::"Finance Charge Memo Line",
                                  FinChrgMemoLine."Line Type"::Rounding);
        IF FinChrgMemoLine.FIND('+') THEN
          NextLineNo := FinChrgMemoLine."Line No."
        ELSE
          NextLineNo := 0;
      END;
      FinChrgMemoLine.SETRANGE("Line Type");
      LineSpacing := 10000;
      InsertTextLines(FinChrgMemoHeader2);
    END;

    LOCAL PROCEDURE InsertTextLines@4(FinChrgMemoHeader2@1000 : Record 302);
    BEGIN
      IF FinChrgText.FIND('-') THEN BEGIN
        IF FinChrgText.Position = FinChrgText.Position::Ending THEN
          InsertBlankLine(FinChrgMemoLine."Line Type"::"Ending Text");
        IF NOT FinChrgTerms.GET(FinChrgMemoHeader2."Fin. Charge Terms Code") THEN
          FinChrgTerms.INIT;

        FinChrgMemoHeader2.CALCFIELDS(
          "Remaining Amount","Interest Amount","Additional Fee","VAT Amount");
        FinChrgMemoTotal :=
          FinChrgMemoHeader2."Remaining Amount" + FinChrgMemoHeader2."Interest Amount" +
          FinChrgMemoHeader2."Additional Fee" + FinChrgMemoHeader2."VAT Amount";

        REPEAT
          NextLineNo := NextLineNo + LineSpacing;
          FinChrgMemoLine.INIT;
          FinChrgMemoLine."Line No." := NextLineNo;
          FinChrgMemoLine.Description :=
            COPYSTR(
              STRSUBSTNO(
                FinChrgText.Text,
                FinChrgMemoHeader2."Document Date",
                FinChrgMemoHeader2."Due Date",
                FinChrgTerms."Interest Rate",
                FinChrgMemoHeader2."Remaining Amount",
                FinChrgMemoHeader2."Interest Amount",
                FinChrgMemoHeader2."Additional Fee",
                FORMAT(FinChrgMemoTotal,0,ApplicationManagement.AutoFormatTranslate(1,FinChrgMemoHeader."Currency Code")),
                FinChrgMemoHeader2."Currency Code",
                FinChrgMemoHeader2."Posting Date"),
              1,MAXSTRLEN(FinChrgMemoLine.Description));
          IF FinChrgText.Position = FinChrgText.Position::Beginning THEN
            FinChrgMemoLine."Line Type" := FinChrgMemoLine."Line Type"::"Beginning Text"
          ELSE
            FinChrgMemoLine."Line Type" := FinChrgMemoLine."Line Type"::"Ending Text";
          FinChrgMemoLine.INSERT;
        UNTIL FinChrgText.NEXT = 0;
        IF FinChrgText.Position = FinChrgText.Position::Beginning THEN
          InsertBlankLine(FinChrgMemoLine."Line Type"::"Beginning Text");
      END;
    END;

    PROCEDURE InsertBlankLine@9(LineType@1000 : Integer);
    BEGIN
      NextLineNo := NextLineNo + LineSpacing;
      FinChrgMemoLine.INIT;
      FinChrgMemoLine."Line No." := NextLineNo;
      FinChrgMemoLine."Line Type" := LineType;
      FinChrgMemoLine.INSERT;
    END;

    PROCEDURE PrintRecords@1();
    VAR
      FinChrgMemoHeader@1000 : Record 302;
      ReportSelection@1001 : Record 77;
    BEGIN
      WITH FinChrgMemoHeader DO BEGIN
        COPY(Rec);
        ReportSelection.SETRANGE(Usage,ReportSelection.Usage::"F.C.Test");
        ReportSelection.SETFILTER("Report ID",'<>0');
        ReportSelection.ASCENDING := FALSE;
        ReportSelection.FIND('-');
        REPEAT
          REPORT.RUN(ReportSelection."Report ID",TRUE,FALSE,FinChrgMemoHeader);
        UNTIL ReportSelection.NEXT = 0;
      END;
    END;

    PROCEDURE FormatAddr@30(VAR AddrLines@1000 : ARRAY [8] OF Text[50]);
    VAR
      Cust@1001 : Record 18;
      FormatAddrCodeunit@1002 : Codeunit 365;
    BEGIN
      Cust.Contact := Contact;
      Cust.Name := Name;
      Cust."Name 2" := "Name 2";
      Cust.Address := Address;
      Cust."Address 2" := "Address 2";
      Cust."Post Code" := "Post Code";
      Cust.City := City;
      Cust.County := County;
      Cust."Country/Region Code" := "Country/Region Code";
      FormatAddrCodeunit.Customer(AddrLines,Cust);
    END;

    PROCEDURE ConfirmDeletion@2() : Boolean;
    BEGIN
      FinChrgMemoIssue.TestDeleteHeader(Rec,IssuedFinChrgMemoHdr);
      IF IssuedFinChrgMemoHdr."No." <> '' THEN
        IF NOT CONFIRM(
             Text005 +
             Text006 +
             Text003,TRUE,
             IssuedFinChrgMemoHdr."No.")
        THEN
          EXIT;
      EXIT(TRUE);
    END;

    PROCEDURE CreateDim@16(Type1@1000 : Integer;No1@1001 : Code[20]);
    VAR
      DocDim@1002 : TEMPORARY Record 357;
      SourceCodeSetup@1003 : Record 242;
      TableID@1004 : ARRAY [10] OF Integer;
      No@1005 : ARRAY [10] OF Code[20];
    BEGIN
      SourceCodeSetup.GET;
      TableID[1] := Type1;
      No[1] := No1;
      "Shortcut Dimension 1 Code" := '';
      "Shortcut Dimension 2 Code" := '';
      DimMgt.GetDefaultDim(
        TableID,No,SourceCodeSetup."Finance Charge Memo",
        "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
      IF "No." <> '' THEN
        DimMgt.UpdateDocDefaultDim(
          DATABASE::"Finance Charge Memo Header",DocDim."Document Type"::" ","No.",0,
          "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
    END;

    PROCEDURE ValidateShortcutDimCode@19(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    VAR
      DocDim@1002 : TEMPORARY Record 357;
    BEGIN
      DimMgt.ValidateDimValueCode(FieldNumber,ShortcutDimCode);
      IF "No." <> '' THEN BEGIN
        DimMgt.SaveDocDim(
          DATABASE::"Finance Charge Memo Header",DocDim."Document Type"::" ","No.",0,FieldNumber,ShortcutDimCode);
        MODIFY;
      END ELSE
        DimMgt.SaveTempDim(FieldNumber,ShortcutDimCode);
    END;

    LOCAL PROCEDURE FinanceChargeRounding@10(FinanceChargeHeader@1001 : Record 302);
    VAR
      TotalAmountInclVAT@1004 : Decimal;
      FinanceChargeRoundingAmount@1000 : Decimal;
    BEGIN
      GetCurrency(FinanceChargeHeader);
      IF Currency."Invoice Rounding Precision" = 0 THEN
        EXIT;

      FinanceChargeHeader.CALCFIELDS(
        "Interest Amount","Additional Fee","VAT Amount");

      TotalAmountInclVAT := FinanceChargeHeader."Interest Amount" +
                            FinanceChargeHeader."Additional Fee" +
                            FinanceChargeHeader."VAT Amount";
      FinanceChargeRoundingAmount :=
        -ROUND(
          TotalAmountInclVAT -
          ROUND(
            TotalAmountInclVAT,
            Currency."Invoice Rounding Precision",
            Currency.InvoiceRoundingDirection),
          Currency."Amount Rounding Precision");
      IF FinanceChargeRoundingAmount <> 0 THEN BEGIN
        CustPostingGr.GET(FinanceChargeHeader."Customer Posting Group");
        CustPostingGr.TESTFIELD("Invoice Rounding Account");
        WITH FinChrgMemoLine DO BEGIN
          INIT;
          VALIDATE(Type,Type::"G/L Account");
          "System-Created Entry" := TRUE;
          VALIDATE("No.",CustPostingGr."Invoice Rounding Account");
          VALIDATE(
            Amount,
            ROUND(
              FinanceChargeRoundingAmount / (1 + ("VAT %" / 100)),
              Currency."Amount Rounding Precision"));
          "VAT Amount" := FinanceChargeRoundingAmount - Amount;
          "Line Type" := "Line Type"::Rounding;
          INSERT;
        END;
      END;
    END;

    LOCAL PROCEDURE GetCurrency@17(FinanceChargeHeader@1001 : Record 302);
    BEGIN
      WITH FinanceChargeHeader DO
        IF "Currency Code" = '' THEN
          Currency.InitRoundingPrecision
        ELSE BEGIN
          Currency.GET("Currency Code");
          Currency.TESTFIELD("Amount Rounding Precision");
        END;
    END;

    PROCEDURE UpdateFinanceChargeRounding@7(FinanceChargeHeader@1000 : Record 302);
    VAR
      OldLineNo@1001 : Integer;
    BEGIN
      FinChrgMemoLine.RESET;
      FinChrgMemoLine.SETRANGE("Finance Charge Memo No.",FinanceChargeHeader."No.");
      FinChrgMemoLine.SETRANGE("Line Type",FinChrgMemoLine."Line Type"::Rounding);
      IF FinChrgMemoLine.FINDFIRST THEN
        FinChrgMemoLine.DELETE(TRUE);

      FinChrgMemoLine.SETRANGE("Line Type");
      FinChrgMemoLine.SETFILTER(Type,'<>%1',FinChrgMemoLine.Type::" ");
      IF FinChrgMemoLine.FINDLAST THEN BEGIN
        OldLineNo := FinChrgMemoLine."Line No.";
        FinChrgMemoLine.SETRANGE(Type);
        IF FinChrgMemoLine.NEXT <> 0 THEN
          FinChrgMemoLine."Line No." := OldLineNo + ((FinChrgMemoLine."Line No." - OldLineNo) / 2)
        ELSE
          FinChrgMemoLine."Line No." := FinChrgMemoLine."Line No." + 10000;
      END ELSE
        FinChrgMemoLine."Line No." := 10000;

      FinanceChargeRounding(FinanceChargeHeader);
    END;

    BEGIN
    END.
  }
}
