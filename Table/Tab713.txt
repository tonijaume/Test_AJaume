OBJECT Table 713 BA Db. Cube Dimension
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("BA Db. Code");
               TESTFIELD("BA Db. Cube Line No.");
               TESTFIELD("Line No.");
               TESTFIELD("Table No.");
               TESTFIELD("Field No.");
               TESTFIELD("BA Db. Dimension Line");
               TESTFIELD(Name);
             END;

    OnModify=BEGIN
               TESTFIELD("BA Db. Cube Line No.");
               TESTFIELD("Table No.");
               TESTFIELD("Field No.");
               IF ("BA Db. Dimension Line" = 0) AND ("Dimension Code" = '') THEN
                 ERROR(Text002,FIELDCAPTION("BA Db. Dimension Line"),FIELDCAPTION("Dimension Code"));
               IF "Dimension Code" <> '' THEN
                 TESTFIELD("Dimension Entry Table No.");
             END;

    CaptionML=[ENU=BA Db. Cube Dimension;
               ESP=Dimensi¢n cubo BD BA];
  }
  FIELDS
  {
    { 1   ;   ;BA Db. Code         ;Code10        ;TableRelation="BA Database";
                                                   CaptionML=[ENU=BA Db. Code;
                                                              ESP=C¢digo BD de BA] }
    { 2   ;   ;BA Db. Cube Line No.;Integer       ;CaptionML=[ENU=BA Db. Cube Line No.;
                                                              ESP=N§ l¡nea del cubo de BD BA] }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ESP=N§ l¡nea] }
    { 4   ;   ;Name                ;Text50        ;OnValidate=VAR
                                                                BAOvwNewElem@1000 : Codeunit 701;
                                                              BEGIN
                                                                TESTFIELD(Name);
                                                                Name := BAOvwNewElem.ConvToDSOName(Name);
                                                              END;

                                                   CaptionML=[ENU=Name;
                                                              ESP=Nombre] }
    { 5   ;   ;Table No.           ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   OnValidate=VAR
                                                                Object@1000 : Record 2000000001;
                                                              BEGIN
                                                                IF "Table No." <> 0 THEN
                                                                  Object.GET(Object.Type::Table,'',"Table No.");
                                                                IF "Table No." <> xRec."Table No." THEN
                                                                  "Field No." := 0;
                                                                CALCFIELDS("Table Name");
                                                              END;

                                                   OnLookup=VAR
                                                              Object@1000 : Record 2000000001;
                                                              Objects@1001 : Form 358;
                                                            BEGIN
                                                              IF "Table No." <> 0 THEN BEGIN
                                                                Object.GET(Object.Type::Table,'',"Table No.");
                                                                Objects.SETRECORD(Object);
                                                              END;
                                                              Object.SETRANGE(Type,Object.Type::Table);
                                                              Objects.SETTABLEVIEW(Object);
                                                              Objects.LOOKUPMODE := TRUE;
                                                              IF Objects.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                Objects.GETRECORD(Object);
                                                                "Table No." := Object.ID;
                                                              END;
                                                              IF "Table No." <> xRec."Table No." THEN
                                                                "Field No." := 0;
                                                              CALCFIELDS("Table Name");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Table No.;
                                                              ESP=Tabla n§] }
    { 6   ;   ;Field No.           ;Integer       ;OnValidate=VAR
                                                                Field@1000 : Record 2000000041;
                                                              BEGIN
                                                                IF "Field No." <> 0 THEN BEGIN
                                                                  TESTFIELD("Table No.");
                                                                  Field.GET("Table No.","Field No.");
                                                                END;
                                                                CALCFIELDS("Field Name");
                                                                IF Name = '' THEN
                                                                  IF STRLEN(STRSUBSTNO(Text001,"Table Name","Field Name")) > MAXSTRLEN(Name) THEN
                                                                    VALIDATE(Name,"Field Name")
                                                                  ELSE
                                                                    VALIDATE(Name,STRSUBSTNO(Text001,"Table Name","Field Name"));
                                                              END;

                                                   OnLookup=VAR
                                                              Field@1000 : Record 2000000041;
                                                              Fields@1001 : Form 7702;
                                                            BEGIN
                                                              TESTFIELD("Table No.");
                                                              IF "Field No." <> 0 THEN BEGIN
                                                                Field.GET("Table No.","Field No.");
                                                                Fields.SETRECORD(Field);
                                                              END;
                                                              Field.SETRANGE(TableNo,"Table No.");
                                                              Field.SETRANGE(Class,Field.Class::Normal);
                                                              Field.SETFILTER(Type,'<>%1',Field.Type::Decimal);
                                                              Fields.SETTABLEVIEW(Field);
                                                              Fields.LOOKUPMODE := TRUE;
                                                              IF Fields.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                Fields.GETRECORD(Field);
                                                                VALIDATE("Field No.",Field."No.");
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Field No.;
                                                              ESP=N§ campo] }
    { 7   ;   ;Table Name          ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table No.)));
                                                   CaptionML=[ENU=Table Name;
                                                              ESP=Nombre tabla] }
    { 8   ;   ;Field Name          ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Table No.),
                                                                                                   No.=FIELD(Field No.)));
                                                   CaptionML=[ENU=Field Name;
                                                              ESP=Nombre de campo] }
    { 9   ;   ;BA Db. Dimension Line;Integer      ;TableRelation="BA Db. Dimension"."Line No." WHERE (BA Db. Code=FIELD(BA Db. Code));
                                                   OnValidate=BEGIN
                                                                IF "BA Db. Dimension Line" <> 0 THEN
                                                                  CALCFIELDS("BA Db. Dimension Name");
                                                              END;

                                                   OnLookup=VAR
                                                              BADbDim@1000 : Record 706;
                                                              BADbDimList@1001 : Form 708;
                                                            BEGIN
                                                              TESTFIELD("BA Db. Code");
                                                              TESTFIELD("BA Db. Cube Line No.");
                                                              BADbDim.SETRANGE("BA Db. Code","BA Db. Code");
                                                              BADbDimList.SETTABLEVIEW(BADbDim);
                                                              IF "BA Db. Dimension Line" <> 0 THEN BEGIN
                                                                BADbDim.GET("BA Db. Code","BA Db. Dimension Line");
                                                                BADbDimList.SETRECORD(BADbDim);
                                                              END;
                                                              BADbDimList.LOOKUPMODE := TRUE;
                                                              IF BADbDimList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                BADbDimList.GETRECORD(BADbDim);
                                                                "BA Db. Dimension Line" := BADbDim."Line No.";
                                                                CALCFIELDS("BA Db. Dimension Name");
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=BA Db. Dimension Line;
                                                              ESP=L¡nea de dimensi¢n de BD de BA];
                                                   BlankZero=Yes }
    { 10  ;   ;BA Db. Dimension Name;Text80       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("BA Db. Dimension".Name WHERE (BA Db. Code=FIELD(BA Db. Code),
                                                                                                     Line No.=FIELD(BA Db. Dimension Line)));
                                                   CaptionML=[ENU=BA Db. Dimension Name;
                                                              ESP=Nombre de dimensi¢n BD de BA] }
    { 11  ;   ;Dimension Code      ;Code20        ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Dimension Name");
                                                                IF xRec."Dimension Code" = '' THEN
                                                                  Name := "Dimension Name";
                                                              END;

                                                   CaptionML=[ENU=Dimension Code;
                                                              ESP=C¢d. dimensi¢n] }
    { 12  ;   ;Dimension Name      ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Dimension.Name WHERE (Code=FIELD(Dimension Code)));
                                                   CaptionML=[ENU=Dimension Name;
                                                              ESP=Nombre de dimensi¢n] }
    { 13  ;   ;Dimension Entry Table No.;Integer  ;TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   OnValidate=VAR
                                                                Object@1000 : Record 2000000001;
                                                              BEGIN
                                                                TESTFIELD("Dimension Code");
                                                                IF "Dimension Entry Table No." <> 0 THEN
                                                                  Object.GET(Object.Type::Table,'',"Dimension Entry Table No.");
                                                                CALCFIELDS("Dimension Entry Table Name");
                                                              END;

                                                   OnLookup=VAR
                                                              Object@1000 : Record 2000000001;
                                                              Objects@1001 : Form 358;
                                                            BEGIN
                                                              TESTFIELD("Dimension Code");
                                                              IF "Dimension Entry Table No." <> 0 THEN BEGIN
                                                                Object.GET(Object.Type::Table,'',"Dimension Entry Table No.");
                                                                Objects.SETRECORD(Object);
                                                              END;
                                                              Object.SETRANGE(Type,Object.Type::Table);
                                                              Object.SETFILTER(
                                                                ID,'%1|%2|%3|%4|%5|%6|%7|%8|%9|%10',
                                                                DATABASE::"Ledger Entry Dimension",
                                                                DATABASE::"Journal Line Dimension",
                                                                DATABASE::"Document Dimension",
                                                                DATABASE::"Production Document Dimension",
                                                                DATABASE::"Posted Document Dimension",
                                                                DATABASE::"G/L Budget Dimension",
                                                                DATABASE::"Service Contract Dimension",
                                                                DATABASE::"Document Dimension Archive",
                                                                DATABASE::"FA Allocation Dimension",
                                                                DATABASE::"Item Budget Dimension");
                                                              Objects.SETTABLEVIEW(Object);
                                                              Objects.LOOKUPMODE := TRUE;
                                                              IF Objects.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                Objects.GETRECORD(Object);
                                                                "Dimension Entry Table No." := Object.ID;
                                                              END;
                                                              CALCFIELDS("Dimension Entry Table Name");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Dimension Entry Table No.;
                                                              ESP=N§ de tabla de mov. dimensi¢n];
                                                   BlankZero=Yes }
    { 14  ;   ;Dimension Entry Table Name;Text80  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Dimension Entry Table No.)));
                                                   CaptionML=[ENU=Dimension Entry Table Name;
                                                              ESP=Nombre de tabla de mov. dimensi¢n] }
    { 15  ;   ;Filter              ;Text250       ;OnValidate=VAR
                                                                BACube@1000 : Record 709;
                                                              BEGIN
                                                                BACube.GET("BA Db. Code","BA Db. Cube Line No.");
                                                                TESTFIELD("Table No.",BACube."Fact Table No.");
                                                              END;

                                                   CaptionML=[ENU=Filter;
                                                              ESP=Filtro] }
  }
  KEYS
  {
    {    ;BA Db. Code,BA Db. Cube Line No.,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=%1 %2;ESP=%1 %2';
      Text002@1001 : TextConst 'ENU=You must specify either %1 or %2.;ESP=Debe especificar %1 o %2.';

    BEGIN
    END.
  }
}
