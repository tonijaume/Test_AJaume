OBJECT Table 7010363 Suplementos x Punto
{
  OBJECT-PROPERTIES
  {
    Date=16/10/09;
    Time=12:30:43;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "N§ Parte" <> '' THEN BEGIN
                 rParte.GET("N§ Parte");
                 IF rParte.Confirmado THEN
                   ERROR(err3);

                 IF MantenNoExp(rParte,FALSE) THEN // Actualizamos el campo Num Exportacion Proveedor
                   rParte.MODIFY;
               END;

               MantenSuplRapidos(FALSE);
             END;

    OnModify=BEGIN
               IF "N§ Parte" <> '' THEN BEGIN
                 rParte.GET("N§ Parte");
                 IF rParte.Confirmado THEN
                   ERROR(err1);

                 IF MantenNoExp(rParte,TRUE) THEN // Actualizamos el campo Num Exportacion Proveedor
                   rParte.MODIFY;
               END;

               MantenSuplRapidos(FALSE);
             END;

    OnDelete=BEGIN
               IF "N§ Parte" <> '' THEN BEGIN
                 rParte.GET("N§ Parte");
                 IF rParte.Confirmado THEN
                   ERROR(err2);

                 IF MantenNoExp(rParte,FALSE) THEN // Actualizamos el campo Num Exportacion Proveedor
                   rParte.MODIFY;
               END;

               MantenSuplRapidos(TRUE);
             END;

    OnRename=BEGIN
               MESSAGE(Text000);
             END;

    CaptionML=ESP=Suplementos x Punto;
    LookupFormID=Form7010408;
    DrillDownFormID=Form7010408;
  }
  FIELDS
  {
    { 1   ;   ;N§ Servicio         ;Code10        ;TableRelation=Servicio;
                                                   CaptionML=ESP=N§ Servicio;
                                                   SQL Data Type=Varchar;
                                                   Description=PK, FK Servicio;
                                                   Editable=No }
    { 3   ;   ;N§ Linea            ;Integer       ;CaptionML=ESP=N§ Linea;
                                                   Description=PK;
                                                   Editable=No }
    { 5   ;   ;Cod. Suplemento     ;Code10        ;TableRelation="Codigo Suplemento";
                                                   CaptionML=ESP=Cod. Suplemento;
                                                   SQL Data Type=Varchar;
                                                   NotBlank=Yes;
                                                   Description=PK, FK Codigo Suplemento }
    { 7   ;   ;Cantidad            ;Decimal       ;CaptionML=ESP=Cantidad;
                                                   DecimalPlaces=0:2 }
    { 9   ;   ;N§ Parte            ;Code10        ;TableRelation=Parte;
                                                   CaptionML=ESP=N§ Parte;
                                                   SQL Data Type=Varchar;
                                                   Description=FK Parte;
                                                   Editable=No }
    { 11  ;   ;Tipo calculo        ;Option        ;CaptionML=ESP=Tipo calculo;
                                                   OptionCaptionML=ESP=Ambos,Coste,Ingreso;
                                                   OptionString=Ambos,Coste,Ingreso }
    { 13  ;   ;Precio              ;Decimal       ;CaptionML=ESP=Precio;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr=Divisa }
    { 15  ;   ;Divisa              ;Code10        ;TableRelation=Currency.Code;
                                                   CaptionML=ESP=Divisa;
                                                   Description=FK Divisa;
                                                   Editable=No }
    { 17  ;   ;Aplica con precio manual;Boolean   ;CaptionML=ESP=Aplica con precio manual }
  }
  KEYS
  {
    {    ;N§ Servicio,N§ Linea,Cod. Suplemento    ;Clustered=Yes }
    {    ;N§ Parte,N§ Linea                        }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100244004 : TextConst 'ESP=1 - Elemento x Punto';
      err1@1100244000 : TextConst 'ESP=No se pueden modificar lineas asociadas a un parte confirmado.';
      err2@1100244001 : TextConst 'ESP=No se pueden borrar lineas asociadas a un parte confirmado.';
      rParte@1100244002 : Record 7010360;
      err3@1100244003 : TextConst 'ESP=No se pueden insertar lineas asociadas a un parte confirmado.';
      rPar@1100253000 : Record 7010311;
      rSerPar@1100253001 : Record 7010359;

    PROCEDURE nom_suplemento@1() : Text[30];
    VAR
      rCodSup@1100244000 : Record 7010318;
    BEGIN
      // nom_suplemento
      //
      IF NOT rCodSup.GET("Cod. Suplemento") THEN
        EXIT('')
      ELSE
        EXIT(rCodSup.Descripcion);
    END;

    PROCEDURE MantenNoExp@1100253007(VAR prParte@1100253000 : Record 7010360;pwModificacion@1100253009 : Boolean) : Boolean;
    VAR
      lwNoCamps@1100253001 : ARRAY [20] OF Integer;
      lwRParte@1100253002 : RecordRef;
      lwRXrecParte@1100253003 : RecordRef;
      lwNo@1100253004 : Integer;
      lwField@1100253005 : FieldRef;
      lwXrecField@1100253006 : FieldRef;
      lwOk@1100253007 : Boolean;
      lwOk2@1100253008 : Boolean;
    BEGIN
      // MantenNoExp
      // JPT 29/03/06 Mantemeos el campo "Num Exportacion Prov" para volver a exportarlo si ha habido cambios

      // Si pwModificacion= False lo marcamos de todos modos
      // Si es una modificacion borramos el numero de exportacion si dicha modificaci¢n afecta a alguno de los siguientes campos

      IF prParte."Num Exportacion Prov"=0 THEN // Si no se ha exportado a£n , salimos
        EXIT;

      lwOk:= TRUE;

      IF pwModificacion THEN BEGIN
        lwRParte.GETTABLE(Rec);
        lwRXrecParte.GETTABLE(xRec);
        lwOk2 := TRUE;
        FOR lwNo:=1 TO lwRParte.FIELDCOUNT DO BEGIN
          lwField     := lwRParte.FIELDINDEX(lwNo);
          lwXrecField := lwRXrecParte.FIELDINDEX(lwNo);
          // Campos que afectan al Parte
          IF lwField.NUMBER IN [5,7,11,13,17] THEN
            lwOk2 := lwOk2 AND (lwField.VALUE = lwXrecField.VALUE);
          IF NOT lwOk2 THEN  // Si ya hay alguna diferencia se realiza una salida prematura del bucle
            lwNo:= lwRParte.FIELDCOUNT;
        END;
        lwOk := NOT lwOk2;
      END;

      IF lwOk THEN
        prParte.MantenNoExp(FALSE);

      EXIT(lwOk);
    END;

    PROCEDURE MantenSuplRapidos@1100253000(pwBorra@1100253000 : Boolean);
    VAR
      lwCont@1100253001 : Integer;
      lwCant@1100253002 : Decimal;
    BEGIN
      // MantenSuplRapidos

      IF "Cod. Suplemento"='' THEN
        EXIT;

      CLEAR(rPar);
      rPar.FINDFIRST;
      IF NOT rSerPar.GET("N§ Servicio","N§ Linea") THEN
        EXIT;

      IF pwBorra THEN
        lwCant := 0
      ELSE
        lwCant := Cantidad;

      FOR lwCont := 1 TO 2 DO BEGIN
        CASE lwCont OF
          1: IF "Cod. Suplemento" = rPar."Cod. Suplemento Rapido 1" THEN
               rSerPar."Suplemento Rapido 1" := lwCant;
          2: IF "Cod. Suplemento" = rPar."Cod. Suplemento Rapido 2" THEN
               rSerPar."Suplemento Rapido 2" := lwCant;
        END;
      END;

      rSerPar.MODIFY;
    END;

    BEGIN
    END.
  }
}
