OBJECT Table 7009948 Cabecera validar factura coste
{
  OBJECT-PROPERTIES
  {
    Date=27/07/15;
    Time=16:46:01;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    OnModify=BEGIN
               IF Registrada THEN
                 ERROR(Text001);
             END;

    OnDelete=BEGIN
               IF Registrada THEN
                 ERROR(Text001);

               rDetValidar.RESET;
               rDetValidar.SETRANGE(Proveedor              , Proveedor);
               rDetValidar.SETRANGE("No. Factura proveedor", "No. Factura proveedor");
               rDetValidar.SETRANGE("Fecha registro"       , "Fecha registro");
               IF rDetValidar.FINDSET(TRUE) THEN
                 rDetValidar.DELETEALL(TRUE);
             END;

  }
  FIELDS
  {
    { 1   ;   ;Proveedor           ;Code20        ;TableRelation=Vendor.No.;
                                                   OnValidate=BEGIN
                                                                rVendor.GET(Proveedor);
                                                                IF rVendor."Digitos fijos NCF" <> '' THEN
                                                                BEGIN
                                                                  "Cod. Grupo NCF"     := rVendor."Cod. Grupo NCF";
                                                                  "Comprobante Fiscal" := rVendor."Digitos fijos NCF";
                                                                  "Digitos fijos NCF"  := rVendor."Digitos fijos NCF";
                                                                END
                                                                ELSE
                                                                BEGIN
                                                                  VALIDATE("Cod. Grupo NCF", rVendor."Cod. Grupo NCF");
                                                                  "Comprobante Fiscal" := '';
                                                                  "Digitos fijos NCF"  := '';
                                                                END;

                                                                //-$003VALIDATE("Cod. Divisa", rVendor."Currency Code");
                                                                "Impuestos incluidos" := rVendor."Prices Including VAT";
                                                              END;

                                                   CaptionML=[ENU=Supplier;
                                                              ESP=Proveedor];
                                                   NotBlank=Yes;
                                                   Description=Vendor.No. }
    { 3   ;   ;No. Factura proveedor;Code20       ;CaptionML=[ENU=Supplier invoice no.;
                                                              ESP=No. Factura proveedor];
                                                   NotBlank=Yes }
    { 5   ;   ;Fecha registro      ;Date          ;OnValidate=BEGIN
                                                                "Fecha emision proveedor" := "Fecha registro";
                                                              END;

                                                   CaptionML=[ENU=Posting Date;
                                                              ESP=Fecha registro];
                                                   NotBlank=Yes }
    { 7   ;   ;Fecha emision proveedor;Date       ;CaptionML=[ENU=Supplier date of issue;
                                                              ESP=Fecha emision proveedor] }
    { 9   ;   ;Departamento        ;Code20        ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   CaptionML=[ENU=Local Office;
                                                              ESP=Departamento];
                                                   Description="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   CaptionClass='1,2,1' }
    { 11  ;   ;Fecha desde         ;Date          ;FieldClass=Normal;
                                                   OnValidate=BEGIN
                                                                BuscarCosteValidacion;
                                                              END;

                                                   CaptionML=[ENU=Date From;
                                                              ESP=Fecha desde] }
    { 13  ;   ;Fecha hasta         ;Date          ;OnValidate=BEGIN
                                                                BuscarCosteValidacion;
                                                              END;

                                                   CaptionML=[ENU=Date To;
                                                              ESP=Fecha hasta] }
    { 15  ;   ;Codigo de barras    ;Code20        ;CaptionML=[ENU=Barcode;
                                                              ESP=Codigo de barras] }
    { 17  ;   ;Cod. Grupo NCF      ;Code10        ;TableRelation="Grupo configuracion NCF".Codigo WHERE (Tipo grupo=CONST(Compras));
                                                   OnValidate=VAR
                                                                lrGrupNCF@1100253000 : Record 7009831;
                                                              BEGIN
                                                                TESTFIELD(Proveedor);

                                                                IF ("Cod. Grupo NCF" <> xRec."Cod. Grupo NCF") AND ("Cod. Grupo NCF" <> '') THEN
                                                                BEGIN
                                                                  CLEAR(lrGrupNCF);
                                                                  lrGrupNCF.SETRANGE ("Tipo grupo", lrGrupNCF."Tipo grupo"::Compras);
                                                                  lrGrupNCF.SETRANGE (Codigo      , "Cod. Grupo NCF");
                                                                  IF lrGrupNCF.FINDFIRST THEN BEGIN
                                                                    IF "Digitos fijos NCF" = '' THEN BEGIN
                                                                      lrGrupNCF.TESTFIELD("Serie NCF Facturas");
                                                                      "Comprobante Fiscal" := cNoSeries.GetNextNo(lrGrupNCF."Serie NCF Facturas", "Fecha registro", FALSE);
                                                                    END;
                                                                  END;
                                                                END;

                                                                IF "Cod. Grupo NCF" = '' THEN
                                                                  "Comprobante Fiscal" := '';
                                                              END;

                                                   CaptionML=[ENU=NCF Group Code;
                                                              ESP=Cod. Grupo NCF];
                                                   Description="Grupo configuracion NCF".Codigo WHERE (Tipo grupo=CONST(Compras)) }
    { 19  ;   ;Comprobante Fiscal  ;Code20        ;OnValidate=VAR
                                                                lrGLSetup@1100253000 : Record 98;
                                                              BEGIN
                                                                lrGLSetup.FINDFIRST;
                                                                IF lrGLSetup."Aplica legislacion NCF" THEN
                                                                BEGIN
                                                                  IF "Digitos fijos NCF" = '' THEN
                                                                    ERROR(Text053);

                                                                  IF STRLEN("Comprobante Fiscal") <> lrGLSetup."Digitos NCF" THEN
                                                                    ERROR(Text055);

                                                                  IF COPYSTR("Comprobante Fiscal", 1, STRLEN("Digitos fijos NCF")) <> "Digitos fijos NCF" THEN
                                                                    ERROR(Text056);
                                                                END
                                                                ELSE
                                                                  ERROR(Text054);
                                                              END;

                                                   CaptionML=[ENU=Fiscal receipt;
                                                              ESP=Comprobante fiscal] }
    { 21  ;   ;Digitos fijos NCF   ;Code20        ;CaptionML=[ENU=NCF fixed digits;
                                                              ESP=Digitos fijos NCF] }
    { 23  ;   ;Cod. Divisa         ;Code10        ;FieldClass=Normal;
                                                   TableRelation=Currency.Code;
                                                   OnValidate=BEGIN
                                                                rGLSetup.FINDFIRST;
                                                                IF (rGLSetup."LCY Code" <> "Cod. Divisa") AND ("Cod. Divisa" <> '') THEN
                                                                  "Tasa de cambio" := TipoCambioDivisa
                                                                ELSE
                                                                  "Tasa de cambio" := 1;
                                                              END;

                                                   CaptionML=[ENU=Currency Code;
                                                              ESP=Cod. Divisa];
                                                   Editable=No }
    { 25  ;   ;Tasa de cambio      ;Decimal       ;OnValidate=BEGIN
                                                                "Importe proveedor (DL)"     := ROUND("Importe proveedor" * "Tasa de cambio");
                                                                "Importe agencia (DL)" := ROUND("Importe agencia" * "Tasa de cambio");
                                                              END;

                                                   CaptionML=[ENU=Exchange rate;
                                                              ESP=Tasa de cambio];
                                                   AutoFormatType=2 }
    { 27  ;   ;Importe proveedor   ;Decimal       ;OnValidate=BEGIN
                                                                "Importe proveedor (DL)" := ROUND("Importe proveedor" * "Tasa de cambio");
                                                              END;

                                                   CaptionML=[ENU=Vendor amount;
                                                              ESP=Importe proveedor] }
    { 29  ;   ;Importe proveedor (DL);Decimal     ;CaptionML=[ENU=Vendor amount (LCY);
                                                              ESP=Importe proveedor (DL)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 31  ;   ;Importe agencia     ;Decimal       ;OnValidate=BEGIN
                                                                "Importe agencia (DL)" := ROUND("Importe agencia" * "Tasa de cambio");
                                                              END;

                                                   CaptionML=[ENU=Agency amount;
                                                              ESP=Importe agencia];
                                                   Editable=No }
    { 33  ;   ;Importe agencia (DL);Decimal       ;CaptionML=[ENU=Agency amount (LCY);
                                                              ESP=Importe agencia (DL)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 37  ;   ;Tipo coste          ;Code10         }
    { 51  ;   ;Usuario creacion    ;Code20        ;TableRelation=User."User ID";
                                                   CaptionML=[ENU=Creation User;
                                                              ESP=Usuario creacion];
                                                   Description=FK Usuario;
                                                   Editable=No }
    { 53  ;   ;Fecha/hora creacion ;DateTime      ;CaptionML=[ENU=Creation datetime;
                                                              ESP=Fecha/Hora creacion];
                                                   Editable=No }
    { 55  ;   ;Registrada          ;Boolean       ;CaptionML=[ENU=Registered;
                                                              ESP=Registrada];
                                                   Editable=No }
    { 57  ;   ;Usuario registro    ;Code20        ;TableRelation=User."User ID";
                                                   CaptionML=[ENU=Register User;
                                                              ESP=Usuario registro];
                                                   Description=FK Usuario;
                                                   Editable=No }
    { 59  ;   ;Fecha/hora registro ;DateTime      ;CaptionML=[ENU=Register datetime;
                                                              ESP=Fecha/Hora registro];
                                                   Editable=No }
    { 70  ;   ;Reclamacion enviada ;Boolean       ;CaptionML=[ENU=Claim sent;
                                                              ESP=Reclamaci¢n enviada];
                                                   Editable=No }
    { 72  ;   ;Reclamada dia       ;Date          ;CaptionML=[ENU=Claim date;
                                                              ESP=Reclamada d¡a];
                                                   Editable=No }
    { 74  ;   ;Reclamada usuario   ;Code20        ;TableRelation=User."User ID";
                                                   CaptionML=[ENU=Claim date;
                                                              ESP=Reclamada usuario];
                                                   Editable=No }
    { 100 ;   ;Modulo              ;Option        ;OptionCaptionML=[ENU=Excursions,Transfer;
                                                                    ESP=Excursiones,Transfer];
                                                   OptionString=Excursiones,Transfer }
    { 110 ;   ;Transportista       ;Code10        ;TableRelation="Transportista transfer".Codigo;
                                                   OnValidate=VAR
                                                                rTransport@1000000000 : Record 7009801;
                                                              BEGIN
                                                                rTransport.GET(Transportista);

                                                                VALIDATE(Proveedor, rTransport.Proveedor)
                                                              END;

                                                   CaptionML=[ENU=Carrier;
                                                              ESP=Transportista];
                                                   Description=FK Transportista Transfer }
    { 112 ;   ;Impuestos incluidos ;Boolean       ;CaptionML=[ENU=Taxes included;
                                                              ESP=Impuestos incluidos] }
    { 7009713;;Importe base        ;Decimal       ;CaptionML=[ENU=Base amount;
                                                              ESP=Importe base] }
    { 7009715;;Importe exento      ;Decimal       ;CaptionML=[ENU=Exempted amount;
                                                              ESP=Importe exento] }
  }
  KEYS
  {
    {    ;Proveedor,No. Factura proveedor,Fecha registro;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1000000011 : TextConst 'ENU=You cannot modify or delete a registered invoice.;ESP="No se puede modificar o borrar una factura registrada "';
      Text053@1000000003 : TextConst 'ENU=This supplier does not have an own numbering of Tax Certificate;ESP=Este proveedor no tiene una numeracion propia de Comprobantes fiscales, el numero se asigna automaticamente.';
      Text054@1000000002 : TextConst 'ENU=In this company the legislation on Numberings of Tax Certificate is not applied;ESP=En esta empresa no se aplica la legislacion de Numeradores de Comprobante fiscal';
      Text055@1000000001 : TextConst 'ENU=The length of the certificate number does not coincide with the established in accounting configuration;ESP=La longitud del numero de comprobante no coincide con la establecida en configuracion de contabilidad.';
      Text056@1000000000 : TextConst 'ENU=Indicated tax certificate number is not correct for this supplier;ESP=El numero de comprobante fiscal indicado es incorrecto para el proveedor.';
      cNoSeries@1000000008 : Codeunit 396;
      rVendor@1000000007 : Record 23;
      rDetValidar@1000000010 : Record 7009949;
      rGLSetup@1000000006 : Record 98;
      rCamb@1000000005 : Record 330;
      cValExc@1000000004 : Codeunit 7010146;
      cValTrf@1000000009 : Codeunit 7009738;

    PROCEDURE TipoCambioDivisa@8() : Decimal;
    VAR
      lwFecha@1103355002 : Date;
    BEGIN
      // TipoCambioDivisa

      rCamb.RESET;
      rCamb.SETRANGE ("Currency Code"  , "Cod. Divisa");
      rCamb.SETFILTER("Starting Date", '<=%1', WORKDATE);
      rCamb.FINDLAST;

      EXIT(rCamb."Relational Exch. Rate Amount")
    END;

    PROCEDURE BuscarCosteValidacion@1000000000();
    BEGIN
      // BuscarCosteValidacion

      IF ("Fecha desde" = 0D) OR ("Fecha hasta" = 0D) THEN
        EXIT;

      CASE Modulo OF
        Modulo::Excursiones: cValExc.BuscaCosteValidacion(Rec);
        Modulo::Transfer: cValTrf.BuscaCosteValidacion(Rec);
      END;
    END;

    BEGIN
    {
      $001 ARM 16042015 CNT-OC-14068 Cambio para validar costes a trav‚s de cabecera y detalle

      $002 AJS 30062015 Cuando se rellena la fecha de registro, se pone tambien en la fecha de emision

      $003 AJS 06072015 La divisa de validaci¢n la tiene que tomar de las lineas, no de la ficha del proveedor

      $004 AJS 09072015 Corregir error en OnDelete

      $005 AJS 27072015 Ampliar el campo Digitos fijos NCF a Code20
    }
    END.
  }
}
