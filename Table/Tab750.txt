OBJECT Table 750 Standard General Journal
{
  OBJECT-PROPERTIES
  {
    Date=20/02/13;
    Time=15:43:08;
    Modified=Yes;
    Version List=AIC2009,NAVW16.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               StdGenJnlLine@1000 : Record 751;
             BEGIN
               StdGenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
               StdGenJnlLine.SETRANGE("Standard Journal Code",Code);

               StdGenJnlLine.DELETEALL(TRUE);
             END;

    OnRename=VAR
               JnlLineDim@1000 : Record 356;
               JnlLineDim2@1001 : Record 356;
             BEGIN
               JnlLineDim.SETRANGE("Table ID",DATABASE::"Standard General Journal Line");
               JnlLineDim.SETRANGE("Journal Template Name",xRec."Journal Template Name");
               JnlLineDim.SETRANGE("Journal Batch Name",xRec.Code);
               IF JnlLineDim.FIND('-') THEN
                 REPEAT
                   JnlLineDim2 := JnlLineDim;
                   JnlLineDim2."Journal Batch Name" := Code;
                   IF JnlLineDim2.INSERT THEN;
                 UNTIL JnlLineDim.NEXT = 0;
               JnlLineDim.DELETEALL;
             END;

    CaptionML=[ENU=Standard General Journal;
               ESP=Diario general est ndar];
    LookupFormID=Form750;
  }
  FIELDS
  {
    { 1   ;   ;Journal Template Name;Code10       ;TableRelation="Gen. Journal Template";
                                                   CaptionML=[ENU=Journal Template Name;
                                                              ESP=Nombre libro diario];
                                                   NotBlank=Yes }
    { 2   ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              ESP=C¢digo];
                                                   NotBlank=Yes }
    { 3   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n] }
  }
  KEYS
  {
    {    ;Journal Template Name,Code              ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      GenJnlBatch@1008 : Record 232;
      LastGenJnlLine@1007 : Record 81;
      GenJnlLine@1006 : Record 81;
      JnlLineDimFrom@1000 : Record 356;
      Window@1003 : Dialog;
      WindowUpdateTime@1002 : Time;
      NoOfJournalsToBeCreated@1001 : Integer;
      Text000@1004 : TextConst 'ENU=Getting Standard General Journal Lines @1@@@@@@@;ESP=Obteniendo l¡neas diario general est ndar @1@@@@@@@';
      NoOfJournalsCreated@1005 : Integer;

    PROCEDURE CreateGenJnlFromStdJnl@1(StdGenJnl@1005 : Record 750;JnlBatchName@1003 : Code[10]);
    VAR
      StdGenJnlLine@1000 : Record 751;
    BEGIN
      Initialize(StdGenJnl,JnlBatchName);

      StdGenJnlLine.SETRANGE("Journal Template Name",StdGenJnl."Journal Template Name");
      StdGenJnlLine.SETRANGE("Standard Journal Code",StdGenJnl.Code);
      OpenWindow(Text000,StdGenJnlLine.COUNT);
      IF StdGenJnlLine.FIND('-') THEN
        REPEAT
          UpdateWindow;
          CopyGenJnlFromStdJnl(StdGenJnlLine);
        UNTIL StdGenJnlLine.NEXT = 0;
    END;

    PROCEDURE Initialize@15(VAR StdGenJnl@1001 : Record 750;JnlBatchName@1000 : Code[10]);
    BEGIN
      GenJnlLine."Journal Template Name" := StdGenJnl."Journal Template Name";
      GenJnlLine."Journal Batch Name" := JnlBatchName;
      GenJnlLine.SETRANGE("Journal Template Name",StdGenJnl."Journal Template Name");
      GenJnlLine.SETRANGE("Journal Batch Name",JnlBatchName);

      LastGenJnlLine.SETRANGE("Journal Template Name",StdGenJnl."Journal Template Name");
      LastGenJnlLine.SETRANGE("Journal Batch Name",JnlBatchName);

      IF LastGenJnlLine.FINDLAST THEN;

      GenJnlBatch.SETRANGE("Journal Template Name",StdGenJnl."Journal Template Name");
      GenJnlBatch.SETRANGE(Name,JnlBatchName);

      IF GenJnlBatch.FIND('-') THEN;
    END;

    PROCEDURE CopyGenJnlFromStdJnl@3(StdGenJnlLine@1001 : Record 751);
    VAR
      GenJnlManagement@1000 : Codeunit 230;
      Balance@1007 : Decimal;
      TotalBalance@1006 : Decimal;
      ShowBalance@1005 : Boolean;
      ShowTotalBalance@1004 : Boolean;
    BEGIN
      GenJnlLine.INIT;
      GenJnlLine."Line No." := 0;
      GenJnlManagement.CalcBalance(GenJnlLine,LastGenJnlLine,Balance,TotalBalance,ShowBalance,ShowTotalBalance);
      GenJnlLine.SetUpNewLine(LastGenJnlLine,Balance,TRUE);
      IF LastGenJnlLine."Line No." <> 0 THEN
        GenJnlLine."Line No." := LastGenJnlLine."Line No." + 10000
      ELSE
        GenJnlLine."Line No." := 10000;

      GenJnlLine.TRANSFERFIELDS(StdGenJnlLine,FALSE);
      GenJnlLine."Currency Factor" := 0; //+$001
      GenJnlLine.UpdateLineBalance;
      GenJnlLine.VALIDATE("Currency Code");

      IF GenJnlLine."VAT Prod. Posting Group" <> '' THEN
        GenJnlLine.VALIDATE("VAT Prod. Posting Group");
      IF (GenJnlLine."VAT %" <> 0) AND GenJnlBatch."Allow VAT Difference" THEN
        GenJnlLine.VALIDATE("VAT Amount",StdGenJnlLine."VAT Amount");
      GenJnlLine.VALIDATE(GenJnlLine."Bal. VAT Prod. Posting Group");

      IF GenJnlBatch."Allow VAT Difference" THEN
        GenJnlLine.VALIDATE("Bal. VAT Amount",StdGenJnlLine."Bal. VAT Amount");
      GenJnlLine.INSERT(TRUE);

      CopyStdGenJnlLineDims(StdGenJnlLine,GenJnlLine);

      LastGenJnlLine := GenJnlLine;
    END;

    LOCAL PROCEDURE CopyStdGenJnlLineDims@2(StdGenJnlLine1@1001 : Record 751;GenJnlLine1@1000 : Record 81);
    VAR
      DimMgt@1003 : Codeunit 408;
    BEGIN
      JnlLineDimFrom.SETRANGE("Table ID",DATABASE::"Standard General Journal Line");
      JnlLineDimFrom.SETRANGE("Journal Template Name",StdGenJnlLine1."Journal Template Name");
      JnlLineDimFrom.SETRANGE("Journal Batch Name",StdGenJnlLine1."Standard Journal Code");
      JnlLineDimFrom.SETRANGE("Journal Line No.",StdGenJnlLine1."Line No.");

      DimMgt.DeleteJnlLineDim(DATABASE::"Gen. Journal Line",GenJnlLine1."Journal Template Name",
        GenJnlLine1."Journal Batch Name",GenJnlLine1."Line No.",0);

      DimMgt.MoveJnlLineDimToJnlLineDim(JnlLineDimFrom,DATABASE::"Gen. Journal Line",
        GenJnlLine1."Journal Template Name",GenJnlLine1."Journal Batch Name",GenJnlLine1."Line No.");
    END;

    PROCEDURE OpenWindow@12(DisplayText@1001 : Text[250];NoOfJournalsToBeCreated2@1000 : Integer);
    BEGIN
      NoOfJournalsCreated := 0;
      NoOfJournalsToBeCreated := NoOfJournalsToBeCreated2;
      WindowUpdateTime := TIME;
      Window.OPEN(DisplayText);
    END;

    PROCEDURE UpdateWindow@4();
    BEGIN
      NoOfJournalsCreated := NoOfJournalsCreated + 1;
      IF TIME - WindowUpdateTime >= 300 THEN BEGIN
        WindowUpdateTime := TIME;
        Window.UPDATE(1,ROUND(NoOfJournalsCreated / NoOfJournalsToBeCreated * 10000,1));
      END;
    END;

    BEGIN
    {
      $001 AJS 20022013 Modifico la funcion CopyGenJnlFromStdJnl para dejar el Factor de la divisa a cero y permitir
                        que se recalcule para la nueva fecha
    }
    END.
  }
}
