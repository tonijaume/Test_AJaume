OBJECT Form 664 Sales Prepayment Percentages
{
  OBJECT-PROPERTIES
  {
    Date=13/02/07;
    Time=12:00:00;
    Version List=NAVW15.00;
  }
  PROPERTIES
  {
    Width=13420;
    Height=6710;
    CaptionML=[ENU=Sales Prepayment Percentages;
               ESP=Porcentajes prepago ventas];
    TableBoxID=1;
    SourceTable=Table459;
    DataCaptionExpr=Caption;
    DelayedInsert=Yes;
    OnOpenForm=BEGIN
                 GetRecFilters;
                 SetRecFilters;
               END;

    OnAfterGetCurrRecord=BEGIN
                           SetEditable;
                         END;

  }
  CONTROLS
  {
    { 1   ;TableBox     ;220  ;2420 ;12980;3300 ;HorzGlue=Both;
                                                 VertGlue=Both }
    { 2   ;TextBox      ;0    ;0    ;550  ;0    ;ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="Sales Type";
                                                 OnAfterValidate=BEGIN
                                                                   SetEditable;
                                                                 END;
                                                                  }
    { 3   ;Label        ;0    ;0    ;0    ;0    ;ParentControl=2;
                                                 InColumnHeading=Yes }
    { 4   ;TextBox      ;0    ;0    ;1650 ;0    ;ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="Sales Code" }
    { 5   ;Label        ;0    ;0    ;0    ;0    ;ParentControl=4;
                                                 InColumnHeading=Yes }
    { 6   ;TextBox      ;0    ;0    ;1650 ;0    ;HorzGlue=Both;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="Item No." }
    { 7   ;Label        ;0    ;0    ;0    ;0    ;ParentControl=6;
                                                 InColumnHeading=Yes }
    { 8   ;TextBox      ;0    ;0    ;1650 ;0    ;ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="Starting Date" }
    { 9   ;Label        ;0    ;0    ;0    ;0    ;ParentControl=8;
                                                 InColumnHeading=Yes }
    { 10  ;TextBox      ;0    ;0    ;1650 ;0    ;ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="Ending Date" }
    { 11  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=10;
                                                 InColumnHeading=Yes }
    { 12  ;TextBox      ;0    ;0    ;1100 ;0    ;ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="Prepayment %" }
    { 13  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=12;
                                                 InColumnHeading=Yes }
    { 14  ;CommandButton;6160 ;5940 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 Default=Yes;
                                                 PushAction=LookupOK;
                                                 InvalidActionAppearance=Hide }
    { 15  ;CommandButton;8580 ;5940 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 Cancel=Yes;
                                                 PushAction=LookupCancel;
                                                 InvalidActionAppearance=Hide }
    { 16  ;CommandButton;11000;5940 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 PushAction=FormHelp }
    { 17  ;TabControl   ;220  ;220  ;12980;2090 ;HorzGlue=Both }
    { 18  ;TextBox      ;3850 ;990  ;2750 ;440  ;ParentControl=17;
                                                 InPage=0;
                                                 CaptionML=[ENU=Sales Type Filter;
                                                            ESP=Filtro tipo ventas];
                                                 OptionCaptionML=[ENU=Customer,Customer Price Group,All Customers,None;
                                                                  ESP=Cliente,Grupo precio cliente,Todos clientes,Ninguno];
                                                 SourceExpr=SalesTypeFilter;
                                                 OnAfterValidate=BEGIN
                                                                   CurrForm.SAVERECORD;
                                                                   SalesCodeFilter := '';
                                                                   SetRecFilters;
                                                                 END;
                                                                  }
    { 23  ;Label        ;440  ;990  ;3300 ;440  ;ParentControl=18 }
    { 19  ;TextBox      ;3850 ;1540 ;2750 ;440  ;Name=SalesCodeFilterCtrl;
                                                 ParentControl=17;
                                                 InPage=0;
                                                 CaptionML=[ENU=Sales Code Filter;
                                                            ESP=Filtro c¢d. ventas];
                                                 ClearOnLookup=No;
                                                 SourceExpr=SalesCodeFilter;
                                                 OnLookup=VAR
                                                            CustList@1005 : Form 22;
                                                            CustPriceGrList@1004 : Form 7;
                                                          BEGIN
                                                            IF SalesTypeFilter = SalesTypeFilter::"All Customers" THEN
                                                              EXIT;

                                                            CASE SalesTypeFilter OF
                                                              SalesTypeFilter::Customer:
                                                                BEGIN
                                                                  CustList.LOOKUPMODE := TRUE;
                                                                  IF CustList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                    Text := CustList.GetSelectionFilter;
                                                                    EXIT(TRUE);
                                                                  END;
                                                                END;
                                                              SalesTypeFilter::"Customer Price Group":
                                                                BEGIN
                                                                  CustPriceGrList.LOOKUPMODE := TRUE;
                                                                  IF CustPriceGrList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                    Text := CustPriceGrList.GetSelectionFilter;
                                                                    EXIT(TRUE);
                                                                  END;
                                                                END;
                                                            END;
                                                          END;

                                                 OnAfterValidate=BEGIN
                                                                   CurrForm.SAVERECORD;
                                                                   SetRecFilters;
                                                                 END;
                                                                  }
    { 22  ;Label        ;440  ;1540 ;3300 ;440  ;ParentControl=19 }
    { 20  ;TextBox      ;10120;1540 ;2750 ;440  ;ParentControl=17;
                                                 InPage=0;
                                                 CaptionML=[ENU=Starting Date Filter;
                                                            ESP=Filtro fecha inicio];
                                                 ClearOnLookup=No;
                                                 SourceExpr=StartingDateFilter;
                                                 OnValidate=VAR
                                                              ApplicationMgt@1000 : Codeunit 1;
                                                            BEGIN
                                                              IF ApplicationMgt.MakeDateFilter(StartingDateFilter) = 0 THEN;
                                                            END;

                                                 OnAfterValidate=BEGIN
                                                                   CurrForm.SAVERECORD;
                                                                   SetRecFilters;
                                                                 END;
                                                                  }
    { 21  ;Label        ;6710 ;1540 ;3300 ;440  ;ParentControl=20 }
    { 24  ;TextBox      ;10120;990  ;2750 ;440  ;Name=CodeFilterCtrl;
                                                 ParentControl=17;
                                                 InPage=0;
                                                 CaptionML=[ENU=Item No. Filter;
                                                            ESP=Filtro n§ prod.];
                                                 ClearOnLookup=No;
                                                 SourceExpr=ItemNoFilter;
                                                 OnLookup=VAR
                                                            ItemList@1000 : Form 31;
                                                          BEGIN
                                                            ItemList.LOOKUPMODE := TRUE;
                                                            IF ItemList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                              Text := ItemList.GetSelectionFilter;
                                                              EXIT(TRUE);
                                                            END;
                                                          END;

                                                 OnAfterValidate=BEGIN
                                                                   CurrForm.SAVERECORD;
                                                                   SetRecFilters;
                                                                 END;
                                                                  }
    { 25  ;Label        ;6710 ;990  ;3300 ;440  ;ParentControl=24 }
  }
  CODE
  {
    VAR
      SalesTypeFilter@1003 : 'Customer,Customer Price Group,All Customers,None';
      SalesCodeFilter@1002 : Text[250];
      ItemNoFilter@1001 : Text[250];
      StartingDateFilter@1000 : Text[30];

    LOCAL PROCEDURE SetEditable@3();
    BEGIN
      CurrForm."Sales Code".EDITABLE := "Sales Type" <> "Sales Type"::"All Customers";
    END;

    PROCEDURE GetRecFilters@4();
    BEGIN
      IF HASFILTER THEN BEGIN
        IF GETFILTER("Sales Type") <> '' THEN
          SalesTypeFilter := "Sales Type"
        ELSE
          SalesTypeFilter := SalesTypeFilter::None;

        SalesCodeFilter := GETFILTER("Sales Code");
        ItemNoFilter := GETFILTER("Item No.");
        EVALUATE(StartingDateFilter,GETFILTER("Starting Date"));
      END;
    END;

    PROCEDURE SetRecFilters@5();
    BEGIN
      CurrForm.SalesCodeFilterCtrl.ENABLED(TRUE);
      CurrForm.CodeFilterCtrl.ENABLED(TRUE);

      IF SalesTypeFilter <> SalesTypeFilter::None THEN
        SETRANGE("Sales Type",SalesTypeFilter)
      ELSE
        SETRANGE("Sales Type");

      IF SalesTypeFilter IN [SalesTypeFilter::"All Customers",SalesTypeFilter::None] THEN BEGIN
        CurrForm.SalesCodeFilterCtrl.ENABLED(FALSE);
        SalesCodeFilter := '';
      END;

      IF SalesCodeFilter <> '' THEN
        SETFILTER("Sales Code",SalesCodeFilter)
      ELSE
        SETRANGE("Sales Code");

      IF ItemNoFilter <> '' THEN
        SETFILTER("Item No.",ItemNoFilter)
      ELSE
        SETRANGE("Item No.");

      IF StartingDateFilter <> '' THEN
        SETFILTER("Starting Date",StartingDateFilter)
      ELSE
        SETRANGE("Starting Date");

      CurrForm.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE Caption@6() : Text[1024];
    VAR
      Item@1005 : Record 27;
      Cust@1004 : Record 18;
      CustPriceGr@1003 : Record 6;
      ItemNoCaption@1001 : Text[1024];
      SalesCodeCaption@1000 : Text[1024];
    BEGIN
      IF ItemNoFilter <> '' THEN BEGIN
        ItemNoCaption := STRSUBSTNO('%1 %2',Item.TABLECAPTION,ItemNoFilter);
        IF Item.GET(COPYSTR(ItemNoFilter,1,MAXSTRLEN(Item."No."))) THEN
          ItemNoCaption := ItemNoCaption + ' - ' + Item.Description;
      END;

      CASE SalesTypeFilter OF
        SalesTypeFilter::Customer:
          BEGIN
            SalesCodeCaption := STRSUBSTNO('%1 %2',Cust.TABLECAPTION,SalesCodeFilter);
            IF Cust.GET(COPYSTR(SalesCodeFilter,1,MAXSTRLEN(Cust."No."))) THEN
              SalesCodeCaption := SalesCodeCaption + ' - ' + Cust.Name;
          END;
        SalesTypeFilter::"Customer Price Group":
          BEGIN
            SalesCodeCaption := STRSUBSTNO('%1 %2',CustPriceGr.TABLECAPTION,SalesCodeFilter);
            IF CustPriceGr.GET(COPYSTR(SalesCodeFilter,1,MAXSTRLEN(CustPriceGr.Code))) THEN
              SalesCodeCaption := SalesCodeCaption + ' - ' + CustPriceGr.Description;
          END;
        SalesTypeFilter::"All Customers":
          SalesCodeCaption := FORMAT(SalesTypeFilter);
      END;

      EXIT(DELCHR(ItemNoCaption + ' ' + SalesCodeCaption,'<>'))
    END;

    BEGIN
    END.
  }
}
