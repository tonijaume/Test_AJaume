OBJECT Form 416 G/L Account Balance Lines
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    Width=16060;
    Height=5060;
    CaptionML=[ENU=G/L Account Balance Lines;
               ESP=L¡neas saldo cuentas];
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    TableBoxID=1;
    LinksAllowed=No;
    SourceTable=Table2000000007;
    OnOpenForm=BEGIN
                 RESET;
               END;

    OnFindRecord=BEGIN
                   EXIT(PeriodFormMgt.FindDate(Which,Rec,GLPeriodLength));
                 END;

    OnNextRecord=BEGIN
                   EXIT(PeriodFormMgt.NextDate(Steps,Rec,GLPeriodLength));
                 END;

    OnAfterGetRecord=BEGIN
                       SetDateFilter;
                       IF DebitCreditTotals THEN
                         GLAcc.CALCFIELDS("Net Change", "Debit Amount", "Credit Amount")
                       ELSE BEGIN
                         GLAcc.CALCFIELDS("Net Change");
                         IF GLAcc."Net Change" > 0 THEN BEGIN
                           GLAcc."Debit Amount" := GLAcc."Net Change";
                           GLAcc."Credit Amount" := 0
                         END ELSE BEGIN
                           GLAcc."Debit Amount" := 0;
                           GLAcc."Credit Amount" := -GLAcc."Net Change"
                         END
                       END
                     END;

  }
  CONTROLS
  {
    { 1   ;TableBox     ;0    ;0    ;16060;5060 ;HorzGlue=Both;
                                                 VertGlue=Both }
    { 14  ;TextBox      ;2064 ;2420 ;1650 ;440  ;Editable=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 CaptionML=[ENU=Period Start;
                                                            ESP=Inicio periodo];
                                                 SourceExpr="Period Start" }
    { 15  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=14;
                                                 InColumnHeading=Yes }
    { 16  ;TextBox      ;4022 ;2310 ;2200 ;440  ;HorzGlue=Both;
                                                 Editable=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 CaptionML=[ENU=Period Name;
                                                            ESP=Nombre periodo];
                                                 SourceExpr="Period Name" }
    { 17  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=16;
                                                 InColumnHeading=Yes }
    { 4   ;TextBox      ;0    ;0    ;2200 ;0    ;Editable=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 DrillDown=Yes;
                                                 CaptionML=[ENU=Debit Amount;
                                                            ESP=Importe debe];
                                                 BlankNumbers=BlankZero;
                                                 SourceExpr=GLAcc."Debit Amount";
                                                 AutoFormatType=1;
                                                 OnDrillDown=BEGIN
                                                               BalanceDrillDown;
                                                             END;
                                                              }
    { 5   ;Label        ;0    ;0    ;0    ;0    ;ParentControl=4;
                                                 InColumnHeading=Yes;
                                                 HorzAlign=Right }
    { 2   ;TextBox      ;5170 ;5107 ;2200 ;440  ;Editable=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 DrillDown=Yes;
                                                 CaptionML=[ENU=Credit Amount;
                                                            ESP=Importe haber];
                                                 BlankNumbers=BlankZero;
                                                 SourceExpr=GLAcc."Credit Amount";
                                                 AutoFormatType=1;
                                                 OnDrillDown=BEGIN
                                                               BalanceDrillDown;
                                                             END;
                                                              }
    { 3   ;Label        ;6032 ;450  ;3300 ;440  ;ParentControl=2;
                                                 InColumnHeading=Yes;
                                                 HorzAlign=Right }
    { 10  ;TextBox      ;8360 ;4201 ;2200 ;440  ;Visible=No;
                                                 Editable=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 DrillDown=Yes;
                                                 CaptionML=[ENU=Net Change;
                                                            ESP=Saldo periodo];
                                                 BlankZero=Yes;
                                                 SourceExpr=GLAcc."Net Change";
                                                 AutoFormatType=1;
                                                 OnDrillDown=BEGIN
                                                               BalanceDrillDown;
                                                             END;
                                                              }
    { 11  ;Label        ;8228 ;450  ;3300 ;440  ;ParentControl=10;
                                                 InColumnHeading=Yes;
                                                 HorzAlign=Right }
  }
  CODE
  {
    VAR
      AccountingPeriod@1000 : Record 50;
      GLAcc@1001 : Record 15;
      PeriodFormMgt@1002 : Codeunit 359;
      GLPeriodLength@1003 : 'Day,Week,Month,Quarter,Year,Accounting Period';
      AmountType@1004 : 'Net Change,Balance at Date';
      ClosingEntryFilter@1005 : 'Include,Exclude';
      DebitCreditTotals@1006 : Boolean;

    PROCEDURE Set@1(VAR NewGLAcc@1000 : Record 15;NewGLPeriodLength@1001 : Integer;NewAmountType@1002 : 'Net Change,Balance';NewClosingEntryFilter@1003 : 'Include,Exclude';NewDebitCreditTotals@1004 : Boolean);
    BEGIN
      GLAcc.COPY(NewGLAcc);
      GLPeriodLength := NewGLPeriodLength;
      AmountType := NewAmountType;
      ClosingEntryFilter := NewClosingEntryFilter;
      DebitCreditTotals := NewDebitCreditTotals;
      CurrForm.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE BalanceDrillDown@3();
    VAR
      GLEntry@1000 : Record 17;
    BEGIN
      SetDateFilter;
      GLEntry.RESET;
      GLEntry.SETCURRENTKEY("G/L Account No.","Posting Date");
      GLEntry.SETRANGE("G/L Account No.",GLAcc."No.");
      IF GLAcc.Totaling <> '' THEN
        GLEntry.SETFILTER("G/L Account No.",GLAcc.Totaling);
      GLEntry.SETFILTER("Posting Date",GLAcc.GETFILTER("Date Filter"));
      GLEntry.SETFILTER("Global Dimension 1 Code",GLAcc.GETFILTER("Global Dimension 1 Filter"));
      GLEntry.SETFILTER("Global Dimension 2 Code",GLAcc.GETFILTER("Global Dimension 2 Filter"));
      GLEntry.SETFILTER("Business Unit Code",GLAcc.GETFILTER("Business Unit Filter"));
      FORM.RUN(0,GLEntry);
    END;

    LOCAL PROCEDURE SetDateFilter@2();
    BEGIN
      IF AmountType = AmountType::"Net Change" THEN
        GLAcc.SETRANGE("Date Filter","Period Start","Period End")
      ELSE
        GLAcc.SETRANGE("Date Filter",0D,"Period End");
      IF ClosingEntryFilter = ClosingEntryFilter::Exclude THEN BEGIN
        AccountingPeriod.SETCURRENTKEY("New Fiscal Year");
        AccountingPeriod.SETRANGE("New Fiscal Year",TRUE);
        IF GLAcc.GETRANGEMIN("Date Filter") = 0D THEN
          AccountingPeriod.SETRANGE("Starting Date",0D,GLAcc.GETRANGEMAX("Date Filter"))
        ELSE
          AccountingPeriod.SETRANGE(
            "Starting Date",
            GLAcc.GETRANGEMIN("Date Filter") + 1,
            GLAcc.GETRANGEMAX("Date Filter"));
        IF AccountingPeriod.FIND('-') THEN
          REPEAT
            GLAcc.SETFILTER(
              "Date Filter",GLAcc.GETFILTER("Date Filter") + '&<>%1',
              CLOSINGDATE(AccountingPeriod."Starting Date" - 1));
          UNTIL AccountingPeriod.NEXT = 0;
      END ELSE
        GLAcc.SETRANGE(
          "Date Filter",
          GLAcc.GETRANGEMIN("Date Filter"),
          CLOSINGDATE(GLAcc.GETRANGEMAX("Date Filter")));
    END;

    BEGIN
    END.
  }
}
