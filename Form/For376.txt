OBJECT Form 376 Job Journal Reconcile
{
  OBJECT-PROPERTIES
  {
    Date=13/02/07;
    Time=12:00:00;
    Version List=NAVW15.00;
  }
  PROPERTIES
  {
    Width=7810;
    Height=6710;
    Editable=No;
    CaptionML=[ENU=Job Journal Reconcile;
               ESP=Control diario proyecto];
    TableBoxID=1;
    SourceTable=Table278;
    DataCaptionExpr=Caption;
    SourceTableTemporary=Yes;
    OnOpenForm=BEGIN
                 JobJnlLine.SETCURRENTKEY("Journal Template Name","Journal Batch Name",Type,"No.","Unit of Measure Code","Work Type Code");
                 IF JobJnlLine.FIND('-') THEN BEGIN
                   OldUnitOfMeasureCode := JobJnlLine."Unit of Measure Code";
                   OldWorkTypeCode := JobJnlLine."Work Type Code";
                   REPEAT
                     IF OldUnitOfMeasureCode <> JobJnlLine."Unit of Measure Code" THEN BEGIN
                       InsertUnitOfMeasureQty;
                       InsertWorkTypeQty;
                     END ELSE
                       IF OldWorkTypeCode <> JobJnlLine."Work Type Code" THEN
                         InsertWorkTypeQty;

                     OldUnitOfMeasureCode := JobJnlLine."Unit of Measure Code";
                     OldWorkTypeCode := JobJnlLine."Work Type Code";

                     UnitOfMeasureQty := UnitOfMeasureQty + JobJnlLine.Quantity;
                     WorkTypeQty := WorkTypeQty + JobJnlLine.Quantity;
                     TotalQty := TotalQty + JobJnlLine.Quantity;
                   UNTIL (JobJnlLine.NEXT = 0);

                   InsertUnitOfMeasureQty;
                   InsertWorkTypeQty;
                   INIT;
                   "Is Total" := TRUE;
                   "Unit of Measure Code" := '';
                   "Line Type" := "Line Type"::Total;
                   "Work Type Code" := '';
                   Quantity := TotalQty;
                   INSERT;

                   TotalQty := 0;
                 END;
               END;

  }
  CONTROLS
  {
    { 1   ;TableBox     ;220  ;220  ;7370 ;5500 ;HorzGlue=Both;
                                                 VertGlue=Both }
    { 4   ;TextBox      ;0    ;0    ;1650 ;0    ;HorzGlue=Both;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="Unit of Measure Code";
                                                 OnFormat=BEGIN
                                                            IF "Line Type" = "Line Type"::Total THEN BEGIN
                                                              CurrForm."Unit of Measure Code".UPDATEFONTBOLD := TRUE;
                                                              Text := STRSUBSTNO(Text000,Text);
                                                            END;
                                                          END;
                                                           }
    { 5   ;Label        ;0    ;0    ;0    ;0    ;ParentControl=4;
                                                 InColumnHeading=Yes }
    { 8   ;TextBox      ;0    ;0    ;1650 ;0    ;ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="Work Type Code" }
    { 9   ;Label        ;0    ;0    ;0    ;0    ;ParentControl=8;
                                                 InColumnHeading=Yes }
    { 10  ;TextBox      ;0    ;0    ;1650 ;0    ;ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr=Quantity;
                                                 OnFormat=BEGIN
                                                            IF "Line Type" = "Line Type"::Total THEN BEGIN
                                                              CurrForm.Quantity.UPDATEFONTBOLD := TRUE;
                                                            END;
                                                          END;
                                                           }
    { 11  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=10;
                                                 InColumnHeading=Yes }
    { 14  ;CommandButton;5390 ;5940 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 PushAction=FormHelp }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Total %1;ESP=Total %1';
      JobJnlLine@1001 : Record 210;
      UnitOfMeasureQty@1003 : Decimal;
      WorkTypeQty@1004 : Decimal;
      TotalQty@1005 : Decimal;
      OldUnitOfMeasureCode@1006 : Text[10];
      OldWorkTypeCode@1007 : Code[10];

    PROCEDURE SetJobJnlLine@1(VAR JobJnlLine2@1000 : Record 210);
    BEGIN
      JobJnlLine2.TESTFIELD(Type,JobJnlLine2.Type::Resource);
      JobJnlLine.COPYFILTERS(JobJnlLine2);
      JobJnlLine.SETRANGE(Type,JobJnlLine2.Type::Resource);
      JobJnlLine.SETRANGE("No.",JobJnlLine2."No.");
    END;

    LOCAL PROCEDURE InsertUnitOfMeasureQty@2();
    BEGIN
      INIT;
      "Is Total" := FALSE;
      "Unit of Measure Code" := OldUnitOfMeasureCode;
      "Line Type" := "Line Type"::Total;
      "Work Type Code" := '';
      Quantity := UnitOfMeasureQty;
      INSERT;
      UnitOfMeasureQty := 0;
    END;

    LOCAL PROCEDURE InsertWorkTypeQty@3();
    BEGIN
      INIT;
      "Is Total" := FALSE;
      "Unit of Measure Code" := OldUnitOfMeasureCode;
      "Line Type" := 0;
      "Work Type Code" := OldWorkTypeCode;
      Quantity := WorkTypeQty;
      INSERT;
      WorkTypeQty := 0;
    END;

    LOCAL PROCEDURE Caption@4() : Text[80];
    VAR
      Res@1000 : Record 156;
    BEGIN
      Res.GET(JobJnlLine.GETRANGEMIN("No."));
      EXIT(Res."No." + ' ' + Res.Name);
    END;

    BEGIN
    END.
  }
}
