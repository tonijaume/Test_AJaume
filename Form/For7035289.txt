OBJECT Form 7035289 Asignacion rapida partes - TRN
{
  OBJECT-PROPERTIES
  {
    Date=27/11/14;
    Time=16:45:57;
    Modified=Yes;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    Width=25740;
    Height=11770;
    CaptionML=ESP=Asignacion rapida partes - TRN;
    ActiveControlOnOpen=1100244012;
    TableBoxID=1100244000;
    SourceTable=Table7010438;
    AutoSplitKey=Yes;
    UpdateOnActivate=Yes;
    DelayedInsert=Yes;
    PopulateAllFields=Yes;
    OnOpenForm=BEGIN
                 rPar.FINDFIRST;

                 FILTERGROUP(2);
                 SETRANGE(Usuario      , USERID);
                 SETRANGE("Tipo accion", "Tipo accion"::Asignacion);
                 FILTERGROUP(0);

                 wDia := TODAY + 1;

                 LimpiaVentana;
                 SetEditable;
               END;

    OnQueryCloseForm=BEGIN
                       CompruebaTrabajoPendiente;
                     END;

    OnNewRecord=BEGIN
                  "Fecha asignacion" := wDia;
                  Usuario            := USERID;
                END;

  }
  CONTROLS
  {
    { 1100244000;TableBox;220 ;2420 ;25190;8250 ;HorzGlue=Both;
                                                 VertGlue=Both;
                                                 HeadingHeight=880 }
    { 1100244001;TextBox;0    ;0    ;2583 ;0    ;ParentControl=1100244000;
                                                 InColumn=Yes;
                                                 NextControl=1100244001;
                                                 SourceExpr="Num Parte" }
    { 1100244002;Label  ;0    ;0    ;0    ;0    ;ParentControl=1100244001;
                                                 InColumnHeading=Yes }
    { 1100244003;TextBox;0    ;0    ;1980 ;0    ;Editable=No;
                                                 ParentControl=1100244000;
                                                 InColumn=Yes;
                                                 SourceExpr=Fecha }
    { 1100244004;Label  ;0    ;0    ;0    ;0    ;ParentControl=1100244003;
                                                 InColumnHeading=Yes }
    { 1100244005;CheckBox;0   ;0    ;1320 ;0    ;ParentControl=1100244000;
                                                 InColumn=Yes;
                                                 ShowCaption=No;
                                                 SourceExpr="Dia siguiente" }
    { 1100244006;Label  ;0    ;0    ;0    ;0    ;ParentControl=1100244005;
                                                 InColumnHeading=Yes }
    { 1103355004;TextBox;2434 ;3410 ;1700 ;440  ;ParentControl=1100244000;
                                                 InColumn=Yes;
                                                 SourceExpr=Garaje }
    { 1103355005;Label  ;0    ;0    ;0    ;0    ;ParentControl=1103355004;
                                                 InColumnHeading=Yes }
    { 1100244017;TextBox;5953 ;2530 ;2090 ;440  ;ParentControl=1100244000;
                                                 InColumn=Yes;
                                                 CaptionML=ESP=Codigo Servicio;
                                                 SourceExpr=CodigoServicio }
    { 1100244018;Label  ;0    ;0    ;0    ;0    ;ParentControl=1100244017;
                                                 InColumnHeading=Yes }
    { 1100244021;TextBox;8917 ;2530 ;2200 ;110  ;ParentControl=1100244000;
                                                 InColumn=Yes;
                                                 CaptionML=ESP=Touroperador Principal;
                                                 SourceExpr=TouroperadorPrincipal }
    { 1100244022;Label  ;0    ;0    ;0    ;0    ;ParentControl=1100244021;
                                                 InColumnHeading=Yes }
    { 1100244031;TextBox;9261 ;2200 ;1700 ;440  ;ParentControl=1100244000;
                                                 InColumn=Yes;
                                                 CaptionML=ESP=Zona trabajo;
                                                 SourceExpr=ZonaTrabajo }
    { 1100244032;Label  ;0    ;0    ;0    ;0    ;ParentControl=1100244031;
                                                 InColumnHeading=Yes }
    { 1100244033;TextBox;11351;2310 ;1700 ;440  ;ParentControl=1100244000;
                                                 InColumn=Yes;
                                                 CaptionML=ESP=Zona Parte;
                                                 SourceExpr=ZonaParte }
    { 1100244034;Label  ;0    ;0    ;0    ;0    ;ParentControl=1100244033;
                                                 InColumnHeading=Yes }
    { 1100244023;TextBox;9578 ;2530 ;1417 ;440  ;Editable=No;
                                                 ParentControl=1100244000;
                                                 InColumn=Yes;
                                                 CaptionML=ESP=Hora inicio;
                                                 SourceExpr=HoraParte;
                                                 Format=<Hours24,2>:<Minutes,2> }
    { 1100244024;Label  ;0    ;0    ;0    ;0    ;ParentControl=1100244023;
                                                 InColumnHeading=Yes }
    { 1100244025;TextBox;11801;2530 ;1198 ;440  ;ParentControl=1100244000;
                                                 InColumn=Yes;
                                                 DecimalPlaces=0:2;
                                                 BlankZero=Yes;
                                                 SourceExpr=AdultosParte }
    { 1100244026;Label  ;0    ;0    ;0    ;0    ;ParentControl=1100244025;
                                                 InColumnHeading=Yes;
                                                 CaptionML=ESP=Adultos }
    { 1100244027;TextBox;13758;2200 ;1217 ;220  ;ParentControl=1100244000;
                                                 InColumn=Yes;
                                                 DecimalPlaces=0:2;
                                                 BlankZero=Yes;
                                                 SourceExpr=Ni¤osParte }
    { 1100244028;Label  ;0    ;0    ;0    ;0    ;ParentControl=1100244027;
                                                 InColumnHeading=Yes;
                                                 CaptionML=ESP=Ni¤os }
    { 1100244007;CommandButton;11110;11000;2200;550;
                                                 HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 Default=Yes;
                                                 PushAction=LookupOK;
                                                 InvalidActionAppearance=Hide }
    { 1100244008;CommandButton;13530;11000;2200;550;
                                                 HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 Cancel=Yes;
                                                 PushAction=LookupCancel;
                                                 InvalidActionAppearance=Hide }
    { 1100244009;CommandButton;23210;11000;2200;550;
                                                 HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 PushAction=FormHelp }
    { 1100244010;TabControl;220;220 ;25300;1980 ;HorzGlue=Both }
    { 1100244012;TextBox;1760 ;990  ;1700 ;440  ;ParentControl=1100244010;
                                                 InPage=0;
                                                 CaptionML=ESP=Dia;
                                                 SourceExpr=wDia;
                                                 OnValidate=BEGIN
                                                              CompruebaTrabajoPendiente;
                                                            END;

                                                 OnAfterValidate=BEGIN
                                                                   LimpiaVentana;

                                                                   IniciaRegistro;
                                                                 END;
                                                                  }
    { 1100244011;Label  ;440  ;990  ;1210 ;440  ;ParentControl=1100244012;
                                                 InPage=0;
                                                 CaptionML=ESP=Dia }
    { 1100244013;TextBox;5830 ;990  ;2530 ;440  ;Name=CodConductor;
                                                 ParentControl=1100244010;
                                                 InPage=0;
                                                 CaptionML=ESP=Conductor;
                                                 SourceExpr=wConductor;
                                                 TableRelation=Conductor.Conductor;
                                                 OnValidate=VAR
                                                              lrProv@1100253001 : Record 23;
                                                            BEGIN
                                                              CompruebaTrabajoPendiente;

                                                              CLEAR(wTextoProv);
                                                              IF rCond.GET(wConductor) THEN BEGIN
                                                                IF rCond.Bloqueado THEN
                                                                  ERROR(Text005);

                                                                rParte.comprobar_conductor(wConductor, wDia, rCond."Nombre Abreviado");

                                                                // Si el conductor es de un proveedor lo ense¤amos
                                                                IF rCond."Empresa Conductor" <> '' THEN BEGIN
                                                                  wTextoProv := rCond."Empresa Conductor";
                                                                  IF lrProv.GET(rCond."Empresa Conductor") THEN
                                                                   wTextoProv := wTextoProv + STRSUBSTNO(' (%1)',lrProv.Name);
                                                                END;

                                                                IF wAsignaGaraje THEN BEGIN
                                                                  IF rPar."Todos los garajes" <> rCond.Garaje THEN BEGIN
                                                                    wGaraje := rCond.Garaje;
                                                                    CompruebaGaraje;
                                                                  END;
                                                                END;
                                                              END;
                                                            END;

                                                 OnAfterValidate=BEGIN
                                                                   LimpiaVentana;

                                                                   IniciaRegistro;
                                                                 END;
                                                                  }
    { 1100244014;Label  ;3850 ;990  ;1870 ;440  ;ParentControl=1100244013;
                                                 InPage=0 }
    { 1100244015;TextBox;15400;990  ;2530 ;440  ;ParentControl=1100244010;
                                                 InPage=0;
                                                 CaptionML=ESP=Vehiculo;
                                                 NextControl=1100244001;
                                                 SourceExpr=wVehiculo;
                                                 TableRelation=Vehiculo.Matricula;
                                                 OnValidate=BEGIN
                                                              CompruebaTrabajoPendiente;

                                                              rVeh.RESET;
                                                              IF rVeh.GET(wVehiculo) THEN BEGIN

                                                                IF rVeh.Bloqueado THEN
                                                                  ERROR(Text004);

                                                                rVeh.SETFILTER ("Filtro Fecha Inicio", '<= %1', wDia);
                                                                rVeh.SETFILTER ("Filtro Fecha Fin"   , '= %1 | > %2', 0D , wDia);
                                                                rVeh.CALCFIELDS(rVeh."Tiene Incidencias Abiertas");
                                                                IF rVeh."Tiene Incidencias Abiertas" THEN
                                                                  MESSAGE(Text003);
                                                              END;
                                                            END;

                                                 OnAfterValidate=BEGIN
                                                                   LimpiaVentana;

                                                                   IniciaRegistro;
                                                                 END;
                                                                  }
    { 1100244016;Label  ;13420;990  ;1870 ;440  ;ParentControl=1100244015;
                                                 InPage=0 }
    { 1100244019;TextBox;8470 ;990  ;4510 ;440  ;Editable=No;
                                                 Focusable=No;
                                                 ParentControl=1100244010;
                                                 InPage=0;
                                                 SourceExpr=NombreConductor }
    { 1100244020;TextBox;18040;990  ;1430 ;440  ;Editable=No;
                                                 Focusable=No;
                                                 ParentControl=1100244010;
                                                 InPage=0;
                                                 HorzAlign=Center;
                                                 FontBold=Yes;
                                                 SourceExpr=NumVehiculo }
    { 1100253000;TextBox;3850 ;1540 ;9130 ;440  ;Focusable=No;
                                                 ParentControl=1100244010;
                                                 InPage=0;
                                                 HorzAlign=Right;
                                                 BackTransparent=Yes;
                                                 Border=No;
                                                 SourceExpr=wTextoProv }
    { 1103355000;TextBox;15400;1540 ;2530 ;440  ;Name=Garaje1;
                                                 ParentControl=1100244010;
                                                 InPage=0;
                                                 CaptionML=ESP=Garaje;
                                                 SourceExpr=wGaraje;
                                                 TableRelation=Garaje.Garaje WHERE (Abierto=CONST(Yes));
                                                 OnValidate=BEGIN
                                                              CompruebaGaraje;
                                                            END;
                                                             }
    { 1103355001;Label  ;13420;1540 ;1870 ;440  ;ParentControl=1103355000 }
    { 1103355002;CheckBox;18040;1540;440  ;440  ;ParentControl=1100244010;
                                                 InPage=0;
                                                 ShowCaption=No;
                                                 CaptionML=ESP=Asigna Garaje;
                                                 SourceExpr=wAsignaGaraje;
                                                 OnPush=BEGIN
                                                          SetEditable;
                                                          IF wAsignaGaraje THEN BEGIN
                                                            IF rCond.GET(wConductor) THEN
                                                              IF rPar."Todos los garajes" <> rCond.Garaje THEN
                                                                wGaraje:= rCond.Garaje;
                                                          END
                                                          ELSE
                                                            wGaraje:='';
                                                        END;
                                                         }
    { 1103355003;Label  ;18590;1540 ;2090 ;440  ;ParentControl=1103355002;
                                                 LeaderDots=No }
    { 1100244029;CommandButton;15950;11000;2200;550;
                                                 HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 CaptionML=ESP=Li&mpiar;
                                                 OnPush=BEGIN
                                                          LimpiaVentana;

                                                          IniciaRegistro;
                                                        END;
                                                         }
    { 1100244030;CommandButton;18370;11000;2200;550;
                                                 HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 CaptionML=ESP=Asi&gnar;
                                                 OnPush=BEGIN
                                                          IF NOT CONFIRM(Text001, TRUE) THEN
                                                            EXIT;

                                                          rTemp.RESET;
                                                          rTemp.SETRANGE("Tipo accion", rTemp."Tipo accion"::Asignacion);
                                                          rTemp.SETRANGE(Usuario, USERID);
                                                          IF rTemp.FINDSET THEN BEGIN
                                                            wActual := 0;
                                                            wTotal := rTemp.COUNT;
                                                            wVentana.OPEN(Text002);
                                                            REPEAT
                                                              wActual += 1;
                                                              wVentana.UPDATE(1, ROUND(wActual / wTotal * 10000, 1));

                                                              IF rParte.GET(rTemp."Num Parte") THEN BEGIN

                                                                // AJS 06.02.2004
                                                                // Marcamos asignacion rapida para que no salgan los mensajes de incidencias de conductor y vehiculo
                                                                // Marcamos Dia siguiente para los informes y para que no se cambie la fecha de facturacion del parte

                                                                rParte.VALIDATE("Asignacion rapida", TRUE);
                                                                IF wGaraje <> '' THEN
                                                                  rParte.VALIDATE(Garaje,wGaraje);

                                                                // JPT-  07/07/05 En Ibizatours no les interesa esta funcionalidad
                                                                IF NOT (rPar."Empresa Real" IN [rPar."Empresa Real"::IBZ, rPar."Empresa Real"::SJO]) THEN
                                                                BEGIN
                                                                  rParte.VALIDATE("Dia siguiente"    , rTemp."Dia siguiente");
                                                                  IF rParte.Fecha <> rTemp."Fecha asignacion" THEN
                                                                    rParte.VALIDATE(Fecha, rTemp."Fecha asignacion");
                                                                END;

                                                                rParte.VALIDATE(Vehiculo       , wVehiculo);
                                                                rParte.VALIDATE("Cod Conductor", wConductor);
                                                                rParte.VALIDATE("Asignacion rapida", FALSE);
                                                                rParte.MODIFY(TRUE);
                                                              END;
                                                            UNTIL rTemp.NEXT = 0;
                                                            wVentana.CLOSE;
                                                          END;

                                                          // Limpiamos la ventana para la siguiente asignacion

                                                          LimpiaVentana;

                                                          CurrForm.CodConductor.ACTIVATE;
                                                        END;
                                                         }
    { 1100244037;MenuButton;20790;11000;2200;550;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 CaptionML=ESP=&Parte;
                                                 Menu=MENUITEMS
                                                 {
                                                   { ID=1100244038;
                                                     PushAction=RunObject;
                                                     CaptionML=ESP=Ficha Parte;
                                                     RunObject=Form 7010400;
                                                     OnPush=BEGIN
                                                              rParte.RESET;
                                                              IF rParte.GET("Num Parte") THEN
                                                                FORM.RUN(7010400,rParte);
                                                            END;
                                                             }
                                                   { ID=1100244043;
                                                     MenuItemType=Separator }
                                                   { ID=1100244039;
                                                     CaptionML=ESP=Parte de Trabajo;
                                                     OnPush=BEGIN
                                                              rPar.GET;
                                                              rParte.RESET;
                                                              rParte.SETRANGE("N§ Parte","Num Parte");
                                                              rRepList.RESET;
                                                              IF NOT rRepList.GET(rPar."Informe partes trabajo") THEN
                                                                CLEAR(rRepList);
                                                              REPORT.RUNMODAL(rRepList."Id. Informe",TRUE,TRUE,rParte);
                                                            END;
                                                             }
                                                   { ID=1100244040;
                                                     CaptionML=ESP=Estructura Trabajo Diaria;
                                                     OnPush=VAR
                                                              lwFecha@1100244000 : Date;
                                                            BEGIN
                                                              rParte.RESET;
                                                              IF wDia <> 0D THEN
                                                                rParte.SETRANGE(Fecha,wDia);
                                                              REPORT.RUNMODAL(7010370,TRUE,TRUE,rParte);
                                                            END;
                                                             }
                                                   { ID=1100244042;
                                                     CaptionML=ESP=Servicios Diarios x C¢digo;
                                                     OnPush=BEGIN
                                                              rParte.RESET;
                                                              IF wDia <> 0D THEN
                                                                rParte.SETRANGE(Fecha,wDia);
                                                              REPORT.RUNMODAL(7010372,TRUE,TRUE,rParte);
                                                            END;
                                                             }
                                                 }
                                                  }
  }
  CODE
  {
    VAR
      wActual@1100244008 : Integer;
      wTotal@1100244009 : Integer;
      wVentana@1100244010 : Dialog;
      wDia@1100244000 : Date;
      wConductor@1100244001 : Code[10];
      wVehiculo@1100244002 : Code[10];
      wTextoProv@1100253000 : Text[250];
      wGaraje@1103355000 : Code[10];
      rPar@1100244013 : Record 7010311;
      rParte@1100244003 : Record 7010360;
      rCond@1100244004 : Record 7010327;
      rVeh@1100244005 : Record 7010346;
      rTemp@1100244006 : Record 7010438;
      Text001@1100244007 : TextConst 'ESP=Va a asignar recursos a los partes seleccionados, continuar?';
      Text002@1100244011 : TextConst 'ESP=ASIGNANDO RECURSOS @1@@@@@@@@@@@';
      Text003@1100244012 : TextConst 'ESP=El vehiculo seleccionado tiene abierta una incidencia para este dia.';
      Text004@1100244015 : TextConst 'ESP=No se puede asignar al parte un vehiculo que esta bloqueado.';
      Text005@1100244014 : TextConst 'ESP=No se puede asignar al parte un conductor que esta bloqueado.';
      rRepList@1100244016 : Record 7035384;
      Text006@1100244017 : TextConst 'ESP=Existe trabajo pendiente en la ventana, si continua este trabajo se borrara.';
      Text007@1100244018 : TextConst 'ESP=Concluya el trabajo mediante el boton ASIGNAR.';
      cFunBas@1103355001 : Codeunit 7010310;
      wAsignaGaraje@1103355002 : Boolean;

    PROCEDURE CodigoServicio@1100244001() : Code[10];
    BEGIN
      // CodigoServicio
      //
      IF rParte.GET("Num Parte") THEN
        EXIT(rParte."Codigo Servicio")
      ELSE
        EXIT('');
    END;

    PROCEDURE TouroperadorPrincipal@1100244003() : Code[10];
    BEGIN
      // TouroperadorPrincipal
      //
      IF rParte.GET("Num Parte") THEN
        EXIT(rParte."Touroperador principal")
      ELSE
        EXIT('');
    END;

    PROCEDURE NombreConductor@1100244000() : Code[20];
    BEGIN
      // NombreConductor
      //
      IF rCond.GET(wConductor) THEN
        EXIT(rCond."Nombre Abreviado")
      ELSE
        EXIT('');
    END;

    PROCEDURE NumVehiculo@1100244005() : Code[10];
    BEGIN
      // NumVehiculo
      //
      IF rVeh.GET(wVehiculo) THEN
        EXIT(rVeh.Numero)
      ELSE
        EXIT('');
    END;

    PROCEDURE HoraParte@1100244002() : Time;
    BEGIN
      // HoraParte
      //
      IF rParte.GET("Num Parte") THEN
        EXIT(rParte."Hora inicio")
      ELSE
        EXIT(0T);
    END;

    PROCEDURE AdultosParte@1100244004() : Decimal;
    BEGIN
      // AdultosParte
      //
      IF rParte.GET("Num Parte") THEN BEGIN
        rParte.CALCFIELDS("Adultos transportados");
        EXIT(rParte."Adultos transportados")
      END
      ELSE
        EXIT(0);
    END;

    PROCEDURE Ni¤osParte@1100244006() : Decimal;
    BEGIN
      // Ni¤osParte
      //
      IF rParte.GET("Num Parte") THEN BEGIN
        rParte.CALCFIELDS("Ni¤os transportados");
        EXIT(rParte."Ni¤os transportados")
      END
      ELSE
        EXIT(0);
    END;

    PROCEDURE LimpiaVentana@1100244007();
    BEGIN
      // LimpiaVentana
      //
      rTemp.RESET;
      rTemp.SETRANGE ("Tipo accion", rTemp."Tipo accion"::Asignacion);
      rTemp.SETRANGE (Usuario      , USERID);
      rTemp.SETFILTER("Num Parte"  , '<>%1', '');
      IF rTemp.FINDFIRST THEN
        rTemp.DELETEALL;
    END;

    PROCEDURE ZonaTrabajo@1100244008() : Code[10];
    BEGIN
      // ZonaTrabajo
      //
      IF rParte.GET("Num Parte") THEN
        EXIT(rParte."Zona trabajo")
      ELSE
        EXIT('');
    END;

    PROCEDURE ZonaParte@1100244011() : Code[10];
    BEGIN
      // ZonaParte
      //
      IF rParte.GET("Num Parte") THEN
        EXIT(rParte."Zona parte")
      ELSE
        EXIT('');
    END;

    PROCEDURE IniciaRegistro@1100244009();
    BEGIN
      // IniciaRegistro
      //
      "Fecha asignacion" := wDia;
      Usuario            := USERID;
      CurrForm.UPDATE;
    END;

    PROCEDURE CompruebaTrabajoPendiente@1100244012();
    BEGIN
      // CompruebaTrabajoPendiente
      //
      rTemp.RESET;
      rTemp.SETRANGE("Tipo accion", rTemp."Tipo accion"::Asignacion);
      rTemp.SETRANGE (Usuario     , USERID);
      rTemp.SETFILTER("Num Parte" , '<>%1', '');
      IF rTemp.FINDFIRST THEN BEGIN
        IF NOT CONFIRM(Text006) THEN
          ERROR(Text007);
      END;
    END;

    PROCEDURE SetEditable@1103355000();
    BEGIN
      // SetEditable

      CurrForm.Garaje1.ENABLED := wAsignaGaraje;
    END;

    PROCEDURE CompruebaGaraje@1103355002();
    BEGIN
      // CompruebaGaraje

      cFunBas.TestGarajeTodos(wGaraje);
      cFunBas.TestRestringido(wGaraje);
    END;

    BEGIN
    END.
  }
}
