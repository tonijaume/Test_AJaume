OBJECT Form 7010356 Ingresos Conductor
{
  OBJECT-PROPERTIES
  {
    Date=18/11/14;
    Time=12:43:12;
    Modified=Yes;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    Width=19030;
    Height=9900;
    CaptionML=ESP=Ingresos Conductor;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table7010335;
    SourceTablePlacement=First;
    DataCaptionExpr='';
    OnOpenForm=VAR
                 rPar@1100244000 : Record 7010311;
                 ERR1@1100244001 : TextConst 'ESP=Usuario no encontrado %1';
                 cFunBas@1100244002 : Codeunit 7010310;
                 v1dia@1100244003 : Date;
                 vUltDia@1100244004 : Date;
               BEGIN

                 vGaraje:=cFunBas.ObtenerGaraje();

                 //Si no hay ning£n registro, ....
                 RESET;
                 SETCURRENTKEY(Usuario,Garaje);
                 FILTERGROUP(2);
                 SETRANGE(Usuario,USERID);
                 FILTERGROUP(0);
                 IF COUNT = 0 THEN BEGIN
                   INIT;
                   Usuario := USERID;
                   INSERT;
                 END;
                 //............

                 IF vGaraje<>'' THEN BEGIN
                    CurrForm.FltGaraje.EDITABLE := FALSE;
                    SETFILTER("Filtro Garaje", vGaraje);
                 END;


                 v1dia:=cFunBas.fdom(TODAY,0);
                 vUltDia:=cFunBas.ldom(TODAY,0);
                 SETRANGE("Filtro Fecha",v1dia,vUltDia);

                 rPar.FINDFIRST;
                 gDiasDescuento:=rPar."Dias a Descontar";
                 vTodosLosGarajes:= rPar."Todos los garajes";
               END;

  }
  CONTROLS
  {
    { 1   ;TabControl   ;220  ;330  ;18590;2530 ;HorzGlue=Both;
                                                 VertGlue=Top }
    { 3   ;TextBox      ;3190 ;1650 ;3850 ;440  ;ParentControl=1;
                                                 InPage=0;
                                                 SourceExpr="Filtro Fecha";
                                                 OnAfterValidate=BEGIN
                                                                   CurrForm.UPDATE;
                                                                 END;
                                                                  }
    { 4   ;Label        ;550  ;1650 ;2530 ;440  ;ParentControl=3;
                                                 FontBold=Yes;
                                                 CaptionML=ESP=Filtro Fecha;
                                                 ToolTipML=ESP=Actuar  a efectos de visualizaci¢n como filtro para el campo <Fecha Inicio>.  Tambien se usar  para indicar el  mbito de fechas a generar. Ej: 011100..301100 }
    { 5   ;TextBox      ;3190 ;2200 ;2530 ;440  ;ValidateTableRelation=Yes;
                                                 ParentControl=1;
                                                 InPage=0;
                                                 SourceExpr=vAuxFltAcuerdo;
                                                 TableRelation="Tipo Acuerdo Conductor"."Tipo Acuerdo";
                                                 OnValidate=BEGIN
                                                              SETFILTER("Filtro Tipo Acuerdo",vAuxFltAcuerdo);
                                                            END;

                                                 OnAfterValidate=BEGIN
                                                                   CurrForm.UPDATE;
                                                                 END;
                                                                  }
    { 6   ;Label        ;550  ;2200 ;2530 ;440  ;ParentControl=5;
                                                 CaptionML=ESP=Tipo Acuerdo }
    { 7   ;TextBox      ;9900 ;1100 ;2530 ;440  ;ValidateTableRelation=Yes;
                                                 ParentControl=1;
                                                 InPage=0;
                                                 SourceExpr=vAuxFltCond;
                                                 TableRelation=Conductor.Conductor;
                                                 LookupFormID=Tabla conductor;
                                                 OnValidate=VAR
                                                              rCond@1100244001 : Record 7010327;
                                                              ES_EXTERNO@1100244002 : TextConst 'ESP=El conductor %1 es externo. No puede seleccionarse.';
                                                            BEGIN
                                                              IF vAuxFltCond<>'' THEN BEGIN
                                                                IF rCond.GET(vAuxFltCond) THEN BEGIN
                                                                  IF rCond.Externo THEN
                                                                    ERROR(ES_EXTERNO,rCond."Nombre Abreviado");
                                                                  vNomAbr := rCond."Nombre Abreviado";
                                                                  SETRANGE("Filtro Conductor",vAuxFltCond);
                                                                END
                                                              END
                                                              ELSE BEGIN
                                                                vNomAbr:='';
                                                                SETRANGE("Filtro Conductor");
                                                              END;
                                                            END;

                                                 OnAfterValidate=BEGIN
                                                                   CurrForm.UPDATE;
                                                                 END;
                                                                  }
    { 8   ;Label        ;7260 ;1100 ;2530 ;440  ;ParentControl=7;
                                                 CaptionML=ESP=Conductor }
    { 11  ;TextBox      ;9900 ;1650 ;1650 ;440  ;Visible=No;
                                                 ParentControl=1;
                                                 InPage=0;
                                                 SourceExpr=gDiasDescuento }
    { 12  ;Label        ;7260 ;1650 ;2530 ;440  ;Visible=No;
                                                 ParentControl=1;
                                                 InPage=0;
                                                 CaptionML=ESP=Viajes a descontar }
    { 16  ;TextBox      ;12540;1100 ;3520 ;440  ;Editable=No;
                                                 ParentControl=1;
                                                 InPage=0;
                                                 SourceExpr=vNomAbr }
    { 17  ;TextBox      ;3190 ;1100 ;2530 ;440  ;Name=FltGaraje;
                                                 ValidateTableRelation=Yes;
                                                 Editable=Yes;
                                                 ParentControl=1;
                                                 InPage=0;
                                                 SourceExpr=vGaraje;
                                                 TableRelation=Garaje.Garaje;
                                                 OnValidate=VAR
                                                              vFiltro@1100244001 : Code[10];
                                                              cFunBas@1100244002 : Codeunit 7010310;
                                                            BEGIN
                                                              cFunBas.TestRestringido(vGaraje);
                                                              IF (vGaraje='') OR (vGaraje=vTodosLosGarajes) THEN
                                                                SETRANGE("Filtro Garaje")
                                                              ELSE
                                                                SETFILTER("Filtro Garaje",vGaraje);
                                                            END;

                                                 OnAfterValidate=BEGIN
                                                                   CurrForm.UPDATE;
                                                                 END;
                                                                  }
    { 18  ;Label        ;550  ;1100 ;2530 ;440  ;ParentControl=17;
                                                 CaptionML=ESP=Garaje }
    { 9   ;SubForm      ;220  ;3080 ;18590;5830 ;Name=TablaIngresos;
                                                 HorzGlue=Both;
                                                 VertGlue=Both;
                                                 Border=No;
                                                 SubFormID=Form7010357;
                                                 SubFormView=SORTING(Garaje,Fecha Inicio,Nombre Abreviado,Concepto Variable,N§ Condicion)
                                                             ORDER(Ascending);
                                                 SubFormLink=Garaje=FIELD(Filtro Garaje),
                                                             Fecha Inicio=FIELD(Filtro Fecha),
                                                             Tipo Acuerdo=FIELD(Filtro Tipo Acuerdo),
                                                             Conductor=FIELD(Filtro Conductor) }
    { 10  ;MenuButton   ;14190;9130 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 CaptionML=ESP=&Ingresos;
                                                 Menu=MENUITEMS
                                                 {
                                                   { ID=21;
                                                     CaptionML=ESP=&Ventanas }
                                                   { ID=14;
                                                     ShortCutKey=Ctrl+A;
                                                     MenuLevel=1;
                                                     CaptionML=ESP=Registro &Actividad;
                                                     OnPush=VAR
                                                              rIng@1100244001 : Record 7010339;
                                                            BEGIN
                                                              CurrForm.TablaIngresos.FORM.GETRECORD(rIng);
                                                              VerHorasRealizadas(rIng);
                                                            END;
                                                             }
                                                   { ID=26;
                                                     ShortCutKey=Ctrl+P;
                                                     MenuLevel=1;
                                                     CaptionML=ESP=Actividad por Parte;
                                                     OnPush=VAR
                                                              rRxC@1100244001 : Record 7035336;
                                                              rIng@1100244002 : Record 7010339;
                                                            BEGIN
                                                              CurrForm.TablaIngresos.FORM.GETRECORD(rIng);

                                                              rRxC.RESET;
                                                              rRxC.SETCURRENTKEY(Conductor,Fecha);
                                                              rRxC.FILTERGROUP(2);
                                                              rRxC.SETRANGE(Conductor,rIng.Conductor);
                                                              rRxC.SETRANGE(Fecha,rIng."Fecha Inicio",rIng."Fecha Fin");
                                                              rRxC.FILTERGROUP(0);
                                                              rRxC.SETRANGE(Concepto,rIng."Concepto Variable");
                                                              FORM.RUNMODAL(7010364,rRxC);
                                                            END;
                                                             }
                                                   { ID=23;
                                                     ShortCutKey=Ctrl+V;
                                                     MenuLevel=1;
                                                     CaptionML=ESP=Condiciones &Variables;
                                                     OnPush=VAR
                                                              cFunBas@1100244001 : Codeunit 7010310;
                                                              vAuxGaraje@1100244002 : Code[10];
                                                              rCond@1100244003 : Record 7010330;
                                                              fCond@1100244004 : Form 7010345;
                                                              rIngresos@1100244005 : Record 7010339;
                                                            BEGIN
                                                              CurrForm.TablaIngresos.FORM.GETRECORD(rIngresos);

                                                              rCond.RESET;
                                                              rCond.INIT;
                                                              rCond.SETCURRENTKEY(Garaje);
                                                              rCond.SETRANGE(Garaje,rIngresos.Garaje);
                                                              IF (vGaraje<>'') AND (vGaraje<> vTodosLosGarajes) THEN BEGIN
                                                                rCond.FILTERGROUP(2);
                                                                rCond.SETRANGE(Garaje, vGaraje);
                                                                rCond.FILTERGROUP(0);
                                                              END;
                                                              CLEAR(fCond);
                                                              fCond.SETTABLEVIEW(rCond);
                                                              fCond.RUN;
                                                              CLEAR(fCond);
                                                              rCond.RESET;
                                                            END;
                                                             }
                                                   { ID=24;
                                                     ShortCutKey=Ctrl+H;
                                                     MenuLevel=1;
                                                     CaptionML=ESP=&Hist¢rico Contratos;
                                                     OnPush=VAR
                                                              rIngresos@1100244001 : Record 7010339;
                                                              rHist@1100244002 : Record 7010331;
                                                              fHist@1100244003 : Form 7010346;
                                                            BEGIN
                                                              CurrForm.TablaIngresos.FORM.GETRECORD(rIngresos);

                                                              rHist.RESET;
                                                              rHist.SETCURRENTKEY(Conductor, "Fecha Alta");
                                                              rHist.FILTERGROUP(2);
                                                              rHist.SETRANGE(Conductor,rIngresos.Conductor);
                                                              rHist.FILTERGROUP(0);

                                                              CLEAR(fHist);
                                                              fHist.SETTABLEVIEW(rHist);
                                                              fHist.RUN;
                                                            END;
                                                             }
                                                   { ID=25;
                                                     ShortCutKey=Ctrl+I;
                                                     MenuLevel=1;
                                                     CaptionML=ESP=&Incidencias;
                                                     OnPush=VAR
                                                              rIngresos@1100244001 : Record 7010339;
                                                              rIncidencias@1100244002 : Record 7010333;
                                                              fIncidencias@1100244003 : Form 7010367;
                                                            BEGIN
                                                              CurrForm.TablaIngresos.FORM.GETRECORD(rIngresos);

                                                              rIncidencias.RESET;
                                                              rIncidencias.SETCURRENTKEY(Conductor, "Fecha Desde");
                                                              rIncidencias.FILTERGROUP(2);
                                                              rIncidencias.SETRANGE(Conductor,rIngresos.Conductor);
                                                              rIncidencias.FILTERGROUP(0);

                                                              CLEAR(fIncidencias);
                                                              fIncidencias.SETTABLEVIEW(rIncidencias);
                                                              fIncidencias.RUN;
                                                            END;
                                                             }
                                                   { ID=15;
                                                     PushAction=RunObject;
                                                     ShortCutKey=Ctrl+R;
                                                     MenuLevel=1;
                                                     CaptionML=ESP=E&rrores (Todos);
                                                     RunObject=Form 7035342 }
                                                   { ID=19;
                                                     ShortCutKey=Ctrl+C;
                                                     MenuLevel=1;
                                                     CaptionML=ESP=Errores (&Conductor);
                                                     OnPush=VAR
                                                              rIngresos@1100244001 : Record 7010339;
                                                              rErr@1100244002 : Record 7010344;
                                                              fErr@1100244003 : Form 7035342;
                                                              NO_HAY_ERRORES@1100244004 : TextConst 'ESP=No se han detectado errores para %1 <%2-%3>';
                                                            BEGIN
                                                              CurrForm.TablaIngresos.FORM.GETRECORD(rIngresos);

                                                              //Realizamos la siguiente comprobaci¢n porque no podemos crear periodos sobre un registro
                                                              //inexistente.
                                                              IF (rIngresos.Fecha <> 0D) AND (rIngresos.Conductor <> '') THEN BEGIN
                                                                rErr.RESET;
                                                                rErr.SETCURRENTKEY("Cod Conductor",Fecha);
                                                                rErr.SETRANGE("Cod Conductor",rIngresos.Conductor);
                                                                rErr.SETRANGE(Fecha,rIngresos."Fecha Inicio",rIngresos."Fecha Fin");

                                                                IF rErr.COUNT=0 THEN
                                                                  ERROR(NO_HAY_ERRORES,rIngresos."Nombre Abreviado",rIngresos."Fecha Inicio",
                                                                                       rIngresos."Fecha Fin");
                                                                CLEAR(fErr);
                                                                fErr.SETTABLEVIEW(rErr);
                                                                fErr.RUN;
                                                              END;
                                                            END;
                                                             }
                                                   { ID=13;
                                                     ShortCutKey=May£s+F4;
                                                     CaptionML=ESP=Eliminar registros seg£n filtro;
                                                     OnPush=VAR
                                                              FALTAN_FECHAS@1100244001 : TextConst 'ESP=Debe introducir un  mbito de fechas';
                                                              rIN@1100244002 : Record 7010339;
                                                              vFecha1@1100244003 : Date;
                                                              vFecha2@1100244004 : Date;
                                                              vConductor@1100244005 : Code[10];
                                                              vGaraje@1100244006 : Code[10];
                                                              vTipoAcuerdo@1100244007 : Code[10];
                                                              vAux@1100244008 : Text[100];
                                                            BEGIN
                                                              IF GETFILTER("Filtro Fecha") = '' THEN
                                                                ERROR(FALTAN_FECHAS);

                                                              vFecha1     := GETRANGEMIN("Filtro Fecha");
                                                              vFecha2     := GETRANGEMAX("Filtro Fecha");
                                                              vConductor  := GETFILTER("Filtro Conductor");
                                                              vGaraje     := GETFILTER("Filtro Garaje");
                                                              vTipoAcuerdo:= GETFILTER("Filtro Tipo Acuerdo");

                                                              //Construimos el mensaje de aviso.
                                                              vAux := Text000;
                                                              IF vConductor <> '' THEN
                                                                vAux += vNomAbr+' ';
                                                              IF vTipoAcuerdo <> '' THEN
                                                                vAux += vTipoAcuerdo+' ';
                                                              IF vGaraje <> '' THEN
                                                                vAux += vGaraje+' ';

                                                              IF vAux = Text000 THEN
                                                                vAux := '';


                                                              IF NOT CONFIRM(Text001+FORMAT(vFecha1)+Text002+
                                                                             FORMAT(vFecha2)+' '+vAux+' ?') THEN
                                                                EXIT;

                                                              IF NOT CONFIRM(Text003) THEN
                                                                EXIT;

                                                              rIN.RESET;
                                                              rIN.SETCURRENTKEY(Garaje,"Fecha Inicio");

                                                              IF vGaraje <> '' THEN
                                                                rIN.SETRANGE(Garaje,vGaraje);

                                                              rIN.SETRANGE("Fecha Inicio",vFecha1,vFecha2);

                                                              IF vTipoAcuerdo <> '' THEN
                                                                rIN.SETRANGE("Tipo Acuerdo",vTipoAcuerdo);

                                                              IF vConductor <> '' THEN
                                                                rIN.SETRANGE(Conductor,vConductor);

                                                              //Important¡simo que haya el trigger.
                                                              rIN.DELETEALL(TRUE);
                                                            END;
                                                             }
                                                   { ID=20;
                                                     ShortCutKey=F9;
                                                     CaptionML=ESP=&Generar;
                                                     OnPush=VAR
                                                              cIng@1100244001 : Codeunit 7010315;
                                                              vFecIni@1100244002 : Date;
                                                              vFecFin@1100244003 : Date;
                                                              vConvenio@1100244004 : Code[10];
                                                              vConductor@1100244005 : Code[10];
                                                              vGaraje@1100244006 : Code[10];
                                                              ok@1100244007 : Boolean;
                                                              error1@1100244008 : TextConst 'ESP=Fechas incorrectas';
                                                            BEGIN
                                                              //Realizamos las correspondientes validaciones sobre las fechas.
                                                              vFecIni   := GETRANGEMIN("Filtro Fecha");
                                                              vFecFin   := GETRANGEMAX("Filtro Fecha");
                                                              vConvenio := GETFILTER("Filtro Tipo Acuerdo");
                                                              vConductor:= GETFILTER("Filtro Conductor");
                                                              vGaraje   := GETFILTER("Filtro Garaje");

                                                              IF (vFecIni<>0D) AND (vFecFin<>0D) AND (vFecIni<=vFecFin) THEN
                                                                 ok := CONFIRM(Text004)
                                                              ELSE
                                                                 ERROR(error1);

                                                              IF ok THEN
                                                                 cIng.CalculoIngresos(vFecIni,vFecFin,vConductor,vConvenio,vGaraje,gDiasDescuento);
                                                            END;
                                                             }
                                                   { ID=22;
                                                     ShortCutKey=Ctrl+T;
                                                     CaptionML=ESP=&Totalizar;
                                                     OnPush=VAR
                                                              rCond@1100244001 : Record 7010327;
                                                              fCond@1100244002 : Form 7010361;
                                                              vF1@1100244003 : Date;
                                                              vF2@1100244004 : Date;
                                                            BEGIN
                                                              rCond.RESET;
                                                              rCond.SETCURRENTKEY(Garaje,Conductor);

                                                              IF vGaraje <> '' THEN BEGIN
                                                                rCond.FILTERGROUP(2);
                                                                rCond.SETRANGE(Garaje,vGaraje);
                                                                rCond.FILTERGROUP(0);
                                                              END;

                                                              IF vAuxFltCond <> '' THEN
                                                                rCond.SETRANGE(Conductor,vAuxFltCond);

                                                              IF GETFILTER("Filtro Fecha") <> '' THEN BEGIN
                                                                vF1 := GETRANGEMIN("Filtro Fecha");
                                                                vF2 := GETRANGEMAX("Filtro Fecha");
                                                                rCond.SETRANGE("Filtro Fechas",vF1,vF2);
                                                              END;

                                                              rCond.SETFILTER(Ingresos,'>%1',0);

                                                              IF vAuxFltAcuerdo <> '' THEN BEGIN
                                                                //rCond.FILTERGROUP(2);
                                                                rCond.SETRANGE("Tipo Acuerdo",vAuxFltAcuerdo);
                                                                //rCond.FILTERGROUP(0);
                                                              END;

                                                              fCond.SETTABLEVIEW(rCond);
                                                              fCond.RUN;
                                                            END;
                                                             }
                                                 }
                                                  }
    { 2   ;CommandButton;16610;9130 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 PushAction=FormHelp }
  }
  CODE
  {
    VAR
      Text000@1100244006 : TextConst 'ESP="para "';
      Text001@1100244007 : TextConst 'ESP="¨Est  seguro de eliminar los registros del "';
      Text002@1100244008 : TextConst 'ESP=" al "';
      Text003@1100244009 : TextConst 'ESP=¨Est  seguro?';
      Text004@1100244010 : TextConst 'ESP=¨Realizar c lculo de ingresos?';
      vGaraje@1100244000 : Code[10];
      gDiasDescuento@1100244001 : Integer;
      vNomAbr@1100244002 : Code[20];
      vAuxFltCond@1100244003 : Code[10];
      vAuxFltAcuerdo@1100244004 : Code[10];
      vTodosLosGarajes@1100244005 : Code[10];

    PROCEDURE VerHorasRealizadas@1(Registro@1100244000 : Record 7010339);
    VAR
      rHoras@1100244001 : Record 7010332;
      fHoras@1100244002 : Form 7010349;
    BEGIN
      rHoras.RESET;
      rHoras.INIT;
      rHoras.SETCURRENTKEY(Conductor,Fecha);
      rHoras.FILTERGROUP(2);
      rHoras.SETRANGE(Conductor,Registro.Conductor);
      rHoras.SETRANGE(Fecha,Registro."Fecha Inicio",Registro."Fecha Fin");
      rHoras.FILTERGROUP(0);

      CLEAR(fHoras);
      fHoras.SETTABLEVIEW(rHoras);
      fHoras.RUN;
      CLEAR(fHoras);
      rHoras.RESET;
    END;

    BEGIN
    {
      //Podria ocurrir un error, si dado un periodo, hay un conductor que cambia de Tipo de acuerdo
      //dentro de ese periodo y adem s se genera el ingreso por Convenio.
    }
    END.
  }
}
