OBJECT Form 161 Purchase Statistics
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00,NAVES6.00;
  }
  PROPERTIES
  {
    Width=13420;
    Height=8910;
    CaptionML=[ENU=Purchase Statistics;
               ESP=Estad¡sticas compras];
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table38;
    OnOpenForm=BEGIN
                 PurchSetup.GET;
                 AllowInvDisc :=
                   NOT (PurchSetup."Calc. Inv. Discount" AND VendInvDiscRecExists("Invoice Disc. Code"));
                 AllowVATDifference :=
                   PurchSetup."Allow VAT Difference" AND
                   NOT ("Document Type" IN ["Document Type"::Quote,"Document Type"::"Blanket Order"]);
                 CurrForm.EDITABLE := AllowVATDifference OR AllowInvDisc;
                 SetVATSpecification;
               END;

    OnQueryCloseForm=BEGIN
                       GetVATSpecification;
                       IF TempVATAmountLine.GetAnyLineModified THEN
                         UpdateVATOnPurchLines;
                       EXIT(TRUE);
                     END;

    OnActivateForm=BEGIN
                     IF "No." = PrevNo THEN
                       GetVATSpecification;
                   END;

    OnAfterGetRecord=VAR
                       PurchLine@1000 : Record 39;
                       TempPurchLine@1001 : TEMPORARY Record 39;
                     BEGIN
                       CurrForm.CAPTION(STRSUBSTNO(Text000,"Document Type"));
                       IF PrevNo = "No." THEN
                         EXIT;
                       PrevNo := "No.";
                       FILTERGROUP(2);
                       SETRANGE("No.",PrevNo);
                       FILTERGROUP(0);
                       CLEAR(PurchLine);
                       CLEAR(TotalPurchLine);
                       CLEAR(TotalPurchLineLCY);
                       CLEAR(PurchPost);

                       PurchPost.GetPurchLines(Rec,TempPurchLine,0);
                       CLEAR(PurchPost);
                       PurchPost.SumPurchLinesTemp(
                         Rec,TempPurchLine,0,TotalPurchLine,TotalPurchLineLCY,VATAmount,VATAmountText);

                       IF "Prices Including VAT" THEN BEGIN
                         TotalAmount2 := TotalPurchLine.Amount;
                         TotalAmount1 := TotalAmount2 + VATAmount;
                         TotalPurchLine."Line Amount" := TotalAmount1 + TotalPurchLine."Inv. Discount Amount"
                           + TotalPurchLine."Pmt. Disc. Rcd. Amount";
                       END ELSE BEGIN
                         TotalAmount1 := TotalPurchLine.Amount;
                         TotalAmount2 := TotalPurchLine."Amount Including VAT";
                       END;

                       IF Vend.GET("Pay-to Vendor No.") THEN
                         Vend.CALCFIELDS("Balance (LCY)")
                       ELSE
                         CLEAR(Vend);

                       PurchLine.CalcVATAmountLines(0,Rec,TempPurchLine,TempVATAmountLine);
                       TempVATAmountLine.MODIFYALL(Modified,FALSE);
                       SetVATSpecification;
                     END;

    OnAfterGetCurrRecord=BEGIN
                           CurrForm.UPDATECONTROLS;
                         END;

  }
  CONTROLS
  {
    { 34  ;CommandButton;11000;8140 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 PushAction=FormHelp }
    { 1   ;TabControl   ;220  ;220  ;12980;5060 ;HorzGlue=Both;
                                                 VertGlue=Top;
                                                 PageNamesML=[ENU=General,Vendor;
                                                              ESP=General,Proveedor] }
    { 97  ;TextBox      ;3850 ;990  ;2750 ;440  ;Editable=No;
                                                 ParentControl=1;
                                                 InPage=0;
                                                 CaptionML=[ENU=Amount;
                                                            ESP=Importe];
                                                 SourceExpr=TotalPurchLine."Line Amount";
                                                 AutoFormatType=1;
                                                 AutoFormatExpr="Currency Code";
                                                 CaptionClass=GetCaptionClass(Text001,FALSE) }
    { 98  ;Label        ;440  ;990  ;3300 ;440  ;ParentControl=97 }
    { 71  ;TextBox      ;10230;3190 ;2750 ;440  ;Editable=No;
                                                 ParentControl=1;
                                                 InPage=0;
                                                 CaptionML=[ENU=Volume;
                                                            ESP=Volumen];
                                                 DecimalPlaces=0:6;
                                                 SourceExpr=TotalPurchLine."Unit Volume" }
    { 72  ;Label        ;6820 ;3190 ;3300 ;440  ;ParentControl=71 }
    { 73  ;TextBox      ;10230;1540 ;2750 ;440  ;Editable=No;
                                                 ParentControl=1;
                                                 InPage=0;
                                                 CaptionML=[ENU=Parcels;
                                                            ESP=Lotes];
                                                 DecimalPlaces=0:6;
                                                 SourceExpr=TotalPurchLine."Units per Parcel" }
    { 74  ;Label        ;6820 ;1540 ;3300 ;440  ;ParentControl=73 }
    { 75  ;TextBox      ;3850 ;3410 ;2750 ;440  ;Editable=No;
                                                 ParentControl=1;
                                                 InPage=0;
                                                 CaptionML=[ENU=VAT Amount;
                                                            ESP=Importe IVA];
                                                 SourceExpr=VATAmount;
                                                 AutoFormatType=1;
                                                 AutoFormatExpr="Currency Code" }
    { 76  ;TextBox      ;3850 ;3960 ;2750 ;440  ;Editable=No;
                                                 ParentControl=1;
                                                 InPage=0;
                                                 CaptionML=[ENU=Total Incl. VAT;
                                                            ESP=Total IVA incl.];
                                                 SourceExpr=TotalAmount2;
                                                 AutoFormatType=1;
                                                 AutoFormatExpr="Currency Code";
                                                 CaptionClass=GetCaptionClass(Text002,TRUE) }
    { 86  ;Label        ;440  ;3960 ;3300 ;440  ;ParentControl=76 }
    { 80  ;TextBox      ;3850 ;2750 ;2750 ;440  ;ParentControl=1;
                                                 InPage=0;
                                                 CaptionML=[ENU=Total;
                                                            ESP=Total];
                                                 SourceExpr=TotalAmount1;
                                                 AutoFormatType=1;
                                                 AutoFormatExpr="Currency Code";
                                                 CaptionClass=GetCaptionClass(Text002,FALSE);
                                                 OnActivate=BEGIN
                                                              CurrForm.UPDATE;
                                                            END;

                                                 OnValidate=BEGIN
                                                              UpdateTotalAmount;
                                                            END;
                                                             }
    { 84  ;Label        ;440  ;2750 ;3300 ;440  ;ParentControl=80 }
    { 82  ;TextBox      ;10230;2640 ;2750 ;440  ;Editable=No;
                                                 ParentControl=1;
                                                 InPage=0;
                                                 CaptionML=[ENU=Gross Weight;
                                                            ESP=Peso bruto];
                                                 DecimalPlaces=0:6;
                                                 SourceExpr=TotalPurchLine."Gross Weight" }
    { 83  ;Label        ;6820 ;2640 ;3300 ;440  ;ParentControl=82 }
    { 91  ;TextBox      ;10230;2090 ;2750 ;440  ;Editable=No;
                                                 ParentControl=1;
                                                 InPage=0;
                                                 CaptionML=[ENU=Net Weight;
                                                            ESP=Peso neto];
                                                 DecimalPlaces=0:6;
                                                 SourceExpr=TotalPurchLine."Net Weight" }
    { 92  ;Label        ;6820 ;2090 ;3300 ;440  ;ParentControl=91 }
    { 93  ;TextBox      ;3850 ;1540 ;2750 ;440  ;ParentControl=1;
                                                 InPage=0;
                                                 CaptionML=[ENU=Inv. Discount Amount;
                                                            ESP=Importe dto. factura];
                                                 SourceExpr=TotalPurchLine."Inv. Discount Amount";
                                                 AutoFormatType=1;
                                                 AutoFormatExpr="Currency Code";
                                                 OnActivate=BEGIN
                                                              CurrForm.UPDATE;
                                                            END;

                                                 OnValidate=BEGIN
                                                              UpdateInvDiscAmount;
                                                            END;
                                                             }
    { 94  ;Label        ;440  ;1540 ;3300 ;440  ;ParentControl=93 }
    { 95  ;TextBox      ;10230;990  ;2750 ;440  ;Editable=No;
                                                 ParentControl=1;
                                                 InPage=0;
                                                 CaptionML=[ENU=Quantity;
                                                            ESP=Cantidad];
                                                 DecimalPlaces=0:6;
                                                 SourceExpr=TotalPurchLine.Quantity }
    { 96  ;Label        ;6820 ;990  ;3300 ;440  ;ParentControl=95 }
    { 2   ;TextBox      ;440  ;3410 ;3300 ;440  ;Editable=No;
                                                 Focusable=No;
                                                 ParentControl=75;
                                                 InPage=0;
                                                 Border=No;
                                                 LeaderDots=Yes;
                                                 CaptionML=[ENU=VAT Amount;
                                                            ESP=Importe IVA];
                                                 SourceExpr=VATAmountText }
    { 3   ;TextBox      ;3850 ;4620 ;2750 ;440  ;Editable=No;
                                                 ParentControl=1;
                                                 InPage=0;
                                                 CaptionML=[ENU=Amount (LCY);
                                                            ESP=Importe (DL)];
                                                 SourceExpr=TotalPurchLineLCY.Amount;
                                                 AutoFormatType=1 }
    { 4   ;Label        ;440  ;4620 ;3300 ;440  ;ParentControl=3;
                                                 CaptionML=[ENU=Purchase (LCY);
                                                            ESP=Compra (DL)] }
    { 1100000;Label     ;440  ;2090 ;3300 ;440  ;ParentControl=1;
                                                 InPage=0;
                                                 CaptionML=[ENU=Pmt. Disc. Rcd. Amount;
                                                            ESP=Dto. P.P.] }
    { 1100001;TextBox   ;3850 ;2090 ;2750 ;440  ;Editable=No;
                                                 ParentControl=1;
                                                 InPage=0;
                                                 CaptionML=[ENU=Pmt. Disc. Rcd. Amount;
                                                            ESP=Dto. P.P.];
                                                 DecimalPlaces=2:6;
                                                 SourceExpr=TotalPurchLine."Pmt. Disc. Rcd. Amount";
                                                 AutoFormatType=1;
                                                 AutoFormatExpr="Currency Code" }
    { 67  ;TextBox      ;3850 ;990  ;2750 ;440  ;Editable=No;
                                                 ParentControl=1;
                                                 InPage=1;
                                                 CaptionML=[ENU=Balance (LCY);
                                                            ESP=Saldo (DL)];
                                                 SourceExpr=Vend."Balance (LCY)";
                                                 AutoFormatType=1 }
    { 70  ;Label        ;440  ;990  ;3300 ;440  ;ParentControl=67 }
    { 5   ;SubForm      ;220  ;5500 ;12980;2420 ;Name=SubForm;
                                                 HorzGlue=Both;
                                                 VertGlue=Both;
                                                 Border=No;
                                                 SubFormID=Form576;
                                                 OnDeactivate=BEGIN
                                                                GetVATSpecification;
                                                              END;
                                                               }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Purchase %1 Statistics;ESP=Estad¡sticas %1 compras';
      Text001@1001 : TextConst 'ENU=Amount;ESP=Importe';
      Text002@1002 : TextConst 'ENU=Total;ESP=Total';
      Text003@1003 : TextConst 'ENU=%1 must not be 0.;ESP=%1 no debe ser 0.';
      Text004@1004 : TextConst 'ENU=%1 must not be greater than %2.;ESP=%1 no debe ser m s grande de %2';
      Text005@1005 : TextConst 'ENU=You cannot change the invoice discount because there is a %1 record for %2 %3.;ESP=No puede cambiar el dto. factura porque hay un %1 registro para %2 %3.';
      TotalPurchLine@1006 : Record 39;
      TotalPurchLineLCY@1007 : Record 39;
      Vend@1008 : Record 23;
      TempVATAmountLine@1009 : TEMPORARY Record 290;
      PurchSetup@1010 : Record 312;
      PurchPost@1011 : Codeunit 90;
      TotalAmount1@1012 : Decimal;
      TotalAmount2@1013 : Decimal;
      VATAmount@1014 : Decimal;
      VATAmountText@1015 : Text[30];
      PrevNo@1016 : Code[20];
      AllowInvDisc@1017 : Boolean;
      AllowVATDifference@1018 : Boolean;

    LOCAL PROCEDURE UpdateHeaderInfo@5();
    VAR
      CurrExchRate@1000 : Record 330;
      UseDate@1001 : Date;
    BEGIN
      TotalPurchLine."Inv. Discount Amount" := TempVATAmountLine.GetTotalInvDiscAmount;
      TotalAmount1 :=
        TotalPurchLine."Line Amount" - TotalPurchLine."Inv. Discount Amount" -
        TotalPurchLine."Pmt. Disc. Rcd. Amount";
      VATAmount := TempVATAmountLine.GetTotalVATAmount;
      IF "Prices Including VAT" THEN BEGIN
        TotalAmount1 := TempVATAmountLine.GetTotalAmountInclVAT;
        TotalAmount2 := TotalAmount1 - VATAmount;
        TotalPurchLine."Line Amount" := TotalAmount1 + TotalPurchLine."Inv. Discount Amount" +
          TotalPurchLine."Pmt. Disc. Rcd. Amount";
      END ELSE
        TotalAmount2 := TotalAmount1 + VATAmount;

      IF "Prices Including VAT" THEN
        TotalPurchLineLCY.Amount := TotalAmount2
      ELSE
        TotalPurchLineLCY.Amount := TotalAmount1;
      IF "Currency Code" <> '' THEN BEGIN
        IF ("Document Type" IN ["Document Type"::"Blanket Order","Document Type"::Quote]) AND
           ("Posting Date" = 0D)
        THEN
          UseDate := WORKDATE
        ELSE
          UseDate := "Posting Date";

        TotalPurchLineLCY.Amount :=
          CurrExchRate.ExchangeAmtFCYToLCY(
            UseDate,"Currency Code",TotalPurchLineLCY.Amount,"Currency Factor");
      END;
    END;

    LOCAL PROCEDURE GetVATSpecification@21();
    BEGIN
      CurrForm.SubForm.FORM.GetTempVATAmountLine(TempVATAmountLine);
      IF TempVATAmountLine.GetAnyLineModified THEN
        UpdateHeaderInfo;
    END;

    LOCAL PROCEDURE SetVATSpecification@11();
    BEGIN
      CurrForm.SubForm.FORM.SetTempVATAmountLine(TempVATAmountLine);
      CurrForm.SubForm.FORM.InitGlobals(
        "Currency Code",AllowVATDifference,AllowVATDifference,
        "Prices Including VAT",AllowInvDisc,"VAT Base Discount %");
    END;

    LOCAL PROCEDURE UpdateTotalAmount@16();
    VAR
      SaveTotalAmount@1000 : Decimal;
    BEGIN
      CheckAllowInvDisc;
      IF "Prices Including VAT" THEN BEGIN
        SaveTotalAmount := TotalAmount1;
        UpdateInvDiscAmount;
        TotalAmount1 := SaveTotalAmount;
      END;

      WITH TotalPurchLine DO
        "Inv. Discount Amount" := "Line Amount" - TotalAmount1;
      UpdateInvDiscAmount;
    END;

    LOCAL PROCEDURE UpdateInvDiscAmount@3();
    VAR
      InvDiscBaseAmount@1000 : Decimal;
    BEGIN
      CheckAllowInvDisc;
      InvDiscBaseAmount := TempVATAmountLine.GetTotalInvDiscBaseAmount(FALSE,"Currency Code");
      IF InvDiscBaseAmount = 0 THEN
        ERROR(Text003,TempVATAmountLine.FIELDCAPTION("Inv. Disc. Base Amount"));

      IF TotalPurchLine."Inv. Discount Amount" / InvDiscBaseAmount > 1 THEN
        ERROR(
          Text004,
          TotalPurchLine.FIELDCAPTION("Inv. Discount Amount"),
          TempVATAmountLine.FIELDCAPTION("Inv. Disc. Base Amount"));

      TempVATAmountLine.SetInvoiceDiscountAmount(
        TotalPurchLine."Inv. Discount Amount","Currency Code","Prices Including VAT","VAT Base Discount %");
      CurrForm.SubForm.FORM.SetTempVATAmountLine(TempVATAmountLine);
      UpdateHeaderInfo;

      "Invoice Discount Calculation" := "Invoice Discount Calculation"::Amount;
      "Invoice Discount Value" := TotalPurchLine."Inv. Discount Amount";
      MODIFY;
      UpdateVATOnPurchLines;
    END;

    LOCAL PROCEDURE GetCaptionClass@2(FieldCaption@1000 : Text[100];ReverseCaption@1001 : Boolean) : Text[80];
    BEGIN
      IF "Prices Including VAT" XOR ReverseCaption THEN
        EXIT('2,1,' + FieldCaption)
      ELSE
        EXIT('2,0,' + FieldCaption);
    END;

    LOCAL PROCEDURE UpdateVATOnPurchLines@1();
    VAR
      PurchLine@1000 : Record 39;
    BEGIN
      GetVATSpecification;
      IF TempVATAmountLine.GetAnyLineModified THEN BEGIN
        PurchLine.UpdateVATOnLines(0,Rec,PurchLine,TempVATAmountLine);
        PurchLine.UpdateVATOnLines(1,Rec,PurchLine,TempVATAmountLine);
      END;
      PrevNo := '';
    END;

    LOCAL PROCEDURE VendInvDiscRecExists@4(InvDiscCode@1000 : Code[20]) : Boolean;
    VAR
      VendInvDisc@1001 : Record 24;
    BEGIN
      VendInvDisc.SETRANGE(Code,InvDiscCode);
      EXIT(VendInvDisc.FIND('-'));
    END;

    LOCAL PROCEDURE CheckAllowInvDisc@8();
    VAR
      VendInvDisc@1000 : Record 24;
    BEGIN
      IF NOT AllowInvDisc THEN
        ERROR(
          Text005,
          VendInvDisc.TABLECAPTION,FIELDCAPTION("Invoice Disc. Code"),"Invoice Disc. Code");
    END;

    BEGIN
    END.
  }
}
