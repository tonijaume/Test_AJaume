OBJECT Form 7009969 Ficha conciliacion cuentas
{
  OBJECT-PROPERTIES
  {
    Date=02/10/12;
    Time=12:00:00;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    Permissions=TableData 17=rm;
    Width=18150;
    Height=11220;
    CaptionML=[ENU=Ledger entries conciliations card;
               ESP=Ficha conciliacion cuentas];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table17;
    DataCaptionExpr="G/L Account No.";
    SourceTableView=SORTING(G/L Account No.,Posting Date);
    OnInit=VAR
             rUsr@1100217000 : Record 2000000002;
           BEGIN
             IF NOT rUsr.GET(USERID) THEN
               ERROR('El usuario no est  registrado en el sistema.');
           END;

    OnOpenForm=VAR
                 FiltroCuenta@1100217000 : Text[20];
               BEGIN
                 FiltroCuenta := GETRANGEMIN("G/L Account No.");
                 FILTERGROUP(2);
                 SETRANGE("G/L Account No.", FiltroCuenta);
                 FILTERGROUP(0);
                 SETRANGE("G/L Account No.");
               END;

    OnAfterGetRecord=BEGIN
                       CALCFIELDS("Importe conciliado");
                       "Importe pendiente" := Amount - "Importe conciliado";
                     END;

    OnAfterGetCurrRecord=BEGIN
                           IF "G/L Account No." <> xRec."G/L Account No." THEN
                             Actualizar_Saldos;
                         END;

  }
  CONTROLS
  {
    { 49  ;CommandButton;7920 ;8800 ;10010;1430 ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 Focusable=No;
                                                 ShowCaption=No;
                                                 BackTransparent=Yes }
    { 1   ;TabControl   ;220  ;220  ;17710;1430 ;HorzGlue=Both;
                                                 VertGlue=Top;
                                                 PageNamesML=[ENU=Account;
                                                              ESP=Cuenta] }
    { 4   ;TextBox      ;3850 ;990  ;2420 ;440  ;Editable=No;
                                                 ParentControl=1;
                                                 InPage=0;
                                                 SourceExpr="G/L Account No." }
    { 5   ;Label        ;440  ;990  ;3300 ;440  ;ParentControl=4 }
    { 24  ;TextBox      ;6380 ;990  ;6160 ;440  ;Editable=No;
                                                 ParentControl=1;
                                                 InPage=0;
                                                 SourceExpr=Nom_Cuenta("G/L Account No.") }
    { 10  ;CommandButton;15730;10450;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 PushAction=FormHelp }
    { 22  ;MenuButton   ;13310;10450;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 CaptionML=[ENU=&conciliation;
                                                            ESP=&Conciliaci¢n];
                                                 Menu=MENUITEMS
                                                 {
                                                   { ID=23;
                                                     ShortCutKey=F9;
                                                     CaptionML=[ENU=Mark;
                                                                ESP=Marcar];
                                                     OnPush=BEGIN
                                                              Marcar_Registros;
                                                            END;
                                                             }
                                                   { ID=26;
                                                     ShortCutKey=F5;
                                                     CaptionML=[ENU=Conciliate;
                                                                ESP=Conciliar];
                                                     OnPush=BEGIN
                                                              cFun.Conciliar_Registros('', Rec, TRUE);
                                                              Actualizar_Saldos;
                                                            END;
                                                             }
                                                   { ID=27;
                                                     ShortCutKey=Ctrl+F5;
                                                     CaptionML=[ENU=Undo conciliation;
                                                                ESP=Desconciliar];
                                                     OnPush=BEGIN
                                                              cFun.Desconciliar_Registros(Rec);
                                                              Actualizar_Saldos;
                                                            END;
                                                             }
                                                   { ID=28;
                                                     ShortCutKey=F11;
                                                     CaptionML=[ENU=Automatic conciliation;
                                                                ESP=Conciliacion automatica];
                                                     OnPush=BEGIN
                                                              cFun.Conciliacion_Automatica("G/L Account No.");
                                                              Actualizar_Saldos;
                                                            END;
                                                             }
                                                   { ID=12;
                                                     ShortCutKey=F6;
                                                     CaptionML=[ENU=Mark related ledger entries;
                                                                ESP=Marcar movs. Relacionados];
                                                     OnPush=BEGIN
                                                              Marcar_Relacionados;
                                                            END;
                                                             }
                                                   { ID=34;
                                                     CaptionML=[ENU=Satement movements;
                                                                ESP=Extracto movimientos];
                                                     OnPush=BEGIN
                                                              REPORT.RUN(REPORT::"Extracto movs. cuenta a fecha", TRUE, FALSE, Rec);
                                                            END;
                                                             }
                                                   { ID=55;
                                                     CaptionML=[ENU=View;
                                                                ESP=Ver] }
                                                   { ID=58;
                                                     ShortCutKey=Ctrl+T;
                                                     MenuLevel=1;
                                                     CaptionML=[ENU=All;
                                                                ESP=Todos];
                                                     OnPush=BEGIN
                                                              wVerRegistros := wVerRegistros::Todos;
                                                              ActualizaVista;
                                                            END;
                                                             }
                                                   { ID=59;
                                                     ShortCutKey=Ctrl+N;
                                                     MenuLevel=1;
                                                     CaptionML=[ENU=No remainings;
                                                                ESP=No pendientes];
                                                     OnPush=BEGIN
                                                              wVerRegistros := wVerRegistros::"No Pendientes";
                                                              ActualizaVista;
                                                            END;
                                                             }
                                                   { ID=60;
                                                     ShortCutKey=Ctrl+P;
                                                     MenuLevel=1;
                                                     CaptionML=[ENU=Open;
                                                                ESP=Pendientes];
                                                     OnPush=BEGIN
                                                              wVerRegistros := wVerRegistros::Pendientes;
                                                              ActualizaVista;
                                                            END;
                                                             }
                                                 }
                                                  }
    { 2   ;TableBox     ;220  ;1760 ;17710;6930 ;HorzGlue=Both;
                                                 VertGlue=Both;
                                                 InlineEditing=Yes }
    { 3   ;TextBox      ;220  ;1760 ;1640 ;440  ;Editable=No;
                                                 ParentControl=2;
                                                 InColumn=Yes;
                                                 SourceExpr="Posting Date";
                                                 OnFormat=VAR
                                                            ImpCon@1100217002 : Decimal;
                                                          BEGIN
                                                            FontBold := "Usuario conciliacion" = UPPERCASE(USERID);
                                                            CurrForm."Posting Date".UPDATEFONTBOLD(FontBold);

                                                            Color := 0;
                                                            IF "Importe pendiente" = 0 THEN
                                                              Color := 35840
                                                            ELSE IF "Importe pendiente" <> Amount THEN
                                                              Color := 16711680
                                                            ELSE IF NOT ("Usuario conciliacion" IN [UPPERCASE(USERID), '']) THEN
                                                              Color := 255;
                                                            IF Color <> 0 THEN
                                                              CurrForm."Posting Date".UPDATEFORECOLOR(Color);
                                                          END;
                                                           }
    { 6   ;Label        ;220  ;1760 ;3300 ;440  ;ParentControl=3;
                                                 InColumnHeading=Yes }
    { 7   ;TextBox      ;220  ;1760 ;550  ;440  ;Visible=No;
                                                 Editable=No;
                                                 ParentControl=2;
                                                 InColumn=Yes;
                                                 SourceExpr="Document Type";
                                                 OnFormat=BEGIN
                                                            CurrForm."Document Type".UPDATEFONTBOLD(FontBold);
                                                            IF Color <> 0 THEN
                                                              CurrForm."Document Type".UPDATEFORECOLOR(Color);
                                                          END;
                                                           }
    { 8   ;Label        ;220  ;1760 ;3300 ;440  ;ParentControl=7;
                                                 InColumnHeading=Yes }
    { 9   ;TextBox      ;220  ;1760 ;1650 ;440  ;Editable=No;
                                                 ParentControl=2;
                                                 InColumn=Yes;
                                                 SourceExpr="Document No.";
                                                 OnFormat=BEGIN
                                                            CurrForm."Document No.".UPDATEFONTBOLD(FontBold);
                                                            IF Color <> 0 THEN
                                                              CurrForm."Document No.".UPDATEFORECOLOR(Color);
                                                          END;
                                                           }
    { 11  ;Label        ;220  ;1760 ;3300 ;440  ;ParentControl=9;
                                                 InColumnHeading=Yes }
    { 61  ;TextBox      ;3096 ;1980 ;1650 ;440  ;ParentControl=2;
                                                 InColumn=Yes;
                                                 SourceExpr="External Document No." }
    { 62  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=61;
                                                 InColumnHeading=Yes }
    { 50  ;CheckBox     ;4815 ;3520 ;990  ;440  ;Editable=No;
                                                 ParentControl=2;
                                                 InColumn=Yes;
                                                 ShowCaption=No;
                                                 SourceExpr=Pendiente }
    { 51  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=50;
                                                 InColumnHeading=Yes }
    { 14  ;TextBox      ;220  ;1760 ;4400 ;440  ;HorzGlue=Both;
                                                 Editable=No;
                                                 ParentControl=2;
                                                 InColumn=Yes;
                                                 SourceExpr=Description;
                                                 OnFormat=BEGIN
                                                            CurrForm.Description.UPDATEFONTBOLD(FontBold);
                                                            IF Color <> 0 THEN
                                                              CurrForm.Description.UPDATEFORECOLOR(Color);
                                                          END;
                                                           }
    { 15  ;Label        ;220  ;1760 ;3300 ;440  ;ParentControl=14;
                                                 InColumnHeading=Yes;
                                                 ForeColor=0 }
    { 16  ;TextBox      ;10962;2530 ;2066 ;440  ;Editable=No;
                                                 ParentControl=2;
                                                 InColumn=Yes;
                                                 SourceExpr="Debit Amount";
                                                 OnFormat=BEGIN
                                                            CurrForm."Debit Amount".UPDATEFONTBOLD(FontBold);
                                                            IF Color <> 0 THEN
                                                              CurrForm."Debit Amount".UPDATEFORECOLOR(Color);
                                                          END;
                                                           }
    { 17  ;Label        ;220  ;1760 ;3300 ;440  ;ParentControl=16;
                                                 InColumnHeading=Yes }
    { 18  ;TextBox      ;12365;2530 ;2200 ;440  ;Editable=No;
                                                 ParentControl=2;
                                                 InColumn=Yes;
                                                 SourceExpr="Credit Amount";
                                                 OnFormat=BEGIN
                                                            CurrForm."Credit Amount".UPDATEFONTBOLD(FontBold);
                                                            IF Color <> 0 THEN
                                                              CurrForm."Credit Amount".UPDATEFORECOLOR(Color);
                                                          END;
                                                           }
    { 19  ;Label        ;220  ;1760 ;3300 ;440  ;ParentControl=18;
                                                 InColumnHeading=Yes }
    { 52  ;TextBox      ;5936 ;3520 ;2200 ;440  ;Editable=No;
                                                 ParentControl=2;
                                                 InColumn=Yes;
                                                 SourceExpr="Importe pendiente";
                                                 OnFormat=BEGIN
                                                            CurrForm."Importe pendiente".UPDATEFONTBOLD(FontBold);
                                                            IF Color <> 0 THEN
                                                              CurrForm."Importe pendiente".UPDATEFORECOLOR(Color);
                                                          END;

                                                 OnDrillDown=VAR
                                                               rMovCon@1100217001 : Record 7009990;
                                                             BEGIN
                                                               IF GETFILTER("Filtro fecha conciliacion") <> '' THEN BEGIN
                                                                 rMovCon.SETCURRENTKEY("Fecha conciliacion");
                                                                 COPYFILTER("Filtro fecha conciliacion", rMovCon."Fecha conciliacion");
                                                               END;
                                                               rMovCon.SETRANGE("N§ Mov.", "Entry No.");
                                                               FORM.RUNMODAL(FORM::"Movs. Conciliacion cuenta", rMovCon);
                                                             END;
                                                              }
    { 53  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=52;
                                                 InColumnHeading=Yes }
    { 31  ;TextBox      ;17232;4070 ;2375 ;440  ;ParentControl=2;
                                                 InColumn=Yes;
                                                 SourceExpr="Importe a conciliar";
                                                 OnFormat=BEGIN
                                                            CurrForm."Importe a conciliar".UPDATEFONTBOLD(FontBold);
                                                            IF Color <> 0 THEN
                                                              CurrForm."Importe a conciliar".UPDATEFORECOLOR(Color);
                                                          END;

                                                 OnBeforeInput=BEGIN
                                                                 CurrForm."Importe a conciliar".UPDATEEDITABLE(NOT cFun.Conciliado("Entry No.", Amount)
                                                                                                                    AND ("Usuario conciliacion" = UPPERCASE(USERID)));
                                                               END;

                                                 OnValidate=VAR
                                                              rMovCon@1100217001 : Record 7009990;
                                                            BEGIN
                                                              IF "Usuario conciliacion" <> UPPERCASE(USERID) THEN
                                                                ERROR('Antes de cambiar %1 debe marcar el %1 para conciliar.', FIELDNAME("Importe a conciliar"), TABLENAME);
                                                              IF cFun.Conciliado("Entry No.", Amount) THEN
                                                                ERROR('El movimiento ya est  conciliado.');

                                                              IF (("Importe a conciliar" > 0) AND (Amount < 0)) OR
                                                                 (("Importe a conciliar" < 0) AND (Amount > 0)) THEN
                                                                ERROR('%1 y %2 deben ser del mismo signo.', FIELDNAME("Importe a conciliar"), FIELDNAME(Amount));

                                                              IF ABS("Importe a conciliar") > ABS("Importe pendiente") THEN
                                                                ERROR('El %1 no puede ser mayor que el %2.', FIELDNAME("Importe a conciliar"), FIELDNAME("Importe pendiente"));

                                                              IF xRec."Importe a conciliar" <> "Importe a conciliar" THEN
                                                                SaldoLiq := SaldoLiq + "Importe a conciliar" - xRec."Importe a conciliar";
                                                            END;
                                                             }
    { 32  ;Label        ;220  ;1760 ;3300 ;440  ;ParentControl=31;
                                                 InColumnHeading=Yes }
    { 20  ;TextBox      ;12840;3080 ;2310 ;440  ;Name=Saldo;
                                                 Visible=No;
                                                 Editable=No;
                                                 ParentControl=2;
                                                 InColumn=Yes;
                                                 CaptionML=[ENU=Balance;
                                                            ESP=Saldo];
                                                 SourceExpr=MostrarSaldo;
                                                 AutoFormatType=1;
                                                 OnFormat=BEGIN
                                                            CurrForm.Saldo.UPDATEFONTBOLD(FontBold);
                                                            IF Color <> 0 THEN
                                                              CurrForm.Saldo.UPDATEFORECOLOR(Color);
                                                          END;
                                                           }
    { 21  ;Label        ;220  ;1760 ;3300 ;440  ;ParentControl=20;
                                                 InColumnHeading=Yes }
    { 37  ;TextBox      ;18571;2420 ;2420 ;440  ;Visible=No;
                                                 Editable=No;
                                                 ParentControl=2;
                                                 InColumn=Yes;
                                                 SourceExpr=Amount;
                                                 OnFormat=BEGIN
                                                            CurrForm.Amount.UPDATEFONTBOLD(FontBold);
                                                            IF Color <> 0 THEN
                                                              CurrForm.Amount.UPDATEFORECOLOR(Color);
                                                          END;
                                                           }
    { 38  ;Label        ;220  ;1760 ;3300 ;440  ;ParentControl=37;
                                                 InColumnHeading=Yes }
    { 1000000000;TextBox;24077;1980 ;2200 ;440  ;Visible=No;
                                                 Editable=No;
                                                 ParentControl=2;
                                                 InColumn=Yes;
                                                 SourceExpr="Add.-Currency Debit Amount";
                                                 OnFormat=BEGIN
                                                            CurrForm."Add.-Currency Debit Amount".UPDATEFONTBOLD(FontBold);
                                                            IF Color <> 0 THEN
                                                              CurrForm."Add.-Currency Debit Amount".UPDATEFORECOLOR(Color);
                                                          END;
                                                           }
    { 1000000001;Label  ;0    ;0    ;0    ;0    ;ParentControl=1000000000;
                                                 InColumnHeading=Yes }
    { 1000000002;TextBox;26277;1980 ;2200 ;440  ;Visible=No;
                                                 Editable=No;
                                                 ParentControl=2;
                                                 InColumn=Yes;
                                                 SourceExpr="Add.-Currency Credit Amount";
                                                 OnFormat=BEGIN
                                                            CurrForm."Add.-Currency Credit Amount".UPDATEFONTBOLD(FontBold);
                                                            IF Color <> 0 THEN
                                                              CurrForm."Add.-Currency Credit Amount".UPDATEFORECOLOR(Color);
                                                          END;
                                                           }
    { 1000000003;Label  ;0    ;0    ;0    ;0    ;ParentControl=1000000002;
                                                 InColumnHeading=Yes }
    { 39  ;TextBox      ;63508;2310 ;1650 ;440  ;Visible=No;
                                                 Editable=No;
                                                 ParentControl=2;
                                                 InColumn=Yes;
                                                 SourceExpr="Usuario conciliacion";
                                                 OnFormat=BEGIN
                                                            CurrForm."Usuario conciliacion".UPDATEFONTBOLD(FontBold);
                                                            IF Color <> 0 THEN
                                                              CurrForm."Usuario conciliacion".UPDATEFORECOLOR(Color);
                                                          END;
                                                           }
    { 40  ;Label        ;220  ;1760 ;3300 ;440  ;ParentControl=39;
                                                 InColumnHeading=Yes }
    { 29  ;TextBox      ;25083;1870 ;1650 ;440  ;ParentControl=2;
                                                 InColumn=Yes;
                                                 SourceExpr="Document Date";
                                                 OnFormat=BEGIN
                                                            CurrForm."Document Date".UPDATEFONTBOLD(FontBold);
                                                            IF Color <> 0 THEN
                                                              CurrForm."Document Date".UPDATEFORECOLOR(Color);
                                                          END;
                                                           }
    { 33  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=29;
                                                 InColumnHeading=Yes }
    { 41  ;TextBox      ;220  ;1760 ;1650 ;440  ;Visible=No;
                                                 Editable=No;
                                                 ParentControl=2;
                                                 InColumn=Yes;
                                                 SourceExpr="Entry No.";
                                                 OnFormat=BEGIN
                                                            CurrForm."Entry No.".UPDATEFONTBOLD(FontBold);
                                                            IF Color <> 0 THEN
                                                              CurrForm."Entry No.".UPDATEFORECOLOR(Color);
                                                          END;
                                                           }
    { 42  ;Label        ;220  ;1760 ;3300 ;440  ;ParentControl=41;
                                                 InColumnHeading=Yes }
    { 1100244002;TextBox;29314;2750 ;2530 ;440  ;Editable=No;
                                                 ParentControl=2;
                                                 InColumn=Yes;
                                                 SourceExpr="Source No." }
    { 1100244003;Label  ;0    ;0    ;0    ;0    ;ParentControl=1100244002;
                                                 InColumnHeading=Yes }
    { 1100244000;TextBox;31007;2750 ;1540 ;440  ;Editable=No;
                                                 ParentControl=2;
                                                 InColumn=Yes;
                                                 SourceExpr="Source Type" }
    { 1100244001;Label  ;0    ;0    ;0    ;0    ;ParentControl=1100244000;
                                                 InColumnHeading=Yes }
    { 35  ;TextBox      ;27674;5170 ;2061 ;440  ;Name=Liq. por n§ documento;
                                                 ParentControl=2;
                                                 InColumn=Yes;
                                                 CaptionML=[ENU=Settle by document No.;
                                                            ESP=Liq. por n§ documento];
                                                 SourceExpr=Liq_por_Num_Doc;
                                                 OnFormat=BEGIN
                                                            CurrForm."Liq. por n§ documento".UPDATEFONTBOLD(FontBold);
                                                            IF Color <> 0 THEN
                                                              CurrForm."Liq. por n§ documento".UPDATEFORECOLOR(Color);
                                                          END;

                                                 OnLookup=VAR
                                                            rHisCabAlbComp@1100217002 : Record 120;
                                                            rHisCabFacComp@1100217003 : Record 122;
                                                            rHisCabAboComp@1100217004 : Record 124;
                                                            rMovProv@1100217005 : Record 25;
                                                          BEGIN
                                                            IF rHisCabAlbComp.GET("Document No.") THEN
                                                              CASE rHisCabAlbComp."Applies-to Doc. Type" OF
                                                                rHisCabAlbComp."Applies-to Doc. Type"::Invoice: BEGIN
                                                                  IF rHisCabFacComp.GET(rHisCabAlbComp."Applies-to Doc. No.") THEN
                                                                    FORM.RUNMODAL(FORM::"Posted Purchase Invoice", rHisCabFacComp)
                                                                  ELSE BEGIN
                                                                    rHisCabFacComp.SETCURRENTKEY("Pre-Assigned No.");
                                                                    rHisCabFacComp.SETRANGE("Pre-Assigned No.", rHisCabAlbComp."Applies-to Doc. No.");
                                                                    IF rHisCabFacComp.FIND('-') THEN
                                                                      FORM.RUNMODAL(FORM::"Posted Purchase Invoice", rHisCabFacComp)
                                                                    ELSE BEGIN
                                                                      rMovProv.SETCURRENTKEY("Document Type", "Document No.", "Vendor No.");
                                                                      rMovProv.SETRANGE("Document Type", rMovProv."Document Type"::Invoice);
                                                                      rMovProv.SETRANGE("Document No."  , rHisCabAlbComp."Applies-to Doc. No.");
                                                                      rMovProv.SETRANGE("Vendor No."  , rHisCabAlbComp."Buy-from Vendor No.");
                                                                      IF rMovProv.FIND('-') THEN
                                                                        FORM.RUNMODAL(0, rMovProv)
                                                                      ELSE
                                                                        ERROR(Text050, rHisCabFacComp.TABLECAPTION, rMovProv.TABLECAPTION, rHisCabAlbComp."Applies-to Doc. Type",
                                                                          rHisCabAlbComp."Applies-to Doc. No.");
                                                                    END;
                                                                  END;
                                                                END;
                                                                rHisCabAlbComp."Applies-to Doc. Type"::"Credit Memo": BEGIN
                                                                  IF rHisCabAboComp.GET(rHisCabAlbComp."Applies-to Doc. No.") THEN
                                                                    FORM.RUNMODAL(FORM::"Posted Purchase Credit Memo", rHisCabAboComp)
                                                                  ELSE BEGIN
                                                                    rHisCabAboComp.SETCURRENTKEY("Pre-Assigned No.");
                                                                    rHisCabAboComp.SETRANGE("Pre-Assigned No.", rHisCabAlbComp."Applies-to Doc. No.");
                                                                    IF rHisCabAboComp.FIND('-') THEN
                                                                      FORM.RUNMODAL(FORM::"Posted Purchase Credit Memo", rHisCabAboComp)
                                                                    ELSE BEGIN
                                                                      rMovProv.SETCURRENTKEY("Document Type", "Document No.", "Vendor No.");
                                                                      rMovProv.SETRANGE("Document Type", rMovProv."Document Type"::"Credit Memo");
                                                                      rMovProv.SETRANGE("Document No."  , rHisCabAlbComp."Applies-to Doc. No.");
                                                                      rMovProv.SETRANGE("Vendor No."  , rHisCabAlbComp."Buy-from Vendor No.");
                                                                      IF rMovProv.FIND('-') THEN
                                                                        FORM.RUNMODAL(0, rMovProv)
                                                                      ELSE
                                                                        ERROR(Text050, rHisCabAboComp.TABLECAPTION, rMovProv.TABLECAPTION, rHisCabAlbComp."Applies-to Doc. Type",
                                                                          rHisCabAlbComp."Applies-to Doc. No.");
                                                                    END;
                                                                  END;
                                                                END;
                                                              END;
                                                          END;
                                                           }
    { 36  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=35;
                                                 InColumnHeading=Yes }
    { 25  ;Label        ;8140 ;9020 ;2420 ;440  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 ParentControl=30 }
    { 30  ;TextBox      ;10670;9020 ;2200 ;440  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 Editable=No;
                                                 CaptionML=[ENU=reconciled balance;
                                                            ESP=Saldo conciliado];
                                                 SourceExpr=SaldoCon }
    { 43  ;TextBox      ;10670;9570 ;2200 ;440  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 Editable=No;
                                                 CaptionML=[ENU=outstanding balance;
                                                            ESP=Saldo pendiente];
                                                 SourceExpr=SaldoPen }
    { 44  ;Label        ;8140 ;9570 ;2420 ;440  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 ParentControl=43 }
    { 45  ;Label        ;12980;9020 ;2420 ;440  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 ParentControl=47 }
    { 46  ;Label        ;12980;9570 ;2420 ;440  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 ParentControl=48 }
    { 47  ;TextBox      ;15510;9020 ;2200 ;440  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 Editable=No;
                                                 CaptionML=[ENU=account balance;
                                                            ESP=Saldo Cuenta];
                                                 SourceExpr=SaldoCta }
    { 48  ;TextBox      ;15510;9570 ;2200 ;440  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 Editable=No;
                                                 CaptionML=[ENU=marked balance;
                                                            ESP=Saldo Marcado];
                                                 SourceExpr=SaldoLiq }
    { 13  ;CommandButton;10890;10450;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 CaptionML=[ENU=&Navigate;
                                                            ESP=&Navegar];
                                                 OnPush=BEGIN
                                                          Navegar.SetDoc("Posting Date","Document No.");
                                                          Navegar.RUN;
                                                        END;
                                                         }
    { 54  ;OptionButton ;770  ;8800 ;550  ;550  ;VertGlue=Bottom;
                                                 FocusOnClick=No;
                                                 HorzAlign=Center;
                                                 Border=Yes;
                                                 FontBold=Yes;
                                                 BitmapPos=None;
                                                 CaptionML=[ENU=N;
                                                            ESP=N];
                                                 ToolTipML=[ENU=Display not remainings;
                                                            ESP=Ver no pendientes];
                                                 SourceExpr=wVerRegistros;
                                                 OptionValue=No Pendientes;
                                                 OnPush=BEGIN
                                                          ActualizaVista;
                                                        END;
                                                         }
    { 56  ;OptionButton ;1320 ;8800 ;550  ;550  ;VertGlue=Bottom;
                                                 FocusOnClick=No;
                                                 HorzAlign=Center;
                                                 Border=Yes;
                                                 FontBold=Yes;
                                                 BitmapPos=None;
                                                 CaptionML=[ENU=P;
                                                            ESP=P];
                                                 ToolTipML=[ENU=Display remainings;
                                                            ESP=Ver pendientes];
                                                 SourceExpr=wVerRegistros;
                                                 OptionValue=Pendientes;
                                                 OnPush=BEGIN
                                                          ActualizaVista;
                                                        END;
                                                         }
    { 57  ;OptionButton ;220  ;8800 ;550  ;550  ;VertGlue=Bottom;
                                                 FocusOnClick=No;
                                                 HorzAlign=Center;
                                                 Border=Yes;
                                                 FontBold=Yes;
                                                 BitmapPos=None;
                                                 CaptionML=[ENU=T;
                                                            ESP=T];
                                                 ToolTipML=[ENU=display all;
                                                            ESP=Ver todos];
                                                 SourceExpr=wVerRegistros;
                                                 OptionValue=Todos;
                                                 OnPush=BEGIN
                                                          ActualizaVista;
                                                        END;
                                                         }
  }
  CODE
  {
    VAR
      Navegar@1100217000 : Form 344;
      SaldoCta@1100217001 : Decimal;
      SaldoLiq@1100217002 : Decimal;
      SaldoCon@1100217003 : Decimal;
      SaldoPen@1100217004 : Decimal;
      cFun@1100217005 : Codeunit 7009907;
      Color@1100217006 : Integer;
      FontBold@1100217007 : Boolean;
      "Importe a conciliar"@1100217008 : Decimal;
      Text050@1100217009 : TextConst 'ENU=%1 and %2 do not exist for %3 %4;ESP=No existe %1 ni %2 para %3 %4.';
      wVerRegistros@1100217010 : 'Todos,No Pendientes,Pendientes';

    PROCEDURE Actualizar_Saldos@6();
    VAR
      rMov@1100217000 : Record 17;
      rMovCon@1100217001 : Record 7009990;
    BEGIN
      // Actualizar_Saldos
      //
      SaldoCta := 0;
      SaldoLiq := 0;
      SaldoCon := 0;
      SaldoPen := 0;

      rMov.RESET;
      rMov.SETCURRENTKEY("G/L Account No.");
      rMov.SETRANGE("G/L Account No.", "G/L Account No.");
      IF rMov.FIND('-') THEN BEGIN
        rMov.CALCSUMS(Amount);
        SaldoCta := rMov.Amount;
      END;

      rMov.RESET;
      rMov.SETCURRENTKEY("G/L Account No.", "Usuario conciliacion");
      rMov.SETRANGE("G/L Account No." , "G/L Account No.");
      rMov.SETRANGE("Usuario conciliacion", UPPERCASE(USERID));
      IF rMov.FIND('-') THEN BEGIN
        rMov.CALCSUMS("Importe a conciliar");
        SaldoLiq := rMov."Importe a conciliar";
      END;

      rMovCon.RESET;
      rMovCon.SETCURRENTKEY("N§ cuenta");
      rMovCon.SETRANGE("N§ cuenta"  , "G/L Account No.");
      IF rMovCon.FIND('-') THEN BEGIN
        rMovCon.CALCSUMS("Importe conciliado");
        SaldoCon := rMovCon."Importe conciliado";
      END;

      SaldoPen := SaldoCta - SaldoCon;
    END;

    PROCEDURE Nom_Cuenta@4(par_Cta@1100217000 : Code[20]) : Text[50];
    VAR
      rCta@1100217001 : Record 15;
    BEGIN
      IF rCta.GET(par_Cta) THEN
        EXIT(rCta.Name)
      ELSE
        EXIT('');
    END;

    PROCEDURE Marcar_Registros@7();
    VAR
      rMov@1100217000 : Record 17;
      rMovCon@1100217001 : Record 7009990;
    BEGIN
      // Marcar_Registros
      //
      CurrForm.SETSELECTIONFILTER(rMov);
      IF rMov.FIND('-') THEN
        REPEAT
          rMovCon.RESET;
          rMovCon.SETRANGE("N§ Mov.", rMov."Entry No.");
          rMovCon.CALCSUMS("Importe conciliado");
          IF rMovCon."Importe conciliado" <> rMov.Amount THEN
            CASE rMov."Usuario conciliacion" OF
              UPPERCASE(USERID): BEGIN
                rMov."Usuario conciliacion" := '';
                rMov."Importe a conciliar" := 0;
                rMov.MODIFY;
              END;
              ''    : BEGIN
                rMov."Usuario conciliacion" := USERID;
                // PLB 21/03/2005
                // teoricamente no haria falta redondear, pero algunos clientes han creado en contabilidad
                // mas decimales de los que toca, y, al volver a poner los decimales como debe ser, no pueden
                // conciliar por problemas de redondeo
                rMov."Importe a conciliar" := ROUND(rMov.Amount - rMovCon."Importe conciliado");
                rMov.MODIFY;
              END;
            END;
        UNTIL rMov.NEXT = 0;

      Actualizar_Saldos;
    END;

    PROCEDURE MostrarSaldo@13() : Decimal;
    VAR
      rCta@1100217000 : Record 15;
      rMov@1100217001 : Record 17;
      Saldo@1100217002 : Decimal;
    BEGIN
      // MostrarSaldo
      //
      Saldo := 0;

      rMov.RESET;
      rMov.SETCURRENTKEY("G/L Account No.", "Posting Date");
      rMov.SETRANGE ("G/L Account No."     , "G/L Account No.");
      rMov.SETFILTER("Posting Date", '<%1', "Posting Date");
      IF rMov.FIND('-') THEN BEGIN
        rMov.CALCSUMS(Amount);
        Saldo := rMov.Amount;
      END;

      rMov.RESET;
      rMov.SETCURRENTKEY("G/L Account No.", "Posting Date");
      rMov.SETRANGE("G/L Account No."     , "G/L Account No.");
      rMov.SETRANGE("Posting Date", "Posting Date");
      IF rMov.FIND('-') THEN
        REPEAT
          IF rMov."Entry No." <= "Entry No." THEN BEGIN
            Saldo := Saldo + rMov.Amount;
          END;
        UNTIL rMov.NEXT = 0;

      EXIT(Saldo);
    END;

    PROCEDURE Marcar_Relacionados@1();
    VAR
      rMov@1100217000 : Record 17;
      rMovCon@1100217001 : Record 7009990;
      rMovCon2@1100217002 : Record 7009990;
    BEGIN
      // Marcar_Relacionados
      //
      IF MARK = TRUE THEN BEGIN
        CLEARMARKS;
        EXIT;
      END;

      IF "Importe pendiente" <> Amount THEN BEGIN
        CLEARMARKS;
        rMov := Rec;
        rMovCon.SETRANGE("N§ Mov.", "Entry No.");
        IF rMovCon.FIND('-') THEN
          REPEAT
            rMovCon2.SETRANGE("N§ Conciliacion", rMovCon."N§ Conciliacion");
            IF rMovCon2.FIND('-') THEN
              REPEAT
                GET(rMovCon2."N§ Mov.");
                MARK(TRUE);
              UNTIL rMovCon2.NEXT = 0;
          UNTIL rMovCon.NEXT = 0;
        Rec := rMov;
      END;
    END;

    PROCEDURE Liq_por_Num_Doc@2() : Code[20];
    VAR
      rHisCabAlbComp@1100217000 : Record 120;
    BEGIN
      IF rHisCabAlbComp.GET("Document No.") THEN
        EXIT(rHisCabAlbComp."Applies-to Doc. No.");
    END;

    PROCEDURE ActualizaVista@3();
    BEGIN
      CASE wVerRegistros OF
        wVerRegistros::Todos: BEGIN
          SETCURRENTKEY("G/L Account No.", "Posting Date");
          SETRANGE(Pendiente);
        END;
        wVerRegistros::"No Pendientes": BEGIN
          SETCURRENTKEY("Posting Date", "G/L Account No.", Pendiente);
          SETRANGE(Pendiente, FALSE);
        END;
        wVerRegistros::Pendientes: BEGIN
          SETCURRENTKEY("Posting Date", "G/L Account No.", Pendiente);
          SETRANGE(Pendiente, TRUE);
        END;
      END;

      CurrForm.UPDATE(FALSE);
    END;

    BEGIN
    {
      // PLB 21/04/2004
      Modificaciones Barcelo:
        - A¤adidos controles "... procedencia mov."

      // PLB 19/07/2004
      Modificaciones Barcelo:
        - Suprimidos controles duplicados "... procedencia mov."
        - A¤adidos controles "... divisa-adicional"

      // PLB 21/03/2005
      // Redondear al marcar movimiento para conciliar
      Teoricamente no haria falta redondear, pero algunos clientes han creado en contabilidad
      mas decimales de los que toca, y, al volver a poner los decimales como debe ser, no pueden
      conciliar por problemas de redondeo
    }
    END.
  }
}
