OBJECT Form 343 Check Credit Limit
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    Width=11110;
    Height=8250;
    Editable=No;
    CaptionML=[ENU=Check Credit Limit;
               ESP=Comprobar l¡mite de cr‚dito];
    Minimizable=No;
    Sizeable=No;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    LinksAllowed=No;
    SourceTable=Table18;
    DataCaptionExpr='';
    OnOpenForm=BEGIN
                 COPY(Cust2);
                 CurrForm.No.ACTIVATE;
               END;

    OnAfterGetRecord=BEGIN
                       CalcCreditLimitLCY;
                       CalcOverdueBalanceLCY;
                     END;

  }
  CONTROLS
  {
    { 1   ;Frame        ;220  ;1320 ;10670;5940 ;HorzGlue=Both;
                                                 VertGlue=Both;
                                                 ShowCaption=No }
    { 5   ;TextBox      ;5170 ;1540 ;2750 ;440  ;ParentControl=1;
                                                 InFrame=Yes;
                                                 SourceExpr="No." }
    { 4   ;Label        ;1760 ;1540 ;3300 ;440  ;ParentControl=5 }
    { 7   ;TextBox      ;5170 ;2090 ;5500 ;440  ;ParentControl=1;
                                                 InFrame=Yes;
                                                 SourceExpr=Name }
    { 6   ;Label        ;1760 ;2090 ;3300 ;440  ;ParentControl=7 }
    { 13  ;TextBox      ;5170 ;3740 ;2750 ;440  ;ParentControl=1;
                                                 InFrame=Yes;
                                                 CaptionML=[ENU=Shipped/Ret. Rcd. Not Invd. (LCY);
                                                            ESP=Enviado/Dev. recib. no fact. (DL)];
                                                 SourceExpr=ShippedRetRcdNotIndLCY }
    { 12  ;Label        ;1760 ;3740 ;3300 ;440  ;ParentControl=13 }
    { 15  ;TextBox      ;5170 ;4290 ;2750 ;440  ;ParentControl=1;
                                                 InFrame=Yes;
                                                 CaptionML=[ENU=Current Amount (LCY);
                                                            ESP=Imp. doc. actual (DL)];
                                                 SourceExpr=OrderAmountThisOrderLCY;
                                                 AutoFormatType=1 }
    { 14  ;Label        ;1760 ;4290 ;3300 ;440  ;ParentControl=15 }
    { 17  ;TextBox      ;5170 ;4950 ;2750 ;440  ;ParentControl=1;
                                                 InFrame=Yes;
                                                 CaptionML=[ENU=Total Amount (LCY);
                                                            ESP=Importe total (DL)];
                                                 SourceExpr=CustCreditAmountLCY;
                                                 AutoFormatType=1 }
    { 16  ;Label        ;1760 ;4950 ;3300 ;440  ;ParentControl=17 }
    { 19  ;TextBox      ;5170 ;5500 ;2750 ;440  ;ParentControl=1;
                                                 InFrame=Yes;
                                                 SourceExpr="Credit Limit (LCY)" }
    { 18  ;Label        ;1760 ;5500 ;3300 ;440  ;ParentControl=19 }
    { 21  ;TextBox      ;5170 ;6490 ;2750 ;440  ;ParentControl=1;
                                                 InFrame=Yes;
                                                 SourceExpr="Balance Due (LCY)" }
    { 20  ;TextBox      ;1760 ;6160 ;3300 ;770  ;Focusable=No;
                                                 ParentControl=21;
                                                 VertAlign=Bottom;
                                                 BackTransparent=Yes;
                                                 Border=No;
                                                 MultiLine=Yes;
                                                 LeaderDots=Yes;
                                                 SourceExpr=STRSUBSTNO(Text001,FORMAT(GETRANGEMAX("Date Filter"))) }
    { 3   ;TextBox      ;5170 ;2640 ;2750 ;440  ;ParentControl=1;
                                                 InFrame=Yes;
                                                 SourceExpr="Balance (LCY)" }
    { 8   ;Label        ;1760 ;2640 ;3300 ;440  ;ParentControl=3 }
    { 31  ;TextBox      ;5170 ;3190 ;2750 ;440  ;ParentControl=1;
                                                 InFrame=Yes;
                                                 CaptionML=[ENU=Outstanding Amt. (LCY);
                                                            ESP=Importes pendientes (DL)];
                                                 SourceExpr=OrderAmountTotalLCY;
                                                 AutoFormatType=1 }
    { 32  ;Label        ;1760 ;3190 ;3300 ;440  ;ParentControl=31 }
    { 22  ;CommandButton;1430 ;7480 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 PushAction=Yes }
    { 23  ;CommandButton;3850 ;7480 ;2200 ;550  ;Name=No;
                                                 HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 PushAction=No }
    { 24  ;MenuButton   ;6270 ;7480 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 CaptionML=[ENU=&Customer;
                                                            ESP=&Cliente];
                                                 Menu=MENUITEMS
                                                 {
                                                   { ID=25;
                                                     PushAction=RunObject;
                                                     ShortCutKey=May£s+F5;
                                                     CaptionML=[ENU=Card;
                                                                ESP=Ficha];
                                                     RunObject=Form 21;
                                                     RunFormLinkType=OnUpdate;
                                                     RunFormLink=No.=FIELD(No.),
                                                                 Date Filter=FIELD(Date Filter),
                                                                 Global Dimension 1 Filter=FIELD(Global Dimension 1 Filter),
                                                                 Global Dimension 2 Filter=FIELD(Global Dimension 2 Filter) }
                                                   { ID=26;
                                                     PushAction=RunObject;
                                                     ShortCutKey=F9;
                                                     CaptionML=[ENU=Statistics;
                                                                ESP=Estad¡sticas];
                                                     RunObject=Form 151;
                                                     RunFormLinkType=OnUpdate;
                                                     RunFormLink=No.=FIELD(No.),
                                                                 Date Filter=FIELD(Date Filter),
                                                                 Global Dimension 1 Filter=FIELD(Global Dimension 1 Filter),
                                                                 Global Dimension 2 Filter=FIELD(Global Dimension 2 Filter) }
                                                 }
                                                  }
    { 27  ;CommandButton;8690 ;7480 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 PushAction=FormHelp }
    { 2   ;TextBox      ;1760 ;220  ;8910 ;770  ;Focusable=No;
                                                 BackTransparent=Yes;
                                                 Border=No;
                                                 MultiLine=Yes;
                                                 SourceExpr=STRSUBSTNO(Text000,Heading) }
    { 28  ;Image        ;440  ;220  ;1100 ;1100 ;Bitmap=5 }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 Do you still want to record the amount?;ESP=%1 ¨Confirma que desea continuar a pesar de ello?';
      Text001@1001 : TextConst 'ENU=Overdue Amounts (LCY) as of %1;ESP=Importes vencidos (DL) a %1';
      Text002@1002 : TextConst 'ENU=The customer''s credit limit has been exceeded.;ESP=El cliente ha superado el cr‚dito m ximo.';
      Text003@1003 : TextConst 'ENU=This customer has an overdue balance.;ESP=El cliente tiene saldos vencidos.';
      CurrExchRate@1004 : Record 330;
      SalesHeader@1005 : Record 36;
      SalesLine@1006 : Record 37;
      ServHeader@1021 : Record 5900;
      ServLine@1020 : Record 5902;
      Cust2@1007 : Record 18;
      SalesSetup@1008 : Record 311;
      CustNo@1009 : Code[20];
      Heading@1010 : Text[250];
      NewOrderAmountLCY@1011 : Decimal;
      OldOrderAmountLCY@1012 : Decimal;
      OrderAmountThisOrderLCY@1013 : Decimal;
      OrderAmountTotalLCY@1014 : Decimal;
      CustCreditAmountLCY@1015 : Decimal;
      ShippedRetRcdNotIndLCY@1016 : Decimal;
      OutstandingRetOrdersLCY@1017 : Decimal;
      RcdNotInvdRetOrdersLCY@1018 : Decimal;
      Text004@1019 : TextConst 'ENU=This customer has an overdue balance and the customer''s credit limit has been exceeded.;ESP=El cliente tiene saldos vencidos y se ha superado su l¡mite de cr‚dito.';

    PROCEDURE GenJnlLineShowWarning@2(GenJnlLine@1000 : Record 81) : Boolean;
    BEGIN
      SalesSetup.GET;
      IF SalesSetup."Credit Warnings" =
         SalesSetup."Credit Warnings"::"No Warning"
      THEN
        EXIT(FALSE);
      IF GenJnlLine."Account Type" = GenJnlLine."Account Type"::Customer THEN
        EXIT(ShowWarning(GenJnlLine."Account No.",GenJnlLine."Amount (LCY)",0,TRUE));
      EXIT(ShowWarning(GenJnlLine."Bal. Account No.",-GenJnlLine.Amount,0,TRUE));
    END;

    PROCEDURE SalesHeaderShowWarning@3(SalesHeader@1000 : Record 36) : Boolean;
    BEGIN
      // Used when additional lines are inserted
      SalesSetup.GET;
      IF SalesSetup."Credit Warnings" =
         SalesSetup."Credit Warnings"::"No Warning"
      THEN
        EXIT(FALSE);
      IF SalesHeader."Currency Code" = '' THEN
        NewOrderAmountLCY := SalesHeader."Amount Including VAT"
      ELSE
        NewOrderAmountLCY :=
          ROUND(
            CurrExchRate.ExchangeAmtFCYToLCY(
              WORKDATE,SalesHeader."Currency Code",
              SalesHeader."Amount Including VAT",SalesHeader."Currency Factor"));

      IF NOT (SalesHeader."Document Type" IN
              [SalesHeader."Document Type"::Quote,
               SalesHeader."Document Type"::Order,
               SalesHeader."Document Type"::"Return Order"])
      THEN
        NewOrderAmountLCY := NewOrderAmountLCY + SalesLineAmount(SalesHeader."Document Type",SalesHeader."No.");
      EXIT(ShowWarning(SalesHeader."Bill-to Customer No.",NewOrderAmountLCY,0,TRUE));
    END;

    PROCEDURE SalesLineShowWarning@4(SalesLine@1000 : Record 37) : Boolean;
    BEGIN
      SalesSetup.GET;
      IF SalesSetup."Credit Warnings" =
         SalesSetup."Credit Warnings"::"No Warning"
      THEN
        EXIT(FALSE);
      IF (SalesHeader."Document Type" <> SalesLine."Document Type") OR
         (SalesHeader."No." <> SalesLine."Document No.")
      THEN
        SalesHeader.GET(SalesLine."Document Type",SalesLine."Document No.");
      NewOrderAmountLCY := SalesLine."Outstanding Amount (LCY)" + SalesLine."Shipped Not Invoiced (LCY)";

      IF SalesLine.FIND THEN
        OldOrderAmountLCY := SalesLine."Outstanding Amount (LCY)" + SalesLine."Shipped Not Invoiced (LCY)"
      ELSE
        OldOrderAmountLCY := 0;

      IF SalesLine."Document Type" <> SalesLine."Document Type"::Order THEN BEGIN
        NewOrderAmountLCY :=
          NewOrderAmountLCY - OldOrderAmountLCY +
          SalesLineAmount(SalesLine."Document Type",SalesLine."Document No.");
        OldOrderAmountLCY := 0;
      END;
      EXIT(ShowWarning(SalesHeader."Bill-to Customer No.",NewOrderAmountLCY,OldOrderAmountLCY,FALSE))
    END;

    PROCEDURE ServiceHeaderShowWarning@8(ServHeader@1000 : Record 5900) : Boolean;
    VAR
      ServSetup@1002 : Record 5911;
    BEGIN
      ServSetup.GET;
      SalesSetup.GET;
      IF SalesSetup."Credit Warnings" =
         SalesSetup."Credit Warnings"::"No Warning"
      THEN
        EXIT(FALSE);

      NewOrderAmountLCY := 0;
      ServLine.RESET;
      ServLine.SETRANGE("Document Type",ServHeader."Document Type");
      ServLine.SETRANGE("Document No.",ServHeader."No.");
      IF ServLine.FINDSET THEN
        REPEAT
          IF ServHeader."Currency Code" = '' THEN
            NewOrderAmountLCY := NewOrderAmountLCY + ServLine."Amount Including VAT"
          ELSE
            NewOrderAmountLCY := NewOrderAmountLCY +
              ROUND(
                CurrExchRate.ExchangeAmtFCYToLCY(
                  WORKDATE,ServHeader."Currency Code",
                  ServLine."Amount Including VAT",ServHeader."Currency Factor"));
        UNTIL ServLine.NEXT = 0;

      IF ServHeader."Document Type" <> ServHeader."Document Type"::Order THEN
        NewOrderAmountLCY := NewOrderAmountLCY + ServLineAmount(ServHeader."Document Type",ServHeader."No.",ServLine);

      EXIT(ShowWarning(ServHeader."Bill-to Customer No.",NewOrderAmountLCY,0,TRUE));
    END;

    PROCEDURE ServiceLineShowWarning@10(ServLine@1000 : Record 5902) : Boolean;
    BEGIN
      SalesSetup.GET;
      IF SalesSetup."Credit Warnings" =
         SalesSetup."Credit Warnings"::"No Warning"
      THEN
        EXIT(FALSE);
      IF (ServHeader."Document Type" <> ServLine."Document Type") OR
         (ServHeader."No." <> ServLine."Document No.")
      THEN
        ServHeader.GET(ServLine."Document Type",ServLine."Document No.");
      NewOrderAmountLCY := ServLine."Outstanding Amount (LCY)" + ServLine."Shipped Not Invoiced (LCY)";

      IF ServLine.FIND THEN
        OldOrderAmountLCY := ServLine."Outstanding Amount (LCY)" + ServLine."Shipped Not Invoiced (LCY)"
      ELSE
        OldOrderAmountLCY := 0;

      IF ServLine."Document Type" <> ServLine."Document Type"::Order THEN BEGIN
        NewOrderAmountLCY :=
          NewOrderAmountLCY - OldOrderAmountLCY +
          ServLineAmount(ServLine."Document Type",ServLine."Document No.",ServLine);
        OldOrderAmountLCY := 0;
      END;
      EXIT(ShowWarning(ServHeader."Bill-to Customer No.",NewOrderAmountLCY,OldOrderAmountLCY,FALSE))
    END;

    LOCAL PROCEDURE SalesLineAmount@5(DocType@1000 : Integer;DocNo@1001 : Code[20]) : Decimal;
    BEGIN
      SalesLine.RESET;
      SalesLine.SETRANGE("Document Type",DocType);
      SalesLine.SETRANGE("Document No.",DocNo);
      SalesLine.CALCSUMS("Outstanding Amount (LCY)","Shipped Not Invoiced (LCY)");
      EXIT(SalesLine."Outstanding Amount (LCY)" + SalesLine."Shipped Not Invoiced (LCY)");
    END;

    LOCAL PROCEDURE ServLineAmount@12(DocType@1000 : Integer;DocNo@1001 : Code[20];VAR ServLine2@1002 : Record 5902) : Decimal;
    BEGIN
      ServLine2.RESET;
      ServLine2.SETRANGE("Document Type",DocType);
      ServLine2.SETRANGE("Document No.",DocNo);
      ServLine2.CALCSUMS("Outstanding Amount (LCY)","Shipped Not Invoiced (LCY)");
      EXIT(ServLine2."Outstanding Amount (LCY)" + ServLine2."Shipped Not Invoiced (LCY)");
    END;

    LOCAL PROCEDURE ShowWarning@1(NewCustNo@1000 : Code[20];NewOrderAmountLCY2@1001 : Decimal;OldOrderAmountLCY2@1002 : Decimal;CheckOverDueBalance@1003 : Boolean) : Boolean;
    VAR
      ExitValue@1004 : Integer;
    BEGIN
      IF NewCustNo = '' THEN
        EXIT;
      CustNo := NewCustNo;
      NewOrderAmountLCY := NewOrderAmountLCY2;
      OldOrderAmountLCY := OldOrderAmountLCY2;
      GET(CustNo);
      SETRANGE("No.","No.");
      Cust2.COPY(Rec);

      IF SalesSetup."Credit Warnings" IN
         [SalesSetup."Credit Warnings"::"Both Warnings",
          SalesSetup."Credit Warnings"::"Credit Limit"]
      THEN BEGIN
        CalcCreditLimitLCY;
        IF (CustCreditAmountLCY > "Credit Limit (LCY)") AND ("Credit Limit (LCY)" <> 0) THEN BEGIN
          ExitValue := 1;
        END;
      END;
      IF CheckOverDueBalance AND
         (SalesSetup."Credit Warnings" IN
          [SalesSetup."Credit Warnings"::"Both Warnings",
           SalesSetup."Credit Warnings"::"Overdue Balance"])
      THEN BEGIN
        CalcOverdueBalanceLCY;
        IF "Balance Due (LCY)" > 0 THEN BEGIN
          ExitValue := ExitValue + 2;
        END;
      END;

      IF ExitValue > 0 THEN BEGIN
        CASE ExitValue OF
          1:
            Heading := Text002;
          2:
            Heading := Text003;
          3:
            Heading := Text004;
        END;
        EXIT(TRUE);
      END;
    END;

    LOCAL PROCEDURE CalcCreditLimitLCY@6();
    BEGIN
      IF GETFILTER("Date Filter") = '' THEN
        SETFILTER("Date Filter",'..%1',WORKDATE);
      CALCFIELDS("Balance (LCY)","Outstanding Orders (LCY)","Shipped Not Invoiced (LCY)");
      CalcReturnAmounts(OutstandingRetOrdersLCY,RcdNotInvdRetOrdersLCY);
      OrderAmountTotalLCY := "Outstanding Orders (LCY)" - OutstandingRetOrdersLCY;
      ShippedRetRcdNotIndLCY := "Shipped Not Invoiced (LCY)" - RcdNotInvdRetOrdersLCY;
      IF "No." = CustNo THEN BEGIN
        OrderAmountThisOrderLCY := NewOrderAmountLCY;
        OrderAmountTotalLCY := OrderAmountTotalLCY - OldOrderAmountLCY;
      END ELSE
        OrderAmountThisOrderLCY := 0;
      CustCreditAmountLCY := "Balance (LCY)" +
        ("Shipped Not Invoiced (LCY)" - RcdNotInvdRetOrdersLCY) +
        OrderAmountTotalLCY + OrderAmountThisOrderLCY;
    END;

    LOCAL PROCEDURE CalcOverdueBalanceLCY@7();
    BEGIN
      IF GETFILTER("Date Filter") = '' THEN
        SETFILTER("Date Filter",'..%1',WORKDATE);
      CALCFIELDS("Balance Due (LCY)");
    END;

    PROCEDURE CalcReturnAmounts@9(VAR OutstandingRetOrdersLCY2@1001 : Decimal;VAR RcdNotInvdRetOrdersLCY2@1002 : Decimal) : Decimal;
    BEGIN
      SalesLine.RESET;
      SalesLine.SETCURRENTKEY("Document Type","Bill-to Customer No.","Currency Code");
      SalesLine.SETRANGE("Document Type",SalesLine."Document Type"::"Return Order");
      SalesLine.SETRANGE("Bill-to Customer No.","No.");
      SalesLine.CALCSUMS("Outstanding Amount (LCY)","Return Rcd. Not Invd. (LCY)");
      OutstandingRetOrdersLCY2 := SalesLine."Outstanding Amount (LCY)";
      RcdNotInvdRetOrdersLCY2 := SalesLine."Return Rcd. Not Invd. (LCY)";
    END;

    BEGIN
    END.
  }
}
