OBJECT Form 7010504 Control impresion conductores
{
  OBJECT-PROPERTIES
  {
    Date=04/06/15;
    Time=[ 9:02:40];
    Modified=Yes;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    Width=19140;
    Height=9680;
    CaptionML=ESP=Control impresion conductores;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    TableBoxID=1103355000;
    SourceTable=Table7010399;
    SourceTableView=SORTING(Dia Siguiente,Hora Inicio);
    OnOpenForm=BEGIN

                 ConfigporMaquinaUser;
               END;

    OnFindRecord=BEGIN
                   EXIT(FindRec(Which, rImpCondTMP));
                 END;

    OnNextRecord=BEGIN
                   EXIT(NextRec(Steps, rImpCondTMP));
                 END;

  }
  CONTROLS
  {
    { 1103355000;TableBox;220 ;1870 ;18700;6930 ;HorzGlue=Both;
                                                 VertGlue=Both;
                                                 HeadingHeight=880 }
    { 1103355042;CheckBox;0   ;4730 ;990  ;440  ;Editable=No;
                                                 ParentControl=1103355000;
                                                 InColumn=Yes;
                                                 ShowCaption=No;
                                                 SourceExpr=Seleccion;
                                                 OnPush=BEGIN

                                                          CambiaSeleccion(Rec, rImpCondTMP);
                                                          CurrForm.UPDATE;
                                                        END;
                                                         }
    { 1103355043;Label  ;0    ;0    ;0    ;0    ;ParentControl=1103355042;
                                                 InColumnHeading=Yes }
    { 1103355001;TextBox;0    ;0    ;1700 ;0    ;Editable=No;
                                                 ParentControl=1103355000;
                                                 InColumn=Yes;
                                                 SourceExpr=Garaje }
    { 1103355002;Label  ;0    ;0    ;0    ;0    ;ParentControl=1103355001;
                                                 InColumnHeading=Yes }
    { 1103355003;TextBox;1693 ;0    ;1540 ;0    ;Editable=No;
                                                 ParentControl=1103355000;
                                                 InColumn=Yes;
                                                 SourceExpr=Conductor }
    { 1103355004;Label  ;0    ;0    ;0    ;0    ;ParentControl=1103355003;
                                                 InColumnHeading=Yes }
    { 1103355037;TextBox;3016 ;2970 ;1700 ;440  ;Editable=No;
                                                 ParentControl=1103355000;
                                                 InColumn=Yes;
                                                 SourceExpr="Hora Inicio" }
    { 1103355039;Label  ;0    ;0    ;0    ;0    ;ParentControl=1103355037;
                                                 InColumnHeading=Yes }
    { 1103355040;CheckBox;5079;5060 ;1354 ;440  ;Editable=No;
                                                 ParentControl=1103355000;
                                                 InColumn=Yes;
                                                 ShowCaption=No;
                                                 SourceExpr="Dia Siguiente" }
    { 1103355041;Label  ;0    ;0    ;0    ;0    ;ParentControl=1103355040;
                                                 InColumnHeading=Yes }
    { 1103355005;TextBox;0    ;0    ;1700 ;0    ;Editable=No;
                                                 ParentControl=1103355000;
                                                 InColumn=Yes;
                                                 DrillDown=No;
                                                 SourceExpr=Partes }
    { 1103355006;Label  ;0    ;0    ;0    ;0    ;ParentControl=1103355005;
                                                 InColumnHeading=Yes }
    { 1103355007;TextBox;0    ;0    ;1700 ;0    ;Editable=No;
                                                 ParentControl=1103355000;
                                                 InColumn=Yes;
                                                 SourceExpr="Nombre Abreviado" }
    { 1103355008;Label  ;0    ;0    ;0    ;0    ;ParentControl=1103355007;
                                                 InColumnHeading=Yes }
    { 1103355009;TextBox;9819 ;0    ;3520 ;0    ;Editable=No;
                                                 ParentControl=1103355000;
                                                 InColumn=Yes;
                                                 SourceExpr=Nombre }
    { 1103355010;Label  ;0    ;0    ;0    ;0    ;ParentControl=1103355009;
                                                 InColumnHeading=Yes }
    { 1103355011;TextBox;13175;0    ;3410 ;0    ;HorzGlue=Both;
                                                 Editable=No;
                                                 ParentControl=1103355000;
                                                 InColumn=Yes;
                                                 SourceExpr=Apellidos }
    { 1103355012;Label  ;0    ;0    ;0    ;0    ;ParentControl=1103355011;
                                                 InColumnHeading=Yes }
    { 1103355015;CommandButton;16720;8910;2200;550;
                                                 HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 PushAction=FormHelp }
    { 1103355016;TabControl;220;220 ;18700;1430 ;HorzGlue=Both;
                                                 PageNamesML=ESP=Filtros }
    { 1103355017;CommandButton;440;990;660;440  ;ParentControl=1103355016;
                                                 InPage=0;
                                                 ShowCaption=No;
                                                 HorzAlign=General;
                                                 BackTransparent=Yes;
                                                 Bitmap=15;
                                                 OnPush=VAR
                                                          cFunBas@1100244001 : Codeunit 7010310;
                                                          lwAuxFecha@1100244002 : Date;
                                                        BEGIN
                                                          IF wFecha = 0D THEN
                                                            primeraFecha
                                                          ELSE BEGIN
                                                            lwAuxFecha:=cFunBas.fdom(wFecha,0);
                                                            IF lwAuxFecha = wFecha THEN
                                                              wFecha:=cFunBas.fdom(wFecha,-1)
                                                            ELSE
                                                              wFecha := lwAuxFecha;
                                                          END;
                                                          Cambiafecha(wFecha);
                                                          CurrForm.UPDATE;
                                                        END;
                                                         }
    { 1103355018;CommandButton;1100;990;440;440 ;ParentControl=1103355016;
                                                 InPage=0;
                                                 ShowCaption=No;
                                                 HorzAlign=General;
                                                 BitmapPos=Right;
                                                 Bitmap=15;
                                                 OnPush=BEGIN
                                                          IF wFecha=0D THEN
                                                            primeraFecha
                                                          ELSE
                                                            wFecha := wFecha - 1;
                                                          Cambiafecha(wFecha);
                                                          CurrForm.UPDATE;
                                                        END;
                                                         }
    { 1103355019;TextBox;1540 ;990  ;1650 ;440  ;Name=FilFecha;
                                                 ParentControl=1103355016;
                                                 InPage=0;
                                                 BackColor=16777215;
                                                 FontName=Helvetica;
                                                 FontSize=8;
                                                 SourceExpr=wFecha;
                                                 OnValidate=BEGIN
                                                               Cambiafecha(wFecha);
                                                            END;

                                                 OnAfterValidate=BEGIN
                                                                   CurrForm.UPDATE;
                                                                 END;
                                                                  }
    { 1103355020;CommandButton;3190;990;440;440 ;ParentControl=1103355016;
                                                 InPage=0;
                                                 ShowCaption=No;
                                                 HorzAlign=General;
                                                 Bitmap=16;
                                                 OnPush=BEGIN
                                                          IF wFecha = 0D THEN
                                                            primeraFecha
                                                          ELSE
                                                            wFecha := wFecha + 1;
                                                          Cambiafecha(wFecha);
                                                          CurrForm.UPDATE;
                                                        END;
                                                         }
    { 1103355021;CommandButton;3630;990;660;440 ;ParentControl=1103355016;
                                                 InPage=0;
                                                 ShowCaption=No;
                                                 HorzAlign=General;
                                                 BackTransparent=Yes;
                                                 BitmapPos=Right;
                                                 Bitmap=16;
                                                 OnPush=VAR
                                                          cFunBas@1100244001 : Codeunit 7010310;
                                                          lwAuxFecha@1100244002 : Date;
                                                        BEGIN
                                                          IF wFecha = 0D THEN
                                                            primeraFecha
                                                          ELSE BEGIN
                                                            lwAuxFecha:=cFunBas.ldom(wFecha,0);
                                                            IF lwAuxFecha = wFecha THEN
                                                              wFecha:=cFunBas.ldom(wFecha,1)
                                                            ELSE
                                                              wFecha := lwAuxFecha;
                                                          END;
                                                          Cambiafecha(wFecha);
                                                          CurrForm.UPDATE;
                                                        END;
                                                         }
    { 1103355024;TextBox;17600;990  ;1100 ;440  ;HorzGlue=Right;
                                                 Editable=No;
                                                 ParentControl=1103355016;
                                                 InPage=0;
                                                 HorzAlign=Center;
                                                 FontBold=Yes;
                                                 CaptionML=ESP=Hojas ma¤ana sin imprimir;
                                                 SourceExpr=HojasSinSacar }
    { 1103355025;Label  ;13860;990  ;3630 ;440  ;HorzGlue=Right;
                                                 ParentControl=1103355024;
                                                 InPage=0 }
    { 1103355026;TextBox;12320;990  ;1430 ;440  ;HorzGlue=Right;
                                                 Editable=No;
                                                 ParentControl=1103355016;
                                                 InPage=0;
                                                 HorzAlign=Center;
                                                 FontBold=Yes;
                                                 CaptionML=ESP=Hojas nunca impresas;
                                                 SourceExpr=HojasNuncaImpresas }
    { 1103355027;Label  ;9020 ;990  ;3190 ;440  ;HorzGlue=Right;
                                                 ParentControl=1103355026;
                                                 CaptionML=ESP=Hojas nunca impresas }
    { 1103355028;CheckBox;8360;990  ;440  ;440  ;ParentControl=1103355016;
                                                 InPage=0;
                                                 ShowCaption=No;
                                                 CaptionML=ESP="Ver Dialogo Impresi¢n ";
                                                 SourceExpr=wDialImp }
    { 1103355029;Label  ;4950 ;990  ;3300 ;440  ;ParentControl=1103355028 }
    { 1103355022;MenuButton;14080;8910;2420;550 ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 CaptionML=ESP=C&onductores;
                                                 Menu=MENUITEMS
                                                 {
                                                   { ID=1103355023;
                                                     CaptionML=ESP=Imprimir Filtrados;
                                                     OnPush=BEGIN

                                                              Imprimir(rImpCondTMP, TRUE);
                                                            END;
                                                             }
                                                   { ID=1103355031;
                                                     CaptionML=ESP=Imprimir Letreros Filtrados;
                                                     OnPush=BEGIN

                                                              ImprimirLetreros(rImpCondTMP, TRUE);
                                                            END;
                                                             }
                                                   { ID=1103355035;
                                                     CaptionML=ESP=Imprimir Documentos Filtrados;
                                                     OnPush=BEGIN
                                                              ImprimirDocumentos(rImpCondTMP, TRUE);
                                                            END;
                                                             }
                                                   { ID=1103355038;
                                                     MenuItemType=Separator }
                                                   { ID=1103355030;
                                                     CaptionML=ESP=Imprimir Actual;
                                                     OnPush=BEGIN

                                                              Imprimir(rImpCondTMP, FALSE);
                                                            END;
                                                             }
                                                   { ID=1103355032;
                                                     CaptionML=ESP=Imprimir Letreros Actual;
                                                     OnPush=BEGIN

                                                              ImprimirLetreros(rImpCondTMP, FALSE);
                                                            END;
                                                             }
                                                   { ID=1103355036;
                                                     CaptionML=ESP=Imprimir Documentos Actual;
                                                     OnPush=BEGIN

                                                              ImprimirDocumentos(rImpCondTMP, FALSE);
                                                            END;
                                                             }
                                                   { ID=1103355033;
                                                     MenuItemType=Separator }
                                                   { ID=1103355034;
                                                     CaptionML=ESP=Configurar Impresora Partes;
                                                     OnPush=BEGIN

                                                              VerConfigInforme;
                                                            END;
                                                             }
                                                 }
                                                  }
    { 1103355044;TextBox;220  ;8910 ;13750;550  ;HorzGlue=Both;
                                                 VertGlue=Bottom;
                                                 HorzAlign=Left;
                                                 VertAlign=Center;
                                                 BackTransparent=Yes;
                                                 BorderStyle=BumpDown;
                                                 FontBold=Yes;
                                                 SourceExpr=GetFiltros }
  }
  CODE
  {
    VAR
      Text50000@1103355007 : TextConst 'ESP=DOC';
      Text50001@1103355008 : TextConst 'ESP=<Hours24>:<Minutes,2>';
      Text50002@1103355009 : TextConst 'ESP=Excursion';
      Text50003@1103355010 : TextConst 'ESP=Salida';
      Text50004@1103355011 : TextConst 'ESP=Entrada';
      Text50005@1103355012 : TextConst 'ESP=ComputerName';
      cFunBas@1103355000 : Codeunit 7010310;
      wFecha@1103355001 : Date;
      rPar@1103355002 : Record 7010311;
      wDialImp@1103355003 : Boolean;
      rImpCondTMP@1103355004 : TEMPORARY Record 7010399;
      Text001@1103355005 : TextConst 'ESP=Actualizando datos impresion : @1@@@@@@@@@@@';
      Text002@1103355006 : TextConst 'ESP="%1 N§:%2    H.Ini:%3   Pax: %4   "';

    PROCEDURE Cambiafecha@1(VAR pwFecha@1100244000 : Date);
    BEGIN
      // Cambiafecha
      IF pwFecha <> 0D THEN
        SETRANGE(Fecha , pwFecha)
      ELSE
        SETRANGE(Fecha);

      CargarConductores(rImpCondTMP, pwFecha, TRUE);
    END;

    PROCEDURE primeraFecha@2();
    BEGIN
      // SI NO HAY FECHA SE LE ASIGNA UNA

      IF wFecha=0D THEN BEGIN
        IF Fecha<>0D THEN
          wFecha:=Fecha
        ELSE
          wFecha:=TODAY;
      END;
    END;

    PROCEDURE HojasSinSacar@1100244000() : Integer;
    VAR
      lrParte@1100244000 : Record 7010360;
    BEGIN
      // HojasSinSacar
      //

      rPar.FINDFIRST;

      lrParte.RESET;
      lrParte.SETCURRENTKEY(Fecha);

      IF GETFILTER(Garaje) <> '' THEN
        COPYFILTER(Garaje, lrParte.Garaje);

      lrParte.SETRANGE(Fecha, TODAY + 1);
      lrParte.SETRANGE(Confirmado, FALSE);
      lrParte.SETRANGE(Impreso, FALSE);
      IF lrParte.FINDFIRST THEN
        EXIT(lrParte.COUNT);
    END;

    PROCEDURE HojasNuncaImpresas@1100253001() : Integer;
    VAR
      lrParte@1100253000 : Record 7010360;
    BEGIN
      // HojasNuncaImpresas
      // Indica el numero de partes del d¡a siguiente que no se han impreso nunca


      rPar.FINDFIRST;

      lrParte.RESET;
      lrParte.SETCURRENTKEY(Fecha);

      IF GETFILTER(Garaje) <> '' THEN
        COPYFILTER(Garaje, lrParte.Garaje);

      lrParte.SETRANGE(Fecha, TODAY + 1);

      lrParte.SETRANGE("Copias Impresas", 0);
      IF lrParte.FINDFIRST THEN
        EXIT(lrParte.COUNT);
    END;

    PROCEDURE Imprimir@1100253000(VAR prImpCondsTMP@1103355001 : Record 7010399;pwFiltrados@1103355002 : Boolean);
    VAR
      lrLista@1100253001 : Record 7035384;
      lrPartes@1103355000 : Record 7010360;
      lrepParte@1103355003 : Report 7010402;
    BEGIN
      // Imprimir

      prImpCondsTMP.RESET;

      IF pwFiltrados THEN
      BEGIN
        prImpCondsTMP.COPY(Rec);

        IF prImpCondsTMP.FINDSET THEN BEGIN

          REPEAT

            lrPartes.RESET;
            lrPartes.SETCURRENTKEY(Fecha, "Cod Conductor", "Dia siguiente", "Hora inicio");
            lrPartes.SETRANGE (Fecha          , prImpCondsTMP.Fecha);
            lrPartes.SETRANGE ("Cod Conductor", prImpCondsTMP.Conductor);

            IF rPar."Empresa Real" = rPar."Empresa Real"::IBZ THEN
              REPORT.RUNMODAL(7010413, wDialImp, FALSE, lrPartes)
            ELSE
              REPORT.RUNMODAL(7010402, wDialImp, FALSE, lrPartes);

          UNTIL prImpCondsTMP.NEXT = 0;

        END;
      END
      ELSE BEGIN

        lrPartes.RESET;
        lrPartes.SETCURRENTKEY(Fecha, "Cod Conductor", "Dia siguiente", "Hora inicio");
        lrPartes.SETRANGE (Fecha          , Fecha);
        lrPartes.SETRANGE ("Cod Conductor", Conductor);

        IF rPar."Empresa Real" = rPar."Empresa Real"::IBZ THEN
          REPORT.RUNMODAL(7010413, wDialImp, FALSE, lrPartes)
        ELSE
          REPORT.RUNMODAL(7010402, wDialImp, FALSE, lrPartes);

      END;
    END;

    LOCAL PROCEDURE ImprimirLetreros@1103355003(VAR prImpCondsTMP@1103355001 : Record 7010399;pwFiltrados@1103355000 : Boolean);
    VAR
      lrPartes@1103355002 : Record 7010360;
    BEGIN
      // ImprimirLetreros.

      prImpCondsTMP.RESET;

      IF pwFiltrados THEN BEGIN

        prImpCondsTMP.COPY(Rec);

        IF prImpCondsTMP.FINDSET THEN BEGIN

          REPEAT

            lrPartes.RESET;
            lrPartes.SETCURRENTKEY(Fecha, "Cod Conductor", "Dia siguiente", "Hora inicio");
            lrPartes.SETRANGE (Fecha          , prImpCondsTMP.Fecha);
            lrPartes.SETRANGE ("Cod Conductor", prImpCondsTMP.Conductor);
            IF lrPartes.FINDSET THEN BEGIN
              REPEAT
                lrPartes.ImprimeLetrero(FALSE, FALSE);
              UNTIL lrPartes.NEXT = 0;
            END;


          UNTIL prImpCondsTMP.NEXT = 0;

        END;

      END
      ELSE BEGIN

        lrPartes.RESET;
        lrPartes.SETCURRENTKEY(Fecha, "Cod Conductor", "Dia siguiente", "Hora inicio");
        lrPartes.SETRANGE (Fecha          , Fecha);
        lrPartes.SETRANGE ("Cod Conductor", Conductor);

        IF lrPartes.FINDSET THEN BEGIN
          REPEAT
            lrPartes.ImprimeLetrero(FALSE, FALSE);
          UNTIL lrPartes.NEXT = 0;
        END;

      END;
    END;

    LOCAL PROCEDURE ImprimirDocumentos@1103355004(VAR prImpCondsTMP@1103355001 : Record 7010399;pwFiltrados@1103355000 : Boolean);
    VAR
      lrPartes@1103355002 : Record 7010360;
      lwTextDoc@1103355004 : ARRAY [5] OF Text[250];
      lwExt@1103355003 : Code[10];
      lrLinsParte@1103355005 : Record 7010359;
      lrPunt@1103355006 : Record 7010315;
    BEGIN
      // ImprimirDocumentos.

      prImpCondsTMP.RESET;

      IF pwFiltrados THEN BEGIN

        prImpCondsTMP.COPY(Rec);

        IF prImpCondsTMP.FINDSET THEN BEGIN

          REPEAT

            lrPartes.RESET;
            lrPartes.SETCURRENTKEY(Fecha, "Cod Conductor", "Dia siguiente", "Hora inicio");
            lrPartes.SETRANGE (Fecha          , prImpCondsTMP.Fecha);
            lrPartes.SETRANGE ("Cod Conductor", prImpCondsTMP.Conductor);

            IF lrPartes.FINDSET THEN BEGIN
              REPEAT

                IF EXISTS(lrPartes."Peticion Servicio Cliente") THEN BEGIN
                  lwExt := COPYSTR(lrPartes."Peticion Servicio Cliente",
                                   STRLEN(lrPartes."Peticion Servicio Cliente")-2);
                  IF lwExt = Text50000 THEN BEGIN // Si es un documento de Word
                    CLEAR(lwTextDoc);
                    lwTextDoc[1] := TextoDoc(lrPartes);
                    cFunBas.ImprimeDocumentoWord(lrPartes."Peticion Servicio Cliente", lwTextDoc);
                  END
                  ELSE BEGIN // Cualquier otro tipo de documento
                    cFunBas.ImprimeDocumento(lrPartes."Peticion Servicio Cliente");
                  END;
                END;

                lrLinsParte.RESET;
                lrLinsParte.SETCURRENTKEY("N§ Parte", "N§ Linea");
                lrLinsParte.SETRANGE ("N§ Parte", lrPartes."N§ Parte");
                IF lrLinsParte.FINDSET THEN BEGIN
                  REPEAT
                    CLEAR(lwTextDoc);
                    lwTextDoc[1] := TextoDoc(lrPartes);
                    IF lrLinsParte."Imprimir Documento Descriptivo" THEN BEGIN
                      IF lrPunt.GET(lrLinsParte."Punto recogida") THEN
                        lrPunt.ImprimeDocumentoDescriptivo(lwTextDoc);
                    END;
                  UNTIL lrLinsParte.NEXT = 0;
                END;

              UNTIL lrPartes.NEXT = 0;
            END;

          UNTIL prImpCondsTMP.NEXT = 0;

        END;

      END
      ELSE BEGIN

        lrPartes.RESET;
        lrPartes.SETCURRENTKEY(Fecha, "Cod Conductor", "Dia siguiente", "Hora inicio");
        lrPartes.SETRANGE (Fecha          , Fecha);
        lrPartes.SETRANGE ("Cod Conductor", Conductor);

        IF lrPartes.FINDSET THEN BEGIN
          REPEAT

            IF EXISTS(lrPartes."Peticion Servicio Cliente") THEN BEGIN
              lwExt := COPYSTR(lrPartes."Peticion Servicio Cliente",
                               STRLEN(lrPartes."Peticion Servicio Cliente")-2);
              IF lwExt = Text50000 THEN BEGIN // Si es un documento de Word
                CLEAR(lwTextDoc);
                lwTextDoc[1] := TextoDoc(lrPartes);
                cFunBas.ImprimeDocumentoWord(lrPartes."Peticion Servicio Cliente", lwTextDoc);
              END
              ELSE BEGIN // Cualquier otro tipo de documento
                cFunBas.ImprimeDocumento(lrPartes."Peticion Servicio Cliente");
              END;
            END;

            lrLinsParte.RESET;
            lrLinsParte.SETCURRENTKEY("N§ Parte", "N§ Linea");
            lrLinsParte.SETRANGE ("N§ Parte", lrPartes."N§ Parte");
            IF lrLinsParte.FINDSET THEN BEGIN
              REPEAT
                CLEAR(lwTextDoc);
                lwTextDoc[1] := TextoDoc(lrPartes);
                IF lrLinsParte."Imprimir Documento Descriptivo" THEN BEGIN
                  IF lrPunt.GET(lrLinsParte."Punto recogida") THEN
                    lrPunt.ImprimeDocumentoDescriptivo(lwTextDoc);
                END;
              UNTIL lrLinsParte.NEXT = 0;
            END;

          UNTIL lrPartes.NEXT = 0;
        END;

      END;
    END;

    LOCAL PROCEDURE NextRec@1103355000(pwSteps@1103355000 : Integer;VAR prImpCondTMP@1103355001 : TEMPORARY Record 7010399) rvSteps : Integer;
    BEGIN
      // NextRec.

      prImpCondTMP.COPY(Rec);

      rvSteps := prImpCondTMP.NEXT(pwSteps);
      IF rvSteps <> 0 THEN
        Rec := prImpCondTMP;

      EXIT(rvSteps);
    END;

    LOCAL PROCEDURE FindRec@1103355001(pwWhich@1103355000 : Text[1024];VAR prImpCondTMP@1103355001 : TEMPORARY Record 7010399) rvFound : Boolean;
    BEGIN
      // FindRec.

      prImpCondTMP.COPY(Rec);

      IF prImpCondTMP.FIND(pwWhich) THEN BEGIN
        Rec := prImpCondTMP;
        EXIT(TRUE);
      END
      ELSE
        EXIT(FALSE);
    END;

    LOCAL PROCEDURE CargarConductores@1103355002(VAR prImpCondTMP@1103355000 : TEMPORARY Record 7010399;pwFecha@1103355001 : Date;pwVerbose@1103355003 : Boolean);
    VAR
      lrConds@1103355002 : Record 7010327;
      lwDialog@1103355004 : Dialog;
      lwCont@1103355005 : Integer;
      lwTot@1103355006 : Integer;
      lrPartes@1103355007 : Record 7010360;
    BEGIN
      // CargarConductores.

      prImpCondTMP.RESET;
      prImpCondTMP.DELETEALL;

      lrConds.SETCURRENTKEY(Externo, Bloqueado);
      lrConds.SETRANGE (Bloqueado, FALSE);

      IF pwVerbose THEN BEGIN
        lwCont := 0;
        lwTot  := lrConds.COUNT;
      END;

      IF lrConds.FINDSET THEN BEGIN

        IF pwVerbose THEN
          lwDialog.OPEN(Text001);

        REPEAT

          IF pwVerbose THEN BEGIN
            lwCont += 1;
            lwDialog.UPDATE(1, ROUND(lwCont / lwTot * 10000, 1));
          END;

          prImpCondTMP.INIT;
          prImpCondTMP.Fecha              := pwFecha;
          prImpCondTMP.Conductor          := lrConds.Conductor;

          prImpCondTMP."Nombre Abreviado" := lrConds."Nombre Abreviado";
          prImpCondTMP.Garaje             := lrConds.Garaje;
          prImpCondTMP.Nombre             := lrConds.Nombre;
          prImpCondTMP.Apellidos          := lrConds.Apellidos;

          prImpCondTMP.CALCFIELDS(Partes);

          IF prImpCondTMP.Partes <> 0 THEN BEGIN

            lrPartes.SETCURRENTKEY(Fecha, "Cod Conductor", "Dia siguiente", "Hora inicio");
            lrPartes.SETRANGE (Fecha          , prImpCondTMP.Fecha);
            lrPartes.SETRANGE ("Cod Conductor", prImpCondTMP.Conductor);
            lrPartes.FINDFIRST;

            prImpCondTMP."Hora Inicio"   := lrPartes."Hora inicio";
            prImpCondTMP."Dia Siguiente" := lrPartes."Dia siguiente";

            prImpCondTMP.INSERT;

          END;

        UNTIL lrConds.NEXT = 0;

        IF pwVerbose THEN
          lwDialog.CLOSE;

      END;
    END;

    LOCAL PROCEDURE TextoDoc@1100253008(VAR prParte@1103355000 : Record 7010360) : Text[250];
    VAR
      lwText@1100253000 : Text[250];
    BEGIN
      // TextoDoc

      prParte.CALCFIELDS("Adultos transportados", "Ni¤os transportados");
      lwText := STRSUBSTNO(Text002, GetTipoExcursion(prParte), prParte."N§ Parte",
                           FORMAT(prParte."Hora inicio",0,Text50001),
                           prParte."Adultos transportados" + prParte."Ni¤os transportados");

      EXIT(lwText);
    END;

    LOCAL PROCEDURE GetTipoExcursion@1103355005(VAR prParte@1103355000 : Record 7010360) rvTextTipo : Text[30];
    VAR
      lrTipoServ@1103355001 : Record 7010319;
      lrPar@1103355002 : Record 7010311;
    BEGIN
      // GetTipoExcursion.

      lrPar.FINDFIRST;
      lrTipoServ.GET(prParte."Tipo Servicio");

      IF lrTipoServ."Tipo fijo" = lrTipoServ."Tipo fijo"::Excursion THEN
        EXIT(Text50002)
      ELSE BEGIN
        IF prParte."Codigo Servicio" = lrPar."Codigo Salidas" THEN
          EXIT(Text50003)
        ELSE
          EXIT(Text50004);
      END;
    END;

    LOCAL PROCEDURE ConfigporMaquinaUser@1103355006();
    VAR
      lrPrintSel@1103355000 : Record 78;
    BEGIN
      // ConfigporMaquinaUser.

      rPar.FINDFIRST;

      lrPrintSel.RESET;
      lrPrintSel.SETRANGE("User ID", USERID);

      IF rPar."Empresa Real" = rPar."Empresa Real"::IBZ THEN
        lrPrintSel.SETRANGE("Report ID", 7010413)
      ELSE
        lrPrintSel.SETRANGE("Report ID", 7010402);
      IF NOT lrPrintSel.FINDFIRST THEN
      BEGIN
        lrPrintSel.INIT;
        lrPrintSel."User ID"            := USERID;

        IF rPar."Empresa Real" = rPar."Empresa Real"::IBZ THEN
          lrPrintSel."Report ID" := 7010413
        ELSE
          lrPrintSel."Report ID" := 7010402;

        lrPrintSel."Printer Name" := '';
        lrPrintSel.INSERT;
      END;
    END;

    LOCAL PROCEDURE VerConfigInforme@1103355009();
    VAR
      lrPrintSel@1103355002 : Record 78;
      lfPrintSel@1103355001 : Form 64;
    BEGIN
      // VerConfigInforme.

      rPar.FINDFIRST;

      lrPrintSel.RESET;
      lrPrintSel.SETRANGE("User ID", USERID);

      IF rPar."Empresa Real" = rPar."Empresa Real"::IBZ THEN
        lrPrintSel.SETRANGE("Report ID", 7010413)
      ELSE
        lrPrintSel.SETRANGE("Report ID", 7010402);

      lfPrintSel.SETTABLEVIEW(lrPrintSel);
      lfPrintSel.RUNMODAL;
    END;

    LOCAL PROCEDURE CambiaSeleccion@1103355007(VAR prRec@1103355001 : Record 7010399;VAR prImpCondsTMP@1103355000 : TEMPORARY Record 7010399);
    BEGIN
      // CambiaSeleccion.

      prImpCondsTMP.RESET;

      prImpCondsTMP := prRec;
      prImpCondsTMP.FIND;

      prImpCondsTMP.Seleccion := NOT prImpCondsTMP.Seleccion;
      prImpCondsTMP.MODIFY;
    END;

    PROCEDURE GetFiltros@1103355008() rvFiltros : Text[1024];
    BEGIN
      // GetFiltros.

      EXIT(GETFILTERS);
    END;

    BEGIN
    END.
  }
}
