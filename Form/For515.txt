OBJECT Form 515 Item Avail. by Location Lines
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    Width=15730;
    Height=5060;
    Editable=Yes;
    CaptionML=[ENU=Item Avail. by Location Lines;
               ESP=Disp. prod. por l¡ns. almac‚n];
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    TableBoxID=1;
    LinksAllowed=No;
    SourceTable=Table14;
    SourceTableView=WHERE(Use As In-Transit=CONST(No));
    OnAfterGetRecord=BEGIN
                       IF Item."No." <> '' THEN BEGIN
                         AmountType2 := AmountType;
                         AmountType := AmountType::"Balance at Date";
                         CalculateNeed;

                         ProjAvailBalance :=
                           Item.Inventory +
                           PlannedOrderReceipt +
                           ScheduledReceipt -
                           GrossRequirement;

                         IF AmountType2 <> AmountType THEN BEGIN
                           AmountType := AmountType2;
                           CalculateNeed;
                         END;

                         ExpectedInventory := AvailabilityMgt.ExpectedQtyOnHand(Item,TRUE,0,QtyAvailable,31129999D);
                       END;
                     END;

  }
  CONTROLS
  {
    { 1   ;TableBox     ;0    ;0    ;15730;5060 ;HorzGlue=Both;
                                                 VertGlue=Both;
                                                 Editable=No }
    { 5   ;TextBox      ;0    ;3190 ;1650 ;440  ;ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr=Code }
    { 6   ;Label        ;0    ;0    ;0    ;0    ;ParentControl=5;
                                                 InColumnHeading=Yes }
    { 7   ;TextBox      ;1350 ;3300 ;4400 ;440  ;HorzGlue=Both;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr=Name }
    { 8   ;Label        ;0    ;0    ;0    ;0    ;ParentControl=7;
                                                 InColumnHeading=Yes }
    { 2   ;TextBox      ;5186 ;220  ;1650 ;440  ;ParentControl=1;
                                                 InColumn=Yes;
                                                 CaptionML=[ENU=Gross Requirement;
                                                            ESP=Necesidades brutas];
                                                 DecimalPlaces=0:5;
                                                 SourceExpr=GrossRequirement;
                                                 OnDrillDown=BEGIN
                                                               CalculateNeed;
                                                               CLEAR(AvailLineList);
                                                               AvailLineList.Init(0,Item);
                                                               AvailLineList.RUNMODAL;
                                                             END;
                                                              }
    { 3   ;Label        ;0    ;0    ;0    ;0    ;ParentControl=2;
                                                 InColumnHeading=Yes }
    { 18  ;TextBox      ;6853 ;110  ;1650 ;440  ;ParentControl=1;
                                                 InColumn=Yes;
                                                 CaptionML=[ENU=Scheduled Receipt;
                                                            ESP=Recepci¢n programada];
                                                 DecimalPlaces=0:5;
                                                 SourceExpr=ScheduledReceipt;
                                                 OnDrillDown=BEGIN
                                                               CalculateNeed;
                                                               CLEAR(AvailLineList);
                                                               AvailLineList.Init(2,Item);
                                                               AvailLineList.RUNMODAL;
                                                             END;
                                                              }
    { 19  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=18;
                                                 InColumnHeading=Yes }
    { 26  ;TextBox      ;8494 ;110  ;1650 ;440  ;ParentControl=1;
                                                 InColumn=Yes;
                                                 CaptionML=[ENU=Planned Receipt;
                                                            ESP=Recep. planif.];
                                                 DecimalPlaces=0:5;
                                                 SourceExpr=PlannedOrderReceipt;
                                                 OnDrillDown=BEGIN
                                                               CalculateNeed;
                                                               CLEAR(AvailLineList);
                                                               AvailLineList.Init(1,Item);
                                                               AvailLineList.RUNMODAL;
                                                             END;
                                                              }
    { 27  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=26;
                                                 InColumnHeading=Yes }
    { 28  ;TextBox      ;9208 ;110  ;1650 ;440  ;ParentControl=1;
                                                 InColumn=Yes;
                                                 CaptionML=[ENU=Projected Available Balance;
                                                            ESP=Saldo disponible estimado];
                                                 DecimalPlaces=0:5;
                                                 SourceExpr=ProjAvailBalance;
                                                 OnDrillDown=BEGIN
                                                               CalculateNeed;
                                                               CLEAR(AvailLineList);
                                                               AvailLineList.Init(4,Item);
                                                               AvailLineList.RUNMODAL;
                                                             END;
                                                              }
    { 29  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=28;
                                                 InColumnHeading=Yes }
    { 4   ;TextBox      ;4525 ;1650 ;1650 ;440  ;Visible=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 DrillDown=Yes;
                                                 CaptionML=[ENU=Inventory;
                                                            ESP=Existencias];
                                                 DecimalPlaces=0:5;
                                                 SourceExpr=Item.Inventory;
                                                 OnDrillDown=BEGIN
                                                               ShowItemLedgerEntries(FALSE);
                                                             END;
                                                              }
    { 9   ;Label        ;0    ;0    ;0    ;0    ;ParentControl=4;
                                                 InColumnHeading=Yes }
    { 10  ;TextBox      ;4763 ;2860 ;1650 ;440  ;Visible=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 DrillDown=Yes;
                                                 CaptionML=[ENU=Qty. on Purch. Order;
                                                            ESP=Cdad. en pedidos compra];
                                                 DecimalPlaces=0:5;
                                                 SourceExpr=Item."Qty. on Purch. Order";
                                                 OnDrillDown=BEGIN
                                                               ShowPurchLines;
                                                             END;
                                                              }
    { 11  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=10;
                                                 InColumnHeading=Yes }
    { 12  ;TextBox      ;6271 ;2530 ;1650 ;440  ;Visible=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 DrillDown=Yes;
                                                 CaptionML=[ENU=Qty. on Sales Order;
                                                            ESP=Cdad. en pedidos venta];
                                                 DecimalPlaces=0:5;
                                                 SourceExpr=Item."Qty. on Sales Order";
                                                 OnDrillDown=BEGIN
                                                               ShowSalesLines;
                                                             END;
                                                              }
    { 13  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=12;
                                                 InColumnHeading=Yes }
    { 32  ;TextBox      ;17278;220  ;1650 ;440  ;Visible=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 CaptionML=[ENU=Trans. Ord. Shipment (Qty.);
                                                            ESP=Env¡o ped. transfer. (cdad.)];
                                                 DecimalPlaces=0:5;
                                                 SourceExpr=Item."Trans. Ord. Shipment (Qty.)";
                                                 OnDrillDown=BEGIN
                                                               ShowTransLines(Item.FIELDNO("Trans. Ord. Shipment (Qty.)"));
                                                             END;
                                                              }
    { 33  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=32;
                                                 InColumnHeading=Yes }
    { 34  ;TextBox      ;9896 ;220  ;1650 ;440  ;Visible=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 CaptionML=[ENU=Qty. in Transit;
                                                            ESP=Cdad. en tr nsito];
                                                 DecimalPlaces=0:5;
                                                 SourceExpr=Item."Qty. in Transit";
                                                 OnDrillDown=BEGIN
                                                               ShowTransLines(Item.FIELDNO("Qty. in Transit"));
                                                             END;
                                                              }
    { 35  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=34;
                                                 InColumnHeading=Yes }
    { 30  ;TextBox      ;17304;220  ;1650 ;440  ;Visible=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 CaptionML=[ENU=Trans. Ord. Receipt (Qty.);
                                                            ESP=Recep. ped. transfer. (cdad.)];
                                                 DecimalPlaces=0:5;
                                                 SourceExpr=Item."Trans. Ord. Receipt (Qty.)";
                                                 OnDrillDown=BEGIN
                                                               ShowTransLines(Item.FIELDNO("Trans. Ord. Receipt (Qty.)"));
                                                             END;
                                                              }
    { 31  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=30;
                                                 InColumnHeading=Yes }
    { 14  ;TextBox      ;8176 ;3630 ;1650 ;440  ;Visible=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 CaptionML=[ENU=Expected Inventory;
                                                            ESP=Stock esperado];
                                                 DecimalPlaces=0:5;
                                                 SourceExpr=ExpectedInventory }
    { 15  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=14;
                                                 InColumnHeading=Yes }
    { 16  ;TextBox      ;9975 ;4180 ;1650 ;440  ;Visible=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 CaptionML=[ENU=Available Inventory;
                                                            ESP=Stock disponible];
                                                 DecimalPlaces=0:5;
                                                 SourceExpr=QtyAvailable }
    { 17  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=16;
                                                 InColumnHeading=Yes }
    { 20  ;TextBox      ;12231;5060 ;1650 ;440  ;Visible=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 CaptionML=[ENU=Scheduled Receipt (Qty.);
                                                            ESP=Recepci¢n programada (cdad.)];
                                                 DecimalPlaces=0:5;
                                                 SourceExpr=Item."Scheduled Receipt (Qty.)";
                                                 OnDrillDown=BEGIN
                                                               ShowSchedReceipt;
                                                             END;
                                                              }
    { 21  ;Label        ;11563;1090 ;3300 ;440  ;ParentControl=20;
                                                 InColumnHeading=Yes }
    { 22  ;TextBox      ;12798;4070 ;1650 ;440  ;Visible=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 CaptionML=[ENU=Scheduled Issue (Qty.);
                                                            ESP=Env¡o programado (cdad.)];
                                                 DecimalPlaces=0:5;
                                                 SourceExpr=Item."Scheduled Need (Qty.)";
                                                 OnDrillDown=BEGIN
                                                               ShowSchedNeed;
                                                             END;
                                                              }
    { 23  ;Label        ;12636;980  ;3300 ;440  ;ParentControl=22;
                                                 InColumnHeading=Yes }
    { 24  ;TextBox      ;16280;5060 ;1650 ;440  ;ParentControl=1;
                                                 InColumn=Yes;
                                                 CaptionML=[ENU=Planned Order Releases;
                                                            ESP=Planif. liberaci¢n ¢rdenes];
                                                 DecimalPlaces=0:5;
                                                 SourceExpr=PlannedOrderReleases;
                                                 OnDrillDown=BEGIN
                                                               CalculateNeed;
                                                               CLEAR(AvailLineList);
                                                               AvailLineList.Init(3,Item);
                                                               AvailLineList.RUNMODAL;
                                                             END;
                                                              }
    { 25  ;Label        ;14843;1640 ;3300 ;440  ;ParentControl=24;
                                                 InColumnHeading=Yes }
    { 36  ;TextBox      ;29158;2310 ;1700 ;440  ;Visible=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 DrillDown=Yes;
                                                 CaptionML=[ENU=Net Change;
                                                            ESP=Saldo periodo];
                                                 DecimalPlaces=0:5;
                                                 SourceExpr=Item."Net Change";
                                                 OnDrillDown=BEGIN
                                                               ShowItemLedgerEntries(TRUE);
                                                             END;
                                                              }
    { 37  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=36;
                                                 InColumnHeading=Yes }
  }
  CODE
  {
    VAR
      Item@1000 : Record 27;
      SalesLine@1001 : Record 37;
      PurchLine@1002 : Record 39;
      ProdOrderLine@1003 : Record 5406;
      ProdOrderComp@1004 : Record 5407;
      AvailabilityMgt@1005 : Codeunit 5400;
      AvailLineList@1006 : Form 99000902;
      ExpectedInventory@1007 : Decimal;
      QtyAvailable@1008 : Decimal;
      ItemPeriodLength@1009 : 'Day,Week,Month,Quarter,Year,Period';
      AmountType@1010 : 'Net Change,Balance at Date';
      AmountType2@1011 : 'Net Change,Balance at Date';
      PlannedOrderReleases@1012 : Decimal;
      GrossRequirement@1013 : Decimal;
      PlannedOrderReceipt@1014 : Decimal;
      ScheduledReceipt@1015 : Decimal;
      ProjAvailBalance@1016 : Decimal;
      PeriodStart@1017 : Date;
      PeriodEnd@1018 : Date;

    PROCEDURE Set@1(VAR NewItem@1000 : Record 27;NewItemPeriodLength@1001 : Integer;NewAmountType@1002 : 'Net Change,Balance at Date');
    BEGIN
      Item.COPY(NewItem);
      PeriodStart := Item.GETRANGEMIN("Date Filter");
      PeriodEnd := Item.GETRANGEMAX("Date Filter");
      ItemPeriodLength := NewItemPeriodLength;
      AmountType := NewAmountType;
      CurrForm.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE CalculateNeed@4();
    BEGIN
      IF Item."No." <> '' THEN BEGIN
        SetItemFilter;
        Item.CALCFIELDS(
          "Qty. on Purch. Order",
          "Qty. on Sales Order",
          "Qty. on Service Order",
          Inventory,
          "Net Change",
          "Scheduled Receipt (Qty.)",
          "Scheduled Need (Qty.)",
          "Planning Issues (Qty.)",
          "Planned Order Receipt (Qty.)",
          "FP Order Receipt (Qty.)",
          "Rel. Order Receipt (Qty.)",
          "Planned Order Release (Qty.)",
          "Purch. Req. Receipt (Qty.)",
          "Purch. Req. Release (Qty.)",
          "Qty. in Transit",
          "Trans. Ord. Shipment (Qty.)",
          "Trans. Ord. Receipt (Qty.)");

        GrossRequirement :=
          Item."Qty. on Sales Order" +
          Item."Qty. on Service Order" +
          Item."Scheduled Need (Qty.)" +
          Item."Trans. Ord. Shipment (Qty.)" +
          Item."Planning Issues (Qty.)" +
          Item.CalcPurchReturn;
        PlannedOrderReceipt :=
          Item."Planned Order Receipt (Qty.)" +
          Item."Purch. Req. Receipt (Qty.)";
        ScheduledReceipt :=
          Item."FP Order Receipt (Qty.)" +
          Item."Rel. Order Receipt (Qty.)" +
          Item."Qty. on Purch. Order" +
          Item."Qty. in Transit" +
          Item."Trans. Ord. Receipt (Qty.)" +
          Item.CalcSalesReturn;
        PlannedOrderReleases :=
          Item."Planned Order Release (Qty.)" +
          Item."Purch. Req. Release (Qty.)";
      END;
    END;

    LOCAL PROCEDURE ShowItemLedgerEntries@10(NetChange@1001 : Boolean);
    VAR
      ItemLedgerEntry@1000 : Record 32;
    BEGIN
      SetItemFilter;
      ItemLedgerEntry.RESET;
      ItemLedgerEntry.SETCURRENTKEY("Item No.","Entry Type","Variant Code","Drop Shipment","Location Code","Posting Date");
      ItemLedgerEntry.SETRANGE("Item No.",Item."No.");
      ItemLedgerEntry.SETFILTER("Variant Code",Item.GETFILTER("Variant Filter"));
      ItemLedgerEntry.SETFILTER("Drop Shipment",Item.GETFILTER("Drop Shipment Filter"));
      ItemLedgerEntry.SETFILTER("Location Code",Item.GETFILTER("Location Filter"));
      ItemLedgerEntry.SETFILTER("Global Dimension 1 Code",Item.GETFILTER("Global Dimension 1 Filter"));
      ItemLedgerEntry.SETFILTER("Global Dimension 2 Code",Item.GETFILTER("Global Dimension 2 Filter"));
      IF NetChange THEN
        ItemLedgerEntry.SETFILTER("Posting Date",Item.GETFILTER("Date Filter"));
      FORM.RUN(0,ItemLedgerEntry);
    END;

    LOCAL PROCEDURE ShowSalesLines@2();
    BEGIN
      SetItemFilter;
      SalesLine.RESET;
      SalesLine.SETCURRENTKEY("Document Type",Type,"No.","Variant Code","Drop Shipment","Location Code","Shipment Date");
      SalesLine.SETRANGE("Document Type",SalesLine."Document Type"::Order);
      SalesLine.SETRANGE(Type,SalesLine.Type::Item);
      SalesLine.SETRANGE("No.",Item."No.");
      SalesLine.SETFILTER("Variant Code",Item.GETFILTER("Variant Filter"));
      SalesLine.SETFILTER("Drop Shipment",Item.GETFILTER("Drop Shipment Filter"));
      SalesLine.SETFILTER("Location Code",Item.GETFILTER("Location Filter"));
      SalesLine.SETFILTER("Shortcut Dimension 1 Code",Item.GETFILTER("Global Dimension 1 Filter"));
      SalesLine.SETFILTER("Shortcut Dimension 2 Code",Item.GETFILTER("Global Dimension 2 Filter"));
      SalesLine.SETFILTER("Shipment Date",Item.GETFILTER("Date Filter"));
      FORM.RUN(0,SalesLine);
    END;

    LOCAL PROCEDURE ShowPurchLines@3();
    BEGIN
      SetItemFilter;
      PurchLine.RESET;
      PurchLine.SETCURRENTKEY(
        "Document Type",Type,"No.","Variant Code","Drop Shipment",
        "Location Code","Expected Receipt Date");
      PurchLine.SETRANGE("Document Type",PurchLine."Document Type"::Order);
      PurchLine.SETRANGE(Type,PurchLine.Type::Item);
      PurchLine.SETRANGE("No.",Item."No.");
      PurchLine.SETFILTER("Variant Code",Item.GETFILTER("Variant Filter"));
      PurchLine.SETFILTER("Drop Shipment",Item.GETFILTER("Drop Shipment Filter"));
      PurchLine.SETFILTER("Location Code",Item.GETFILTER("Location Filter"));
      PurchLine.SETFILTER("Shortcut Dimension 1 Code",Item.GETFILTER("Global Dimension 1 Filter"));
      PurchLine.SETFILTER("Shortcut Dimension 2 Code",Item.GETFILTER("Global Dimension 2 Filter"));
      PurchLine.SETFILTER("Expected Receipt Date",Item.GETFILTER("Date Filter"));
      FORM.RUN(0,PurchLine);
    END;

    LOCAL PROCEDURE ShowSchedReceipt@7();
    BEGIN
      SetItemFilter;
      ProdOrderLine.RESET;
      ProdOrderLine.SETCURRENTKEY("Item No.","Variant Code","Location Code",Status,"Ending Date");
      ProdOrderLine.SETRANGE(Status,ProdOrderLine.Status::Planned,ProdOrderLine.Status::Released);
      ProdOrderLine.SETRANGE("Item No.",Item."No.");
      ProdOrderLine.SETFILTER("Variant Code",Item.GETFILTER("Variant Filter"));
      ProdOrderLine.SETFILTER("Location Code",Item.GETFILTER("Location Filter"));
      ProdOrderLine.SETFILTER("Shortcut Dimension 1 Code",Item.GETFILTER("Global Dimension 1 Filter"));
      ProdOrderLine.SETFILTER("Shortcut Dimension 2 Code",Item.GETFILTER("Global Dimension 2 Filter"));
      ProdOrderLine.SETFILTER("Ending Date",Item.GETFILTER("Date Filter"));
      FORM.RUN(0,ProdOrderLine);
    END;

    LOCAL PROCEDURE ShowSchedNeed@8();
    BEGIN
      SetItemFilter;
      ProdOrderComp.RESET;
      ProdOrderComp.SETCURRENTKEY("Item No.","Variant Code","Location Code",Status,"Due Date");
      ProdOrderComp.SETRANGE(Status,ProdOrderComp.Status::Planned,ProdOrderComp.Status::Released);
      ProdOrderComp.SETRANGE("Item No.",Item."No.");
      ProdOrderComp.SETFILTER("Variant Code",Item.GETFILTER("Variant Filter"));
      ProdOrderComp.SETFILTER("Location Code",Item.GETFILTER("Location Filter"));
      ProdOrderComp.SETFILTER("Shortcut Dimension 1 Code",Item.GETFILTER("Global Dimension 1 Filter"));
      ProdOrderComp.SETFILTER("Shortcut Dimension 2 Code",Item.GETFILTER("Global Dimension 2 Filter"));
      ProdOrderComp.SETFILTER("Due Date",Item.GETFILTER("Date Filter"));
      FORM.RUN(0,ProdOrderComp);
    END;

    LOCAL PROCEDURE ShowTransLines@5(What@1000 : Integer);
    VAR
      TransLine@1001 : Record 5741;
    BEGIN
      SetItemFilter;
      TransLine.RESET;
      CASE What OF
        Item.FIELDNO("Trans. Ord. Shipment (Qty.)"):
          BEGIN
            TransLine.SETCURRENTKEY(
              "Transfer-from Code",Status,"Derived From Line No.",
              "Item No.","Variant Code",
              "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code",
              "Shipment Date","In-Transit Code");
            TransLine.SETFILTER("Transfer-from Code",Item.GETFILTER("Location Filter"));
            TransLine.SETFILTER("Shipment Date",Item.GETFILTER("Date Filter"));
          END;
        Item.FIELDNO("Qty. in Transit"),Item.FIELDNO("Trans. Ord. Receipt (Qty.)"):
          BEGIN
            TransLine.SETCURRENTKEY(
              "Transfer-to Code",Status,"Derived From Line No.",
              "Item No.","Variant Code",
              "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code",
              "Receipt Date","In-Transit Code");
            TransLine.SETFILTER("Transfer-to Code",Item.GETFILTER("Location Filter"));
            TransLine.SETFILTER("Receipt Date",Item.GETFILTER("Date Filter"));
          END;
      END;
      TransLine.SETRANGE("Item No.",Item."No.");
      TransLine.SETRANGE("Derived From Line No.",0);
      TransLine.SETFILTER("Variant Code",Item.GETFILTER("Variant Filter"));
      TransLine.SETFILTER("Shortcut Dimension 1 Code",Item.GETFILTER("Global Dimension 1 Filter"));
      TransLine.SETFILTER("Shortcut Dimension 2 Code",Item.GETFILTER("Global Dimension 2 Filter"));
      FORM.RUN(0,TransLine);
    END;

    LOCAL PROCEDURE SetItemFilter@6();
    BEGIN
      IF Item."No." <> '' THEN BEGIN
        IF AmountType = AmountType::"Net Change" THEN
          Item.SETRANGE("Date Filter",PeriodStart,PeriodEnd)
        ELSE
          Item.SETRANGE("Date Filter",0D,PeriodEnd);
        Item.SETRANGE("Location Filter",Code);
      END;
    END;

    BEGIN
    END.
  }
}
