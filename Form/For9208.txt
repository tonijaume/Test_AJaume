OBJECT Form 9208 _Invt. Analysis by Dimensions
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    Width=18590;
    Height=4290;
    CaptionML=ENU=Invt. Analysis by Dimensions;
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    LinksAllowed=No;
    SourceTable=Table367;
    DataCaptionExpr=CurrentItemAnalysisViewCode;
    OnOpenForm=VAR
                 MATRIX_Step@1000 : 'First,Previous,Same,Next';
               BEGIN
                 CurrentAnalysisArea := CurrentAnalysisArea::Inventory;

                 GLSetup.GET;
                 NormalFormatString := Text001 + GLSetup."Amount Decimal Places" + Text002;

                 ItemAnalysisMgt.AnalysisViewSelection(
                   CurrentAnalysisArea,CurrentItemAnalysisViewCode,ItemAnalysisView,ItemStatisticsBuffer,
                   Dim1Filter,Dim2Filter,Dim3Filter);

                 IF (NewItemAnalysisCode <> '') AND (NewItemAnalysisCode <> CurrentItemAnalysisViewCode) THEN BEGIN
                   CurrentItemAnalysisViewCode := NewItemAnalysisCode;
                   ItemAnalysisMgt.CheckAnalysisView(CurrentAnalysisArea,CurrentItemAnalysisViewCode,ItemAnalysisView);
                   ItemAnalysisMgt.SetItemAnalysisView(
                     CurrentAnalysisArea,CurrentItemAnalysisViewCode,ItemAnalysisView,ItemStatisticsBuffer,
                     Dim1Filter,Dim2Filter,Dim3Filter);
                 END;

                 ItemAnalysisMgt.SetLineAndColDim(
                   ItemAnalysisView,LineDimCode,LineDimOption,ColumnDimCode,ColumnDimOption);
                 UpdateFilterFields;

                 FindPeriod('');
                 MATRIX_GenerateColumnCaptions(MATRIX_Step::First);
                 CurrForm.UPDATECONTROLS;
               END;

  }
  CONTROLS
  {
    { 1   ;TabControl   ;220  ;220  ;18150;3080 ;HorzGlue=Both;
                                                 VertGlue=Top;
                                                 PageNamesML=ENU=General,Filters,Options,Matrix Options }
    { 5   ;TextBox      ;3850 ;990  ;2750 ;440  ;ParentControl=1;
                                                 InPage=0;
                                                 CaptionML=ENU=Analysis View Code;
                                                 SourceExpr=CurrentItemAnalysisViewCode;
                                                 OnValidate=BEGIN
                                                              ItemAnalysisMgt.CheckAnalysisView(CurrentAnalysisArea,CurrentItemAnalysisViewCode,ItemAnalysisView)
                                                            END;

                                                 OnLookup=BEGIN
                                                            ItemAnalysisMgt.LookupItemAnalysisView(
                                                              CurrentAnalysisArea,CurrentItemAnalysisViewCode,ItemAnalysisView,ItemStatisticsBuffer,
                                                              Dim1Filter,Dim2Filter,Dim3Filter);
                                                            ItemAnalysisMgt.SetLineAndColDim(
                                                              ItemAnalysisView,LineDimCode,LineDimOption,ColumnDimCode,ColumnDimOption);
                                                            UpdateFilterFields;
                                                            CurrForm.UPDATE(FALSE);
                                                            CurrForm.UPDATECONTROLS;
                                                          END;

                                                 OnAfterValidate=BEGIN
                                                                   ItemAnalysisMgt.SetItemAnalysisView(
                                                                     CurrentAnalysisArea,CurrentItemAnalysisViewCode,ItemAnalysisView,ItemStatisticsBuffer,
                                                                     Dim1Filter,Dim2Filter,Dim3Filter);
                                                                   ItemAnalysisMgt.SetLineAndColDim(
                                                                     ItemAnalysisView,LineDimCode,LineDimOption,ColumnDimCode,ColumnDimOption);
                                                                   UpdateFilterFields;
                                                                   CurrForm.UPDATE(FALSE);
                                                                   CurrForm.UPDATECONTROLS;
                                                                 END;
                                                                  }
    { 7   ;Label        ;440  ;990  ;3300 ;440  ;ParentControl=5 }
    { 6   ;TextBox      ;3850 ;1650 ;2750 ;440  ;ParentControl=1;
                                                 InPage=0;
                                                 CaptionML=ENU=Show as Lines;
                                                 SourceExpr=LineDimCode;
                                                 OnValidate=BEGIN
                                                              IF (UPPERCASE(LineDimCode) = UPPERCASE(ColumnDimCode)) AND (LineDimCode <> '') THEN BEGIN
                                                                ColumnDimCode := '';
                                                                ItemAnalysisMgt.ValidateColumnDimCode(
                                                                  ItemAnalysisView,ColumnDimCode,ColumnDimOption,LineDimOption,
                                                                  InternalDateFilter,DateFilter,ItemStatisticsBuffer,PeriodInitialized);
                                                              END;
                                                              ItemAnalysisMgt.ValidateLineDimCode(
                                                                ItemAnalysisView,LineDimCode,LineDimOption,ColumnDimOption,
                                                                InternalDateFilter,DateFilter,ItemStatisticsBuffer,PeriodInitialized);
                                                              IF LineDimOption = LineDimOption::Period THEN
                                                                SETCURRENTKEY("Period Start")
                                                              ELSE
                                                                SETCURRENTKEY(Code);
                                                            END;

                                                 OnLookup=VAR
                                                            NewCode@1002 : Text[30];
                                                          BEGIN
                                                            NewCode := ItemAnalysisMgt.GetDimSelection(LineDimCode,ItemAnalysisView);
                                                            IF NewCode <> LineDimCode THEN BEGIN
                                                              Text := NewCode;
                                                              EXIT(TRUE);
                                                            END;
                                                          END;

                                                 OnAfterValidate=BEGIN
                                                                   CurrForm.UPDATE(FALSE);
                                                                 END;
                                                                  }
    { 8   ;Label        ;440  ;1650 ;3300 ;440  ;ParentControl=6 }
    { 9   ;TextBox      ;3850 ;2310 ;2750 ;440  ;ParentControl=1;
                                                 InPage=0;
                                                 CaptionML=ENU=Show as Columns;
                                                 SourceExpr=ColumnDimCode;
                                                 OnValidate=VAR
                                                              MATRIX_SetWanted@1001 : 'First,Previous,Same,Next';
                                                            BEGIN
                                                              IF (UPPERCASE(LineDimCode) = UPPERCASE(ColumnDimCode)) AND (LineDimCode <> '') THEN BEGIN
                                                                LineDimCode := '';
                                                                ItemAnalysisMgt.ValidateLineDimCode(
                                                                  ItemAnalysisView,LineDimCode,LineDimOption,ColumnDimOption,
                                                                  InternalDateFilter,DateFilter,ItemStatisticsBuffer,PeriodInitialized);
                                                              END;
                                                              ItemAnalysisMgt.ValidateColumnDimCode(
                                                                ItemAnalysisView,ColumnDimCode,ColumnDimOption,LineDimOption,
                                                                InternalDateFilter,DateFilter,ItemStatisticsBuffer,PeriodInitialized);

                                                              MATRIX_GenerateColumnCaptions(MATRIX_SetWanted::First);
                                                            END;

                                                 OnLookup=VAR
                                                            NewCode@1002 : Text[30];
                                                            MATRIX_SetWanted@1003 : 'First,Previous,Same,Next';
                                                          BEGIN
                                                            NewCode := ItemAnalysisMgt.GetDimSelection(ColumnDimCode,ItemAnalysisView);
                                                            IF NewCode <> ColumnDimCode THEN BEGIN
                                                              Text := NewCode;
                                                              MATRIX_GenerateColumnCaptions(MATRIX_SetWanted::First);
                                                              EXIT(TRUE);

                                                            END;
                                                          END;

                                                 OnAfterValidate=BEGIN
                                                                   CurrForm.UPDATE(FALSE);
                                                                 END;
                                                                  }
    { 10  ;Label        ;440  ;2310 ;3300 ;440  ;ParentControl=9 }
    { 47  ;TextBox      ;12980;990  ;2750 ;440  ;ParentControl=1;
                                                 InPage=0;
                                                 CaptionML=ENU=Show Value As;
                                                 OptionCaptionML=ENU=Sales Amount,Inventory Value,Quantity;
                                                 SourceExpr=ValueType }
    { 48  ;Label        ;9570 ;990  ;3300 ;440  ;ParentControl=47 }
    { 36  ;TextBox      ;3850 ;990  ;2750 ;440  ;ParentControl=1;
                                                 InPage=1;
                                                 CaptionML=ENU=Date Filter;
                                                 SourceExpr=DateFilter;
                                                 OnValidate=VAR
                                                              ApplicationManagement@1002 : Codeunit 1;
                                                              MATRIX_Step@1102601001 : 'First,Previous,Same,Next';
                                                            BEGIN
                                                              IF ApplicationManagement.MakeDateFilter(DateFilter) = 0 THEN;
                                                              ItemStatisticsBuffer.SETFILTER("Date Filter",DateFilter);
                                                              DateFilter := ItemStatisticsBuffer.GETFILTER("Date Filter");
                                                              InternalDateFilter := DateFilter;

                                                              MATRIX_GenerateColumnCaptions(MATRIX_Step::First);
                                                            END;

                                                 OnAfterValidate=BEGIN
                                                                   CurrForm.UPDATE;
                                                                 END;
                                                                  }
    { 37  ;Label        ;440  ;990  ;3300 ;440  ;ParentControl=36 }
    { 30  ;TextBox      ;12980;1540 ;2750 ;440  ;Name=Dim2Filter;
                                                 ParentControl=1;
                                                 InPage=1;
                                                 CaptionML=ENU=Dimension 2 Filter;
                                                 ClearOnLookup=No;
                                                 SourceExpr=Dim2Filter;
                                                 CaptionClass=ItemAnalysisMgt.GetCaptionClass(2,ItemAnalysisView);
                                                 OnLookup=BEGIN
                                                            EXIT(ItemAnalysisMgt.LookUpDimFilter(ItemAnalysisView."Dimension 2 Code",Text));
                                                          END;

                                                 OnAfterValidate=BEGIN
                                                                   CurrForm.UPDATE;
                                                                 END;
                                                                  }
    { 34  ;TextBox      ;12980;990  ;2750 ;440  ;Name=Dim1Filter;
                                                 ParentControl=1;
                                                 InPage=1;
                                                 CaptionML=ENU=Dimension 1 Filter;
                                                 ClearOnLookup=No;
                                                 SourceExpr=Dim1Filter;
                                                 CaptionClass=ItemAnalysisMgt.GetCaptionClass(1,ItemAnalysisView);
                                                 OnLookup=BEGIN
                                                            EXIT(ItemAnalysisMgt.LookUpDimFilter(ItemAnalysisView."Dimension 1 Code",Text));
                                                          END;

                                                 OnAfterValidate=BEGIN
                                                                   CurrForm.UPDATE;
                                                                 END;
                                                                  }
    { 46  ;TextBox      ;12980;2090 ;2750 ;440  ;Name=Dim3Filter;
                                                 ParentControl=1;
                                                 InPage=1;
                                                 CaptionML=ENU=Dimension 3 Filter;
                                                 ClearOnLookup=No;
                                                 SourceExpr=Dim3Filter;
                                                 CaptionClass=ItemAnalysisMgt.GetCaptionClass(3,ItemAnalysisView);
                                                 OnLookup=BEGIN
                                                            EXIT(ItemAnalysisMgt.LookUpDimFilter(ItemAnalysisView."Dimension 3 Code",Text));
                                                          END;

                                                 OnAfterValidate=BEGIN
                                                                   CurrForm.UPDATE;
                                                                 END;
                                                                  }
    { 38  ;TextBox      ;3850 ;1540 ;2750 ;440  ;ParentControl=1;
                                                 InPage=1;
                                                 CaptionML=ENU=Item Filter;
                                                 ClearOnLookup=No;
                                                 SourceExpr=ItemFilter;
                                                 OnLookup=VAR
                                                            ItemList@1002 : Form 31;
                                                          BEGIN
                                                            ItemList.LOOKUPMODE(TRUE);
                                                            IF ItemList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                              Text := ItemList.GetSelectionFilter;
                                                              EXIT(TRUE);
                                                            END;
                                                          END;

                                                 OnAfterValidate=BEGIN
                                                                   ItemStatisticsBuffer.SETFILTER("Item Filter",ItemFilter);
                                                                   CurrForm.UPDATE(FALSE);
                                                                 END;
                                                                  }
    { 39  ;Label        ;440  ;1540 ;3300 ;440  ;ParentControl=38 }
    { 31  ;Label        ;9570 ;990  ;3300 ;440  ;ParentControl=34;
                                                 InPage=1 }
    { 33  ;Label        ;9570 ;1540 ;3300 ;440  ;ParentControl=30;
                                                 InPage=1 }
    { 44  ;Label        ;9570 ;2090 ;3300 ;440  ;ParentControl=46;
                                                 InPage=1 }
    { 13  ;TextBox      ;3850 ;2090 ;2750 ;440  ;ParentControl=1;
                                                 InPage=1;
                                                 CaptionML=ENU=Location Filter;
                                                 ClearOnLookup=No;
                                                 SourceExpr=LocationFilter;
                                                 OnLookup=VAR
                                                            LocationList@1002 : Form 15;
                                                          BEGIN
                                                            LocationList.LOOKUPMODE(TRUE);
                                                            IF LocationList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                              Text := LocationList.GetSelectionFilter;
                                                              EXIT(TRUE);
                                                            END;
                                                          END;

                                                 OnAfterValidate=BEGIN
                                                                   ItemStatisticsBuffer.SETFILTER("Location Filter",LocationFilter);
                                                                   CurrForm.UPDATE(FALSE);
                                                                 END;
                                                                  }
    { 14  ;Label        ;440  ;2090 ;3300 ;440  ;ParentControl=13 }
    { 17  ;TextBox      ;3850 ;2640 ;2750 ;440  ;ParentControl=1;
                                                 InPage=1;
                                                 CaptionML=ENU=Budget Filter;
                                                 SourceExpr=BudgetFilter;
                                                 OnLookup=VAR
                                                            ItemBudgetName@1000 : Record 7132;
                                                          BEGIN
                                                            ItemBudgetName.FILTERGROUP := 2;
                                                            ItemBudgetName.SETRANGE("Analysis Area",CurrentAnalysisArea);
                                                            ItemBudgetName.FILTERGROUP := 0;
                                                            IF FORM.RUNMODAL(0,ItemBudgetName) = ACTION::LookupOK THEN BEGIN
                                                              Text := ItemBudgetName.Name;
                                                              EXIT(TRUE);
                                                            END;
                                                          END;

                                                 OnAfterValidate=BEGIN
                                                                   ItemStatisticsBuffer.SETFILTER("Budget Filter",BudgetFilter);
                                                                   CurrForm.UPDATE(FALSE);
                                                                 END;
                                                                  }
    { 29  ;Label        ;440  ;2640 ;3300 ;440  ;ParentControl=17 }
    { 4   ;CheckBox     ;3850 ;2090 ;440  ;440  ;ParentControl=1;
                                                 InPage=2;
                                                 ShowCaption=No;
                                                 CaptionML=ENU=Show Column Name;
                                                 SourceExpr=ShowColumnName;
                                                 OnValidate=VAR
                                                              MATRIX_Step@1000 : 'First,Previous,Same,Next';
                                                            BEGIN
                                                              MATRIX_GenerateColumnCaptions(MATRIX_Step::Same);
                                                            END;
                                                             }
    { 3   ;Label        ;440  ;2090 ;3300 ;440  ;ParentControl=4 }
    { 18  ;TextBox      ;3850 ;1540 ;2750 ;440  ;ParentControl=1;
                                                 InPage=2;
                                                 CaptionML=ENU=Rounding Factor;
                                                 OptionCaptionML=ENU=None,1,1000,1000000;
                                                 SourceExpr=RoundingFactor }
    { 28  ;Label        ;440  ;1540 ;3300 ;440  ;ParentControl=18 }
    { 32  ;TextBox      ;3850 ;990  ;2750 ;440  ;ParentControl=1;
                                                 InPage=2;
                                                 CaptionML=ENU=Show;
                                                 OptionCaptionML=ENU=Actual Amounts,Budgeted Amounts,Variance,Variance%,Index%;
                                                 SourceExpr=ShowActualBudget }
    { 35  ;Label        ;440  ;990  ;3300 ;440  ;ParentControl=32 }
    { 50  ;CheckBox     ;3850 ;2640 ;440  ;440  ;ParentControl=1;
                                                 InPage=2;
                                                 ShowCaption=No;
                                                 CaptionML=ENU=Show Opposite Sign;
                                                 SourceExpr=ShowOppositeSign }
    { 51  ;Label        ;440  ;2640 ;3300 ;440  ;ParentControl=50;
                                                 MultiLine=Yes }
    { 1140;TextBox      ;11440;990  ;3410 ;440  ;Editable=No;
                                                 ParentControl=1;
                                                 InPage=3;
                                                 CaptionML=ENU=Column Set;
                                                 SourceExpr=MATRIX_CaptionRange }
    { 1141;CommandButton;14960;880  ;550  ;550  ;VertGlue=Bottom;
                                                 FocusOnClick=No;
                                                 ParentControl=1140;
                                                 InPage=3;
                                                 ShowCaption=No;
                                                 BitmapPos=Center;
                                                 AutoRepeat=Yes;
                                                 Bitmap=15;
                                                 CaptionML=ENU=Previous Set;
                                                 ToolTipML=ENU=Previous Set;
                                                 OnPush=VAR
                                                          MATRIX_Step@1001 : 'First,Previous,Same,Next';
                                                        BEGIN
                                                          MATRIX_GenerateColumnCaptions(MATRIX_Step::Previous);
                                                        END;
                                                         }
    { 1142;Label        ;7920 ;990  ;3300 ;440  ;ParentControl=1140;
                                                 InPage=3 }
    { 1143;CommandButton;15510;880  ;550  ;550  ;VertGlue=Bottom;
                                                 FocusOnClick=No;
                                                 ParentControl=1140;
                                                 InPage=3;
                                                 ShowCaption=No;
                                                 BitmapPos=Center;
                                                 AutoRepeat=Yes;
                                                 Bitmap=16;
                                                 CaptionML=ENU=Next Set;
                                                 ToolTipML=ENU=Next Set;
                                                 OnPush=VAR
                                                          MATRIX_Step@1001 : 'First,Previous,Same,Next';
                                                        BEGIN
                                                          MATRIX_GenerateColumnCaptions(MATRIX_Step::Next);
                                                        END;
                                                         }
    { 1138;TextBox      ;3850 ;990  ;2750 ;440  ;ParentControl=1;
                                                 InPage=3;
                                                 CaptionML=ENU=View by;
                                                 OptionCaptionML=ENU=Day,Week,Month,Quarter,Year,Accounting Period;
                                                 SourceExpr=PeriodType;
                                                 OnValidate=BEGIN
                                                              FindPeriod('');
                                                            END;

                                                 OnAfterValidate=VAR
                                                                   MATRIX_Step@1001 : 'First,Previous,Next';
                                                                 BEGIN
                                                                   IF ColumnDimOption = ColumnDimOption::Period THEN
                                                                     MATRIX_GenerateColumnCaptions(MATRIX_Step::First);
                                                                 END;
                                                                  }
    { 1144;Label        ;440  ;990  ;3300 ;440  ;ParentControl=1138 }
    { 2   ;CommandButton;16170;3520 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 PushAction=FormHelp }
    { 19  ;CommandButton;11330;3520 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 CaptionML=ENU=&Show Matrix;
                                                 OnPush=VAR
                                                          MatrixForm@1129 : Form 9209;
                                                        BEGIN
                                                          CLEAR(MatrixForm);
                                                          MatrixForm.Load(MATRIX_CaptionSet,MATRIX_MatrixRecords,MATRIX_CurrentNoOfColumns,
                                                            LineDimOption,ColumnDimOption,RoundingFactor,DateFilter,
                                                            ValueType,ItemAnalysisView,CurrentItemAnalysisViewCode);
                                                          MatrixForm.RUNMODAL;
                                                        END;
                                                         }
    { 58  ;MenuButton   ;13750;3520 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 CaptionML=ENU=&Actions;
                                                 Menu=MENUITEMS
                                                 {
                                                   { ID=59;
                                                     CaptionML=ENU=Reverse Lines and Columns;
                                                     OnPush=VAR
                                                              TempDimCode@1001 : Text[30];
                                                            BEGIN
                                                              TempDimCode := ColumnDimCode;
                                                              ColumnDimCode := LineDimCode;
                                                              LineDimCode := TempDimCode;
                                                              ItemAnalysisMgt.ValidateLineDimCode(
                                                                ItemAnalysisView,LineDimCode,LineDimOption,ColumnDimOption,
                                                                InternalDateFilter,DateFilter,ItemStatisticsBuffer,PeriodInitialized);
                                                              ItemAnalysisMgt.ValidateColumnDimCode(
                                                                ItemAnalysisView,ColumnDimCode,ColumnDimOption,LineDimOption,
                                                                InternalDateFilter,DateFilter,ItemStatisticsBuffer,PeriodInitialized);
                                                            END;
                                                             }
                                                   { ID=56;
                                                     CaptionML=ENU=Export to Excel;
                                                     OnPush=VAR
                                                              ItemAnalysisViewEntry@1000 : Record 7154;
                                                              ItemAnalysisViewToExcel@1001 : Codeunit 7152;
                                                            BEGIN
                                                              ItemAnalysisViewToExcel.SetCommonFilters(
                                                                CurrentAnalysisArea,CurrentItemAnalysisViewCode,
                                                                ItemAnalysisViewEntry,DateFilter,ItemFilter,Dim1Filter,Dim2Filter,Dim3Filter,LocationFilter);
                                                              ItemAnalysisViewEntry.FIND('-');
                                                              ItemAnalysisViewToExcel.ExportData(
                                                                ItemAnalysisViewEntry,LineDimCode,ColumnDimCode,ValueType,
                                                                PeriodType,ShowColumnName,DateFilter,ItemFilter,BudgetFilter,
                                                                Dim1Filter,Dim2Filter,Dim3Filter,ShowActualBudget,LocationFilter,ShowOppositeSign);
                                                            END;
                                                             }
                                                 }
                                                  }
  }
  CODE
  {
    VAR
      MATRIX_MatrixRecords@1130 : ARRAY [32] OF Record 367;
      MATRIX_CaptionSet@1131 : ARRAY [32] OF Text[1024];
      MATRIX_CaptionRange@1132 : Text[1024];
      FirstColumn@1133 : Text[1024];
      LastColumn@1134 : Text[1024];
      MATRIX_CaptionFieldNo@1135 : Integer;
      MATRIX_PrimKeyFirstCaptionInCu@1136 : Text[1024];
      MATRIX_CurrentNoOfColumns@1137 : Integer;
      GLSetup@1025 : Record 98;
      ItemAnalysisView@1011 : Record 7152;
      ItemStatisticsBuffer@1002 : Record 5821;
      ItemAnalysisMgt@1038 : Codeunit 7153;
      CurrentAnalysisArea@1001 : 'Sales,Purchase,Inventory';
      CurrentItemAnalysisViewCode@1016 : Code[10];
      ItemFilter@1003 : Code[250];
      LocationFilter@1021 : Code[250];
      BudgetFilter@1026 : Code[250];
      ValueType@1004 : 'Sales Amount,Inventory Value,Sales Quantity';
      ShowActualBudget@1027 : 'Actual Amounts,Budgeted Amounts,Variance,Variance%,Index%';
      RoundingFactor@1005 : 'None,1,1000,1000000';
      LineDimOption@1007 : 'Item,Period,Location,Dimension 1,Dimension 2,Dimension 3';
      ColumnDimOption@1006 : 'Item,Period,Location,Dimension 1,Dimension 2,Dimension 3';
      PeriodType@1008 : 'Day,Week,Month,Quarter,Year,Accounting Period';
      Dim1Filter@1012 : Code[250];
      Dim2Filter@1010 : Code[250];
      Dim3Filter@1009 : Code[250];
      LineDimCode@1020 : Text[30];
      ColumnDimCode@1019 : Text[30];
      DateFilter@1013 : Text[30];
      InternalDateFilter@1014 : Text[30];
      PeriodInitialized@1015 : Boolean;
      AnalysisValue@1017 : Decimal;
      MatrixHeader@1018 : Text[50];
      ShowColumnName@1029 : Boolean;
      ShowOppositeSign@1030 : Boolean;
      NormalFormatString@1022 : Text[80];
      Text001@1024 : TextConst 'ENU=<Precision,';
      Text002@1023 : TextConst 'ENU=><Standard Format,0>';
      Text100@1000 : TextConst 'ENU=Period';
      NewItemAnalysisCode@1028 : Code[10];

    PROCEDURE MATRIX_GenerateColumnCaptions@1145(MATRIX_SetWanted@1000 : 'First,Previous,Same,Next');
    VAR
      CurrentMatrixRecordOrdinal@1010 : Integer;
      MatrixMgt@1009 : Codeunit 9200;
      MATRIX_PeriodRecords@1008 : ARRAY [32] OF Record 2000000007;
      i@1007 : Integer;
      Location@1006 : Record 14;
      RecRef@1005 : RecordRef;
      Item@1004 : Record 27;
      Customer@1003 : Record 18;
      Vendor@1002 : Record 23;
      ColumnCaptionValue@1001 : Integer;
    BEGIN
      CLEAR(MATRIX_CaptionSet);
      CurrentMatrixRecordOrdinal := 1;
      CLEAR(MATRIX_MatrixRecords);
      FirstColumn := '';
      LastColumn := '';

      IF ShowColumnName THEN ColumnCaptionValue := 2
        ELSE ColumnCaptionValue := 1;


      CASE ColumnDimCode OF
        Text100 :  {Period}
          BEGIN
            MatrixMgt.GeneratePeriodMatrixData(MATRIX_SetWanted,ARRAYLEN(MATRIX_CaptionSet),ShowColumnName,
                                 PeriodType,DateFilter,MATRIX_PrimKeyFirstCaptionInCu,
                                 MATRIX_CaptionSet,MATRIX_CaptionRange,MATRIX_CurrentNoOfColumns,MATRIX_PeriodRecords);
            FOR i := 1 TO ARRAYLEN(MATRIX_CaptionSet) DO BEGIN
              MATRIX_MatrixRecords[i]."Period Start" := MATRIX_PeriodRecords[i]."Period Start";
              MATRIX_MatrixRecords[i]."Period End" := MATRIX_PeriodRecords[i]."Period End";
            END;
            FirstColumn := FORMAT(MATRIX_PeriodRecords[1]."Period Start");
            LastColumn := FORMAT(MATRIX_PeriodRecords[MATRIX_CurrentNoOfColumns]."Period End");
          END;

        Location.TABLECAPTION :
          BEGIN
            CLEAR(MATRIX_CaptionSet);
            RecRef.GETTABLE(Location);
            RecRef.SETTABLE(Location);
            MatrixMgt.GenerateMatrixData(RecRef,MATRIX_SetWanted,ARRAYLEN(MATRIX_CaptionSet),1,
               MATRIX_PrimKeyFirstCaptionInCu,MATRIX_CaptionSet,MATRIX_CaptionRange,MATRIX_CurrentNoOfColumns);
            FOR i := 1 TO MATRIX_CurrentNoOfColumns DO
              MATRIX_MatrixRecords[i].Code := MATRIX_CaptionSet[i];
            IF ShowColumnName THEN
            MatrixMgt.GenerateMatrixData(RecRef,MATRIX_SetWanted::Same,ARRAYLEN(MATRIX_CaptionSet),2,
               MATRIX_PrimKeyFirstCaptionInCu,MATRIX_CaptionSet,MATRIX_CaptionRange,MATRIX_CurrentNoOfColumns);


          END;
        Item.TABLECAPTION :
          BEGIN
            CLEAR(MATRIX_CaptionSet);
            RecRef.GETTABLE(Item);
            RecRef.SETTABLE(Item);

            MatrixMgt.GenerateMatrixData(RecRef,MATRIX_SetWanted,ARRAYLEN(MATRIX_CaptionSet),1,
               MATRIX_PrimKeyFirstCaptionInCu,MATRIX_CaptionSet,MATRIX_CaptionRange,MATRIX_CurrentNoOfColumns);
            FOR i := 1 TO MATRIX_CurrentNoOfColumns DO
              MATRIX_MatrixRecords[i].Code := MATRIX_CaptionSet[i];
            IF ShowColumnName THEN
            MatrixMgt.GenerateMatrixData(RecRef,MATRIX_SetWanted::Same,ARRAYLEN(MATRIX_CaptionSet),3,
               MATRIX_PrimKeyFirstCaptionInCu,MATRIX_CaptionSet,MATRIX_CaptionRange,MATRIX_CurrentNoOfColumns);


          END;
        Customer.TABLECAPTION :
          BEGIN
            CLEAR(MATRIX_CaptionSet);
            RecRef.GETTABLE(Customer);
            RecRef.SETTABLE(Customer);
            MatrixMgt.GenerateMatrixData(RecRef,MATRIX_SetWanted,ARRAYLEN(MATRIX_CaptionSet),1,
               MATRIX_PrimKeyFirstCaptionInCu,MATRIX_CaptionSet,MATRIX_CaptionRange,MATRIX_CurrentNoOfColumns);
            FOR i := 1 TO MATRIX_CurrentNoOfColumns DO
              MATRIX_MatrixRecords[i].Code := MATRIX_CaptionSet[i];
            IF ShowColumnName THEN
            MatrixMgt.GenerateMatrixData(RecRef,MATRIX_SetWanted::Same,ARRAYLEN(MATRIX_CaptionSet),2,
               MATRIX_PrimKeyFirstCaptionInCu,MATRIX_CaptionSet,MATRIX_CaptionRange,MATRIX_CurrentNoOfColumns);

          END;
        Vendor.TABLECAPTION :
          BEGIN
            CLEAR(MATRIX_CaptionSet);
            RecRef.GETTABLE(Vendor);
            RecRef.SETTABLE(Vendor);
            MatrixMgt.GenerateMatrixData(RecRef,MATRIX_SetWanted,ARRAYLEN(MATRIX_CaptionSet),1,
               MATRIX_PrimKeyFirstCaptionInCu,MATRIX_CaptionSet,MATRIX_CaptionRange,MATRIX_CurrentNoOfColumns);
            FOR i := 1 TO MATRIX_CurrentNoOfColumns DO
              MATRIX_MatrixRecords[i].Code := MATRIX_CaptionSet[i];
            IF ShowColumnName THEN
            MatrixMgt.GenerateMatrixData(RecRef,MATRIX_SetWanted::Same,ARRAYLEN(MATRIX_CaptionSet),2,
               MATRIX_PrimKeyFirstCaptionInCu,MATRIX_CaptionSet,MATRIX_CaptionRange,MATRIX_CurrentNoOfColumns);

          END;

        ItemAnalysisView."Dimension 1 Code":
          BEGIN
            MatrixMgt.SetDimColumnSet(ItemAnalysisView."Dimension 1 Code",
                 Dim1Filter,MATRIX_SetWanted,MATRIX_PrimKeyFirstCaptionInCu,FirstColumn,LastColumn,MATRIX_CurrentNoOfColumns);
            MatrixMgt.DimToCaptions(MATRIX_CaptionSet,MATRIX_MatrixRecords,ColumnDimCode,
                 FirstColumn,LastColumn,MATRIX_CurrentNoOfColumns,ShowColumnName,MATRIX_CaptionRange);

          END;
        ItemAnalysisView."Dimension 2 Code" :
          BEGIN
            MatrixMgt.SetDimColumnSet(ItemAnalysisView."Dimension 2 Code",
                 Dim2Filter,MATRIX_SetWanted,MATRIX_PrimKeyFirstCaptionInCu,FirstColumn,LastColumn,MATRIX_CurrentNoOfColumns);
            MatrixMgt.DimToCaptions(MATRIX_CaptionSet,MATRIX_MatrixRecords,ColumnDimCode,
                 FirstColumn,LastColumn,MATRIX_CurrentNoOfColumns,ShowColumnName,MATRIX_CaptionRange);
          END;

        ItemAnalysisView."Dimension 3 Code" :
          BEGIN
            MatrixMgt.SetDimColumnSet(ItemAnalysisView."Dimension 3 Code",
                 Dim2Filter,MATRIX_SetWanted,MATRIX_PrimKeyFirstCaptionInCu,FirstColumn,LastColumn,MATRIX_CurrentNoOfColumns);
            MatrixMgt.DimToCaptions(MATRIX_CaptionSet,MATRIX_MatrixRecords,ColumnDimCode,
                 FirstColumn,LastColumn,MATRIX_CurrentNoOfColumns,ShowColumnName,MATRIX_CaptionRange);
          END;
      END;


      IF MATRIX_CurrentNoOfColumns > 0 THEN BEGIN
      END;
    END;

    LOCAL PROCEDURE FindPeriod@2(SearchText@1000 : Code[10]);
    VAR
      Item@1003 : Record 27;
      Calendar@1001 : Record 2000000007;
      PeriodFormMgt@1002 : Codeunit 359;
    BEGIN
      IF DateFilter <> '' THEN BEGIN
        Calendar.SETFILTER("Period Start",DateFilter);
        IF NOT PeriodFormMgt.FindDate('+',Calendar,PeriodType) THEN
          PeriodFormMgt.FindDate('+',Calendar,PeriodType::Day);
        Calendar.SETRANGE("Period Start");
      END;
      PeriodFormMgt.FindDate(SearchText,Calendar,PeriodType);
      Item.SETRANGE("Date Filter",Calendar."Period Start",Calendar."Period End");
      IF Item.GETRANGEMIN("Date Filter") = Item.GETRANGEMAX("Date Filter") THEN
        Item.SETRANGE("Date Filter",Item.GETRANGEMIN("Date Filter"));
      InternalDateFilter := Item.GETFILTER("Date Filter");
      IF (LineDimOption <> LineDimOption::Period) AND (ColumnDimOption <> ColumnDimOption::Period) THEN
        DateFilter := InternalDateFilter;
    END;

    LOCAL PROCEDURE UpdateFilterFields@4();
    VAR
      ItemAnalysisViewFilter@1000 : Record 7153;
    BEGIN
      ItemFilter := ItemAnalysisView."Item Filter";
      Dim1Filter := '';
      Dim2Filter := '';
      Dim3Filter := '';

      CurrForm.Dim1Filter.ENABLED := ItemAnalysisView."Dimension 1 Code" <> '';
      CurrForm.Dim2Filter.ENABLED := ItemAnalysisView."Dimension 2 Code" <> '';
      CurrForm.Dim3Filter.ENABLED := ItemAnalysisView."Dimension 3 Code" <> '';

      IF CurrForm.Dim1Filter.ENABLED THEN
        IF ItemAnalysisViewFilter.GET(
          ItemAnalysisView."Analysis Area",ItemAnalysisView.Code,ItemAnalysisView."Dimension 1 Code")
        THEN
          Dim1Filter := ItemAnalysisViewFilter."Dimension Value Filter";

      IF CurrForm.Dim2Filter.ENABLED THEN
        IF ItemAnalysisViewFilter.GET(
          ItemAnalysisView."Analysis Area",ItemAnalysisView.Code,ItemAnalysisView."Dimension 2 Code")
        THEN
          Dim2Filter := ItemAnalysisViewFilter."Dimension Value Filter";

      IF CurrForm.Dim3Filter.ENABLED THEN
        IF ItemAnalysisViewFilter.GET(
          ItemAnalysisView."Analysis Area",ItemAnalysisView.Code,ItemAnalysisView."Dimension 3 Code")
        THEN
          Dim3Filter := ItemAnalysisViewFilter."Dimension Value Filter";
    END;

    PROCEDURE SetCurrentAnalysisViewCode@1(NewAnalysisViewCode@1000 : Code[10]);
    BEGIN
      NewItemAnalysisCode := NewAnalysisViewCode;
    END;

    BEGIN
    END.
  }
}
