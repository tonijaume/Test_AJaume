OBJECT Form 7010304 Exc. comparativa compra/venta
{
  OBJECT-PROPERTIES
  {
    Date=09/10/14;
    Time=19:21:09;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    Width=19690;
    Height=11660;
    TableBoxID=1100217000;
    SourceTable=Table7009769;
    SourceTableTemporary=Yes;
    OnOpenForm=BEGIN
                 _RellenarTemporal;
               END;

  }
  CONTROLS
  {
    { 1100217000;TableBox;220 ;220  ;19250;10450;HorzGlue=Both;
                                                 VertGlue=Both;
                                                 Editable=No;
                                                 HeadingHeight=880 }
    { 1100217001;TextBox;0    ;0    ;2803 ;0    ;ParentControl=1100217000;
                                                 InColumn=Yes;
                                                 SourceExpr=Codigo }
    { 1100217002;Label  ;0    ;0    ;0    ;0    ;ParentControl=1100217001;
                                                 InColumnHeading=Yes }
    { 1100217003;TextBox;0    ;0    ;4400 ;0    ;HorzGlue=Both;
                                                 ParentControl=1100217000;
                                                 InColumn=Yes;
                                                 SourceExpr=Descripcion }
    { 1100217004;Label  ;0    ;0    ;0    ;0    ;ParentControl=1100217003;
                                                 InColumnHeading=Yes }
    { 1100217005;CheckBox;0   ;0    ;1700 ;0    ;Visible=No;
                                                 ParentControl=1100217000;
                                                 InColumn=Yes;
                                                 ShowCaption=No;
                                                 SourceExpr="Coste x ticket" }
    { 1100217006;Label  ;0    ;0    ;0    ;0    ;ParentControl=1100217005;
                                                 InColumnHeading=Yes }
    { 1000000000;TextBox;8678 ;550  ;1723 ;440  ;ParentControl=1100217000;
                                                 InColumn=Yes;
                                                 SourceExpr="Precio final AD" }
    { 1000000001;Label  ;0    ;0    ;0    ;0    ;ParentControl=1000000000;
                                                 InColumnHeading=Yes }
    { 1000000002;TextBox;10878;550  ;1760 ;440  ;ParentControl=1100217000;
                                                 InColumn=Yes;
                                                 SourceExpr="Precio final NI" }
    { 1000000003;Label  ;0    ;0    ;0    ;0    ;ParentControl=1000000002;
                                                 InColumnHeading=Yes }
    { 1100217010;TextBox;8440 ;770  ;1760 ;440  ;ParentControl=1100217000;
                                                 InColumn=Yes;
                                                 SourceExpr="Precio venta AD" }
    { 1100217011;Label  ;0    ;0    ;0    ;0    ;ParentControl=1100217010;
                                                 InColumnHeading=Yes }
    { 1100217012;TextBox;10640;770  ;1833 ;440  ;ParentControl=1100217000;
                                                 InColumn=Yes;
                                                 SourceExpr="Precio venta NI" }
    { 1100217013;Label  ;0    ;0    ;0    ;0    ;ParentControl=1100217012;
                                                 InColumnHeading=Yes }
    { 1100217014;TextBox;12840;770  ;1870 ;440  ;ParentControl=1100217000;
                                                 InColumn=Yes;
                                                 SourceExpr="EX - Precio AD" }
    { 1100217015;Label  ;0    ;0    ;0    ;0    ;ParentControl=1100217014;
                                                 InColumnHeading=Yes }
    { 1100217016;TextBox;15040;770  ;1870 ;440  ;ParentControl=1100217000;
                                                 InColumn=Yes;
                                                 SourceExpr="EX - Precio NI" }
    { 1100217017;Label  ;0    ;0    ;0    ;0    ;ParentControl=1100217016;
                                                 InColumnHeading=Yes }
    { 1100217026;TextBox;15902;660  ;1846 ;440  ;ParentControl=1100217000;
                                                 InColumn=Yes;
                                                 SourceExpr="TR - Precio AD" }
    { 1100217027;Label  ;0    ;0    ;0    ;0    ;ParentControl=1100217026;
                                                 InColumnHeading=Yes }
    { 1100217028;TextBox;18102;660  ;1760 ;440  ;ParentControl=1100217000;
                                                 InColumn=Yes;
                                                 SourceExpr="TR - Precio NI" }
    { 1100217029;Label  ;0    ;0    ;0    ;0    ;ParentControl=1100217028;
                                                 InColumnHeading=Yes }
    { 1100217018;TextBox;16272;660  ;1626 ;440  ;ParentControl=1100217000;
                                                 InColumn=Yes;
                                                 SourceExpr="Margen AD" }
    { 1100217019;Label  ;0    ;0    ;0    ;0    ;ParentControl=1100217018;
                                                 InColumnHeading=Yes }
    { 1100217020;TextBox;18472;660  ;1760 ;440  ;ParentControl=1100217000;
                                                 InColumn=Yes;
                                                 SourceExpr="Margen NI" }
    { 1100217021;Label  ;0    ;0    ;0    ;0    ;ParentControl=1100217020;
                                                 InColumnHeading=Yes }
    { 1100217022;TextBox;18362;770  ;1649 ;440  ;ParentControl=1100217000;
                                                 InColumn=Yes;
                                                 SourceExpr="Margen AD %" }
    { 1100217023;Label  ;0    ;0    ;0    ;0    ;ParentControl=1100217022;
                                                 InColumnHeading=Yes }
    { 1100217024;TextBox;20562;770  ;1760 ;440  ;ParentControl=1100217000;
                                                 InColumn=Yes;
                                                 SourceExpr="Margen NI %" }
    { 1100217025;Label  ;0    ;0    ;0    ;0    ;ParentControl=1100217024;
                                                 InColumnHeading=Yes }
    { 1100217007;CommandButton;12430;10890;2200;550;
                                                 HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 Default=Yes;
                                                 PushAction=LookupOK;
                                                 InvalidActionAppearance=Hide }
    { 1100217008;CommandButton;14850;10890;2200;550;
                                                 HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 Cancel=Yes;
                                                 PushAction=LookupCancel;
                                                 InvalidActionAppearance=Hide }
    { 1100217009;CommandButton;17270;10890;2200;550;
                                                 HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 PushAction=FormHelp }
  }
  CODE
  {
    VAR
      rPar@1100217000 : Record 7009700;
      rParPer@1100217001 : Record 7009701;
      rRsv@1100217002 : Record 7010162;

    LOCAL PROCEDURE _RellenarTemporal@1100217002();
    VAR
      lrProd@1100217000 : Record 7009710;
      lwPrecioAD@1100217001 : Decimal;
      lwPrecioNI@1100217002 : Decimal;
    BEGIN
      // _RellenarTemporal

      rPar.FINDFIRST;
      rParPer.FINDFIRST;

      lrProd.RESET;
      lrProd.SETRANGE("Tipo Producto" , lrProd."Tipo Producto"::Excursion);
      lrProd.SETRANGE(Caducado        , FALSE);
      IF lrProd.FINDSET THEN
      BEGIN
        REPEAT
          IF lrProd."Coste x ticket" THEN
          BEGIN
            INIT;
            Codigo            := lrProd.Codigo;
            Descripcion       := lrProd.Descripcion;
            "Coste x ticket"  := lrProd."Coste x ticket";
            Divisa            := rPar."Divisa venta excursiones";
            "Precio venta AD" := _GetPrecioVentaAD(Codigo, "Precio final AD");
            "Precio venta NI" := _GetPrecioVentaNI(Codigo, "Precio final NI");

            _GetCosteServicio(Codigo, lwPrecioAD, lwPrecioNI);

            "EX - Precio AD"  := lwPrecioAD;
            "EX - Precio NI"  := lwPrecioNI;

            _GetCosteTransporte(Codigo, lwPrecioAD, lwPrecioNI);

            "TR - Precio AD"  := lwPrecioAD;
            "TR - Precio NI"  := lwPrecioNI;


            IF ("EX - Precio AD" + "TR - Precio AD") <> 0 THEN
            BEGIN
              "Margen AD" := "Precio venta AD" - ("EX - Precio AD" + "TR - Precio AD");
              IF "Precio venta AD" <> 0 THEN
                "Margen AD %" := 100 - ROUND ( ("EX - Precio AD" + "TR - Precio AD") / "Precio venta AD") * 100;
            END;

            IF ("EX - Precio NI" + "TR - Precio NI") <> 0 THEN
            BEGIN
              "Margen NI" := "Precio venta NI" - ("EX - Precio NI" + "TR - Precio NI");
              IF "Precio venta NI" <> 0 THEN
                "Margen NI %" := 100 - ROUND ( ("EX - Precio NI" + "TR - Precio NI") / "Precio venta NI") * 100;
            END;

            INSERT;
          END
          //. Coste calculado a partir de un margen bruto
          //+$002 <
          ELSE
          BEGIN
            INIT;
            Codigo            := lrProd.Codigo;
            Descripcion       := lrProd.Descripcion;
            "Coste x ticket"  := lrProd."Coste x ticket";
            Divisa            := rPar."Divisa venta excursiones";
            "Precio venta AD" := _GetPrecioVentaAD(Codigo, "Precio final AD");
            "Precio venta NI" := _GetPrecioVentaNI(Codigo, "Precio final NI");

            "EX - Precio AD" := 0;
            "EX - Precio AD" := ROUND("Precio venta AD" * lrProd."Margen bruto" / 100);
            "EX - Precio AD" := "Precio venta AD" - "EX - Precio AD";

            "EX - Precio NI" := 0;
            "EX - Precio NI" := ROUND("Precio venta NI" * lrProd."Margen bruto" / 100);
            "EX - Precio NI" := "Precio venta NI" - "EX - Precio NI";

            IF ("EX - Precio AD" + "TR - Precio AD") <> 0 THEN
            BEGIN
              "Margen AD" := "Precio venta AD" - "EX - Precio AD";
              IF "Precio venta AD" <> 0 THEN
                "Margen AD %" := 100 - ROUND ("EX - Precio AD" / "Precio venta AD") * 100;
            END;

            IF ("EX - Precio NI" + "TR - Precio NI") <> 0 THEN
            BEGIN
              "Margen NI" := "Precio venta NI" - "EX - Precio NI";
              IF "Precio venta NI" <> 0 THEN
                "Margen NI %" := 100 - ROUND ("EX - Precio NI" / "Precio venta NI") * 100;
            END;

            INSERT;
          END;
          //+$002 >
        UNTIL lrProd.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE _GetPrecioVentaAD@1100217000(pwCodigo@1100217002 : Code[10];VAR pwPVP@1000000000 : Decimal) : Decimal;
    VAR
      lrTar@1100217000 : Record 7010147;
      lrPre@1100217001 : Record 7010148;
      lwPrecio@1100217003 : Decimal;
    BEGIN
      // _GetPrecioVentaAD

      rRsv.Delegacion        := rPar.Delegacion;
      rRsv.Producto          := pwCodigo;
      rRsv."Fecha Excursion" := TODAY;
      rRsv."Fecha Venta"     := TODAY;

      rRsv.busca_tarifa(lrTar);

      CLEAR(lrPre);
      lrPre.SETRANGE (Delegacion   , lrTar.Delegacion);
      lrPre.SETRANGE ("Tipo precio", lrPre."Tipo precio"::"Precio Base");
      lrPre.SETRANGE (Excursion    , lrTar.Excursion);
      lrPre.SETRANGE ("ID Tarifa"  , lrTar."ID Tarifa");
      lrPre.SETFILTER("Fecha Desde", '<=%1', TODAY); //+$001
      lrPre.SETFILTER("Fecha Hasta", '>=%1', TODAY); //+$001
      lrPre.SETRANGE (Codigo       , rParPer."Codigo Adulto");
      IF lrPre.FINDFIRST THEN
      BEGIN
        IF lrTar."Impuestos Incluidos" THEN
        BEGIN
          pwPVP := lrPre.Precio; //+$003

          lwPrecio := lrPre.Precio - lrPre."Precio exento";
          lwPrecio := ROUND(lwPrecio / _GetIVAVenta(lrTar));
          lwPrecio := lwPrecio + lrPre."Precio exento";

          EXIT(lwPrecio);
        END
        ELSE
        BEGIN
          //+$003 <
          pwPVP := lrPre.Precio - lrPre."Precio exento";
          pwPVP := ROUND(lwPrecio * _GetIVAVenta(lrTar));
          pwPVP := lwPrecio + lrPre."Precio exento";
          //+$003 >

          EXIT(lrPre.Precio);
        END;
      END;
    END;

    LOCAL PROCEDURE _GetPrecioVentaNI@1100217001(pwCodigo@1100217002 : Code[10];VAR pwPVP@1000000000 : Decimal) : Decimal;
    VAR
      lrTar@1100217000 : Record 7010147;
      lrPre@1100217001 : Record 7010148;
      lwPrecio@1100217003 : Decimal;
    BEGIN
      // _GetPrecioVentaNI

      rRsv.Delegacion        := rPar.Delegacion;
      rRsv.Producto          := pwCodigo;
      rRsv."Fecha Excursion" := TODAY;
      rRsv."Fecha Venta"     := TODAY;

      rRsv.busca_tarifa(lrTar);

      CLEAR(lrPre);
      lrPre.SETRANGE (Delegacion   , lrTar.Delegacion);
      lrPre.SETRANGE ("Tipo precio", lrPre."Tipo precio"::"Precio Base");
      lrPre.SETRANGE (Excursion    , lrTar.Excursion);
      lrPre.SETRANGE ("ID Tarifa"  , lrTar."ID Tarifa");
      lrPre.SETFILTER("Fecha Desde", '<=%1', TODAY); //+$001
      lrPre.SETFILTER("Fecha Hasta", '>=%1', TODAY); //+$001
      lrPre.SETRANGE (Codigo       , rParPer."Codigo Ni¤o");
      IF lrPre.FINDFIRST THEN
      BEGIN
        IF lrTar."Impuestos Incluidos" THEN
        BEGIN
          pwPVP := lrPre.Precio; //+$003

          lwPrecio := lrPre.Precio - lrPre."Precio exento";
          lwPrecio := ROUND(lwPrecio / _GetIVAVenta(lrTar));
          lwPrecio := lwPrecio + lrPre."Precio exento";
          EXIT(lwPrecio);
        END
        ELSE
        BEGIN
          //+$003 <
          pwPVP := lrPre.Precio - lrPre."Precio exento";
          pwPVP := ROUND(lwPrecio * _GetIVAVenta(lrTar));
          pwPVP := lwPrecio + lrPre."Precio exento";
          //+$003 >
          EXIT(lrPre.Precio);
        END;
      END;
    END;

    LOCAL PROCEDURE _GetIVAVenta@1100217004(prTar@1100217000 : Record 7010147) : Decimal;
    VAR
      lrRep@1100217001 : Record 7010241;
      lrCustomer@1100217002 : Record 18;
      lrVATSetup@1100217003 : Record 325;
    BEGIN
      // _GetIVAVenta

      //. Buscamos el primer representante de publico general

      lrRep.SETRANGE ("Trabaja a credito"  , FALSE);
      lrRep.SETRANGE (Bloqueado            , FALSE);
      lrRep.SETFILTER("Cliente Facturacion", '<>%1', '');
      lrRep.FINDFIRST;

      //. Buscamos el cliente del representante
      lrCustomer.GET(lrRep."Cliente Facturacion");

      //. Obtenemos la configuracion de IVA
      lrVATSetup.GET(lrCustomer."VAT Bus. Posting Group", prTar."Gr. Registro IVA Producto");
      EXIT(1 + (lrVATSetup."VAT+EC %" / 100) );
    END;

    LOCAL PROCEDURE _GetIVACompra@1100217008(pwProveedor@1100217003 : Code[20];pwIVAProducto@1100217002 : Code[10]) : Decimal;
    VAR
      lrVendor@1100217001 : Record 23;
      lrVATSetup@1100217000 : Record 325;
    BEGIN
      //. Buscamos el proveedor
      lrVendor.GET(pwProveedor);

      //. Obtenemos la configuracion de IVA
      lrVATSetup.GET(lrVendor."VAT Bus. Posting Group", pwIVAProducto);
      EXIT(1 + (lrVATSetup."VAT+EC %" / 100) );
    END;

    LOCAL PROCEDURE _GetCosteServicio@1100217006(pwCodigo@1100217000 : Code[10];VAR pwPrecioAD@1100217001 : Decimal;VAR pwPrecioNI@1100217003 : Decimal);
    VAR
      lrPrestCont@1100217002 : Record 7010141;
      lrContrato@1100217004 : Record 7010142;
      lrCurrExc@1100217005 : Record 330;
      lwPrecioAD@1100217006 : Decimal;
      lwPrecioNI@1100217007 : Decimal;
    BEGIN
      // _GetCosteServicio

      pwPrecioAD := 0;
      pwPrecioNI := 0;

      lrPrestCont.SETRANGE("Codigo Producto", pwCodigo);
      lrPrestCont.SETRANGE("Tipo Contrato"  , lrPrestCont."Tipo Contrato"::Excursion);
      IF lrPrestCont.FINDSET THEN
      BEGIN
        REPEAT
          lrContrato.SETRANGE(Delegacion          , rPar.Delegacion);
          lrContrato.SETRANGE("N§ Contrato compra", lrPrestCont.Contrato);
          lrContrato.SETRANGE("Tipo Contrato"     , lrContrato."Tipo Contrato"::Excursion);
          lrContrato.SETRANGE(Confirmado          , TRUE);
          IF lrContrato.FINDSET THEN
          BEGIN
            lwPrecioAD := _GetCosteServicioAD(lrContrato);
            lwPrecioNI := _GetCosteServicioNI(lrContrato);

            IF lrContrato."Impuestos incluidos" THEN
            BEGIN
              lwPrecioAD := ROUND ( lwPrecioAD / _GetIVACompra(lrContrato.Proveedor, lrContrato."Grupo registro iva prod.") );
              lwPrecioNI := ROUND ( lwPrecioNI / _GetIVACompra(lrContrato.Proveedor, lrContrato."Grupo registro iva prod.") );
            END;

            IF COPYSTR(lrContrato."Cod. divisa", 1, 2) <> COPYSTR(rPar."Divisa venta excursiones", 1, 2) THEN
            BEGIN
              lwPrecioAD := ROUND ( lrCurrExc.ExchangeAmtFCYToFCY(TODAY,
                                                                  lrContrato."Cod. divisa",
                                                                  rPar."Divisa venta excursiones",
                                                                  lwPrecioAD)
                                  );
              lwPrecioNI := ROUND ( lrCurrExc.ExchangeAmtFCYToFCY(TODAY,
                                                                  lrContrato."Cod. divisa",
                                                                  rPar."Divisa venta excursiones",
                                                                  lwPrecioNI)
                                  );
            END;
          END;

          pwPrecioAD += lwPrecioAD;
          pwPrecioNI += lwPrecioNI;
        UNTIL lrPrestCont.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE _GetCosteServicioAD@1100217003(prContrato@1100217000 : Record 7010142) : Decimal;
    VAR
      lrPre@1100217001 : Record 7010151;
    BEGIN
      lrPre.SETRANGE (Delegacion   , prContrato.Delegacion);
      lrPre.SETRANGE ("N§ Contrato", prContrato."N§ Contrato compra");
      lrPre.SETRANGE ("Tipo precio", lrPre."Tipo precio"::"Precio Excursion");
      lrPre.SETFILTER("Fecha desde", '<=%1', TODAY);
      lrPre.SETFILTER("Fecha hasta", '>=%1', TODAY);
      lrPre.SETFILTER("Rango desde", '<=%1', 1);
      lrPre.SETFILTER("Rango hasta", '>=%1', 1);
      lrPre.SETRANGE (Activo       , TRUE);
      IF lrPre.FINDFIRST THEN
        EXIT(lrPre."Precio Adulto");
    END;

    LOCAL PROCEDURE _GetCosteServicioNI@1100217005(prContrato@1100217000 : Record 7010142) : Decimal;
    VAR
      lrPre@1100217001 : Record 7010151;
    BEGIN
      lrPre.SETRANGE (Delegacion   , prContrato.Delegacion);
      lrPre.SETRANGE ("N§ Contrato", prContrato."N§ Contrato compra");
      lrPre.SETRANGE ("Tipo precio", lrPre."Tipo precio"::"Precio Excursion");
      lrPre.SETFILTER("Fecha desde", '<=%1', TODAY);
      lrPre.SETFILTER("Fecha hasta", '>=%1', TODAY);
      lrPre.SETFILTER("Rango desde", '<=%1', 1);
      lrPre.SETFILTER("Rango hasta", '>=%1', 1);
      lrPre.SETRANGE (Activo       , TRUE);
      IF lrPre.FINDFIRST THEN
        EXIT(lrPre."Precio Ni¤o");
    END;

    LOCAL PROCEDURE _GetCosteTransporte@1100217007(pwCodigo@1100217000 : Code[10];VAR pwPrecioAD@1100217001 : Decimal;VAR pwPrecioNI@1100217003 : Decimal);
    VAR
      lrPrestCont@1100217002 : Record 7010141;
      lrContrato@1100217004 : Record 7010142;
      lrCurrExc@1100217005 : Record 330;
      lwPrecioAD@1100217006 : Decimal;
      lwPrecioNI@1100217007 : Decimal;
    BEGIN
      // _GetCosteTransporte

      pwPrecioAD := 0;
      pwPrecioNI := 0;

      lrPrestCont.SETRANGE("Codigo Producto", pwCodigo);
      lrPrestCont.SETRANGE("Tipo Contrato"  , lrPrestCont."Tipo Contrato"::Transportista);
      IF lrPrestCont.FINDSET THEN
      BEGIN
        REPEAT
          lrContrato.SETRANGE(Delegacion          , rPar.Delegacion);
          lrContrato.SETRANGE("N§ Contrato compra", lrPrestCont.Contrato);
          lrContrato.SETRANGE("Tipo Contrato"     , lrContrato."Tipo Contrato"::Transportista);
          lrContrato.SETRANGE(Confirmado          , TRUE);
          IF lrContrato.FINDSET THEN
          BEGIN
            lwPrecioAD := _GetCosteTransporteAD(lrContrato);
            lwPrecioNI := _GetCosteTransporteNI(lrContrato);

            IF lrContrato."Impuestos incluidos" THEN
            BEGIN
              lwPrecioAD := ROUND ( lwPrecioAD / _GetIVACompra(lrContrato.Proveedor, lrContrato."Grupo registro iva prod.") );
              lwPrecioNI := ROUND ( lwPrecioNI / _GetIVACompra(lrContrato.Proveedor, lrContrato."Grupo registro iva prod.") );
            END;

            IF COPYSTR(lrContrato."Cod. divisa", 1, 2) <> COPYSTR(rPar."Divisa venta excursiones", 1, 2) THEN
            BEGIN
              lwPrecioAD := ROUND ( lrCurrExc.ExchangeAmtFCYToFCY(TODAY,
                                                                  lrContrato."Cod. divisa",
                                                                  rPar."Divisa venta excursiones",
                                                                  lwPrecioAD)
                                  );
              lwPrecioNI := ROUND ( lrCurrExc.ExchangeAmtFCYToFCY(TODAY,
                                                                  lrContrato."Cod. divisa",
                                                                  rPar."Divisa venta excursiones",
                                                                  lwPrecioNI)
                                  );
            END;
          END;

          pwPrecioAD += lwPrecioAD;
          pwPrecioNI += lwPrecioNI;
        UNTIL lrPrestCont.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE _GetCosteTransporteAD@1100217018(prContrato@1100217000 : Record 7010142) : Decimal;
    VAR
      lrPre@1100217001 : Record 7010150;
    BEGIN
      lrPre.SETRANGE (Delegacion    , prContrato.Delegacion);
      lrPre.SETRANGE ("N§ Contrato"  , prContrato."N§ Contrato compra");
      lrPre.SETRANGE ("Tipo precio"  , lrPre."Tipo precio"::"Precio zona");
      lrPre.SETFILTER("Fecha desde"  , '<=%1', TODAY);
      lrPre.SETFILTER("Fecha hasta"  , '>=%1', TODAY);
      lrPre.SETFILTER("Rango desde"  , '<=%1', 1);
      lrPre.SETFILTER("Rango hasta"  , '>=%1', 1);
      lrPre.SETRANGE ("Codigo suplemento", rParPer."Codigo Adulto");
      lrPre.SETRANGE (Activo         , TRUE);
      IF lrPre.FINDFIRST THEN
        EXIT(lrPre.Precio);
    END;

    LOCAL PROCEDURE _GetCosteTransporteNI@1100217017(prContrato@1100217000 : Record 7010142) : Decimal;
    VAR
      lrPre@1100217001 : Record 7010150;
    BEGIN
      lrPre.SETRANGE (Delegacion     , prContrato.Delegacion);
      lrPre.SETRANGE ("N§ Contrato" , prContrato."N§ Contrato compra");
      lrPre.SETRANGE ("Tipo precio"  , lrPre."Tipo precio"::"Precio zona");
      lrPre.SETFILTER("Fecha desde"  , '<=%1', TODAY);
      lrPre.SETFILTER("Fecha hasta"  , '>=%1', TODAY);
      lrPre.SETFILTER("Rango desde"  , '<=%1', 1);
      lrPre.SETFILTER("Rango hasta"  , '>=%1', 1);
      lrPre.SETRANGE ("Codigo suplemento", rParPer."Codigo Ni¤o");
      lrPre.SETRANGE (Activo         , TRUE);
      IF lrPre.FINDFIRST THEN
        EXIT(lrPre.Precio);
    END;

    BEGIN
    {
      $001 AJS 06032014 Al buscar precios de venta hay que buscar los vigentes a dia de hoy

      $002 AJS 24032014 CNT-OC-14033, Las excursiones que no tienen precio por ticket se calculan a partir de un margen bruto

      $003 AJS 09102014 Agregar columnas para los precios de venta con impuestos
    }
    END.
  }
}
