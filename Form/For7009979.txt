OBJECT Form 7009979 Seleccion Touroperadores
{
  OBJECT-PROPERTIES
  {
    Date=02/10/12;
    Time=12:00:00;
    Modified=Yes;
    Version List=AIC2009;
  }
  PROPERTIES
  {
    Width=16500;
    Height=6710;
    CaptionML=[ENU=Touroperator selection;
               ESP=Seleccion Touroperadores];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    TableBoxID=1103355000;
    SourceTable=Table7009717;
    OnInit=BEGIN
             Limpia;
           END;

    OnOpenForm=BEGIN
                 RESET;
               END;

    OnAfterGetRecord=BEGIN
                       wMarca := GetMarca(Rec);
                     END;

  }
  CONTROLS
  {
    { 1103355000;TableBox;220 ;220  ;16060;5500 ;HorzGlue=Both;
                                                 VertGlue=Both;
                                                 HeadingHeight=880 }
    { 1103355016;CheckBox;0   ;2750 ;1923 ;440  ;ParentControl=1103355000;
                                                 InColumn=Yes;
                                                 ShowCaption=No;
                                                 CaptionML=[ENU=Mark;
                                                            ESP=Marca];
                                                 SourceExpr=wMarca;
                                                 OnPush=BEGIN
                                                          SetMarca(Rec);
                                                        END;
                                                         }
    { 1103355017;Label  ;0    ;0    ;0    ;0    ;ParentControl=1103355016;
                                                 InColumnHeading=Yes }
    { 1103355001;TextBox;0    ;0    ;2253 ;0    ;Editable=No;
                                                 ParentControl=1103355000;
                                                 InColumn=Yes;
                                                 SourceExpr=TourOperador;
                                                 OnFormat=BEGIN
                                                            CurrForm.TourOperador.UPDATEFONTBOLD(wMarca);
                                                          END;
                                                           }
    { 1103355002;Label  ;0    ;0    ;0    ;0    ;ParentControl=1103355001;
                                                 InColumnHeading=Yes }
    { 1103355003;TextBox;0    ;0    ;4400 ;0    ;HorzGlue=Both;
                                                 Editable=No;
                                                 ParentControl=1103355000;
                                                 InColumn=Yes;
                                                 SourceExpr=Nombre;
                                                 OnFormat=BEGIN
                                                            CurrForm.Nombre.UPDATEFONTBOLD(wMarca);
                                                          END;
                                                           }
    { 1103355004;Label  ;0    ;0    ;0    ;0    ;ParentControl=1103355003;
                                                 InColumnHeading=Yes }
    { 1103355011;TextBox;8334 ;0    ;3300 ;0    ;ParentControl=1103355000;
                                                 InColumn=Yes;
                                                 SourceExpr="Grupo Touroperador";
                                                 OnFormat=BEGIN
                                                            CurrForm."Grupo Touroperador".UPDATEFONTBOLD(wMarca);
                                                          END;
                                                           }
    { 1103355012;Label  ;0    ;0    ;0    ;0    ;ParentControl=1103355011;
                                                 InColumnHeading=Yes }
    { 1103355013;CommandButton;6270;5940;2200;550;
                                                 HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 Default=Yes;
                                                 PushAction=LookupOK;
                                                 InvalidActionAppearance=Hide }
    { 1103355014;CommandButton;8690;5940;2200;550;
                                                 HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 Cancel=Yes;
                                                 PushAction=LookupCancel;
                                                 InvalidActionAppearance=Hide }
    { 1103355015;CommandButton;14080;5940;2200;550;
                                                 HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 PushAction=FormHelp }
    { 1103355018;MenuButton;11110;5940;2750;550 ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 CaptionML=[ENU=&TourOperators;
                                                            ESP=&TourOperadores];
                                                 Menu=MENUITEMS
                                                 {
                                                   { ID=1103355019;
                                                     ShortCutKey=Ctrl+F1;
                                                     CaptionML=[ENU=Mark;
                                                                ESP=Marcar];
                                                     OnPush=VAR
                                                              lrTTOs@1103355000 : Record 7009717;
                                                            BEGIN
                                                              CurrForm.SETSELECTIONFILTER(lrTTOs);
                                                              IF lrTTOs.FINDSET THEN BEGIN
                                                                wMarca := NOT wMarca;
                                                                REPEAT
                                                                  SetMarca(lrTTOs);
                                                                UNTIL lrTTOs.NEXT=0;
                                                              END;
                                                            END;
                                                             }
                                                 }
                                                  }
  }
  CODE
  {
    VAR
      rTemp@1103355000 : TEMPORARY Record 7009717;
      wMarca@1103355001 : Boolean;

    PROCEDURE Limpia@1103355002();
    BEGIN
      // Limpia
      // Borra el temporal de touroperadores y por tanto toda la seleccion

      CLEAR(rTemp);
      rTemp.DELETEALL;
    END;

    PROCEDURE SetMarca@1103355006(prRec@1103355000 : Record 7009717);
    BEGIN
      // SetMarca
      // Marcamos o desmarcamos

      IF wMarca THEN BEGIN
        IF NOT GetMarca(prRec) THEN BEGIN
         CLEAR(rTemp);
         rTemp:= prRec;
         rTemp.INSERT;
        END;
      END
      ELSE BEGIN
        IF GetMarca(prRec) THEN
          rTemp.DELETE;
      END;
    END;

    PROCEDURE GetMarca@1103355007(prRec@1103355001 : Record 7009717) : Boolean;
    VAR
      lwExist@1103355000 : Boolean;
    BEGIN
      // GetMarca
      // Buscamos si el registro actual est  seleccionado

      CLEAR(rTemp);
      rTemp:= prRec;
      lwExist := rTemp.FIND;
      EXIT(lwExist);
    END;

    PROCEDURE GetTemp@1103355000(VAR prTempTTOO@1103355000 : TEMPORARY Record 7009717) : Boolean;
    VAR
      lwEnc@1103355001 : Boolean;
    BEGIN
      // GetTemp
      // Devolvemos el temporal de Touroperadores Seleccionados
      // Devuelve true si se ha seleccionado algo

      CLEAR(prTempTTOO);
      prTempTTOO.DELETEALL;

      CLEAR(rTemp);

      lwEnc := rTemp.FINDSET;
      IF lwEnc THEN BEGIN
        REPEAT
          prTempTTOO := rTemp;
          prTempTTOO.INSERT;
        UNTIL rTemp.NEXT=0;
      END;

      EXIT(lwEnc);
    END;

    PROCEDURE SetTemp@1103355003(VAR prTempTTOO@1103355000 : TEMPORARY Record 7009717);
    BEGIN
      // SetTemp
      // Insertamos el temporal de Touroperadores Seleccionados

      CLEAR(rTemp);
      rTemp.DELETEALL;

      CLEAR(prTempTTOO);
      IF prTempTTOO.FINDSET THEN BEGIN
        REPEAT
          rTemp := prTempTTOO;
          rTemp.INSERT;
        UNTIL prTempTTOO.NEXT=0;
      END;
    END;

    PROCEDURE TextSel@1103355001() : Text[1024];
    VAR
      lwText@1103355000 : Text[1024];
      lwCont@1103355001 : Integer;
    BEGIN
      // TextSel
      // Devuelve una concatencaci¢n de touroperadores seleccionados
      // Solo los 5 primeros

      CLEAR(lwText);
      CLEAR(rTemp);
      IF rTemp.FINDSET THEN BEGIN
        CLEAR(lwCont);
        REPEAT
          lwCont +=1;
          IF lwText <> '' THEN
            lwText += ', ';
          lwText += rTemp.TourOperador;
          IF lwCont >= 5 THEN
            lwText += '...';
        UNTIL (rTemp.NEXT=0) OR (lwCont >= 5);
      END;

      EXIT(lwText);
    END;

    BEGIN
    END.
  }
}
