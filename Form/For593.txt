OBJECT Form 593 Change Log Setup (Table) List
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    Width=13310;
    Height=6710;
    CaptionML=[ENU=Change Log Setup (Table) List;
               ESP=Lista (Tabla) config. log cambios];
    InsertAllowed=No;
    DeleteAllowed=No;
    TableBoxID=1;
    SourceTable=Table2000000058;
    SourceTableTemporary=Yes;
    OnOpenForm=BEGIN
                 FILTERGROUP(2);
                 SETRANGE("Object Type","Object Type"::Table);
                 SETRANGE("Object ID",0,2000000006);
                 FILTERGROUP(0);
               END;

    OnAfterGetRecord=BEGIN
                       GetRec;
                     END;

  }
  CONTROLS
  {
    { 1   ;TableBox     ;220  ;220  ;12800;5500 ;HorzGlue=Both;
                                                 VertGlue=Both }
    { 15  ;TextBox      ;0    ;1760 ;1700 ;440  ;Editable=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 CaptionML=[ENU=ID;
                                                            ESP=ID];
                                                 SourceExpr="Object ID" }
    { 16  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=15;
                                                 InColumnHeading=Yes }
    { 19  ;TextBox      ;5882 ;2090 ;4400 ;440  ;HorzGlue=Both;
                                                 Editable=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 CaptionML=[ENU=Name;
                                                            ESP=Nombre];
                                                 SourceExpr="Object Caption" }
    { 20  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=19;
                                                 InColumnHeading=Yes }
    { 6   ;TextBox      ;0    ;0    ;1700 ;0    ;ParentControl=1;
                                                 InColumn=Yes;
                                                 CaptionML=[ENU=Log Insertion;
                                                            ESP=Inserci¢n log];
                                                 OptionCaptionML=[ENU=" ,Some Fields,All Fields";
                                                                  ESP=" ,Algunos campos,Todos los campos"];
                                                 OptionString=[ ,Some Fields,All Fields];
                                                 SourceExpr=ChangeLogSetupTable."Log Insertion";
                                                 OnValidate=VAR
                                                              NewValue@1001 : Option;
                                                            BEGIN
                                                              IF ChangeLogSetupTable."Table No." <> "Object ID" THEN
                                                              BEGIN
                                                                NewValue := ChangeLogSetupTable."Log Insertion";
                                                                GetRec;
                                                                ChangeLogSetupTable."Log Insertion" := NewValue;
                                                              END;

                                                              IF xChangeLogSetupTable.GET(ChangeLogSetupTable."Table No.") THEN
                                                              BEGIN
                                                                IF (xChangeLogSetupTable."Log Insertion" = xChangeLogSetupTable."Log Insertion"::"Some Fields") AND
                                                                   (xChangeLogSetupTable."Log Insertion" <> ChangeLogSetupTable."Log Insertion")
                                                                THEN
                                                                  IF CONFIRM(
                                                                    STRSUBSTNO(Text002,xChangeLogSetupTable.FIELDCAPTION("Log Insertion"),xChangeLogSetupTable."Log Insertion"),FALSE)
                                                                  THEN
                                                                    ChangeLogSetupTable.DelChangeLogFields(0);
                                                              END;
                                                            END;

                                                 OnAssistEdit=BEGIN
                                                                WITH ChangeLogSetupTable DO
                                                                  TESTFIELD("Log Insertion","Log Insertion"::"Some Fields");
                                                                AssistEdit;
                                                              END;

                                                 OnAfterValidate=BEGIN
                                                                   UpdateRec;
                                                                 END;
                                                                  }
    { 7   ;Label        ;0    ;0    ;0    ;0    ;ParentControl=6;
                                                 InColumnHeading=Yes }
    { 8   ;TextBox      ;0    ;0    ;1700 ;0    ;ParentControl=1;
                                                 InColumn=Yes;
                                                 CaptionML=[ENU=Log Modification;
                                                            ESP=Modificaci¢n log];
                                                 OptionCaptionML=[ENU=" ,Some Fields,All Fields";
                                                                  ESP=" ,Algunos campos,Todos los campos"];
                                                 OptionString=[ ,Some Fields,All Fields];
                                                 SourceExpr=ChangeLogSetupTable."Log Modification";
                                                 OnValidate=VAR
                                                              NewValue@1001 : Option;
                                                            BEGIN
                                                              IF ChangeLogSetupTable."Table No." <> "Object ID" THEN
                                                              BEGIN
                                                                NewValue := ChangeLogSetupTable."Log Modification";
                                                                GetRec;
                                                                ChangeLogSetupTable."Log Modification" := NewValue;
                                                              END;

                                                              IF xChangeLogSetupTable.GET(ChangeLogSetupTable."Table No.") THEN
                                                              BEGIN
                                                                IF (xChangeLogSetupTable."Log Modification" = xChangeLogSetupTable."Log Modification"::"Some Fields") AND
                                                                   (xChangeLogSetupTable."Log Modification" <> ChangeLogSetupTable."Log Modification")
                                                                THEN
                                                                  IF CONFIRM(
                                                                    STRSUBSTNO(Text002,xChangeLogSetupTable.FIELDCAPTION("Log Modification"),xChangeLogSetupTable."Log Modification"),FALSE)
                                                                  THEN
                                                                    ChangeLogSetupTable.DelChangeLogFields(1);
                                                              END;
                                                            END;

                                                 OnAssistEdit=BEGIN
                                                                WITH ChangeLogSetupTable DO
                                                                  TESTFIELD("Log Modification","Log Modification"::"Some Fields");
                                                                AssistEdit;
                                                              END;

                                                 OnAfterValidate=BEGIN
                                                                   UpdateRec;
                                                                 END;
                                                                  }
    { 9   ;Label        ;0    ;0    ;0    ;0    ;ParentControl=8;
                                                 InColumnHeading=Yes }
    { 10  ;TextBox      ;0    ;0    ;1700 ;0    ;ParentControl=1;
                                                 InColumn=Yes;
                                                 CaptionML=[ENU=Log Deletion;
                                                            ESP=Eliminaci¢n log];
                                                 OptionCaptionML=[ENU=" ,Some Fields,All Fields";
                                                                  ESP=" ,Algunos campos,Todos los campos"];
                                                 OptionString=[ ,Some Fields,All Fields];
                                                 SourceExpr=ChangeLogSetupTable."Log Deletion";
                                                 OnValidate=VAR
                                                              NewValue@1001 : Option;
                                                            BEGIN
                                                              IF ChangeLogSetupTable."Table No." <> "Object ID" THEN
                                                              BEGIN
                                                                NewValue := ChangeLogSetupTable."Log Deletion";
                                                                GetRec;
                                                                ChangeLogSetupTable."Log Deletion" := NewValue;
                                                              END;

                                                              IF xChangeLogSetupTable.GET(ChangeLogSetupTable."Table No.") THEN
                                                              BEGIN
                                                                IF (xChangeLogSetupTable."Log Deletion" = xChangeLogSetupTable."Log Deletion"::"Some Fields") AND
                                                                   (xChangeLogSetupTable."Log Deletion" <> ChangeLogSetupTable."Log Deletion")
                                                                THEN
                                                                  IF CONFIRM(
                                                                    STRSUBSTNO(Text002,xChangeLogSetupTable.FIELDCAPTION("Log Deletion"),xChangeLogSetupTable."Log Deletion"),FALSE)
                                                                  THEN
                                                                    ChangeLogSetupTable.DelChangeLogFields(2);
                                                              END;
                                                            END;

                                                 OnAssistEdit=BEGIN
                                                                WITH ChangeLogSetupTable DO
                                                                  TESTFIELD("Log Deletion","Log Deletion"::"Some Fields");
                                                                AssistEdit;
                                                              END;

                                                 OnAfterValidate=BEGIN
                                                                   UpdateRec;
                                                                 END;
                                                                  }
    { 11  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=10;
                                                 InColumnHeading=Yes }
    { 12  ;CommandButton;6050 ;5940 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 Default=Yes;
                                                 PushAction=LookupOK;
                                                 InvalidActionAppearance=Hide }
    { 13  ;CommandButton;8470 ;5940 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 Cancel=Yes;
                                                 PushAction=LookupCancel;
                                                 InvalidActionAppearance=Hide }
    { 14  ;CommandButton;10890;5940 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 PushAction=FormHelp }
  }
  CODE
  {
    VAR
      ChangeLogSetupTable@1000 : Record 403;
      xChangeLogSetupTable@1002 : Record 403;
      Text002@1003 : TextConst 'ENU=You have changed the %1 field to no longer be %2. Do you want to remove the field selections?;ESP=Ha modificado el campo %1 para que no sea %2. ¨Desea eliminar las selecciones de campo?';

    LOCAL PROCEDURE AssistEdit@1();
    VAR
      Field@1001 : Record 2000000041;
      ChangeLogSetupFieldList@1002 : Form 594;
    BEGIN
      Field.FILTERGROUP(2);
      Field.SETRANGE(TableNo,"Object ID");
      Field.FILTERGROUP(0);
      WITH ChangeLogSetupTable DO
        ChangeLogSetupFieldList.SelectColumn(
          "Log Insertion" = "Log Insertion"::"Some Fields",
          "Log Modification" = "Log Modification"::"Some Fields",
          "Log Deletion" = "Log Deletion"::"Some Fields");
      ChangeLogSetupFieldList.SETTABLEVIEW(Field);
      ChangeLogSetupFieldList.RUN;
    END;

    LOCAL PROCEDURE UpdateRec@18();
    VAR
      RecRef@1003 : RecordRef;
      xRecRef@1002 : RecordRef;
      ChangeLogMgt@1001 : Codeunit 423;
    BEGIN
      RecRef.GETTABLE(ChangeLogSetupTable);
      xRecRef.GETTABLE(xChangeLogSetupTable);
      WITH ChangeLogSetupTable DO
        IF ("Log Insertion" = "Log Insertion"::" ") AND ("Log Modification" = "Log Modification"::" ") AND
           ("Log Deletion" = "Log Deletion"::" ")
        THEN BEGIN
          IF ChangeLogSetupTable.DELETE THEN
            ChangeLogMgt.LogDeletion(xRecRef);
        END ELSE
          IF ChangeLogSetupTable.MODIFY THEN
            ChangeLogMgt.LogModification(RecRef,xRecRef)
          ELSE BEGIN
            ChangeLogSetupTable.INSERT;
            ChangeLogMgt.LogInsertion(RecRef);
          END;
    END;

    LOCAL PROCEDURE GetRec@2();
    BEGIN
      IF NOT ChangeLogSetupTable.GET("Object ID") THEN BEGIN
        ChangeLogSetupTable.INIT;
        ChangeLogSetupTable."Table No." := "Object ID";
      END;
    END;

    PROCEDURE SetSource@4();
    VAR
      AllObjWithCaption@1001 : Record 2000000058;
    BEGIN
      DELETEALL;

      AllObjWithCaption.SETCURRENTKEY("Object Type","Object ID");
      AllObjWithCaption.SETRANGE("Object Type","Object Type"::Table);
      AllObjWithCaption.SETRANGE("Object ID",0,2000000006);

      IF AllObjWithCaption.FIND('-') THEN
        REPEAT
          IF TableHasReadPermission(AllObjWithCaption."Object ID") THEN BEGIN
            Rec := AllObjWithCaption;
            INSERT;
          END;
        UNTIL AllObjWithCaption.NEXT = 0;
    END;

    LOCAL PROCEDURE TableHasReadPermission@3(TableID@1001 : Integer) : Boolean;
    VAR
      RecRef@1002 : RecordRef;
    BEGIN
      RecRef.OPEN(TableID);
      RecRef.SETPERMISSIONFILTER;
      IF NOT RecRef.READPERMISSION THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}
