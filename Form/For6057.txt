OBJECT Form 6057 Contract Line Selection
{
  OBJECT-PROPERTIES
  {
    Date=15/09/04;
    Time=12:00:00;
    Version List=NAVW14.00;
  }
  PROPERTIES
  {
    Width=16500;
    Height=7370;
    CaptionML=[ENU=Contract Line Selection;
               ESP=Selecci¢n l¡nea contrato];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    TableBoxID=1;
    LookupMode=Yes;
    SourceTable=Table5940;
    DataCaptionFields=Customer No.;
    OnOpenForm=BEGIN
                 OKButton := FALSE;
                 SETCURRENTKEY("Customer No.","Ship-to Code");
                 FILTERGROUP(2);
                 SETRANGE("Customer No.",CustomerNo);
                 SETFILTER(Status,'<>%1',Status::Defective);
                 FILTERGROUP(0);
                 SETRANGE("Ship-to Code",ShipToCode);
                 SetSelectionFilter;
               END;

    OnQueryCloseForm=BEGIN
                       IF OKButton THEN BEGIN
                         ServContract.GET(ContractType,ContractNo);
                         Rec.MARKEDONLY(TRUE);
                         ServContractLine.HideDialogBox(TRUE);
                         IF Rec.FIND('-') THEN BEGIN
                           REPEAT
                             CheckServContractLine;
                           UNTIL Rec.NEXT = 0;
                           Rec.MARKEDONLY(FALSE);
                         END ELSE BEGIN
                           Rec.MARKEDONLY(FALSE);
                           CheckServContractLine;
                         END;
                         CreateServContractLines;
                         COMMIT;
                       END;
                     END;

  }
  CONTROLS
  {
    { 1   ;TableBox     ;220  ;880  ;16060;5500 ;HorzGlue=Both;
                                                 VertGlue=Both;
                                                 Editable=No }
    { 6   ;TextBox      ;0    ;330  ;1650 ;440  ;ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="No." }
    { 7   ;Label        ;0    ;0    ;0    ;0    ;ParentControl=6;
                                                 InColumnHeading=Yes }
    { 18  ;TextBox      ;6483 ;0    ;1650 ;440  ;ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="Item No." }
    { 19  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=18;
                                                 InColumnHeading=Yes;
                                                 CaptionML=[ENU=Item No.;
                                                            ESP=N§ producto] }
    { 4   ;TextBox      ;0    ;0    ;1650 ;0    ;ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="Serial No." }
    { 5   ;Label        ;0    ;0    ;0    ;0    ;ParentControl=4;
                                                 InColumnHeading=Yes }
    { 22  ;TextBox      ;19024;2090 ;4400 ;440  ;HorzGlue=Both;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr=Description }
    { 23  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=22;
                                                 InColumnHeading=Yes }
    { 28  ;TextBox      ;6086 ;1650 ;1650 ;440  ;ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="Customer No." }
    { 29  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=28;
                                                 InColumnHeading=Yes }
    { 10  ;TextBox      ;2487 ;1760 ;1650 ;440  ;ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="Ship-to Code" }
    { 11  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=10;
                                                 InColumnHeading=Yes }
    { 14  ;TextBox      ;19288;1870 ;1650 ;440  ;Visible=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="Vendor No." }
    { 15  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=14;
                                                 InColumnHeading=Yes }
    { 8   ;TextBox      ;0    ;0    ;2200 ;0    ;Visible=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="Vendor Item No." }
    { 9   ;Label        ;0    ;0    ;0    ;0    ;ParentControl=8;
                                                 InColumnHeading=Yes }
    { 66  ;CommandButton;6820 ;6600 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 Default=Yes;
                                                 PushAction=LookupOK;
                                                 InvalidActionAppearance=Hide;
                                                 OnPush=BEGIN
                                                          OKButton := TRUE;
                                                        END;
                                                         }
    { 67  ;CommandButton;9240 ;6600 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 Cancel=Yes;
                                                 PushAction=LookupCancel;
                                                 InvalidActionAppearance=Hide }
    { 68  ;CommandButton;14080;6600 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 PushAction=FormHelp }
    { 32  ;MenuButton   ;11660;6600 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 CaptionML=[ENU=&Serv. Item;
                                                            ESP=&Prod. serv.];
                                                 Menu=MENUITEMS
                                                 {
                                                   { ID=33;
                                                     PushAction=RunObject;
                                                     ShortCutKey=May£s+F5;
                                                     CaptionML=[ENU=&Card;
                                                                ESP=&Ficha];
                                                     RunObject=Form 5980;
                                                     RunFormLinkType=OnUpdate;
                                                     RunFormOnRec=Yes;
                                                     RunFormLink=No.=FIELD(No.) }
                                                 }
                                                  }
    { 2   ;TextBox      ;3630 ;220  ;2750 ;440  ;CaptionML=[ENU=Selection Filter;
                                                            ESP=Filtro selecci¢n];
                                                 OptionCaptionML=[ENU=All Service Items,Service Items without Contract;
                                                                  ESP=Todos prod. servicio,Prod. servicio sin contrato];
                                                 SourceExpr=SelectionFilter;
                                                 OnAfterValidate=BEGIN
                                                                   CurrForm.UPDATE;
                                                                   SetSelectionFilter;
                                                                 END;
                                                                  }
    { 3   ;Label        ;220  ;220  ;3300 ;440  ;ParentControl=2 }
  }
  CODE
  {
    VAR
      ServContract@1000 : Record 5965;
      ServContractLine@1008 : Record 5964;
      ServItem@1001 : Record 5940;
      OKButton@1002 : Boolean;
      AllServItems@1003 : Boolean;
      CustomerNo@1004 : Code[20];
      ShipToCode@1005 : Code[10];
      ContractNo@1006 : Code[20];
      ContractType@1007 : Integer;
      LineNo@1009 : Integer;
      Text000@1010 : TextConst 'ENU=%1 %2 already exists in this service contract.;ESP=Ya existe %1 %2 en este contrato servicio.';
      Text001@1012 : TextConst 'ENU=%1 %2 already belongs to one or more service contracts/quotes.\\Do you want to include this service item into the document?;ESP=%1 %2 ya pertenece a una o m s ofertas/contratos servicio.\\¨Desea incluir este prod. servicio en el documento?';
      Text002@1011 : TextConst 'ENU=%1 %2 has a different ship-to code for this customer.\\Do you want to include this service item into the document?;ESP=%1 %2 tiene un C¢d. direcci¢n env¡o diferente para este cliente.\\¨Desea incluir este prod. servicio en el documento?';
      SelectionFilter@1013 : 'All Service Items,Service Items without Contract';

    PROCEDURE SetSelection@1(CustNo@1000 : Code[20];ShipNo@1001 : Code[10];CtrType@1004 : Integer;CtrNo@1002 : Code[20];AllServItem@1003 : Boolean);
    BEGIN
      AllServItems := AllServItem;
      CustomerNo := CustNo;
      ShipToCode := ShipNo;
      ContractType := CtrType;
      ContractNo := CtrNo;
    END;

    PROCEDURE FindlineNo@2() : Integer;
    BEGIN
      ServContractLine.RESET;
      ServContractLine.SETRANGE("Contract Type",ContractType);
      ServContractLine.SETRANGE("Contract No.",ContractNo);
      IF ServContractLine.FIND('+') THEN
        EXIT(ServContractLine."Line No.")
      ELSE
        EXIT(0);
    END;

    PROCEDURE CheckServContractLine@3();
    BEGIN
      ServItem.GET("No.");

      ServContractLine.RESET;
      ServContractLine.SETCURRENTKEY("Service Item No.");
      ServContractLine.SETRANGE("Contract No.",ServContract."Contract No.");
      ServContractLine.SETRANGE("Contract Type",ServContract."Contract Type");
      ServContractLine.SETRANGE("Service Item No.",ServItem."No.");
      IF ServContractLine.FIND('-') THEN BEGIN
        MESSAGE(Text000,ServItem.TABLECAPTION,ServItem."No.");
        EXIT;
      END;

      ServContractLine.RESET;
      ServContractLine.SETCURRENTKEY("Service Item No.","Contract Status");
      ServContractLine.SETRANGE("Service Item No.",ServItem."No.");
      ServContractLine.SETFILTER("Contract Status",'<>%1',ServContractLine."Contract Status"::Cancelled);
      ServContractLine.SETRANGE("Contract Type",ServContractLine."Contract Type"::Contract);
      ServContractLine.SETFILTER("Contract No.",'<>%1',ServContract."Contract No.");
      IF ServContractLine.FIND('-') THEN BEGIN
        IF NOT CONFIRM(Text001,TRUE,ServItem.TABLECAPTION,ServItem."No.") THEN
          EXIT;
      END ELSE BEGIN
        ServContractLine.RESET;
        ServContractLine.SETCURRENTKEY("Service Item No.");
        ServContractLine.SETRANGE("Service Item No.",ServItem."No.");
        ServContractLine.SETRANGE("Contract Type",ServContractLine."Contract Type"::Quote);
        ServContractLine.SETFILTER("Contract No.",'<>%1',ServContract."Contract No.");
        IF ServContractLine.FIND('-') THEN
          IF NOT CONFIRM(Text001,TRUE,ServItem.TABLECAPTION,ServItem."No.") THEN
            EXIT;
      END;

      IF (ServItem."Ship-to Code" <> ServContract."Ship-to Code") THEN
        IF NOT CONFIRM(Text002,FALSE,ServItem.TABLECAPTION,ServItem."No.") THEN
          EXIT;

      ServItem.MARK(TRUE);
    END;

    PROCEDURE CreateServContractLines@4();
    BEGIN
      ServContractLine.LOCKTABLE;
      LineNo := FindlineNo + 10000;
      ServItem.MARKEDONLY(TRUE);
      IF ServItem.FIND('-') THEN
        REPEAT
          ServContractLine.INIT;
          ServContractLine.HideDialogBox(TRUE);
          ServContractLine."Contract Type" := ServContract."Contract Type";
          ServContractLine."Contract No." := ServContract."Contract No.";
          ServContractLine."Line No." := LineNo;
          ServContractLine.SetupNewLine;
          ServContractLine.VALIDATE("Service Item No.",ServItem."No.");
          ServContractLine.VALIDATE("Line Value",ServItem."Default Contract Value");
          ServContractLine.VALIDATE("Line Discount %",ServItem."Default Contract Discount %");
          ServContractLine.INSERT(TRUE);
          LineNo := LineNo + 10000;
        UNTIL ServItem.NEXT = 0
    END;

    PROCEDURE SetSelectionFilter@5();
    BEGIN
      CASE SelectionFilter OF
        SelectionFilter::"All Service Items":
          SETRANGE("No. of Active Contracts");
        SelectionFilter::"Service Items without Contract":
          SETRANGE("No. of Active Contracts",0);
      END;
      CurrForm.UPDATE(FALSE);
    END;

    BEGIN
    END.
  }
}
