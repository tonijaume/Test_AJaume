OBJECT Form 81003 Select Objects
{
  OBJECT-PROPERTIES
  {
    Date=16/10/09;
    Time=[ 9:47:53];
    Modified=Yes;
    Version List=Ctrl2009;
  }
  PROPERTIES
  {
    Width=10340;
    Height=3850;
    CaptionML=ENU=Select Objects;
    OnOpenForm=BEGIN
                 Lisenced := TRUE;
               END;

  }
  CONTROLS
  {
    { 1000000002;CommandButton;5500;2970;2200;550;
                                                 HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 CaptionML=ENU=Select;
                                                 OnPush=BEGIN
                                                          IF DBM_TableNo = 0 THEN
                                                            ERROR(Text002,Object.FIELDCAPTION("DBM Table No."));

                                                          ExcelBufTemp.OpenBook(FileName,SheetName);
                                                          ExcelBufTemp.ReadSheet;


                                                          Wnd.OPEN(
                                                            'Process                 #1######### \' +
                                                            '@2@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@\');

                                                          ClearDBM(TRUE);
                                                          Wnd.UPDATE(1,'Mark');
                                                          ExcelBufTemp.SETFILTER("Row No.",'>1');
                                                          ExcelBufTemp.SETFILTER("Column No.",'<3');
                                                          Counter := 0;
                                                          MaxCounter := ExcelBufTemp.COUNT;
                                                          IF ExcelBufTemp.FIND('-') THEN
                                                          REPEAT
                                                            Counter += 1;
                                                            Wnd.UPDATE(2,ROUND(Counter*10000/MaxCounter,1));
                                                            IF ExcelBufTemp."Column No." = 1 THEN BEGIN
                                                              IF EVALUATE(ObjectType,ExcelBufTemp."Cell Value as Text") THEN
                                                                ObjectTypeOK := TRUE
                                                              ELSE
                                                                ObjectTypeOK := FALSE;
                                                            END;
                                                            IF ExcelBufTemp."Column No." = 2 THEN BEGIN
                                                              IF EVALUATE(ObjectID,ExcelBufTemp."Cell Value as Text") THEN
                                                                ObjectID_OK := TRUE
                                                              ELSE
                                                                ObjectID_OK := FALSE;
                                                              IF ObjectTypeOK AND ObjectID_OK THEN
                                                                IF Object.GET(ObjectType,Object."Company Name",ObjectID) THEN BEGIN
                                                                  IF CheckLisence(Object) THEN BEGIN
                                                                    Object."DBM Table No." := DBM_TableNo;
                                                                    Object.MODIFY;
                                                                  END;
                                                                END;
                                                            END;
                                                          UNTIL ExcelBufTemp.NEXT=0;
                                                          Wnd.CLOSE;
                                                          MESSAGE('To finish selection set the TABLE FILTER to:\\'+
                                                                  '   "N§ tabla DBM" = %1',DBM_TableNo);
                                                        END;
                                                         }
    { 1000000003;CommandButton;3080;2970;2200;550;
                                                 HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 CaptionML=ENU=Clear;
                                                 OnPush=BEGIN
                                                          ClearDBM(FALSE);
                                                        END;
                                                         }
    { 1000000004;TextBox;3630 ;1430 ;1700 ;440  ;CaptionML=ENU=New DBM Table No.;
                                                 SourceExpr=DBM_TableNo }
    { 1000000005;Label  ;220  ;1430 ;3300 ;440  ;ParentControl=1000000004 }
    { 1000000006;CommandButton;7920;2970;2200;550;
                                                 HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 PushAction=Close;
                                                 CaptionML=ENU=Close }
    { 1000000007;CheckBox;3630;1980 ;440  ;440  ;ShowCaption=No;
                                                 CaptionML=ENU=Lisenced Objects Only;
                                                 SourceExpr=Lisenced }
    { 1000000008;Label  ;220  ;1980 ;3300 ;440  ;ParentControl=1000000007 }
    { 1000000000;TextBox;3630 ;880  ;5500 ;440  ;CaptionML=ENU=Worksheet Name;
                                                 SourceExpr=SheetName;
                                                 OnAssistEdit=VAR
                                                                ExcelBuf@1001 : Record 370;
                                                              BEGIN
                                                                SheetName := ExcelBuf.SelectSheetsName(FileName);
                                                              END;
                                                               }
    { 1000000001;TextBox;3630 ;330  ;5500 ;440  ;CaptionML=ENU=Workbook File Name;
                                                 SourceExpr=FileName;
                                                 OnAssistEdit=VAR
                                                                CommonDialogMgt@1001 : Codeunit 412;
                                                              BEGIN
                                                                FileName := CommonDialogMgt.OpenFile(Text001,'',2,'',0);
                                                              END;
                                                               }
    { 1000000009;Label  ;220  ;330  ;3300 ;440  ;ParentControl=1000000001 }
    { 1000000010;Label  ;220  ;880  ;3300 ;440  ;ParentControl=1000000000 }
  }
  CODE
  {
    VAR
      ExcelBufTemp@1000000001 : TEMPORARY Record 370;
      Object@1000000009 : Record 2000000001;
      FileName@1000000000 : Text[250];
      SheetName@1000000019 : Text[250];
      ObjectType@1000000007 : Integer;
      ObjectID@1000000008 : Integer;
      DBM_TableNo@1000000015 : Integer;
      Counter@1000000004 : Integer;
      MaxCounter@1000000005 : Integer;
      Lisenced@1000000016 : Boolean;
      Text001@1000000020 : TextConst 'ENU=Import Excel File';
      Text002@1000000021 : TextConst 'ENU=%1 must be greater then 0.';
      ObjectTypeOK@1000000012 : Boolean;
      ObjectID_OK@1000000013 : Boolean;
      Wnd@1000000002 : Dialog;

    PROCEDURE ClearDBM@1000000011(Window@1000000000 : Boolean);
    BEGIN
      IF Window THEN
        Wnd.UPDATE(1,'Clear marks');
      Object.RESET;
      Object.SETFILTER(Type,'<>%1',Object.Type::TableData);
      Object.SETFILTER("DBM Table No.",'>%1',0);
      MaxCounter := Object.COUNT;
      Counter := 0;
      IF Object.FIND('-') THEN
        REPEAT
          Counter += 1;
          IF Window THEN
            Wnd.UPDATE(2,ROUND(Counter*10000/MaxCounter,1));
          IF CheckLisence(Object) THEN BEGIN
            Object."DBM Table No." := 0;
            Object.MODIFY;
          END;
        UNTIL Object.NEXT=0;
    END;

    PROCEDURE CheckLisence@1000000002(ObjectNew@1000000000 : Record 2000000001) : Boolean;
    VAR
      "License Permission"@1000000001 : Record 2000000043;
    BEGIN
      IF "License Permission".GET(ObjectNew.Type,ObjectNew.ID) THEN
        IF ("License Permission"."Read Permission"="License Permission"."Read Permission"::Yes) AND
           ("License Permission"."Modify Permission"="License Permission"."Modify Permission"::Yes)
          THEN
            EXIT(TRUE);
    END;

    BEGIN
    {
      SO1.00 2008-07 Eduard Sanosian
      SAN Business Solutions, The Netherlands, www.sanbs.nl
      E-mail: info@sanbs.nl
    }
    END.
  }
}
