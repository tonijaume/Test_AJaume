OBJECT Form 345 Reconciliation
{
  OBJECT-PROPERTIES
  {
    Date=13/02/07;
    Time=12:00:00;
    Version List=NAVW15.00;
  }
  PROPERTIES
  {
    Width=12100;
    Height=6710;
    Editable=No;
    CaptionML=[ENU=Reconciliation;
               ESP=Control saldos];
    TableBoxID=6;
    SourceTable=Table269;
    DataCaptionExpr=Heading;
    SourceTableTemporary=Yes;
  }
  CONTROLS
  {
    { 6   ;TableBox     ;220  ;220  ;11660;5500 ;HorzGlue=Both;
                                                 VertGlue=Both }
    { 8   ;TextBox      ;0    ;0    ;1650 ;0    ;ParentControl=6;
                                                 InColumn=Yes;
                                                 CaptionML=[ENU=Account;
                                                            ESP=Cuenta];
                                                 SourceExpr="No." }
    { 4   ;Label        ;0    ;0    ;0    ;0    ;ParentControl=8;
                                                 InColumnHeading=Yes }
    { 9   ;TextBox      ;0    ;0    ;4400 ;0    ;HorzGlue=Both;
                                                 ParentControl=6;
                                                 InColumn=Yes;
                                                 SourceExpr=Name }
    { 5   ;Label        ;0    ;0    ;0    ;0    ;ParentControl=9;
                                                 InColumnHeading=Yes }
    { 10  ;TextBox      ;0    ;0    ;2200 ;0    ;ParentControl=6;
                                                 InColumn=Yes;
                                                 SourceExpr="Net Change in Jnl." }
    { 2   ;Label        ;0    ;0    ;0    ;0    ;ParentControl=10;
                                                 InColumnHeading=Yes }
    { 11  ;TextBox      ;0    ;0    ;2200 ;0    ;ParentControl=6;
                                                 InColumn=Yes;
                                                 CaptionML=[ENU=Balance after Posting;
                                                            ESP=Saldo despu‚s del registro];
                                                 SourceExpr="Balance after Posting" }
    { 3   ;Label        ;0    ;0    ;0    ;0    ;ParentControl=11;
                                                 InColumnHeading=Yes }
    { 14  ;CommandButton;9680 ;5940 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 PushAction=FormHelp }
  }
  CODE
  {
    VAR
      GenJnlLine@1000 : Record 81;
      GLAcc@1001 : Record 15;
      BankAccPostingGr@1002 : Record 277;
      BankAcc@1003 : Record 270;
      Heading@1005 : Code[10];

    PROCEDURE SetGenJnlLine@1(VAR NewGenJnlLine@1000 : Record 81);
    BEGIN
      GenJnlLine.COPY(NewGenJnlLine);
      Heading := GenJnlLine."Journal Batch Name";
      DELETEALL;
      GLAcc.SETCURRENTKEY("Reconciliation Account");
      GLAcc.SETRANGE("Reconciliation Account",TRUE);
      IF GLAcc.FIND('-') THEN
        REPEAT
          InsertGLAccNetChange;
        UNTIL GLAcc.NEXT = 0;

      IF GenJnlLine.FIND('-') THEN
        REPEAT
          SaveNetChange(
            GenJnlLine."Account Type",GenJnlLine."Account No.",
            ROUND(GenJnlLine."Amount (LCY)" / (1 + GenJnlLine."VAT %" / 100)));
          SaveNetChange(
            GenJnlLine."Bal. Account Type",GenJnlLine."Bal. Account No.",
            -ROUND(GenJnlLine."Amount (LCY)" / (1 + GenJnlLine."Bal. VAT %" / 100)));
        UNTIL GenJnlLine.NEXT = 0;
      IF FIND('-') THEN;
    END;

    LOCAL PROCEDURE SaveNetChange@2(AccType@1000 : Integer;AccNo@1001 : Code[20];NetChange@1002 : Decimal);
    BEGIN
      IF AccNo = '' THEN
        EXIT;
      CASE  AccType OF
        GenJnlLine."Account Type"::"G/L Account":
          IF NOT GET(AccNo) THEN
            EXIT;
        GenJnlLine."Account Type"::"Bank Account":
          BEGIN
            IF AccNo <> BankAcc."No." THEN BEGIN
              BankAcc.GET(AccNo);
              BankAcc.TESTFIELD("Bank Acc. Posting Group");
              BankAccPostingGr.GET(BankAcc."Bank Acc. Posting Group");
              BankAccPostingGr.TESTFIELD("G/L Bank Account No.");
            END;
            AccNo := BankAccPostingGr."G/L Bank Account No.";
            IF NOT GET(AccNo) THEN BEGIN
              GLAcc.GET(AccNo);
              InsertGLAccNetChange;
            END;
          END;
        ELSE
          EXIT;
      END;

      "Net Change in Jnl." := "Net Change in Jnl." + NetChange;
      "Balance after Posting" := "Balance after Posting" + NetChange;
      MODIFY;
    END;

    PROCEDURE InsertGLAccNetChange@6();
    BEGIN
      GLAcc.CALCFIELDS("Balance at Date");
      INIT;
      "No." := GLAcc."No.";
      Name := GLAcc.Name;
      "Balance after Posting" := GLAcc."Balance at Date";
      INSERT;
    END;

    BEGIN
    END.
  }
}
