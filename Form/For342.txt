OBJECT Form 342 Check Availability
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    Width=9680;
    Height=10560;
    CaptionML=[ENU=Check Availability;
               ESP=Comprobaci¢n disponibilidad];
    Minimizable=No;
    Sizeable=No;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    LinksAllowed=No;
    SourceTable=Table27;
    DataCaptionExpr='';
    OnOpenForm=BEGIN
                 COPY(Item2);
                 CurrForm.No.ACTIVATE;
               END;

  }
  CONTROLS
  {
    { 18  ;CommandButton;2420 ;9790 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 PushAction=Yes }
    { 19  ;CommandButton;4840 ;9790 ;2200 ;550  ;Name=No;
                                                 HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 PushAction=No }
    { 23  ;CommandButton;7260 ;9790 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 PushAction=FormHelp }
    { 1   ;Frame        ;220  ;1870 ;9240 ;7700 ;ShowCaption=No }
    { 9   ;TextBox      ;3850 ;2200 ;2750 ;440  ;Editable=No;
                                                 ParentControl=1;
                                                 InFrame=Yes;
                                                 SourceExpr="No." }
    { 3   ;Label        ;440  ;2200 ;3300 ;440  ;ParentControl=9 }
    { 10  ;TextBox      ;3850 ;2750 ;5500 ;440  ;Editable=No;
                                                 ParentControl=1;
                                                 InFrame=Yes;
                                                 SourceExpr=Description }
    { 4   ;Label        ;440  ;2750 ;3300 ;440  ;ParentControl=10 }
    { 11  ;TextBox      ;3850 ;3410 ;2750 ;440  ;Name=InventoryQty;
                                                 Editable=No;
                                                 ParentControl=1;
                                                 InFrame=Yes;
                                                 CaptionML=[ENU=Inventory;
                                                            ESP=Existencias];
                                                 DecimalPlaces=0:5;
                                                 SourceExpr=InventoryQty;
                                                 AutoCalcField=No }
    { 6   ;Label        ;440  ;3410 ;3300 ;440  ;Name=<Control11>;
                                                 ParentControl=11 }
    { 12  ;TextBox      ;3850 ;3960 ;2750 ;440  ;Name=GrossReq;
                                                 Editable=No;
                                                 ParentControl=1;
                                                 InFrame=Yes;
                                                 CaptionML=[ENU=Gross Requirement;
                                                            ESP=Necesidades brutas];
                                                 DecimalPlaces=0:5;
                                                 SourceExpr=GrossReq;
                                                 AutoCalcField=No }
    { 7   ;Label        ;440  ;3960 ;3300 ;440  ;ParentControl=12 }
    { 13  ;TextBox      ;3850 ;4510 ;2750 ;440  ;Editable=No;
                                                 ParentControl=1;
                                                 InFrame=Yes;
                                                 CaptionML=[ENU=Scheduled Receipt;
                                                            ESP=Recepci¢n programada];
                                                 DecimalPlaces=0:5;
                                                 SourceExpr=SchedRcpt;
                                                 AutoCalcField=No }
    { 8   ;Label        ;440  ;4510 ;3300 ;440  ;ParentControl=13 }
    { 14  ;TextBox      ;3850 ;5060 ;2750 ;440  ;Editable=No;
                                                 ParentControl=1;
                                                 InFrame=Yes;
                                                 CaptionML=[ENU=Current Quantity;
                                                            ESP=Cantidad l¡n. actual];
                                                 DecimalPlaces=0:5;
                                                 SourceExpr=-ItemNetChange;
                                                 AutoCalcField=No }
    { 15  ;Label        ;440  ;5060 ;3300 ;440  ;ParentControl=14 }
    { 16  ;TextBox      ;3850 ;5610 ;2750 ;440  ;Editable=No;
                                                 ParentControl=1;
                                                 InFrame=Yes;
                                                 CaptionML=[ENU=Total Quantity;
                                                            ESP=Cantidad total];
                                                 DecimalPlaces=0:5;
                                                 SourceExpr=InitialQtyAvailable+ItemNetChange;
                                                 AutoCalcField=No }
    { 17  ;Label        ;440  ;5610 ;3300 ;440  ;ParentControl=16 }
    { 20  ;TextBox      ;3850 ;6160 ;2750 ;440  ;Editable=No;
                                                 ParentControl=1;
                                                 InFrame=Yes;
                                                 CaptionML=[ENU=Earliest Availability Date;
                                                            ESP=Fecha disponib. m s cercana];
                                                 DecimalPlaces=0:5;
                                                 SourceExpr=EarliestAvailDate;
                                                 AutoCalcField=No }
    { 21  ;Label        ;440  ;6160 ;3300 ;440  ;ParentControl=20 }
    { 22  ;CheckBox     ;3850 ;6710 ;440  ;440  ;ParentControl=1;
                                                 InFrame=Yes;
                                                 ShowCaption=No;
                                                 SourceExpr="Substitutes Exist" }
    { 24  ;Label        ;440  ;6710 ;3300 ;440  ;ParentControl=22 }
    { 25  ;TextBox      ;3850 ;7260 ;2750 ;440  ;Editable=No;
                                                 ParentControl=1;
                                                 InFrame=Yes;
                                                 Lookup=No;
                                                 SourceExpr="Variant Filter" }
    { 26  ;Label        ;440  ;7260 ;3300 ;440  ;ParentControl=25 }
    { 27  ;TextBox      ;3850 ;7810 ;2750 ;440  ;Editable=No;
                                                 ParentControl=1;
                                                 InFrame=Yes;
                                                 Lookup=No;
                                                 SourceExpr="Location Filter" }
    { 28  ;Label        ;440  ;7810 ;3300 ;440  ;ParentControl=27 }
    { 29  ;TextBox      ;3850 ;8360 ;2750 ;440  ;Editable=No;
                                                 ParentControl=1;
                                                 InFrame=Yes;
                                                 Lookup=No;
                                                 CaptionML=[ENU=Unit of Measure Code;
                                                            ESP=C¢d. unidad medida];
                                                 SourceExpr=UnitOfMeasureCode }
    { 30  ;Label        ;440  ;8360 ;3300 ;440  ;ParentControl=29 }
    { 31  ;TextBox      ;3850 ;8910 ;2750 ;440  ;Editable=No;
                                                 ParentControl=1;
                                                 InFrame=Yes;
                                                 Lookup=No;
                                                 SourceExpr="Date Filter" }
    { 32  ;Label        ;440  ;8910 ;3300 ;440  ;ParentControl=31 }
    { 2   ;Image        ;440  ;330  ;1100 ;1100 ;Bitmap=5 }
    { 5   ;Label        ;1870 ;330  ;7590 ;1100 ;MultiLine=Yes;
                                                 LeaderDots=No;
                                                 CaptionML=[ENU=The quantity on inventory is not sufficient to cover the net change in inventory. Do you still want to record the quantity?;
                                                            ESP=Las existencias disponibles no son suficientes para la cantidad solicitada. ¨Confirma que desea continuar?] }
  }
  CODE
  {
    VAR
      SalesSetup@1000 : Record 311;
      OldSalesLine@1001 : Record 37;
      OldTransLine@1002 : Record 5741;
      CompanyInfo@1003 : Record 79;
      Item2@1004 : Record 27;
      OldServiceInvLine@1012 : Record 5902;
      AvailToPromise@1015 : Codeunit 5790;
      ItemNo@1005 : Code[20];
      UnitOfMeasureCode@1006 : Code[10];
      NewItemNetChange@1007 : Decimal;
      OldItemNetChange@1008 : Decimal;
      ItemNetChange@1009 : Decimal;
      QtyPerUnitOfMeasure@1010 : Decimal;
      InitialQtyAvailable@1011 : Decimal;
      OldServItemNetChange@1013 : Decimal;
      UseOrderPromise@1014 : Boolean;
      GrossReq@1016 : Decimal;
      SchedRcpt@1017 : Decimal;
      SetupDataIsPresent@1019 : Boolean;
      AvailableQty@1020 : Decimal;
      EarliestAvailDate@1021 : Date;
      InventoryQty@1022 : Decimal;
      OldItemShipmentDate@1023 : Date;

    PROCEDURE ItemJnlLineShowWarning@1(ItemJnlLine@1000 : Record 83) : Boolean;
    BEGIN
      SalesSetup.GET;
      IF NOT SalesSetup."Stockout Warning" THEN
        EXIT(FALSE);
      CASE ItemJnlLine."Entry Type" OF
        ItemJnlLine."Entry Type"::Purchase,ItemJnlLine."Entry Type"::"Positive Adjmt.":
          ItemNetChange := ItemJnlLine.Quantity;
        ItemJnlLine."Entry Type"::Sale,ItemJnlLine."Entry Type"::"Negative Adjmt.",ItemJnlLine."Entry Type"::Transfer:
          ItemNetChange := -ItemJnlLine.Quantity;
      END;
      EXIT(
        ShowWarning(
          ItemJnlLine."Item No.",
          ItemJnlLine."Variant Code",
          ItemJnlLine."Location Code",
          ItemJnlLine."Unit of Measure Code",
          ItemJnlLine."Qty. per Unit of Measure",
          ItemNetChange,
          0,
          0D,
          0D));
    END;

    PROCEDURE SalesLineShowWarning@2(SalesLine@1000 : Record 37) : Boolean;
    BEGIN
      IF SalesLine."Drop Shipment" THEN
        EXIT(FALSE);
      SalesSetup.GET;
      IF NOT SalesSetup."Stockout Warning" THEN
        EXIT(FALSE);
      OldItemNetChange := 0;
      OldSalesLine := SalesLine;
      IF OldSalesLine.FIND THEN // Find previous quantity
        IF (OldSalesLine."Document Type" = OldSalesLine."Document Type"::Order) AND
           (OldSalesLine."No." = SalesLine."No.") AND
           (OldSalesLine."Variant Code" = SalesLine."Variant Code") AND
           (OldSalesLine."Location Code" = SalesLine."Location Code") AND
           (OldSalesLine."Bin Code" = SalesLine."Bin Code") AND
           NOT OldSalesLine."Drop Shipment"
        THEN
          OldItemNetChange := -OldSalesLine."Outstanding Qty. (Base)";

      IF SalesLine."Document Type" = SalesLine."Document Type"::Order THEN
        UseOrderPromise := TRUE;
      EXIT(
        ShowWarning(
          SalesLine."No.",
          SalesLine."Variant Code",
          SalesLine."Location Code",
          SalesLine."Unit of Measure Code",
          SalesLine."Qty. per Unit of Measure",
          -SalesLine."Outstanding Quantity",
          OldItemNetChange,
          SalesLine."Shipment Date",
          OldSalesLine."Shipment Date"));
    END;

    LOCAL PROCEDURE ShowWarning@3(ItemNo2@1000 : Code[20];ItemVariantCode@1001 : Code[10];ItemLocationCode@1002 : Code[10];UnitOfMeasureCode2@1004 : Code[10];QtyPerUnitOfMeasure2@1005 : Decimal;NewItemNetChange2@1006 : Decimal;OldItemNetChange2@1007 : Decimal;ShipmentDate@1008 : Date;OldShipmentDate@1009 : Date) : Boolean;
    BEGIN
      ItemNo := ItemNo2;
      UnitOfMeasureCode := UnitOfMeasureCode2;
      QtyPerUnitOfMeasure := QtyPerUnitOfMeasure2;
      NewItemNetChange := NewItemNetChange2;
      OldItemNetChange := ConvertQty(OldItemNetChange2);
      OldItemShipmentDate := OldShipmentDate;

      IF NewItemNetChange >= 0 THEN
        EXIT(FALSE);

      GET(ItemNo);
      SETRANGE("No.","No.");
      SETRANGE("Variant Filter",ItemVariantCode);
      SETRANGE("Location Filter",ItemLocationCode);
      SETRANGE("Drop Shipment Filter",FALSE);

      IF UseOrderPromise THEN
        SETRANGE("Date Filter",0D,ShipmentDate)
      ELSE
        SETRANGE("Date Filter",0D,WORKDATE);

      Item2.COPY(Rec);

      Calculate;
      EXIT(InitialQtyAvailable + ItemNetChange < 0);
    END;

    LOCAL PROCEDURE Calculate@4();
    BEGIN
      IF NOT SetupDataIsPresent THEN
        GetSetupData;

      AvailToPromise.QtyAvailabletoPromise(
        Rec,GrossReq,SchedRcpt,GETRANGEMAX("Date Filter"),
        CompanyInfo."Check-Avail. Time Bucket",CompanyInfo."Check-Avail. Period Calc.");

      EarliestAvailDate :=
        AvailToPromise.EarliestAvailabilityDate(
          Rec,-NewItemNetChange,GETRANGEMAX("Date Filter"),-OldItemNetChange,OldItemShipmentDate,AvailableQty,
          CompanyInfo."Check-Avail. Time Bucket",CompanyInfo."Check-Avail. Period Calc.");

      IF NOT UseOrderPromise THEN
        SchedRcpt := 0;

      CALCFIELDS(Inventory,"Reserved Qty. on Inventory");
      InventoryQty := ConvertQty(Inventory - "Reserved Qty. on Inventory");
      GrossReq :=  ConvertQty(GrossReq);
      SchedRcpt := ConvertQty(SchedRcpt);

      ItemNetChange := 0;
      IF "No." = ItemNo THEN BEGIN
        ItemNetChange := NewItemNetChange;
        GrossReq := GrossReq + OldItemNetChange;
      END;

      InitialQtyAvailable := InventoryQty + SchedRcpt - GrossReq;
    END;

    LOCAL PROCEDURE ConvertQty@31(Qty@1000 : Decimal) : Decimal;
    BEGIN
      IF QtyPerUnitOfMeasure = 0 THEN
        QtyPerUnitOfMeasure := 1;
      EXIT(ROUND(Qty / QtyPerUnitOfMeasure,0.00001));
    END;

    PROCEDURE TransferLineShowWarning@5(TransLine@1000 : Record 5741) : Boolean;
    BEGIN
      SalesSetup.GET;
      IF NOT SalesSetup."Stockout Warning" THEN
        EXIT(FALSE);

      UseOrderPromise := TRUE;

      OldTransLine := TransLine;
      IF OldTransLine.FIND THEN // Find previous quantity
        IF (OldTransLine."Item No." = TransLine."Item No.") AND
           (OldTransLine."Variant Code" = TransLine."Variant Code") AND
           (OldTransLine."Transfer-from Code" = TransLine."Transfer-from Code")
        THEN
          OldItemNetChange := -OldTransLine."Outstanding Qty. (Base)";

      EXIT(
        ShowWarning(
          TransLine."Item No.",
          TransLine."Variant Code",
          TransLine."Transfer-from Code",
          TransLine."Unit of Measure Code",
          TransLine."Qty. per Unit of Measure",
          -TransLine.Quantity,
          OldItemNetChange,
          TransLine."Shipment Date",
          OldTransLine."Shipment Date"));
    END;

    PROCEDURE ServiceInvLineShowWarning@6(ServInvLine@1000 : Record 5902) : Boolean;
    BEGIN
      SalesSetup.GET;
      IF NOT SalesSetup."Stockout Warning" THEN
        EXIT(FALSE);

      OldServItemNetChange := 0;

      OldServiceInvLine := ServInvLine;

      IF OldServiceInvLine.FIND THEN // Find previous quantity
        IF (OldServiceInvLine."No." = ServInvLine."No.") AND
           (OldServiceInvLine."Variant Code" = ServInvLine."Variant Code") AND
           (OldServiceInvLine."Location Code" = ServInvLine."Location Code") AND
           (OldServiceInvLine."Bin Code" = ServInvLine."Bin Code")
        THEN
          OldServItemNetChange := -OldServiceInvLine."Quantity (Base)";

      UseOrderPromise := TRUE;
      EXIT(
        ShowWarning(
          ServInvLine."No.",
          ServInvLine."Variant Code",
          ServInvLine."Location Code",
          ServInvLine."Unit of Measure Code",
          ServInvLine."Qty. per Unit of Measure",
          -ServInvLine.Quantity,
          OldServItemNetChange,
          ServInvLine."Posting Date",
          OldServiceInvLine."Posting Date"));
    END;

    PROCEDURE GetSetupData@7();
    BEGIN
      CompanyInfo.GET;
      SetupDataIsPresent := TRUE;
    END;

    BEGIN
    END.
  }
}
