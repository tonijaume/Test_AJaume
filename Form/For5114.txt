OBJECT Form 5114 Contact Profile Answers
{
  OBJECT-PROPERTIES
  {
    Date=26/07/02;
    Time=12:00:00;
    Version List=NAVW13.60;
  }
  PROPERTIES
  {
    Width=9350;
    Height=7370;
    CaptionML=[ENU=Contact Profile Answers;
               ESP=Respuestas perfil contacto];
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    TableBoxID=1;
    SourceTable=Table5088;
    AutoSplitKey=Yes;
    DataCaptionExpr=CaptionStr;
    OnOpenForm=BEGIN
                 IF ContactProfileAnswerCode = '' THEN
                   CurrentQuestionsChecklistCode :=
                     ProfileManagement.ProfileQuestionnaireAllowed(Cont,CurrentQuestionsChecklistCode)
                 ELSE
                   CurrentQuestionsChecklistCode := ContactProfileAnswerCode;

                 ProfileManagement.SetName(CurrentQuestionsChecklistCode,Rec,ContactProfileAnswerLine);

                 IF (Cont."Company No." <> '') AND (Cont."No." <> Cont."Company No.") THEN BEGIN
                   CaptionStr := COPYSTR(Cont."Company No." + ' ' + Cont."Company Name",1,MAXSTRLEN(CaptionStr));
                   CaptionStr := COPYSTR(CaptionStr + ' ' + Cont."No." + ' ' + Cont.Name,1,MAXSTRLEN(CaptionStr));
                 END ELSE
                   CaptionStr := COPYSTR(Cont."No." + ' ' + Cont.Name,1,MAXSTRLEN(CaptionStr));
               END;

    OnFindRecord=BEGIN
                   ProfileQuestionnaireLine2.COPY(Rec);

                   IF NOT ProfileQuestionnaireLine2.FIND(Which) THEN
                     EXIT(FALSE);

                   ProfileQuestLineQuestion := ProfileQuestionnaireLine2;
                   IF (ProfileQuestionnaireLine2.Type = Type::Answer) THEN
                     ProfileQuestLineQuestion.GET(ProfileQuestionnaireLine2."Profile Questionnaire Code",ProfileQuestLineQuestion.FindQuestionLine);

                   OK := TRUE;
                   IF ProfileQuestLineQuestion."Auto Contact Classification" THEN BEGIN
                     OK := FALSE;
                     REPEAT
                       IF Which = '+' THEN
                         GoNext := ProfileQuestionnaireLine2.NEXT(-1) <> 0
                       ELSE
                         GoNext := ProfileQuestionnaireLine2.NEXT(1) <> 0;
                       IF GoNext THEN BEGIN
                         ProfileQuestLineQuestion := ProfileQuestionnaireLine2;
                         IF (ProfileQuestionnaireLine2.Type = Type::Answer) THEN
                           ProfileQuestLineQuestion.GET(
                             ProfileQuestionnaireLine2."Profile Questionnaire Code",ProfileQuestLineQuestion.FindQuestionLine);
                         OK := NOT ProfileQuestLineQuestion."Auto Contact Classification";
                       END;
                     UNTIL (NOT GoNext) OR OK;
                   END;

                   IF NOT OK THEN
                     EXIT(FALSE)
                   ELSE BEGIN
                     Rec := ProfileQuestionnaireLine2;
                     EXIT(TRUE);
                   END;
                 END;

    OnNextRecord=VAR
                   ActualSteps@1000 : Integer;
                   Step@1001 : Integer;
                   NoOneFound@1002 : Boolean;
                 BEGIN
                   ProfileQuestionnaireLine2.COPY(Rec);

                   IF Steps > 0 THEN
                     Step := 1
                   ELSE
                     Step := -1;

                   REPEAT
                     IF ProfileQuestionnaireLine2.NEXT(Step) <> 0 THEN BEGIN
                       IF (ProfileQuestionnaireLine2.Type = Type::Answer) THEN
                         ProfileQuestLineQuestion.GET(
                           ProfileQuestionnaireLine2."Profile Questionnaire Code",ProfileQuestionnaireLine2.FindQuestionLine);
                       IF ((NOT ProfileQuestLineQuestion."Auto Contact Classification") AND
                          (ProfileQuestionnaireLine2.Type = Type::Answer)) OR
                          ((ProfileQuestionnaireLine2.Type = Type::Question) AND (NOT ProfileQuestionnaireLine2."Auto Contact Classification"))
                       THEN BEGIN
                         ActualSteps := ActualSteps + Step;
                         IF Steps <> 0 THEN
                           Rec := ProfileQuestionnaireLine2;
                       END;
                     END ELSE
                       NoOneFound := TRUE
                   UNTIL (ActualSteps = Steps) OR NoOneFound;

                   EXIT(ActualSteps);
                 END;

    OnAfterGetRecord=BEGIN
                       Set := ContProfileAnswer.GET(Cont."No.","Profile Questionnaire Code","Line No.");
                     END;

  }
  CONTROLS
  {
    { 1   ;TableBox     ;220  ;880  ;8910 ;5500 ;HorzGlue=Both;
                                                 VertGlue=Both }
    { 2   ;TextBox      ;0    ;0    ;550  ;0    ;Editable=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr=Type;
                                                 OnFormat=BEGIN
                                                            IF Type = Type::Question THEN
                                                              CurrForm.Type.UPDATEFONTBOLD := TRUE
                                                          END;
                                                           }
    { 3   ;Label        ;0    ;0    ;0    ;0    ;ParentControl=2;
                                                 InColumnHeading=Yes }
    { 4   ;TextBox      ;0    ;0    ;4400 ;0    ;HorzGlue=Both;
                                                 Editable=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr=Description;
                                                 OnFormat=BEGIN
                                                            IF Type = Type::Question THEN
                                                              CurrForm.Description.UPDATEFONTBOLD := TRUE
                                                            ELSE
                                                              CurrForm.Description.UPDATEINDENT(220);
                                                          END;
                                                           }
    { 5   ;Label        ;0    ;0    ;0    ;0    ;ParentControl=4;
                                                 InColumnHeading=Yes }
    { 9   ;TextBox      ;5292 ;2640 ;1650 ;440  ;Visible=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="No. of Contacts" }
    { 10  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=9;
                                                 InColumnHeading=Yes }
    { 15  ;CheckBox     ;3863 ;2640 ;1100 ;440  ;ParentControl=1;
                                                 InColumn=Yes;
                                                 ShowCaption=No;
                                                 CaptionML=[ENU=Set;
                                                            ESP=Selecciona];
                                                 SourceExpr=Set;
                                                 OnValidate=BEGIN
                                                              TESTFIELD(Type,Type::Answer);

                                                              IF Set THEN BEGIN
                                                                ContProfileAnswer.INIT;
                                                                ContProfileAnswer."Contact No." := Cont."No.";
                                                                ContProfileAnswer."Contact Company No." := Cont."Company No.";
                                                                ContProfileAnswer.VALIDATE("Profile Questionnaire Code",CurrentQuestionsChecklistCode);
                                                                ContProfileAnswer.VALIDATE("Line No.","Line No.");
                                                                ContProfileAnswer."Last Date Updated" := TODAY;
                                                                ContProfileAnswer.INSERT(TRUE);
                                                              END ELSE BEGIN
                                                                ContProfileAnswer.GET(Cont."No.",CurrentQuestionsChecklistCode,"Line No.");
                                                                ContProfileAnswer.DELETE(TRUE);
                                                              END;
                                                            END;
                                                             }
    { 16  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=15;
                                                 InColumnHeading=Yes }
    { 6   ;CommandButton;2090 ;6600 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 Default=Yes;
                                                 PushAction=LookupOK;
                                                 InvalidActionAppearance=Hide }
    { 7   ;CommandButton;4510 ;6600 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 Cancel=Yes;
                                                 PushAction=LookupCancel;
                                                 InvalidActionAppearance=Hide }
    { 8   ;CommandButton;6930 ;6600 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 PushAction=FormHelp }
    { 11  ;TextBox      ;3630 ;220  ;1650 ;440  ;CaptionML=[ENU=Profile Questionnaire Code;
                                                            ESP=C¢d. cuestionario perfil];
                                                 SourceExpr=CurrentQuestionsChecklistCode;
                                                 OnValidate=BEGIN
                                                              ProfileManagement.CheckName(CurrentQuestionsChecklistCode,Cont);
                                                            END;

                                                 OnLookup=BEGIN
                                                            CurrForm.SAVERECORD;
                                                              ProfileManagement.LookupName(CurrentQuestionsChecklistCode,Rec,Cont);
                                                            CurrForm.UPDATE(FALSE);
                                                          END;

                                                 OnAfterValidate=BEGIN
                                                                   CurrForm.SAVERECORD;
                                                                   ProfileManagement.SetName(CurrentQuestionsChecklistCode,Rec,0);
                                                                   CurrForm.UPDATE(FALSE);
                                                                 END;
                                                                  }
    { 12  ;Label        ;220  ;220  ;3300 ;440  ;ParentControl=11 }
  }
  CODE
  {
    VAR
      Cont@1000 : Record 5050;
      ContProfileAnswer@1001 : Record 5089;
      ProfileQuestionnaireLine2@1008 : Record 5088;
      ProfileQuestLineQuestion@1009 : Record 5088;
      ProfileManagement@1002 : Codeunit 5059;
      CurrentQuestionsChecklistCode@1003 : Code[10];
      ContactProfileAnswerCode@1006 : Code[10];
      ContactProfileAnswerLine@1007 : Integer;
      Set@1004 : Boolean;
      GoNext@1010 : Boolean;
      OK@1011 : Boolean;
      CaptionStr@1005 : Text[260];

    PROCEDURE SetParameters@1(VAR SetCont@1000 : Record 5050;SetProfileQuestionnaireCode@1001 : Code[10];SetContProfileAnswerCode@1003 : Code[10];SetContProfileAnswerLine@1002 : Integer);
    BEGIN
      Cont := SetCont;
      CurrentQuestionsChecklistCode := SetProfileQuestionnaireCode;
      ContactProfileAnswerCode := SetContProfileAnswerCode;
      ContactProfileAnswerLine := SetContProfileAnswerLine;
    END;

    BEGIN
    END.
  }
}
