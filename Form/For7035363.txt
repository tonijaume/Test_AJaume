OBJECT Form 7035363 Calendario FB
{
  OBJECT-PROPERTIES
  {
    Date=16/10/09;
    Time=12:33:01;
    Version List=TRANSFER;
  }
  PROPERTIES
  {
    Width=4895;
    Height=1430;
    BackColor=12632256;
    CaptionML=ESP=Calendario;
    Minimizable=No;
    Maximizable=No;
    Sizeable=No;
    ActiveControlOnOpen=1;
    SaveControlInfo=No;
    HorzGrid=55;
    VertGrid=55;
    OnInit=BEGIN
             intBaseYear := 1 // <= Change this date if you desire to a different starting year  ( 1..9998 )
           END;

    OnOpenForm=VAR
                 txtDayNames@1100244000 : Text[250];
                 intCounter@1100244001 : Integer;
                 dateStart@1100244002 : Date;
               BEGIN
                 txtDayNames := Text000;

                 FOR intCounter := 1 TO 7 DO
                   txtDayLabel[intCounter] := SELECTSTR(intCounter,txtDayNames);
                 IF FORMAT(dateChosen) = '' THEN
                   IF (DATE2DMY( WORKDATE, 3) >=intBaseYear) AND (DATE2DMY( WORKDATE, 3) <= 9998)  THEN
                     dateChosen := WORKDATE
                   ELSE
                     dateChosen := DMY2DATE(1,1,intBaseYear);
                 dateStart := dateChosen;

                 optMonth := DATE2DMY( dateChosen, 2 ) - 1;
                 IntYear := DATE2DMY( dateChosen, 3 );
                 UpdateDays( dateChosen, optMonth, IntYear, intDayNumber );
                 UpdateWeeks( dateChosen, optMonth, IntYear, IntWeekNumber );
                 dateChosen := dateStart;
                 UpdateFrame( DATE2DMY( dateStart,1 )+DateOffset( dateStart ), 880,1540,1320,495 );
               END;

  }
  CONTROLS
  {
    { 13  ;Shape        ;3135 ;715  ;1320 ;550  ;Name=ctlFrame;
                                                 Visible=No;
                                                 BackColor=16777215;
                                                 BackTransparent=No;
                                                 BorderColor=16711680;
                                                 BorderWidth=2pt }
    { 1   ;TextBox      ;220  ;165  ;2145 ;440  ;Name=ctlSelectMonth;
                                                 SourceExpr=optMonth;
                                                 OnAfterValidate=BEGIN
                                                                   UpdateDays( dateChosen, optMonth, IntYear, intDayNumber );
                                                                   UpdateWeeks( dateChosen, optMonth, IntYear, IntWeekNumber );
                                                                 END;
                                                                  }
    { 5   ;CommandButton;220  ;770  ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 Default=Yes;
                                                 PushAction=Close;
                                                 InvalidActionAppearance=Normal;
                                                 CaptionML=ESP=&Aceptar;
                                                 OnPush=BEGIN
                                                          dateReturnValue := dateChosen;
                                                        END;
                                                         }
    { 6   ;CommandButton;2530 ;770  ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 Cancel=No;
                                                 PushAction=Close;
                                                 InvalidActionAppearance=Normal;
                                                 CaptionML=ESP=&Cancelar;
                                                 OnPush=BEGIN
                                                          dateReturnValue := dateInitial;
                                                        END;
                                                         }
    { 17  ;CommandButton;55   ;165  ;165  ;440  ;ShowCaption=No;
                                                 HorzAlign=Center;
                                                 BackTransparent=Yes;
                                                 Border=No;
                                                 BitmapPos=Center;
                                                 AutoRepeat=Yes;
                                                 Bitmap=15;
                                                 OnPush=BEGIN
                                                          IF optMonth >0 THEN
                                                            optMonth := optMonth - 1
                                                          ELSE
                                                            IF IntYear > intBaseYear THEN
                                                              BEGIN
                                                                optMonth := 11;
                                                                IntYear  := IntYear-1
                                                              END
                                                            ELSE
                                                              BEEP(500,300);

                                                          UpdateDays( dateChosen, optMonth, IntYear, intDayNumber );
                                                          UpdateWeeks( dateChosen, optMonth, IntYear, IntWeekNumber );
                                                        END;
                                                         }
    { 18  ;CommandButton;2365 ;165  ;220  ;440  ;ShowCaption=No;
                                                 HorzAlign=Left;
                                                 BackTransparent=Yes;
                                                 Border=No;
                                                 BitmapPos=Center;
                                                 AutoRepeat=Yes;
                                                 Bitmap=16;
                                                 OnPush=VAR
                                                          i@1100244001 : Integer;
                                                        BEGIN
                                                          IF optMonth <11 THEN
                                                            optMonth := optMonth + 1
                                                          ELSE
                                                            IF IntYear < 9998 THEN
                                                              BEGIN
                                                                optMonth := 0;
                                                                IntYear := IntYear+1
                                                              END
                                                            ELSE
                                                              BEEP( 500,300);

                                                          UpdateDays( dateChosen, optMonth, IntYear, intDayNumber );
                                                          UpdateWeeks( dateChosen, optMonth, IntYear, IntWeekNumber );
                                                        END;
                                                         }
    { 14  ;CommandButton;2805 ;165  ;275  ;440  ;ShowCaption=No;
                                                 HorzAlign=Center;
                                                 BackTransparent=Yes;
                                                 Border=No;
                                                 BitmapPos=Center;
                                                 AutoRepeat=Yes;
                                                 Bitmap=15;
                                                 OnPush=BEGIN
                                                            IF IntYear > intBaseYear THEN
                                                                IntYear := IntYear-1
                                                            ELSE
                                                              BEEP(500,300);

                                                          UpdateDays( dateChosen, optMonth, IntYear, intDayNumber );
                                                          UpdateWeeks( dateChosen, optMonth, IntYear, IntWeekNumber );
                                                        END;
                                                         }
    { 15  ;CommandButton;4565 ;165  ;220  ;440  ;ShowCaption=No;
                                                 HorzAlign=Center;
                                                 BackTransparent=Yes;
                                                 Border=No;
                                                 BitmapPos=Center;
                                                 AutoRepeat=Yes;
                                                 Bitmap=16;
                                                 OnPush=BEGIN
                                                            IF IntYear < 9998 THEN
                                                              IntYear := IntYear+1
                                                            ELSE
                                                              BEEP(500,300);

                                                          UpdateDays( dateChosen, optMonth, IntYear, intDayNumber );
                                                          UpdateWeeks( dateChosen, optMonth, IntYear, IntWeekNumber );
                                                        END;
                                                         }
    { 3   ;TextBox      ;3080 ;165  ;1485 ;440  ;SourceExpr=IntYear;
                                                 OnLookup=VAR
                                                            recInteger@1100244002 : Record 2000000026;
                                                          BEGIN
                                                            //frmLU.LOOKUPMODE := TRUE;
                                                            recInteger.Number := IntYear;
                                                            recInteger.SETRANGE( Number, intBaseYear,9998 );
                                                          END;

                                                 OnAfterValidate=BEGIN
                                                                   UpdateDays( dateChosen, optMonth, IntYear, intDayNumber );
                                                                   UpdateWeeks( dateChosen, optMonth, IntYear, IntWeekNumber );
                                                                 END;
                                                                  }
  }
  CODE
  {
    VAR
      Text000@1100244009 : TextConst 'ESP=Lun,Mar,Mie,Jue,Vie,Sab,Dom';
      optMonth@1100244000 : 'Enero,Febrero,Marzo,Abril,Mayo,Junio,Julio,Agosto,Septiembre,Octubre,Noviembre,Diciembre';
      txtDayLabel@1100244001 : ARRAY [7] OF Text[30];
      intDayNumber@1100244002 : ARRAY [37] OF Integer;
      dateChosen@1100244003 : Date;
      dateReturnValue@1100244004 : Date;
      dateInitial@1100244005 : Date;
      intBaseYear@1100244006 : Integer;
      IntYear@1100244007 : Integer;
      IntWeekNumber@1100244008 : ARRAY [6] OF Integer;

    PROCEDURE UpdateDays@18(VAR p_dateSetDate@1100244000 : Date;p_optMonth@1100244001 : 'January,February,March,April,May,June,July,August,September,October,November,December';p_intYear@1100244002 : Integer;VAR p_intDayArray@1100244003 : ARRAY [37] OF Integer);
    VAR
      dateDayNumber@1100244004 : ARRAY [42] OF Integer;
      dateFirstDayOfSelection@1100244005 : Date;
      dateOpDate@1100244006 : Date;
      intDayStart@1100244007 : Integer;
      intCounter@1100244008 : Integer;
      intMonth@1100244009 : Integer;
    BEGIN
      //------------------------------------------------------------------------------------------------------------
      //WHAT:  Given a month <p_optMonth> and a year <p_optYear>, updates the array <p_intDayArray> which holds the
      //       day number for every day in that month of that year. It also sets the parameter <p_dateSetDate> to
      //       the first day of that month.
      //
      //INPUT: VAR <p_dateSetDate> : Holds the variable where we'll set the first day of the month
      //           <p_optMonth>    : Month chosen
      //           <p_intYear>     : Year chosen
      //       VAR <p_intDayArray> : Holder for the array of the days of that month
      //
      //
      //WHO:     JFC 22.06.99
      //------------------------------------------------------------------------------------------------------------

      intMonth := p_optMonth;
      dateFirstDayOfSelection := DMY2DATE( 1, 1+p_optMonth, p_intYear );
      p_dateSetDate := dateFirstDayOfSelection;
      dateOpDate := dateFirstDayOfSelection;
      FOR intCounter := 1 TO 37 DO p_intDayArray[ intCounter ] := 0;
      intCounter := 0;
      intDayStart := DATE2DWY( dateFirstDayOfSelection,1 );
      UpdateFrame( intDayStart, 880,1540,1320,495 );

      WHILE DATE2DMY( dateOpDate,2 ) = DATE2DMY( dateFirstDayOfSelection,2 ) DO
        BEGIN
          p_intDayArray[ intCounter + intDayStart ] := intCounter+1;
          dateOpDate := dateOpDate + 1;
          intCounter := intCounter + 1;
        END;
    END;

    PROCEDURE UpdateFrame@22(p_intDayNumber@1100244000 : Integer;p_intXOrigin@1100244001 : Integer;p_intYOrigin@1100244002 : Integer;p_intXStep@1100244003 : Integer;p_intYStep@1100244004 : Integer);
    BEGIN
      //------------------------------------------------------------------------------------------------------------
      //WHAT:  Given the index of a day <p_intDayNumber> and a set of origin coordinates, computes the adequate
      //       coordinates and sets the frame to the correct position
      //
      //INPUT: <p_intDayNumber> : The index of a day within a month
      //       <p_intXOrigin>,
      //       <p_intYOrigin>,
      //       <p_intXStep>,
      //       <p_intYStep>     : Coordinates of the origin and distance between days
      //
      //OUTPUT: Nothing
      //
      //WHO:    JFC 22.06.99
      //------------------------------------------------------------------------------------------------------------

      CurrForm.ctlFrame.VISIBLE := TRUE;
      CurrForm.ctlFrame.XPOS := p_intXOrigin+((p_intDayNumber-1) MOD 7)*p_intXStep - 60;
      CurrForm.ctlFrame.YPOS := p_intYOrigin+((p_intDayNumber-1) DIV 7)*p_intYStep - 50;
    END;

    PROCEDURE DateOffset@1(p_dateAnyDate@1100244000 : Date) : Integer;
    VAR
      dateFirstOfMonth@1100244001 : Date;
    BEGIN
      //------------------------------------------------------------------------------------------------------------
      //WHAT:  Given a date <p_dateAnyDate>, returns the number of days between that date and the first day of
      //       its month.
      //
      //INPUT: <p_dateAnyDate> The date whose month offset we are looking for
      //
      //OUTPUT: [Integer] -> The month offset for the parameter.
      //
      //WHO:    JFC 22.06.99
      //------------------------------------------------------------------------------------------------------------
      dateFirstOfMonth := DMY2DATE( 1, DATE2DMY(p_dateAnyDate,2), DATE2DMY(p_dateAnyDate,3)) ;
      EXIT( DATE2DWY( dateFirstOfMonth, 1 )-1);
    END;

    PROCEDURE SetStartDate@17(p_dateStart@1100244000 : Date);
    BEGIN
      //------------------------------------------------------------------------------------------------------------
      //WHAT:  Sets the three globals dateInitial, dateReturnValue, dateChosen to the value of the parameter
      //
      //INPUT: <p_dateStart> The date we want to set the globals to
      //
      //OUTPUT: Nothing
      //
      //WHO:    JFC 22.06.99
      //------------------------------------------------------------------------------------------------------------
      dateInitial     := p_dateStart;
      dateReturnValue := p_dateStart;
      dateChosen      := p_dateStart;
    END;

    PROCEDURE GetChosenDate@2() : Date;
    BEGIN
      //------------------------------------------------------------------------------------------------------------
      //WHAT:  Returns the global variable dateReturnValue
      //       Note: It is needed because functions are the only way to communicate with forms
      //
      //
      //OUTPUT: Date -> dateReturnValue; the value chosen by the user
      //
      //
      //WHO:    JFC 22.06.99
      //------------------------------------------------------------------------------------------------------------
      EXIT( dateReturnValue );
    END;

    PROCEDURE OnActivateDayLabel@7(p_intDay@1100244000 : Integer;VAR p_dateChosen@1100244001 : Date);
    BEGIN
      //------------------------------------------------------------------------------------------------------------
      //WHAT:  Perform the common code of the OnActivate triggers of the Day fields. Moves the frame over the
      //       focused field and updates the second parameter to the corresponding date.
      //
      //INPUT: <p_intDay>         : Index to the intDay[] array.
      //       VAR <p_dateChosen> : Variable which will get the chosen date.
      //
      //OUTPUT: Nothing
      //
      //GLOBALS: Uses intDayNumber[], optMonth, intYear
      //
      //WHO:    JFC 22.06.99
      //------------------------------------------------------------------------------------------------------------
      IF intDayNumber[p_intDay] <>0 THEN
        BEGIN
          UpdateFrame( p_intDay, 880,1540,1320,495 );
          CurrForm.UPDATE(FALSE);
          p_dateChosen := DMY2DATE( intDayNumber[p_intDay], optMonth+1, IntYear );
        END
    END;

    PROCEDURE UpdateWeeks@3(VAR p_dateSetDate@1100244000 : Date;p_optMonth@1100244001 : 'Enero,Febrero,Marzo,Abril,Mayo,Junio,Julio,Agosto,Setiembre,Octubre,Noviembre,Diciembre';p_intYear@1100244002 : Integer;VAR p_intWeekArray@1100244003 : ARRAY [6] OF Integer);
    VAR
      dateDayNumber@1100244004 : ARRAY [42] OF Integer;
      dateFirstWeekOfSelection@1100244005 : Date;
      dateOpDate@1100244006 : Date;
      intDayStart@1100244007 : Integer;
      intCounter@1100244008 : Integer;
      intMonth@1100244009 : Integer;
      intCounterWeek@1100244010 : Integer;
    BEGIN
      //------------------------------------------------------------------------------------------------------------
      //WHAT:  Given a month <p_optMonth> and a year <p_optYear>, updates the array <p_intWeekArray> which holds the
      //       week number for every week in that month of that year. It also sets the parameter <p_dateSetDate> to
      //       the first day of that month.
      //
      //INPUT: VAR <p_dateSetDate> : Holds the variable where we'll set the first day of the month
      //           <p_optMonth>    : Month chosen
      //           <p_intYear>     : Year chosen
      //       VAR <p_intDayArray> : Holder for the array of the days of that month
      //
      //
      //WHO:     FLB 23.10.00
      //------------------------------------------------------------------------------------------------------------

      intMonth := p_optMonth;
      dateFirstWeekOfSelection := DMY2DATE( 1, 1+p_optMonth, p_intYear );
      p_dateSetDate := dateFirstWeekOfSelection;
      dateOpDate := dateFirstWeekOfSelection;
      FOR intCounter := 1 TO 6 DO p_intWeekArray[ intCounter ] := 0;
      intCounter := 1;
      intCounterWeek := 1;
      intDayStart := DATE2DWY( dateFirstWeekOfSelection,1 );

      p_intWeekArray[1] := DATE2DWY(dateFirstWeekOfSelection, 2);

      WHILE intCounter < 36 DO
        BEGIN
          intCounter := intCounter + 7;
          intCounterWeek := intCounterWeek + 1;
          IF intDayNumber[intCounter] <> 0 THEN
            p_intWeekArray[intCounterWeek] := DATE2DWY(
                                   DMY2DATE(intDayNumber[intCounter], p_optMonth + 1, p_intYear),2);
        END;
    END;

    BEGIN
    END.
  }
}
