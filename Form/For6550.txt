OBJECT Form 6550 Whse. Item Tracking Lines
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    Permissions=TableData 6550=rimd;
    Width=12320;
    Height=10340;
    CaptionML=[ENU=Whse. Item Tracking Lines;
               ESP=L¡ns. seguim. prod. almac‚n];
    TableBoxID=1;
    SourceTable=Table6550;
    DelayedInsert=Yes;
    PopulateAllFields=Yes;
    DataCaptionFields=Item No.,Variant Code,Description;
    OnOpenForm=BEGIN
                 UpdateUndefinedQty;
                 SaveItemTrkgLine(TempInitialTrkgLine);
               END;

    OnCloseForm=BEGIN
                  IF FormUpdated THEN
                    IF NOT UpdateUndefinedQty THEN
                      RestoreInitialTrkgLine
                    ELSE
                      IF NOT CopyToReservEntry THEN
                        RestoreInitialTrkgLine;
                END;

    OnQueryCloseForm=BEGIN
                       IF FormUpdated THEN
                         IF NOT UpdateUndefinedQty THEN
                           EXIT(CONFIRM(Text002));
                     END;

    OnAfterGetCurrRecord=BEGIN
                           UpdateExpDateEditable;
                         END;

    OnInsertRecord=VAR
                     WhseItemTrackingLine2@1000 : Record 6550;
                   BEGIN
                     FormUpdated := TRUE;
                     IF ("Serial No." = '') AND ("Lot No." = '') THEN
                       EXIT(FALSE);
                     IF WhseItemTrackingLine2.FIND('+') THEN;
                     "Entry No." := WhseItemTrackingLine2."Entry No." + 1;
                     EXIT(TRUE);
                   END;

    OnModifyRecord=BEGIN
                     FormUpdated := TRUE;
                   END;

    OnDeleteRecord=BEGIN
                     FormUpdated := TRUE;
                     DELETE; // to ensure correct recalculation
                     CalculateSums;
                   END;

  }
  CONTROLS
  {
    { 1   ;TableBox     ;220  ;3850 ;11880;5610 ;HorzGlue=Both;
                                                 VertGlue=Both }
    { 22  ;TextBox      ;0    ;0    ;1650 ;0    ;HorzGlue=Both;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="Serial No.";
                                                 OnAssistEdit=VAR
                                                                MaxQuantity@1106000000 : Decimal;
                                                              BEGIN
                                                                IF ColorOfQuantityArray[1] = 0 THEN
                                                                  MaxQuantity := UndefinedQtyArray[1] + ("Quantity (Base)" - "Quantity Handled (Base)");

                                                                LookUpTrackingSummary(Rec,0,MaxQuantity,-1,TRUE);
                                                                CurrForm.UPDATE;
                                                                CalculateSums;
                                                              END;

                                                 OnAfterValidate=BEGIN
                                                                   UpdateExpDateEditable;
                                                                   CurrForm.UPDATE;
                                                                 END;
                                                                  }
    { 23  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=22;
                                                 InColumnHeading=Yes }
    { 10  ;TextBox      ;2276 ;3740 ;1650 ;440  ;HorzGlue=Both;
                                                 Visible=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="New Serial No." }
    { 11  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=10;
                                                 InColumnHeading=Yes }
    { 24  ;TextBox      ;0    ;0    ;1650 ;0    ;HorzGlue=Both;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="Lot No.";
                                                 OnAssistEdit=VAR
                                                                MaxQuantity@1106000000 : Decimal;
                                                              BEGIN
                                                                IF ColorOfQuantityArray[1] = 0 THEN
                                                                  MaxQuantity := UndefinedQtyArray[1] + ("Quantity (Base)" - "Quantity Handled (Base)");

                                                                LookUpTrackingSummary(Rec,1,MaxQuantity,-1,TRUE);
                                                                CurrForm.UPDATE;
                                                                CalculateSums;
                                                              END;

                                                 OnAfterValidate=BEGIN
                                                                   UpdateExpDateEditable;
                                                                   CurrForm.UPDATE;
                                                                 END;
                                                                  }
    { 25  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=24;
                                                 InColumnHeading=Yes }
    { 46  ;TextBox      ;3926 ;3740 ;1650 ;440  ;HorzGlue=Both;
                                                 Visible=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="New Lot No." }
    { 47  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=46;
                                                 InColumnHeading=Yes }
    { 53  ;TextBox      ;3238 ;4950 ;1650 ;440  ;Visible=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="Expiration Date";
                                                 OnFormat=BEGIN
                                                            UpdateExpDateColor;
                                                          END;
                                                           }
    { 54  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=53;
                                                 InColumnHeading=Yes }
    { 16  ;TextBox      ;6879 ;3850 ;1700 ;440  ;Visible=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="New Expiration Date" }
    { 17  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=16;
                                                 InColumnHeading=Yes }
    { 48  ;TextBox      ;1588 ;4950 ;1650 ;440  ;Visible=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="Warranty Date" }
    { 49  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=48;
                                                 InColumnHeading=Yes }
    { 2   ;TextBox      ;0    ;0    ;1650 ;0    ;Visible=No;
                                                 Editable=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="Item No." }
    { 3   ;Label        ;0    ;0    ;0    ;0    ;ParentControl=2;
                                                 InColumnHeading=Yes }
    { 4   ;TextBox      ;0    ;0    ;1650 ;0    ;Visible=No;
                                                 Editable=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="Variant Code" }
    { 5   ;Label        ;0    ;0    ;0    ;0    ;ParentControl=4;
                                                 InColumnHeading=Yes }
    { 6   ;TextBox      ;0    ;0    ;4400 ;0    ;HorzGlue=Both;
                                                 Visible=No;
                                                 Editable=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr=Description }
    { 7   ;Label        ;0    ;0    ;0    ;0    ;ParentControl=6;
                                                 InColumnHeading=Yes }
    { 8   ;TextBox      ;0    ;0    ;1650 ;0    ;Visible=No;
                                                 Editable=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="Location Code" }
    { 9   ;Label        ;0    ;0    ;0    ;0    ;ParentControl=8;
                                                 InColumnHeading=Yes }
    { 12  ;TextBox      ;0    ;0    ;1650 ;0    ;Name=Quantity;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="Quantity (Base)";
                                                 OnFormat=BEGIN
                                                            CurrForm.Quantity.UPDATEFORECOLOR("Buffer Status");
                                                          END;

                                                 OnAfterValidate=BEGIN
                                                                   CurrForm.UPDATE;
                                                                   CalculateSums;
                                                                 END;
                                                                  }
    { 13  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=12;
                                                 InColumnHeading=Yes }
    { 14  ;TextBox      ;0    ;0    ;1650 ;0    ;ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="Qty. to Handle (Base)";
                                                 OnFormat=BEGIN
                                                            CurrForm."Qty. to Handle (Base)".UPDATEFORECOLOR("Buffer Status");
                                                          END;

                                                 OnAfterValidate=BEGIN
                                                                   CurrForm.UPDATE;
                                                                   CalculateSums;
                                                                 END;
                                                                  }
    { 15  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=14;
                                                 InColumnHeading=Yes }
    { 18  ;TextBox      ;0    ;0    ;1650 ;0    ;Visible=No;
                                                 Editable=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr="Quantity Handled (Base)" }
    { 19  ;Label        ;0    ;0    ;0    ;0    ;ParentControl=18;
                                                 InColumnHeading=Yes }
    { 26  ;CommandButton;2750 ;9570 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 Default=Yes;
                                                 PushAction=LookupOK;
                                                 InvalidActionAppearance=Hide }
    { 27  ;CommandButton;5170 ;9570 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 Cancel=Yes;
                                                 PushAction=LookupCancel;
                                                 InvalidActionAppearance=Hide }
    { 28  ;CommandButton;9900 ;9570 ;2200 ;550  ;HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 PushAction=FormHelp }
    { 59  ;Frame        ;220  ;220  ;11880;2420 ;HorzGlue=Both;
                                                 ShowCaption=No }
    { 40  ;TextBox      ;7590 ;1540 ;1650 ;440  ;Name=Quantity3;
                                                 Editable=No;
                                                 ParentControl=59;
                                                 InFrame=Yes;
                                                 BlankZero=Yes;
                                                 SourceExpr=UndefinedQtyArray[1];
                                                 OnFormat=BEGIN
                                                            CurrForm.Quantity3.UPDATEFORECOLOR(ColorOfQuantityArray[1]);
                                                            CurrForm.Quantity3.UPDATEFONTBOLD(ColorOfQuantityArray[1] > 0);
                                                          END;
                                                           }
    { 35  ;TextBox      ;5720 ;1540 ;1650 ;440  ;Editable=No;
                                                 ParentControl=59;
                                                 InFrame=Yes;
                                                 CaptionML=[ENU=Quantity;
                                                            ESP=Cantidad];
                                                 DecimalPlaces=0:5;
                                                 SourceExpr=TotalWhseItemTrackingLine."Quantity (Base)" }
    { 39  ;Label        ;5720 ;440  ;1650 ;440  ;ParentControl=59;
                                                 InFrame=Yes;
                                                 LeaderDots=No;
                                                 CaptionML=[ENU=Item Tracking;
                                                            ESP=Seguim. prod.] }
    { 36  ;TextBox      ;5720 ;2090 ;1650 ;440  ;Name=Handle2;
                                                 Editable=No;
                                                 ParentControl=59;
                                                 InFrame=Yes;
                                                 CaptionML=[ENU=Qty. to Ship/Receive;
                                                            ESP=Cdad. a env./recibir];
                                                 DecimalPlaces=0:5;
                                                 SourceExpr=TotalWhseItemTrackingLine."Qty. to Handle (Base)" }
    { 41  ;TextBox      ;7590 ;2090 ;1650 ;440  ;Name=Handle3;
                                                 Editable=No;
                                                 ParentControl=59;
                                                 InFrame=Yes;
                                                 BlankZero=Yes;
                                                 SourceExpr=UndefinedQtyArray[2];
                                                 OnFormat=BEGIN
                                                            CurrForm.Handle3.UPDATEFORECOLOR(ColorOfQuantityArray[2]);
                                                            CurrForm.Handle3.UPDATEFONTBOLD(ColorOfQuantityArray[2] > 0);
                                                          END;
                                                           }
    { 29  ;TextBox      ;3850 ;1540 ;1650 ;440  ;Editable=No;
                                                 ParentControl=59;
                                                 InFrame=Yes;
                                                 CaptionML=[ENU=Quantity;
                                                            ESP=Cantidad];
                                                 DecimalPlaces=0:5;
                                                 SourceExpr=SourceQuantityArray[1] }
    { 30  ;Label        ;440  ;1540 ;3300 ;440  ;ParentControl=29 }
    { 31  ;TextBox      ;3850 ;2090 ;1650 ;440  ;Name=Handle1;
                                                 Editable=No;
                                                 ParentControl=59;
                                                 InFrame=Yes;
                                                 CaptionML=[ENU=Qty. to Handle;
                                                            ESP=Cdad.a manipular];
                                                 DecimalPlaces=0:5;
                                                 SourceExpr=SourceQuantityArray[2] }
    { 32  ;Label        ;440  ;2090 ;3300 ;440  ;Name=Handle0;
                                                 ParentControl=31 }
    { 60  ;Label        ;7590 ;440  ;1650 ;440  ;ParentControl=59;
                                                 InFrame=Yes;
                                                 LeaderDots=No;
                                                 CaptionML=[ENU=Undefined;
                                                            ESP=Indefinido] }
    { 38  ;TextBox      ;3850 ;990  ;1650 ;440  ;Editable=No;
                                                 Focusable=No;
                                                 ParentControl=59;
                                                 InFrame=Yes;
                                                 Border=No;
                                                 BorderStyle=Normal;
                                                 SourceExpr=TextCaption }
    { 50  ;Label        ;3850 ;440  ;1650 ;440  ;ParentControl=59;
                                                 InFrame=Yes;
                                                 LeaderDots=No;
                                                 CaptionML=[ENU=Source;
                                                            ESP=Origen] }
    { 51  ;TextBox      ;5720 ;990  ;1650 ;440  ;Visible=No;
                                                 ParentControl=59;
                                                 InFrame=Yes;
                                                 ForeColor=65535;
                                                 SourceExpr=Text003 }
    { 52  ;TextBox      ;7590 ;990  ;1650 ;440  ;Visible=No;
                                                 ParentControl=59;
                                                 InFrame=Yes;
                                                 ForeColor=65535;
                                                 SourceExpr=Text003 }
    { 20  ;MenuButton   ;7590 ;9570 ;2200 ;550  ;Name=ButtonLine;
                                                 HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 CaptionML=[ENU=&Line;
                                                            ESP=&L¡nea];
                                                 Menu=MENUITEMS
                                                 {
                                                   { ID=21;
                                                     PushAction=RunObject;
                                                     CaptionML=[ENU=Serial No. Information Card;
                                                                ESP=Ficha informaci¢n n§ serie];
                                                     RunObject=Form 6504;
                                                     RunFormLinkType=OnUpdate;
                                                     RunFormLink=Item No.=FIELD(Item No.),
                                                                 Variant Code=FIELD(Variant Code),
                                                                 Serial No.=FIELD(Serial No.);
                                                     OnPush=BEGIN
                                                              TESTFIELD("Serial No.");
                                                            END;
                                                             }
                                                   { ID=33;
                                                     PushAction=RunObject;
                                                     CaptionML=[ENU=Lot No. Information Card;
                                                                ESP=Ficha informaci¢n n§ lote];
                                                     RunObject=Form 6505;
                                                     RunFormLinkType=OnUpdate;
                                                     RunFormLink=Item No.=FIELD(Item No.),
                                                                 Variant Code=FIELD(Variant Code),
                                                                 Lot No.=FIELD(Lot No.);
                                                     OnPush=BEGIN
                                                              TESTFIELD("Lot No.");
                                                            END;
                                                             }
                                                 }
                                                  }
    { 34  ;MenuButton   ;7590 ;9570 ;2200 ;550  ;Name=ButtonLineReclass;
                                                 HorzGlue=Right;
                                                 VertGlue=Bottom;
                                                 Visible=No;
                                                 CaptionML=[ENU=&Line;
                                                            ESP=&L¡nea];
                                                 Menu=MENUITEMS
                                                 {
                                                   { ID=37;
                                                     PushAction=RunObject;
                                                     CaptionML=[ENU=Serial No. Information Card;
                                                                ESP=Ficha informaci¢n n§ serie];
                                                     RunObject=Form 6504;
                                                     RunFormLinkType=OnUpdate;
                                                     RunFormLink=Item No.=FIELD(Item No.),
                                                                 Variant Code=FIELD(Variant Code),
                                                                 Serial No.=FIELD(Serial No.);
                                                     OnPush=BEGIN
                                                              TESTFIELD("Serial No.");
                                                            END;
                                                             }
                                                   { ID=42;
                                                     PushAction=RunObject;
                                                     CaptionML=[ENU=Lot No. Information Card;
                                                                ESP=Ficha informaci¢n n§ lote];
                                                     RunObject=Form 6505;
                                                     RunFormLinkType=OnUpdate;
                                                     RunFormLink=Item No.=FIELD(Item No.),
                                                                 Variant Code=FIELD(Variant Code),
                                                                 Lot No.=FIELD(Lot No.);
                                                     OnPush=BEGIN
                                                              TESTFIELD("Lot No.");
                                                            END;
                                                             }
                                                   { ID=44;
                                                     MenuItemType=Separator }
                                                   { ID=45;
                                                     CaptionML=[ENU=New S&erial No. Information;
                                                                ESP=Nu&eva informaci¢n n§ serie];
                                                     OnPush=VAR
                                                              SerialNoInfoForm@1001 : Form 6504;
                                                              SerialNoInfoNew@1000 : Record 6504;
                                                            BEGIN
                                                              TESTFIELD("New Serial No.");

                                                              CLEAR(SerialNoInfoForm);
                                                              SerialNoInfoForm.InitWhse(Rec);

                                                              SerialNoInfoNew.SETRANGE("Item No.",Rec."Item No.");
                                                              SerialNoInfoNew.SETRANGE("Variant Code",Rec."Variant Code");
                                                              SerialNoInfoNew.SETRANGE("Serial No.",Rec."New Serial No.");

                                                              SerialNoInfoForm.SETTABLEVIEW(SerialNoInfoNew);
                                                              SerialNoInfoForm.RUN();
                                                            END;
                                                             }
                                                   { ID=55;
                                                     CaptionML=[ENU=New L&ot No. Information;
                                                                ESP=Nueva inf&ormaci¢n n§ lote];
                                                     RunFormLinkType=OnUpdate;
                                                     RunFormOnRec=No;
                                                     OnPush=VAR
                                                              LotNoInfoForm@1001 : Form 6505;
                                                              LotNoInfoNew@1002 : Record 6505;
                                                            BEGIN
                                                              TESTFIELD("New Lot No.");

                                                              CLEAR(LotNoInfoForm);
                                                              LotNoInfoForm.InitWhse(Rec);

                                                              LotNoInfoNew.SETRANGE("Item No.",Rec."Item No.");
                                                              LotNoInfoNew.SETRANGE("Variant Code",Rec."Variant Code");
                                                              LotNoInfoNew.SETRANGE("Lot No.",Rec."New Lot No.");

                                                              LotNoInfoForm.SETTABLEVIEW(LotNoInfoNew);
                                                              LotNoInfoForm.RUN();
                                                            END;
                                                             }
                                                 }
                                                  }
    { 43  ;Frame        ;220  ;2750 ;11880;880  ;ShowCaption=No }
    { 503 ;TextBox      ;3850 ;2970 ;1650 ;440  ;Editable=No;
                                                 ParentControl=43;
                                                 InFrame=Yes;
                                                 Lookup=Yes;
                                                 CaptionML=[ENU=Item Tracking Code;
                                                            ESP=C¢d. seguim. prod.];
                                                 SourceExpr=ItemTrackingCode.Code;
                                                 OnLookup=BEGIN
                                                            FORM.RUNMODAL(0,ItemTrackingCode);
                                                          END;
                                                           }
    { 504 ;Label        ;440  ;2970 ;3300 ;440  ;ParentControl=503 }
    { 505 ;TextBox      ;5720 ;2970 ;4400 ;440  ;Editable=No;
                                                 ParentControl=43;
                                                 InFrame=Yes;
                                                 CaptionML=[ENU=Description;
                                                            ESP=Descripci¢n];
                                                 SourceExpr=ItemTrackingCode.Description }
  }
  CODE
  {
    VAR
      WhseWorksheetLine@1007 : Record 7326;
      TotalWhseItemTrackingLine@1002 : Record 6550;
      TempInitialTrkgLine@1009 : TEMPORARY Record 6550;
      Item@1005 : Record 27;
      ItemTrackingCode@1006 : Record 6502;
      ItemTrackingMgt@1010 : Codeunit 6500;
      UndefinedQtyArray@1001 : ARRAY [2] OF Decimal;
      SourceQuantityArray@1000 : ARRAY [2] OF Decimal;
      ColorOfQuantityArray@1003 : ARRAY [2] OF Integer;
      Text001@1004 : TextConst 'ENU=Line;ESP=L¡nea';
      Text002@1008 : TextConst 'ENU=The corrections cannot be saved as excess quantity has been defined.\Close the form anyway?;ESP=No se pueden guardar las correcciones porque existe un exceso de cdad.\¨Desea cerrar el formulario?';
      FormSourceType@1011 : Integer;
      FormUpdated@1012 : Boolean;
      Reclass@1014 : Boolean;
      Text003@1013 : TextConst 'ENU=Placeholder;ESP=Marcador de posici¢n';

    PROCEDURE TextCaption@1() : Text[30];
    VAR
      PostedWhseRcptLine@1000 : Record 7319;
      WhseShipmentLine@1001 : Record 7321;
    BEGIN
      CASE "Source Type" OF
        DATABASE::"Posted Whse. Receipt Line":
          EXIT(PostedWhseRcptLine.TABLECAPTION);
        DATABASE::"Warehouse Shipment Line":
          EXIT(WhseShipmentLine.TABLECAPTION);
        ELSE
          EXIT(Text001);
      END;
    END;

    PROCEDURE SetSource@3(WhseWrkshLine@1000 : Record 7326;SourceType@1001 : Integer);
    VAR
      Controls@1002 : 'Handle,Reclass,Expiration';
      WhseSNRequired@1003 : Boolean;
      WhseLNRequired@1004 : Boolean;
    BEGIN
      FormUpdated := FALSE;
      FormSourceType := SourceType;
      WhseWorksheetLine := WhseWrkshLine;
      GetItem(WhseWorksheetLine."Item No.");
      ItemTrackingMgt.CheckWhseItemTrkgSetup(
        WhseWorksheetLine."Item No.",WhseSNRequired,WhseLNRequired,TRUE);

      IF SourceType = DATABASE::"Warehouse Journal Line" THEN
        SetControls(Controls::Handle,FALSE)
      ELSE
        SetControls(Controls::Handle,TRUE);

      Reclass := IsReclass(SourceType,WhseWorksheetLine."Worksheet Template Name");
      SetControls(Controls::Reclass,Reclass);

      SetFilters(Rec,FormSourceType);
      SourceQuantityArray[1] := ABS(WhseWorksheetLine."Qty. (Base)");
      SourceQuantityArray[2] := ABS(WhseWorksheetLine."Qty. to Handle (Base)");
      CalculateSums;
    END;

    PROCEDURE SetFilters@12(VAR WhseItemTrkgLine2@1000 : Record 6550;SourceType@1001 : Integer);
    BEGIN
      WITH WhseItemTrkgLine2 DO BEGIN

        FILTERGROUP := 2;
        SETRANGE("Source Type",SourceType);
        SETRANGE("Location Code",WhseWorksheetLine."Location Code");
        SETRANGE("Item No.",WhseWorksheetLine."Item No.");
        SETRANGE("Variant Code",WhseWorksheetLine."Variant Code");
        SETRANGE("Qty. per Unit of Measure",WhseWorksheetLine."Qty. per Unit of Measure");

        CASE SourceType OF
          DATABASE::"Posted Whse. Receipt Line",
          DATABASE::"Warehouse Shipment Line",
          DATABASE::"Whse. Internal Put-away Line",
          DATABASE::"Whse. Internal Pick Line":
            BEGIN
              SETRANGE("Source ID",WhseWorksheetLine."Whse. Document No.");
              SETRANGE("Source Ref. No.",WhseWorksheetLine."Whse. Document Line No.");
            END;
          DATABASE::"Prod. Order Component":
            BEGIN
              SETRANGE("Source Subtype",WhseWorksheetLine."Source Subtype");
              SETRANGE("Source ID",WhseWorksheetLine."Source No.");
              SETRANGE("Source Prod. Order Line",WhseWorksheetLine."Source Line No.");
              SETRANGE("Source Ref. No.",WhseWorksheetLine."Source Subline No.");
            END;
          DATABASE::"Whse. Worksheet Line",
          DATABASE::"Warehouse Journal Line":
            BEGIN
              SETRANGE("Source Batch Name",WhseWorksheetLine."Worksheet Template Name");
              SETRANGE("Source ID",WhseWorksheetLine.Name);
              SETRANGE("Source Ref. No.",WhseWorksheetLine."Line No.");
            END;
        END;
        FILTERGROUP := 0;
      END;
    END;

    LOCAL PROCEDURE SetControls@13(Controls@1002 : 'Handle,Reclass';SetAccess@1001 : Boolean);
    BEGIN
      CASE Controls OF
        Controls::Handle:
          BEGIN
            CurrForm.Handle0.VISIBLE(SetAccess);
            CurrForm.Handle1.VISIBLE(SetAccess);
            CurrForm.Handle2.VISIBLE(SetAccess);
            CurrForm.Handle3.VISIBLE(SetAccess);
            CurrForm."Qty. to Handle (Base)".VISIBLE(SetAccess);
            CurrForm."Qty. to Handle (Base)".EDITABLE(SetAccess);
          END;
        Controls::Reclass:
          BEGIN
            CurrForm."New Serial No.".VISIBLE(SetAccess);
            CurrForm."New Serial No.".EDITABLE(SetAccess);
            CurrForm."New Lot No.".VISIBLE(SetAccess);
            CurrForm."New Lot No.".EDITABLE(SetAccess);
            CurrForm."New Expiration Date".VISIBLE(SetAccess);
            CurrForm."New Expiration Date".EDITABLE(SetAccess);
            CurrForm.ButtonLineReclass.VISIBLE(SetAccess);
            CurrForm.ButtonLine.VISIBLE(NOT SetAccess);
          END;
      END;
    END;

    LOCAL PROCEDURE UpdateExpDateColor@41();
    BEGIN
      IF BlockExpDate THEN
        CurrForm."Expiration Date".UPDATEFORECOLOR(8421504)
      ELSE
        CurrForm."Expiration Date".UPDATEFORECOLOR(0);
    END;

    LOCAL PROCEDURE UpdateExpDateEditable@42();
    BEGIN
      CurrForm."Expiration Date".EDITABLE(NOT BlockExpDate);
    END;

    LOCAL PROCEDURE BlockExpDate@15() : Boolean;
    BEGIN
      EXIT(
        ("Buffer Status2" = "Buffer Status2"::"ExpDate blocked") OR
        (WhseWorksheetLine."Qty. (Base)" < 0) OR
        Reclass OR
        (FormSourceType IN
         [DATABASE::"Whse. Worksheet Line",
          DATABASE::"Posted Whse. Receipt Line",
          DATABASE::"Whse. Internal Put-away Line"]));
    END;

    LOCAL PROCEDURE CalculateSums@2();
    BEGIN
      WITH TotalWhseItemTrackingLine DO BEGIN
        SETCURRENTKEY(
          "Source ID","Source Type","Source Subtype","Source Batch Name",
          "Source Prod. Order Line","Source Ref. No.");
        SETRANGE("Source Type",FormSourceType);
        SETRANGE("Location Code",WhseWorksheetLine."Location Code");

        CASE FormSourceType OF
          DATABASE::"Posted Whse. Receipt Line",
          DATABASE::"Warehouse Shipment Line",
          DATABASE::"Whse. Internal Put-away Line",
          DATABASE::"Whse. Internal Pick Line":
            BEGIN
              SETRANGE("Source ID",WhseWorksheetLine."Whse. Document No.");
              SETRANGE("Source Ref. No.",WhseWorksheetLine."Whse. Document Line No.");
            END;
          DATABASE::"Prod. Order Component":
            BEGIN
              SETRANGE("Source Subtype",WhseWorksheetLine."Source Subtype");
              SETRANGE("Source ID",WhseWorksheetLine."Source No.");
              SETRANGE("Source Prod. Order Line",WhseWorksheetLine."Source Line No.");
              SETRANGE("Source Ref. No.",WhseWorksheetLine."Source Subline No.");
            END;
          DATABASE::"Whse. Worksheet Line",
          DATABASE::"Warehouse Journal Line":
            BEGIN
              SETRANGE("Source Batch Name",WhseWorksheetLine."Worksheet Template Name");
              SETRANGE("Source ID",WhseWorksheetLine.Name);
              SETRANGE("Source Ref. No.",WhseWorksheetLine."Line No.");
            END;
        END;
        CALCSUMS("Quantity (Base)","Qty. to Handle (Base)");
      END;

      UpdateUndefinedQty;
    END;

    LOCAL PROCEDURE UpdateUndefinedQty@5() QtyIsValid@1000 : Boolean;
    BEGIN
      UndefinedQtyArray[1] := SourceQuantityArray[1] - TotalWhseItemTrackingLine."Quantity (Base)";
      UndefinedQtyArray[2] := SourceQuantityArray[2] - TotalWhseItemTrackingLine."Qty. to Handle (Base)";

      IF (ABS(SourceQuantityArray[1]) < ABS(TotalWhseItemTrackingLine."Quantity (Base)")) THEN BEGIN
        ColorOfQuantityArray[1] := 255;
        QtyIsValid := FALSE;
      END ELSE BEGIN
        ColorOfQuantityArray[1] := 0;
        QtyIsValid := TRUE;
      END;

      IF (ABS(SourceQuantityArray[2]) < ABS(TotalWhseItemTrackingLine."Qty. to Handle (Base)")) THEN
        ColorOfQuantityArray[2] := 255
      ELSE
        ColorOfQuantityArray[2] := 0;
    END;

    LOCAL PROCEDURE GetItem@4(ItemNo@1000 : Code[20]);
    BEGIN
      IF Item."No." <> ItemNo THEN BEGIN
        Item.GET(ItemNo);
        Item.TESTFIELD("Item Tracking Code");
        IF ItemTrackingCode.Code <> Item."Item Tracking Code" THEN
          ItemTrackingCode.GET(Item."Item Tracking Code");
      END;
    END;

    PROCEDURE SaveItemTrkgLine@6(VAR TempWhseItemTrkgLine@1001 : TEMPORARY Record 6550);
    VAR
      WhseItemTrkgLine2@1000 : Record 6550;
    BEGIN
      SetFilters(WhseItemTrkgLine2,FormSourceType);
      TempWhseItemTrkgLine.RESET;
      TempWhseItemTrkgLine.DELETEALL;
      IF WhseItemTrkgLine2.FIND('-') THEN
        REPEAT
          TempWhseItemTrkgLine := WhseItemTrkgLine2;
          TempWhseItemTrkgLine.INSERT;
        UNTIL WhseItemTrkgLine2.NEXT = 0;
    END;

    PROCEDURE RestoreInitialTrkgLine@9();
    VAR
      WhseItemTrkgLine2@1000 : Record 6550;
    BEGIN
      SetFilters(WhseItemTrkgLine2,FormSourceType);
      WhseItemTrkgLine2.DELETEALL;
      IF TempInitialTrkgLine.FIND('-') THEN
        REPEAT
          WhseItemTrkgLine2 := TempInitialTrkgLine;
          WhseItemTrkgLine2.INSERT;
        UNTIL TempInitialTrkgLine.NEXT = 0;
    END;

    PROCEDURE CopyToReservEntry@10() : Boolean;
    VAR
      WhseItemTrkgLine2@1000 : Record 6550;
      SourceWhseItemTrkgLine@1006 : TEMPORARY Record 6550;
      WhseShptLine@1002 : Record 7321;
      ProdOrderComp@1003 : Record 5407;
      QuantityBase@1005 : Decimal;
      DueDate@1004 : Date;
      Updated@1001 : Boolean;
    BEGIN
      SetFilters(WhseItemTrkgLine2,FormSourceType);

      IF WhseItemTrkgLine2.FIND('-') THEN
        SourceWhseItemTrkgLine := WhseItemTrkgLine2
      ELSE
        IF TempInitialTrkgLine.FIND('-') THEN
          SourceWhseItemTrkgLine := TempInitialTrkgLine
        ELSE
          EXIT(TRUE);

      CASE FormSourceType OF
        DATABASE::"Prod. Order Component":
          BEGIN
            ProdOrderComp.GET(SourceWhseItemTrkgLine."Source Subtype",SourceWhseItemTrkgLine."Source ID",
              SourceWhseItemTrkgLine."Source Prod. Order Line",SourceWhseItemTrkgLine."Source Ref. No.");
            QuantityBase := ProdOrderComp."Expected Qty. (Base)";
            DueDate := ProdOrderComp."Due Date";
            Updated := UpdateReservEntry(
              SourceWhseItemTrkgLine."Source Type",
              SourceWhseItemTrkgLine."Source Subtype",
              SourceWhseItemTrkgLine."Source ID",
              SourceWhseItemTrkgLine."Source Prod. Order Line",
              SourceWhseItemTrkgLine."Source Ref. No.",
              SourceWhseItemTrkgLine,QuantityBase,DueDate);
          END;
        DATABASE::"Warehouse Shipment Line":
          BEGIN
            WhseShptLine.GET(SourceWhseItemTrkgLine."Source ID",SourceWhseItemTrkgLine."Source Ref. No.");
            QuantityBase := WhseShptLine."Qty. (Base)";
            DueDate := WhseShptLine."Due Date";
            Updated := UpdateReservEntry(
              WhseShptLine."Source Type",
              WhseShptLine."Source Subtype",
              WhseShptLine."Source No.",
              0,
              WhseShptLine."Source Line No.",
              SourceWhseItemTrkgLine,QuantityBase,DueDate);
          END;
        ELSE
          EXIT(TRUE);
      END;
      EXIT(Updated)
    END;

    PROCEDURE UpdateReservEntry@7(SourceType@1000 : Integer;SourceSubtype@1001 : Integer;SourceID@1002 : Code[20];SourceProdOrderLine@1003 : Integer;SourceRefNo@1004 : Integer;SourceWhseItemTrkgLine@1005 : TEMPORARY Record 6550;QuantityBase@1013 : Decimal;DueDate@1012 : Date) : Boolean;
    VAR
      TempTrkgSpec@1006 : TEMPORARY Record 336;
      SourceSpecification@1009 : Record 336;
      WhseItemTrkgLine2@1014 : Record 6550;
      ItemTrackingMgt@1007 : Codeunit 6500;
      LastEntryNo@1008 : Integer;
    BEGIN
      IF TempInitialTrkgLine.FIND('-') THEN
        REPEAT
          TempTrkgSpec.TRANSFERFIELDS(TempInitialTrkgLine);
          TempTrkgSpec."Quantity (Base)" *= -1;
          TempTrkgSpec."Entry No." := LastEntryNo + 1;
          TempTrkgSpec.INSERT;
          LastEntryNo := TempTrkgSpec."Entry No.";
        UNTIL TempInitialTrkgLine.NEXT = 0;

      SetFilters(WhseItemTrkgLine2,FormSourceType);
      IF WhseItemTrkgLine2.FIND('-') THEN
        REPEAT
          TempTrkgSpec.TRANSFERFIELDS(WhseItemTrkgLine2);
          TempTrkgSpec."Entry No." := LastEntryNo + 1;
          TempTrkgSpec.INSERT;
          LastEntryNo := TempTrkgSpec."Entry No.";
        UNTIL WhseItemTrkgLine2.NEXT = 0;

      SourceSpecification."Source Type" := SourceType;
      SourceSpecification."Source Subtype" := SourceSubtype;
      SourceSpecification."Source ID" := SourceID;
      SourceSpecification."Source Batch Name" := '';
      SourceSpecification."Source Prod. Order Line" := SourceProdOrderLine;
      SourceSpecification."Source Ref. No." := SourceRefNo;
      SourceSpecification."Quantity (Base)" := QuantityBase;
      SourceSpecification."Item No." := SourceWhseItemTrkgLine."Item No.";
      SourceSpecification."Variant Code" := SourceWhseItemTrkgLine."Variant Code";
      SourceSpecification."Location Code" := SourceWhseItemTrkgLine."Location Code";
      SourceSpecification.Description := SourceWhseItemTrkgLine.Description;
      SourceSpecification."Qty. per Unit of Measure" := SourceWhseItemTrkgLine."Qty. per Unit of Measure";
      ItemTrackingMgt.SetGlobalParameters(SourceSpecification,TempTrkgSpec,DueDate);
      EXIT(ItemTrackingMgt.RUN);
    END;

    PROCEDURE InsertItemTrackingLine@8(WhseWrkshLine@1004 : Record 7326;SerialNo@1000 : Code[20];LotNo@1001 : Code[20];ExpirationDate@1005 : Date;QtyToEmpty@1002 : Decimal);
    VAR
      WhseItemTrackingLine2@1003 : Record 6550;
    BEGIN
      UpdateUndefinedQty;
      SaveItemTrkgLine(TempInitialTrkgLine);
      "Lot No." := LotNo;
      "Serial No." := SerialNo;
      "Expiration Date" := ExpirationDate;
      "Qty. per Unit of Measure" := WhseWrkshLine."Qty. per Unit of Measure";
      VALIDATE("Quantity (Base)",QtyToEmpty);
      "Source Type" := FormSourceType;
      "Source ID" := WhseWorksheetLine."Whse. Document No.";
      "Source Ref. No." := WhseWorksheetLine."Whse. Document Line No.";
      "Source Batch Name" := WhseWrkshLine."Worksheet Template Name";
      "Location Code" := WhseWorksheetLine."Location Code";
      "Item No." := WhseWorksheetLine."Item No.";
      "Variant Code" := WhseWrkshLine."Variant Code";
      IF WhseItemTrackingLine2.FIND('+') THEN;
      "Entry No." := WhseItemTrackingLine2."Entry No." + 1;
      INSERT;
    END;

    BEGIN
    {
      Function button Line exist in two overlayed versions to make dynamic show/hide/enable of
      individual menu items possible.
    }
    END.
  }
}
