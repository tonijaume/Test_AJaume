OBJECT Form 5169 Profile Questn. Line Subform
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    Width=8140;
    Height=7370;
    CaptionML=[ENU=Profile Questn. Line Subform;
               ESP=Subform. l¡nea cuest. perfil];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    TableBoxID=1;
    LinksAllowed=No;
    SourceTable=Table5088;
    OnAfterGetRecord=BEGIN
                       Set := TempProfileQuestionnaireLine.GET("Profile Questionnaire Code","Line No.");
                     END;

  }
  CONTROLS
  {
    { 1   ;TableBox     ;0    ;0    ;8140 ;7370 ;HorzGlue=Both;
                                                 VertGlue=Both }
    { 8   ;CheckBox     ;4763 ;1430 ;1100 ;440  ;ParentControl=1;
                                                 InColumn=Yes;
                                                 ShowCaption=No;
                                                 CaptionML=[ENU=Select;
                                                            ESP=Selecciona];
                                                 SourceExpr=Set;
                                                 OnValidate=BEGIN
                                                              TESTFIELD(Type,Type::Answer);

                                                              IF Set THEN BEGIN
                                                                TempProfileQuestionnaireLine.INIT;
                                                                TempProfileQuestionnaireLine.VALIDATE("Profile Questionnaire Code","Profile Questionnaire Code");
                                                                TempProfileQuestionnaireLine.VALIDATE("Line No.","Line No.");
                                                                TempProfileQuestionnaireLine.INSERT;
                                                              END ELSE BEGIN
                                                                TempProfileQuestionnaireLine.GET("Profile Questionnaire Code","Line No.");
                                                                TempProfileQuestionnaireLine.DELETE;
                                                              END;
                                                            END;
                                                             }
    { 9   ;Label        ;0    ;0    ;0    ;0    ;ParentControl=8;
                                                 InColumnHeading=Yes }
    { 2   ;TextBox      ;1111 ;0    ;4730 ;0    ;HorzGlue=Both;
                                                 Editable=No;
                                                 ParentControl=1;
                                                 InColumn=Yes;
                                                 SourceExpr=Description;
                                                 OnFormat=BEGIN
                                                            IF Type = Type::Question THEN
                                                              CurrForm.Description.UPDATEFONTBOLD := TRUE
                                                            //ELSE
                                                            //  CurrForm.Description.UPDATEINDENT(220);
                                                          END;
                                                           }
    { 3   ;Label        ;0    ;0    ;0    ;0    ;ParentControl=2;
                                                 InColumnHeading=Yes }
  }
  CODE
  {
    VAR
      TempProfileQuestionnaireLine@1001 : TEMPORARY Record 5088;
      Set@1000 : Boolean;

    PROCEDURE GetParameters@7(VAR GetProfileQuestionnaireLine@1000 : Record 5088);
    VAR
      ProfileQuestionnaireLine2@1001 : Record 5088;
      FirstPQLine@1002 : Integer;
      LastPQLine@1003 : Integer;
      SelectionFilter@1004 : Code[250];
      PQLineCount@1005 : Integer;
      More@1006 : Boolean;
    BEGIN
      ProfileQuestionnaireLine2.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
      IF TempProfileQuestionnaireLine.FIND('-') THEN
        REPEAT
          IF ProfileQuestionnaireLine2.GET(
               TempProfileQuestionnaireLine."Profile Questionnaire Code",TempProfileQuestionnaireLine."Line No.")
          THEN
            ProfileQuestionnaireLine2.MARK(TRUE);
        UNTIL TempProfileQuestionnaireLine.NEXT = 0;

      ProfileQuestionnaireLine2.MARKEDONLY(TRUE);
      PQLineCount := ProfileQuestionnaireLine2.COUNT;
      IF PQLineCount > 0 THEN BEGIN
        ProfileQuestionnaireLine2.FIND('-');
        WHILE PQLineCount > 0 DO BEGIN
          PQLineCount := PQLineCount - 1;
          ProfileQuestionnaireLine2.MARKEDONLY(FALSE);
          FirstPQLine := ProfileQuestionnaireLine2."Line No.";
          LastPQLine := FirstPQLine;
          More := (PQLineCount > 0);
          WHILE More DO
            IF ProfileQuestionnaireLine2.NEXT = 0 THEN
              More := FALSE
            ELSE
              IF NOT ProfileQuestionnaireLine2.MARK THEN
                More := FALSE
              ELSE BEGIN
                LastPQLine := ProfileQuestionnaireLine2."Line No.";
                PQLineCount := PQLineCount - 1;
                IF PQLineCount = 0 THEN
                  More := FALSE;
              END;
          IF SelectionFilter <> '' THEN
            SelectionFilter := SelectionFilter + '|';
          IF FirstPQLine = LastPQLine THEN
            SelectionFilter := SelectionFilter + FORMAT(FirstPQLine)
          ELSE
            SelectionFilter := SelectionFilter + FORMAT(FirstPQLine) + '..' + FORMAT(LastPQLine);
          IF PQLineCount > 0 THEN BEGIN
            ProfileQuestionnaireLine2.MARKEDONLY(TRUE);
            ProfileQuestionnaireLine2.NEXT;
          END;
        END;
      END;
      GetProfileQuestionnaireLine.SETFILTER("Line No.",SelectionFilter);
    END;

    PROCEDURE SetProfileQnLine@1(VAR FromProfileQuestionnaireLine@1000 : Record 5088);
    BEGIN
      WITH FromProfileQuestionnaireLine DO BEGIN
        ClearSettings;
        IF FIND('-') THEN
          REPEAT
            TempProfileQuestionnaireLine := FromProfileQuestionnaireLine;
            TempProfileQuestionnaireLine.INSERT;
          UNTIL NEXT = 0;
      END;
    END;

    PROCEDURE GetProfileQnLine@9(VAR ToProfileQuestionnaireLine@1000 : Record 5088);
    BEGIN
      WITH ToProfileQuestionnaireLine DO BEGIN
        DELETEALL;
        IF TempProfileQuestionnaireLine.FIND('-') THEN
          REPEAT
            ToProfileQuestionnaireLine := TempProfileQuestionnaireLine;
            INSERT;
          UNTIL TempProfileQuestionnaireLine.NEXT = 0;
      END;
    END;

    PROCEDURE ClearSettings@2();
    BEGIN
      IF TempProfileQuestionnaireLine.FIND('-') THEN
        TempProfileQuestionnaireLine.DELETEALL;
    END;

    BEGIN
    END.
  }
}
